       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C835.                                                    
      *AUTHOR.        RACHEL.                                                   
      *DATE-WRITTEN.  11/03/95.                                                 
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE  :                                        *                
      *        1.  PROGRAM TO MATCH HOUSEHOLD TYPE WHICH NEWLY *                
      *            DETERMINE FOR EACH QUARTER WITH THE         *                
      *            HOUSEHOLD TYPE IN SOC MASTER, IF THERE IS   *                
      *            A CHANGE, PRINT REPORT TO SALES.            *                
      *                                                        *                
      *        2. SALES WILL MANUALLY CHECK AND UPDATE THRU    *                
      *           THE FORCE THROUGH PANEL.                     *                
      *                                                        *                
      *     INPUT FILES :                                      *                
      *       1. BP13F800  :  SOC MASTER                       *                
      *       2. BP13F500  :  BALLOT SEQUENCE FILE             *                
      *                                                        *                
      *                                                        *                
      *  CHG NO   BY   ON        DESCRIPTION                   *                
      *  ------   --   --        -----------                   *                
      * C950316   RGM  130395    ADD REPORT ON CHANGE OF HHTYPE*                
      * BP130030  LSB  150995    ADD REOPT TAG                 *                
      * BP130296  LSB  230497    OUTPUT REGN-NO TO BP13F310    *                
      *                          FOR CFS CHECK                 *                
      * BP130323  RHB  080797    TO CHECK 10YR-OCCPN           *                
      * BP130348  LSB  060897    CHECK NUMERIC MTH-RESIDE      *                
      * BP130393  NOR  071197    CHANGE BP13L835 HEADER AND    *                
      *                          INSTEAD PRINTING FRZ-HH       *                
      * BP130410  LSB  211197   OUTPUT AL TO F310 INSTEAD OF RQ*                
      * BP130410  MPA  251197   ADD OLD & EXST HHTY-TYP TO REPT*                
      * BP130418  MPA  101098   YCK CHANGES                    *                
      * BP130623  LSB  060299   P13L835A PRINT IF MTH DIFF 3 MH*                
      * BP130652  NOR  190299   EXPAND F500 RECL FR 300 TO 500 *                
      * BP132626  SK   241104   CHANGE DTE-REGN TO DTE-REQUEST *                
      * BP132603  JF4  010305   CHANGE 10-YR TIMEBAR TO 5-YR   *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
           SELECT BP13F500 ASSIGN TO BP13F500.                                  
           SELECT BP13F310 ASSIGN TO BP13F310.                                  
           SELECT P13L835A ASSIGN TO P13L835A.                                  
           SELECT P13L835B ASSIGN TO P13L835B.                                  
           SELECT P13L835C ASSIGN TO P13L835C.                                  
                                                                                
                                                                                
           EJECT                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F500                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 500 CHARACTERS               
                                   RECORDING MODE IS F                          
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F500.                                                           
                                                                                
       FD  BP13F800                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 2000 CHARACTERS              
                                   RECORDING MODE IS F                          
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F800.                                                           
                                                                                
       FD  BP13F310                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 150 CHARACTERS               
                                   RECORDING MODE IS F                          
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F310.                                                           
                                                                                
       FD  P13L835A                RECORD CONTAINS 132 CHARACTERS               
                                   RECORDING MODE IS F                          
                                   LABEL RECORDS ARE OMITTED.                   
       01  PRINT-REC               PIC X(132).                                  
                                                                                
       FD  P13L835B                RECORD CONTAINS 132 CHARACTERS               
                                   RECORDING MODE IS F                          
                                   LABEL RECORDS ARE OMITTED.                   
       01  PRINT-REC-A             PIC X(132).                                  
                                                                                
       FD  P13L835C                RECORD CONTAINS 132 CHARACTERS               
                                   RECORDING MODE IS F                          
                                   LABEL RECORDS ARE OMITTED.                   
       01  PRINT-REC-B             PIC X(132).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-PRT-P13L835C         PIC X       VALUE 'N'.                       
       01  WS-F500-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-F800-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-L835A-PRINT          PIC 9(8)    VALUE ZERO.                      
       01  WS-L835B-PRINT          PIC 9(8)    VALUE ZERO.                      
       01  WS-L835C-PRINT          PIC 9(8)    VALUE ZERO.                      
       01  WS-HHTY-CHG             PIC 9(8)    VALUE ZERO.                      
       01  WS-5YR-CHG              PIC 9(8)    VALUE ZERO.                      
       01  WS-MTH-RES-CHG          PIC 9(8)    VALUE ZERO.                      
       01  WS-MTH-RES-MTCH         PIC 9(8)    VALUE ZERO.                      
       01  WS-CDE-HHMATCH          PIC 9(8)    VALUE ZERO.                      
       01  WS-CDE-5YRMATCH         PIC 9(8)    VALUE ZERO.                      
       01  WS-MATCH                PIC 9(8)    VALUE ZERO.                      
       01  WS-F800-NOTMATCH        PIC 9(8)    VALUE ZERO.                      
       01  WS-F500-NOTMATCH        PIC 9(8)    VALUE ZERO.                      
       01  WS-NO-OF-PAGE-L835A     PIC 9(3)    VALUE ZERO.                      
       01  WS-NO-OF-PAGE-L835B     PIC 9(3)    VALUE ZERO.                      
       01  WS-NO-OF-PAGE-L835C     PIC 9(3)    VALUE ZERO.                      
       01  WS-SERIAL-NO            PIC 9(5)    VALUE ZERO.                      
       01  WS-SERIAL-NO-A          PIC 9(5)    VALUE ZERO.                      
       01  WS-CNT-LINE-L835A       PIC 9(2)    VALUE 66.                        
       01  WS-CNT-LINE-L835B       PIC 9(2)    VALUE 66.                        
       01  WS-CNT-LINE-L835C       PIC 9(2)    VALUE 66.                        
       01  WS-MAX-LINES            PIC 9(2)    VALUE 59.                        
       01  WS-MTH-RESIDE           PIC S9(3)    VALUE +0.                       
       01  WS-F310-WRITE           PIC 9(8)    VALUE ZERO.                      
       01  WS-PRINT-DATE.                                                       
           05 WS-PRINT-DATE-DD     PIC X(2)    VALUE SPACES.                    
           05 FILLER               PIC X(1)    VALUE '/'.                       
           05 WS-PRINT-DATE-MM     PIC X(2)    VALUE SPACES.                    
           05 FILLER               PIC X(1)    VALUE '/'.                       
           05 WS-PRINT-DATE-CCYY   PIC X(4)    VALUE SPACES.                    
       01  WS-PR-CUR-DATE.                                                      
           05 WS-PC-DD             PIC X(2)    VALUE SPACES.                    
           05 FILLER               PIC X(1)    VALUE '/'.                       
           05 WS-PC-MM             PIC X(2)    VALUE SPACES.                    
           05 FILLER               PIC X(1)    VALUE '/'.                       
           05 WS-PC-CC             PIC X(2)    VALUE SPACES.                    
           05 WS-PC-YY             PIC X(2)    VALUE SPACES.                    
       01  WS-DATE.                                                             
           05 WS-DATE-CCYY         PIC X(4)    VALUE SPACES.                    
           05 WS-DATE-MM           PIC X(2)    VALUE SPACES.                    
           05 WS-DATE-DD           PIC X(2)    VALUE SPACES.                    
       01  WS-CURRENT-DATE.                                                     
           05 WS-CUR-CC            PIC 9(2)    VALUE ZEROS.                     
           05 WS-CUR-YY            PIC 9(2)    VALUE ZEROS.                     
           05 WS-CUR-MM            PIC 9(2)    VALUE ZEROS.                     
           05 WS-CUR-DD            PIC 9(2)    VALUE ZEROS.                     
       01  WS-F800-MTH-RESIDE.                                                  
           05  WS-F800-MTH         PIC 9(3)    VALUE 0.                         
                                                                                
       77  WS-EOF-F800             PIC X(3).                                    
           88  EOF-OF-F800                     VALUE 'YES'.                     
       77  WS-EOF-F500             PIC X(3).                                    
           88  EOF-OF-F500                     VALUE 'YES'.                     
                                                                                
       01  L835-PR-HEAD-01.                                                     
           05  FILLER                  PIC X(13)   VALUE 'P13L835A'.            
           05  FILLER                  PIC X(26)   VALUE 'HDB3'.                
           05  FILLER                  PIC X(60)   VALUE                        
               'S Y S T E M     O F    C O M M I T M E N T'.                    
           05  FILLER                  PIC X(7)    VALUE 'DATE: '.              
           05  PRINT-DATE-A            PIC X(10)    VALUE SPACES.               
           05  FILLER                  PIC X(3)    VALUE SPACES.                
           05  FILLER                  PIC X(7)    VALUE 'PAGE: '.              
           05  PRINT-PAGE              PIC ZZZ9.                                
                                                                                
       01  L835-PR-HEAD-02.                                                     
           05  FILLER                  PIC X(41)   VALUE SPACES.                
           05  FILLER                  PIC X(50)   VALUE                        
              'SOC - REPORT ON CHANGE OF HOUSEHOLD TYPE'.                       
                                                                                
       01  L835-PR-HEAD-03.                                                     
           05  FILLER                  PIC X(06)   VALUE SPACES.                
           05  FILLER                  PIC X(09)   VALUE 'S/NO'.                
           05  FILLER                  PIC X(12)   VALUE 'REGN-NO'.             
           05  FILLER                  PIC X(16)   VALUE '   NRIC1   '.         
           05  FILLER                  PIC X(09)   VALUE 'ZONE'.                
           05  FILLER                  PIC X(07)   VALUE 'FT'.                  
           05  FILLER                  PIC X(12)   VALUE 'ALC-SCH'.             
           05  FILLER                  PIC X(15)   VALUE 'JB-REGN-NO'.          
           05  FILLER                  PIC X(12)   VALUE 'DTE-REQ'.             
           05  FILLER                  PIC X(09)   VALUE 'EXIST-HH'.            
           05  FILLER                  PIC X(08)   VALUE 'NEW-HH'.              
           05  FILLER                  PIC X(08)   VALUE 'FRZ-HH '.             
           05  FILLER                  PIC X(14)   VALUE '3RM-REOPT'.           
                                                                                
       01  L835-PR-DETAIL-1.                                                    
           05  FILLER                  PIC X(06)   VALUE SPACES.                
           05  L835-SERIAL-NO          PIC ZZZZ9   VALUE ZEROS.                 
           05  FILLER                  PIC X(04)   VALUE SPACES.                
           05  L835-REGN-NO            PIC X(08)   VALUE SPACES.                
           05  FILLER                  PIC X(04)   VALUE SPACES.                
           05  L835-NRIC               PIC X(09)   VALUE SPACES.                
           05  FILLER                  PIC X(08)   VALUE SPACES.                
           05  L835-ZONE               PIC X(02)   VALUE SPACES.                
           05  FILLER                  PIC X(06)   VALUE SPACES.                
           05  L835-FT                 PIC X(02)   VALUE SPACES.                
           05  FILLER                  PIC X(07)   VALUE SPACES.                
           05  L835-ALC-SCH            PIC X(03)   VALUE SPACES.                
           05  FILLER                  PIC X(07)   VALUE SPACES.                
           05  L835-JB-REGN-NO         PIC X(08)   VALUE SPACES.                
           05  FILLER                  PIC X(06)   VALUE SPACES.                
           05  L835-DTE-REGN           PIC X(10)   VALUE SPACES.                
           05  FILLER                  PIC X(06)   VALUE SPACES.                
           05  L835-EXIST-HHTY         PIC X(01)   VALUE SPACES.                
           05  FILLER                  PIC X(07)   VALUE SPACES.                
           05  L835-NEW-HHTY           PIC X(01)   VALUE SPACES.                
           05  FILLER                  PIC X(07)   VALUE SPACES.                
           05  L835-FREEZE             PIC X(01)   VALUE SPACES.                
           05  FILLER                  PIC X(07)   VALUE SPACES.                
           05  L835-REOPT              PIC X(01)   VALUE SPACES.                
                                                                                
       01  L835A-PR-HEAD-01.                                                    
           05  FILLER                  PIC X(13)   VALUE 'P13L835B'.            
           05  FILLER                  PIC X(26)   VALUE 'HDB3'.                
           05  FILLER                  PIC X(60)   VALUE                        
               'S Y S T E M     O F    C O M M I T M E N T'.                    
           05  FILLER                  PIC X(7)    VALUE 'DATE: '.              
           05  PRINT-DATE-B            PIC X(10)   VALUE SPACES.                
           05  FILLER                  PIC X(3)    VALUE SPACES.                
           05  FILLER                  PIC X(7)    VALUE 'PAGE: '.              
           05  PRINT-PAGE-B            PIC ZZZ9.                                
                                                                                
       01  L835C-PR-HEAD-01.                                                    
           05  FILLER                  PIC X(13)   VALUE 'P13L835C'.            
           05  FILLER                  PIC X(26)   VALUE 'HDB3'.                
           05  FILLER                  PIC X(60)   VALUE                        
               'S Y S T E M     O F    C O M M I T M E N T'.                    
           05  FILLER                  PIC X(7)    VALUE 'DATE: '.              
           05  PRINT-DATE-C            PIC X(10)   VALUE SPACES.                
           05  FILLER                  PIC X(3)    VALUE SPACES.                
           05  FILLER                  PIC X(7)    VALUE 'PAGE: '.              
           05  PRINT-PAGE-C            PIC ZZZ9.                                
                                                                                
       01  L835A-PR-HEAD-02.                                                    
           05  FILLER                  PIC X(41)   VALUE SPACES.                
           05  FILLER                  PIC X(50)   VALUE                        
              'SOC - REPORT ON CHANGE OF 5YR OCCUPATION '.                      
                                                                                
       01  L835C-PR-HEAD-02.                                                    
           05  FILLER                  PIC X(41)   VALUE SPACES.                
           05  FILLER                  PIC X(91)   VALUE                        
              'SOC - REPORT ON CHANGE OF MTH-RESIDE FOR 5YR OCCUPATION          
      -       '= N '.                                                           
       01  L835A-PR-HEAD-03.                                                    
           05  FILLER                  PIC X(09)   VALUE 'S/NO'.                
           05  FILLER                  PIC X(10)   VALUE 'REGN-NO'.             
           05  FILLER                  PIC X(11)   VALUE ' NRIC1 '.             
           05  FILLER                  PIC X(06)   VALUE 'ZONE'.                
           05  FILLER                  PIC X(04)   VALUE 'FT'.                  
           05  FILLER                  PIC X(09)   VALUE 'ALC-SCH'.             
           05  FILLER                  PIC X(10)   VALUE 'JB-REGNO'.            
           05  FILLER                  PIC X(11)   VALUE 'DTE-REQ '.            
           05  FILLER                  PIC X(11)   VALUE '5YR-OCCUP '.          
           05  FILLER                  PIC X(12)   VALUE 'MTH-RESIDE'.          
           05  FILLER                  PIC X(12)   VALUE 'JB-MTH-RES'.          
           05  FILLER                  PIC X(12)   VALUE ' HH-TYPE'.            
           05  FILLER                  PIC X(09)   VALUE 'FRZ-5YR '.            
           05  FILLER                  PIC X(12)   VALUE '  3RM'.               
                                                                                
       01  L835A-PR-HEAD-04.                                                    
           05  FILLER                  PIC X(71)   VALUE SPACES.                
           05  FILLER                  PIC X(12)   VALUE 'EXT  NEW'.            
           05  FILLER                  PIC X(12)   VALUE 'EXT  NEW'.            
           05  FILLER                  PIC X(12)   VALUE 'EXT  NEW'.            
           05  FILLER                  PIC X(18)   VALUE 'EXT  NEW'.            
           05  FILLER                  PIC X(11)   VALUE '  REOPT'.             
                                                                                
       01  L835A-PR-DETAIL-1.                                                   
           05  L835A-SERIAL-NO         PIC ZZZZ9   VALUE ZEROS.                 
           05  FILLER                  PIC X(04)   VALUE SPACES.                
           05  L835A-REGN-NO           PIC X(08)   VALUE SPACES.                
           05  FILLER                  PIC X(02)   VALUE SPACES.                
           05  L835A-NRIC              PIC X(09)   VALUE SPACES.                
           05  FILLER                  PIC X(03)   VALUE SPACES.                
           05  L835A-ZONE              PIC X(02)   VALUE SPACES.                
           05  FILLER                  PIC X(03)   VALUE SPACES.                
           05  L835A-FT                PIC X(02)   VALUE SPACES.                
           05  FILLER                  PIC X(04)   VALUE SPACES.                
           05  L835A-ALC-SCH           PIC X(03)   VALUE SPACES.                
           05  FILLER                  PIC X(04)   VALUE SPACES.                
           05  L835A-JB-REGN-NO        PIC X(08)   VALUE SPACES.                
           05  FILLER                  PIC X(02)   VALUE SPACES.                
           05  L835A-DTE-REGN          PIC X(10)   VALUE SPACES.                
           05  FILLER                  PIC X(03)   VALUE SPACES.                
           05  L835A-EXIST-5YR         PIC X(01)   VALUE SPACES.                
           05  FILLER                  PIC X(04)   VALUE SPACES.                
           05  L835A-NEW-5YR           PIC X(01)   VALUE SPACES.                
           05  FILLER                  PIC X(05)   VALUE SPACES.                
           05  L835A-MTHRES            PIC X(03)   VALUE SPACES.                
           05  FILLER                  PIC X(02)   VALUE SPACES.                
           05  L835A-NMTHRES           PIC X(03)   VALUE SPACES.                
           05  FILLER                  PIC X(04)   VALUE SPACES.                
           05  L835A-JBMTHRES          PIC X(03)   VALUE SPACES.                
           05  FILLER                  PIC X(02)   VALUE SPACES.                
           05  L835A-NJBMTHRES         PIC X(03)   VALUE SPACES.                
           05  FILLER                  PIC X(05)   VALUE SPACES.                
           05  L835A-EXIST-HH          PIC X(01)   VALUE SPACES.                
           05  FILLER                  PIC X(04)   VALUE SPACES.                
           05  L835A-NEW-HH            PIC X(01)   VALUE SPACES.                
           05  FILLER                  PIC X(06)   VALUE SPACES.                
           05  L835A-FREEZE            PIC X(01)   VALUE SPACES.                
           05  FILLER                  PIC X(08)   VALUE SPACES.                
           05  L835A-REOPT             PIC X(01)   VALUE SPACES.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
       10-MAIN-LOGIC.                                                           
           PERFORM 20-INITIALIZATION.                                           
           PERFORM 200-READ-F800 THRU 200-EXIT.                                 
           PERFORM 300-READ-F500 THRU 300-EXIT.                                 
           PERFORM 100-PROCESS   THRU 100-EXIT                                  
              UNTIL F800-NUM-REGN = HIGH-VALUE                                  
               AND  F500-NUM-REGN = HIGH-VALUE.                                 
           PERFORM 900-FINALIZATION.                                            
       10-FINISH.                                                               
           STOP RUN.                                                            
                                                                                
       20-INITIALIZATION.                                                       
           OPEN INPUT  BP13F800                                                 
                       BP13F500                                                 
                OUTPUT BP13F310                                                 
                       P13L835A                                                 
                       P13L835B                                                 
                       P13L835C.                                                
           MOVE 'NO' TO WS-EOF-F800                                             
                        WS-EOF-F500.                                            
                                                                                
           ACCEPT WS-CURRENT-DATE FROM DATE YYYYMMDD.                           
                                                                                
           MOVE WS-CUR-DD         TO   WS-PC-DD.                                
           MOVE WS-CUR-MM         TO   WS-PC-MM.                                
           MOVE WS-CUR-CC         TO   WS-PC-CC.                                
           MOVE WS-CUR-YY         TO   WS-PC-YY.                                
                                                                                
           MOVE WS-PR-CUR-DATE    TO   PRINT-DATE-A                             
                                       PRINT-DATE-B                             
                                       PRINT-DATE-C.                            
      *****************************************************************         
      *  MATCH REF                                                    *         
      *****************************************************************         
       100-PROCESS.                                                             
           IF F800-NUM-REGN = F500-NUM-REGN                                     
               ADD 1 TO WS-MATCH                                                
               PERFORM 350-CHECK-5-YR-TIME-BAR THRU 350-EXIT                    
                                                                                
               IF F800-NUM-HOUSEHOLD NOT = F500-CDE-HOUSEHOLD                   
                  ADD   1        TO   WS-HHTY-CHG                               
                  PERFORM 400-PRINT-REPORT THRU 400-EXIT                        
                  PERFORM 500-WRITE-BP13F310 THRU 500-EXIT                      
               ELSE                                                             
                  ADD   1        TO   WS-CDE-HHMATCH                            
               END-IF                                                           
               PERFORM 200-READ-F800 THRU 200-EXIT                              
               PERFORM 300-READ-F500 THRU 300-EXIT                              
           ELSE                                                                 
           IF (F800-NUM-REGN   < F500-NUM-REGN)                                 
              ADD 1 TO WS-F800-NOTMATCH                                         
              PERFORM 200-READ-F800 THRU 200-EXIT                               
           ELSE                                                                 
              ADD   1        TO   WS-F500-NOTMATCH                              
              PERFORM 300-READ-F500 THRU 300-EXIT.                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-F800.                                                           
           READ BP13F800 AT END MOVE 'YES' TO WS-EOF-F800                       
                                MOVE HIGH-VALUE TO F800-NUM-REGN                
               GO TO 200-EXIT.                                                  
            ADD 1 TO WS-F800-READ.                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-READ-F500.                                                           
           READ BP13F500 AT END MOVE 'YES' TO WS-EOF-F500                       
                                MOVE HIGH-VALUE TO F500-NUM-REGN                
               GO TO 300-EXIT.                                                  
            ADD 1 TO WS-F500-READ.                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       350-CHECK-5-YR-TIME-BAR.                                                 
                                                                                
           MOVE 0 TO WS-MTH-RESIDE.                                             
                                                                                
           IF F800-NUM-MTH-RESIDE IS NOT NUMERIC                                
               MOVE ZEROS TO F800-NUM-MTH-RESIDE                                
           END-IF.                                                              
           IF F800-NUM-MTH-RESIDE = SPACES OR LOW-VALUES                        
               MOVE ZEROS TO F800-NUM-MTH-RESIDE                                
           END-IF.                                                              
           IF F500-NUM-MTH-RESIDE IS NOT NUMERIC                                
               MOVE ZEROS TO F500-NUM-MTH-RESIDE                                
           END-IF.                                                              
           IF F500-NUM-MTH-RESIDE = SPACES OR LOW-VALUES                        
               MOVE ZEROS TO F500-NUM-MTH-RESIDE                                
           END-IF.                                                              
           MOVE F800-NUM-MTH-RESIDE TO WS-F800-MTH-RESIDE.                      
                                                                                
           IF F800-NUM-5YR-OCCUPN NOT = F500-NUM-5YR-OCCUPN                     
      *                                                                         
      *  PRINT REPORT P13L835B                                                  
      *                                                                         
              ADD   1              TO WS-5YR-CHG                                
              MOVE 'N'             TO WS-PRT-P13L835C                           
              PERFORM 600-PRINT-5YR-REPORT THRU 600-EXIT                        
           ELSE                                                                 
              IF (F800-NUM-MTH-RESIDE > '000' AND < '060') OR                   
                 (F500-NUM-MTH-RESIDE > '000' AND < '060')                      
                 COMPUTE WS-MTH-RESIDE =                                        
                         F500-NUM-MTH-RESIDE - WS-F800-MTH                      
      *                                                                         
      *  PRINT REPORT P13L835C                                                  
      *                                                                         
                  IF WS-MTH-RESIDE = +3                                         
                     ADD   1        TO   WS-CDE-5YRMATCH                        
                                        WS-MTH-RES-MTCH                         
                  ELSE                                                          
                     ADD   1        TO   WS-MTH-RES-CHG                         
                     MOVE 'Y'       TO   WS-PRT-P13L835C                        
                     PERFORM 600-PRINT-5YR-REPORT THRU 600-EXIT                 
                ELSE                                                            
                    ADD   1        TO   WS-CDE-5YRMATCH.                        
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-PRINT-REPORT.                                                        
           IF WS-CNT-LINE-L835A   > WS-MAX-LINES                                
              PERFORM 410-PRINT-HEADING THRU 410-EXIT.                          
                                                                                
            ADD  1                   TO   WS-SERIAL-NO.                         
            MOVE WS-SERIAL-NO        TO   L835-SERIAL-NO.                       
            MOVE F500-NUM-REGN       TO   L835-REGN-NO.                         
            MOVE F500-NUM-NRIC1      TO   L835-NRIC.                            
            MOVE F500-CDE-ZONE       TO   L835-ZONE.                            
            MOVE F500-CDE-FLAT-TYPE  TO   L835-FT.                              
            MOVE F500-CDE-ALLOC-SCH  TO   L835-ALC-SCH.                         
            MOVE F500-NUM-JB-REGN    TO   L835-JB-REGN-NO.                      
            MOVE F500-DTE-REQUEST    TO   WS-DATE.                              
            MOVE WS-DATE-CCYY        TO   WS-PRINT-DATE-CCYY.                   
            MOVE WS-DATE-MM          TO   WS-PRINT-DATE-MM.                     
            MOVE WS-DATE-DD          TO   WS-PRINT-DATE-DD.                     
            MOVE WS-PRINT-DATE       TO   L835-DTE-REGN.                        
            MOVE F800-NUM-HOUSEHOLD  TO   L835-EXIST-HHTY                       
            MOVE F500-CDE-HOUSEHOLD  TO   L835-NEW-HHTY.                        
            MOVE F800-NUM-HOUSEHOLD-FREEZE  TO   L835-FREEZE.                   
            MOVE F800-NUM-REOPT             TO   L835-REOPT.                    
                                                                                
           WRITE PRINT-REC             FROM L835-PR-DETAIL-1 AFTER 1            
           MOVE  SPACES                TO   PRINT-REC.                          
           ADD   1                     TO   WS-CNT-LINE-L835A.                  
           ADD   1                     TO   WS-L835A-PRINT.                     
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       410-PRINT-HEADING.                                                       
           MOVE SPACES         TO    PRINT-REC.                                 
           ADD 1               TO    WS-NO-OF-PAGE-L835A.                       
           MOVE WS-NO-OF-PAGE-L835A  TO    PRINT-PAGE.                          
           WRITE PRINT-REC     FROM  L835-PR-HEAD-01 AFTER PAGE.                
           WRITE PRINT-REC     FROM  L835-PR-HEAD-02 AFTER 1.                   
           WRITE PRINT-REC     FROM  L835-PR-HEAD-03 AFTER 3.                   
           MOVE SPACES         TO    PRINT-REC.                                 
           WRITE PRINT-REC     AFTER 1.                                         
           MOVE 6              TO    WS-CNT-LINE-L835A.                         
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-WRITE-BP13F310.                                                      
                                                                                
           MOVE SPACES TO BP13F310-TRANS-REC.                                   
           MOVE F500-NUM-REGN       TO F310-REGN-NO.                            
           MOVE 'AL'                TO F310-CDE-TRANS.                          
           WRITE BP13F310-TRANS-REC.                                            
           ADD 1 TO WS-F310-WRITE.                                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       600-PRINT-5YR-REPORT.                                                    
           IF WS-PRT-P13L835C = 'Y'                                             
             IF(WS-CNT-LINE-L835C  > WS-MAX-LINES)                              
               PERFORM 620-PRINT-HEADING THRU 620-EXIT                          
             ELSE                                                               
               NEXT SENTENCE                                                    
           ELSE                                                                 
             IF WS-CNT-LINE-L835B  > WS-MAX-LINES                               
               PERFORM 610-PRINT-HEADING THRU 610-EXIT                          
             ELSE                                                               
               NEXT SENTENCE.                                                   
                                                                                
           ADD  1                          TO   WS-SERIAL-NO-A.                 
           MOVE WS-SERIAL-NO-A             TO   L835A-SERIAL-NO.                
           MOVE F500-NUM-REGN              TO   L835A-REGN-NO.                  
           MOVE F500-NUM-NRIC1             TO   L835A-NRIC.                     
           MOVE F500-CDE-ZONE              TO   L835A-ZONE.                     
           MOVE F500-CDE-FLAT-TYPE         TO   L835A-FT.                       
           MOVE F500-CDE-ALLOC-SCH         TO   L835A-ALC-SCH.                  
           MOVE F500-NUM-JB-REGN           TO   L835A-JB-REGN-NO.               
           MOVE F500-DTE-REQUEST           TO   WS-DATE.                        
           MOVE WS-DATE-CCYY               TO   WS-PRINT-DATE-CCYY.             
           MOVE WS-DATE-MM                 TO   WS-PRINT-DATE-MM.               
           MOVE WS-DATE-DD                 TO   WS-PRINT-DATE-DD.               
           MOVE WS-PRINT-DATE              TO   L835A-DTE-REGN.                 
           MOVE F800-NUM-5YR-OCCUPN        TO   L835A-EXIST-5YR.                
           MOVE F500-NUM-5YR-OCCUPN        TO   L835A-NEW-5YR.                  
           MOVE F800-NUM-FREEZE-10YR       TO   L835A-FREEZE.                   
           MOVE F800-NUM-REOPT             TO   L835A-REOPT.                    
           MOVE F800-NUM-HOUSEHOLD         TO   L835A-EXIST-HH.                 
           MOVE F500-CDE-HOUSEHOLD         TO   L835A-NEW-HH.                   
           MOVE F800-NUM-MTH-RES-JB-IND    TO   L835A-JBMTHRES.                 
           MOVE F500-NUM-MTH-RES-JB-IND    TO   L835A-NJBMTHRES.                
           MOVE F800-NUM-MTH-RESIDE        TO   L835A-MTHRES.                   
           MOVE F500-NUM-MTH-RESIDE        TO   L835A-NMTHRES.                  
                                                                                
           IF WS-PRT-P13L835C = 'Y'                                             
              WRITE PRINT-REC-B         FROM L835A-PR-DETAIL-1 AFTER 1          
              MOVE  SPACES                TO   PRINT-REC-B                      
              ADD   1                     TO   WS-CNT-LINE-L835C                
              ADD   1                     TO   WS-L835C-PRINT                   
           ELSE                                                                 
              WRITE PRINT-REC-A         FROM L835A-PR-DETAIL-1 AFTER 1          
              MOVE  SPACES                TO   PRINT-REC-A                      
              MOVE 'N'                    TO   WS-PRT-P13L835C                  
              ADD   1                     TO   WS-CNT-LINE-L835B                
              ADD   1                     TO   WS-L835B-PRINT.                  
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
       610-PRINT-HEADING.                                                       
           MOVE SPACES          TO    PRINT-REC-A.                              
           ADD 1                TO    WS-NO-OF-PAGE-L835B.                      
           MOVE WS-NO-OF-PAGE-L835B  TO    PRINT-PAGE-B.                        
           WRITE PRINT-REC-A    FROM  L835A-PR-HEAD-01 AFTER PAGE.              
           WRITE PRINT-REC-A    FROM  L835A-PR-HEAD-02 AFTER 1.                 
           WRITE PRINT-REC-A    FROM  L835A-PR-HEAD-03 AFTER 3.                 
           WRITE PRINT-REC-A    FROM  L835A-PR-HEAD-04 AFTER 1.                 
           MOVE SPACES          TO    PRINT-REC-A.                              
           WRITE PRINT-REC-A    AFTER 1.                                        
           MOVE 6               TO    WS-CNT-LINE-L835B.                        
                                                                                
       610-EXIT.                                                                
           EXIT.                                                                
                                                                                
       620-PRINT-HEADING.                                                       
           MOVE SPACES          TO    PRINT-REC-B.                              
           ADD 1                TO    WS-NO-OF-PAGE-L835C.                      
           MOVE WS-NO-OF-PAGE-L835C TO    PRINT-PAGE-C.                         
           WRITE PRINT-REC-B    FROM  L835C-PR-HEAD-01 AFTER PAGE.              
           WRITE PRINT-REC-B    FROM  L835C-PR-HEAD-02 AFTER 1.                 
           WRITE PRINT-REC-B    FROM  L835A-PR-HEAD-03 AFTER 3.                 
           WRITE PRINT-REC-B    FROM  L835A-PR-HEAD-04 AFTER 1.                 
           MOVE SPACES          TO    PRINT-REC-B.                              
           WRITE PRINT-REC-B    AFTER 1.                                        
           MOVE 6               TO    WS-CNT-LINE-L835C.                        
                                                                                
       620-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-FINALIZATION.                                                        
             DISPLAY '*----------- BP13C835  ------------*'.                    
             DISPLAY ' F800-READ      ' WS-F800-READ.                           
             DISPLAY ' F500-READ      ' WS-F500-READ.                           
             DISPLAY ' L835A-PRINT     ' WS-L835A-PRINT.                        
             DISPLAY ' L835B-PRINT     ' WS-L835B-PRINT.                        
             DISPLAY ' L835C-PRINT     ' WS-L835C-PRINT.                        
             DISPLAY ' RECORD MATCHED  ' WS-MATCH.                              
             DISPLAY ' F800-REGN-NOT-MT' WS-F800-NOTMATCH.                      
             DISPLAY ' F500-REGN-NOT-MT' WS-F500-NOTMATCH.                      
             DISPLAY ' CDE HH MATCH    ' WS-CDE-HHMATCH.                        
             DISPLAY ' CDE HH CHANGED  ' WS-HHTY-CHG.                           
             DISPLAY ' CDE 5YR MATCH   ' WS-CDE-5YRMATCH.                       
             DISPLAY ' CDE 5YR CHANGED ' WS-5YR-CHG.                            
             DISPLAY ' MTH RES MATCH   ' WS-MTH-RES-MTCH.                       
             DISPLAY ' MTH RES CHANGED ' WS-MTH-RES-CHG.                        
             DISPLAY ' F310-WRITE      ' WS-F310-WRITE.                         
                                                                                
           CLOSE      BP13F800                                                  
                      BP13F500                                                  
                      BP13F310                                                  
                      P13L835A                                                  
                      P13L835B                                                  
                      P13L835C.                                                 
                                                                                
