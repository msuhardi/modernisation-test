       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CH14.                                                 
      *AUTHOR.        MICHAEL ARRIOLA.                                          
      *DATE-WRITTEN.  14/10/2008.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVES: TO UPDATE CDE-BALLOT-HOUSEHOLD...              *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF   BY    DATE      DETAILS                          *            
      * -------   ====  ========= ================================ *            
      * BP133440  MSD1  20081014  NEW PGM                          *            
      * BP133501  IL5   20080115  TO WRITE RECORD IN BP13F730      *            
      *                           ONLY WHEN THERE IS A CHANGE      *            
      * BP133796  IL5   20100112  TO AMEND CONDITION FOR NON-      *            
      *                           SELECTION                        *            
      * BP134024  ZDD   20101006  REPLACE BP13K816 TO BP13K813     *            
      * BP133847  ZDD1  20100326  ADD NEW FIELDS ON THE REPORT     *            
      *                           BYPASS UPDATE ON BP13K730 IF IT  *            
      *                           IS NON BTO/SBF CASE              *            
      * BP133847  ZDD1  20100326  TO CHECK FOR SPOUSE AND FIANCE/  *            
      *                           FIANCEE FOR NON SELECTION        *            
      *                           ADD NEW FIELDS ON THE REPORT     *            
      *                           BYPASS UPDATE ON BP13K730 IF IT  *            
      *                           IS A DBSS CASE                   *            
      * BP134356  ZDD1  20110811  STOP CHECKING OTHER NRIC ONCE    *            
      *                           CONDITION WAS MET FOR NON-SELECTION*          
      * BP134782  SMR2  20130214  ALLOCATE 3 BYTES FOR NT FIELD    *            
      * BP134996  SMR2  20130719  IF NUM-FT2T = 'Y' & NON-SEL < 2  *            
      *                           THEN BAL-HH = 'F' ELSE 'G'       *            
      * BP135028  SMR2  20130813  DO NOT CHECK FOR NON-SELECTION   *            
      *                           WHEN ALLOCATION SCHEME = 'FTS'   *            
      * BP135039  SMR2  20140123  CATER FOR CHANGE IN BP13KH10/KH12*            
      * BP135190  SMR2  20131204  EXPAND BP13K813 TO 1000          *            
      * BP135998  SMR2  20150812  CATER FOR BHH = 'N' (NTIMER)     *            
      * BP136852  SMR2  20170623  INCLUDE SALES-MODE = 'ROF'       *            
      * BP138164  AM25  20191216  CATER FOR CHANGE IN NON-SEL      *            
      *                 20200115  NEW OUTPUT FILE P13FH14A         *            
      * BP139875  AM25  20240216  SIMPLIFY & INCL FTPMC IN NON-SEL *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F730  ASSIGN        TO BP13F730.                          
                                                                                
           SELECT BP13KH10  ASSIGN        TO BP13KH10                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KH10-KEY-FLD                       
                            FILE STATUS   IS BP13KH10-STATUS.                   
           SELECT BP13K730  ASSIGN        TO BP13K730                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K730-NUM-REGN                      
                            FILE STATUS   IS BP13K730-STATUS.                   
           SELECT BP13K813  ASSIGN        TO BP13K813                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K813-KEY-FLD                       
                            FILE STATUS   IS BP13K813-STATUS.                   
           SELECT BP13KH12  ASSIGN        TO BP13KH12                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KH12-KEY-FLD                       
                            FILE STATUS   IS BP13KH12-STATUS.                   
           SELECT BP13K825  ASSIGN        TO BP13K825                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K825-KEY-FLD                       
                            FILE STATUS   IS BP13K825-STATUS.                   
                                                                                
                                                                                
           SELECT P13F730A  ASSIGN        TO P13F730A.                          
           SELECT P13LH14A  ASSIGN        TO P13LH14A.                          
           SELECT P13FH14A  ASSIGN        TO P13FH14A.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F730                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F730.                                                           
                                                                                
       FD   BP13KH10                                                            
            RECORD CONTAINS  2000 CHARACTERS.                                   
       COPY BP13KH10.                                                           
                                                                                
       FD   BP13K730                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BP13K730.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13KH12                                                            
            RECORD CONTAINS  2000 CHARACTERS.                                   
       COPY BP13KH12.                                                           
                                                                                
       FD   BP13K825                                                            
            RECORD CONTAINS  200 CHARACTERS.                                    
       COPY BP13K825.                                                           
                                                                                
       FD   P13F730A                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   P13F730A-REC           PIC X(500).                                  
                                                                                
       FD   P13LH14A                                                            
            LABEL RECORDS ARE OMITTED.                                          
       01 P13LH14A-REC             PIC X(132).                                  
                                                                                
       FD   P13FH14A                                                            
            RECORD CONTAINS 150 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   P13FH14A-REC.                                                       
            05 FH14-REGN           PIC X(8).                                    
            05 FH14-NRIC           PIC X(9).                                    
            05 FH14-CDE-NT         PIC X(3).                                    
            05 FH14-CDE-FT         PIC X(2).                                    
            05 FH14-CDE-HH         PIC X(1).                                    
            05 FH14-BALLOT-HH      PIC X(1).                                    
            05 FH14-DTE-REQ        PIC X(10).                                   
            05 FH14-DTE-DEBAR      PIC X(10).                                   
            05 FH14-DTE-EXPIRY     PIC X(10).                                   
            05 FH14-DTE-CLOSE      PIC X(10).                                   
            05 FILLER              PIC X(86).                                   
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01 WS-FILE-STATUS.                                                       
          05 BP13KH10-STATUS       PIC 9(02) VALUE ZEROES.                      
          05 BP13K730-STATUS       PIC 9(02) VALUE ZEROES.                      
          05 BP13K813-STATUS       PIC 9(02) VALUE ZEROES.                      
          05 BP13KH12-STATUS       PIC 9(02) VALUE ZEROES.                      
          05 BP13K825-STATUS       PIC 9(02) VALUE ZEROES.                      
                                                                                
       01 WS-COUNTERS.                                                          
          05 WS-F730-READ          PIC 9(08) VALUE ZEROES.                      
          05 WS-KH10-READ          PIC 9(08) VALUE ZEROES.                      
          05 WS-K730-UPD           PIC 9(08) VALUE ZEROES.                      
          05 WS-G-UPDATED          PIC 9(08) VALUE ZEROES.                      
          05 WS-F730-WRITE         PIC 9(08) VALUE ZEROES.                      
          05 WS-FH14-WRITE         PIC 9(08) VALUE ZEROES.                      
          05 WS-NON-BTO-SBF        PIC 9(08) VALUE ZEROES.                      
                                                                                
       01 WS-VARIABLES.                                                         
          05 WS-SNO                PIC 9(04) VALUE ZERO.                        
          05 WS-PAGENO             PIC 9(03) VALUE ZERO.                        
          05 WS-LINE-CNT           PIC 9(02) VALUE 56.                          
          05 WS-DATE               PIC X(08) VALUE SPACES.                      
          05 WS-NUM-UIN            PIC X(09) VALUE SPACES.                      
          05 WS-NON-SELECTION      PIC X(02).                                   
          05 FILLER REDEFINES WS-NON-SELECTION.                                 
             10 WS-NUM-NON-SELECT  PIC 9(02).                                   
          05 WS-TAG                PIC X(02) VALUE 'N'.                         
          05 WS-DTE-DEBAR-EXPIRY   PIC X(08) VALUE SPACES.                      
          05 WS-DTE-DEBAR          PIC X(08) VALUE SPACES.                      
          05 WS-EOF-KH12           PIC X(01) VALUE 'N'.                         
          05 WS-K825-FOUND         PIC X(02) VALUE 'N'.                         
          05 WS-EOF-K825           PIC X(02) VALUE 'N'.                         
          05 WS-CTR                PIC 9(02) VALUE ZEROES.                      
          05 WS-NON-SEL            PIC X(01) VALUE 'N'.                         
          05 WS-KH12-NON-SEL       PIC X(01) VALUE 'N'.                         
                                                                                
      ***************************                                               
      * PRINT AREA                                                              
      ***************************                                               
       01 LH14-HEADER1.                                                         
          05 FILLER             PIC X(08) VALUE 'P13LH14A'.                     
          05 FILLER             PIC X(06) VALUE SPACES.                         
          05 FILLER             PIC X(04) VALUE 'HDB3'.                         
          05 FILLER             PIC X(38) VALUE SPACES.                         
          05 FILLER             PIC X(20) VALUE 'SYSTEM OF COMMITMENT'.         
          05 FILLER             PIC X(26) VALUE SPACES.                         
          05 FILLER             PIC X(07) VALUE 'DATE : '.                      
          05 LH14-DATE          PIC X(10).                                      
          05 FILLER             PIC X(02) VALUE SPACES.                         
          05 FILLER             PIC X(07) VALUE 'PAGE : '.                      
          05 LH14-PAGE          PIC Z(04).                                      
                                                                                
       01 LH14-HEADER2.                                                         
          05 FILLER             PIC X(46) VALUE SPACES.                         
          05 FILLER             PIC X(40) VALUE                                 
             "  REPORT ON UPDATE OF HOUSE HOLD TYPE   ".                        
                                                                                
       01 LH14-HEADER3.                                                         
          05 FILLER             PIC X(03) VALUE SPACES.                         
          05 FILLER             PIC X(06) VALUE 'SEQ NO'.                       
          05 FILLER             PIC X(03) VALUE SPACES.                         
          05 FILLER             PIC X(08) VALUE 'REGN NO '.                     
          05 FILLER             PIC X(03) VALUE SPACES.                         
          05 FILLER             PIC X(09) VALUE 'NRIC HA1 '.                    
          05 FILLER             PIC X(03) VALUE SPACES.                         
          05 FILLER             PIC X(03) VALUE 'NT '.                          
          05 FILLER             PIC X(03) VALUE SPACES.                         
          05 FILLER             PIC X(02) VALUE 'FT'.                           
          05 FILLER             PIC X(03) VALUE SPACES.                         
          05 FILLER             PIC X(02) VALUE 'HH'.                           
          05 FILLER             PIC X(03) VALUE SPACES.                         
          05 FILLER             PIC X(09) VALUE 'BALLOT HH'.                    
          05 FILLER             PIC X(03) VALUE SPACES.                         
          05 FILLER             PIC X(11) VALUE 'DTE REQUEST'.                  
          05 FILLER             PIC X(03) VALUE SPACES.                         
          05 FILLER             PIC X(10) VALUE 'DTE DEBAR'.                    
          05 FILLER             PIC X(03) VALUE SPACES.                         
          05 FILLER             PIC X(16) VALUE 'DTE DEBAR EXPIRY'.             
          05 FILLER             PIC X(03) VALUE SPACES.                         
          05 FILLER             PIC X(13) VALUE 'DTE BTO CLOSE'.                
                                                                                
       01 LH14-DASH-LINE        PIC X(132) VALUE ALL '-'.                       
                                                                                
                                                                                
       01 LH14-DETAILS.                                                         
          05 FILLER             PIC X(03) VALUE SPACES.                         
          05 LH14-SNO           PIC Z(06).                                      
          05 FILLER             PIC X(03) VALUE SPACES.                         
          05 LH14-REGN          PIC X(08).                                      
          05 FILLER             PIC X(03) VALUE SPACES.                         
          05 LH14-NRIC          PIC X(09).                                      
          05 FILLER             PIC X(03) VALUE SPACES.                         
          05 LH14-CDE-NT        PIC X(03).                                      
          05 FILLER             PIC X(03) VALUE SPACES.                         
          05 LH14-CDE-FT        PIC X(02).                                      
          05 FILLER             PIC X(03) VALUE SPACES.                         
          05 LH14-CDE-HH        PIC X(02).                                      
          05 FILLER             PIC X(03) VALUE SPACES.                         
          05 LH14-BALLOT-HH     PIC X(09).                                      
          05 FILLER             PIC X(03) VALUE SPACES.                         
          05 LH14-DTE-REQ       PIC X(11).                                      
          05 FILLER             PIC X(03) VALUE SPACES.                         
          05 LH14-DTE-DEBAR     PIC X(10).                                      
          05 FILLER             PIC X(03) VALUE SPACES.                         
          05 LH14-DTE-EXPIRY    PIC X(16).                                      
          05 FILLER             PIC X(03) VALUE SPACES.                         
          05 LH14-DTE-CLOSE     PIC X(10).                                      
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13F730  THRU 2000-EXIT.                          
           PERFORM 3000-PROCESS-DATA   THRU 3000-EXIT                           
              UNTIL F730-KEY-FLD = HIGH-VALUES.                                 
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13KH10                                                 
                       BP13KH12                                                 
                  I-O  BP13K730                                                 
                INPUT  BP13K813                                                 
                       BP13K825                                                 
               OUTPUT  P13F730A                                                 
                       P13LH14A                                                 
                       P13FH14A.                                                
                                                                                
           IF BP13KH10-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13KH10 : ' BP13KH10-STATUS             
              MOVE     BP13KH10-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
           IF BP13K730-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13K730 : ' BP13K730-STATUS             
              MOVE     BP13K730-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
           IF BP13K813-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13K813 : ' BP13K813-STATUS             
              MOVE     BP13K813-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
           IF BP13KH12-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13KH12 : ' BP13KH12-STATUS             
              MOVE     BP13KH12-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
           IF BP13K825-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13K825 : ' BP13K825-STATUS             
              MOVE     BP13K825-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)     TO WS-DATE.                      
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                           DELIMITED BY SIZE INTO LH14-DATE.                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F730.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F730 AT END                                                 
                MOVE HIGH-VALUES TO F730-KEY-FLD                                
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1   TO WS-F730-READ.                                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2100-READ-BP13KH10.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE WS-NUM-UIN         TO KH10-NUM-UIN.                             
                                                                                
           READ BP13KH10.                                                       
                                                                                
           EVALUATE BP13KH10-STATUS                                             
             WHEN 00                                                            
                ADD                          1 TO WS-KH10-READ                  
                PERFORM 2110-PROCESS-KH10 THRU 2110-EXIT                        
             WHEN 23                                                            
                DISPLAY 'REC NOT FOUND IN BP13KH10: ' KH10-NUM-UIN              
             WHEN OTHER                                                         
                DISPLAY 'BP13KH10 READ ERROR,STATUS : ' BP13KH10-STATUS         
                      ', KH10 KEY FLD : ' KH10-KEY-FLD                          
                MOVE BP13KH10-STATUS       TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2110-PROCESS-KH10.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF ((F730-DTE-REQUEST >= KH10-DTE-DEBAR-FTPMC) AND                   
              (K813-DTE-CLOSE-BTO NOT = SPACES AND LOW-VALUES)                  
              AND (K813-DTE-CLOSE-BTO <=                                        
                   KH10-DTE-DEBAR-EXPIRY-FTPMC)) AND                            
              KH10-NUM-NON-SELN-FTPMC > 00                                      
              MOVE KH10-NUM-NON-SELN-FTPMC TO WS-NON-SELECTION                  
              MOVE KH10-DTE-DEBAR-FTPMC    TO WS-DTE-DEBAR                      
              MOVE KH10-DTE-DEBAR-EXPIRY-FTPMC                                  
                                        TO WS-DTE-DEBAR-EXPIRY                  
           ELSE                                                                 
              IF ((F730-DTE-REQUEST >= KH10-DTE-DEBAR-FT-HH) AND                
                 (K813-DTE-CLOSE-BTO NOT = SPACES AND LOW-VALUES)               
                 AND (K813-DTE-CLOSE-BTO <=                                     
                      KH10-DTE-DEBAR-EXPIRY-FT-HH)) AND                         
                 KH10-NUM-NON-SELN-FT > 00                                      
                 MOVE KH10-NUM-NON-SELN-FT   TO WS-NON-SELECTION                
                 MOVE KH10-DTE-DEBAR-FT-HH   TO WS-DTE-DEBAR                    
                 MOVE KH10-DTE-DEBAR-EXPIRY-FT-HH                               
                                           TO WS-DTE-DEBAR-EXPIRY               
              ELSE                                                              
                 IF ((F730-DTE-REQUEST >= KH10-DTE-DEBAR-FT-HH) AND             
                    (K813-DTE-CLOSE-BTO NOT = SPACES AND LOW-VALUES)            
                    AND (K813-DTE-CLOSE-BTO <=                                  
                         KH10-DTE-DEBAR-EXPIRY-FT-HG)) AND                      
                    KH10-NUM-NON-SELN-FT > 00                                   
                    MOVE KH10-NUM-NON-SELN-FT   TO WS-NON-SELECTION             
                    MOVE KH10-DTE-DEBAR-FT-HG   TO WS-DTE-DEBAR                 
                    MOVE KH10-DTE-DEBAR-EXPIRY-FT-HG                            
                                              TO WS-DTE-DEBAR-EXPIRY            
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2150-READ-BP13KH12.                                                      
      *-------------------------------------------------------------            
                                                                                
           IF WS-NUM-UIN(1:1) = SPACES OR LOW-VALUES OR '#'                     
              GO TO 2150-EXIT.                                                  
                                                                                
           MOVE 'N'                TO WS-EOF-KH12                               
                                      WS-KH12-NON-SEL.                          
           MOVE SPACES             TO KH12-KEY-FLD                              
                                      WS-DTE-DEBAR                              
                                      WS-DTE-DEBAR-EXPIRY.                      
           MOVE ZEROES             TO WS-NUM-NON-SELECT.                        
           MOVE WS-NUM-UIN         TO KH12-NUM-NRIC.                            
           START BP13KH12 KEY IS >= KH12-KEY-FLD.                               
                                                                                
           EVALUATE BP13KH12-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
                PERFORM 2160-READNEXT-KH12   THRU 2160-EXIT                     
                   UNTIL WS-EOF-KH12 = 'Y' OR                                   
                   KH12-NUM-NRIC NOT = WS-NUM-UIN                               
                IF WS-KH12-NON-SEL = 'N'                                        
                   PERFORM 2100-READ-BP13KH10 THRU 2100-EXIT                    
                END-IF                                                          
             WHEN 23                                                            
                PERFORM 2100-READ-BP13KH10 THRU 2100-EXIT                       
             WHEN OTHER                                                         
                DISPLAY 'BP13KH12 READ ERROR,STATUS : ' BP13KH12-STATUS         
                      ', KH12 KEY FLD : ' KH12-KEY-FLD                          
                MOVE BP13KH12-STATUS       TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       2150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2160-READNEXT-KH12.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13KH12 NEXT RECORD.                                           
                                                                                
           EVALUATE BP13KH12-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
                IF KH12-NUM-NRIC = WS-NUM-UIN                                   
                   PERFORM 2170-PROCESS-KH12 THRU 2170-EXIT                     
                END-IF                                                          
             WHEN 10                                                            
                MOVE 'Y' TO WS-EOF-KH12                                         
             WHEN OTHER                                                         
                DISPLAY 'BP13KH12 READ ERROR,STATUS : ' BP13KH12-STATUS         
                      ', KH12 KEY FLD : ' KH12-KEY-FLD                          
                MOVE BP13KH12-STATUS       TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       2160-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2170-PROCESS-KH12.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF ((F730-DTE-REQUEST >= KH12-DTE-DEBAR-FTPMC) AND                   
              (K813-DTE-CLOSE-BTO NOT = SPACES AND LOW-VALUES)                  
              AND (K813-DTE-CLOSE-BTO <=                                        
                   KH12-DTE-DEBAR-EXPIRY-FTPMC)) AND                            
              KH12-NUM-NON-SELN-FTPMC > 00                                      
              MOVE KH12-NUM-NON-SELN-FTPMC TO WS-NON-SELECTION                  
              MOVE KH12-DTE-DEBAR-FTPMC    TO WS-DTE-DEBAR                      
              MOVE KH12-DTE-DEBAR-EXPIRY-FTPMC                                  
                                        TO WS-DTE-DEBAR-EXPIRY                  
              MOVE 'Y'                  TO WS-KH12-NON-SEL                      
           ELSE                                                                 
              IF ((F730-DTE-REQUEST >= KH12-DTE-DEBAR-FT-HH) AND                
                 (K813-DTE-CLOSE-BTO NOT = SPACES AND LOW-VALUES)               
                 AND (K813-DTE-CLOSE-BTO <=                                     
                      KH12-DTE-DEBAR-EXPIRY-FT-HH)) AND                         
                 KH12-NUM-NON-SELN-FT > 00                                      
                 MOVE KH12-NUM-NON-SELN-FT   TO WS-NON-SELECTION                
                 MOVE KH12-DTE-DEBAR-FT-HH   TO WS-DTE-DEBAR                    
                 MOVE KH12-DTE-DEBAR-EXPIRY-FT-HH                               
                                           TO WS-DTE-DEBAR-EXPIRY               
                 MOVE 'Y'                  TO WS-KH12-NON-SEL                   
              ELSE                                                              
                 IF ((F730-DTE-REQUEST >= KH12-DTE-DEBAR-FT-HH) AND             
                    (K813-DTE-CLOSE-BTO NOT = SPACES AND LOW-VALUES)            
                    AND (K813-DTE-CLOSE-BTO <=                                  
                         KH12-DTE-DEBAR-EXPIRY-FT-HG)) AND                      
                    KH12-NUM-NON-SELN-FT > 00                                   
                    MOVE KH12-NUM-NON-SELN-FT   TO WS-NON-SELECTION             
                    MOVE KH12-DTE-DEBAR-FT-HG   TO WS-DTE-DEBAR                 
                    MOVE KH12-DTE-DEBAR-EXPIRY-FT-HG                            
                                              TO WS-DTE-DEBAR-EXPIRY            
                    MOVE 'Y'                  TO WS-KH12-NON-SEL                
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2170-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2200-READ-BP13K813.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACE                 TO BP13K813-REC.                          
           INITIALIZE                    BP13K813-REC.                          
                                                                                
           MOVE F730-CDE-NT1          TO K813-NUM-ZONE.                         
           MOVE F730-CDE-FLAT-TYPE    TO K813-NUM-FLAT-TYPE.                    
           MOVE F730-DTE-BALLOT       TO K813-DTE-BALLOT.                       
                                                                                
           READ BP13K813.                                                       
           EVALUATE BP13K813-STATUS                                             
             WHEN 00                                                            
                CONTINUE                                                        
             WHEN 23                                                            
                DISPLAY 'REC NOT FOUND IN BP13K813'                             
                      ', K813-NUM-ZONE: ' K813-NUM-ZONE                         
             WHEN OTHER                                                         
                DISPLAY 'BP13K813 READ ERROR,STATUS : ' BP13K813-STATUS         
                      ', K813 KEY FLD : ' K813-KEY-FLD                          
                MOVE BP13K813-STATUS       TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2300-PROCESS-NON-SEL.                                                    
      *-------------------------------------------------------------            
                                                                                
           EVALUATE WS-CTR                                                      
             WHEN 1                                                             
                MOVE F730-NUM-NRIC1        TO WS-NUM-UIN                        
                PERFORM 2150-READ-BP13KH12 THRU 2150-EXIT                       
             WHEN 2                                                             
                MOVE F730-NUM-NRIC2        TO WS-NUM-UIN                        
                PERFORM 2150-READ-BP13KH12 THRU 2150-EXIT                       
             WHEN 3                                                             
                MOVE F730-NUM-NRIC3        TO WS-NUM-UIN                        
                PERFORM 2150-READ-BP13KH12 THRU 2150-EXIT                       
             WHEN 4                                                             
                MOVE F730-NUM-NRIC4        TO WS-NUM-UIN                        
                PERFORM 2150-READ-BP13KH12 THRU 2150-EXIT                       
             WHEN 5                                                             
      ***TO CHECK FOR SPOUSE NRIC***                                            
                MOVE SPACES                TO K825-KEY-FLD                      
                MOVE F730-NUM-REGN         TO K825-NUM-REGN                     
                MOVE F730-NUM-NRIC1        TO WS-NUM-UIN                        
                PERFORM 6000-READ-BP13K825 THRU 6000-EXIT                       
                IF WS-K825-FOUND = 'Y'                                          
                  PERFORM 2150-READ-BP13KH12 THRU 2150-EXIT                     
                END-IF                                                          
             WHEN 6                                                             
                MOVE F730-NUM-NRIC2        TO WS-NUM-UIN                        
                PERFORM 6000-READ-BP13K825 THRU 6000-EXIT                       
                IF WS-K825-FOUND = 'Y'                                          
                   PERFORM 2150-READ-BP13KH12 THRU 2150-EXIT                    
                END-IF                                                          
             WHEN 7                                                             
                MOVE F730-NUM-NRIC3        TO WS-NUM-UIN                        
                PERFORM 6000-READ-BP13K825 THRU 6000-EXIT                       
                IF WS-K825-FOUND = 'Y'                                          
                  PERFORM 2150-READ-BP13KH12 THRU 2150-EXIT                     
                END-IF                                                          
             WHEN 8                                                             
                MOVE F730-NUM-NRIC4        TO WS-NUM-UIN                        
                PERFORM 6000-READ-BP13K825 THRU 6000-EXIT                       
                IF WS-K825-FOUND = 'Y'                                          
                  PERFORM 2150-READ-BP13KH12 THRU 2150-EXIT                     
                END-IF                                                          
             WHEN 9                                                             
      ***TO CHECK FOR FIANCE/FIANCEE NRIC***                                    
                MOVE 'N'                    TO WS-K825-FOUND                    
                MOVE SPACES                 TO K825-KEY-FLD                     
                MOVE F730-NUM-REGN          TO K825-NUM-REGN                    
                PERFORM 6100-START-BP13K825 THRU 6100-EXIT                      
                IF WS-K825-FOUND = 'Y'                                          
                  PERFORM 2150-READ-BP13KH12 THRU 2150-EXIT                     
                END-IF                                                          
           END-EVALUATE.                                                        
                                                                                
           IF WS-NUM-NON-SELECT > 00 AND                                        
              (F730-CDE-BALLOT-HOUSEHOLD = 'H' OR 'T' OR 'F')                   
              IF ((F730-DTE-REQUEST >= WS-DTE-DEBAR) AND                        
                 (K813-DTE-CLOSE-BTO NOT = SPACES AND LOW-VALUES)               
                  AND (K813-DTE-CLOSE-BTO <= WS-DTE-DEBAR-EXPIRY))              
                 ADD                1 TO WS-G-UPDATED                           
                 MOVE 'G'             TO F730-CDE-BALLOT-HOUSEHOLD              
                 MOVE 'Y'             TO WS-TAG                                 
                 MOVE 'Y'             TO WS-NON-SEL                             
              END-IF                                                            
           END-IF.                                                              
                                                                                
           ADD 1 TO WS-CTR.                                                     
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE F730-CDE-HOUSEHOLD    TO F730-CDE-BALLOT-HOUSEHOLD.             
           IF F730-CDE-HOUSEHOLD = 'G' AND F730-NUM-FT2T = 'Y'                  
              MOVE 'F'                TO F730-CDE-BALLOT-HOUSEHOLD              
           END-IF.                                                              
           IF F730-CDE-HOUSEHOLD = 'G' AND F730-NUM-NTIMER = 'Y'                
              MOVE 'N'                TO F730-CDE-BALLOT-HOUSEHOLD              
           END-IF.                                                              
                                                                                
           PERFORM 2200-READ-BP13K813 THRU 2200-EXIT.                           
                                                                                
           MOVE 1                     TO WS-CTR.                                
           MOVE 'N'                   TO WS-NON-SEL.                            
           MOVE 'N'                   TO WS-TAG.                                
                                                                                
           IF F730-CDE-ALLOC-SCH NOT = 'FTS'                                    
              PERFORM 2300-PROCESS-NON-SEL THRU 2300-EXIT                       
                UNTIL WS-CTR > 9 OR WS-NON-SEL = 'Y'                            
           END-IF.                                                              
                                                                                
           WRITE P13F730A-REC       FROM BP13F730-REC                           
           ADD                      1 TO WS-F730-WRITE.                         
                                                                                
           IF F730-NUM-ALLO-CAT = 'BTO' OR 'SBF'                                
             PERFORM 4000-UPDATE-BP13K730  THRU 4000-EXIT                       
           ELSE                                                                 
             ADD 1 TO WS-NON-BTO-SBF                                            
           END-IF.                                                              
                                                                                
           IF WS-TAG = 'Y'                                                      
              PERFORM 5000-GEN-RPT          THRU 5000-EXIT                      
              PERFORM 5500-WRITE-P13FH14A   THRU 5500-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F730    THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-UPDATE-BP13K730.                                                    
      *-------------------------------------------------------------            
                                                                                
           MOVE F730-NUM-REGN         TO K730-NUM-REGN.                         
                                                                                
           READ BP13K730.                                                       
           EVALUATE BP13K730-STATUS                                             
             WHEN 00                                                            
                PERFORM 4100-REWRITE-BP13K730 THRU 4100-EXIT                    
             WHEN 23                                                            
                DISPLAY 'REC NOT FOUND IN BP13K730'                             
                      ', K730-NUM-REGN: ' K730-NUM-REGN                         
             WHEN OTHER                                                         
                DISPLAY 'BP13K730 READ ERROR,STATUS : ' BP13K730-STATUS         
                      ', K730 KEY FLD : ' K730-KEY-FLD                          
                MOVE BP13K730-STATUS       TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4100-REWRITE-BP13K730.                                                   
      *-------------------------------------------------------------            
                                                                                
           MOVE F730-CDE-BALLOT-HOUSEHOLD TO                                    
                                  K730-CDE-BALLOT-HOUSEHOLD.                    
                                                                                
           REWRITE BP13K730-REC.                                                
           EVALUATE BP13K730-STATUS                                             
           WHEN 00                                                              
               ADD 1 TO WS-K730-UPD                                             
           WHEN OTHER                                                           
               DISPLAY 'BP13K730 REWRITE ERROR STATUS ' BP13K730-STATUS         
                       ' KEY IS ' K730-KEY-FLD                                  
               MOVE BP13K730-STATUS          TO RETURN-CODE                     
               PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-GEN-RPT.                                                            
      *-------------------------------------------------------------            
           IF WS-LINE-CNT > 55                                                  
              PERFORM 5100-PRINT-HEADER THRU 5100-EXIT.                         
                                                                                
           MOVE SPACES                    TO LH14-DETAILS.                      
                                                                                
           ADD                          1 TO WS-SNO.                            
           MOVE WS-SNO                    TO LH14-SNO.                          
                                                                                
           MOVE F730-NUM-REGN             TO LH14-REGN                          
           MOVE F730-CDE-HOUSEHOLD        TO LH14-CDE-HH                        
           MOVE F730-CDE-BALLOT-HOUSEHOLD TO LH14-BALLOT-HH                     
           MOVE F730-CDE-NT1              TO LH14-CDE-NT                        
           MOVE F730-CDE-FLAT-TYPE        TO LH14-CDE-FT                        
           MOVE F730-NUM-NRIC1            TO LH14-NRIC                          
                                                                                
           STRING F730-DTE-REQUEST(7:2) '/' F730-DTE-REQUEST(5:2) '/'           
                  F730-DTE-REQUEST(1:4)                                         
                  DELIMITED BY SIZE INTO LH14-DTE-REQ.                          
                                                                                
      *    STRING KH10-DTE-DEBAR(7:2) '/' KH10-DTE-DEBAR(5:2) '/'               
      *           KH10-DTE-DEBAR(1:4)                                           
      *           DELIMITED BY SIZE INTO LH14-DTE-DEBAR.                        
                                                                                
           STRING WS-DTE-DEBAR(7:2) '/' WS-DTE-DEBAR(5:2) '/'                   
                  WS-DTE-DEBAR(1:4)                                             
                  DELIMITED BY SIZE INTO LH14-DTE-DEBAR.                        
                                                                                
      *    STRING KH10-DTE-DEBAR-EXPIRY(7:2) '/'                                
      *           KH10-DTE-DEBAR-EXPIRY(5:2)  '/'                               
      *           KH10-DTE-DEBAR-EXPIRY(1:4)                                    
      *           DELIMITED BY SIZE INTO LH14-DTE-EXPIRY.                       
                                                                                
           STRING WS-DTE-DEBAR-EXPIRY(7:2) '/'                                  
                  WS-DTE-DEBAR-EXPIRY(5:2)  '/'                                 
                  WS-DTE-DEBAR-EXPIRY(1:4)                                      
                  DELIMITED BY SIZE INTO LH14-DTE-EXPIRY.                       
                                                                                
           STRING K813-DTE-CLOSE-BTO(7:2) '/'                                   
                  K813-DTE-CLOSE-BTO(5:2)  '/' K813-DTE-CLOSE-BTO(1:4)          
                  DELIMITED BY SIZE INTO LH14-DTE-CLOSE.                        
                                                                                
           WRITE P13LH14A-REC           FROM LH14-DETAILS.                      
           ADD 1        TO WS-LINE-CNT.                                         
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5100-PRINT-HEADER.                                                       
      *-------------------------------------------------------------            
           ADD   1            TO WS-PAGENO.                                     
           MOVE WS-PAGENO     TO LH14-PAGE.                                     
                                                                                
                                                                                
           WRITE P13LH14A-REC  FROM LH14-HEADER1   AFTER PAGE                   
           WRITE P13LH14A-REC  FROM LH14-HEADER2   AFTER 1                      
           WRITE P13LH14A-REC  FROM LH14-HEADER3   AFTER 2                      
           WRITE P13LH14A-REC  FROM LH14-DASH-LINE AFTER 1                      
           MOVE   5              TO WS-LINE-CNT.                                
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5500-WRITE-P13FH14A.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                 TO P13FH14A-REC.                         
           INITIALIZE P13FH14A-REC.                                             
                                                                                
           MOVE LH14-REGN              TO FH14-REGN.                            
           MOVE LH14-NRIC              TO FH14-NRIC.                            
           MOVE LH14-CDE-NT            TO FH14-CDE-NT.                          
           MOVE LH14-CDE-FT            TO FH14-CDE-FT.                          
           MOVE LH14-CDE-HH            TO FH14-CDE-HH.                          
           MOVE LH14-BALLOT-HH         TO FH14-BALLOT-HH.                       
           MOVE LH14-DTE-REQ           TO FH14-DTE-REQ.                         
           MOVE LH14-DTE-DEBAR         TO FH14-DTE-DEBAR.                       
           MOVE LH14-DTE-EXPIRY        TO FH14-DTE-EXPIRY.                      
           MOVE LH14-DTE-CLOSE         TO FH14-DTE-CLOSE.                       
                                                                                
           WRITE P13FH14A-REC.                                                  
           ADD 1                       TO WS-FH14-WRITE.                        
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-READ-BP13K825.                                                      
      *-------------------------------------------------------------            
                                                                                
           IF WS-NUM-UIN(1:1) = SPACES OR LOW-VALUES OR '#'                     
              MOVE 'N'  TO WS-K825-FOUND                                        
              GO TO 6000-EXIT.                                                  
                                                                                
           MOVE WS-NUM-UIN         TO K825-NUM-NRIC.                            
                                                                                
           READ BP13K825.                                                       
                                                                                
           EVALUATE BP13K825-STATUS                                             
             WHEN 00                                                            
                  MOVE 'Y'  TO WS-K825-FOUND                                    
                  MOVE K825-NUM-NRIC-SPOUSE TO WS-NUM-UIN                       
             WHEN 23                                                            
                MOVE 'N'  TO WS-K825-FOUND                                      
                DISPLAY 'REC NOT FOUND IN BP13K825'                             
                      ', K825-NUM-NRIC: ' K825-NUM-NRIC                         
             WHEN OTHER                                                         
                DISPLAY 'BP13K825 READ ERROR,STATUS : ' BP13K825-STATUS         
                      ', K825 KEY FLD : ' K825-KEY-FLD                          
                MOVE BP13K825-STATUS       TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6100-START-BP13K825.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE 'N'     TO  WS-EOF-K825.                                        
                                                                                
           START BP13K825 KEY IS >= K825-NUM-REGN                               
                                                                                
           EVALUATE BP13K825-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
                 PERFORM 6200-READNEXT-K825   THRU 6200-EXIT                    
                   UNTIL F730-NUM-REGN NOT = K825-NUM-REGN OR                   
                          WS-EOF-K825 = 'Y'                                     
             WHEN 23                                                            
             WHEN 10                                                            
                MOVE 'N'  TO WS-K825-FOUND                                      
                MOVE 'Y'  TO WS-EOF-K825                                        
                DISPLAY 'REC NOT FOUND IN BP13K825-PARA 6100'                   
                      ', K825-NUM-NRIC: ' K825-NUM-NRIC                         
             WHEN OTHER                                                         
                DISPLAY 'BP13K825 READ ERROR,STATUS : ' BP13K825-STATUS         
                      ', K825 KEY FLD : ' K825-KEY-FLD                          
                MOVE BP13K825-STATUS       TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6200-READNEXT-K825.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K825 NEXT RECORD                                            
                                                                                
           EVALUATE BP13K825-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
                 IF F730-NUM-REGN  = K825-NUM-REGN                              
                   IF K825-NUM-NRIC NOT = F730-NUM-NRIC1 AND                    
                                          F730-NUM-NRIC2 AND                    
                                          F730-NUM-NRIC3 AND                    
                                          F730-NUM-NRIC4                        
                     IF K825-NUM-RELATIONSHIP   = '09'                          
                        MOVE 'Y' TO WS-K825-FOUND                               
                        MOVE 'Y' TO WS-EOF-K825                                 
                        MOVE K825-NUM-NRIC TO WS-NUM-UIN                        
                     END-IF                                                     
                   END-IF                                                       
                 END-IF                                                         
                                                                                
             WHEN 23                                                            
             WHEN 10                                                            
                MOVE 'N'  TO WS-K825-FOUND                                      
                MOVE 'Y'  TO WS-EOF-K825                                        
                DISPLAY 'REC NOT FOUND IN BP13K825-PARA 6100'                   
                      ', K825-NUM-NRIC: ' K825-NUM-NRIC                         
             WHEN OTHER                                                         
                DISPLAY 'BP13K825 READ ERROR,STATUS : ' BP13K825-STATUS         
                      ', K825 KEY FLD : ' K825-KEY-FLD                          
                MOVE BP13K825-STATUS       TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
       6200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' BP13CH14 CONTROL TOTAL '.                                  
           DISPLAY '------------------------------------------'.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECS READ  FROM BP13F730 : ' WS-F730-READ.            
           DISPLAY 'NO OF RECS READ  FROM BP13KH10 : ' WS-KH10-READ.            
           DISPLAY 'NO OF RECS UPDATED    BP13K730 : ' WS-K730-UPD.             
           DISPLAY 'NO OF RECS WRITTEN    P13F730A : ' WS-F730-WRITE.           
           DISPLAY 'NO OF RECS WRITTEN    P13FH14A : ' WS-FH14-WRITE.           
           DISPLAY 'NO OF RECS UPDATED TO HHTYP=G  : ' WS-G-UPDATED.            
           DISPLAY 'NO OF RECS NON BTO AND SBF     : ' WS-NON-BTO-SBF.          
                                                                                
           CLOSE BP13F730                                                       
                 BP13KH10                                                       
                 BP13KH12                                                       
                 BP13K730                                                       
                 BP13K813                                                       
                 BP13K825                                                       
                 P13F730A                                                       
                 P13LH14A                                                       
                 P13FH14A.                                                      
                                                                                
           IF BP13KH10-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13KH10 : ' BP13KH10-STATUS             
              MOVE BP13KH10-STATUS TO RETURN-CODE.                              
           IF BP13K730-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13K730 : ' BP13K730-STATUS             
              MOVE BP13K730-STATUS TO RETURN-CODE.                              
           IF BP13K813-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13K813 : ' BP13K813-STATUS             
              MOVE BP13K813-STATUS TO RETURN-CODE.                              
           IF BP13KH12-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13KH12 : ' BP13KH12-STATUS             
              MOVE BP13KH12-STATUS TO RETURN-CODE.                              
           IF BP13K825-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13K825 : ' BP13K825-STATUS             
              MOVE BP13K825-STATUS TO RETURN-CODE.                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
