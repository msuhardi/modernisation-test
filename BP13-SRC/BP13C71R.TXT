       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C71R.                                                  
      *AUTHOR.       EDWIN ANGELES (EAA1).                                      
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *  OBJECTIVE:                                                *            
      *    UPDATE THE FF. NEW FIELDS FROM BP17K200 TO SOC MASTER   *            
      *    (BP13K800):                                             *            
      *      -> K200-NUM-HH-CITIZENSHIP                            *            
      *      -> K200-NUM-SPRQUOTA-TAG                              *            
      *      -> K200-NUM-SCSPR-OPT                                 *            
      *      -> K200-NUM-LBS                                       *            
      *      -> K200-NUM-BAL-LEASE                                 *            
      *      -> K200-NUM-BUY-SELL-TAG                              *            
      *      -> K200-NUM-SERS-RSL-STATUS                           *            
      *      -> K200-NUM-SALE-BROCHURE-SCPY                        *            
      *                                                            *            
      *  READ BP13F520 TO EXTRACT BP17K200 AND UPDATE SOC MASTER   *            
      *  (BP13K800)                                                *            
      *                                                            *            
      *  I    :  BP13F520 / BP17K000 / BP17K200                    *            
      *  I/O  :  BP13K800                                          *            
      *------------------------------------------------------------*            
      *  REQ-NO  DATE     OIC  DESCRIPTION                         *            
      *  ======  ====     ===  ==============                      *            
      * BP134838 23052013 EAA1 CHANGE BP17K000 FROM K000.SCHREF TO *            
      *                        K000.SCHREF.SERSITE AND READ IT BY  *            
      *                        K000-NUM-REF                        *            
      * BP134838 03092013 ASY5 TO MOVE K200-NUM-SALE-BROCHURE-SCPY *            
      *                        TO K800-NUM-SALE-BROCHURE-SCPY      *            
      * BP135168 02122013 YCH2 TO UPDATE HLE NUMBER FROM BP17K270  *            
      * BP135974 02112015 IL5  CHANGE OF FIELD NAME IN BP13        *            
      *                        K800-NUM-LEASE-BAL TO               *            
      *                        K800-NUM-LEASE-TENURE               *            
      * BP139645 13072023 YCH2 MOVE K200-NUM-ELG-50YR  TO          *            
      *                        K800-NUM-ELG-50YR   AND             *            
      *                        K200-NUM-ELDERLY-EXEMPTION TO       *            
      *                        K800-NUM-ELDERLY-EXEMPTION          *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F520 ASSIGN TO BP13F520.                                  
                                                                                
           SELECT BP17K000  ASSIGN TO BP17K000                                  
                  ORGANIZATION     IS INDEXED                                   
                  ACCESS MODE      IS RANDOM                                    
                  RECORD KEY       IS K000-KEY-FLD                              
                  ALTERNATE RECORD KEY IS K000-NUM-REF                          
                  FILE STATUS      IS WS-BP17K000-STATUS.                       
                                                                                
           SELECT BP17K200  ASSIGN TO BP17K200                                  
                  ORGANIZATION     IS INDEXED                                   
                  ACCESS MODE      IS RANDOM                                    
                  RECORD KEY       IS K200-KEY-FLD                              
                  FILE STATUS      IS WS-BP17K200-STATUS.                       
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS WS-BP13K800-STATUS.                        
                                                                                
           SELECT BP17K270 ASSIGN TO BP17K270                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K270-KEY-FLD                               
                  FILE STATUS     IS WS-BP17K270-STATUS.                        
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F520   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 500 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD                                
                      RECORDING MODE IS F.                                      
       COPY BP13F520.                                                           
                                                                                
       FD   BP17K000                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP17K000.                                                           
                                                                                
       FD   BP17K200                                                            
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP17K200.                                                           
                                                                                
       FD   BP17K270                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP17K270.                                                           
                                                                                
       FD  BP13K800                                                             
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-SWITCHES.                                                         
           05  WS-BP13K800-STATUS        PIC 9(2)  VALUE ZEROES.                
           05  WS-BP17K000-STATUS        PIC 9(2)  VALUE ZEROES.                
           05  WS-BP17K200-STATUS        PIC 9(2)  VALUE ZEROES.                
           05  WS-BP17K270-STATUS        PIC 9(2)  VALUE ZEROES.                
                                                                                
           05  WS-F520-EOF               PIC X     VALUE 'N'.                   
           05  WS-K270-FOUND             PIC X     VALUE 'N'.                   
                                                                                
           05  WS-F520-READ              PIC 9(6)  VALUE ZEROS.                 
           05  WS-K000-FOUND             PIC 9(6)  VALUE ZEROS.                 
           05  WS-K000-NOTFOUND          PIC 9(6)  VALUE ZEROS.                 
           05  WS-K200-FOUND             PIC 9(6)  VALUE ZEROS.                 
           05  WS-K200-NOTFOUND          PIC 9(6)  VALUE ZEROS.                 
           05  WS-K800-READ              PIC 9(6)  VALUE ZEROS.                 
           05  WS-K800-REWRITE           PIC 9(6)  VALUE ZEROS.                 
           05  WS-K800-NOTFOUND          PIC 9(6)  VALUE ZEROS.                 
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-LOGIC.                                                          
            PERFORM 100-OPEN-FILES      THRU 100-EXIT.                          
            PERFORM 200-READ-BP13F520   THRU 200-EXIT.                          
            PERFORM 300-PROCESS-F520    THRU 300-EXIT                           
                    UNTIL WS-F520-EOF = 'Y'.                                    
            PERFORM 999-CLOSE-FILES     THRU 999-EXIT.                          
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F520                                                 
                       BP17K000                                                 
                       BP17K200                                                 
                       BP17K270                                                 
                I-O    BP13K800.                                                
                                                                                
           IF WS-BP13K800-STATUS NOT = 00 AND 97                                
              DISPLAY 'OPENING ERROR, K800-STATUS ' WS-BP13K800-STATUS          
              MOVE WS-BP13K800-STATUS   TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           IF WS-BP17K000-STATUS NOT = 00 AND 97                                
              DISPLAY 'OPENING ERROR, K000-STATUS ' WS-BP17K000-STATUS          
              MOVE WS-BP17K000-STATUS   TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           IF WS-BP17K200-STATUS NOT = 00 AND 97                                
              DISPLAY 'OPENING ERROR, K200-STATUS ' WS-BP17K200-STATUS          
              MOVE WS-BP17K200-STATUS   TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           IF WS-BP17K270-STATUS NOT = 00 AND 97                                
              DISPLAY 'OPENING ERROR, K270-STATUS ' WS-BP17K270-STATUS          
              MOVE WS-BP17K270-STATUS   TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-BP13F520.                                                       
      *-------------------------------------------------------------            
           READ BP13F520 AT END                                                 
                MOVE 'Y'  TO WS-F520-EOF                                        
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1      TO WS-F520-READ.                                          
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PROCESS-F520.                                                        
      *-------------------------------------------------------------            
           MOVE SPACES                      TO K000-NUM-REF.                    
           MOVE F520-NUM-REF                TO K000-NUM-REF.                    
                                                                                
           PERFORM 400-READ-BP17K000      THRU 400-EXIT.                        
           PERFORM 200-READ-BP13F520      THRU 200-EXIT.                        
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-READ-BP17K000.                                                       
      *-------------------------------------------------------------            
           READ BP17K000 KEY IS K000-NUM-REF.                                   
                                                                                
           EVALUATE WS-BP17K000-STATUS                                          
              WHEN 00                                                           
              WHEN 02                                                           
                   IF F520-NUM-REGN = K000-NUM-REGNO                            
                      ADD 1                   TO WS-K000-FOUND                  
                      MOVE SPACES             TO K270-KEY-FLD                   
                      MOVE K000-NUM-SCH-ACC   TO K270-NUM-HDB-REF(1:9)          
                      MOVE '#########'        TO K270-NUM-ID                    
                      PERFORM 402-READ-BP17K270 THRU 402-EXIT                   
                                                                                
                      MOVE K000-NUM-SCH-ACC   TO K200-NUM-SCH-ACC               
                      PERFORM 405-READ-BP17K200 THRU 405-EXIT                   
                   ELSE                                                         
                      DISPLAY 'BP17K000 REGNO NOT FOUND = '                     
                               F520-NUM-REGN                                    
                      ADD  1                      TO WS-K000-NOTFOUND           
                   END-IF                                                       
                                                                                
              WHEN 23                                                           
                   DISPLAY 'BP17K000 REFNUM NOT FOUND = '  F520-NUM-REF         
                   ADD  1                         TO WS-K000-NOTFOUND           
                                                                                
              WHEN OTHER                                                        
                   DISPLAY 'ERROR READING BP17K000 = '                          
      -                                           WS-BP17K000-STATUS            
                   ADD  1                         TO WS-K000-NOTFOUND           
           END-EVALUATE.                                                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       402-READ-BP17K270.                                                       
      *-------------------------------------------------------------            
           READ BP17K270.                                                       
                                                                                
           EVALUATE WS-BP17K270-STATUS                                          
              WHEN 00                                                           
              WHEN 02                                                           
                   MOVE 'Y'      TO WS-K270-FOUND                               
                                                                                
              WHEN 23                                                           
                   MOVE 'N'      TO WS-K270-FOUND                               
                                                                                
              WHEN OTHER                                                        
                   MOVE 'N'      TO WS-K270-FOUND                               
                   DISPLAY 'READ BP17K270 ERROR ' WS-BP17K270-STATUS            
                   DISPLAY 'SCHACC ' K000-NUM-SCH-ACC                           
           END-EVALUATE.                                                        
                                                                                
       402-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       405-READ-BP17K200.                                                       
      *-------------------------------------------------------------            
           READ BP17K200.                                                       
                                                                                
           EVALUATE WS-BP17K200-STATUS                                          
              WHEN 00                                                           
              WHEN 02                                                           
                   ADD 1                          TO WS-K200-FOUND              
                   PERFORM 500-UPDATE-BP13K800  THRU 500-EXIT                   
                                                                                
              WHEN 23                                                           
                   DISPLAY 'BP17K200 SCHACC NOT FOUND = '                       
      -                                           K000-NUM-SCH-ACC              
                   ADD  1                         TO WS-K200-NOTFOUND           
                                                                                
              WHEN OTHER                                                        
                   DISPLAY 'ERROR READING BP17K200 = '                          
      -                                           WS-BP17K200-STATUS            
                   ADD  1                         TO WS-K200-NOTFOUND           
           END-EVALUATE.                                                        
                                                                                
       405-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-UPDATE-BP13K800.                                                     
      *-------------------------------------------------------------            
           MOVE F520-NUM-REGN TO K800-NUM-REGN.                                 
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-BP13K800-STATUS                                          
              WHEN 00                                                           
                   ADD 1                          TO WS-K800-READ               
                   PERFORM 505-MOVE-BP17K200    THRU 505-EXIT                   
                   PERFORM 510-REWRITE-BP13K800 THRU 510-EXIT                   
                                                                                
              WHEN 23                                                           
                   DISPLAY 'BP13K800 REGN NOT FOUND = ' F520-NUM-REGN           
                   ADD 1                          TO WS-K800-NOTFOUND           
                                                                                
              WHEN OTHER                                                        
                   DISPLAY 'ERROR READ BP13K800 = ' WS-BP13K800-STATUS          
                   MOVE WS-BP13K800-STATUS        TO RETURN-CODE                
                   GO TO 999-CLOSE-FILES                                        
           END-EVALUATE.                                                        
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       505-MOVE-BP17K200.                                                       
      *-------------------------------------------------------------            
           MOVE K200-NUM-HH-CITIZENSHIP                                         
                                      TO K800-CDE-CITIZENSHIP-HOUSEHOLD.        
           MOVE K200-NUM-SPRQUOTA-TAG TO K800-NUM-SPRQUOTA-TAG.                 
           MOVE K200-NUM-SCSPR-OPT    TO K800-NUM-SCSPR-OPT.                    
           MOVE K200-NUM-LBS          TO K800-NUM-LEASE-BUY-BACK.               
      *    MOVE K200-NUM-BAL-LEASE    TO K800-NUM-LEASE-BALANCE.                
                                                                                
           IF K200-NUM-LEASE-TENURE = SPACES OR LOW-VALUES OR ZEROES            
              IF K200-NUM-LBS = 'Y'                                             
                 MOVE K200-NUM-BAL-LEASE    TO K800-NUM-LEASE-TENURE            
              END-IF                                                            
           ELSE                                                                 
              MOVE K200-NUM-LEASE-TENURE TO K800-NUM-LEASE-TENURE               
           END-IF                                                               
                                                                                
           MOVE K200-NUM-BUY-SELL-TAG TO K800-NUM-BUY-SELL-TAG.                 
           MOVE K200-NUM-SERS-RSL-STATUS TO K800-NUM-SERS-RS.                   
           MOVE K200-NUM-SALE-BROCHURE-SCPY                                     
                                      TO K800-NUM-SALE-BROCHURE-SCPY.           
           IF WS-K270-FOUND = 'Y'                                               
              MOVE K270-NUM-HLA       TO K800-NUM-HLA                           
           END-IF.                                                              
                                                                                
           MOVE K200-NUM-ELDERLY-EXEMPTION TO K800-NUM-ELDERLY-EXEMPTION        
           MOVE K200-NUM-50YR-ELIG    TO K800-NUM-ELG-50YR.                     
                                                                                
       505-EXIT.                                                                
           EXIT.                                                                
                                                                                
       510-REWRITE-BP13K800.                                                    
      *-------------------------------------------------------------            
           REWRITE BP13K800-MASTER.                                             
                                                                                
           IF WS-BP13K800-STATUS = 00                                           
              ADD 1                    TO WS-K800-REWRITE                       
           ELSE                                                                 
              DISPLAY 'ERROR REWRITING BP13K800 = ' WS-BP13K800-STATUS          
              MOVE WS-BP13K800-STATUS  TO RETURN-CODE                           
              PERFORM 999-CLOSE-FILES  THRU 999-EXIT                            
           END-IF.                                                              
                                                                                
       510-EXIT.                                                                
           EXIT.                                                                
                                                                                
       999-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
           DISPLAY '* ------------ BP13C71R ------------- * '.                  
           DISPLAY '  NO OF REC READ    BP13F520 = ' WS-F520-READ.              
           DISPLAY '  NO OF REC FOUND   BP17K000 = ' WS-K000-FOUND.             
           DISPLAY '  NO OF REC NOT FND BP17K000 = ' WS-K000-NOTFOUND.          
           DISPLAY '  NO OF REC FOUND   BP17K200 = ' WS-K200-FOUND.             
           DISPLAY '  NO OF REC NOT FND BP17K200 = ' WS-K200-NOTFOUND.          
           DISPLAY '  NO OF REC READ    BP13K800 = ' WS-K800-READ.              
           DISPLAY '  NO OF REC REWRITE BP13K800 = ' WS-K800-REWRITE.           
           DISPLAY '  NO OF REC NOT FND BP13K800 = ' WS-K800-NOTFOUND.          
                                                                                
           CLOSE BP13F520                                                       
                 BP17K000                                                       
                 BP17K200                                                       
                 BP17K270                                                       
                 BP13K800.                                                      
                                                                                
           IF WS-BP13K800-STATUS NOT = 00 AND 97                                
              DISPLAY 'ERROR CLOSING BP13K800 ' WS-BP13K800-STATUS      US      
              MOVE WS-BP13K800-STATUS    TO RETURN-CODE.                        
                                                                                
           IF WS-BP17K000-STATUS NOT = 00 AND 97                                
              DISPLAY 'ERROR CLOSING BP17K000 ' WS-BP17K000-STATUS      US      
              MOVE WS-BP17K000-STATUS    TO RETURN-CODE.                        
                                                                                
           IF WS-BP17K200-STATUS NOT = 00 AND 97                                
              DISPLAY 'ERROR CLOSING BP17K200 ' WS-BP17K200-STATUS      US      
              MOVE WS-BP17K200-STATUS    TO RETURN-CODE.                        
                                                                                
           IF WS-BP17K270-STATUS NOT = 00 AND 97                                
              DISPLAY 'ERROR CLOSING BP17K270 ' WS-BP17K270-STATUS      US      
              MOVE WS-BP17K270-STATUS    TO RETURN-CODE.                        
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
