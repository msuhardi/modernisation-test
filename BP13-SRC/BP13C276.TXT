      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.   BP13C276.                                                  
       AUTHOR.       JULITO A. MACAPAGAL.                                       
       DATE-WRITTEN. 26/08/2002.                                                
      ******************************************************                    
      *                                                    *                    
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                *                    
      *                                                    *                    
      *  SYSTEM ID   : BP13                                *                    
      *                                                    *                    
      *  OBJECTIVE   :                                     *                    
      *  1. TO PRINT LISTING OF REJECTED FLAT UNDER WIS    *                    
      *                                                    *                    
      *  INPUT FILE(S):                                    *                    
      *  1. BP13F205 - SEQUENTIAL FILE, ALLOCATION DATES   *                    
      *  2. BP13K270 - VSAM FILE - BOOKING PROGRAM         *                    
      *  3. BM06K110 - VSAM FILE - FLAT INVENTORY          *                    
      *                                                    *                    
      *  OUTPUT FILE(S):                                   *                    
      *  1. BP13L276                                       *                    
      *====================================================*                    
      * CHNGE #    BY    DATE     DESCRIPTION              *                    
      * --------- ------ -------- -------------------------*                    
      * BP132180  JAM1   26082002 NEW PROGRAM              *                    
      * BP132541  SK     04052004 EXPAND ALLOCN-TAG TO     *                    
      *                           3 BYTES                  *                    
      * BP132688  GAN1   18042005 CREATE OUTPUT FILE K248  *                    
      *                           FOR REPORT DATA          *                    
      * BP132688  LMS1   07072005 DO NOT CHECK FLAT TYPE   *                    
      * BP137436  FNP1   29102018 CATER FOR PPJ ALLO-TAG   *                    
      ******************************************************                    
       ENVIRONMENT DIVISION.                                                    
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205 ASSIGN TO BP13F205.                                  
                                                                                
           SELECT BP13K270 ASSIGN TO BP13K270                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE IS DYNAMIC                                        
                  RECORD KEY IS K270-KEY-FLD                                    
                  FILE STATUS IS WS-K270-STATUS.                                
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE IS DYNAMIC                                        
                  RECORD KEY IS K110-KEY-FLD                                    
                  ALTERNATE KEY IS K110-AIX1                                    
                  FILE STATUS IS WS-K110-STATUS.                                
                                                                                
           SELECT BP13K248 ASSIGN TO BP13K248                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE IS RANDOM                                         
                  RECORD KEY IS K248-KEY-FLD                                    
                  FILE STATUS IS WS-K248-STATUS.                                
                                                                                
           SELECT BP13L276 ASSIGN TO BP13L276.                                  
                                                                                
      *-------------*                                                           
       DATA DIVISION.                                                           
      *-------------*                                                           
       FILE SECTION.                                                            
                                                                                
       FD BP13K270                                                              
           RECORD CONTAINS 250 CHARACTERS.                                      
       COPY BP13K270.                                                   00320000
                                                                                
       FD BM06K110                                                              
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                   00320000
                                                                                
       FD BP13F205                                                              
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 80 CHARACTERS.                                       
       COPY BP13F205.                                                           
                                                                                
       FD BP13K248                                                              
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13K248.                                                   00320000
                                                                                
       FD  BP13L276                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING MODE F                                                     
           LABEL RECORDS OMITTED.                                               
       01  BP13L276-REC     PIC X(132).                                         
                                                                                
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-STATUS.                                                           
           05 WS-K270-STATUS    PIC  9(2) VALUE 0.                              
           05 WS-K110-STATUS    PIC  9(2) VALUE 0.                              
           05 WS-K248-STATUS    PIC  9(2) VALUE 0.                              
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F205-CNT-READ     PIC 9(08) VALUE ZEROES.                      
           05 WS-K270-CNT-READ     PIC 9(08) VALUE ZEROES.                      
           05 WS-K110-CNT-READ     PIC 9(08) VALUE ZEROES.                      
           05 WS-K248-CNT-WRITE    PIC 9(08) VALUE ZEROES.                      
           05 WS-MAX-LINES         PIC 9(05) VALUE 58.                          
           05 WS-LINE-CTR          PIC 9(05) VALUE 99.                          
           05 WS-SER-CTR           PIC 9(03) VALUE ZEROES.                      
           05 WS-PAGE-CTR          PIC 9(04) VALUE ZEROES.                      
           05 WS-TOT-CTR           PIC 9(04) VALUE ZEROES.                      
           05 WS-TOT-UNITS         PIC 9(04) VALUE ZEROES.                      
           05 WS-LINES-REMAIN      PIC S9(05) VALUE ZEROES.                     
           05 WS-PIDB-ERR-CTR      PIC 9(08) VALUE ZEROES.                      
           05 WS-K110-BYPASS-CTR   PIC 9(08) VALUE ZEROES.                      
                                                                                
       01  WS-FILE-FLAGS.                                                       
           05 WS-F205-FLAG      PIC  X(1) VALUE 'N'.                            
              88 WS-F205-EOF              VALUE 'Y'.                            
           05 WS-K270-FLAG      PIC  X(1) VALUE 'N'.                            
              88 WS-K270-EOF              VALUE 'Y'.                            
           05 WS-K110-FLAG      PIC  X(1) VALUE 'N'.                            
              88 WS-K110-EOF              VALUE 'Y'.                            
                                                                                
       01  WS-SYSTEM-DATETIME.                                                  
           05  WS-SYS-DATE             PIC 9(08) VALUE ZEROES.                  
           05  WS-SYS-TIME             PIC 9(08) VALUE ZEROES.                  
           05  FILLER                  PIC X(05) VALUE SPACES.                  
                                                                                
       01  WS-DISP-DATE.                                                        
           05  WS-DISP-DD              PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE '/'.                     
           05  WS-DISP-MM              PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE '/'.                     
           05  WS-DISP-CCYY            PIC X(04) VALUE SPACES.                  
                                                                                
       01  WS-OTHER-VARIABLES.                                                  
           05  WS-K270-ESTATE          PIC X(03) VALUE SPACES.                  
           05  WS-K270-ESTCDE REDEFINES WS-K270-ESTATE PIC 9(03).               
           05  WS-FLAT-TYPE            PIC X(02).                               
                                                                                
       01  WS-LINER                 PIC X(132) VALUE ALL '~'.                   
                                                                                
       01  WS-L276-LINER1.                                                      
           10  FILLER               PIC X(132) VALUE ALL "-".                   
                                                                                
       01  WS-L276-TITLE1.                                                      
           05  FILLER              PIC X(08)     VALUE 'BP13L276'.              
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'HDB3'.                  
           05  FILLER              PIC X(36)     VALUE SPACES.                  
           05  FILLER              PIC X(26)     VALUE                          
               'SOC - SYSTEM OF COMMITMENT'.                                    
           05  FILLER              PIC X(28)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  WS-L276-PAGENO      PIC ZZZZ.                                    
           05  FILLER              PIC X(13)     VALUE SPACES.                  
                                                                                
                                                                                
       01  WS-L276-TITLE2.                                                      
           05  FILLER               PIC X(50) VALUE SPACES.                     
           05  FILLER               PIC X(32) VALUE                             
               'LIST OF REJECTED FLATS UNDER WIS'.                              
           05  FILLER               PIC X(26) VALUE SPACES.                     
           05  FILLER               PIC X(07)     VALUE 'DATE : '.              
           05  WS-L276-CUR-DATE.                                                
              10  WS-L276-CUR-DD    PIC X(02).                                  
              10  FILLER            PIC X(01) VALUE '/'.                        
              10  WS-L276-CUR-MM    PIC X(02).                                  
              10  FILLER            PIC X(01) VALUE '/'.                        
              10  WS-L276-CUR-CC    PIC X(02).                                  
              10  WS-L276-CUR-YY    PIC X(02).                                  
           05  FILLER               PIC X(07) VALUE SPACES.                     
                                                                                
       01  WS-L276-TITLE3.                                                      
           05  FILLER               PIC X(11) VALUE                             
               'ESTATE   : '.                                                   
           05  L276-ESTCDE-FT       PIC X(06).                                  
           05  FILLER               PIC X(08) VALUE SPACES.                     
           05  FILLER               PIC X(17) VALUE                             
               'ALLOCATION DATE: '.                                             
           05  L276-ALLOCN-DATE     PIC X(10).                                  
           05  FILLER               PIC X(80) VALUE SPACES.                     
                                                                                
       01  WS-L276-HEADER1.                                                     
           10  FILLER               PIC X(57) VALUE                             
           'SNO.   SCHM-ACC     BLOCK  STREET NAME                   '.         
           10  FILLER               PIC X(24) VALUE                             
           ' LEVEL/UNIT   ALLOC TAG '.                                          
           10  FILLER               PIC X(41) VALUE SPACES.                     
                                                                                
       01  WS-L276-HEADER2.                                                     
           10  FILLER               PIC X(57) VALUE                             
           '-----  -----------  -----  ------------------------------'.         
           10  FILLER               PIC X(24) VALUE                             
           ' -----------  --------- '.                                          
           10  FILLER               PIC X(51) VALUE SPACES.                     
                                                                                
       01  WS-L276-UNT-DETL.                                                    
           10  L276-SNO             PIC ZZ9.                                    
           10  FILLER               PIC X(04) VALUE SPACES.                     
           10  L276-SCHM-ACC        PIC X(11).                                  
           10  FILLER               PIC X(02) VALUE SPACES.                     
           10  L276-BLOCK           PIC X(05).                                  
           10  FILLER               PIC X(02) VALUE SPACES.                     
           10  L276-NME-STREET      PIC X(32).                                  
           10  FILLER               PIC X(01) VALUE SPACES.                     
           10  L276-LEVEL-UNIT      PIC X(11).                                  
           10  FILLER               PIC X(06) VALUE SPACES.                     
           10  L276-ALLOCN-TAG      PIC X(03).                                  
           10  FILLER               PIC X(54) VALUE SPACES.                     
                                                                                
       01  LINK-REC.                                                            
           05  LINK-NUM-SCH-ACC.                                                
               10  LINK-NUM-SCH             PIC X(4).                           
               10  LINK-NUM-ACC             PIC X(5).                           
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *------------------*                                                      
       PROCEDURE DIVISION.                                                      
      *------------------*                                                      
                                                                                
       0000-MAIN-CONTROL.                                                       
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-F205-READ     THRU 2000-EXIT.                           
           PERFORM 3000-PROCESS-PARA  THRU 3000-EXIT                            
                   UNTIL WS-F205-EOF.                                           
           PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                             
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------*                                                         
       1000-OPEN-FILES.                                                         
      *---------------*                                                         
                                                                                
           OPEN INPUT  BP13F205                                                 
                       BP13K270                                                 
                       BM06K110                                                 
                I-O    BP13K248                                                 
                OUTPUT BP13L276.                                                
                                                                                
           IF WS-K270-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN BP13K270 FAIL STATUS = ' WS-K270-STATUS             
              MOVE WS-K270-STATUS TO RETURN-CODE                                
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN BM06K110 FAIL STATUS = ' WS-K110-STATUS             
              MOVE WS-K110-STATUS TO RETURN-CODE                                
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF WS-K248-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN BP13K248 FAIL STATUS = ' WS-K248-STATUS             
              MOVE WS-K248-STATUS TO RETURN-CODE                                
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATETIME.                  
           MOVE WS-SYS-DATE(1:4)        TO WS-DISP-CCYY.                        
           MOVE WS-SYS-DATE(5:2)        TO WS-DISP-MM.                          
           MOVE WS-SYS-DATE(7:2)        TO WS-DISP-DD.                          
           MOVE WS-DISP-DATE            TO WS-L276-CUR-DATE.                    
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------*                                                          
       2000-F205-READ.                                                          
      *--------------*                                                          
            READ BP13F205                                                       
                 AT END MOVE 'Y' TO WS-F205-FLAG                                
                 GO TO 2000-EXIT.                                               
            ADD 1 TO WS-F205-CNT-READ.                                          
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------*                                                          
       2100-K270-READ.                                                          
      *--------------*                                                          
                                                                                
            READ BP13K270 NEXT RECORD                                           
                 AT END MOVE 'Y' TO WS-K270-FLAG                                
                        MOVE HIGH-VALUES TO K270-KEY-FLD                        
                 GO TO 2100-EXIT.                                               
                                                                                
            IF F205-DTE-ALLOCN = K270-ALLOCN-DATE                               
               ADD 1 TO WS-K270-CNT-READ                                        
            END-IF.                                                             
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------*                                                          
       2200-K110-READ.                                                          
      *--------------*                                                          
                                                                                
           READ BM06K110 NEXT RECORD                                            
                 AT END MOVE 'Y' TO WS-K110-FLAG                                
                        MOVE HIGH-VALUES TO K110-AIX1.                          
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       2500-STARTBR-K270.                                                       
      *-----------------*                                                       
                                                                                
           MOVE LOW-VALUES       TO K270-GEN-FLD.                               
           MOVE F205-DTE-ALLOCN  TO K270-ALLOCN-DATE.                           
           START BP13K270 KEY >= K270-GEN-FLD.                                  
           EVALUATE WS-K270-STATUS                                              
           WHEN 00                                                              
                PERFORM 2100-K270-READ    THRU 2100-EXIT                        
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'BP13K270 - ERROR START BROWSE, KEY: '                  
                        F205-DTE-ALLOCN ', STATUS: ' WS-K270-STATUS             
                MOVE HIGH-VALUES TO K270-ALLOCN-DATE                            
           END-EVALUATE.                                                        
                                                                                
       2500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------*                                                       
       3000-PROCESS-PARA.                                                       
      *-----------------*                                                       
                                                                                
           PERFORM 2500-STARTBR-K270 THRU 2500-EXIT.                            
           PERFORM 3100-PROC-K270    THRU 3100-EXIT                             
               UNTIL F205-DTE-ALLOCN NOT = K270-ALLOCN-DATE.                    
           PERFORM 2000-F205-READ     THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------*                                                         
       3100-PROC-K270.                                                          
      *---------------*                                                         
                                                                                
           MOVE K270-ESTATE        TO  K110-ESTATE.                             
           MOVE K270-NEIGHBOURHOOD TO  K110-NEIGHBOURHOOD.                      
           MOVE K270-CONTRACT-NO   TO  K110-CONTRACT-NO.                        
           MOVE K270-NUM-BLK       TO  K110-BLK-NO.                             
           MOVE K270-AIX1-KEY      TO  K110-AIX1.                               
           START BM06K110 KEY >= K110-AIX1.                                     
           EVALUATE WS-K110-STATUS                                              
           WHEN 00                                                              
                PERFORM 2200-K110-READ THRU 2200-EXIT                           
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'BP13K270 - ERROR START BROWSE, KEY: '                  
                        F205-DTE-ALLOCN ', STATUS: ' WS-K270-STATUS             
                MOVE HIGH-VALUES TO K110-AIX1                                   
           END-EVALUATE.                                                        
                                                                                
           PERFORM 3120-PROCESS-NEXT-K110 THRU 3120-EXIT                        
                   UNTIL K110-AIX1 NOT = K270-AIX1-KEY.                         
                                                                                
           PERFORM 2100-K270-READ    THRU 2100-EXIT.                            
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3110-GET-PIDB-INFO.                                                      
           INITIALIZE BP13COMM8-REC.                                            
           MOVE K110-SCH-ACC-NO(1:9) TO  LINK-NUM-SCH-ACC.                      
           CALL 'BP13C913' USING LINK-REC, BP13COMM8-REC.                       
           IF COMM8-CDE-SYSERR = ZEROES                                         
              PERFORM 3111-DECODE-FTYPE THRU 3111-EXIT                          
           ELSE                                                                 
              ADD 1 TO WS-PIDB-ERR-CTR                                          
           END-IF.                                                              
                                                                                
       3110-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3111-DECODE-FTYPE.                                                       
                                                                                
           MOVE SPACES TO WS-FLAT-TYPE.                                         
           EVALUATE COMM8-CDE-TYP-ACTLUSE                                       
           WHEN '03'                                                            
               MOVE '3 ' TO WS-FLAT-TYPE                                        
                                                                                
           WHEN '04'                                                            
               MOVE '4 ' TO WS-FLAT-TYPE                                        
                                                                                
           WHEN '05'                                                            
               MOVE '5 ' TO WS-FLAT-TYPE                                        
                                                                                
           WHEN '06'                                                            
               MOVE 'E ' TO WS-FLAT-TYPE                                        
                                                                                
           END-EVALUATE.                                                        
                                                                                
       3111-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       3120-PROCESS-NEXT-K110.                                                  
                                                                                
           IF (K110-ALLOCN-TAG = 'REJ' OR 'PPJ') AND                            
              K110-DTE-ALLOCN = F205-DTE-ALLOCN                                 
              PERFORM 3110-GET-PIDB-INFO THRU 3110-EXIT                         
              ADD  1  TO WS-K110-CNT-READ                                       
              PERFORM 3125-PROCESS-REC-K110 THRU 3125-EXIT                      
           ELSE                                                                 
              ADD 1 TO WS-K110-BYPASS-CTR                                       
           END-IF.                                                              
                                                                                
           PERFORM 2200-K110-READ THRU 2200-EXIT.                               
                                                                                
       3120-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3125-PROCESS-REC-K110.                                                   
                                                                                
           IF K270-ESTATE NOT = WS-K270-ESTATE                                  
              MOVE K270-ESTATE        TO WS-K270-ESTATE                         
              MOVE K270-CDE-NT        TO L276-ESTCDE-FT                         
              STRING F205-DTE-ALLOCN(7:2) '/'                                   
                     F205-DTE-ALLOCN(5:2) '/'                                   
                     F205-DTE-ALLOCN(1:4) DELIMITED BY SIZE                     
                 INTO L276-ALLOCN-DATE                                          
              PERFORM 3200-PRT-HEADINGS THRU 3200-EXIT                          
              MOVE ZEROES  TO WS-SER-CTR                                        
              ADD  1       TO WS-TOT-CTR                                        
           END-IF.                                                              
                                                                                
           IF COMM8-CDE-SYSERR = ZEROES                                         
              MOVE COMM8-NME-STREET     TO  L276-NME-STREET                     
              MOVE SPACES TO L276-LEVEL-UNIT                                    
              IF COMM8-NUM-UNIT-SUB NOT = SPACES AND LOW-VALUES                 
                 STRING '#' COMM8-NUM-LEVEL                                     
                        '-' COMM8-NUM-UNIT-MAIN                                 
                        '-' COMM8-NUM-UNIT-SUB DELIMITED BY SIZE                
                      INTO  L276-LEVEL-UNIT                                     
              ELSE                                                              
                 STRING '#' COMM8-NUM-LEVEL                                     
                        '-' COMM8-NUM-UNIT-MAIN                                 
                        ' ' COMM8-NUM-UNIT-SUB DELIMITED BY SIZE                
                      INTO  L276-LEVEL-UNIT                                     
              END-IF                                                            
           ELSE                                                                 
              MOVE SPACES               TO  L276-LEVEL-UNIT                     
                                            L276-NME-STREET                     
           END-IF.                                                              
           MOVE K110-SCH-ACC-NO   TO L276-SCHM-ACC.                             
           MOVE K110-BLK-NO       TO L276-BLOCK.                                
           MOVE K110-ALLOCN-TAG   TO L276-ALLOCN-TAG.                           
           PERFORM 3300-PRT-DETLS     THRU 3300-EXIT.                           
                                                                                
       3125-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------*                                                      
       3200-PRT-HEADINGS.                                                       
      *------------------*                                                      
                                                                                
           ADD 1 TO WS-PAGE-CTR.                                                
           MOVE WS-PAGE-CTR TO WS-L276-PAGENO.                                  
           WRITE BP13L276-REC FROM WS-L276-TITLE1 AFTER PAGE.                   
           WRITE BP13L276-REC FROM WS-L276-TITLE2 AFTER 1.                      
           WRITE BP13L276-REC FROM WS-L276-TITLE3 AFTER 2.                      
           WRITE BP13L276-REC FROM WS-L276-HEADER1 AFTER 2.                     
           WRITE BP13L276-REC FROM WS-L276-HEADER2 AFTER 1.                     
           MOVE SPACES TO BP13L276-REC.                                         
           WRITE BP13L276-REC AFTER 1.                                          
           MOVE 10 TO WS-LINE-CTR.                                              
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------*                                                      
       3300-PRT-DETLS.                                                          
      *------------------*                                                      
                                                                                
           IF WS-LINE-CTR > WS-MAX-LINES                                        
              PERFORM 3200-PRT-HEADINGS THRU 3200-EXIT                          
           END-IF.                                                              
           ADD 1 TO WS-SER-CTR, WS-TOT-UNITS.                                   
           MOVE WS-SER-CTR         TO L276-SNO.                                 
           WRITE BP13L276-REC FROM WS-L276-UNT-DETL AFTER 1                     
           ADD 1 TO WS-LINE-CTR.                                                
                                                                                
           PERFORM 3350-WRITE-K248      THRU 3350-EXIT.                         
                                                                                
       3300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------*                                                      
       3350-WRITE-K248.                                                         
      *------------------*                                                      
                                                                                
           MOVE       SPACES TO BP13K248-REC.                                   
           INITIALIZE BP13K248-REC.                                             
                                                                                
           MOVE   K110-DTE-ALLOCN         TO  K248-DTE-ALLOC.                   
           MOVE   K110-SCH-ACC-NO         TO  K248-NUM-SCH-ACC.                 
           MOVE   K110-BLK-NO             TO  K248-NUM-BLK.                     
           IF COMM8-CDE-SYSERR = ZEROES                                         
              MOVE   COMM8-NME-STREET     TO  K248-NME-STREET                   
              MOVE   COMM8-NUM-LEVEL      TO  K248-NUM-LEVEL                    
              MOVE   COMM8-NUM-UNIT-MAIN  TO  K248-NUM-MAIN                     
              MOVE   COMM8-NUM-UNIT-SUB   TO  K248-NUM-SUB                      
           ELSE                                                                 
              MOVE   SPACES               TO  K248-NME-STREET                   
                                              K248-NUM-LEVEL                    
                                              K248-NUM-MAIN                     
                                              K248-NUM-SUB                      
           END-IF.                                                              
           MOVE   K110-ALLOCN-TAG         TO  K248-NUM-ALLOC-TAG.               
           MOVE   'BP13C276'              TO  K248-NUM-USERID.                  
           MOVE   WS-SYS-DATE             TO  K248-DTE-UPDATE.                  
                                                                                
           WRITE  BP13K248-REC.                                                 
           ADD 1  TO WS-K248-CNT-WRITE.                                         
                                                                                
       3350-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------*                                                        
       9000-CLOSE-FILES.                                                        
      *----------------*                                                        
                                                                                
           MOVE SPACES TO BP13L276-REC.                                         
           IF WS-LINE-CTR > WS-MAX-LINES                                        
              PERFORM 3200-PRT-HEADINGS THRU 3200-EXIT                          
           ELSE                                                                 
              MOVE ALL '-' TO BP13L276-REC                                      
              WRITE BP13L276-REC                                                
           END-IF.                                                              
                                                                                
           MOVE '>>>>>>>>>> RUN STATISTICS <<<<<<<<<<<<'                        
                                          TO BP13L276-REC.                      
           WRITE BP13L276-REC AFTER PAGE.                                       
           DISPLAY BP13L276-REC.                                                
           MOVE 'PROGRAM ID   : BP13C276' TO BP13L276-REC.                      
           WRITE BP13L276-REC AFTER 1.                                          
           DISPLAY BP13L276-REC.                                                
           MOVE SPACES TO BP13L276-REC.                                         
           STRING 'RUN DATE     : ' WS-DISP-DATE DELIMITED BY SIZE              
                INTO BP13L276-REC.                                              
           WRITE BP13L276-REC AFTER 1.                                          
           DISPLAY BP13L276-REC.                                                
           MOVE SPACES TO BP13L276-REC.                                         
           WRITE BP13L276-REC AFTER 1.                                          
           DISPLAY BP13L276-REC.                                                
           MOVE SPACES TO BP13L276-REC.                                         
           STRING 'NO OF RECS READ FROM BP13F205  :' WS-F205-CNT-READ           
                DELIMITED BY SIZE INTO BP13L276-REC.                            
           WRITE BP13L276-REC AFTER 1.                                          
           DISPLAY BP13L276-REC.                                                
           MOVE SPACES TO BP13L276-REC.                                         
           STRING 'NO OF RECS READ FROM BP13K270  :' WS-K270-CNT-READ           
                DELIMITED BY SIZE INTO BP13L276-REC.                            
           WRITE BP13L276-REC AFTER 1.                                          
           DISPLAY BP13L276-REC.                                                
           MOVE SPACES TO BP13L276-REC.                                         
           STRING 'NO OF RECS READ FROM BM06K110  :' WS-K110-CNT-READ           
                DELIMITED BY SIZE INTO BP13L276-REC.                            
           WRITE BP13L276-REC AFTER 1.                                          
           DISPLAY BP13L276-REC.                                                
           MOVE SPACES TO BP13L276-REC.                                         
           STRING 'NO OF RECS BYPASSED  BM06K110  :' WS-K110-BYPASS-CTR         
                DELIMITED BY SIZE INTO BP13L276-REC.                            
           WRITE BP13L276-REC AFTER 1.                                          
           DISPLAY BP13L276-REC.                                                
           MOVE SPACES TO BP13L276-REC.                                         
           STRING 'NO OF RECS NOT FOUND IN PIDB   :' WS-PIDB-ERR-CTR            
                DELIMITED BY SIZE INTO BP13L276-REC.                            
           WRITE BP13L276-REC AFTER 1.                                          
           DISPLAY BP13L276-REC.                                                
           MOVE SPACES TO BP13L276-REC.                                         
           STRING 'TOTAL NUMBER OF ESTATES        :' WS-TOT-CTR                 
                DELIMITED BY SIZE INTO BP13L276-REC.                            
           WRITE BP13L276-REC AFTER 1.                                          
           DISPLAY BP13L276-REC.                                                
           MOVE SPACES TO BP13L276-REC.                                         
           STRING 'TOTAL NUMBER OF UNITS          :' WS-TOT-UNITS               
                DELIMITED BY SIZE INTO BP13L276-REC.                            
           WRITE BP13L276-REC AFTER 1.                                          
           DISPLAY BP13L276-REC.                                                
           MOVE '>>>>>>>>>> EOJ BP13C276 <<<<<<<<<<<<<<'                        
                                          TO BP13L276-REC.                      
           WRITE BP13L276-REC AFTER 1.                                          
           DISPLAY BP13L276-REC.                                                
                                                                                
           CLOSE BP13F205, BP13K270, BM06K110, BP13K248, BP13L276.              
                                                                                
           IF WS-K110-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BM06K110 FAIL STATUS = ' WS-K110-STATUS            
           END-IF.                                                              
                                                                                
           IF WS-K270-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K270 FAIL STATUS = ' WS-K270-STATUS            
           END-IF.                                                              
                                                                                
           IF WS-K248-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K248 FAIL STATUS = ' WS-K248-STATUS            
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
