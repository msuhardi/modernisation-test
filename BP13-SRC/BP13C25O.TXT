      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C25O.                                                 
       AUTHOR.        ELAINE S ARGA.                                            
       DATE-WRITTEN.  06 JUNE 2018.                                             
                                                                                
      * ====================================================== *                
      *    ONE-TIME PROGRAM.                                   *                
      *--------------------------------------------------------*                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : TO FILTER THE APPOINTMENT DATE = 31   *                
      *                  DAYS FROM THE CURRENT DATE            *                
      *--------------------------------------------------------*                
      * CHG-NO   BY   DATE     DETAILS                         *                
      * -------  ---  ------   -----------------------------   *                
      * BP137047 ESA1 060618   NEW PROGRAM                     *                
      * ====================================================== *                
                                                                                
      *---------------------*                                                   
       ENVIRONMENT DIVISION.                                                    
      *---------------------*                                                   
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
      *---------------------*                                                   
       INPUT-OUTPUT SECTION.                                                    
      *---------------------*                                                   
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FQ10 ASSIGN TO BP13FQ10.                                  
                                                                                
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS K800-STATUS.                               
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K820-KEY-FLD                               
                  FILE STATUS     IS K820-STATUS.                               
                                                                                
           SELECT BP13K200 ASSIGN TO BP13K200                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K200-KEY-FLD                               
                  ALTERNATE KEY   IS K200-NUM-REGN                              
                  FILE STATUS     IS K200-STATUS.                               
                                                                                
           SELECT BP13K76D ASSIGN TO BP13K76D                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K76D-KEY-FLD                               
                  FILE STATUS     IS K76D-STATUS.                               
                                                                                
           SELECT BP13L25O ASSIGN TO BP13L25O.                                  
                                                                                
      *-------------*                                                           
       DATA DIVISION.                                                           
      *-------------*                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13FQ10                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 400 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13FQ10.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BP13K200                                                             
           RECORD CONTAINS 300 CHARACTERS.                                      
       COPY BP13K200.                                                           
                                                                                
       FD  BP13K76D                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K76D.                                                           
                                                                                
       FD  BP13L25O                                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01  BP13L25O-REC                     PIC X(200).                         
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
       01  WS-FILE-STATUS.                                                      
           05 K800-STATUS                 PIC 99    VALUE ZEROS.                
           05 K820-STATUS                 PIC 99    VALUE ZEROS.                
           05 K76D-STATUS                 PIC 99    VALUE ZEROS.                
           05 K200-STATUS                 PIC 99    VALUE ZEROS.                
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-FQ10-READ                PIC 9(5)  VALUE 0.                    
           05 WS-L25O-WRITE               PIC 9(5)  VALUE 0.                    
           05 WS-K76D-NFND-CNT            PIC 9(5)  VALUE 0.                    
           05 WS-K76D-FND-CNT             PIC 9(5)  VALUE 0.                    
           05 WS-K800-FND                 PIC 9(5)  VALUE 0.                    
           05 WS-K800-NFND                PIC 9(5)  VALUE 0.                    
           05 WS-K800-CANCEL              PIC 9(5)  VALUE 0.                    
           05 WS-K800-NCANCEL             PIC 9(5)  VALUE 0.                    
       01  WS-SNO-CTR                     PIC 9(04) VALUE ZEROES.               
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-K76D-FND                 PIC X     VALUE 'N'.                  
                                                                                
       01  WS-EOF-FQ10                    PIC 9     VALUE 0.                    
           88 EOF-FQ10                              VALUE 1.                    
                                                                                
       01  WS-CONTROL-DATE.                                                     
           05 WS-CTRL-CC                  PIC XX.                               
           05 WS-CTRL-YY                  PIC XX.                               
           05 WS-CTRL-MM                  PIC XX.                               
           05 WS-CTRL-DD                  PIC XX.                               
                                                                                
       01  WS-DATE.                                                             
           05 WS-CCYY.                                                          
              10 WS-CC                    PIC 99.                               
              10 WS-YY                    PIC 99.                               
           05 WS-CCYY1 REDEFINES WS-CCYY  PIC 9(4).                             
           05 WS-MM                       PIC 99.                               
           05 WS-DD                       PIC 99.                               
       01  WS-DATE9 REDEFINES WS-DATE     PIC 9(8).                             
       01  WS-INTEGER-CUR-DATE2           PIC S9(9) VALUE ZEROES.               
       01  WS-DATE2-N                     PIC  9(8) VALUE ZEROES.               
       01  WS-DATE2 REDEFINES WS-DATE2-N  PIC  X(8).                            
       01  WS-DTE-LETTER                  PIC 9(08) VALUE ZEROES.               
       01  WS-DTE-LETTER-2                PIC 9(08) VALUE ZEROES.               
       01  WS-NUM-DTE-LETTER              PIC 9(07) VALUE ZEROES.               
                                                                                
      *============================================================*            
      *            P R I N T    L A Y O U T                        *            
      *============================================================*            
       01  L25O-HEADER-1.                                                       
           05  FILLER                     PIC X(02) VALUE SPACES.               
           05  FILLER                     PIC X(48) VALUE                       
               'LIST OF CANCELLED APPLICATION WITH NOTIFICATION '.              
           05  FILLER                     PIC X(10) VALUE                       
               'LETTER    '.                                                    
           05  FILLER                     PIC X(20) VALUE SPACES.               
                                                                                
       01  L25O-HEADER-2.                                                       
           05  FILLER                     PIC X(06) VALUE SPACES.               
           05  FILLER                     PIC X(40) VALUE                       
               'S/NO   REGN NO.   NRIC1       HP NO.   '.                       
           05  FILLER                     PIC X(54) VALUE                       
               ' MGP QUEUE   PA QUEUE   MGP APPT DTE   PA APPT DTE'.            
           05  FILLER                     PIC X(32) VALUE SPACES.               
                                                                                
       01  L25O-DATA.                                                           
           05  FILLER                     PIC X(06) VALUE SPACES.               
           05  L25O-SN                    PIC ZZZ9  VALUE ZEROES.               
           05  FILLER                     PIC X(03) VALUE ' ) '.                
           05  L25O-REGN-NO               PIC X(08) VALUE SPACES.               
           05  FILLER                     PIC X(03) VALUE SPACES.               
           05  L25O-NRIC1                 PIC X(09) VALUE SPACES.               
           05  FILLER                     PIC X(03) VALUE SPACES.               
           05  L25O-HPNO                  PIC X(08) VALUE SPACES.               
           05  FILLER                     PIC X(03) VALUE SPACES.               
           05  L25O-MGPS-QUEUE            PIC X(05) VALUE SPACES.               
           05  FILLER                     PIC X(07) VALUE SPACES.               
           05  L25O-PA-QUEUE              PIC X(05) VALUE SPACES.               
           05  FILLER                     PIC X(06) VALUE SPACES.               
           05  L25O-MGPS-APPMT            PIC X(10) VALUE SPACES.               
           05  FILLER                     PIC X(05) VALUE SPACES.               
           05  L25O-PA-APPMT              PIC X(10) VALUE SPACES.               
           05  FILLER                     PIC X(28) VALUE SPACES.               
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *------------------*                                                      
       000-MAIN-ROUTINE.                                                        
      *------------------*                                                      
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 150-READ-SY02F001  THRU 150-EXIT.                            
           PERFORM 200-READ-BP13FQ10  THRU 200-EXIT.                            
           PERFORM 300-PROCESS-RTN    THRU 300-EXIT                             
             UNTIL EOF-FQ10.                                                    
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       100-INITIALIZATION.                                                      
      *------------------*                                                      
                                                                                
           OPEN INPUT  BP13FQ10                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BP13K200                                                 
                       BP13K76D                                                 
                       SY02F001                                                 
                OUTPUT BP13L25O.                                                
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K800 OPEN ERROR, STATUS ' K800-STATUS                
              MOVE K800-STATUS        TO RETURN-CODE                            
              PERFORM 999-CLOSE-FILES    THRU 999-EXIT                          
           END-IF.                                                              
                                                                                
           IF K820-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K820 OPEN ERROR, STATUS ' K820-STATUS                
              MOVE K820-STATUS        TO RETURN-CODE                            
              PERFORM 999-CLOSE-FILES    THRU 999-EXIT                          
           END-IF.                                                              
                                                                                
           IF K200-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K200 OPEN ERROR, STATUS ' K200-STATUS                
              MOVE K200-STATUS        TO RETURN-CODE                            
              PERFORM 999-CLOSE-FILES    THRU 999-EXIT                          
           END-IF.                                                              
                                                                                
           IF K76D-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K76D OPEN ERROR, STATUS ' K76D-STATUS                
              MOVE K76D-STATUS        TO RETURN-CODE                            
              PERFORM 999-CLOSE-FILES    THRU 999-EXIT                          
           END-IF.                                                              
                                                                                
           MOVE ZEROS  TO WS-COUNTERS.                                          
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       150-READ-SY02F001.                                                       
      *-------------------------------------------------------------            
           READ SY02F001                                                        
                AT END                                                          
                   GO TO 150-EXIT.                                              
                                                                                
           MOVE F001-DTE-CURRENT TO WS-DATE.                                    
                                                                                
           COMPUTE WS-INTEGER-CUR-DATE2 =                                       
              FUNCTION INTEGER-OF-DATE(WS-DATE9) + 31.                          
                                                                                
           COMPUTE WS-DATE2-N =                                                 
              FUNCTION DATE-OF-INTEGER(WS-INTEGER-CUR-DATE2).                   
                                                                                
       150-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       200-READ-BP13FQ10.                                                       
      *------------------*                                                      
           READ BP13FQ10 AT END                                                 
                MOVE 1   TO WS-EOF-FQ10                                         
                GO TO 200-EXIT.                                                 
                                                                                
           ADD  1  TO WS-FQ10-READ.                                             
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       300-PROCESS-RTN.                                                         
      *------------------*                                                      
           PERFORM 400-READ-BP13K800 THRU 400-EXIT.                             
           PERFORM 200-READ-BP13FQ10 THRU 200-EXIT.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       400-READ-BP13K800.                                                       
      *------------------*                                                      
           MOVE FQ10-NUM-REGN           TO K800-NUM-REGN.                       
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF K800-STATUS = 00                                                  
              ADD  1    TO WS-K800-FND                                          
              IF K800-NUM-STATUS = 'C'                                          
                 ADD  1    TO WS-K800-CANCEL                                    
                 PERFORM 600-PRINT-REPORT   THRU 600-EXIT                       
              ELSE                                                              
                 ADD  1    TO WS-K800-NCANCEL                                   
                 PERFORM 500-READ-BP13K76D  THRU 500-EXIT                       
                 IF WS-K76D-FND = 'N'                                           
                    CONTINUE                                                    
                 ELSE                                                           
                    PERFORM 600-PRINT-REPORT   THRU 600-EXIT                    
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              ADD  1    TO WS-K800-NFND                                         
              IF K800-STATUS = 23                                               
                 DISPLAY 'RECORD NOT FOUND IN BP13K800, ' K800-NUM-REGN         
                 GO TO 400-EXIT                                                 
              ELSE                                                              
                 MOVE K800-STATUS       TO RETURN-CODE                          
                 DISPLAY 'ERROR READING BP13K800, ' K800-NUM-REGN               
                 GO TO 999-CLOSE-FILES                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       500-READ-BP13K76D.                                                       
      *------------------*                                                      
           MOVE K800-NUM-REGN          TO K76D-NUM-REGN.                        
                                                                                
           READ BP13K76D.                                                       
                                                                                
           IF K76D-STATUS = 00                                                  
              MOVE 'Y'      TO WS-K76D-FND                                      
              ADD 1   TO WS-K76D-FND-CNT                                        
           ELSE                                                                 
             IF K76D-STATUS = 23                                                
                MOVE 'N'      TO WS-K76D-FND                                    
                ADD 1 TO WS-K76D-NFND-CNT                                       
             ELSE                                                               
                DISPLAY 'READ BP13K76D ERROR ' K76D-NUM-REGN                    
                DISPLAY 'STATUS ' K76D-STATUS                                   
                MOVE K76D-STATUS TO RETURN-CODE                                 
                PERFORM 999-CLOSE-FILES   THRU 999-EXIT                         
             END-IF                                                             
           END-IF.                                                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       600-PRINT-REPORT.                                                        
      *------------------*                                                      
                                                                                
           PERFORM 700-READ-BP13K200    THRU 700-EXIT.                          
                                                                                
           IF K800-NUM-MGPS = 'P' OR 'C'                                        
              IF K200-DTE-BKAPPMT-DATE-MGPS = WS-DATE2                          
                 CONTINUE                                                       
              ELSE                                                              
                 GO TO 600-EXIT                                                 
              END-IF                                                            
           ELSE                                                                 
              IF K200-DTE-BKAPPMT-DATE = WS-DATE2                               
                 CONTINUE                                                       
              ELSE                                                              
                 GO TO 600-EXIT                                                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
           ADD 1    TO WS-SNO-CTR.                                              
                                                                                
           IF WS-SNO-CTR = 1                                                    
              WRITE BP13L25O-REC FROM L25O-HEADER-1                             
              WRITE BP13L25O-REC FROM L25O-HEADER-2                             
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO L25O-DATA.                        
           MOVE WS-SNO-CTR                 TO L25O-SN.                          
           MOVE FQ10-NUM-REGN              TO L25O-REGN-NO.                     
           MOVE K800-NUM-NRIC1             TO L25O-NRIC1.                       
                                                                                
           PERFORM 800-READ-BP13K820     THRU 800-EXIT.                         
           MOVE K820-NUM-HP-PGR            TO L25O-HPNO.                        
           MOVE K800-NUM-QUEUE-MGPS        TO L25O-MGPS-QUEUE.                  
           MOVE K800-NUM-NT-FT-QUEUE       TO L25O-PA-QUEUE.                    
           MOVE K200-DTE-BKAPPMT-DATE-MGPS TO L25O-MGPS-APPMT.                  
           MOVE K200-DTE-BKAPPMT-DATE      TO L25O-PA-APPMT.                    
           WRITE BP13L25O-REC FROM L25O-DATA.                                   
           ADD 1  TO WS-L25O-WRITE.                                             
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       700-READ-BP13K200.                                                       
      *------------------*                                                      
           MOVE FQ10-NUM-REGN            TO K200-NUM-REGN.                      
                                                                                
           READ BP13K200 KEY IS K200-NUM-REGN.                                  
                                                                                
           EVALUATE K200-STATUS                                                 
               WHEN 00                                                          
                    CONTINUE                                                    
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES          TO BP13K200-REC                        
                    DISPLAY 'REGN NO. NOT FOUND IN BP13K200. REGN = '           
                            K200-NUM-REGN                                       
               WHEN OTHER                                                       
                    DISPLAY 'READ ERROR BP13K200, STATUS '                      
                            K200-STATUS ' ' K200-NUM-REGN                       
                    MOVE K200-STATUS  TO RETURN-CODE                            
                    PERFORM 999-CLOSE-FILES    THRU 999-EXIT                    
           END-EVALUATE.                                                        
                                                                                
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       800-READ-BP13K820.                                                       
      *------------------*                                                      
                                                                                
           MOVE SPACES          TO K820-KEY-FLD.                                
           MOVE K800-NUM-REGN   TO K820-NUM-REGN.                               
           MOVE K800-NUM-NRIC1  TO K820-NUM-NRIC.                               
                                                                                
           READ BP13K820.                                                       
                                                                                
           EVALUATE K820-STATUS                                                 
               WHEN '00'                                                        
                    CONTINUE                                                    
               WHEN '23'                                                        
                    MOVE SPACES    TO K820-NUM-HP-PGR                           
                    DISPLAY 'REC NOT FOUND IN BP13K820 ' K820-KEY-FLD           
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K820, STATUS '                   
                            K820-STATUS                                         
                    DISPLAY 'K820-KEY-FLD ' K820-KEY-FLD                        
                    PERFORM 999-CLOSE-FILES    THRU 999-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------*                                                          
       999-CLOSE-FILES.                                                         
      *--------------*                                                          
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13C25O ================*'.              
           DISPLAY '    NO. OF FQ10 RECORDS READ     = ' WS-FQ10-READ.          
           DISPLAY '    NO. OF L25O RECORDS WRITTEN  = ' WS-L25O-WRITE.         
           DISPLAY '    NO. OF RECORDS IN BP13K800   = '                        
                        WS-K800-FND                                             
           DISPLAY '    NO. OF REC NT FND BP13K800   = '                        
                        WS-K800-NFND                                            
           DISPLAY '    NO. OF RECORDS CANCEL(K800)  = '                        
                        WS-K800-CANCEL                                          
           DISPLAY '    NO. OF REC NT CANCEL (K800)  = '                        
                        WS-K800-NCANCEL                                         
           DISPLAY '    NO. OF REC NT FND BP13K800   = '                        
                        WS-K800-NFND                                            
           DISPLAY '    NO. OF RECORDS IN BLOCKLIST  = '                        
                        WS-K76D-FND-CNT                                         
           DISPLAY '    NO. OF REC NT FND BLOCKLIST  = '                        
                        WS-K76D-NFND-CNT                                        
           DISPLAY '*==========================================*'.              
                                                                                
           CLOSE BP13FQ10                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K200                                                       
                 BP13K76D                                                       
                 BP13L25O.                                                      
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR BP13K800, STATUS ' K800-STATUS               
              MOVE K800-STATUS        TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           IF K820-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR BP13K820, STATUS ' K820-STATUS               
              MOVE K820-STATUS        TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           IF K200-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR BP13K200, STATUS ' K200-STATUS               
              MOVE K200-STATUS        TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           IF K76D-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR BP13K76D, STATUS ' K76D-STATUS               
              MOVE K76D-STATUS        TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
        999-EXIT.                                                               
            EXIT.                                                               
