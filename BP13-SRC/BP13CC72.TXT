       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC72.                                                 
      *AUTHOR.        ED1.                                                      
      *DATE-WRITTEN.  APR 02, 2007.                                             
      *****************************************************************         
      * OBJECTIVE  - TO UPDATE DBSS MASTER FILE WITH NEW INSTAL. RATE *         
      *---------------------------------------------------------------*         
      * UPDATES DBSS MASTER FILE BASED ON UPDATE LOAN FILE BP13KD92   *         
      * BP13M713 - JCL                                                *         
      *---------------------------------------------------------------*         
      * REF NO   DATE    BY   AMENDMENTS/ENHANCEMENTS                 *         
      * -------  ----    ---  -----------------------                 *         
      * BP133033 070402  ED1  NEW PGM                                 *         
      * BP133052 070523  ED1  WRITE UPDATED CASES TO P13FD92A         *         
      * BP133171 071102  ED1  REMOVED UPD OF KD94-DTE&TME UPDATE      *         
      * BP133209 090408  ED1  CHECK KD00-AMT-CPF-RATE TO DET. CPF-STAT*         
      * BP133314 300608  JB8  TO CATER FOR REVISE REPAY PERIOD        *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FD92 ASSIGN       TO BP13FD92.                            
           SELECT P13FD92A ASSIGN       TO P13FD92A.                            
           SELECT P13LC72A ASSIGN       TO P13LC72A.                            
           SELECT P13LC72B ASSIGN       TO P13LC72B.                            
           SELECT SY02F001 ASSIGN       TO SY02F001.                            
                                                                                
           SELECT BP13KD92 ASSIGN       TO BP13KD92                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS KD92-KEY-FLD                         
                           FILE STATUS  IS KD92-STATUS.                         
                                                                                
           SELECT BP13KD94 ASSIGN       TO BP13KD94                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS KD94-KEY-FLD                         
                           FILE STATUS  IS KD94-STATUS.                         
                                                                                
           SELECT BP13KD00 ASSIGN       TO BP13KD00                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS KD00-KEY-FLD                         
                           FILE STATUS  IS KD00-STATUS.                         
                                                                                
           SELECT BP13KD99 ASSIGN       TO BP13KD99                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS KD99-KEY-FLD                         
                           FILE STATUS  IS KD99-STATUS.                         
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13FD92                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 1000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13FD92.                                                           
                                                                                
       FD   P13FD92A                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 1000 CHARACTERS                                     
            LABEL RECORD IS OMITTED                                             
            RECORDING MODE IS F.                                                
       01   P13FD92A-REC  PIC X(1000).                                          
                                                                                
       FD   BP13KD94                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13KD94.                                                           
                                                                                
       FD   BP13KD92                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13KD92.                                                           
                                                                                
       FD   BP13KD99                                                            
            RECORD CONTAINS 1650 CHARACTERS.                                    
       COPY BP13KD99.                                                           
                                                                                
       FD   BP13KD00                                                            
            RECORD CONTAINS 1600 CHARACTERS.                                    
       COPY BP13KD00.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   P13LC72A                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORD IS OMITTED                                             
            RECORDING MODE IS F.                                                
       01   LC72A-PRTREC PIC X(132).                                            
                                                                                
       FD   P13LC72B                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORD IS OMITTED                                             
            RECORDING MODE IS F.                                                
       01   LC72B-PRTREC PIC X(132).                                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FD38-STATUS                   PIC 9(02) VALUE 0.                     
       01  KD00-STATUS                   PIC 9(02) VALUE 0.                     
       01  KD92-STATUS                   PIC 9(02) VALUE 0.                     
       01  KD94-STATUS                   PIC 9(02) VALUE 0.                     
       01  KD99-STATUS                   PIC 9(02) VALUE 0.                     
       01  WS-KD00-FOUND                 PIC X(01) VALUE 'N'.                   
       01  WS-KD92-EOF                   PIC X(01) VALUE 'N'.                   
       01  WS-DATE-HDR                   PIC X(10) VALUE SPACES.                
       01  WS-MAX-LINE                   PIC 99    VALUE 58.                    
       01  WS-KD92-AMT-INST              PIC 9(6)V99 VALUE 0.                   
       01  WS-KD92-CPFRATE               PIC 9(6)V99 VALUE 0.                   
       01  WS-KD92-INST-CASH             PIC 9(6)V99 VALUE 0.                   
       01  WS-KD00-AMT-INST              PIC 9(6)V99 VALUE 0.                   
       01  WS-EDITED-DATE                PIC X(10) VALUE SPACES.                
       01  WS-SYSTEM-DATE-TIME.                                                 
           05  WS-SYS-DATE               PIC X(08) VALUE SPACES.                
           05  WS-SYS-TIME               PIC X(08) VALUE SPACES.                
           05  FILLER                    PIC X(05) VALUE SPACES.                
                                                                                
       01  WS-SYSTEM-DATE                PIC X(08) VALUE SPACES.                
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-CNT-KD92-READ           PIC 9(07) VALUE 0.                     
           05 WS-CNT-KD00-UPD            PIC 9(07) VALUE 0.                     
           05 WS-CNT-KD92-DEL            PIC 9(07) VALUE 0.                     
           05 WS-CNT-KD92-ERR            PIC 9(07) VALUE 0.                     
           05 WS-SNO                     PIC 9(05) VALUE 0.                     
           05 WS-PAGE-CTR                PIC 9(04) VALUE 0.                     
           05 WS-LINE-CTR                PIC 9(02) VALUE 0.                     
           05 WS-SNOA                    PIC 9(05) VALUE 0.                     
           05 WS-PAGE-CTRA               PIC 9(04) VALUE 0.                     
           05 WS-LINE-CTRA               PIC 9(02) VALUE 0.                     
                                                                                
       01  WS-BLANK-LINE                 PIC X(132) VALUE SPACES.               
       01  WS-DASH-LINE                  PIC X(132) VALUE ALL '-'.              
                                                                                
      *---> DETAIL LINES FOR REPORT                                             
       01  LC72B-HEADER01.                                                      
           05  WS-DOCID            PIC X(8)      VALUE 'P13LC72B'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  LC72B-DATE-HDR      PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LC72B-PAGE-HDR      PIC ZZZ9      VALUE ZEROES.                  
                                                                                
       01  LC72A-HEADER02.                                                      
           05  FILLER              PIC X(44)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
               'REPORT ON CHANGE OF CPF INSTALMENT/CASH'.                       
                                                                                
       01  LC72B-HEADER02.                                                      
           05  FILLER              PIC X(40)     VALUE SPACES.                  
           05  FILLER              PIC X(47)     VALUE                          
               'ERROR REPORT ON CHANGE OF CPF INSTALMENT/CASH'.                 
                                                                                
       01  LC72B-HEADER03.                                                      
           05  FILLER              PIC X(121)    VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'TOTAL IN'.              
                                                                                
       01  LC72B-HEADER04.                                                      
           05  FILLER              PIC X(03)     VALUE 'S/N'.                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'SCH-ACC'.               
           05  FILLER              PIC X(09)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'REGN NO'.               
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'EFF-DATE'.              
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'CPF1 AMT'.              
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'CPF2 AMT'.              
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'CPF3 AMT'.              
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'CPF4 AMT'.              
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE 'CASH     '.             
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE 'TOTAL'.                 
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'PP MASTER '.            
                                                                                
       01 LC72B-DETAILS.                                                        
          05 LC72B-DTL-SERIAL     PIC ZZZZ9     VALUE ZEROES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 LC72B-SCH-ACC        PIC X(14)     VALUE SPACES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 LC72B-REGNO          PIC X(08)     VALUE SPACES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 LC72B-EFF-DTE        PIC X(10)     VALUE SPACES.                   
          05 FILLER               PIC X(01)     VALUE SPACES.                   
          05 LC72B-CPF1           PIC ZZZ,ZZ9.99.                               
          05 FILLER               PIC X(03)     VALUE SPACES.                   
          05 LC72B-CPF2           PIC ZZZ,ZZ9.99.                               
          05 FILLER               PIC X(03)     VALUE SPACES.                   
          05 LC72B-CPF3           PIC ZZZ,ZZ9.99.                               
          05 FILLER               PIC X(03)     VALUE SPACES.                   
          05 LC72B-CPF4           PIC ZZZ,ZZ9.99.                               
          05 FILLER               PIC X(03)     VALUE SPACES.                   
          05 LC72B-CASH           PIC ZZZ,ZZ9.99.                               
          05 FILLER               PIC X(03)     VALUE SPACES.                   
          05 LC72B-TOTAL          PIC ZZZ,ZZ9.99.                               
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 LC72B-AMT-INST       PIC ZZZ,ZZ9.99.                               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      **************                                                            
       0000-CONTROL.                                                            
      **************                                                            
           PERFORM 1000-START-UP         THRU 1000-EXIT.                        
           PERFORM 2000-READ-BP13FD92    THRU 2000-EXIT.                        
                                                                                
           PERFORM 3000-MAIN-ROUTINE     THRU 3000-EXIT                         
                UNTIL FD92-NUM-REGN = HIGH-VALUES.                              
                                                                                
           PERFORM 9000-CLOSING-ROUTINE  THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      **************                                                            
       1000-START-UP.                                                           
      **************                                                            
           OPEN INPUT  BP13FD92                                                 
                       SY02F001                                                 
                I-O    BP13KD00                                                 
                       BP13KD92                                                 
                       BP13KD94                                                 
                       BP13KD99                                                 
                OUTPUT P13FD92A                                                 
                       P13LC72A                                                 
                       P13LC72B.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE-TIME.                   
                                                                                
           STRING WS-SYS-DATE(7:2) '/'                                          
                  WS-SYS-DATE(5:2) '/'                                          
                  WS-SYS-DATE(1:4)                                              
           DELIMITED BY SIZE INTO WS-EDITED-DATE.                               
                                                                                
           IF  KD00-STATUS NOT = 0 AND 97                                       
               DISPLAY 'OPEN FAILED FOR BP13KD00 ' KD00-STATUS                  
               PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF  KD92-STATUS NOT = 0 AND 97                                       
               DISPLAY 'OPEN FAILED FOR BP13KD92 ' KD92-STATUS                  
               PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF  KD94-STATUS NOT = 0 AND 97                                       
               DISPLAY 'OPEN FAILED FOR BP13KD94 ' KD94-STATUS                  
               PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF  KD99-STATUS NOT = 0 AND 97                                       
               DISPLAY 'OPEN FAILED FOR BP13KD99 ' KD99-STATUS                  
               PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
                                                                                
           MOVE F001-DTE-CURRENT      TO WS-SYSTEM-DATE.                        
                                                                                
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ********************                                                      
       2000-READ-BP13FD92.                                                      
      ********************                                                      
           READ BP13FD92                                                        
                AT END MOVE HIGH-VALUES TO FD92-NUM-REGN                        
                       GO TO 2000-EXIT                                          
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-CNT-KD92-READ.                                           
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *******************                                                       
       3000-MAIN-ROUTINE.                                                       
      *******************                                                       
                                                                                
           IF FD92-DTE-REVSD-INSTAL-EFF(1:6) = WS-SYSTEM-DATE(1:6)              
              PERFORM 3100-READ-BP13KD00  THRU 3100-EXIT                        
              PERFORM 3150-INIT-FIELDS    THRU 3150-EXIT                        
                                                                                
              IF WS-KD00-FOUND = 'Y'                                            
                                                                                
                 MOVE ZEROES                 TO WS-KD92-AMT-INST                
                 COMPUTE WS-KD92-AMT-INST = FD92-CPF-INSTAL-TOT-NEW  +          
                                            FD92-CPF-INSTAL-CASH-NEW            
                                                                                
                 IF FD92-NUM-TRANS = 'RATE'                                     
                    IF WS-KD92-AMT-INST NOT =  KD00-AMT-INSTAL                  
                       PERFORM 4000-READ-BP13KD92    THRU 4000-EXIT             
                       PERFORM 4100-DELETE-BP13KD92  THRU 4100-EXIT             
                       PERFORM 5100-WRITE-P13LC72B   THRU 5100-EXIT             
                       PERFORM 2000-READ-BP13FD92    THRU 2000-EXIT             
                       GO TO 3000-EXIT                                          
                    END-IF                                                      
                 END-IF                                                         
                                                                                
                 PERFORM 3200-WRITE-BP13KD99       THRU 3200-EXIT               
                 PERFORM 3300-MOVE-TO-BP13KD00     THRU 3300-EXIT               
                 PERFORM 3400-REWRITE-BP13KD00     THRU 3400-EXIT               
                 PERFORM 4000-READ-BP13KD92        THRU 4000-EXIT               
                                                                                
                 MOVE     SPACES                   TO   P13FD92A-REC            
                 WRITE P13FD92A-REC                FROM BP13KD92-REC            
                                                                                
                 PERFORM 4100-DELETE-BP13KD92      THRU 4100-EXIT               
                 PERFORM 5000-WRITE-P13LC72A       THRU 5000-EXIT               
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13FD92        THRU 2000-EXIT.                    
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *********************                                                     
       3100-READ-BP13KD00.                                                      
      *********************                                                     
           INITIALIZE            KD00-KEY-FLD.                                  
           MOVE 'N'           TO WS-KD00-FOUND.                                 
           MOVE SPACES        TO KD00-KEY-FLD.                                  
           MOVE FD92-NUM-REGN TO KD00-KEY-FLD.                                  
           READ BP13KD00.                                                       
                                                                                
           EVALUATE KD00-STATUS                                                 
           WHEN 00                                                              
               MOVE 'Y' TO WS-KD00-FOUND                                        
                                                                                
           WHEN 23                                                              
               DISPLAY 'BP13KD00 - RECORD NOT FOUND, KEY: '                     
                         KD00-KEY-FLD                                           
           WHEN OTHER                                                           
               DISPLAY 'BP13KD00 - ERROR IN READ, STATUS: '                     
                        KD00-STATUS ' KEY ' KD00-KEY-FLD                        
               PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************                                                        
       3150-INIT-FIELDS.                                                        
      ******************                                                        
           IF FD92-CPF-INSTAL-CPF1-NEW NOT NUMERIC                              
              MOVE 0 TO FD92-CPF-INSTAL-CPF1-NEW                                
           END-IF.                                                              
           IF FD92-CPF-INSTAL-CPF2-NEW NOT NUMERIC                              
              MOVE 0 TO FD92-CPF-INSTAL-CPF2-NEW                                
           END-IF.                                                              
           IF FD92-CPF-INSTAL-CPF3-NEW NOT NUMERIC                              
              MOVE 0 TO FD92-CPF-INSTAL-CPF3-NEW                                
           END-IF.                                                              
           IF FD92-CPF-INSTAL-CPF4-NEW NOT NUMERIC                              
              MOVE 0 TO FD92-CPF-INSTAL-CPF4-NEW                                
           END-IF.                                                              
           IF FD92-CPF-INSTAL-CASH-NEW NOT NUMERIC                              
              MOVE 0 TO FD92-CPF-INSTAL-CASH-NEW                                
           END-IF.                                                              
           IF KD00-AMT-INSTAL          NOT NUMERIC                              
              MOVE 0 TO KD00-AMT-INSTAL                                         
           END-IF.                                                              
           IF KD92-CPF-INSTAL-TOT-NEW  NOT NUMERIC                              
              MOVE 0 TO KD92-CPF-INSTAL-TOT-NEW                                 
           END-IF.                                                              
           IF KD92-CPF-INSTAL-CASH-NEW NOT NUMERIC                              
              MOVE 0 TO KD92-CPF-INSTAL-CASH-NEW                                
           END-IF.                                                              
                                                                                
       3150-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *********************                                                     
       3200-WRITE-BP13KD99.                                                     
      *********************                                                     
           MOVE BP13KD00-REC               TO BP13KD99-REC.                     
           MOVE KD92-NUM-REGN              TO KD99-NUM-REGN-LOG.                
           MOVE WS-SYS-DATE                TO KD99-DTE-UPDATE-LOG.              
           MOVE WS-SYS-TIME                TO KD99-TME-UPDATE-LOG.              
           WRITE BP13KD99-REC.                                                  
                                                                                
           EVALUATE KD99-STATUS                                                 
           WHEN 00                                                              
           WHEN 22                                                              
                CONTINUE                                                        
           WHEN OTHER                                                           
               DISPLAY 'BP13KD99 - ERROR IN WRITE, STATUS: '                    
                        KD99-STATUS ' KEY ' KD99-KEY-FLD                        
           END-EVALUATE.                                                        
                                                                                
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ***********************                                                   
       3300-MOVE-TO-BP13KD00.                                                   
      ***********************                                                   
           MOVE FD92-CPF-INSTAL-TOT-NEW   TO KD00-AMT-CPF-RATE.                 
           MOVE FD92-CPF-INSTAL-CASH-NEW  TO KD00-AMT-INSTAL-CASH.              
           MOVE FD92-DTE-REVSD-INSTAL-EFF TO KD00-DTE-INSTAL-EFF.               
           MOVE FD92-CPF-INSTAL-CPF1-NEW  TO KD00-AMT-INSTAL-CPF1.              
           MOVE FD92-CPF-INSTAL-CPF2-NEW  TO KD00-AMT-INSTAL-CPF2.              
           MOVE FD92-CPF-INSTAL-CPF3-NEW  TO KD00-AMT-INSTAL-CPF3.              
           MOVE FD92-CPF-INSTAL-CPF4-NEW  TO KD00-AMT-INSTAL-CPF4.              
           MOVE WS-KD92-AMT-INST          TO KD00-AMT-INSTAL.                   
                                                                                
           IF KD00-AMT-CPF-RATE > ZEROS                                         
              MOVE '1' TO KD00-CDE-CPF-STATUS                                   
           ELSE                                                                 
              MOVE '0' TO KD00-CDE-CPF-STATUS                                   
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ***********************                                                   
       3400-REWRITE-BP13KD00.                                                   
      ***********************                                                   
           MOVE 'BP13CC72'           TO KD00-NUM-USERID.                        
           MOVE  WS-SYS-DATE         TO KD00-DTE-UPDATE.                        
           MOVE  WS-SYS-TIME         TO KD00-TME-UPDATE.                        
           REWRITE BP13KD00-REC.                                                
                                                                                
           EVALUATE KD00-STATUS                                                 
           WHEN 00                                                              
                ADD 1 TO WS-CNT-KD00-UPD                                        
                                                                                
           WHEN OTHER                                                           
               DISPLAY 'BP13KD00 - ERROR IN REWRITE, STATUS: '                  
                        KD00-STATUS ' KEY ' KD00-KEY-FLD                        
           END-EVALUATE.                                                        
                                                                                
                                                                                
       3400-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ********************                                                      
       4000-READ-BP13KD92.                                                      
      ********************                                                      
           INITIALIZE            KD92-KEY-FLD.                                  
           MOVE SPACES        TO KD92-KEY-FLD.                                  
           MOVE FD92-KEY-FLD  TO KD92-KEY-FLD.                                  
           READ BP13KD92.                                                       
                                                                                
           EVALUATE KD92-STATUS                                                 
           WHEN 00                                                              
              CONTINUE                                                          
                                                                                
           WHEN 23                                                              
              DISPLAY 'BP13KD92 - RECORD NOT FND, KEY: '                        
                       KD92-KEY-FLD                                             
           WHEN OTHER                                                           
              DISPLAY 'BP13KD92 - ERROR IN READ, STATUS: '                      
                       KD92-STATUS ' KEY ' KD92-KEY-FLD                         
           END-EVALUATE.                                                        
                                                                                
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      **********************                                                    
       4100-DELETE-BP13KD92.                                                    
      **********************                                                    
           DELETE BP13KD92.                                                     
                                                                                
           EVALUATE KD92-STATUS                                                 
           WHEN 00                                                              
                ADD   1  TO   WS-CNT-KD92-DEL                                   
                PERFORM  4200-MOVE-TO-BP13KD94  THRU 4200-EXIT                  
                PERFORM  4300-WRITE-BP13KD94    THRU 4300-EXIT                  
                                                                                
           WHEN OTHER                                                           
               DISPLAY 'BP13KD92 - ERROR IN DELETE, STATUS: '                   
                        KD92-STATUS ' KEY ' KD92-KEY-FLD                        
           END-EVALUATE.                                                        
                                                                                
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ***********************                                                   
       4200-MOVE-TO-BP13KD94.                                                   
      ***********************                                                   
      *   LOG DELETED RECORD TO HISTORY FILE                                    
      * ---------------------------------------                                 
           INITIALIZE                  BP13KD94-REC.                            
           MOVE SPACES              TO BP13KD94-REC.                            
           MOVE BP13KD92-REC        TO BP13KD94-REC.                            
           MOVE KD92-NUM-REGN       TO KD94-NUM-REGN-LOG.                       
           MOVE KD92-NUM-TRANS      TO KD94-NUM-TRANS-LOG.                      
           MOVE 'BP13CC72'          TO KD94-NUM-USERID.                         
                                                                                
       4200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *********************                                                     
       4300-WRITE-BP13KD94.                                                     
      *********************                                                     
           WRITE BP13KD94-REC.                                                  
                                                                                
           EVALUATE KD94-STATUS                                                 
           WHEN 00                                                              
                CONTINUE                                                        
           WHEN OTHER                                                           
               DISPLAY 'BP13KD94 - ERROR IN WRITE, STATUS: '                    
                        KD94-STATUS ' KEY ' KD94-KEY-FLD                        
           END-EVALUATE.                                                        
                                                                                
       4300-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *********************                                                     
       5000-WRITE-P13LC72A.                                                     
      *********************                                                     
      *   PRINT REPORT FOR UPDATED CASES                                        
      * ---------------------------------------                                 
           MOVE SPACES  TO LC72B-DETAILS.                                       
           INITIALIZE      LC72B-DETAILS.                                       
                                                                                
           STRING FD92-NUM-SCH-ACC(1:4) '-'                                     
                  FD92-NUM-SCH-ACC(5:4) '-'                                     
                  FD92-NUM-SCH-ACC(9:1) '-'                                     
                  FD92-NUM-SCH-ACC(10:2)                                        
                  DELIMITED BY SIZE INTO LC72B-SCH-ACC.                         
                                                                                
           MOVE FD92-NUM-REGN               TO LC72B-REGNO.                     
           MOVE FD92-CPF-INSTAL-CASH-NEW    TO LC72B-CASH.                      
           MOVE FD92-CPF-INSTAL-CPF1-NEW    TO LC72B-CPF1.                      
           MOVE FD92-CPF-INSTAL-CPF2-NEW    TO LC72B-CPF2.                      
           MOVE FD92-CPF-INSTAL-CPF3-NEW    TO LC72B-CPF3.                      
           MOVE FD92-CPF-INSTAL-CPF4-NEW    TO LC72B-CPF4.                      
           MOVE KD00-AMT-INSTAL             TO LC72B-AMT-INST.                  
           MOVE WS-KD92-AMT-INST            TO LC72B-TOTAL.                     
                                                                                
           IF FD92-DTE-REVSD-INSTAL-EFF NOT = SPACES AND LOW-VALUES             
                                            AND ZEROES                          
               STRING FD92-DTE-REVSD-INSTAL-EFF(7:2) '/'                        
                      FD92-DTE-REVSD-INSTAL-EFF(5:2) '/'                        
                      FD92-DTE-REVSD-INSTAL-EFF(1:4)                            
                      DELIMITED BY SIZE INTO LC72B-EFF-DTE                      
           END-IF.                                                              
                                                                                
           ADD   1                      TO  WS-SNOA.                            
           MOVE  WS-SNOA                TO  LC72B-DTL-SERIAL.                   
           MOVE  WS-EDITED-DATE         TO  LC72B-DATE-HDR.                     
           MOVE  'P13LC72A'             TO  WS-DOCID.                           
                                                                                
           IF WS-LINE-CTRA > WS-MAX-LINE OR                                     
              WS-SNOA       = 1                                                 
              ADD             1   TO WS-PAGE-CTRA                               
              MOVE ZEROES         TO LC72B-PAGE-HDR                             
              MOVE WS-PAGE-CTRA   TO LC72B-PAGE-HDR                             
              PERFORM 6000-PRINT-HEADING THRU 6000-EXIT                         
           END-IF.                                                              
                                                                                
           WRITE LC72A-PRTREC FROM LC72B-DETAILS.                               
                                                                                
           ADD   1   TO   WS-LINE-CTRA.                                         
                                                                                
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *********************                                                     
       5100-WRITE-P13LC72B.                                                     
      *********************                                                     
      *   PRINT ERROR REPORT                                                    
      * ---------------------------------------                                 
           MOVE SPACES  TO LC72B-DETAILS.                                       
           INITIALIZE      LC72B-DETAILS.                                       
                                                                                
           STRING FD92-NUM-SCH-ACC(1:4) '-'                                     
                  FD92-NUM-SCH-ACC(5:4) '-'                                     
                  FD92-NUM-SCH-ACC(9:1) '-'                                     
                  FD92-NUM-SCH-ACC(10:2)                                        
                  DELIMITED BY SIZE INTO LC72B-SCH-ACC.                         
                                                                                
           MOVE FD92-NUM-REGN               TO LC72B-REGNO.                     
           MOVE FD92-CPF-INSTAL-CASH-NEW    TO LC72B-CASH.                      
           MOVE FD92-CPF-INSTAL-CPF1-NEW    TO LC72B-CPF1.                      
           MOVE FD92-CPF-INSTAL-CPF2-NEW    TO LC72B-CPF2.                      
           MOVE FD92-CPF-INSTAL-CPF3-NEW    TO LC72B-CPF3.                      
           MOVE FD92-CPF-INSTAL-CPF4-NEW    TO LC72B-CPF4.                      
           MOVE KD00-AMT-INSTAL             TO LC72B-AMT-INST.                  
           MOVE WS-KD92-AMT-INST            TO LC72B-TOTAL.                     
                                                                                
           IF FD92-DTE-REVSD-INSTAL-EFF NOT = SPACES AND LOW-VALUES             
                                            AND ZEROES                          
               STRING FD92-DTE-REVSD-INSTAL-EFF(7:2) '/'                        
                      FD92-DTE-REVSD-INSTAL-EFF(5:2) '/'                        
                      FD92-DTE-REVSD-INSTAL-EFF(1:4)                            
                      DELIMITED BY SIZE INTO LC72B-EFF-DTE                      
           END-IF.                                                              
                                                                                
           ADD   1                      TO  WS-SNO.                             
           MOVE  WS-SNO                 TO  LC72B-DTL-SERIAL.                   
           MOVE  WS-EDITED-DATE         TO  LC72B-DATE-HDR.                     
           MOVE  'P13LC72B'             TO  WS-DOCID.                           
                                                                                
           IF WS-LINE-CTR > WS-MAX-LINE OR                                      
              WS-SNO       = 1                                                  
              ADD            1   TO WS-PAGE-CTR                                 
              MOVE ZEROES        TO LC72B-PAGE-HDR                              
              MOVE WS-PAGE-CTR   TO LC72B-PAGE-HDR                              
              PERFORM 6000-PRINT-HEADING THRU 6000-EXIT                         
           END-IF.                                                              
                                                                                
           WRITE LC72B-PRTREC FROM LC72B-DETAILS.                               
                                                                                
           ADD   1   TO   WS-LINE-CTR.                                          
           ADD   1   TO   WS-CNT-KD92-ERR.                                      
                                                                                
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ********************                                                      
       6000-PRINT-HEADING.                                                      
      ********************                                                      
            IF WS-DOCID = 'P13LC72A'                                            
               WRITE LC72A-PRTREC FROM WS-BLANK-LINE                            
               WRITE LC72A-PRTREC FROM WS-BLANK-LINE                            
               WRITE LC72A-PRTREC FROM WS-BLANK-LINE                            
               WRITE LC72A-PRTREC FROM LC72B-HEADER01 AFTER PAGE                
               WRITE LC72A-PRTREC FROM LC72A-HEADER02                           
               WRITE LC72A-PRTREC FROM WS-BLANK-LINE                            
               WRITE LC72A-PRTREC FROM LC72B-HEADER03                           
               WRITE LC72A-PRTREC FROM LC72B-HEADER04                           
               WRITE LC72A-PRTREC FROM WS-DASH-LINE                             
                                                                                
               INITIALIZE WS-LINE-CTRA                                          
               MOVE  9 TO WS-LINE-CTRA                                          
                                                                                
            ELSE                                                                
               WRITE LC72B-PRTREC FROM WS-BLANK-LINE                            
               WRITE LC72B-PRTREC FROM WS-BLANK-LINE                            
               WRITE LC72B-PRTREC FROM WS-BLANK-LINE                            
               WRITE LC72B-PRTREC FROM LC72B-HEADER01 AFTER PAGE                
               WRITE LC72B-PRTREC FROM LC72B-HEADER02                           
               WRITE LC72B-PRTREC FROM WS-BLANK-LINE                            
               WRITE LC72B-PRTREC FROM LC72B-HEADER03                           
               WRITE LC72B-PRTREC FROM LC72B-HEADER04                           
               WRITE LC72B-PRTREC FROM WS-DASH-LINE                             
                                                                                
               INITIALIZE WS-LINE-CTR                                           
               MOVE  9 TO WS-LINE-CTR                                           
                                                                                
             END-IF.                                                            
                                                                                
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      **********************                                                    
       9000-CLOSING-ROUTINE.                                                    
      **********************                                                    
           DISPLAY  SPACES.                                                     
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13CC72'.                                     
           DISPLAY 'RUN DATE   : ' WS-SYS-DATE.                                 
           DISPLAY  SPACES.                                                     
           DISPLAY 'BP13KD00 RECS UPDATED      = ' WS-CNT-KD00-UPD.             
           DISPLAY 'BP13KD92 RECS READ         = ' WS-CNT-KD92-READ.            
           DISPLAY 'BP13KD92 RECS DELETED      = ' WS-CNT-KD92-DEL.             
           DISPLAY 'BP13KD92 ERORRED RECS      = ' WS-CNT-KD92-ERR.             
                                                                                
           CLOSE BP13KD00                                                       
                 BP13KD92                                                       
                 BP13KD99                                                       
                 BP13FD92                                                       
                 SY02F001                                                       
                 BP13KD94                                                       
                 P13FD92A                                                       
                 P13LC72A                                                       
                 P13LC72B.                                                      
                                                                                
                                                                                
           IF  KD00-STATUS NOT = 0                                              
               DISPLAY 'CLOSING ERROR - BP13KD00, STATUS ' KD00-STATUS          
               MOVE KD00-STATUS TO RETURN-CODE                                  
           END-IF.                                                              
                                                                                
           IF  KD92-STATUS NOT = 0                                              
               DISPLAY 'CLOSING ERROR - BP13KD92, STATUS ' KD92-STATUS          
               MOVE KD92-STATUS TO RETURN-CODE                                  
           END-IF.                                                              
                                                                                
           IF  KD94-STATUS NOT = 0                                              
               DISPLAY 'CLOSING ERROR - BP13KD94, STATUS ' KD94-STATUS          
               MOVE KD94-STATUS TO RETURN-CODE                                  
           END-IF.                                                              
                                                                                
           IF  KD99-STATUS NOT = 0                                              
               DISPLAY 'CLOSING ERROR - BP13KD99, STATUS ' KD99-STATUS          
               MOVE KD99-STATUS TO RETURN-CODE                                  
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
