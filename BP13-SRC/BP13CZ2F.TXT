       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CZ2F.                                                 
      *AUTHOR.        IMC1.                                                     
      *DATE-WRITTEN.  16/07/2013.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE    : CHECK IF OCCUPANTS ARE SINGLE >= 35 OF AGE  *            
      *                                                            *            
      * INPUT  FILE  : BP13F730 - CASES PER REGN                   *            
      *                BP13K825 - CHECK IF NRIC1 IS FOUND          *            
      * OUTPUT FILE  : BP13LCZA - ELIGIBLE                         *            
      *                BP13LCZB - NON ELIGIBLE                     *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP135002  IMC1  12072013  NEW PROGRAM                      *            
      * BP135190  SMR2  04122013  EXPAND BP13K813 TO 1000          *            
      * BP136007  SMR2  04112015  CATER FOR 2-ROOM FLEXI           *            
      * BP136852  SMR2  21072017  CATER FOR RF                     *            
      * BP137439  AM25  03092018  CORRECT PAGINATION ISSUE         *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN       TO BP13F730.                            
                                                                                
           SELECT BP13K825 ASSIGN       TO BP13K825                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K825-KEY-FLD                         
                           FILE STATUS  IS WS-K825-STATUS.                      
                                                                                
           SELECT BP13K813  ASSIGN        TO BP13K813                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K813-KEY-FLD                       
                            FILE STATUS   IS WS-K813-STATUS.                    
                                                                                
           SELECT BP13LCZA ASSIGN       TO BP13LCZA.                            
                                                                                
           SELECT BP13LCZB ASSIGN       TO BP13LCZB.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F730                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  500 CHARACTERS                                     
            RECORDING MODE    IS F                                              
            LABEL RECORD      IS STANDARD.                                      
       COPY BP13F730.                                                           
                                                                                
       FD   BP13K825                                                            
            RECORD CONTAINS  200 CHARACTERS.                                    
       COPY BP13K825.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13LCZA                                                            
            RECORD CONTAINS   132 CHARACTERS                                    
            RECORDING MODE    IS F.                                             
       01   BP13LCZA-REC                   PIC X(132).                          
                                                                                
       FD   BP13LCZB                                                            
            RECORD CONTAINS   132 CHARACTERS                                    
            RECORDING MODE    IS F.                                             
       01   BP13LCZB-REC                   PIC X(132).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K730-STATUS              PIC 9(02)   VALUE ZEROES.            
           05  WS-K757-STATUS              PIC 9(02)   VALUE ZEROES.            
           05  WS-K813-STATUS              PIC 9(02)   VALUE ZEROES.            
           05  WS-K825-STATUS              PIC 9(02)   VALUE ZEROES.            
           05  WS-K030-STATUS              PIC 9(02)   VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F730-READ                PIC 9(08)   VALUE ZEROES.            
           05  WS-LCZA-WRITE               PIC 9(08)   VALUE ZEROES.            
           05  WS-LCZB-WRITE               PIC 9(08)   VALUE ZEROES.            
           05  WS-CNT-CHILD                PIC 9(02)   VALUE ZEROES.            
           05  WS-SN-LCZA                  PIC 9(07)   VALUE ZEROES.            
           05  WS-SN-LCZB                  PIC 9(07)   VALUE ZEROES.            
           05  WS-CNT                      PIC 9(01)   VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-AST-ELIG                 PIC X(01)   VALUE 'N'.               
           05  WS-F730-EOF                 PIC X(01)   VALUE 'N'.               
           05  WS-K825-EOF                 PIC X(01)   VALUE 'N'.               
           05  WS-K825-FND                 PIC X(01)   VALUE 'N'.               
           05  WS-K730-FND                 PIC X(01)   VALUE 'N'.               
           05  WS-K020-FND                 PIC X(01)   VALUE 'N'.               
           05  WS-K010-FND                 PIC X(01)   VALUE 'N'.               
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-NUM-BIRTH                PIC 9(08)   VALUE ZEROES.            
           05  WS-DTE-REQUEST              PIC 9(08)   VALUE ZEROES.            
           05  WS-SYSTEM-DATE              PIC 9(08)   VALUE ZEROES.            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-SN                       PIC 9(04)   VALUE ZEROES.            
           05  WS-NUM-SPOUSE-CTZ           PIC X(02)   VALUE SPACES.            
           05  WS-NUM-REGN                 PIC X(08)   VALUE SPACES.            
           05  WS-NUM-NRIC                 PIC X(09)   VALUE SPACES.            
           05  WS-NUM-NRIC-PROC            PIC X(09)   VALUE SPACES.            
           05  WS-NUM-NRIC-SP              PIC X(09)   VALUE SPACES.            
           05  WS-NUM-AGE                  PIC 9(08)   VALUE ZEROES.            
           05  WS-CHILD-AGE REDEFINES WS-NUM-AGE.                               
               10  WS-AGE-CY               PIC 9(04).                           
               10  WS-AGE-MM               PIC 9(02).                           
               10  WS-AGE-DD               PIC 9(02).                           
           05  WS-NRIC-INFO             PIC X(108).                             
           05  FILLER REDEFINES WS-NRIC-INFO OCCURS 4 TIMES.                    
               07  WS-NRIC              PIC X(09).                              
               07  WS-NRIC-AGE          PIC 9(02).                              
               07  WS-NRIC-RL           PIC X(02).                              
               07  WS-NRIC-MS           PIC X(01).                              
               07  WS-NRIC-SPOUSE       PIC X(09).                              
               07  WS-NRIC-SPOUSE-CTZ   PIC X(02).                              
               07  WS-NRIC-CHILD        PIC 9(02).                              
           05  WS-PAGE-CNT-A               PIC 9(04)   VALUE ZEROES.            
           05  WS-PAGE-CNT-B               PIC 9(04)   VALUE ZEROES.            
           05  WS-LINE-CNT-A               PIC 9(02)   VALUE ZEROES.            
           05  WS-LINE-CNT-B               PIC 9(02)   VALUE ZEROES.            
                                                                                
       01  LZ2F-HEADER01-ELIG.                                                  
           05  FILLER              PIC X(8)      VALUE 'BP13LZ2F'.              
           05  FILLER              PIC X(11)     VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(19)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  LZ2F-DATE-HDR-A     PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LZ2F-PAGE-HDR-A     PIC ZZZ9      VALUE ZEROES.                  
                                                                                
       01  LZ2F-HEADER01-NONELIG.                                               
           05  FILLER              PIC X(8)      VALUE 'BP13LZ2F'.              
           05  FILLER              PIC X(11)     VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(19)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  LZ2F-DATE-HDR-B     PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LZ2F-PAGE-HDR-B     PIC ZZZ9      VALUE ZEROES.                  
                                                                                
       01  LZ2F-HEADER02-ELIG.                                                  
           05  FILLER              PIC X(45)     VALUE SPACES.                  
           05  FILLER              PIC X(42)     VALUE                          
               'REPORT ON ELIGIBLE CASES FOR SINGLE SCHEME'.                    
           05  FILLER              PIC X(45)     VALUE SPACES.                  
                                                                                
       01  LZ2F-HEADER02-NONELIG.                                               
           05  FILLER              PIC X(43)     VALUE SPACES.                  
           05  FILLER              PIC X(46)     VALUE                          
               'REPORT ON NOT ELIGIBLE CASES FOR SINGLE SCHEME'.                
           05  FILLER              PIC X(43)     VALUE SPACES.                  
                                                                                
       01  LZ2F-HEADER03.                                                       
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(07)    VALUE '    S/N'.                
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(08)    VALUE 'REGN NO '.               
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(09)    VALUE 'NRIC'.                   
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(05)    VALUE 'MS   '.                  
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(03)    VALUE 'AGE'.                    
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(09)    VALUE 'SPOUSE'.                 
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(04)    VALUE 'SCTZ'.                   
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(05)    VALUE 'CHILD'.                  
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(02)    VALUE 'RL'.                     
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(02)    VALUE 'NT'.                     
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(02)    VALUE 'FT'.                     
           05  FILLER              PIC X(03)    VALUE SPACES.                   
           05  FILLER              PIC X(02)    VALUE 'HH'.                     
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(04)    VALUE 'B-HH'.                   
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(04)    VALUE 'ELIG'.                   
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(04)    VALUE 'ALLO'.                   
           05  FILLER              PIC X(33)    VALUE SPACES.                   
                                                                                
       01  LZ2F-BLANK.                                                          
           05  FILLER              PIC X(132)   VALUE SPACES.                   
                                                                                
       01  LZ2F-DETAIL01.                                                       
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  LZ2F-SN             PIC X(07)        VALUE ZEROES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LZ2F-REGN           PIC X(08)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LZ2F-NRIC           PIC X(09)        VALUE SPACES.               
           05  FILLER              PIC X(03)        VALUE SPACES.               
           05  LZ2F-MARITAL-STATUS PIC X(01)        VALUE SPACES.               
           05  FILLER              PIC X(05)        VALUE SPACES.               
           05  LZ2F-NUM-AGE        PIC 9(02)        VALUE ZEROES.               
           05  FILLER              PIC X(03)        VALUE SPACES.               
           05  LZ2F-NRIC-SP        PIC X(09)        VALUE SPACES.               
           05  FILLER              PIC X(03)        VALUE SPACES.               
           05  LZ2F-NUM-CTZ-SP     PIC X(02)        VALUE SPACES.               
           05  FILLER              PIC X(04)        VALUE SPACES.               
           05  LZ2F-CNT-CHILD      PIC X(02)        VALUE SPACES.               
           05  FILLER              PIC X(04)        VALUE SPACES.               
           05  LZ2F-NUM-RELATION   PIC X(02)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LZ2F-NUM-NEW-TOWN   PIC X(03)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LZ2F-NUM-FLAT-TYPE  PIC X(02)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LZ2F-NUM-HOUSEHOLD  PIC X(02)        VALUE SPACES.               
           05  FILLER              PIC X(03)        VALUE SPACES.               
           05  LZ2F-NUM-BALLOT-HH  PIC X(02)        VALUE SPACES.               
           05  FILLER              PIC X(04)        VALUE SPACES.               
           05  LZ2F-NUM-ELIG-SCH   PIC X(03)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LZ2F-NUM-ALLO-SCH   PIC X(03)        VALUE SPACES.               
           05  FILLER              PIC X(33)        VALUE SPACES.               
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ********************                                                      
       0000-MAIN-ROUTINE.                                                       
      ********************                                                      
                                                                                
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F730      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-RECS       THRU 3000-EXIT                       
             UNTIL WS-F730-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       1000-OPEN-FILES.                                                         
      ******************                                                        
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13K825                                                 
                       BP13K813                                                 
                OUTPUT BP13LCZA                                                 
                       BP13LCZB.                                                
                                                                                
           IF WS-K825-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K825 FILE ERROR ' WS-K825-STATUS             
              MOVE WS-K825-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K813 FILE ERROR ' WS-K813-STATUS             
              MOVE WS-K813-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)          TO WS-SYSTEM-DATE.          
           STRING WS-SYSTEM-DATE(7:2) '/'                                       
                  WS-SYSTEM-DATE(5:2) '/'                                       
                  WS-SYSTEM-DATE(1:4)                                           
                  DELIMITED BY SIZE INTO LZ2F-DATE-HDR-A.                       
           MOVE LZ2F-DATE-HDR-A                     TO LZ2F-DATE-HDR-B.         
                                                                                
           PERFORM 2100-PRINT-HEADER-LCZA  THRU 2100-EXIT.                      
           PERFORM 2200-PRINT-HEADER-LCZB  THRU 2200-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       2000-READ-BP13F730.                                                      
      *********************                                                     
                                                                                
           READ BP13F730 AT END                                                 
                MOVE 'Y'                   TO WS-F730-EOF                       
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                           TO WS-F730-READ.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       2100-PRINT-HEADER-LCZA.                                                  
      *===============================================================*         
                                                                                
           ADD 1                                    TO WS-PAGE-CNT-A.           
           MOVE WS-PAGE-CNT-A                       TO LZ2F-PAGE-HDR-A.         
                                                                                
           WRITE BP13LCZA-REC    FROM LZ2F-HEADER01-ELIG AFTER PAGE.            
           WRITE BP13LCZA-REC    FROM LZ2F-HEADER02-ELIG.                       
           WRITE BP13LCZA-REC    FROM LZ2F-BLANK.                               
           WRITE BP13LCZA-REC    FROM LZ2F-HEADER03 AFTER 2.                    
           WRITE BP13LCZA-REC    FROM LZ2F-BLANK.                               
                                                                                
           MOVE 5                                   TO WS-LINE-CNT-A.           
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       2200-PRINT-HEADER-LCZB.                                                  
      *===============================================================*         
                                                                                
           ADD 1                                    TO WS-PAGE-CNT-B.           
           MOVE WS-PAGE-CNT-B                       TO LZ2F-PAGE-HDR-B.         
                                                                                
           WRITE BP13LCZB-REC    FROM LZ2F-HEADER01-NONELIG AFTER PAGE.         
           WRITE BP13LCZB-REC    FROM LZ2F-HEADER02-NONELIG.                    
           WRITE BP13LCZB-REC    FROM LZ2F-BLANK.                               
           WRITE BP13LCZB-REC    FROM LZ2F-HEADER03 AFTER 2.                    
           WRITE BP13LCZB-REC    FROM LZ2F-BLANK.                               
                                                                                
           MOVE 5                                   TO WS-LINE-CNT-B.           
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       3000-PROCESS-RECS.                                                       
      ********************                                                      
                                                                                
           INITIALIZE                      BP13K825-REC                         
                                           WS-NRIC-INFO.                        
           MOVE SPACES                  TO WS-NUM-REGN                          
                                           WS-NUM-NRIC                          
           MOVE ZEROES                  TO WS-NUM-AGE                           
                                           WS-CNT-CHILD.                        
           MOVE 'Y'                     TO WS-AST-ELIG.                         
           MOVE 'N'                     TO WS-K825-FND.                         
           MOVE 1                       TO WS-CNT.                              
                                                                                
           PERFORM 4900-READ-BP13K813   THRU 4900-EXIT.                         
                                                                                
           IF F730-CDE-HOUSEHOLD      = 'G'   OR                                
             (F730-CDE-FLAT-TYPE NOT  = '2 ' AND '2F' AND 'RF') OR              
              K813-NUM-MATURE-EST-TAG = 'Y'                                     
              PERFORM 3100-GET-NRIC-INFO   THRU 3100-EXIT                       
              MOVE 1                       TO WS-CNT                            
              PERFORM 7100-WRITE-BP13LCZB  THRU 7100-EXIT                       
                UNTIL WS-NRIC(WS-CNT) = SPACES                                  
                   OR WS-CNT > 4                                                
           ELSE                                                                 
              PERFORM 3100-GET-NRIC-INFO     THRU 3100-EXIT                     
              MOVE 1                         TO WS-CNT                          
              PERFORM 4100-CHECK-ELIG        THRU 4100-EXIT                     
                UNTIL WS-NRIC(WS-CNT) = SPACES                                  
                   OR WS-CNT > 4                                                
                                                                                
              IF WS-AST-ELIG = 'Y'                                              
                 MOVE 1       TO WS-CNT                                         
                 PERFORM 7000-WRITE-BP13LCZA THRU 7000-EXIT                     
                   UNTIL WS-NRIC(WS-CNT) = SPACES                               
                      OR WS-CNT > 4                                             
              ELSE                                                              
                 MOVE 1       TO WS-CNT                                         
                 PERFORM 7100-WRITE-BP13LCZB THRU 7100-EXIT                     
                   UNTIL WS-NRIC(WS-CNT) = SPACES                               
                      OR WS-CNT > 4                                             
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F730        THRU 2000-EXIT.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       3100-GET-NRIC-INFO.                                                      
      ********************                                                      
                                                                                
           MOVE F730-NUM-REGN                TO WS-NUM-REGN                     
           MOVE F730-NUM-NRIC1               TO WS-NUM-NRIC                     
                                                WS-NRIC(WS-CNT)                 
           PERFORM 4000-PROCESS-NRIC       THRU 4000-EXIT                       
                                                                                
           IF F730-NUM-NRIC2(1:1) NOT = '#' AND SPACES                          
              MOVE 2                         TO WS-CNT                          
              MOVE F730-NUM-NRIC2            TO WS-NUM-NRIC                     
                                                WS-NRIC(WS-CNT)                 
              PERFORM 4000-PROCESS-NRIC    THRU 4000-EXIT                       
           END-IF                                                               
                                                                                
           IF F730-NUM-NRIC3(1:1) NOT = '#' AND SPACES                          
              MOVE 3                         TO WS-CNT                          
              MOVE F730-NUM-NRIC3            TO WS-NUM-NRIC                     
                                                WS-NRIC(WS-CNT)                 
              PERFORM 4000-PROCESS-NRIC    THRU 4000-EXIT                       
           END-IF                                                               
                                                                                
           IF F730-NUM-NRIC4(1:1) NOT = '#' AND SPACES                          
              MOVE 4                         TO WS-CNT                          
              MOVE F730-NUM-NRIC4            TO WS-NUM-NRIC                     
                                                WS-NRIC(WS-CNT)                 
              PERFORM 4000-PROCESS-NRIC    THRU 4000-EXIT                       
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       4000-PROCESS-NRIC.                                                       
      **********************                                                    
                                                                                
           PERFORM 4050-READ-BP13K825   THRU 4050-EXIT                          
                                                                                
           MOVE K825-NUM-MARITAL-STATUS TO WS-NRIC-MS(WS-CNT)                   
           MOVE K825-NUM-RELATIONSHIP   TO WS-NRIC-RL(WS-CNT)                   
                                                                                
           PERFORM 4400-COMPUTE-AGE     THRU 4400-EXIT                          
                                                                                
           IF K825-NUM-MARITAL-STATUS = '2'                                     
              PERFORM 4200-GET-SPOUSE-CTZ   THRU 4200-EXIT                      
           END-IF                                                               
                                                                                
           IF K825-NUM-MARITAL-STATUS NOT = '1'                                 
              PERFORM 4500-READ-OCCUPIERS   THRU 4500-EXIT                      
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       4050-READ-BP13K825.                                                      
      ********************                                                      
                                                                                
           MOVE SPACES                               TO BP13K825-REC.           
           INITIALIZE                                   BP13K825-REC.           
                                                                                
           MOVE WS-NUM-REGN                          TO K825-NUM-REGN.          
           MOVE WS-NUM-NRIC                          TO K825-NUM-NRIC.          
                                                                                
           READ BP13K825.                                                       
                                                                                
           EVALUATE WS-K825-STATUS                                              
              WHEN  00                                                          
                    MOVE 'Y'              TO WS-K825-FND                        
              WHEN  23                                                          
                    MOVE 'N'              TO WS-AST-ELIG                        
                    MOVE 'N'              TO WS-K825-FND                        
                    DISPLAY 'BP13K825 REC NOT FND: ' K825-KEY-FLD               
              WHEN  OTHER                                                       
                    DISPLAY 'NEXT BP13K825 ERROR ' WS-K825-STATUS               
                    MOVE WS-K825-STATUS TO RETURN-CODE                          
                    PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       4100-CHECK-ELIG.                                                         
      **********************                                                    
                                                                                
           EVALUATE TRUE                                                        
              WHEN WS-NRIC-MS(WS-CNT) = '1'                                     
                   IF F730-CDE-ELIG-SCH NOT = 'OR'                              
                      IF WS-NRIC-AGE(WS-CNT) < 31                               
                         MOVE 'N'       TO WS-AST-ELIG                          
                      ELSE                                                      
                         IF WS-NRIC-AGE(WS-CNT) < 21                            
                            MOVE 'N'    TO WS-AST-ELIG                          
                         END-IF                                                 
                      END-IF                                                    
                   END-IF                                                       
                                                                                
              WHEN WS-NRIC-MS(WS-CNT) = '2'                                     
                   IF WS-NRIC-AGE(WS-CNT)        <  31  OR                      
                      WS-NRIC-SPOUSE-CTZ(WS-CNT) = '10' OR                      
                      WS-NRIC-CHILD(WS-CNT)      > 0                            
                      MOVE 'N'       TO WS-AST-ELIG                             
                   ELSE                                                         
                      CONTINUE                                                  
                   END-IF                                                       
                                                                                
      * WIDOWED                                                                 
              WHEN WS-NRIC-MS(WS-CNT) = '3'                                     
                   IF WS-NRIC-AGE(WS-CNT)   < 21 OR                             
                      WS-NRIC-CHILD(WS-CNT) > 0                                 
                      MOVE 'N'       TO WS-AST-ELIG                             
                   ELSE                                                         
                      CONTINUE                                                  
                   END-IF                                                       
                                                                                
      * DIVORCED                                                                
              WHEN WS-NRIC-MS(WS-CNT) = '4'                                     
                   IF WS-NRIC-AGE(WS-CNT)   < 31 OR                             
                      WS-NRIC-CHILD(WS-CNT) > 0                                 
                      MOVE 'N'       TO WS-AST-ELIG                             
                   ELSE                                                         
                      CONTINUE                                                  
                   END-IF                                                       
      * SEPARATED                                                               
              WHEN WS-NRIC-MS(WS-CNT) = '5'                                     
                   MOVE 'N'       TO WS-AST-ELIG                                
                                                                                
           END-EVALUATE.                                                        
                                                                                
           ADD 1   TO WS-CNT.                                                   
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       4200-GET-SPOUSE-CTZ.                                                     
      *===============================================================*         
           INITIALIZE WS-NRIC-SPOUSE(WS-CNT)                                    
                      WS-NRIC-SPOUSE-CTZ(WS-CNT)                                
                                                                                
           MOVE K825-NUM-NRIC-SPOUSE    TO WS-NUM-NRIC                          
                                           WS-NRIC-SPOUSE(WS-CNT)               
                                                                                
           PERFORM 4050-READ-BP13K825   THRU 4050-EXIT.                         
                                                                                
           IF WS-K825-FND = 'Y'                                                 
              MOVE K825-NUM-CITIZENSHIP TO WS-NRIC-SPOUSE-CTZ(WS-CNT)           
           ELSE                                                                 
              DISPLAY 'K825 SPOUSE NOT FND: ' K825-KEY-FLD                      
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       4400-COMPUTE-AGE.                                                        
      *===============================================================*         
                                                                                
           IF F730-DTE-REQUEST NOT NUMERIC                                      
              MOVE ZEROES                         TO WS-DTE-REQUEST             
           ELSE                                                                 
              MOVE F730-DTE-REQUEST               TO WS-DTE-REQUEST             
           END-IF.                                                              
                                                                                
           IF K825-DTE-BIRTH NOT NUMERIC                                        
              MOVE ZEROES                         TO WS-NUM-BIRTH               
           ELSE                                                                 
              MOVE K825-DTE-BIRTH                 TO WS-NUM-BIRTH               
           END-IF.                                                              
                                                                                
           IF WS-NUM-REGN > WS-NUM-BIRTH                                        
              COMPUTE WS-NUM-AGE = WS-DTE-REQUEST - WS-NUM-BIRTH                
           ELSE                                                                 
              COMPUTE WS-NUM-AGE = 0                                            
           END-IF.                                                              
                                                                                
           MOVE WS-AGE-CY(3:2)      TO WS-NRIC-AGE(WS-CNT).                     
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       4500-READ-OCCUPIERS.                                                     
      *===============================================================*         
                                                                                
           INITIALIZE WS-CNT-CHILD                                              
                      WS-NRIC-CHILD(WS-CNT)                                     
                                                                                
           PERFORM 4600-START-BP13K825            THRU 4600-EXIT                
           IF WS-K825-FND = 'Y'                                                 
              PERFORM 4800-COMPUTE-CHILD THRU 4800-EXIT                         
                UNTIL WS-K825-EOF = 'Y' OR                                      
                      K825-NUM-REGN NOT = F730-NUM-REGN                         
           END-IF.                                                              
                                                                                
           MOVE WS-CNT-CHILD              TO WS-NRIC-CHILD(WS-CNT).             
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       4600-START-BP13K825.                                                     
      *===============================================================*         
                                                                                
           MOVE SPACES                            TO BP13K825-REC               
           INITIALIZE                                BP13K825-REC.              
           MOVE 'N'                               TO WS-K825-EOF                
                                                     WS-K825-FND.               
                                                                                
           MOVE F730-NUM-REGN                     TO K825-NUM-REGN              
                                                                                
           START BP13K825 KEY >= K825-KEY-FLD.                                  
                                                                                
           IF WS-K825-STATUS = 00 OR 02                                         
              MOVE 'Y'                            TO WS-K825-FND                
           ELSE                                                                 
              IF WS-K825-STATUS = 23 OR 10                                      
                 MOVE 'N'                         TO WS-K825-FND                
              ELSE                                                              
                 DISPLAY 'BP13K825 - ERROR STARTING : ' WS-K825-STATUS          
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       4700-READNEXT-BP13K825.                                                  
      *===============================================================*         
                                                                                
           READ BP13K825 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'    TO WS-K825-EOF                                      
                GO          TO 4700-EXIT                                        
           END-READ.                                                            
                                                                                
       4700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       4800-COMPUTE-CHILD.                                                      
      *===============================================================*         
                                                                                
           IF K825-NUM-REGN = F730-NUM-REGN                                     
              IF K825-NUM-RELATIONSHIP = 41                                     
                 ADD 1                        TO WS-CNT-CHILD                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 4700-READNEXT-BP13K825   THRU 4700-EXIT.                     
                                                                                
       4800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       4900-READ-BP13K813.                                                      
      ********************                                                      
                                                                                
           MOVE SPACES                         TO BP13K813-REC.                 
           INITIALIZE                             BP13K813-REC.                 
                                                                                
           MOVE F730-CDE-NT1                   TO K813-NUM-ZONE.                
           MOVE F730-CDE-FLAT-TYPE             TO K813-NUM-FLAT-TYPE.           
           MOVE F730-DTE-BALLOT                TO K813-DTE-BALLOT.              
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
              WHEN  00                                                          
                    CONTINUE                                                    
              WHEN  23                                                          
                    DISPLAY 'BP13K813 REC NOT FND: ' K813-KEY-FLD               
              WHEN  OTHER                                                       
                    DISPLAY 'NEXT BP13K813 ERROR ' WS-K813-STATUS               
                    MOVE WS-K813-STATUS TO RETURN-CODE                          
                    PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4900-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       7000-WRITE-BP13LCZA.                                                     
      **********************                                                    
      *    OK                                                                   
                                                                                
           INITIALIZE BP13LCZA-REC                                              
                      LZ2F-DETAIL01.                                            
                                                                                
           IF WS-LINE-CNT-A > 59                                                
              MOVE ZEROES                  TO WS-LINE-CNT-A                     
                                                                                
              PERFORM 2100-PRINT-HEADER-LCZA THRU 2100-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-CNT = 1                                                        
              ADD 1                        TO   WS-SN-LCZA                      
              MOVE WS-SN-LCZA              TO   LZ2F-SN                         
              MOVE F730-NUM-REGN           TO   LZ2F-REGN                       
           END-IF.                                                              
                                                                                
           MOVE WS-NRIC(WS-CNT)            TO   LZ2F-NRIC                       
           MOVE WS-NRIC-AGE(WS-CNT)        TO   LZ2F-NUM-AGE                    
           MOVE F730-CDE-NT1               TO   LZ2F-NUM-NEW-TOWN               
           MOVE F730-CDE-FLAT-TYPE         TO   LZ2F-NUM-FLAT-TYPE              
           MOVE WS-NRIC-RL(WS-CNT)         TO   LZ2F-NUM-RELATION               
           MOVE WS-NRIC-CHILD(WS-CNT)      TO   LZ2F-CNT-CHILD                  
           MOVE F730-CDE-HOUSEHOLD         TO   LZ2F-NUM-HOUSEHOLD              
           MOVE F730-CDE-BALLOT-HOUSEHOLD  TO   LZ2F-NUM-BALLOT-HH              
           MOVE F730-CDE-ELIG-SCH          TO   LZ2F-NUM-ELIG-SCH               
           MOVE F730-CDE-ALLOC-SCH         TO   LZ2F-NUM-ALLO-SCH               
           MOVE WS-NRIC-SPOUSE(WS-CNT)     TO   LZ2F-NRIC-SP                    
           MOVE WS-NRIC-SPOUSE-CTZ(WS-CNT) TO   LZ2F-NUM-CTZ-SP                 
                                                                                
           EVALUATE TRUE                                                        
           WHEN WS-NRIC-MS(WS-CNT) = '1'                                        
                MOVE 'S'                   TO   LZ2F-MARITAL-STATUS             
           WHEN WS-NRIC-MS(WS-CNT) = '2'                                        
                MOVE 'M'                   TO   LZ2F-MARITAL-STATUS             
           WHEN WS-NRIC-MS(WS-CNT) = '3'                                        
                MOVE 'W'                   TO   LZ2F-MARITAL-STATUS             
           WHEN WS-NRIC-MS(WS-CNT) = '4'                                        
                MOVE 'D'                   TO   LZ2F-MARITAL-STATUS             
           WHEN WS-NRIC-MS(WS-CNT) = '5'                                        
                MOVE 'P'                   TO   LZ2F-MARITAL-STATUS             
           END-EVALUATE.                                                        
                                                                                
           WRITE BP13LCZA-REC              FROM LZ2F-DETAIL01.                  
           ADD 1                           TO WS-LCZA-WRITE                     
                                              WS-CNT                            
                                              WS-LINE-CNT-A.                    
           MOVE SPACES                     TO BP13LCZA-REC.                     
           INITIALIZE                         BP13LCZA-REC.                     
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       7100-WRITE-BP13LCZB.                                                     
      **********************                                                    
      *    NOT ELIGIBLE FOR AST                                                 
                                                                                
           INITIALIZE BP13LCZB-REC                                              
                      LZ2F-DETAIL01.                                            
                                                                                
           IF WS-LINE-CNT-B > 59                                                
              MOVE ZEROES                  TO WS-LINE-CNT-B                     
                                                                                
              PERFORM 2200-PRINT-HEADER-LCZB THRU 2200-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-CNT = 1                                                        
              ADD 1   TO WS-SN-LCZB                                             
              MOVE WS-SN-LCZB                 TO   LZ2F-SN                      
              MOVE F730-NUM-REGN              TO   LZ2F-REGN                    
           END-IF                                                               
                                                                                
           MOVE WS-NRIC(WS-CNT)            TO   LZ2F-NRIC                       
           MOVE WS-NRIC-AGE(WS-CNT)        TO   LZ2F-NUM-AGE                    
           MOVE F730-CDE-NT1               TO   LZ2F-NUM-NEW-TOWN               
           MOVE F730-CDE-FLAT-TYPE         TO   LZ2F-NUM-FLAT-TYPE              
           MOVE WS-NRIC-RL(WS-CNT)         TO   LZ2F-NUM-RELATION               
           MOVE WS-NRIC-CHILD(WS-CNT)      TO   LZ2F-CNT-CHILD                  
           MOVE F730-CDE-HOUSEHOLD         TO   LZ2F-NUM-HOUSEHOLD              
           MOVE F730-CDE-BALLOT-HOUSEHOLD  TO   LZ2F-NUM-BALLOT-HH              
           MOVE F730-CDE-ELIG-SCH          TO   LZ2F-NUM-ELIG-SCH               
           MOVE F730-CDE-ALLOC-SCH         TO   LZ2F-NUM-ALLO-SCH               
           MOVE WS-NRIC-SPOUSE(WS-CNT)     TO   LZ2F-NRIC-SP                    
           MOVE WS-NRIC-SPOUSE-CTZ(WS-CNT) TO   LZ2F-NUM-CTZ-SP                 
                                                                                
           EVALUATE TRUE                                                        
           WHEN WS-NRIC-MS(WS-CNT) = '1'                                        
                MOVE 'S'                   TO   LZ2F-MARITAL-STATUS             
           WHEN WS-NRIC-MS(WS-CNT) = '2'                                        
                MOVE 'M'                   TO   LZ2F-MARITAL-STATUS             
           WHEN WS-NRIC-MS(WS-CNT) = '3'                                        
                MOVE 'W'                   TO   LZ2F-MARITAL-STATUS             
           WHEN WS-NRIC-MS(WS-CNT) = '4'                                        
                MOVE 'D'                   TO   LZ2F-MARITAL-STATUS             
           WHEN WS-NRIC-MS(WS-CNT) = '5'                                        
                MOVE 'P'                   TO   LZ2F-MARITAL-STATUS             
           END-EVALUATE.                                                        
                                                                                
           WRITE BP13LCZB-REC              FROM LZ2F-DETAIL01.                  
           ADD 1                           TO WS-LCZB-WRITE                     
                                              WS-CNT                            
                                              WS-LINE-CNT-B.                    
           MOVE SPACES                     TO BP13LCZB-REC.                     
           INITIALIZE                         BP13LCZB-REC.                     
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *******************                                                       
       9000-CLOSE-FILES.                                                        
      *******************                                                       
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13CZ2F ================*'.              
           DISPLAY '  NO OF F730 RECORDS READ     = ' WS-F730-READ.             
           DISPLAY '  NO OF LCZA RECORDS WRITTEN  = ' WS-LCZA-WRITE.            
           DISPLAY '  NO OF LCZB RECORDS WRITTEN  = ' WS-LCZB-WRITE.            
           DISPLAY '*==========================================*'.              
                                                                                
           CLOSE BP13F730                                                       
                 BP13K825                                                       
                 BP13K813                                                       
                 BP13LCZA                                                       
                 BP13LCZB.                                                      
                                                                                
           IF WS-K825-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K825 FILE ERROR ' WS-K825-STATUS             
              MOVE WS-K825-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K813 FILE ERROR ' WS-K813-STATUS             
              MOVE WS-K813-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
        9000-EXIT.                                                              
            EXIT.                                                               
                                                                                
