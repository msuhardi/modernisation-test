      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
       PROGRAM-ID. BP13CM27.                                                    
       AUTHOR.        SUJITHRA CHINNIAH.                                        
       DATE-WRITTEN.  01/08/2023.                                               
                                                                                
      ******************************************************************        
      *                                                                *        
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                          *        
      *    SYSTEM ID   : BP13                                          *        
      *    OBJECTIVE   : TO GENERATE ELIGIBLE REPORTS.                 *        
      *                  PRINT NIL LISTING IF NO RECORD TO PRINT.      *        
      *                  THE PROGRAM IS REUSED BY BP13DM28 JOB.        *        
      *                                                                *        
      *    INPUT  FILE : BP13FIN1 - ACCEPTED AND REJECTED FILE         *        
      *                  BP13FIN2 - REPORT-ID                          *        
      *                                                                *        
      *    OUTPUT FILE : BP13LRP1 - REPORT WITH CAT=BTO                *        
      *                  BP13LRP2 - REPORT WITH CAT=SBF OR OTHERS      *        
      ******************************************************************        
      *----------------------------------------------------------------*        
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS               *        
      * -------- ---------- ---- -----------------------               *        
      * BP139530 01/08/2023 SC54 NEW PROGRAM.                          *        
      * BP139530 16/10/2023 SC54 REMOVED RETURN CODE =121 FOR NIL LSTNG*        
      ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
            SELECT FIN1 ASSIGN TO BP13FIN1                                      
            FILE STATUS IS WS-FIN1-STA.                                         
                                                                                
            SELECT FIN2 ASSIGN TO BP13FIN2                                      
            FILE STATUS IS WS-FIN2-STA.                                         
                                                                                
            SELECT RPT01 ASSIGN TO BP13LRP1.                                    
                                                                                
            SELECT RPT02 ASSIGN TO BP13LRP2.                                    
                                                                                
      ******************************************************************        
        DATA DIVISION.                                                          
      ******************************************************************        
        FILE SECTION.                                                           
                                                                                
        FD FIN1                                                                 
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORD IS STANDARD.                                           
        01 INPUT-FIN1                        PIC X(100).                        
                                                                                
        FD FIN2                                                                 
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 80 CHARACTERS                                       
            RECORDING MODE IS F                                                 
            LABEL RECORD IS STANDARD.                                           
        01 INPUT-FIN2                        PIC X(80).                         
                                                                                
        FD RPT01                                                                
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORD IS STANDARD.                                           
        01 OUTPUT-RPT01                      PIC X(132).                        
                                                                                
        FD RPT02                                                                
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORD IS STANDARD.                                           
        01 OUTPUT-RPT02                      PIC X(132).                        
                                                                                
        WORKING-STORAGE SECTION.                                                
                                                                                
        01 FIN1-HEADER.                                                         
           05 FIN1-DTE-BALLOT-QTR            PIC 9(06).                         
           05 FIN1-HDR-LOW-VAL               PIC X(08).                         
           05 FIN1-DTE-CRE                   PIC 9(08).                         
           05 FIN1-TIME-CRE                  PIC 9(06).                         
           05 FILLER                         PIC X(72).                         
        01 FIN1-DETAIL REDEFINES FIN1-HEADER.                                   
           05 FILLER-BALLOT                  PIC 9(06).                         
           05 FIN1-NUM-REGN                  PIC X(08).                         
           05 FIN1-CDE-ELIG-SCH              PIC X(03).                         
           05 FIN1-NUM-ALLO-CAT              PIC X(03).                         
           05 FIN1-CDE-HOUSEHOLD             PIC X(01).                         
           05 FIN1-NUM-FT2T                  PIC X(01).                         
           05 FIN1-DTE-REQUEST               PIC X(08).                         
           05 FIN1-NUM-HFE                   PIC X(09).                         
           05 FIN1-DTE-HFE-APPLN             PIC X(08).                         
           05 FIN1-CDE-FAMILY-OUTCOME        PIC X(01).                         
           05 FIN1-CDE-RESIDENTIAL-OUTCOME   PIC X(01).                         
           05 FIN1-CDE-CURR-FLAT             PIC X.                             
           05 FIN1-CDE-EX-FLAT               PIC X.                             
           05 FIN1-CDE-PRIVATE-RES           PIC X.                             
           05 FIN1-FILLER2                   PIC X(02).                         
           05 FIN1-CDE-CHANCE-OUTCOME        PIC X(01).                         
           05 FIN1-CDE-FTPMC                 PIC X(01).                         
           05 FIN1-REMARK                    PIC X(30).                         
           05 FILLER                         PIC X(14).                         
                                                                                
        01 FIN2-INPUT.                                                          
           05 FIN2-JOB-ID                    PIC X(08) VALUE SPACES.            
           05 FILLER                         PIC X(72) VALUE SPACES.            
                                                                                
        01 WS-HEADER-SPACE                   PIC X(132) VALUE SPACES.           
        01 WS-HEADER-LINE                    PIC X(132) VALUE ALL '-'.          
                                                                                
        01 WS-HEADER-LINE-01.                                                   
           05 WS-HDR-TITLE                   PIC X(08).                         
           05 FILLER                         PIC X(05) VALUE '_RPT0'.           
           05 WS-HDR-RPT-NO                  PIC X(01).                         
           05 FILLER                         PIC X(39) VALUE SPACES.            
           05 FILLER                         PIC X(20)                          
                                             VALUE 'SYSTEM OF COMMITMENT        
      -                                      ''.                                
           05 FILLER                         PIC X(29) VALUE SPACES.            
           05 WS-HDR-DATE                    PIC X(10)                          
                                             VALUE 'DD/MM/CCYY'.                
           05 FILLER                         PIC X(01) VALUE SPACES.            
           05 WS-HDR-TIME                    PIC X(08)                          
                                             VALUE 'HH:MM:SS'.                  
           05 FILLER                         PIC X(06) VALUE ' PAGE '.          
           05 WS-HDR-PAGENO                  PIC ZZZZ9.                         
      *                                                                         
        01 WS-HEADER-LINE-ELI-02.                                               
           05 FILLER                         PIC X(40) VALUE SPACES.            
           05 FILLER                         PIC X(44)                          
                                             VALUE 'FIRST TIMER APPLICAT        
      -                                      'ION ELIGIBLE FOR FT(PMC)'.        
           05 FILLER                         PIC X(48) VALUE SPACES.            
      *                                                                         
        01 WS-HEADER-LINE-INELI-02.                                             
           05 FILLER                         PIC X(32) VALUE SPACES.            
           05 FILLER                         PIC X(46)                          
                                             VALUE 'FIRST TIMER APPLICAT        
      -                                      'ION INELIGIBLE FOR FT(PMC)        
      -                                      ''.                                
           05 FILLER                         PIC X(07)                          
                                             VALUE ' REPORT'.                   
           05 FILLER                         PIC X(47) VALUE SPACES.            
      *                                                                         
        01 WS-HEADER-LINE-03.                                                   
           05 FILLER                         PIC X(41) VALUE SPACES.            
           05 FILLER                         PIC X(20)                          
                                             VALUE 'FOR BALLOT QUARTER:         
      -                                      ''.                                
           05 WS-HDR-BALLOT-QTR              PIC X(07) VALUE 'CCYY/MM'.         
           05 FILLER                         PIC X(12)                          
                                             VALUE ', CATEGORY: '.              
           05 WS-HDR-CATEGORY                PIC X(03).                         
           05 FILLER                         PIC X(49) VALUE SPACES.            
      *                                                                         
        01 WS-HEADER-DTE-01.                                                    
           05 FILLER                         PIC X(15)                          
                                             VALUE 'SNO   APPL DTE '.           
           05 FILLER                         PIC X(31)                          
                                             VALUE 'REGNO    ALLO ELIG H        
      -                                      'H FT2T HFE '.                     
           05 FILLER                         PIC X(11)                          
                                             VALUE 'DATE HFE NO'.               
           05 FILLER                         PIC X(04) VALUE SPACES.            
           05 FILLER                         PIC X(18)                          
                                             VALUE 'FAM RES CHC FTPMC '.        
           05 WS-HDR-REMARKS                 PIC X(30).                         
           05 FILLER                         PIC X(23) VALUE SPACES.            
                                                                                
      *                                                                         
        01 WS-OUTPUT-DETAIL.                                                    
           05 WS-RPT-SNO                     PIC ZZZZ9.                         
           05 FILLER                         PIC X(01) VALUE SPACES.            
           05 WS-RPT-DTE-HFE-APPLN           PIC X(08).                         
           05 FILLER                         PIC X(01) VALUE SPACES.            
           05 WS-RPT-REGN                    PIC X(08).                         
           05 FILLER                         PIC X(01) VALUE SPACES.            
           05 WS-RPT-ALLOC-CAT               PIC X(03).                         
           05 FILLER                         PIC X(02) VALUE SPACES.            
           05 WS-RPT-ELIG-SCH                PIC X(03).                         
           05 FILLER                         PIC X(02) VALUE SPACES.            
           05 WS-RPT-HOUSEHOLD               PIC X(01).                         
           05 FILLER                         PIC X(02) VALUE SPACES.            
           05 WS-RPT-NUM-FT2T                PIC X(01).                         
           05 FILLER                         PIC X(04) VALUE SPACES.            
           05 WS-RPT-DTE-REQ                 PIC X(08).                         
           05 FILLER                         PIC X(01) VALUE SPACES.            
           05 WS-RPT-HFE                     PIC X(09).                         
           05 FILLER                         PIC X(01) VALUE SPACES.            
           05 WS-RPT-CDE-FAMILY-OC           PIC X(01).                         
           05 FILLER                         PIC X(03) VALUE SPACES.            
           05 WS-RPT-CDE-REDTL-OC            PIC X(01).                         
           05 FILLER                         PIC X(03) VALUE SPACES.            
           05 WS-RPT-CDE-CHANCE-OC           PIC X(01).                         
           05 FILLER                         PIC X(03) VALUE SPACES.            
           05 WS-RPT-FTPMC                   PIC X(01).                         
           05 FILLER                         PIC X(05) VALUE SPACES.            
           05 WS-RPT-REMARK                  PIC X(30).                         
           05 FILLER                         PIC X(23) VALUE SPACES.            
                                                                                
        01 WS-PGM-VARIABLE.                                                     
           05 WS-PAGE-NO-RPT01               PIC 9(05) VALUE ZEROS.             
           05 WS-PAGE-NO-RPT02               PIC 9(05) VALUE ZEROS.             
           05 WS-REC-CNT-1                   PIC 9(02) VALUE ZEROS.             
           05 WS-REC-CNT-2                   PIC 9(02) VALUE ZEROS.             
           05 WS-MAX-REC                     PIC 9(02) VALUE 56.                
           05 WS-SNO-RPT01                   PIC 9(05) VALUE ZEROS.             
           05 WS-SNO-RPT02                   PIC 9(05) VALUE ZEROS.             
           05 WS-FILE-STATUS.                                                   
              10 WS-FIN1-STA                 PIC 9(02) VALUE ZEROS.             
              10 WS-FIN2-STA                 PIC 9(02) VALUE ZEROS.             
           05 WS-EOF-FIN1                    PIC X(01) VALUE 'N'.               
           05 WS-REC-CNT-VAR.                                                   
              10 WS-FIN1-CNT                 PIC 9(08) VALUE ZEROS.             
              10 WS-FIN2-CNT                 PIC 9(08) VALUE ZEROS.             
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      ******************************************************************        
       0000-INIT-PROCESS.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT FIN1                                                      
           OPEN INPUT FIN2                                                      
           OPEN OUTPUT RPT01                                                    
           OPEN OUTPUT RPT02                                                    
           MOVE 01                            TO WS-HDR-PAGENO                  
           PERFORM  1000-INIT-READ-BP13FIN2                                     
           PERFORM  2000-MAIN-LOGIC-PROCESS                                     
             .                                                                  
                                                                                
      ******************************************************************        
       1000-INIT-READ-BP13FIN2.                                                 
      ******************************************************************        
                                                                                
           READ FIN2                                                            
           AT END                                                               
              IF WS-FIN2-CNT = ZEROES                                           
                 DISPLAY 'BP13CM27-E01:  BP13FIN2 IS EMPTY.'                    
                 PERFORM 9000-CLOSE-INPUT-FILES                                 
                 PERFORM 9100-CANCEL-JOB                                        
              END-IF                                                            
           NOT AT END                                                           
              MOVE INPUT-FIN2                TO FIN2-INPUT                      
              IF FIN2-JOB-ID = SPACES OR ZEROES                                 
                 DISPLAY 'BP13CM27-E02:  BP13FIN2 IS EMPTY.'                    
                 PERFORM 9000-CLOSE-INPUT-FILES                                 
                 PERFORM 9100-CANCEL-JOB                                        
              ELSE                                                              
                 ADD 1                       TO WS-FIN2-CNT                     
                 MOVE FIN2-JOB-ID            TO WS-HDR-TITLE                    
                 PERFORM 3000-READ-INPUT-FIN1                                   
                 IF WS-EOF-FIN1 NOT = 'Y'                                       
                    PERFORM 4000-HEADER-PROCESS                                 
                 END-IF                                                         
              END-IF                                                            
                .                                                               
                                                                                
      ******************************************************************        
       2000-MAIN-LOGIC-PROCESS.                                                 
      ******************************************************************        
             MOVE 01                          TO WS-PAGE-NO-RPT01               
                                                 WS-PAGE-NO-RPT02               
             PERFORM 3000-READ-INPUT-FIN1                                       
             PERFORM UNTIL FIN1-NUM-REGN = HIGH-VALUES                          
                        OR WS-EOF-FIN1 = 'Y'                                    
                PERFORM 2100-OUTPUT-DETAIL                                      
                PERFORM 3000-READ-INPUT-FIN1                                    
             END-PERFORM                                                        
             WRITE OUTPUT-RPT01 FROM WS-HEADER-SPACE                            
             WRITE OUTPUT-RPT02 FROM WS-HEADER-SPACE                            
             PERFORM 9000-CLOSE-INPUT-FILES                                     
             PERFORM 9200-PRINT-CTL-SUMMARY                                     
             .                                                                  
                                                                                
      ******************************************************************        
       2100-OUTPUT-DETAIL.                                                      
      ******************************************************************        
             MOVE  FIN1-NUM-REGN             TO WS-RPT-REGN                     
             MOVE  FIN1-CDE-ELIG-SCH         TO WS-RPT-ELIG-SCH                 
             MOVE  FIN1-NUM-ALLO-CAT         TO WS-RPT-ALLOC-CAT                
             MOVE  FIN1-CDE-HOUSEHOLD        TO WS-RPT-HOUSEHOLD                
             MOVE  FIN1-NUM-FT2T             TO WS-RPT-NUM-FT2T                 
             MOVE  FIN1-DTE-REQUEST          TO WS-RPT-DTE-REQ                  
             MOVE  FIN1-NUM-HFE              TO WS-RPT-HFE                      
             MOVE  FIN1-DTE-HFE-APPLN        TO WS-RPT-DTE-HFE-APPLN            
             MOVE  FIN1-CDE-FAMILY-OUTCOME   TO WS-RPT-CDE-FAMILY-OC            
             MOVE  FIN1-CDE-RESIDENTIAL-OUTCOME                                 
                                             TO WS-RPT-CDE-REDTL-OC             
             MOVE  FIN1-CDE-CHANCE-OUTCOME   TO WS-RPT-CDE-CHANCE-OC            
             MOVE  FIN1-CDE-FTPMC            TO WS-RPT-FTPMC                    
             MOVE  FIN1-REMARK               TO WS-RPT-REMARK                   
             IF FIN2-JOB-ID = 'BP13DM27'                                        
                IF FIN1-NUM-ALLO-CAT = 'BTO'                                    
                   ADD 1                           TO WS-SNO-RPT01              
                   MOVE WS-SNO-RPT01               TO WS-RPT-SNO                
                   IF WS-REC-CNT-1 <  WS-MAX-REC - 1                            
                      WRITE OUTPUT-RPT01 FROM WS-HEADER-SPACE                   
                      WRITE OUTPUT-RPT01 FROM WS-OUTPUT-DETAIL                  
                   ELSE                                                         
                      PERFORM 5000-NEXT-PAGE-PROC                               
                      WRITE OUTPUT-RPT01 FROM WS-HEADER-SPACE                   
                      WRITE OUTPUT-RPT01 FROM WS-OUTPUT-DETAIL                  
                   END-IF                                                       
                   ADD 2                     TO WS-REC-CNT-1                    
                ELSE                                                            
                   ADD 1                           TO WS-SNO-RPT02              
                   MOVE WS-SNO-RPT02               TO WS-RPT-SNO                
                   IF WS-REC-CNT-2 <  WS-MAX-REC - 1                            
                      WRITE OUTPUT-RPT02 FROM WS-HEADER-SPACE                   
                      WRITE OUTPUT-RPT02 FROM WS-OUTPUT-DETAIL                  
                   ELSE                                                         
                      PERFORM 5000-NEXT-PAGE-PROC                               
                      WRITE OUTPUT-RPT02 FROM WS-HEADER-SPACE                   
                      WRITE OUTPUT-RPT02 FROM WS-OUTPUT-DETAIL                  
                   END-IF                                                       
                   ADD 2                     TO WS-REC-CNT-2                    
                END-IF                                                          
             ELSE                                                               
                IF FIN1-NUM-ALLO-CAT = 'BTO'                                    
                   ADD 1                           TO WS-SNO-RPT01              
                   MOVE WS-SNO-RPT01               TO WS-RPT-SNO                
                   IF WS-REC-CNT-1  <  WS-MAX-REC - 1                           
                      WRITE OUTPUT-RPT01 FROM WS-HEADER-SPACE                   
                      WRITE OUTPUT-RPT01 FROM WS-OUTPUT-DETAIL                  
                   ELSE                                                         
                      PERFORM 5000-NEXT-PAGE-PROC                               
                      WRITE OUTPUT-RPT01 FROM WS-HEADER-SPACE                   
                      WRITE OUTPUT-RPT01 FROM WS-OUTPUT-DETAIL                  
                   END-IF                                                       
                   ADD 2                     TO WS-REC-CNT-1                    
                ELSE                                                            
                   ADD 1                           TO WS-SNO-RPT02              
                   MOVE WS-SNO-RPT02               TO WS-RPT-SNO                
                   IF WS-REC-CNT-2  <  WS-MAX-REC - 1                           
                      WRITE OUTPUT-RPT02 FROM WS-HEADER-SPACE                   
                      WRITE OUTPUT-RPT02 FROM WS-OUTPUT-DETAIL                  
                   ELSE                                                         
                      PERFORM 5000-NEXT-PAGE-PROC                               
                      WRITE OUTPUT-RPT02 FROM WS-HEADER-SPACE                   
                      WRITE OUTPUT-RPT02 FROM WS-OUTPUT-DETAIL                  
                   END-IF                                                       
                   ADD 2                     TO WS-REC-CNT-2                    
                END-IF                                                          
             END-IF                                                             
             .                                                                  
                                                                                
      ******************************************************************        
       3000-READ-INPUT-FIN1.                                                    
      ******************************************************************        
                                                                                
             READ FIN1                                                          
             AT END                                                             
                MOVE 'Y' TO WS-EOF-FIN1                                         
                IF WS-FIN1-CNT = ZEROES                                         
                   PERFORM 4000-HEADER-PROCESS                                  
                   PERFORM 3200-PRINT-NILL-LISTING                              
                END-IF                                                          
             NOT AT END                                                         
                MOVE INPUT-FIN1                    TO FIN1-DETAIL               
                EVALUATE TRUE                                                   
                   WHEN FIN1-NUM-REGN = LOW-VALUES                              
                      PERFORM 3100-BALL-DATE-TIME-FORMATTING                    
                   WHEN FIN1-NUM-REGN NOT = LOW-VALUES AND HIGH-VALUES          
                      ADD 1 TO WS-FIN1-CNT                                      
                                                                                
                   WHEN FIN1-NUM-REGN = HIGH-VALUES                             
                      MOVE 'Y' TO  WS-EOF-FIN1                                  
                      IF WS-FIN1-CNT = ZERO                                     
                         PERFORM 3200-PRINT-NILL-LISTING                        
                      END-IF                                                    
                                                                                
                   WHEN OTHER                                                   
                      DISPLAY 'FILE STATUS  READ FIN1:' WS-FIN1-STA             
                END-EVALUATE                                                    
             END-READ                                                           
             .                                                                  
                                                                                
      ******************************************************************        
       3100-BALL-DATE-TIME-FORMATTING.                                          
      ******************************************************************        
                                                                                
             MOVE FIN1-DTE-BALLOT-QTR(1:4)   TO WS-HDR-BALLOT-QTR(1:4)          
             MOVE '/'                        TO WS-HDR-BALLOT-QTR(5:1)          
             MOVE FIN1-DTE-BALLOT-QTR(5:2)   TO WS-HDR-BALLOT-QTR(6:2)          
                                                                                
             MOVE FIN1-DTE-CRE(1:4)          TO WS-HDR-DATE(7:4)                
             MOVE '/'                        TO WS-HDR-DATE(6:1)                
             MOVE FIN1-DTE-CRE(5:2)          TO WS-HDR-DATE(4:2)                
             MOVE '/'                        TO WS-HDR-DATE(3:1)                
             MOVE FIN1-DTE-CRE(7:2)          TO WS-HDR-DATE(1:2)                
                                                                                
             MOVE FIN1-TIME-CRE(1:2)         TO WS-HDR-TIME(1:2)                
             MOVE ':'                        TO WS-HDR-TIME(3:1)                
             MOVE FIN1-TIME-CRE(3:2)         TO WS-HDR-TIME(4:2)                
             MOVE ':'                        TO WS-HDR-TIME(6:1)                
             MOVE FIN1-TIME-CRE(5:2)         TO WS-HDR-TIME(7:2)                
             .                                                                  
                                                                                
      ******************************************************************        
       3200-PRINT-NILL-LISTING.                                                 
      ******************************************************************        
                                                                                
             MOVE SPACES                     TO WS-OUTPUT-DETAIL                
             MOVE 'NIL LISTINGS'             TO WS-OUTPUT-DETAIL                
             IF WS-SNO-RPT01 = ZERO                                             
                WRITE OUTPUT-RPT01 FROM WS-HEADER-SPACE                         
                WRITE OUTPUT-RPT01 FROM WS-OUTPUT-DETAIL                        
             END-IF                                                             
             IF WS-SNO-RPT02 = ZERO                                             
                WRITE OUTPUT-RPT02 FROM WS-HEADER-SPACE                         
                WRITE OUTPUT-RPT02 FROM WS-OUTPUT-DETAIL                        
             END-IF                                                             
      * START CR9530 PKG015228                                                  
      *      MOVE 121                        TO RETURN-CODE                     
      * END CR9530 PKG015228                                                    
             .                                                                  
                                                                                
      ******************************************************************        
       4000-HEADER-PROCESS.                                                     
      ******************************************************************        
                MOVE 1                       TO WS-HDR-RPT-NO                   
                PERFORM 4100-WRITE-COM-HEADER-01-RPT01                          
                MOVE 2                       TO WS-HDR-RPT-NO                   
                PERFORM 4200-WRITE-COM-HEADER-01-RPT02                          
                IF FIN2-JOB-ID = 'BP13DM27'                                     
                   PERFORM 4300-WRITE-HEADER-ELI-RPT01                          
                   PERFORM 4400-WRITE-HEADER-ELI-RPT02                          
                ELSE                                                            
                   PERFORM 4500-WRITE-HEADER-INELI-RPT01                        
                   PERFORM 4600-WRITE-HEADER-INELI-RPT02                        
                END-IF                                                          
                PERFORM 4700-WRITE-COM-HEADER-02-RPT01                          
                PERFORM 4800-WRITE-COM-HEADER-02-RPT02                          
             .                                                                  
                                                                                
      ******************************************************************        
       4100-WRITE-COM-HEADER-01-RPT01.                                          
      ******************************************************************        
             ADD 1         TO WS-REC-CNT-1                                      
             WRITE OUTPUT-RPT01 FROM WS-HEADER-LINE-01 AFTER PAGE               
             .                                                                  
      ******************************************************************        
       4200-WRITE-COM-HEADER-01-RPT02.                                          
      ******************************************************************        
             ADD 1         TO WS-REC-CNT-2                                      
             WRITE OUTPUT-RPT02 FROM WS-HEADER-LINE-01 AFTER PAGE               
             .                                                                  
                                                                                
      ******************************************************************        
       4300-WRITE-HEADER-ELI-RPT01.                                             
      ******************************************************************        
             ADD 1         TO WS-REC-CNT-1                                      
             WRITE OUTPUT-RPT01 FROM WS-HEADER-LINE-ELI-02                      
             .                                                                  
                                                                                
      ******************************************************************        
       4400-WRITE-HEADER-ELI-RPT02.                                             
      ******************************************************************        
             ADD 1         TO WS-REC-CNT-2                                      
             WRITE OUTPUT-RPT02 FROM WS-HEADER-LINE-ELI-02                      
             .                                                                  
                                                                                
      ******************************************************************        
       4500-WRITE-HEADER-INELI-RPT01.                                           
      ******************************************************************        
             ADD 1         TO WS-REC-CNT-1                                      
             WRITE OUTPUT-RPT01 FROM WS-HEADER-LINE-INELI-02                    
             MOVE  'REMARKS'   TO WS-HDR-REMARKS                                
             .                                                                  
                                                                                
      ******************************************************************        
       4600-WRITE-HEADER-INELI-RPT02.                                           
      ******************************************************************        
             ADD 1         TO WS-REC-CNT-2                                      
             WRITE OUTPUT-RPT02 FROM WS-HEADER-LINE-INELI-02                    
             MOVE  'REMARKS'   TO WS-HDR-REMARKS                                
             .                                                                  
                                                                                
      ******************************************************************        
       4700-WRITE-COM-HEADER-02-RPT01.                                          
      ******************************************************************        
             ADD 6         TO WS-REC-CNT-1                                      
             MOVE 'BTO'                      TO WS-HDR-CATEGORY                 
             WRITE OUTPUT-RPT01 FROM WS-HEADER-LINE-03                          
             WRITE OUTPUT-RPT01 FROM WS-HEADER-SPACE                            
             WRITE OUTPUT-RPT01 FROM WS-HEADER-SPACE                            
             WRITE OUTPUT-RPT01 FROM WS-HEADER-DTE-01                           
             WRITE OUTPUT-RPT01 FROM WS-HEADER-SPACE                            
             WRITE OUTPUT-RPT01 FROM WS-HEADER-LINE                             
             .                                                                  
                                                                                
      ******************************************************************        
       4800-WRITE-COM-HEADER-02-RPT02.                                          
      ******************************************************************        
             ADD 6         TO WS-REC-CNT-2                                      
             MOVE 'SBF'                      TO WS-HDR-CATEGORY                 
             WRITE OUTPUT-RPT02 FROM WS-HEADER-LINE-03                          
             WRITE OUTPUT-RPT02 FROM WS-HEADER-SPACE                            
             WRITE OUTPUT-RPT02 FROM WS-HEADER-SPACE                            
             WRITE OUTPUT-RPT02 FROM WS-HEADER-DTE-01                           
             WRITE OUTPUT-RPT02 FROM WS-HEADER-SPACE                            
             WRITE OUTPUT-RPT02 FROM WS-HEADER-LINE                             
             .                                                                  
                                                                                
      ******************************************************************        
       5000-NEXT-PAGE-PROC.                                                     
      ******************************************************************        
                                                                                
             IF WS-REC-CNT-1 >= WS-MAX-REC - 1                                  
                AND WS-EOF-FIN1 = 'N'                                           
                MOVE 1                       TO WS-HDR-RPT-NO                   
                WRITE OUTPUT-RPT01 FROM WS-HEADER-SPACE                         
                ADD 1                        TO WS-PAGE-NO-RPT01                
                MOVE WS-PAGE-NO-RPT01        TO WS-HDR-PAGENO                   
                MOVE 0                       TO WS-REC-CNT-1                    
                PERFORM 4100-WRITE-COM-HEADER-01-RPT01                          
                IF FIN2-JOB-ID = 'BP13DM27'                                     
                      PERFORM 4300-WRITE-HEADER-ELI-RPT01                       
                ELSE                                                            
                      PERFORM 4500-WRITE-HEADER-INELI-RPT01                     
                END-IF                                                          
                PERFORM 4700-WRITE-COM-HEADER-02-RPT01                          
             END-IF                                                             
             IF WS-REC-CNT-2 >= WS-MAX-REC - 1                                  
                AND WS-EOF-FIN1 = 'N'                                           
                MOVE 2                       TO WS-HDR-RPT-NO                   
                WRITE OUTPUT-RPT02 FROM WS-HEADER-SPACE                         
                ADD 1                        TO WS-PAGE-NO-RPT02                
                MOVE WS-PAGE-NO-RPT02        TO WS-HDR-PAGENO                   
                MOVE 0                       TO WS-REC-CNT-2                    
                PERFORM 4200-WRITE-COM-HEADER-01-RPT02                          
                IF FIN2-JOB-ID = 'BP13DM27'                                     
                      PERFORM 4400-WRITE-HEADER-ELI-RPT02                       
                ELSE                                                            
                      PERFORM 4600-WRITE-HEADER-INELI-RPT02                     
                END-IF                                                          
                PERFORM 4800-WRITE-COM-HEADER-02-RPT02                          
             END-IF                                                             
             .                                                                  
                                                                                
      ******************************************************************        
       9000-CLOSE-INPUT-FILES.                                                  
      ******************************************************************        
              CLOSE FIN1                                                        
              CLOSE FIN2                                                        
           .                                                                    
                                                                                
      ****************************************************************          
       9100-CANCEL-JOB.                                                         
      ****************************************************************          
                                                                                
           MOVE 111                          TO RETURN-CODE                     
           STOP RUN.                                                            
                                                                                
                                                                                
      ******************************************************************        
       9200-PRINT-CTL-SUMMARY.                                                  
      ******************************************************************        
             DISPLAY 'BP13CM27-I01: CONTROL SUMMARY'                            
             DISPLAY 'BP13FIN1 PROCESS CNT : ' WS-FIN1-CNT                      
             DISPLAY 'FIN1-BTO CNT         : ' WS-SNO-RPT01                     
             DISPLAY 'FIN1-SBF CNT         : ' WS-SNO-RPT02                     
             .                                                                  
                                                                                
            STOP RUN.                                                           
