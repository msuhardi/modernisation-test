       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C218.                                                 
      *AUTHOR.        LMS.                                                      
      *DATE-WRITTEN.  14/06/01.                                                 
      * ====================================================== *                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      * ====================================================== *                
      *    OBJECTIVE  :                                        *                
      *            TO CHECK BP13K240 FOR REVISED IN ALLOCATION *                
      *            DATE USING BP13K230.                        *                
      *                                                        *                
      *    INPUT    :  BP13F205                                *                
      *             :  BP13K230                                *                
      *             :  BP13K240                                *                
      *    OUTPUT   :  P13F240W                                *                
      *                P13F240D                                *                
      *--------------------------------------------------------*                
      *  CHG-NO BY    ON     DESCRIPTION                       *                
      *  -------------------------------                       *                
      * BP131017 LMS  010614  NEW PROGRAM.                     *                
      * BP132278 JF4  030328  CHGE BP13F230/F240 LRECL TO 450. *                
      * BP135572 ESA1 141121  TO CATER FOR NEIGH&CNTRCT IN     *                
      *                       BP13K240 KEY-FLD                 *                
      * BP135701 KG12 150331  TO USE K230-NUM-NEIGH &          *                
      *                       K230-NUM-CNTRCT TO READ BP13K240 *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205 ASSIGN TO BP13F205.                                  
           SELECT BP13K230 ASSIGN TO BP13K230.                                  
           SELECT P13F240D ASSIGN TO P13F240D.                                  
           SELECT P13F240W ASSIGN TO P13F240W.                                  
                                                                                
           SELECT BP13K240 ASSIGN TO BP13K240                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K240-KEY-FLD                               
                  FILE STATUS     IS BP13K240-STATUS.                           
                                                                                
           SELECT BP13L218 ASSIGN TO BP13L218.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F205                                                             
           BLOCK  CONTAINS  0  RECORDS                                          
           RECORD CONTAINS 80  CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F205.                                                           
                                                                                
       FD  BP13K230                                                             
           BLOCK  CONTAINS  0  RECORDS                                          
           RECORD CONTAINS 450 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13K230.                                                           
                                                                                
       FD  P13F240D                                                             
           BLOCK  CONTAINS  0  RECORDS                                          
           RECORD CONTAINS 450 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  F240D-HEADER-REC     PIC X(450).                                     
                                                                                
       FD  P13F240W                                                             
           BLOCK  CONTAINS  0  RECORDS                                          
           RECORD CONTAINS 450 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  F240W-HEADER-REC     PIC X(450).                                     
                                                                                
       FD  BP13K240                                                             
           RECORD CONTAINS 450 CHARACTERS.                                      
       COPY BP13K240.                                                           
                                                                                
       FD  BP13L218                                                             
           BLOCK CONTAINS  0   RECORDS                                          
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  PRINT-REC-1              PIC X(132).                                 
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  BP13K240-STATUS          PIC 9(2)  VALUE ZEROS.                      
       01  K230-READ-CNT            PIC 9(6)  VALUE ZEROS.                      
       01  K240-READ-CNT            PIC 9(6)  VALUE ZEROS.                      
       01  K240-UPDATE-CNT          PIC 9(6)  VALUE ZEROS.                      
       01  F240-WRITE-CNT           PIC 9(6)  VALUE ZEROS.                      
       01  F240-DELETE-CNT          PIC 9(6)  VALUE ZEROS.                      
       01  WS-YEAR                  PIC 9(2)  VALUE ZEROS.                      
       01  WS-MONTH                 PIC 9(2)  VALUE ZEROS.                      
       01  WS-PRINT-PAGE            PIC 9(3)  VALUE ZEROS.                      
       01  WS-K240-KEY-FLD-01       PIC X(52) VALUE SPACES.                     
       01  WS-K240-KEY-FLD-03       PIC X(52) VALUE SPACES.                     
                                                                                
       01  WS-CTRL-KEY.                                                         
           05 CTRL-OLD-DATE.                                                    
              10 WS-CC              PIC 9(02) VALUE ZEROS.                      
              10 WS-YY              PIC 9(02) VALUE ZEROS.                      
              10 WS-MM              PIC 9(02) VALUE ZEROS.                      
              10 WS-DD              PIC 9(02) VALUE ZEROS.                      
           05 CTRL-CDE-NT-ZONE      PIC X(03) VALUE SPACES.                     
                                                                                
       01  CTRL-NEW-DATE.                                                       
              05 WS-CC              PIC 9(02) VALUE ZEROS.                      
              05 WS-YY              PIC 9(02) VALUE ZEROS.                      
              05 WS-MM              PIC 9(02) VALUE ZEROS.                      
              05 WS-DD              PIC 9(02) VALUE ZEROS.                      
                                                                                
       01  EOF-FLAG.                                                            
           05 EOF-F205              PIC X     VALUE 'N'.                        
           05 EOF-K230              PIC X     VALUE 'N'.                        
           05 EOF-K240              PIC X     VALUE 'N'.                        
                                                                                
       01  WS-DATE.                                                             
           05 WS-YY                 PIC 9(2).                                   
           05 WS-MM                 PIC 9(2).                                   
           05 WS-DD                 PIC 9(2).                                   
                                                                                
       01  WS-EDITED-DATE.                                                      
           05 WS-DD                 PIC 9(2)  VALUE ZEROS.                      
           05 FILLER                PIC X     VALUE '/'.                        
           05 WS-MM                 PIC 9(2)  VALUE ZEROS.                      
           05 FILLER                PIC X     VALUE '/'.                        
           05 WS-CC                 PIC 9(2)  VALUE ZEROS.                      
           05 WS-YY                 PIC 9(2)  VALUE ZEROS.                      
                                                                                
       01  WS-TIME.                                                             
           05 WS-HH                 PIC X(2).                                   
           05 WS-MM                 PIC X(2).                                   
           05 WS-SS                 PIC X(2).                                   
                                                                                
       01  WS-EDITED-TIME.                                                      
           05 WS-SYS-TIME           PIC X(6).                                   
           05 WS-MICRO-SEC          PIC 9(1)  VALUE 0.                          
                                                                                
       01  L218-LINE1.                                                          
           05 FILLER                PIC X(5)  VALUE SPACES.                     
           05 FILLER                PIC X(7)  VALUE 'PAGE : '.                  
           05 PRINT-PAGE            PIC ZZ9.                                    
           05 FILLER                PIC X(28) VALUE SPACES.                     
           05 FILLER                PIC X(24) VALUE                             
             'SYSTEM OF COMMITMENT'.                                            
           05 FILLER                PIC X(36) VALUE SPACES.                     
           05 FILLER                PIC X(07) VALUE 'DATE : '.                  
           05 PRINT-DATE.                                                       
              10 WS-DD              PIC 9(2).                                   
              10 FILLER             PIC X     VALUE '/'.                        
              10 WS-MM              PIC 9(2).                                   
              10 FILLER             PIC X     VALUE '/'.                        
              10 WS-CC              PIC 9(2).                                   
              10 WS-YY              PIC 9(2).                                   
           05 FILLER                PIC X(5)  VALUE SPACES.                     
                                                                                
       01  L218-LINE2.                                                          
           05 FILLER                PIC X(35) VALUE SPACES.                     
           05 FILLER                PIC X(43) VALUE                             
             'UPDATING OF REVISED ALLOCATION DATE'.                             
           05 FILLER                PIC X(25) VALUE SPACES.                     
           05 FILLER                PIC X(22) VALUE                             
             'LISTING-ID  : BP13L218'.                                          
           05 FILLER                PIC X(7)  VALUE SPACES.                     
                                                                                
       01  L218-LINE3.                                                          
           05 FILLER                PIC X(08) VALUE SPACES.                     
           05 FILLER                PIC X(22) VALUE                             
             'OLD ALLOCATION DATE : '.                                          
           05 L-OLD-DATE            PIC X(10)  VALUE SPACES.                    
           05 FILLER                PIC X(27)                                   
              VALUE '     NEW ALLOCATION DATE : '.                              
           05 L-NEW-DATE            PIC X(10)  VALUE SPACES.                    
           05 FILLER                PIC X(5)  VALUE SPACES.                     
           05 FILLER                PIC X(15) VALUE 'CODE-NT-ZONE : '.          
           05 L-CDE-NT              PIC X(3)   VALUE SPACES.                    
           05 FILLER                PIC X(29) VALUE SPACES.                     
                                                                                
       01  L218-LINE20.                                                         
           05 FILLER                PIC X(24) VALUE SPACES.                     
           05 FILLER                PIC X(10) VALUE SPACES.                     
           05 FILLER                PIC X(45) VALUE                             
           'NO OF DETAIL INTV RECORDS READ             : '.                     
           05 L-K230-READ-CNT       PIC ZZZZZ9 VALUE ZEROS.                     
                                                                                
       01  L218-LINE22.                                                         
           05 FILLER                PIC X(24) VALUE SPACES.                     
           05 FILLER                PIC X(10) VALUE SPACES.                     
           05 FILLER                PIC X(45) VALUE                             
           'NO OF SUMMARY INTV RECORDS READ            : '.                     
           05 L-K240-READ-CNT       PIC ZZZZZ9 VALUE ZEROS.                     
                                                                                
       01  L218-LINE23.                                                         
           05 FILLER                PIC X(34) VALUE SPACES.                     
           05 FILLER                PIC X(45) VALUE                             
           'NO OF SUMMARY INTV RECORDS WRITTEN (DELETE): '.                     
           05 L-F240-DELETE-CNT     PIC ZZZZZ9 VALUE ZEROS.                     
                                                                                
       01  L218-LINE24.                                                         
           05 FILLER                PIC X(34) VALUE SPACES.                     
           05 FILLER                PIC X(45) VALUE                             
           'NO OF SUMMARY INTV RECORDS WRITTEN (ADD)   : '.                     
           05 L-F240-WRITE-CNT      PIC ZZZZZ9 VALUE ZEROS.                     
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN.                                                                
      *-------------------------------------------------------------            
           PERFORM 100-OPEN-FILES       THRU 100-EXIT.                          
           PERFORM 150-READ-BP13F205    THRU 150-EXIT.                          
           MOVE F205-NUM-NT-ZONE        TO CTRL-CDE-NT-ZONE.                    
           MOVE F205-DTE-ALLOCN         TO CTRL-OLD-DATE.                       
           MOVE F205-DTE-ALLOCN-NEW     TO CTRL-NEW-DATE.                       
                                                                                
           PERFORM 800-PRINT-CONTROL    THRU 800-EXIT.                          
           PERFORM 160-READ-BP13K230    THRU 160-EXIT.                          
           PERFORM 200-PROCESS-BP13K230 THRU 200-EXIT                           
                   UNTIL EOF-K230 = 'Y'.                                        
           MOVE K230-READ-CNT       TO L-K230-READ-CNT.                         
           WRITE PRINT-REC-1      FROM L218-LINE20    AFTER 2.                  
           MOVE K240-READ-CNT       TO L-K240-READ-CNT.                         
           MOVE F240-DELETE-CNT     TO L-F240-DELETE-CNT.                       
           MOVE F240-WRITE-CNT      TO L-F240-WRITE-CNT.                        
           WRITE PRINT-REC-1      FROM L218-LINE22    AFTER 2.                  
           WRITE PRINT-REC-1      FROM L218-LINE23    AFTER 2.                  
           WRITE PRINT-REC-1      FROM L218-LINE24    AFTER 2.                  
                                                                                
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F205                                                 
                       BP13K230                                                 
                       BP13K240                                                 
                OUTPUT BP13L218                                                 
                       P13F240D                                                 
                       P13F240W.                                                
                                                                                
           IF BP13K240-STATUS NOT = 00 AND 97                                   
               DISPLAY 'FILE BP13K240 OPEN ERROR'                               
               DISPLAY 'ERROR STATUS = ' BP13K240-STATUS                        
               PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                          
           END-IF.                                                              
                                                                                
           MOVE ZEROS TO  K230-READ-CNT                                         
                          K240-READ-CNT                                         
                          K240-UPDATE-CNT                                       
                          CTRL-OLD-DATE                                         
                          CTRL-NEW-DATE                                         
                          F240-WRITE-CNT                                        
                          F240-DELETE-CNT.                                      
           MOVE SPACES TO CTRL-CDE-NT-ZONE                                      
                          EOF-FLAG.                                             
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       150-READ-BP13F205.                                                       
      *-------------------------------------------------------------            
           READ BP13F205 AT END                                                 
                MOVE 'Y' TO EOF-F205                                            
                GO TO 150-EXIT                                                  
                END-READ.                                                       
                                                                                
       150-EXIT.                                                                
           EXIT.                                                                
                                                                                
       160-READ-BP13K230.                                                       
      *-------------------------------------------------------------            
           READ BP13K230 AT END                                                 
                MOVE 'Y' TO EOF-K230                                            
                GO TO 160-EXIT                                                  
                END-READ.                                                       
                                                                                
           ADD 1 TO K230-READ-CNT.                                              
                                                                                
       160-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-PROCESS-BP13K230.                                                    
      *-------------------------------------------------------------            
           PERFORM 250-READ-BP13K240 THRU 250-EXIT.                             
           PERFORM 160-READ-BP13K230    THRU 160-EXIT.                          
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       250-READ-BP13K240.                                                       
      *-------------------------------------------------------------            
                                                                                
      *-----------------------------------------------                          
      *  CHANGE ALLOCATION DATE OF HEADER RECORDS                               
      *-----------------------------------------------                          
           MOVE SPACES                 TO K240-KEY-FLD.                         
           MOVE K230-DETAIL-REC (5:52) TO K240-KEY-FLD.                         
           MOVE '01'                   TO K240-CDE-HEADER.                      
           MOVE K230-NUM-NEIGH         TO K240-NUM-NEIGH.                       
           MOVE K230-NUM-CNTRCT        TO K240-NUM-CNTRCT.                      
           MOVE SPACES                 TO K240-HEADER-REC (23:7).               
           MOVE CTRL-OLD-DATE          TO K240-DTE-ALLOC.                       
                                                                                
           IF K240-KEY-FLD NOT = WS-K240-KEY-FLD-01                             
              PERFORM 770-READ-K240-REC THRU 770-EXIT                           
              MOVE K240-KEY-FLD TO WS-K240-KEY-FLD-01                           
           END-IF.                                                              
                                                                                
      *-----------------------------------------------                          
      *  DELETE TRAILER RECORDS                                                 
      *-----------------------------------------------                          
                                                                                
           MOVE SPACES                 TO K240-KEY-FLD.                         
           MOVE K230-DETAIL-REC (5:52) TO K240-KEY-FLD.                         
           MOVE '03'                   TO K240-CDE-HEADER.                      
           MOVE K230-NUM-NEIGH         TO K240-NUM-NEIGH.                       
           MOVE K230-NUM-CNTRCT        TO K240-NUM-CNTRCT.                      
           MOVE SPACES                 TO K240-HEADER-REC (23:7).               
           MOVE CTRL-OLD-DATE          TO K240-DTE-ALLOC.                       
                                                                                
           IF K240-KEY-FLD NOT = WS-K240-KEY-FLD-03                             
              PERFORM 770-READ-K240-REC THRU 770-EXIT                           
              MOVE    K240-KEY-FLD      TO   WS-K240-KEY-FLD-03                 
           END-IF.                                                              
                                                                                
       250-EXIT.                                                                
           EXIT.                                                                
                                                                                
       770-READ-K240-REC.                                                       
      *-------------------------------------------------------------            
           READ BP13K240.                                                       
      *-----------------------------------------------------                    
      *   ROUTINE TO DELETE OLD RECORD (W/ OLD DATE ALLOC)                      
      *-----------------------------------------------------                    
           IF BP13K240-STATUS = 00                                              
              ADD 1 TO K240-READ-CNT                                            
              WRITE F240D-HEADER-REC FROM K240-HEADER-REC                       
              ADD 1 TO F240-DELETE-CNT                                          
      *-----------------------------------------------------                    
      *   ROUTINE TO SAVE UPDATED RECORD (W/ NEW DATE ALLOC)                    
      *-----------------------------------------------------                    
              MOVE CTRL-NEW-DATE TO K240-DTE-ALLOC                              
              WRITE F240W-HEADER-REC FROM K240-HEADER-REC                       
              ADD 1 TO F240-WRITE-CNT                                           
           ELSE                                                                 
              DISPLAY 'READ ERROR K240 : ' BP13K240-STATUS                      
              DISPLAY 'K240-KEY-FLD : ' K240-KEY-FLD                            
           END-IF.                                                              
                                                                                
       770-EXIT.                                                                
           EXIT.                                                                
                                                                                
       800-PRINT-CONTROL.                                                       
      *-------------------------------------------------------------            
           MOVE CTRL-CDE-NT-ZONE   TO L-CDE-NT.                                 
           ACCEPT WS-DATE        FROM DATE.                                     
           MOVE CORR WS-DATE       TO PRINT-DATE.                               
           IF WS-YY OF WS-DATE > 85                                             
              MOVE '19' TO WS-CC OF PRINT-DATE                                  
           ELSE                                                                 
              MOVE '20' TO WS-CC OF PRINT-DATE.                                 
                                                                                
           MOVE CORR CTRL-OLD-DATE TO WS-EDITED-DATE.                           
           MOVE WS-EDITED-DATE     TO L-OLD-DATE.                               
           MOVE CORR CTRL-NEW-DATE TO WS-EDITED-DATE.                           
           MOVE WS-EDITED-DATE     TO L-NEW-DATE.                               
           ADD  1                  TO WS-PRINT-PAGE.                            
           MOVE WS-PRINT-PAGE      TO PRINT-PAGE.                               
                                                                                
           WRITE PRINT-REC-1 FROM L218-LINE1 AFTER PAGE.                        
           WRITE PRINT-REC-1 FROM L218-LINE2.                                   
           WRITE PRINT-REC-1 FROM L218-LINE3    AFTER 5.                        
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
      *-------------------------------------------------------------            
            CLOSE      BP13F205                                                 
                       BP13K230                                                 
                       P13F240D                                                 
                       P13F240W                                                 
                       BP13K240                                                 
                       BP13L218.                                                
                                                                                
            IF   BP13K240-STATUS NOT = 00                                       
                 DISPLAY 'BP13K240-STATUS ' BP13K240-STATUS                     
            END-IF.                                                             
                                                                                
            STOP RUN.                                                           
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
