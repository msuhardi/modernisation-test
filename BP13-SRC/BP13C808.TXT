       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C808.                                                 
      *AUTHOR.        CHYEO.                                                    
      *DATE-WRITTEN.  31/08/94.                                                 
      * ====================================================== *                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      * ====================================================== *                
      *    OBJECTIVE   : 1. HA'S + TAG OCCUPIER AS LESSEE IN   *                
      *                     SMS AND A DIRECT PURCHASE FLAT     *                
      *                     IF SMS-SALES-TYPE =2A OR OUTRIGNT  *                
      *                     PRUCHASED ::                       *                
      *                     OUTPUT FOR RDB MOVING IN DATE CHECK*                
      *                     ELSE      ::                       *                
      *                     OUTPUT FOR 5YR COMPUTATION BASED ON*                
      *                     SALES ORDER DATE                   *                
      *                  2. HA'S + TAG OCCUPIERS WHO SOLD      *                
      *                     A HUDC FLAT OUTPUT REPORT AND      *                
      *                     OUTPUT REC FOR RESALE HISTORY CHECK*                
      *                  3. OUTPUT EX-LESSEE CASES FOR RESALE  *                
      *                     HISTORY CHECK                      *                
      *                                                        *                
      *    INPUT FILE  : BP13F558 - IF THE REGN-NO/REF-NO FD   *                
      *                             BP13.F500.SEQUENCE AS UPG  *                
      *                                                        *                
      *   OUTPUT FILE  :                                       *                
      *                  P13F558B - EX-LESSEE OR HUDC CASES    *                
      *                                                        *                
      *                  BP13F716 - EXISTING LESSEE IN SMS     *                
      *                             EXCLUDE TYPE 2A, OUTRIGHT  *                
      *                             TRANSFER CASE,USE SMS SOLD *                
      *                             DATE FOR 5-YR CHECK        *                
      *                                                        *                
      *                  P13F716A  - EXISTING LESSEE IN SMS WITH*               
      *                             SALES TYPE 2A, AND OUTRIGHT*                
      *                             TRANSFER CASE, USE MOBILITY*                
      *                             MOVE-IN DATE FOR 5-YR CHECK*                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY   DATE     DETAILS                          *                
      * ------  ---  ----     -------                          *                
      *BP130373 LSB  040997   CHECK RDB FOR TYPE 8 REGARDLESS  *                
      *                       DTE-TRFS-SMS                     *                
      *BP130393 LSB  111997   EXPAND FILE-STATUS-RSL TO 3 BYTES*                
      *BP130445 SSS  050298   EXPAND F558 RECL FROM 150 TO 400 *                
      *BP130542 LSB  110898   ADD HUDC-CODE = 'H2'             *                
      *BP130630 NOR  140199   EXPAND F715 RECL FROM 100 TO 250 *                
      *              070699   CHGE F715, F716A TO F716 350 BYTES*               
      *BP130715 LSB  200799   OUTPUT SALES-TY-SMS TO BP13F716  *                
      *BP130819 SRA  110400   OUTPUT REC WHEN SALES-TYPE-SMS = *                
      *                       7C,7J,8A,8B,8E TO P13F716A.      *                
      *BP130819 SRA  280400   ADD 8D                           *                
      *BP130897 AKT  040800   EXPAND F558 RECL FROM 400 TO 600 *                
      *BP130925 ANC  231000   RESALE-CD='SOM', NO 10-YR CHECK  *                
      *BP132059 ANC  090202   CHGE DEBARMENT-3 TO DEBARMENT-4  *                
      *BP132603 JF4  040305   CHGE 10 YR OCCUP TO 5 YR         *                
      *BP132674 SK   060405   CATER FOR SMS-SALE-TYPE = '8C'   *                
      *BP139518 EAA2 280223   TO INCLUDE 6 SALES TYPE CODE IN  *                
      *                       HOUSEHOLD DETERMINATION          *                
      *                       (8L/8M/8N/8R/8S/8T)              *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
           SELECT P13F558A ASSIGN TO P13F558A.                                  
           SELECT BP13F716 ASSIGN TO BP13F716.                                  
           SELECT P13F716A ASSIGN TO P13F716A.                                  
           SELECT BP13L808 ASSIGN TO BP13L808.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F558                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F558.                                                           
                                                                                
       FD  P13F558A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  P13F558A-REC                   PIC X(600).                           
                                                                                
       FD  BP13F716                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 350 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F716.                                                           
                                                                                
       FD  P13F716A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 350 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
                                                                                
       01  P13F716A-REC.                                                        
            05  F716A-NUM-REGN                PIC X(08).                        
            05  F716A-NUM-NRIC                PIC X(09).                        
            05  F716A-NUM-RDB-DP-OM           PIC X(01).                        
            05  F716A-NUM-HHSIZE              PIC X(02).                        
            05  F716A-NUM-RELATION-APPLN      PIC X(02).                        
            05  F716A-NUM-HA1-AGE             PIC X(03).                        
            05  F716A-NUM-CCC                 PIC X(02).                        
            05  F716A-DTE-REGN                PIC X(08).                        
            05  FILLER                       PIC X(04).                         
            05  F716A-CDE-NT-REQ              PIC X(03).                        
            05  F716A-NUM-PRIORITY-SCH        PIC X(03).                        
            05  F716A-CDE-FLAT-TYPE-REQ       PIC X(02).                        
            05  F716A-CDE-PARENT-CHILD        PIC X(01).                        
            05  F716A-CDE-SOLE-LESSEE-SMS     PIC X(01).                        
            05  F716A-NUM-CATEGORY            PIC X(01).                        
            05  F716A-NUM-HDB-REF.                                              
                10  F716A-NUM-SCH-ACCNT       PIC X(09).                        
                10  F716A-NUM-LSE-CDE         PIC X(02).                        
            05  FILLER                       PIC X(13).                         
            05  F716A-NUM-ROLE                PIC X(03).                        
            05  F716A-NUM-MTH-RESIDE          PIC X(04).                        
            05  F716A-CDE-FLAT-TYPE-RESIDE    PIC X(02).                        
            05  F716A-CDE-ED-CURR             PIC X(02).                        
            05  F716A-CDE-NT-RESIDE           PIC X(03).                        
            05  F716A-CDE-OCCUPIER            PIC X(01).                        
            05  F716A-NUM-SUB-TENANT          PIC X(01).                        
            05  F716A-CDE-FILE                PIC X(01).                        
            05  F716A-CDE-HOUSEHOLD           PIC X(01).                        
            05  F716A-RES-STATUS              PIC X(02).                        
            05  F716A-CDE-RESALE-STATUS       PIC X(03).                        
            05  FILLER                       PIC X(01).                         
            05  F716A-CDE-RELATIONSHIP        PIC X(02).                        
            05  F716A-DTE-BALLOT              PIC X(06).                        
            05  F716A-DTE-ACCEPTED            PIC X(08).                        
            05  F716A-DTE-MOVING-IN           PIC X(08).                        
            05  F716A-DTE-MOVING-OUT          PIC X(08).                        
            05  F716A-CDE-ED-GEOG             PIC X(02).                        
            05  F716A-NUM-SALES-TYPE-SMS      PIC X(02).                        
            05  FILLER                       PIC X(164).                        
            05  F716A-ADDRESS.                                                  
                10  F716A-NUM-BLK             PIC X(5).                         
                10  F716A-NME-STREET          PIC X(32).                        
                10  F716A-NUM-HSE.                                              
                    15  F716A-NUM-HSELVL      PIC X(2).                         
                    15  F716A-NUM-HSEMAIN     PIC X(4).                         
                    15  F716A-NUM-HSESUB      PIC X(3).                         
                10  F716A-NUM-POSTAL-DSTRCT   PIC X(6).                         
                                                                                
       FD  BP13L808                                                             
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  BP13L808-REC                   PIC X(132).                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-COUNTERS.                                                         
           05 WS-F558-READ                PIC 9(8)    VALUE ZEROS.              
           05 WS-OUTPUT-EX-LESSEE-REC-CNT PIC 9(8)    VALUE 0.                  
           05 WS-OUTPUT-FOR-RDB-REC-CNT   PIC 9(8)    VALUE 0.                  
           05 WS-OUTPUT-FOR-HUDC-REC-CNT  PIC 9(8)    VALUE 0.                  
           05 WS-OUTPUT-BP13F716          PIC 9(8)    VALUE 0.                  
           05 WS-NHH-CNT                  PIC 9(8)    VALUE 0.                  
           05 WS-SER-NO                   PIC 9(4)    VALUE 0.                  
           05 WS-LINE                     PIC 9(2)    VALUE 60.                 
           05 WS-PAGENO                   PIC 9(2)    VALUE 0.                  
                                                                                
       01  WS-CURR-DATE.                                                        
           05 WS-CC                       PIC X(2)    VALUE SPACES.             
           05 WS-YY                       PIC X(2)    VALUE SPACES.             
           05 WS-MM                       PIC X(2)    VALUE SPACES.             
           05 WS-DD                       PIC X(2)    VALUE SPACES.             
                                                                                
                                                                                
       01  WS-SOLD-DATE.                                                        
           05 WS-SOLD-CC                  PIC X(2)    VALUE SPACES.             
           05 WS-SOLD-YYMMDD              PIC X(6)    VALUE SPACES.             
                                                                                
       01  WS-PREV-REGN.                                                        
           05  WS-PREV-REF                PIC X(6)    VALUE SPACES.             
           05  FILLER                     PIC X(2)    VALUE SPACES.             
                                                                                
       01  WS-EOF-F558                    PIC X.                                
           88 EOF-F558                    VALUE 'Y'.                            
                                                                                
       01  L808-HEAD01.                                                         
           05  FILLER              PIC X(8)      VALUE 'BP13L808'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
                   'S Y S T E M   O F   C O M M I T M E N T'.                   
           05  FILLER              PIC X(34)     VALUE SPACES.                  
           05  FILLER              PIC X(5)      VALUE 'DATE'.                  
           05  L808-DATE           PIC X(10).                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
                                                                                
       01  L808-HEAD02.                                                         
           05  FILLER              PIC X(42)     VALUE SPACES.                  
           05  FILLER              PIC X(75)     VALUE                          
               'REPORT ON RESALE HUDC/TCP FOR 5 YR OCCUPATION CHECK '.          
           05  FILLER              PIC X(5)      VALUE 'PAGE'.                  
           05  L808-PAGENO         PIC ZZZZ9.                                   
                                                                                
       01  L808-HEAD03.                                                         
           05  FILLER              PIC X(20)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'SER NO '.               
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(11)     VALUE 'REF/REG NUM'.           
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(9)      VALUE 'NRIC NO'.               
           05  FILLER              PIC X(10)     VALUE SPACES.                  
                                                                                
       01  L808-DETAIL.                                                         
           05  FILLER                PIC X(22)     VALUE SPACES.                
           05  L808-SER-NO           PIC ZZZ9.                                  
           05  L808-SERIAL-NO REDEFINES L808-SER-NO PIC X(4).                   
           05  FILLER                PIC X(05)     VALUE SPACES.                
           05  L808-NUM-REGN         PIC X(8)      VALUE SPACES.                
           05  FILLER                PIC X(6)      VALUE SPACES.                
           05  L808-NUM-NRIC         PIC X(9)      VALUE SPACES.                
           05  FILLER                PIC X(25)     VALUE SPACES.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F558      THRU 200-EXIT.                            
           PERFORM 400-PROCESS-RTN    THRU 400-EXIT                             
             UNTIL EOF-F558.                                                    
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-INITIALIZATION.                                                      
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F558                                                 
                OUTPUT P13F558A                                                 
                       BP13F716                                                 
                       P13F716A                                                 
                       BP13L808.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CURR-DATE.                     
           STRING WS-DD '/' WS-MM '/' WS-CC WS-YY                               
                  DELIMITED BY SIZE INTO L808-DATE.                             
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-F558.                                                           
      *-------------------------------------------------------------            
           READ BP13F558 AT END                                                 
                MOVE HIGH-VALUE TO F558-NUM-REGN                                
                MOVE 'Y'        TO WS-EOF-F558                                  
                GO TO 200-EXIT.                                                 
                                                                                
           ADD  1 TO WS-F558-READ.                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      * TAKE SMS-SALES-ORDER-DATE OT SALE-EFF-DATE WHICH EVER IS                
      * SMALLER AS MOVE-IN-DATE FOR COMPUTATION OF 5-YR OCCP PERIOD             
      * FOR SALES TYPE 2 A OR OUTRIGHT TRANSFER TYPE 8, OUTPUT REC              
      * TO RDB TO GET MOVE-IN-DATE                                              
                                                                                
       400-PROCESS-RTN.                                                         
      *-------------------------------------------------------------            
           IF (F558-CDE-SALES-TYPE-SMS = '7C' OR '7J' OR '8A' OR '8B'           
                                      OR '8E' OR '7 ' OR '8L'                   
                                      OR '8M' OR '8N' OR '8R'                   
                                      OR '8S' OR '8T') AND                      
              (F558-NUM-DEBARMENT-4 = '62' OR '64' OR '66' OR 'GA' OR           
                                      'GB')                                     
              PERFORM 500-WRITE-OUTPUT-TO-RDB THRU 500-EXIT                     
           ELSE                                                                 
              IF F558-CDE-FILE-STATUS-SMS = SPACES                              
                 IF F558-CDE-FLAT-TYPE-SMS = SPACES                             
                    PERFORM 450-CHECK-RESALE-HISTORY THRU 450-EXIT              
                 ELSE                                                           
                    IF F558-CDE-SALES-TYPE-SMS = '2A' OR '8 '                   
                       PERFORM 500-WRITE-OUTPUT-TO-RDB THRU 500-EXIT            
                    ELSE                                                        
                       PERFORM 600-WRITE-OUTPUT-FROM-SMS                        
                                                     THRU 600-EXIT              
                    END-IF                                                      
                 END-IF                                                         
              ELSE                                                              
                 PERFORM 450-CHECK-RESALE-HISTORY THRU 450-EXIT                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-F558 THRU 200-EXIT.                                 
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       450-CHECK-RESALE-HISTORY.                                                
      *-------------------------------------------------------------            
           IF F558-NUM-HDB-REF-RSL = SPACES                                     
                ADD 1 TO WS-NHH-CNT                                             
           ELSE                                                                 
              IF F558-CDE-FILE-STATUS-RSL = 'H1 ' OR 'CL ' OR 'CN ' OR          
                                            'CP ' OR '2L ' OR '2N ' OR          
                                            '2P ' OR '2D ' OR 'OL ' OR          
                                            'ON ' OR 'OP ' OR 'H2 ' OR          
                                            'SOM'                               
                 ADD 1 TO WS-SER-NO                                             
                 MOVE WS-SER-NO TO L808-SER-NO                                  
                 MOVE F558-NUM-REGN  TO L808-NUM-REGN                           
                 PERFORM 810-PRINT-HUDC-TCP THRU 810-EXIT                       
                 PERFORM 850-WRITE-EX-LESSEE-HUDC THRU 850-EXIT                 
             ELSE                                                               
                 PERFORM 850-WRITE-EX-LESSEE-HUDC THRU 850-EXIT.                
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-WRITE-OUTPUT-TO-RDB.                                                 
      *-------------------------------------------------------------            
           MOVE SPACES TO P13F716A-REC.                                         
           MOVE F558-NUM-REGN       TO F716A-NUM-REGN.                          
           MOVE F558-NUM-NRIC       TO F716A-NUM-NRIC.                          
           MOVE F558-DTE-ISSUE-REGN TO F716A-DTE-REGN.                          
           MOVE F558-CDE-SALES-TYPE-SMS TO                                      
                F716A-NUM-SALES-TYPE-SMS                                        
           WRITE P13F716A-REC.                                                  
           ADD 1 TO WS-OUTPUT-FOR-RDB-REC-CNT.                                  
                                                                                
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       600-WRITE-OUTPUT-FROM-SMS.                                               
      *-------------------------------------------------------------            
           IF F558-NUM-HDB-REF-SMS NOT = SPACES                                 
              IF F558-DTE-FLAT-SOLD-SMS  = SPACES                               
                 ADD 1 TO WS-NHH-CNT                                            
                 DISPLAY 'BLANK SOLD-DTE IN SMS '                               
                               F558-NUM-REGN                                    
                    ' NRIC = ' F558-NUM-NRIC                                    
                    ' SMS-SCH = ' F558-NUM-HDB-REF-SMS                          
               ELSE                                                             
                 PERFORM 700-OUTPUT-BP13F716 THRU 700-EXIT                      
           ELSE                                                                 
             ADD 1 TO WS-NHH-CNT.                                               
                                                                                
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
       700-OUTPUT-BP13F716.                                                     
      *-------------------------------------------------------------            
           MOVE F558-DTE-FLAT-SOLD-SMS TO WS-SOLD-DATE.                         
                                                                                
           MOVE SPACES TO BP13F716-REC.                                         
           MOVE F558-NUM-REGN        TO F716-NUM-REGN.                          
           MOVE F558-NUM-NRIC        TO F716-NUM-NRIC.                          
           MOVE F558-CDE-SOLE-LESSEE TO F716-CDE-SOLE-LESSEE-SMS.               
           MOVE F558-NUM-HDB-REF-SMS TO F716-NUM-SCH-ACCNT.                     
           MOVE WS-SOLD-DATE         TO F716-DTE-ACCEPTED.                      
           MOVE WS-SOLD-DATE         TO F716-DTE-MOVING-IN.                     
           MOVE F558-DTE-ISSUE-REGN  TO F716-DTE-REGN.                          
           MOVE F558-CDE-FLAT-TYPE-SMS TO F716-CDE-FLAT-TYPE-RESIDE.            
           MOVE '1' TO  F716-CDE-OCCUPIER.                                      
           MOVE 'S' TO  F716-CDE-FILE.                                          
           MOVE F558-CDE-HOUSEHOLD TO F716-CDE-HOUSEHOLD.                       
           MOVE F558-CDE-RELATIONSHIP TO F716-CDE-RELATIONSHIP.                 
           WRITE  BP13F716-REC.                                                 
           ADD 1 TO WS-OUTPUT-BP13F716.                                         
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
       810-PRINT-HUDC-TCP.                                                      
           IF WS-LINE > 45                                                      
              MOVE 0 TO WS-LINE                                                 
              ADD 1 TO WS-PAGENO                                                
              MOVE WS-PAGENO TO L808-PAGENO                                     
              WRITE BP13L808-REC FROM L808-HEAD01 AFTER PAGE                    
              WRITE BP13L808-REC FROM L808-HEAD02 AFTER 1                       
              WRITE BP13L808-REC FROM L808-HEAD03 AFTER 2                       
              MOVE SPACES TO BP13L808-REC                                       
              WRITE BP13L808-REC AFTER 1.                                       
                                                                                
           MOVE F558-NUM-NRIC TO L808-NUM-NRIC.                                 
           WRITE BP13L808-REC FROM L808-DETAIL.                                 
           ADD 1 TO WS-OUTPUT-FOR-HUDC-REC-CNT.                                 
           MOVE SPACES TO L808-NUM-REGN.                                        
           MOVE SPACES TO L808-SERIAL-NO.                                       
           ADD 1 TO WS-LINE.                                                    
                                                                                
       810-EXIT.                                                                
           EXIT.                                                                
                                                                                
       850-WRITE-EX-LESSEE-HUDC.                                                
           MOVE BP13F558-REC TO P13F558A-REC.                                   
           WRITE P13F558A-REC.                                                  
           ADD 1 TO WS-OUTPUT-EX-LESSEE-REC-CNT.                                
                                                                                
       850-EXIT.                                                                
           EXIT.                                                                
                                                                                
       999-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
           CLOSE BP13F558                                                       
                 P13F558A                                                       
                 BP13F716                                                       
                 P13F716A                                                       
                 BP13L808.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13C808 CONTROL TOTALS'.                                   
           DISPLAY '---------------------------------------------'.             
           DISPLAY '1.  NO. OF F558 RECORDS READ    = ' WS-F558-READ.           
           DISPLAY ' '.                                                         
           DISPLAY '3.  NO. OF F558 RECORDS OUTPUT   '.                         
           DISPLAY '    FOR RDB EX-LESSEE/HUDC = '                              
                                           WS-OUTPUT-EX-LESSEE-REC-CNT.         
           DISPLAY '4.  NO. HUDC REPORT        = '                              
                                           WS-OUTPUT-FOR-HUDC-REC-CNT.          
           DISPLAY ' '.                                                         
           DISPLAY '5.  NO. OF F715 - NO RDB CHECK  = '                         
                                           WS-OUTPUT-BP13F716.                  
           DISPLAY '6.  NO. OF F716A FOR RDB CHECK   = '                        
                                           WS-OUTPUT-FOR-RDB-REC-CNT.           
           DISPLAY '7.  NO. OF BYPASS NHH RECORD    = '                         
                                           WS-NHH-CNT.                          
           DISPLAY ' '.                                                         
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
        999-EXIT.                                                               
            EXIT.                                                               
