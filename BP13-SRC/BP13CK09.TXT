       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CK09.                                                 
      *AUTHOR.        YCH.                                                      
      *DATE-WRITTEN.  05/11/2008.                                               
      *========================================================*                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *      1.  USE NRIC IN REQUEST FILE                      *                
      *          MATCH WITH PPOS, SMS AND RESALE               *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO    BY  DATE    DESCRIPTION                      *                
      * -------  ---  ------  -----------                      *                
      * BP133484 YCH2 061108  PRINT EXCEPTIONAL REPORT BEFORE  *                
      *                       BALLOT COMMENCE                  *                
      * BP135552 SMR2 121114  INCREASE HOUSEHOLD INCOME        *                
      * BP136469 SMR2 031016  DROP CASES WHERE EX-PPO HAS BEEN *                
      *                       DISPOSED MORE THAN 30 MONTHS AGO *                
      * BP137310 JF7  030518  2ND FLAT LESSEE - EXCL NTIMER 'Y'*                
      *                       DEB - INCLUDE DEB CODE 'E5'      *                
      * BP138038 AM25 041119  MATCH 9 BYTES IN SMS & RSL HDBREF*                
      * BP139518 EAA2 26042023 TO INCLUDE 6 SALES TYPE CODE    *                
      *                          (8L/8M/8N/8R/8S/8T) 8E        *                
      *========================================================*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F825 ASSIGN TO BP13F825.                                  
                                                                                
           SELECT BP13K730 ASSIGN TO BP13K730                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K730-NUM-REGN                              
                  FILE STATUS     IS WS-BP13K730-STATUS.                        
                                                                                
      ** USING THE DDNAME BUT DIFFERENT LAYOUT                                  
      ** TO STORE INFORMATION ON HOUSEHOLD INCOME OF THE EXERCISE               
      ** ONLY FOR EXCEPTIONAL HH INCOME                                         
                                                                                
           SELECT BP13K813 ASSIGN TO BP13K813                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K813-KEY-FLD                               
                  FILE STATUS     IS WS-BP13K813-STATUS.                        
                                                                                
           SELECT P04K010  ASSIGN TO P04K010                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K010-PRIME-KEY                             
                  FILE STATUS     IS WS-P04K010-STATUS.                         
                                                                                
           SELECT BR10K010 ASSIGN TO BR10K010                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K010-KEY-FLD                               
                  FILE STATUS     IS WS-BR10K010-STATUS.                        
                                                                                
           SELECT BR10K080 ASSIGN TO BR10K080                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K080-KEY-FLD                               
                  FILE STATUS     IS WS-BR10K080-STATUS.                        
                                                                                
           SELECT AB03K080 ASSIGN TO AB03K080                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K080-KEY                                   
                  FILE STATUS     IS WS-AB03K080-STATUS.                        
                                                                                
           SELECT AB03K030 ASSIGN TO AB03K030                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K030-NUM-HDB-REF                           
                  FILE STATUS     IS WS-AB03K030-STATUS.                        
                                                                                
           SELECT BB24K500  ASSIGN TO BB24K500                                  
                  ORGANIZATION     IS INDEXED                                   
                  ACCESS MODE      IS RANDOM                                    
                  RECORD KEY       IS K500-KEY-FLD                              
                  FILE STATUS      IS WS-BB24K500-STATUS.                       
                                                                                
           SELECT BB24K510  ASSIGN TO BB24K510                                  
                  ORGANIZATION     IS INDEXED                                   
                  ACCESS MODE      IS DYNAMIC                                   
                  RECORD KEY       IS K510-KEY-FLD                              
                  FILE STATUS      IS WS-BB24K510-STATUS.                       
                                                                                
           SELECT BP13LK09 ASSIGN TO BP13LK09.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F825                                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F825.                                                           
                                                                                
       FD   BP13K813.                                                           
       COPY BP13K813.                                                           
                                                                                
       FD   BP13K730.                                                           
       COPY BP13K730.                                                           
                                                                                
       FD   P04K010.                                                            
       COPY P04K010.                                                            
                                                                                
       FD   BR10K010.                                                           
       COPY BR10K010.                                                           
                                                                                
       FD   BR10K080.                                                           
       COPY BR10K080.                                                           
                                                                                
       FD   AB03K080.                                                           
       COPY AB03K080.                                                           
                                                                                
       FD   AB03K030.                                                           
       COPY AB03K030.                                                           
                                                                                
       FD   BB24K500.                                                           
       COPY BB24K500.                                                           
                                                                                
       FD   BB24K510.                                                           
       COPY BB24K510.                                                           
                                                                                
       FD   BP13LK09                                                            
            RECORD CONTAINS 133 CHARACTERS                                      
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
                                                                                
       01   BP13LK09-REC            PIC X(133).                                 
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FILE-STATUS.                                                      
           05  WS-BB24K500-STATUS   PIC 9(2)    VALUE ZEROES.                   
           05  WS-BB24K510-STATUS   PIC 9(2)    VALUE ZEROES.                   
           05  WS-AB03K030-STATUS   PIC 9(2)    VALUE ZEROES.                   
           05  WS-AB03K080-STATUS   PIC 9(2)    VALUE ZEROES.                   
           05  WS-BR10K010-STATUS   PIC 9(2)    VALUE ZEROES.                   
           05  WS-BR10K080-STATUS   PIC 9(2)    VALUE ZEROES.                   
           05  WS-BP13K730-STATUS   PIC 9(2)    VALUE ZEROES.                   
           05  WS-BP13K813-STATUS   PIC 9(2)    VALUE ZEROES.                   
           05  WS-P04K010-STATUS    PIC 9(2)    VALUE ZEROES.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F825-READ          PIC 9(8)    VALUE ZEROES.                   
           05 WS-F825-BYPASS        PIC 9(8)    VALUE ZEROES.                   
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-DEB-FND           PIC X(1)     VALUE SPACES.                  
           05  WS-SMS-FND           PIC X(1)     VALUE SPACES.                  
           05  WS-SMSRSL-FND        PIC X(1)     VALUE SPACES.                  
           05  WS-RSL-FND           PIC X(1)     VALUE SPACES.                  
           05  WS-PPO-FND           PIC X(1)     VALUE SPACES.                  
           05  WS-PPOH-FND          PIC X(1)     VALUE SPACES.                  
           05  WS-VALID-PPOH        PIC X(1)     VALUE SPACES.                  
           05  WS-INC-MAX           PIC X(1)     VALUE SPACES.                  
                                                                                
       01  WS-WORKING-FIELDS.                                                   
           05  WS-NUM-REGN          PIC X(8)     VALUE SPACES.                  
           05  WS-SMS-HDBREF        PIC X(11)    VALUE SPACES.                  
           05  WS-DATE              PIC X(8)     VALUE SPACES.                  
           05  WS-HH-INC            PIC 9(6)     VALUE ZEROES.                  
           05  WS-SNO               PIC 9(5)     VALUE ZEROES.                  
           05  WS-PAGE              PIC 9(5)     VALUE ZEROES.                  
           05  WS-LINE              PIC 9(2)     VALUE 66.                      
           05  WS-DTE-DISP          PIC X(8)     VALUE SPACES.                  
           05  WS-DTE-DISP-NUM REDEFINES WS-DTE-DISP                            
                                    PIC 9(8).                                   
           05  WS-DTE-REQ           PIC X(8)     VALUE SPACES.                  
           05  WS-DTE-REQ-R    REDEFINES WS-DTE-REQ.                            
               10  WS-DTE-REQ-NUM.                                              
                   15  WS-DTE-REQ-CCYY   PIC 9(4).                              
                   15  WS-DTE-REQ-MM     PIC 9(2).                              
                   15  WS-DTE-REQ-DD     PIC 9(2).                              
           05  WS-NUM-NTIMER        PIC X(1)     VALUE SPACES.                  
                                                                                
       01  WS-EOF-SWITCHES.                                                     
           05  EOF-BP13F825         PIC X        VALUE 'N'.                     
           05  EOF-P04K010          PIC X        VALUE 'N'.                     
           05  EOF-AB03K080         PIC X        VALUE 'N'.                     
           05  EOF-AB03K030         PIC X        VALUE 'N'.                     
           05  EOF-BR10K010         PIC X        VALUE 'N'.                     
           05  EOF-BR10K080         PIC X        VALUE 'N'.                     
           05  EOF-BB24K500         PIC X        VALUE 'N'.                     
           05  EOF-BB24K510         PIC X        VALUE 'N'.                     
                                                                                
       01  WS-K010-REASON           PIC X(2)    VALUE SPACES.                   
           88 VALID-REASON          VALUE 'PA' 'PB' 'PC' 'PD'                   
                                          'PE' 'PF'                             
                                          'EC' 'ES' 'EU'                        
                                          'CG' 'E5'.                            
                                                                                
       01  WS-SMS-SALES-TYPE        PIC X(2)    VALUE SPACES.                   
           88 INVALID-SALES-TYPE    VALUE '  ' '12' '7 ' '7C' '7J'              
                                          '8A' '8B'                             
                                          '8E' '8L' '8M' '8N' '8R'              
                                          '8S' '8T'                             
                                          '9A' '9B'.                            
                                                                                
       01  WS-RSL-STATUS            PIC X(3)    VALUE SPACES.                   
           88 INVALID-RSL-STATUS    VALUE 'NA ' 'OM ' 'OMS' 'CM '               
                                          'LD ' 'PD ' 'SA ' 'MOM'               
                                          'MNA' 'GSR' 'NG '.                    
                                                                                
      *============================================================*            
      *         BP13LK09 -   R E P O R T    L A Y O U T            *            
      *============================================================*            
                                                                                
       01  LK09-HDG1.                                                           
           05  FILLER              PIC X(08)     VALUE 'BP13LK09'.              
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(25)     VALUE SPACES.                  
           05  FILLER              PIC X(41)     VALUE                          
               'S Y S T E M    O F    C O M M I T M E N T'.                     
           05  FILLER              PIC X(12)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE :'.                
           05  LK09-RUN-DATE       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LK09-PAGE-NO        PIC ZZZZ9 .                                  
                                                                                
       01  LK09-HDG2.                                                           
           05  FILLER              PIC X(42)     VALUE SPACES.                  
           05  FILLER              PIC X(46)     VALUE                          
           'EXCEPTIONAL REPORT BEFORE BALLOT COMMENCE FOR '.                    
           05  LK09-ALLO-CAT       PIC X(3)      VALUE SPACES.                  
                                                                                
       01  LK09-HDG3.                                                           
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE ' S/N '.                 
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE 'NRIC'.                  
           05  FILLER              PIC X(03)     VALUE ' | '.                   
           05  FILLER              PIC X(03)     VALUE 'DEB'.                   
           05  FILLER              PIC X(03)     VALUE ' | '.                   
           05  FILLER              PIC X(03)     VALUE '2ND'.                   
           05  FILLER              PIC X(12)     VALUE ' FLAT LESSEE'.          
           05  FILLER              PIC X(03)     VALUE ' | '.                   
           05  FILLER              PIC X(03)     VALUE 'PPO'.                   
           05  FILLER              PIC X(03)     VALUE ' | '.                   
           05  FILLER              PIC X(08)     VALUE 'PPO HIST'.              
           05  FILLER              PIC X(03)     VALUE ' | '.                   
           05  FILLER              PIC X(10)     VALUE 'HH INC>MAX'.            
           05  FILLER              PIC X(03)     VALUE ' | '.                   
                                                                                
       01  LK09-HDG4.                                                           
           05  FILLER              PIC X(130)    VALUE ALL '-'.                 
                                                                                
       01  LK09-DETAIL.                                                         
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LK09-SNO            PIC ZZZZ9.                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LK09-REGNO          PIC X(08).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE ' | '.                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  LK09-DEB            PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE ' | '.                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  LK09-SMS            PIC X(1)      VALUE SPACES.                  
           05  FILLER              PIC X(6)      VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE ' | '.                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  LK09-PPO            PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE ' | '.                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  LK09-PPOH           PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE ' | '.                   
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LK09-HH-GEMAX       PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE ' | '.                   
                                                                                
       PROCEDURE DIVISION.                                                      
      *------------------------------------------------------------             
       0000-MAIN.                                                               
      *------------------------------------------------------------             
                                                                                
           OPEN INPUT  BB24K500                                                 
                       BB24K510                                                 
                       BP13F825                                                 
                       BR10K010                                                 
                       BR10K080                                                 
                       AB03K080                                                 
                       AB03K030                                                 
                       P04K010                                                  
                       BP13K730                                                 
                       BP13K813                                                 
                OUTPUT BP13LK09.                                                
                                                                                
           IF WS-BP13K813-STATUS NOT = 0 AND 97                                 
              DISPLAY 'FILE BP13K813 OPEN ERROR'  WS-BP13K813-STATUS            
              MOVE WS-BP13K813-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-BP13K730-STATUS NOT = 0 AND 97                                 
              DISPLAY 'FILE BP13K730 OPEN ERROR'  WS-BP13K730-STATUS            
              MOVE WS-BP13K730-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-BR10K010-STATUS NOT = 0 AND 97                                 
              DISPLAY 'FILE BR10K010 OPEN ERROR'  WS-BR10K010-STATUS            
              MOVE WS-BR10K010-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-BR10K080-STATUS NOT = 0 AND 97                                 
              DISPLAY 'FILE BR10K080 OPEN ERROR'  WS-BR10K080-STATUS            
              MOVE WS-BR10K080-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-AB03K030-STATUS NOT = 0 AND 97                                 
              DISPLAY 'OPEN ERROR AB03K030 ' WS-AB03K030-STATUS                 
              MOVE WS-AB03K030-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-AB03K080-STATUS NOT = 0 AND 97                                 
              DISPLAY 'OPEN ERROR AB03K080 ' WS-AB03K080-STATUS                 
              MOVE WS-AB03K080-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-BB24K500-STATUS NOT = ZEROS AND 97                             
              DISPLAY 'FILE BB24K500 OPEN ERROR' WS-BB24K500-STATUS             
              MOVE WS-BB24K500-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-BB24K510-STATUS NOT = ZEROS AND 97                             
              DISPLAY 'FILE BB24K510 OPEN ERROR' WS-BB24K510-STATUS             
              MOVE WS-BB24K510-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-P04K010-STATUS NOT = ZEROS AND 97                              
              DISPLAY 'FILE P04K010  OPEN ERROR' WS-P04K010-STATUS              
              MOVE WS-P04K010-STATUS TO RETURN-CODE                             
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO WS-DATE.                         
                                                                                
           STRING WS-DATE(7:2) '/'                                              
                  WS-DATE(5:2) '/'                                              
                  WS-DATE(1:4)                                                  
                  DELIMITED BY SIZE                                             
             INTO LK09-RUN-DATE                                                 
                                                                                
           PERFORM 1000-READ-F825-REC THRU 1000-EXIT.                           
           PERFORM 2000-PROCESS-REC   THRU 2000-EXIT                            
                   UNTIL EOF-BP13F825 = 'Y'.                                    
           PERFORM 9000-CLOSE-ROUTINE.                                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       1000-READ-F825-REC.                                                      
      *------------------------------------------------------------             
                                                                                
           READ BP13F825 AT END MOVE 'Y' TO EOF-BP13F825                        
                GO TO 1000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F825-READ.                                               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       2000-PROCESS-REC.                                                        
      *------------------------------------------------------------             
           MOVE ZEROES        TO WS-HH-INC.                                     
           MOVE SPACES        TO WS-SMS-FND                                     
                                 WS-RSL-FND                                     
                                 WS-SMSRSL-FND                                  
                                 WS-DEB-FND                                     
                                 WS-PPO-FND                                     
                                 WS-PPOH-FND                                    
                                 WS-VALID-PPOH                                  
                                 WS-INC-MAX.                                    
                                                                                
           MOVE F825-NUM-REGN TO WS-NUM-REGN.                                   
                                                                                
           IF F825-NUM-NRIC = 'X88888888' OR 'X77777777'                        
              ADD 1 TO WS-F825-BYPASS                                           
              PERFORM 1000-READ-F825-REC  THRU 1000-EXIT                        
              GO TO 2000-EXIT                                                   
           END-IF                                                               
                                                                                
           PERFORM 2100-PROCESS-CASE THRU 2100-EXIT                             
             UNTIL F825-NUM-REGN NOT = WS-NUM-REGN                              
                OR EOF-BP13F825 = 'Y'.                                          
                                                                                
           PERFORM 2200-READ-BP13K730 THRU 2200-EXIT.                           
                                                                                
           IF WS-PPOH-FND = 'Y'                                                 
              PERFORM 2300-COMPUTE-REQ THRU 2300-EXIT                           
              IF (WS-DTE-DISP = ZEROES OR SPACES OR LOW-VALUES) OR              
                 (WS-DTE-REQ = ZEROES OR SPACES OR LOW-VALUES) OR               
                 (WS-DTE-DISP-NUM >= WS-DTE-REQ-NUM)                            
                 MOVE 'Y' TO WS-VALID-PPOH                                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF K730-CDE-FLAT-TYPE NOT = '1A'                                     
              IF WS-DEB-FND = 'Y' OR                                            
                 WS-SMSRSL-FND = 'Y' OR                                         
                 WS-PPO-FND = 'Y' OR                                            
                (WS-PPOH-FND = 'Y' AND WS-VALID-PPOH = 'Y') OR                  
                 WS-INC-MAX = 'Y'                                               
                 PERFORM 8000-PRINT-DETAIL THRU 8000-EXIT                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       2100-PROCESS-CASE.                                                       
      *------------------------------------------------------------             
                                                                                
           MOVE SPACES TO WS-SMS-FND WS-RSL-FND.                                
                                                                                
           IF F825-AMT-INCOME NOT NUMERIC                                       
              MOVE ZEROES TO F825-AMT-INCOME                                    
           END-IF.                                                              
                                                                                
           COMPUTE WS-HH-INC = WS-HH-INC + F825-AMT-INCOME.                     
                                                                                
           IF WS-DEB-FND = SPACES                                               
              PERFORM 3000-CHECK-P04K010  THRU 3000-EXIT                        
           END-IF.                                                              
                                                                                
           IF WS-SMSRSL-FND = SPACES                                            
              PERFORM 4000-CHECK-AB03K080 THRU 4000-EXIT                        
              PERFORM 5000-CHECK-BB24K510 THRU 5000-EXIT                        
              IF (WS-SMS-FND = 'Y' AND WS-RSL-FND = 'Y')                        
                 MOVE 'Y' TO WS-SMSRSL-FND                                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-PPO-FND = SPACES                                               
              PERFORM 6000-CHECK-BR10K010 THRU 6000-EXIT                        
           END-IF.                                                              
                                                                                
           IF WS-PPOH-FND = SPACES                                              
              PERFORM 7000-CHECK-BR10K080 THRU 7000-EXIT                        
           END-IF.                                                              
                                                                                
           PERFORM 1000-READ-F825-REC  THRU 1000-EXIT.                          
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       2200-READ-BP13K730.                                                      
      *------------------------------------------------------------             
            MOVE SPACES           TO WS-DTE-REQ                                 
                                     WS-NUM-NTIMER.                             
            MOVE WS-NUM-REGN      TO K730-NUM-REGN.                             
                                                                                
            READ BP13K730.                                                      
                                                                                
            EVALUATE WS-BP13K730-STATUS                                         
              WHEN 00                                                           
                   MOVE K730-NUM-ALLO-CAT TO LK09-ALLO-CAT                      
                   MOVE K730-DTE-REQUEST  TO WS-DTE-REQ                         
                   MOVE K730-NUM-NTIMER   TO WS-NUM-NTIMER                      
                   PERFORM 2210-CHECK-HHINC THRU 2210-EXIT                      
                                                                                
              WHEN 10                                                           
              WHEN 23                                                           
                   CONTINUE                                                     
              WHEN OTHER                                                        
                   DISPLAY 'READ BP13K730 ERROR ' WS-BP13K730-STATUS            
                   MOVE WS-BP13K730-STATUS          TO RETURN-CODE              
                   PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       2210-CHECK-HHINC.                                                        
      *------------------------------------------------------------             
            MOVE SPACES             TO WS-INC-MAX                               
                                       BP13K813-REC.                            
            INITIALIZE                 BP13K813-REC.                            
            MOVE K730-CDE-NT1       TO K813-NUM-ZONE.                           
            MOVE K730-CDE-FLAT-TYPE TO K813-NUM-FLAT-TYPE.                      
            MOVE K730-DTE-BALLOT    TO K813-DTE-BALLOT.                         
                                                                                
            READ BP13K813.                                                      
                                                                                
            EVALUATE WS-BP13K813-STATUS                                         
              WHEN 00                                                           
                   IF WS-HH-INC > K813-AMT-INCOME-CEILING                       
                      MOVE 'Y' TO WS-INC-MAX                                    
                   END-IF                                                       
              WHEN 10                                                           
              WHEN 23                                                           
                   CONTINUE                                                     
              WHEN OTHER                                                        
                   DISPLAY 'READ BP13K813 ERROR ' WS-BP13K813-STATUS            
                   MOVE WS-BP13K813-STATUS          TO RETURN-CODE              
                   PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       2210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2300-COMPUTE-REQ.                                                        
      *--------------------------------------------------------*                
           IF WS-DTE-REQ = ZEROES OR SPACES OR LOW-VALUES                       
              GO TO 2300-EXIT                                                   
           END-IF.                                                              
                                                                                
      * --------------------------------                                        
      * MINUS 30 MONTHS (2 YRS & 6 MOS)                                         
      * --------------------------------                                        
           COMPUTE WS-DTE-REQ-CCYY = WS-DTE-REQ-CCYY - 2.                       
                                                                                
           IF WS-DTE-REQ-MM > 6                                                 
              COMPUTE WS-DTE-REQ-MM = WS-DTE-REQ-MM - 6                         
           ELSE                                                                 
              IF WS-DTE-REQ-MM = 6                                              
                 COMPUTE WS-DTE-REQ-CCYY = WS-DTE-REQ-CCYY - 1                  
                 MOVE 12 TO WS-DTE-REQ-MM                                       
              ELSE                                                              
                 IF WS-DTE-REQ-MM < 6                                           
                    COMPUTE WS-DTE-REQ-CCYY = WS-DTE-REQ-CCYY - 1               
                    COMPUTE WS-DTE-REQ-MM = WS-DTE-REQ-MM + 12 - 6              
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       3000-CHECK-P04K010.                                                      
      *------------------------------------------------------------             
            MOVE SPACES           TO WS-DEB-FND.                                
            MOVE SPACES           TO K010-PRIME-KEY.                            
            MOVE F825-NUM-NRIC    TO K010-NUM-UINFIN.                           
                                                                                
            START P04K010 KEY >= K010-NUM-UINFIN.                               
                                                                                
            EVALUATE WS-P04K010-STATUS                                          
              WHEN 00                                                           
                   PERFORM 3100-READNEXT-P04K010    THRU 3100-EXIT              
                     UNTIL K010-NUM-UINFIN NOT = F825-NUM-NRIC                  
                       OR   WS-DEB-FND  = 'Y'                                   
                       OR   EOF-P04K010 = 'Y'                                   
              WHEN 23                                                           
                   CONTINUE                                                     
              WHEN OTHER                                                        
                   DISPLAY 'START P04K010 ERROR ' WS-P04K010-STATUS             
                   MOVE WS-P04K010-STATUS           TO RETURN-CODE              
                   PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       3100-READNEXT-P04K010.                                                   
      *------------------------------------------------------------             
            READ P04K010 NEXT.                                                  
                                                                                
            EVALUATE WS-P04K010-STATUS                                          
              WHEN 00                                                           
                   IF K010-NUM-UINFIN  = F825-NUM-NRIC                          
                      MOVE K010-REASON-CODE TO WS-K010-REASON                   
                      IF VALID-REASON                                           
                         MOVE 'Y' TO WS-DEB-FND                                 
                      END-IF                                                    
                   END-IF                                                       
              WHEN 23                                                           
                   CONTINUE                                                     
              WHEN 10                                                           
                   MOVE 'Y' TO EOF-P04K010                                      
              WHEN OTHER                                                        
                   DISPLAY 'READNEXT P04K010 ERROR ' WS-P04K010-STATUS          
                   MOVE WS-P04K010-STATUS           TO RETURN-CODE              
                   PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       4000-CHECK-AB03K080.                                                     
      *------------------------------------------------------------             
           MOVE SPACES               TO WS-SMS-FND.                             
           MOVE SPACES               TO K080-KEY.                               
           MOVE F825-NUM-NRIC        TO K080-NUM-UIN OF AB03K080.               
                                                                                
           START AB03K080 KEY >= K080-KEY.                                      
                                                                                
            EVALUATE WS-AB03K080-STATUS                                         
              WHEN 00                                                           
                   PERFORM 4100-READNEXT-AB03K080   THRU 4100-EXIT              
                     UNTIL K080-NUM-UIN OF AB03K080-REC NOT =                   
                                           F825-NUM-NRIC                        
                      OR   WS-SMS-FND = 'Y'                                     
                      OR   EOF-AB03K080 = 'Y'                                   
              WHEN 23                                                           
                   CONTINUE                                                     
              WHEN OTHER                                                        
                   DISPLAY 'START AB03K080 ERROR ' WS-AB03K080-STATUS           
                   MOVE WS-AB03K080-STATUS          TO RETURN-CODE              
                   PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       4100-READNEXT-AB03K080.                                                  
      *------------------------------------------------------------             
            READ AB03K080 NEXT.                                                 
                                                                                
            EVALUATE WS-AB03K080-STATUS                                         
              WHEN 00                                                           
                   IF K080-NUM-UIN OF AB03K080 = F825-NUM-NRIC                  
                      PERFORM 4200-READ-AB03K030 THRU 4200-EXIT                 
                   END-IF                                                       
              WHEN 10                                                           
                   MOVE 'Y' TO EOF-AB03K080                                     
              WHEN 23                                                           
                   CONTINUE                                                     
              WHEN OTHER                                                        
                   DISPLAY 'START AB03K080 ERROR ' WS-AB03K080-STATUS           
                   MOVE WS-AB03K080-STATUS          TO RETURN-CODE              
                   PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       4200-READ-AB03K030.                                                      
      *------------------------------------------------------------             
                                                                                
           MOVE SPACES           TO WS-SMS-HDBREF.                              
           MOVE K080-NUM-HDB-REF TO K030-NUM-HDB-REF.                           
           READ AB03K030.                                                       
                                                                                
           EVALUATE WS-AB03K030-STATUS                                          
              WHEN 00                                                           
                   IF K030-CDE-ACCT-STAT = 'C'                                  
                      MOVE K030-CDE-SALES-TYP TO WS-SMS-SALES-TYPE              
                      IF NOT INVALID-SALES-TYPE                                 
                         MOVE K030-NUM-HDB-REF TO WS-SMS-HDBREF                 
                         MOVE 'Y' TO WS-SMS-FND                                 
                      END-IF                                                    
                   END-IF                                                       
              WHEN 10                                                           
              WHEN 23                                                           
                   CONTINUE                                                     
              WHEN OTHER                                                        
                   DISPLAY 'START AB03K030 ERROR ' WS-AB03K030-STATUS           
                   MOVE WS-AB03K030-STATUS          TO RETURN-CODE              
                   PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       5000-CHECK-BB24K510.                                                     
      *------------------------------------------------------------             
           MOVE SPACES                  TO WS-RSL-FND.                          
           MOVE SPACES                  TO K510-KEY-FLD.                        
           MOVE F825-NUM-NRIC           TO K510-NUM-UIN.                        
                                                                                
           START BB24K510 KEY >=  K510-KEY-FLD.                                 
                                                                                
           EVALUATE WS-BB24K510-STATUS                                          
              WHEN 00                                                           
                   PERFORM 5100-READNEXT-BB24K510   THRU 5100-EXIT              
                     UNTIL K510-NUM-UIN NOT = F825-NUM-NRIC                     
                      OR   WS-RSL-FND = 'Y'                                     
                      OR   EOF-BB24K510 = 'Y'                                   
              WHEN 23                                                           
                   CONTINUE                                                     
              WHEN OTHER                                                        
                   DISPLAY 'START BB24K510 ERROR ' WS-BB24K510-STATUS           
                   MOVE WS-BB24K510-STATUS          TO RETURN-CODE              
                   PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       5100-READNEXT-BB24K510.                                                  
      *------------------------------------------------------------             
            READ BB24K510 NEXT.                                                 
                                                                                
            EVALUATE WS-BB24K510-STATUS                                         
              WHEN 00                                                           
                   IF (K510-NUM-UIN = F825-NUM-NRIC) AND                        
                      (K510-CDE-STATUS = 'P' OR                                 
                       K510-CDE-STATUS = 'S')                                   
                      PERFORM 5200-READ-BB24K500 THRU 5200-EXIT                 
                   END-IF                                                       
              WHEN 10                                                           
                   MOVE 'Y' TO EOF-BB24K510                                     
              WHEN 23                                                           
                   CONTINUE                                                     
              WHEN OTHER                                                        
                   DISPLAY 'START BB24K510 ERROR ' WS-BB24K510-STATUS           
                   MOVE WS-BB24K510-STATUS          TO RETURN-CODE              
                   PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       5200-READ-BB24K500.                                                      
      *------------------------------------------------------------             
                                                                                
           MOVE K510-NUM-HDB-REF   TO K500-NUM-HDB-REF.                         
           MOVE K510-DTE-APPROVAL  TO K500-DTE-APPROVAL.                        
                                                                                
           READ BB24K500.                                                       
                                                                                
           EVALUATE WS-BB24K500-STATUS                                          
             WHEN 00                                                            
                  IF K500-CDE-RESALE-STATUS2 = LOW-VALUES OR SPACES             
                     MOVE K500-CDE-RESALE-STATUS TO WS-RSL-STATUS               
                  ELSE                                                          
                     IF (F825-NUM-NRIC = K500-NUM-UIN-SELLER2) OR               
                        (F825-NUM-NRIC = K500-NUM-UIN-SPOUSE2)                  
                         MOVE K500-CDE-RESALE-STATUS TO WS-RSL-STATUS           
                     ELSE                                                       
                         MOVE K500-CDE-RESALE-STATUS2 TO WS-RSL-STATUS          
                     END-IF                                                     
                  END-IF                                                        
                                                                                
                  IF NOT INVALID-RSL-STATUS                                     
                     IF K500-NUM-HDB-REF(1:9) = WS-SMS-HDBREF(1:9)              
                        CONTINUE                                                
                     ELSE                                                       
                        MOVE 'Y' TO WS-RSL-FND                                  
                     END-IF                                                     
                  END-IF                                                        
                                                                                
              WHEN 10                                                           
              WHEN 23                                                           
                   CONTINUE                                                     
              WHEN OTHER                                                        
                   DISPLAY 'READ BB24K500 ERROR ' WS-BB24K500-STATUS            
                   MOVE WS-BB24K500-STATUS          TO RETURN-CODE              
                   PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       6000-CHECK-BR10K010.                                                     
      *------------------------------------------------------------             
           MOVE SPACES          TO WS-PPO-FND.                                  
           MOVE SPACES          TO K010-KEY-FLD.                                
           MOVE F825-NUM-NRIC   TO K010-NUM-UIN.                                
                                                                                
           START BR10K010 KEY > K010-KEY-FLD.                                   
                                                                                
           EVALUATE WS-BR10K010-STATUS                                          
             WHEN   00                                                          
                    PERFORM 6100-READNEXT-BR10K010 THRU 6100-EXIT               
                      UNTIL K010-NUM-UIN NOT = F825-NUM-NRIC                    
                         OR EOF-BR10K010 = 'Y'                                  
                         OR WS-PPO-FND = 'Y'                                    
             WHEN   23                                                          
                    CONTINUE                                                    
             WHEN   OTHER                                                       
                    DISPLAY 'BR10K010 START ERROR ' WS-BR10K010-STATUS          
                    DISPLAY 'BR10K010 RECORD KEY IS ' K010-KEY-FLD              
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       6100-READNEXT-BR10K010.                                                  
      *------------------------------------------------------------             
           READ BR10K010 NEXT.                                                  
                                                                                
           EVALUATE WS-BR10K010-STATUS                                          
              WHEN  00                                                          
                    IF K010-NUM-UIN = F825-NUM-NRIC                             
                       IF K010-CDE-PPTY-TYPE NOT = 'HLQTR' AND                  
                                                   'HLUSH' AND                  
                                                   'HSHSH'                      
                          MOVE 'Y' TO WS-PPO-FND                                
                       END-IF                                                   
                    END-IF                                                      
              WHEN  23                                                          
                    CONTINUE                                                    
              WHEN  OTHER                                                       
                    DISPLAY 'NEXT BR10K010 ERROR ' WS-BR10K010-STATUS           
                    MOVE WS-BR10K010-STATUS TO RETURN-CODE                      
                   PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       7000-CHECK-BR10K080.                                                     
      *------------------------------------------------------------             
           MOVE SPACES          TO K080-KEY-FLD.                                
           MOVE SPACES          TO WS-PPOH-FND.                                 
           MOVE SPACES          TO WS-DTE-DISP.                                 
           MOVE F825-NUM-NRIC   TO K080-NUM-UIN OF BR10K080-REC.                
                                                                                
           START BR10K080 KEY > K080-KEY-FLD.                                   
                                                                                
           EVALUATE WS-BR10K080-STATUS                                          
             WHEN   00                                                          
                    PERFORM 7100-READNEXT-BR10K080 THRU 7100-EXIT               
                      UNTIL K080-NUM-UIN OF BR10K080-REC NOT =                  
                                            F825-NUM-NRIC                       
                         OR EOF-BR10K080 = 'Y'                                  
                         OR WS-PPOH-FND = 'Y'                                   
             WHEN   23                                                          
                    CONTINUE                                                    
             WHEN   OTHER                                                       
                    DISPLAY 'BR10K080 START ERROR ' WS-BR10K080-STATUS          
                    DISPLAY 'BR10K080 RECORD KEY IS ' K010-KEY-FLD              
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       7100-READNEXT-BR10K080.                                                  
      *------------------------------------------------------------             
           READ BR10K080 NEXT.                                                  
                                                                                
           EVALUATE WS-BR10K080-STATUS                                          
              WHEN  00                                                          
                    IF K080-NUM-UIN OF BR10K080-REC = F825-NUM-NRIC             
                       IF K080-CDE-PPTY-TYPE NOT = 'HLQTR' AND                  
                                                   'HLUSH' AND                  
                                                   'HSHSH'                      
                          MOVE 'Y' TO WS-PPOH-FND                               
                          MOVE K080-DTE-DISPOSAL TO WS-DTE-DISP                 
                       END-IF                                                   
                    END-IF                                                      
              WHEN  23                                                          
                    CONTINUE                                                    
              WHEN  OTHER                                                       
                    DISPLAY 'NEXT BR10K080 ERROR ' WS-BR10K080-STATUS           
                    MOVE WS-BR10K080-STATUS TO RETURN-CODE                      
                   PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       8000-PRINT-DETAIL.                                                       
                                                                                
           ADD 1 TO WS-SNO.                                                     
           MOVE WS-SNO        TO LK09-SNO.                                      
           MOVE WS-NUM-REGN   TO LK09-REGNO.                                    
           MOVE WS-DEB-FND    TO LK09-DEB.                                      
           IF WS-NUM-NTIMER NOT = 'Y'                                           
              MOVE WS-SMSRSL-FND TO LK09-SMS                                    
           ELSE                                                                 
              MOVE SPACES        TO LK09-SMS                                    
           END-IF.                                                              
           MOVE WS-PPO-FND    TO LK09-PPO.                                      
           IF WS-VALID-PPOH = 'Y'                                               
              MOVE WS-PPOH-FND   TO LK09-PPOH                                   
           ELSE                                                                 
              MOVE SPACES        TO LK09-PPOH                                   
           END-IF.                                                              
           MOVE WS-INC-MAX    TO LK09-HH-GEMAX.                                 
                                                                                
           IF WS-LINE > 60                                                      
              ADD  1       TO WS-PAGE                                           
              MOVE WS-PAGE TO LK09-PAGE-NO                                      
              WRITE BP13LK09-REC FROM LK09-HDG1 AFTER PAGE                      
              WRITE BP13LK09-REC FROM LK09-HDG2 AFTER 1                         
              WRITE BP13LK09-REC FROM LK09-HDG3 AFTER 2                         
              WRITE BP13LK09-REC FROM LK09-HDG4 AFTER 1                         
              MOVE 1 TO WS-LINE                                                 
           END-IF.                                                              
                                                                                
           WRITE BP13LK09-REC FROM LK09-DETAIL AFTER 1.                         
           ADD 1 TO WS-LINE.                                                    
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       9000-CLOSE-ROUTINE.                                                      
      *------------------------------------------------------------             
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY 'BP13CK09            CURR-DATE : ' WS-DATE.                  
           DISPLAY '*-----------------------------------------------*'.         
           DISPLAY 'EXCEPTIONAL REPORT BEFORE BALLOT COMMENCE'.                 
           DISPLAY '*-----------------------------------------------*'.         
           DISPLAY 'NO OF RECORDS READ FROM F825   ' WS-F825-READ.              
           DISPLAY 'NO OF RECORDS BYPASS FROM F825 ' WS-F825-BYPASS.            
                                                                                
           CLOSE BP13F825                                                       
                 BP13K730                                                       
                 BP13K813                                                       
                 P04K010                                                        
                 AB03K080                                                       
                 AB03K030                                                       
                 BR10K010                                                       
                 BR10K080                                                       
                 BB24K500                                                       
                 BB24K510.                                                      
                                                                                
           IF  WS-BP13K813-STATUS NOT = ZEROS AND 97                            
               DISPLAY 'CLOSING ERROR '                                         
               DISPLAY 'WS-BP13K813-STATUS ' WS-BP13K813-STATUS.                
                                                                                
           IF  WS-BP13K730-STATUS NOT = ZEROS AND 97                            
               DISPLAY 'CLOSING ERROR '                                         
               DISPLAY 'WS-BP13K730-STATUS ' WS-BP13K730-STATUS.                
                                                                                
           IF  WS-P04K010-STATUS NOT = ZEROS AND 97                             
               DISPLAY 'CLOSING ERROR '                                         
               DISPLAY 'WS-P04K010-STATUS ' WS-P04K010-STATUS.                  
                                                                                
           IF  WS-BR10K010-STATUS NOT = ZEROS AND 97                            
               DISPLAY 'CLOSING ERROR '                                         
               DISPLAY 'WS-BR10K010-STATUS ' WS-BR10K010-STATUS.                
                                                                                
           IF  WS-BR10K080-STATUS NOT = ZEROS AND 97                            
               DISPLAY 'CLOSING ERROR '                                         
               DISPLAY 'WS-BR10K080-STATUS ' WS-BR10K080-STATUS.                
                                                                                
           IF  WS-AB03K080-STATUS NOT = ZEROS AND 97                            
               DISPLAY 'CLOSING ERROR '                                         
               DISPLAY 'WS-AB03K080-STATUS ' WS-AB03K080-STATUS.                
                                                                                
           IF  WS-AB03K030-STATUS NOT = ZEROS AND 97                            
               DISPLAY 'CLOSING ERROR '                                         
               DISPLAY 'WS-AB03K030-STATUS ' WS-AB03K030-STATUS.                
                                                                                
           IF  WS-BB24K500-STATUS NOT = ZEROS AND 97                            
               DISPLAY 'CLOSING ERROR '                                         
               DISPLAY 'WS-BB24K500-STATUS ' WS-BB24K500-STATUS.                
                                                                                
           IF  WS-BB24K510-STATUS NOT = ZEROS AND 97                            
               DISPLAY 'CLOSING ERROR '                                         
               DISPLAY 'WS-BB24K510-STATUS ' WS-BB24K510-STATUS.                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
