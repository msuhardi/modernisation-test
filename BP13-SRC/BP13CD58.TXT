       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD58.                                                 
       AUTHOR.        LSB.                                                      
       DATE-WRITTEN.  09/02/09.                                                 
      *****************************************************************         
      * OBJECTIVE 1)DBSS TOP CASES                                              
      *             TO ROLL-OVER BALANCES IN ACCOUNT HISTORY (BP13KD60)         
      *             TO BACKUP FILE (BP13KD61).                                  
      *                                                                         
      * SUBROUTINE     - NIL                                                    
      *                                                                         
      *----------------------------------------------------------------         
      * REF NO   DATE    BY   AMENDMENTS/ENHANCEMENTS                           
      * -------  ----    ---  -----------------------                           
      * BP133535 090209  LSB  NEW PGM                                           
      *****************************************************************         
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F136  ASSIGN TO BP13F136.                                 
           SELECT BP13KD60  ASSIGN TO BP13KD60                                  
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS KD60-KEY-FLD                              
                  FILE STATUS      IS KD60-STATUS                               
                  ACCESS           IS RANDOM.                                   
           SELECT BP13KD61  ASSIGN TO BP13KD61                                  
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS KD61-KEY-FLD                              
                  FILE STATUS      IS KD61-STATUS                               
                  ACCESS           IS RANDOM.                                   
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F136                   BLOCK CONTAINS 0 RECORDS                  
                                      RECORD CONTAINS 150                       
                                      LABEL RECORD IS STANDARD                  
                                      RECORDING MODE IS F.                      
       COPY BP13F136.                                                           
                                                                                
       FD  BP13KD60                   BLOCK CONTAINS 0 RECORDS                  
                                      RECORD CONTAINS 250 CHARACTERS            
                                      LABEL RECORDS ARE STANDARD.               
       COPY BP13KD60.                                                           
                                                                                
       FD  BP13KD61                   BLOCK CONTAINS 0 RECORDS                  
                                      RECORD CONTAINS 250 CHARACTERS            
                                      LABEL RECORDS ARE STANDARD.               
       COPY BP13KD61.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-STATUS.                                                           
           05 KD60-STATUS              PIC 99       VALUE ZEROS.                
           05 KD61-STATUS              PIC 99       VALUE ZEROS.                
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-CNT-F136-RD           PIC 9(6) COMP VALUE 0.                   
           05 WS-CNT-KD60-RD           PIC 9(6) COMP VALUE 0.                   
           05 WS-CNT-KD60-DEL          PIC 9(6) COMP VALUE 0.                   
           05 WS-CNT-KD61-WRT          PIC 9(6) COMP VALUE 0.                   
                                                                                
       01  WS-CURR-DATE.                                                        
           02 WS-CURRENT-DATE.                                                  
              05 WS-DATE-CYM.                                                   
                 10 WS-DATE-CCYY       PIC X(4).                                
                 10 WS-DATE-MM         PIC X(2).                                
              05 WS-DATE-DD            PIC X(2).                                
                                                                                
           02 WS-DTE-TODAY REDEFINES WS-CURRENT-DATE.                           
              05 WS-DTE-TODAY-CC       PIC 9(2).                                
              05 WS-DTE-TODAY-YMD.                                              
                 10 WS-DTE-TODAY-YY    PIC 9(2).                                
                 10 WS-DTE-TODAY-MM    PIC 9(2).                                
                 10 WS-DTE-TODAY-DD    PIC 9(2).                                
                                                                                
       01  WS-TMP-DATE.                                                         
           05  WS-TMP-CYM.                                                      
               10  WS-TMP-CCYY         PIC X(4).                                
               10  WS-TMP-MM           PIC X(2).                                
           05  WS-TMP-DD               PIC X(2).                                
                                                                                
       01  WS-CURRENT-DATE1.                                                    
           05  WS-DATE1.                                                        
                10  WS-DATE1-CYM.                                               
                    15  WS-DATE1-CCYY  PIC X(4).                                
                    15  WS-DATE1-MM    PIC X(2).                                
                10  WS-DATE1-DD        PIC X(2).                                
           05  WS-CURRENT-TIME         PIC X(9).                                
           05  WS-TIME REDEFINES WS-CURRENT-TIME.                               
                10  WS-HH              PIC 9(2).                                
                10  WS-MM              PIC 9(2).                                
                10  WS-SS              PIC 9(2).                                
                10  WS-MS              PIC 9(2).                                
                10  WS-GM              PIC 9(1).                                
           05  FILLER                  PIC X(4).                                
                                                                                
       01  WS-GM-CNT                   PIC S9(8) COMP VALUE ZEROS.              
                                                                                
       01  WS-TRANS-DTE.                                                        
           05  WS-TRANS-DTE-CYM.                                                
               10  WS-TRANS-DATE-CCYY  PIC X(4).                                
               10  WS-TRANS-DATE-MM    PIC X(2).                                
           05  WS-TRANS-DATE-DD        PIC X(2) VALUE '01'.                     
                                                                                
       01  WS-DTE-KD60-UPD             PIC X(6).                                
                                                                                
       01  WS-SER-VAR.                                                          
           05  WS-SER-CHR.                                                      
               10 WS-SER-CHR-1          PIC X(01).                              
               10 WS-SER-CHR-2          PIC X(01).                              
               10 WS-SER-CHR-3          PIC X(01).                              
           05  WS-NUM-SER REDEFINES WS-SER-CHR                                  
                                       PIC 9(03).                               
                                                                                
       01  WS-SWITCH.                                                           
           05 WS-F136-END              PIC X(1) VALUE 'N'.                      
           05 WS-KD60-FND              PIC X(1) VALUE 'N'.                      
           05 WS-WRITE-KD61            PIC X(1) VALUE 'N'.                      
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-START-UP       THRU 1000-EXIT.                          
           PERFORM 2000-MAIN-PROCESS   THRU 2000-EXIT.                          
           PERFORM 9999-WRAP-UP        THRU 9999-EXIT.                          
                                                                                
                                                                                
      ******************************************************************        
       1000-START-UP.                                                           
      ******************************************************************        
                                                                                
            OPEN INPUT BP13F136                                                 
                I-O    BP13KD60                                                 
                       BP13KD61.                                                
                                                                                
           IF  KD60-STATUS NOT = '00' AND '97'                                  
               DISPLAY 'ERROR OPEN BP13KD60 : ' KD60-STATUS                     
               MOVE KD60-STATUS TO RETURN-CODE                                  
               PERFORM 9999-WRAP-UP        THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF  KD61-STATUS NOT = '00' AND '97'                                  
               DISPLAY 'ERROR OPEN BP13KD61 : ' KD61-STATUS                     
               MOVE KD61-STATUS TO RETURN-CODE                                  
               PERFORM 9999-WRAP-UP        THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2000-MAIN-PROCESS.                                                       
      ******************************************************************        
                                                                                
           PERFORM 3000-READ-BP13F136       THRU 3000-EXIT.                     
           PERFORM 5000-READ-UPDATE-PROCESS THRU 5000-EXIT                      
                   UNTIL WS-F136-END = 'Y'.                                     
                                                                                
       2000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       3000-READ-BP13F136.                                                      
      ******************************************************************        
                                                                                
           READ BP13F136 AT END                                                 
                MOVE 'Y'         TO WS-F136-END                                 
                GO TO 3000-EXIT.                                                
           ADD 1 TO WS-CNT-F136-RD.                                             
                                                                                
       3000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       5000-READ-UPDATE-PROCESS.                                                
      ******************************************************************        
           IF F136-CDE-TRANS-TYPE = '70'                                        
              PERFORM 5100-READ-KD60        THRU 5100-EXIT                      
              IF WS-KD60-FND = 'Y'                                              
                 PERFORM 5200-MOVE-VALUES-KD61   THRU 5200-EXIT                 
                 MOVE 'N'    TO WS-WRITE-KD61                                   
                 PERFORM 5300-WRITE-KD61         THRU 5300-EXIT                 
                   UNTIL WS-WRITE-KD61 = 'Y'                                    
                 PERFORM 5750-DELETE-BP13KD60   THRU 5750-EXIT                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-BP13F136          THRU 3000-EXIT.                  
                                                                                
       5000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
                                                                                
      ******************************************************************        
       5100-READ-KD60.                                                          
      ******************************************************************        
                                                                                
           MOVE   SPACES             TO     BP13KD60-REC.                       
           INITIALIZE                       BP13KD60-REC.                       
           MOVE  F136-NUM-REGN       TO     KD60-NUM-REGN.                      
           READ BP13KD60.                                                       
           IF KD60-STATUS = 00 OR 02                                            
              MOVE 'Y'               TO     WS-KD60-FND                         
              ADD 1                  TO     WS-CNT-KD60-RD                      
           ELSE                                                                 
             IF KD60-STATUS = 23                                                
                MOVE 'N'             TO     WS-KD60-FND                         
             ELSE                                                               
                DISPLAY 'BP13KD60-READ ERROR. STATUS: '                         
                       KD60-STATUS                                              
                MOVE   KD60-STATUS   TO   RETURN-CODE                           
                PERFORM 9999-WRAP-UP        THRU 9999-EXIT                      
             END-IF                                                             
           END-IF.                                                              
                                                                                
       5100-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       5200-MOVE-VALUES-KD61.                                                   
      ******************************************************************        
           MOVE SPACES                 TO BP13KD61-REC.                         
           INITIALIZE                     BP13KD61-REC.                         
           MOVE BP13KD60-REC           TO BP13KD61-REC.                         
                                                                                
       5200-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       5300-WRITE-KD61.                                                         
      ******************************************************************        
           WRITE BP13KD61-REC.                                                  
           IF KD61-STATUS = 00 OR 02                                            
              ADD 1    TO WS-CNT-KD61-WRT                                       
              MOVE 'Y' TO WS-WRITE-KD61                                         
           ELSE                                                                 
              IF KD61-STATUS = 22                                               
                 PERFORM 5400-SERIAL-PROCESS  THRU 5400-EXIT                    
              ELSE                                                              
                 DISPLAY ' BP13KD61-WRITING ERROR, STATUS: '                    
                         KD61-STATUS                                            
                 MOVE KD61-STATUS           TO   RETURN-CODE                    
                 PERFORM 9999-WRAP-UP        THRU 9999-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5300-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       5400-SERIAL-PROCESS.                                                     
      ******************************************************************        
           MOVE KD61-NUM-SEQ           TO   WS-SER-CHR.                         
           IF WS-NUM-SER IS NOT NUMERIC                                         
              MOVE ZEROES              TO   WS-NUM-SER                          
           END-IF.                                                              
                                                                                
           ADD +1                      TO   WS-NUM-SER.                         
           MOVE WS-NUM-SER             TO   KD61-NUM-SEQ.                       
                                                                                
       5400-EXIT.  EXIT.                                                        
                                                                                
                                                                                
                                                                                
                                                                                
      ******************************************************************        
       5750-DELETE-BP13KD60.                                                    
      ******************************************************************        
           DELETE  BP13KD60.                                                    
                                                                                
           IF KD60-STATUS = 00 OR 02                                            
              ADD 1 TO WS-CNT-KD60-DEL                                          
           ELSE                                                                 
              DISPLAY ' BP13KD60-REWRITING ERROR, STATUS: '                     
                       KD60-STATUS                                              
              MOVE KD60-STATUS           TO   RETURN-CODE                       
              PERFORM 9999-WRAP-UP        THRU 9999-EXIT                        
           END-IF.                                                              
                                                                                
       5750-EXIT.  EXIT.                                                        
                                                                                
                                                                                
                                                                                
                                                                                
      ******************************************************************        
       9999-WRAP-UP.                                                            
      ******************************************************************        
           DISPLAY  SPACES.                                                     
           DISPLAY '******* CONTROL   COUNTS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13CC13'                                      
           DISPLAY  SPACES.                                                     
           DISPLAY 'NO. OF BP13F136 RECS READ    : ' WS-CNT-F136-RD.            
           DISPLAY 'NO. OF BP13KD60 RECS READ    : ' WS-CNT-KD60-RD.            
           DISPLAY 'NO. OF BP13KD60 RECS DELETE  : ' WS-CNT-KD60-DEL.           
           DISPLAY 'NO. OF BP13KD61 RECS WRITTEN : ' WS-CNT-KD61-WRT.           
                                                                                
           CLOSE    BP13F136                                                    
                    BP13KD60                                                    
                    BP13KD61.                                                   
                                                                                
           IF  KD60-STATUS NOT = '00'                                           
               DISPLAY 'ERROR CLOSE BP13KD60 : ' KD60-STATUS                    
               MOVE KD60-STATUS TO RETURN-CODE                                  
           END-IF.                                                              
                                                                                
           IF  KD61-STATUS NOT = '00'                                           
               DISPLAY 'ERROR CLOSE BP13KD61 : ' KD61-STATUS                    
               MOVE KD61-STATUS TO RETURN-CODE                                  
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
                                                                                
