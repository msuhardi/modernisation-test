       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C237.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  OCT 06 2009.                                              
      * ============================================================            
      * SYSTEM OF COMMITMENT (SOC)                                              
      * ============================================================            
      * OBJECTIVE  - TO PROVIDE INTERFACE FILE FOR MAMS ON AVAILABLE            
      *              FLATS FOR NPL (AMR)                                        
      *                                                                         
      * INPUT FILES:  BM06F110                                                  
      *               BP13K270                                                  
      *               BM06K100                                                  
      *               BB14K500                                                  
      * OUTPUT     :  BP13F472 - INTERFACE FILE FOR MAMS                        
      * ------------------------------------------------------------            
      * REF NO    DATE  BY  DESCRIPTIONS                                        
      * -------- ------ --- ------------                                        
      * BP133725 061009 ESA1 NEW PGM                                            
      * BP133924 080610 ESA1 ADD UPDATE OF F472-DTE-BALLOT                      
      * ============================================================            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06F110 ASSIGN        TO BM06F110.                           
                                                                                
           SELECT BP13K270 ASSIGN        TO BP13K270                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS RANDOM                              
                           RECORD KEY    IS K270-KEY-FLD                        
                           ALTERNATE KEY IS K270-AIX1-KEY                       
                           FILE STATUS   IS K270-STATUS.                        
                                                                                
           SELECT BM06K100 ASSIGN        TO BM06K100                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS RANDOM                              
                           RECORD KEY    IS K100-KEY-FLD                        
                           FILE STATUS   IS K100-STATUS.                        
                                                                                
           SELECT BB14K500 ASSIGN        TO BB14K500                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS RANDOM                              
                           RECORD KEY    IS K500-KEY-FLD                        
                           FILE STATUS   IS K500-STATUS.                        
                                                                                
           SELECT BP13F472 ASSIGN       TO BP13F472.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD   BM06F110                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK  CONTAINS 0   RECORDS.                                        
       COPY BM06F110.                                                           
                                                                                
       FD   BP13K270                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K270.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K100.                                                           
                                                                                
       FD   BB14K500                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BB14K500.                                                           
                                                                                
       FD   BP13F472                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 100 CHARACTERS                                      
            BLOCK  CONTAINS 0   RECORDS.                                        
       COPY BP13F472.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  K270-STATUS                   PIC 99    VALUE 0.                     
       01  K100-STATUS                   PIC 99    VALUE 0.                     
       01  K500-STATUS                   PIC 99    VALUE 0.                     
       01  WS-F110-EOF                   PIC X     VALUE 'N'.                   
       01  WS-DATE                       PIC X(08) VALUE SPACES.                
       01  WS-SCH-ACC-NO                 PIC X(11) VALUE SPACES.                
       01  WS-NUM-ALLOCN-QTR             PIC X(6)  VALUE SPACES.                
       01  WS-AMT-SELLING-PRICE          PIC 9(8)  VALUE 0.                     
       01  WS-NUM-QUOTA-CHINESE          PIC 9(3)  VALUE 0.                     
       01  WS-NUM-QUOTA-MALAY            PIC 9(3)  VALUE 0.                     
       01  WS-NUM-QUOTA-OTHER            PIC 9(3)  VALUE 0.                     
       01  WS-DTE-LCD                    PIC X(8)  VALUE SPACES.                
       01  WS-DTE-ECD                    PIC X(8)  VALUE SPACES.                
       01  WS-SQL-CODE                   PIC 9(4).                              
       01  WS-IDX                        PIC 9(2) VALUE 0.                      
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-CNT-F110-READ           PIC 9(07)  VALUE 0.                    
           05 WS-CNT-K270-MISS           PIC 9(07)  VALUE 0.                    
           05 WS-CNT-K100-MISS           PIC 9(07)  VALUE 0.                    
           05 WS-CNT-K500-MISS           PIC 9(07)  VALUE 0.                    
           05 WS-CNT-F472-WRITE          PIC 9(07)  VALUE 0.                    
                                                                                
           05 WS-ERROR-FL                PIC X(1) VALUE 'N'.                    
              88 WS-NO-ERROR             VALUE 'N'.                             
              88 WS-ERROR                VALUE 'Y'.                             
                                                                                
      *=======================================================*                 
      *    LINKAGE VARIABLES                                  *                 
      *=======================================================*                 
       01   WS-LINK-REC.                                                        
            05 WS-LINK-NUM-SCH            PIC X(4).                             
            05 WS-LINK-NUM-ACC            PIC X(4).                             
            05 WS-LINK-NUM-CHK            PIC X(1).                             
                                                                                
       COPY P13COMM8.                                                           
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-CONTROL.                                                            
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 2000-READ-BM06F110    THRU 2000-EXIT.                        
           PERFORM 3000-MAIN-ROUTINE     THRU 3000-EXIT                         
                   UNTIL WS-F110-EOF = 'Y'.                                     
           PERFORM 9999-WRAP-UP          THRU 9999-EXIT.                        
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
            OPEN INPUT  BM06F110                                                
                        BP13K270                                                
                        BM06K100                                                
                        BB14K500                                                
                OUTPUT  BP13F472.                                               
                                                                                
            IF K270-STATUS NOT = 00 AND 97                                      
               DISPLAY 'OPEN FAILED FOR BP13K270,STATUS ' K270-STATUS           
               MOVE K270-STATUS TO RETURN-CODE                                  
               PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                         
            END-IF.                                                             
                                                                                
            IF K100-STATUS NOT = 00 AND 97                                      
               DISPLAY 'OPEN FAILED FOR BM06K100,STATUS ' K100-STATUS           
               MOVE K100-STATUS TO RETURN-CODE                                  
               PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                         
            END-IF.                                                             
                                                                                
            IF K500-STATUS NOT = 00 AND 97                                      
               DISPLAY 'OPEN FAILED FOR BB14K500,STATUS ' K500-STATUS           
               MOVE K500-STATUS TO RETURN-CODE                                  
               PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                         
            END-IF.                                                             
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       2000-READ-BM06F110.                                                      
      *-------------------------------------------------------------            
            READ BM06F110 AT END                                                
                 MOVE 'Y'          TO WS-F110-EOF                               
                 GO TO 2000-EXIT                                                
            END-READ.                                                           
                                                                                
            MOVE F110-KEY-FLD             TO WS-SCH-ACC-NO.                     
            MOVE F110-CURR-SELLING-PRICE  TO WS-AMT-SELLING-PRICE.              
            MOVE F110-NUM-ALLOCN-QTR      TO WS-NUM-ALLOCN-QTR.                 
                                                                                
            ADD  1                   TO WS-CNT-F110-READ.                       
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       3000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           SET WS-NO-ERROR           TO TRUE.                                   
           PERFORM 4000-GET-FT          THRU 4000-EXIT.                         
                                                                                
           IF WS-NO-ERROR                                                       
              PERFORM 5000-READ-BP13K270   THRU 5000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 6000-READ-BM06K100   THRU 6000-EXIT.                         
           PERFORM 8000-WRITE-BP13F472  THRU 8000-EXIT.                         
                                                                                
           PERFORM 2000-READ-BM06F110   THRU 2000-EXIT.                         
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       4000-GET-FT.                                                             
      *------------------------------------------------------                   
           MOVE SPACES           TO BP13COMM8-REC.                              
           MOVE F110-SCHEME      TO WS-LINK-NUM-SCH.                            
           MOVE F110-ACC         TO WS-LINK-NUM-ACC.                            
           MOVE F110-CHK         TO WS-LINK-NUM-CHK.                            
                                                                                
           CALL 'BP13C913' USING                                                
                 WS-LINK-REC, BP13COMM8-REC                                     
           END-CALL.                                                            
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              MOVE COMM8-CDE-SYSERR  TO WS-SQL-CODE                             
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE' WS-SQL-CODE           
              ELSE                                                              
                 DISPLAY 'ERROR READING PBF TABLE' WS-SQL-CODE                  
              END-IF                                                            
              MOVE SPACES        TO BP13COMM8-REC                               
              SET WS-ERROR       TO TRUE                                        
           END-IF.                                                              
                                                                                
           EVALUATE  COMM8-CDE-TYP-ACTLUSE                                      
           WHEN '01'                                                            
                MOVE  1   TO  WS-IDX                                            
           WHEN '02'                                                            
                MOVE 2    TO  WS-IDX                                            
           WHEN '03'                                                            
                MOVE 3    TO  WS-IDX                                            
           WHEN '04'                                                            
                MOVE 4    TO  WS-IDX                                            
           WHEN '05'                                                            
                MOVE 6    TO  WS-IDX                                            
           WHEN '06'                                                            
                MOVE 7    TO  WS-IDX                                            
           WHEN OTHER                                                           
                MOVE 8    TO  WS-IDX                                            
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       5000-READ-BP13K270.                                                      
      *-------------------------------------------------------------            
           MOVE ZEROES       TO  WS-NUM-QUOTA-MALAY                             
                                 WS-NUM-QUOTA-OTHER                             
                                 WS-NUM-QUOTA-CHINESE.                          
           MOVE F110-AIX1    TO  K270-AIX1-KEY.                                 
                                                                                
           READ BP13K270 KEY IS K270-AIX1-KEY.                                  
                                                                                
           EVALUATE K270-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                IF WS-IDX = 4                                                   
                  COMPUTE WS-NUM-QUOTA-MALAY =                                  
                                          K270-NUM-MAX-CAT1(WS-IDX) +           
                                          K270-NUM-MAX-CAT1(WS-IDX + 1)         
                  COMPUTE WS-NUM-QUOTA-OTHER =                                  
                                          K270-NUM-MAX-CAT3(WS-IDX) +           
                                          K270-NUM-MAX-CAT3(WS-IDX + 1)         
                  COMPUTE WS-NUM-QUOTA-CHINESE =                                
                                          K270-NUM-MAX-CAT2(WS-IDX) +           
                                          K270-NUM-MAX-CAT2(WS-IDX + 1)         
                ELSE                                                            
                 IF WS-IDX = 8                                                  
                    MOVE ZEROES   TO WS-NUM-QUOTA-MALAY                         
                                     WS-NUM-QUOTA-OTHER                         
                                     WS-NUM-QUOTA-CHINESE                       
                 ELSE                                                           
                  MOVE K270-NUM-MAX-CAT1(WS-IDX) TO WS-NUM-QUOTA-MALAY          
                  MOVE K270-NUM-MAX-CAT3(WS-IDX) TO WS-NUM-QUOTA-OTHER          
                  MOVE K270-NUM-MAX-CAT2(WS-IDX) TO WS-NUM-QUOTA-CHINESE        
                 END-IF                                                         
                END-IF                                                          
               WHEN 10                                                          
               WHEN 23                                                          
                ADD  1                  TO WS-CNT-K270-MISS                     
                MOVE ZEROES             TO WS-NUM-QUOTA-CHINESE                 
                                           WS-NUM-QUOTA-MALAY                   
                                           WS-NUM-QUOTA-OTHER                   
               WHEN OTHER                                                       
                MOVE K270-STATUS              TO RETURN-CODE                    
                DISPLAY 'ERROR READING BP13K270, STATUS '                       
                             K270-STATUS                                        
                PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       6000-READ-BM06K100.                                                      
      *-------------------------------------------------------------            
           MOVE F110-AIX1        TO   K100-KEY-FLD.                             
                                                                                
           READ BM06K100.                                                       
                                                                                
           EVALUATE K100-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 7000-READ-BB14K500    THRU 7000-EXIT                
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD  1                  TO WS-CNT-K100-MISS                 
                    DISPLAY 'K100 REC NOT FOUND : ' K100-KEY-FLD                
                    PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                    
               WHEN OTHER                                                       
                    MOVE K100-STATUS              TO RETURN-CODE                
                    DISPLAY 'ERROR READING BM06K100, STATUS '                   
                             K100-STATUS                                        
                    PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       7000-READ-BB14K500.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES           TO   WS-DTE-LCD                                
                                      WS-DTE-ECD.                               
           MOVE K100-CDE-BLK     TO   K500-KEY-FLD.                             
                                                                                
           READ BB14K500.                                                       
                                                                                
           EVALUATE K500-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    MOVE K500-DTE-LEASE-COMM-FLAT  TO WS-DTE-LCD                
                    MOVE K500-DTE-BLDG-COMPLETE    TO WS-DTE-ECD                
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD  1                  TO WS-CNT-K500-MISS                 
                    MOVE SPACES             TO WS-DTE-LCD                       
                                               WS-DTE-ECD                       
               WHEN OTHER                                                       
                    MOVE K500-STATUS              TO RETURN-CODE                
                    DISPLAY 'ERROR READING BB14K500, STATUS '                   
                             K500-STATUS                                        
                    PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       8000-WRITE-BP13F472.                                                     
      *-------------------------------------------------------------            
           MOVE SPACES                 TO BP13F472-REC.                         
           MOVE WS-SCH-ACC-NO          TO F472-SCH-ACC-NO.                      
           MOVE WS-AMT-SELLING-PRICE   TO F472-AMT-SELLING-PRICE.               
           MOVE WS-NUM-QUOTA-CHINESE   TO F472-NUM-QUOTA-CHINESE.               
           MOVE WS-NUM-QUOTA-MALAY     TO F472-NUM-QUOTA-MALAY.                 
           MOVE WS-NUM-QUOTA-OTHER     TO F472-NUM-QUOTA-OTHER.                 
           MOVE WS-DTE-LCD             TO F472-DTE-LCD.                         
           MOVE WS-DTE-ECD             TO F472-DTE-ECD.                         
           MOVE WS-NUM-ALLOCN-QTR      TO F472-DTE-BALLOT.                      
           WRITE BP13F472-REC.                                                  
           ADD 1  TO WS-CNT-F472-WRITE.                                         
                                                                                
       8000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       9900-HANDLE-ABEND.                                                       
      *-------------------------------------------------------------            
           DISPLAY ' '.                                                         
           DISPLAY '- BP13C237 CONTROL TOTAL -------------'.                    
           DISPLAY 'TOTAL BM06F110 RECS READ      = ' WS-CNT-F110-READ.         
           DISPLAY 'TOTAL MISSING BP13K270 REC    = ' WS-CNT-K270-MISS.         
           DISPLAY 'TOTAL MISSING BM06K100 REC    = ' WS-CNT-K100-MISS.         
           DISPLAY 'TOTAL MISSING BB14K500 REC    = ' WS-CNT-K500-MISS.         
           DISPLAY 'TOTAL BP13F472 RECS WRITE     = ' WS-CNT-F472-WRITE.        
                                                                                
           CLOSE BM06F110                                                       
                 BP13K270                                                       
                 BM06K100                                                       
                 BB14K500                                                       
                 BP13F472.                                                      
                                                                                
           IF K270-STATUS NOT = 00 AND 97                                       
              MOVE K270-STATUS TO RETURN-CODE                                   
              DISPLAY 'CLOSE FAILED FOR BP13K270 ' K270-STATUS                  
           END-IF.                                                              
                                                                                
           IF K100-STATUS NOT = 00 AND 97                                       
              MOVE K100-STATUS TO RETURN-CODE                                   
              DISPLAY 'CLOSE FAILED FOR BM06K100 ' K100-STATUS                  
           END-IF.                                                              
                                                                                
           IF K500-STATUS NOT = 00 AND 97                                       
              MOVE K500-STATUS TO RETURN-CODE                                   
              DISPLAY 'CLOSE FAILED FOR BB14K500 ' K500-STATUS                  
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9900-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       9999-WRAP-UP.                                                            
      *-------------------------------------------------------------            
           DISPLAY ' '.                                                         
           DISPLAY '- BP13C237 CONTROL TOTAL -------------'.                    
           DISPLAY ' TOTAL BM06F110 REC READ     : ' WS-CNT-F110-READ.          
           DISPLAY ' TOTAL MISSING BP13K270 REC  : ' WS-CNT-K270-MISS.          
           DISPLAY ' TOTAL MISSING BM06K100 REC  : ' WS-CNT-K100-MISS.          
           DISPLAY ' TOTAL MISSING BB14K500 REC  : ' WS-CNT-K500-MISS.          
           DISPLAY ' TOTAL BP13F472 RECS WRITE   : ' WS-CNT-F472-WRITE.         
                                                                                
           CLOSE BM06F110                                                       
                 BP13K270                                                       
                 BM06K100                                                       
                 BB14K500                                                       
                 BP13F472.                                                      
                                                                                
           IF K270-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE FAILED FOR BP13K270 ' K270-STATUS                  
              MOVE K270-STATUS TO RETURN-CODE.                                  
                                                                                
           IF K100-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE FAILED FOR BM06K100 ' K100-STATUS                  
              MOVE K100-STATUS TO RETURN-CODE.                                  
                                                                                
           IF K500-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE FAILED FOR BB14K500 ' K500-STATUS                  
              MOVE K500-STATUS TO RETURN-CODE.                                  
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
