       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C788.                                                  
      *AUTHOR.       YCH.                                                       
      *DATE-WRITTEN. 5/5/1988.                                                  
           SKIP1                                                                
      *========================================================*                
      *    SYSTEM OF COMMITMENT (BP13)                         *                
      *========================================================*                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *      1.  THIS IS A PROGRAM TO SPLIT THE UPGRADER INFO  *                
      *          INTO F716 FORMAT (350) AND OTHERS INFO INTO   *                
      *          F717  FORMAT (50) AND MATCH AGAINST RDB FILE  *                
      *                                                        *                
      * CHG-NO   BY  ON       DESCRIPTION                      *                
      * --------------------------------                       *                
      * C940786  LSB 940803 - UP3 AS UPGRADER                  *                
      * N940014  YCH 941005 - FOR TABULATION OF BP13S755       *                
      * N940014  YCH 941011 - CHANGE OF KEY FIELD              *                
      * BP130261 YCH 970403 - BREAKDOWN OF RFS APPLNS          *                
      * BP130393 LSB 971011 - EXPAND FILE-STATUS-RSL TO 3 BYTES*                
      * BP130445 SSS 980205 - EXPAND F558 RECL FROM 150 TO 400 *                
      * BP130630 NOR 990114 - CHGE F715 F716 EXPAND TO 350     *                
      *              990607 - EXPAND F716A RECL FROM 50 TO 100 *                
      * BP130897 AKT 000804 - EXPAND F558 RECL FROM 400 TO 600 *                
      * BP132384 ANC 030827 - TO CATER FOR SMS/RSL OM/DP FLATS *                
      *========================================================*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F558   ASSIGN TO BP13F558.                                
           SELECT P13F716A   ASSIGN TO P13F716A.                                
           SELECT BP13F716   ASSIGN TO BP13F716.                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F558                                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13F558.                                                           
                                                                                
       FD   P13F716A                                                            
            RECORD CONTAINS 350 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       01   P13F716A-REC.                                                       
            05  F716A-NUM-REGN                PIC X(08).                        
            05  F716A-NUM-NRIC                PIC X(09).                        
            05  F716A-NUM-RDB-DP-OM           PIC X(01).                        
            05  FILLER                        PIC X(09).                        
            05  F716A-DTE-REGN                PIC X(08).                        
            05  FILLER                        PIC X(315).                       
                                                                                
       FD   BP13F716                                                            
            RECORD CONTAINS 350 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13F716.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-F558-READ            PIC 9(6)    VALUE 0.                         
       01  WS-F716A-WRITE          PIC 9(6)    VALUE 0.                         
       01  WS-F716-WRITE           PIC 9(6)    VALUE 0.                         
       01  WS-RDB-DP-OM-D          PIC 9(6)    VALUE 0.                         
       01  WS-RDB-DP-OM-M          PIC 9(6)    VALUE 0.                         
       01  WS-RDB-DP-OM-O          PIC 9(6)    VALUE 0.                         
       01  WS-RDB-DP-OM-R          PIC 9(6)    VALUE 0.                         
       01  WS-RDB-DP-OM-S          PIC 9(6)    VALUE 0.                         
       01  WS-RDB-DP-OM-X          PIC 9(6)    VALUE 0.                         
       01  WS-FLAT-TYPE-RSL.                                                    
           05  FILLER              PIC X(1)    VALUE '0'.                       
           05  WS-FLAT-TYPE        PIC X(1)    VALUE SPACES.                    
       01  WS-DTE-APPROVAL-RSL     PIC X(8)    VALUE SPACES.                    
       01  WS-NUM-RSL-FT           PIC X(2)    VALUE SPACES.                    
                                                                                
       01  EOF-BP13F558            PIC X        VALUE 'N'.                      
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-YY               PIC 9999.                                    
           05  WS-MM               PIC 99.                                      
           05  WS-DD               PIC 99.                                      
                                                                                
       01  WS-SOLD-DATE.                                                        
           05  WS-CC               PIC 99.                                      
           05  WS-IN-DATE          PIC 9(6).                                    
                                                                                
       PROCEDURE DIVISION.                                                      
       000-MAIN.                                                                
           OPEN INPUT  BP13F558                                                 
                OUTPUT P13F716A                                                 
                       BP13F716.                                                
                                                                                
           PERFORM 100-READ-F558-REC THRU 100-EXIT.                             
           PERFORM 200-PROCESS-REC   THRU 200-EXIT                              
                   UNTIL EOF-BP13F558 = 'Y'                                     
                     OR  F558-NUM-NRIC = HIGH-VALUE.                            
           PERFORM 900-CLOSE-ROUTINE.                                           
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-READ-F558-REC.                                                       
           READ BP13F558 AT END MOVE 'Y' TO EOF-BP13F558                        
                MOVE HIGH-VALUE TO F558-NUM-NRIC                                
                GO TO 100-EXIT.                                                 
           ADD 1 TO WS-F558-READ.                                               
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
      *--------------------------------------------------------*                
      * TAG USED IN NUM-RDB-DP-OM                              *                
      *--------------------------------------------------------*                
      *  ==> UPGRADER                                          *                
      * 'D' => EXISTING FLAT BOUGHT DIRECTLY FROM BOARD        *                
      * 'M' => EXISTING FLAT BOUGHT FROM OM (FND IN SMS AS OM) *                
      * 'O' => EX-LESSEE WITH OM FLAT IN SMS                   *                
      * 'R' => EX-LESSEE WITH NO RECORD FND IN SMS             *                
      *  ==> FIRST TIMER                                       *                
      * 'S' => EXISTING FLAT BOUGHT FROM OM                    *                
      * 'X' => PREVIOUS FLAT BOUGHT FROM OM                    *                
      *  ==> OCCUPIER                                          *                
      * ' ' => OCCUPIER GOING TO READ FROM RDB                 *                
      *--------------------------------------------------------*                
       200-PROCESS-REC.                                                         
           IF F558-CDE-HOUSEHOLD = 'G' OR '3'                                   
              MOVE SPACES             TO BP13F716-REC                           
              MOVE F558-CDE-FILE      TO F716-CDE-FILE                          
              PERFORM 250-OUTPUT-F716 THRU 250-EXIT                             
           ELSE                                                                 
              IF F558-CDE-HOUSEHOLD = 'H'                                       
                 IF F558-CDE-FILE-STATUS-SMS = 'OM'                             
                    MOVE SPACES        TO BP13F716-REC                          
                    MOVE 'S'           TO F716-NUM-RDB-DP-OM                    
                    MOVE 'S'           TO F716-CDE-FILE                         
                    PERFORM 250-OUTPUT-F716 THRU 250-EXIT                       
                 ELSE                                                           
                    IF F558-CDE-FILE-STATUS-RSL = 'OM '                         
                       MOVE SPACES        TO BP13F716-REC                       
                       MOVE 'X'           TO F716-NUM-RDB-DP-OM                 
                       MOVE 'R'           TO F716-CDE-FILE                      
                       PERFORM 250-OUTPUT-F716 THRU 250-EXIT                    
                    ELSE                                                        
                       MOVE SPACES              TO P13F716A-REC                 
                       MOVE F558-NUM-REGN       TO F716A-NUM-REGN               
                       MOVE F558-DTE-ISSUE-REGN TO F716A-DTE-REGN               
                       MOVE F558-NUM-NRIC       TO F716A-NUM-NRIC               
                       MOVE 'Z'                 TO F716A-NUM-RDB-DP-OM          
                       WRITE P13F716A-REC                                       
                       ADD 1 TO WS-F716A-WRITE.                                 
                                                                                
            PERFORM 100-READ-F558-REC THRU 100-EXIT.                            
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       250-OUTPUT-F716.                                                         
           MOVE F558-NUM-REGN       TO F716-NUM-REGN.                           
           MOVE F558-NUM-NRIC       TO F716-NUM-NRIC.                           
           MOVE F558-DTE-ISSUE-REGN TO F716-DTE-REGN.                           
                                                                                
      *---------------------------------------------------------------          
      *  TO DECIDE THE FLAT TYPE RESIDE                                         
      *===============================================================          
      *  IF SMS FLAT TYPE NOT = SPACES                                          
      *       - MOVE FLAT-TYPE-SMS  TO FLAT-TYPE-RESIDE                         
      *       - MOVE DTE-SOLD-SMS   TO DTE-ACCEPT AND DTE-MOVING-IN             
      *       IF CDE-HOUSEHOLD = 'G' OR '3' AND CDE-FILE = 'R'                  
      *          - MOVE FLAT-TYPE-RESALE TO FLAT-TYPE-RESIDE                    
      *       END-IF                                                            
      *  ELSE                                                                   
      *       IF RESALE FLAT TYPE NOT = SPACES                                  
      *          - MOVE FLAT-TYPE-RESALE TO FLAT-TYPE-RESIDE                    
      *          - DTE-ACCEPT AND DTE-MOVING-IN WILL BE EXTRACTED FROM          
      *                RESALE HISTORY IN ANOTHER PROGRAM                        
      *       END-IF                                                            
      *---------------------------------------------------------------          
                                                                                
           IF F558-CDE-FLAT-TYPE-SMS NOT = SPACES                               
              MOVE F558-NUM-HDB-REF-SMS   TO F716-NUM-SCH-ACCNT                 
              MOVE F558-CDE-FLAT-TYPE-SMS TO F716-CDE-FLAT-TYPE-RESIDE          
              MOVE F558-DTE-FLAT-SOLD-SMS TO WS-SOLD-DATE                       
              MOVE WS-SOLD-DATE           TO F716-DTE-ACCEPTED                  
              MOVE WS-SOLD-DATE           TO F716-DTE-MOVING-IN                 
           ELSE                                                                 
              IF F558-CDE-FLAT-TYPE-RESALE NOT = SPACES                         
                 MOVE F558-NUM-HDB-REF-RSL TO F716-NUM-SCH-ACCNT                
                 MOVE F558-CDE-FLAT-TYPE-RESALE TO WS-FLAT-TYPE                 
                 MOVE WS-FLAT-TYPE-RSL   TO F716-CDE-FLAT-TYPE-RESIDE           
              ELSE                                                              
                 NEXT SENTENCE.                                                 
                                                                                
      *---------------------------------------------------------------          
      *  TO DECIDE THE DIRECT PURCHASE OR OPEN MARKET FLAT                      
      *===============================================================          
      *  IF CDE-HOUSEHOLD = 'G' OR '3'                                          
      *     IF CDE-FILE = 'S'                                                   
      *        IF CDE-FILE-STATUS-SMS = SPACES                                  
      *           'D' - UPG WHO ARE EXISTING (DP) FLAT LESSEES                  
      *        ELSE                                                             
      *           'M' - UPG WHO ARE EX-DP FLAT LESSEES                          
      *                 (EXISTING LESSEE OF AN OM FLAT)                         
      *     ELSE                                                                
      *        IF CDE-FILE = 'R'                                                
      *           IF CDE-FILE-STATUS-SMS = 'OM'                                 
      *              'O' - UPG WHO ARE EXISTING RESALE FLAT LESSEE              
      *           ELSE                                                          
      *              'R' - UPG WHO ARE NON-LESSEE OF EXISTING FLAT              
      *---------------------------------------------------------------          
                                                                                
           IF F558-CDE-HOUSEHOLD = 'G' OR '3'                                   
              IF F558-CDE-FILE = 'S'                                            
                IF F558-CDE-FILE-STATUS-SMS = SPACES                            
      ** DP IN SMS -EXISTING LESSEE                                             
                    MOVE 'D'            TO F716-NUM-RDB-DP-OM                   
                ELSE                                                            
      ** OM IN SMS -EXISTING LESSEE BUT OM FLAT                                 
                     MOVE 'M'            TO F716-NUM-RDB-DP-OM                  
              ELSE                                                              
              IF F558-CDE-FILE = 'R'                                            
                IF F558-CDE-FILE-STATUS-SMS = 'OM'                              
      ** DP IN RSL -EX-LESSEE WITH OM FLAT IN SMS                               
                     MOVE 'O'            TO F716-NUM-RDB-DP-OM                  
                 MOVE F558-NUM-HDB-REF-RSL TO F716-NUM-SCH-ACCNT                
                 MOVE F558-CDE-FLAT-TYPE-RESALE TO WS-FLAT-TYPE                 
                 MOVE WS-FLAT-TYPE-RSL   TO F716-CDE-FLAT-TYPE-RESIDE           
                ELSE                                                            
      ** DP IN RSL -EX-LESSEE NO REC  IN SMS                                    
                     MOVE 'R'            TO F716-NUM-RDB-DP-OM                  
                 MOVE F558-NUM-HDB-REF-RSL TO F716-NUM-SCH-ACCNT                
                 MOVE F558-CDE-FLAT-TYPE-RESALE TO WS-FLAT-TYPE                 
                 MOVE WS-FLAT-TYPE-RSL   TO F716-CDE-FLAT-TYPE-RESIDE           
                END-IF                                                          
           END-IF.                                                              
                                                                                
           IF F558-CDE-FILE-STATUS-SMS = 'OM'                                   
              MOVE 'M'       TO F716-NUM-SMS-STATUS                             
           ELSE                                                                 
              IF F558-NUM-HDB-REF-SMS NOT = SPACES AND LOW-VALUES               
                 MOVE 'D'       TO F716-NUM-SMS-STATUS                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F558-CDE-FLAT-TYPE-SMS NOT = SPACES AND LOW-VALUES                
              IF F558-CDE-FLAT-TYPE-SMS(1:1) = 'E'                              
                 MOVE '06'      TO F558-CDE-FLAT-TYPE-SMS                       
              ELSE                                                              
                 MOVE F558-CDE-FLAT-TYPE-SMS TO F716-NUM-SMS-FT                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F558-CDE-FILE-STATUS-RSL = 'OM '                                  
              MOVE 'M'       TO F716-NUM-RSL-STATUS                             
           ELSE                                                                 
              IF F558-NUM-HDB-REF-RSL NOT = SPACES AND LOW-VALUES               
                 MOVE 'D'       TO F716-NUM-RSL-STATUS                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F558-CDE-FLAT-TYPE-RESALE NOT = SPACES AND LOW-VALUES             
              IF F558-CDE-FLAT-TYPE-RESALE = 'E'                                
                 MOVE '06'            TO WS-NUM-RSL-FT                          
              ELSE                                                              
                 MOVE '0'             TO WS-NUM-RSL-FT(1:1)                     
                 MOVE F558-CDE-FLAT-TYPE-RESALE TO WS-NUM-RSL-FT(2:1)           
              END-IF                                                            
              MOVE WS-NUM-RSL-FT          TO F716-NUM-RSL-FT                    
              MOVE F558-DTE-APPROVAL-RSL  TO F716-DTE-APPROVAL-RSL              
           END-IF.                                                              
                                                                                
           PERFORM 260-WRITE-BP13F716 THRU 260-EXIT.                            
                                                                                
       250-EXIT.                                                                
           EXIT.                                                                
                                                                                
       260-WRITE-BP13F716.                                                      
           IF F716-NUM-RDB-DP-OM = 'D'                                          
              ADD 1 TO WS-RDB-DP-OM-D                                           
           ELSE                                                                 
              IF F716-NUM-RDB-DP-OM = 'M'                                       
                 ADD 1 TO WS-RDB-DP-OM-M                                        
              ELSE                                                              
                 IF F716-NUM-RDB-DP-OM = 'O'                                    
                    ADD 1 TO WS-RDB-DP-OM-O                                     
                 ELSE                                                           
                    IF F716-NUM-RDB-DP-OM = 'R'                                 
                       ADD 1 TO WS-RDB-DP-OM-R                                  
                    ELSE                                                        
                       IF F716-NUM-RDB-DP-OM = 'S'                              
                          ADD 1 TO WS-RDB-DP-OM-S                               
                       ELSE                                                     
                          IF F716-NUM-RDB-DP-OM = 'X'                           
                             ADD 1 TO WS-RDB-DP-OM-X                            
                          END-IF                                                
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE F558-CDE-HOUSEHOLD TO F716-CDE-HOUSEHOLD.                       
           WRITE BP13F716-REC.                                                  
           ADD 1 TO WS-F716-WRITE.                                              
                                                                                
       260-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
      *    ACCEPT WS-CUR-DATE FROM DATE.                                        
           MOVE FUNCTION CURRENT-DATE   TO  WS-CUR-DATE.                        
                                                                                
           DISPLAY 'PROGRAM-ID : BP13C788 '.                                    
           DISPLAY 'SPLIT F558 INTO TWO FILE FORMAT '.                          
           DISPLAY 'CURR-DATE  : ' WS-CUR-DATE.                                 
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS READ FROM F558 ' WS-F558-READ.                
           DISPLAY 'NO OF REORDS WRITE TO F716   ' WS-F716-WRITE.               
           DISPLAY '      F716 - D               ' WS-RDB-DP-OM-D.              
           DISPLAY '      F716 - M               ' WS-RDB-DP-OM-M.              
           DISPLAY '      F716 - O               ' WS-RDB-DP-OM-O.              
           DISPLAY '      F716 - R               ' WS-RDB-DP-OM-R.              
           DISPLAY '      F716 - S               ' WS-RDB-DP-OM-S.              
           DISPLAY '      F716 - X               ' WS-RDB-DP-OM-X.              
           DISPLAY 'NO OF REORDS WRITE TO F716A   ' WS-F716A-WRITE.             
                                                                                
           CLOSE BP13F558                                                       
                 P13F716A                                                       
                 BP13F716.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
