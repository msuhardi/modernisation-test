       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C085.                                                 
      *AUTHOR.        ED.                                                       
      *DATE-WRITTEN.  11 JUL 2002.                                              
      * ========================================================== *            
      * FLAT INVENTORY SYSTEM (BP13)                               *            
      * ========================================================== *            
      *  OBJECTIVE   : FOR SUCC GRO APPLICATION, IN ORDER TO GIVE  *            
      *                ANOTHER CHANCE UNDER PUBLIC SHORTLISTION    *            
      *                PROGRAM TO CREATE RECORD UNDER PA SAME REFNO*            
      *  INPUT FILE  : BP13F740                                    *            
      *  I-O   FILES : BP13K740                                                 
      *  OUTPUTFILE  : P13F740B                                                 
      * ---------------------------------------------------------- *            
      * CHG-NO   BY  DATE    DETAILS                               *            
      * -------- --- ------  -------                               *            
      * BP132171 ED  110702  NEW PGM                               *            
      * BP132412 SK  021002  ADD BP13K520                          *            
      * BP132563 CKK 150205  MOVE SPACES TO GRO-RANK AND WARD      *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
                                                                                
           SELECT BP13K740 ASSIGN TO BP13K740                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K740-KEY-FLD                               
                  FILE STATUS     IS WS-K740-STATUS.                            
                                                                                
           SELECT BP13K520 ASSIGN TO BP13K520                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K520-KEY-NEW                               
                  FILE STATUS     IS WS-K520-STATUS.                            
                                                                                
           SELECT P13F740B ASSIGN TO P13F740B.                                  
           SELECT BP13L085 ASSIGN TO BP13L085.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F740                                                            
            RECORD CONTAINS 500  CHARACTERS                                     
            BLOCK CONTAINS    0  RECORDS                                        
            RECORDING MODE   IS  F                                              
            LABEL RECORDS        STANDARD.                                      
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K740                                                            
            RECORD CONTAINS 500  CHARACTERS.                                    
       COPY BP13K740.                                                           
                                                                                
       FD   BP13K520                                                            
            RECORD CONTAINS 500  CHARACTERS.                                    
       COPY BP13K520.                                                           
                                                                                
       FD   P13F740B                                                            
            RECORD CONTAINS 500  CHARACTERS                                     
            BLOCK CONTAINS    0  RECORDS                                        
            RECORDING MODE   IS  F                                              
            LABEL RECORDS        STANDARD.                                      
       01   P13F740B-REC         PIC X(500).                                    
                                                                                
       FD  BP13L085                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS  132 CHARACTERS.                                     
                                                                                
       01  BP13L085-REC         PIC X(132).                                     
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-INDICATORS.                                                  
           05  WS-F740-EOF                PIC X(01) VALUE 'N'.                  
           05  WS-K520-FOUND              PIC X(01) VALUE 'N'.                  
           05  WS-K740-FOUND              PIC X(01) VALUE 'N'.                  
           05  WS-K740-STATUS             PIC 9(02) VALUE ZEROS.                
           05  WS-K520-STATUS             PIC 9(02) VALUE ZEROS.                
           05  WS-F740-READ               PIC 9(06) VALUE ZEROS.                
           05  WS-K740-DUPLICATE          PIC 9(06) VALUE ZEROS.                
           05  WS-K740-WRITE              PIC 9(06) VALUE ZEROS.                
           05  WS-F740-WRITE              PIC 9(06) VALUE ZEROS.                
           05  WS-K520-DUPLICATE          PIC 9(06) VALUE ZEROS.                
           05  WS-K520-WRITE              PIC 9(06) VALUE ZEROS.                
                                                                                
       01  WS-CURRENT-DATE.                                                     
           02  WS-DATE                    PIC X(8).                             
           02  WS-CURRENT-TIME            PIC X(9).                             
           02  FILLER                     PIC X(4).                             
                                                                                
       01  WS-CNT-LINE-ON-PAGE            PIC 9(8)    VALUE 66.                 
       01  WS-CNT-PRINT-L085              PIC 9(8)    VALUE 1.                  
       01  WS-CNT-WRITE-L085              PIC 9(8)    VALUE 0.                  
       01  WS-NO-OF-PAGE                  PIC 9(8)    VALUE 0.                  
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY                PIC 9(4).                             
           05  WS-SYS-MM                  PIC 9(2).                             
           05  WS-SYS-DD                  PIC 9(2).                             
                                                                                
       01  WS-EDITED-DATE                 PIC X(10).                            
                                                                                
      *--------------------------------------------------------*                
      *         BP13L085 -  R E P O R T    L A Y O U T        *                 
      *--------------------------------------------------------*                
                                                                                
       01  L085-PR-HEAD-01.                                                     
           05  FILLER                   PIC X(14) VALUE 'BP13L085'.             
           05  FILLER                   PIC X(8)  VALUE 'HDBCAT 3'.             
           05  FILLER                   PIC X(25) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(13) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE: '.               
           05  L085-RUN-DATE            PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'PAGE: '.               
           05  L085-PAGE-NO             PIC ZZZ9.                               
                                                                                
       01  L085-PR-HEAD-02.                                                     
           05  FILLER                   PIC X(55) VALUE SPACES.                 
           05  FILLER                   PIC X(73) VALUE                         
                'RECORD ADDED TO BALLOT FILE'.                                  
                                                                                
       01  L085-PR-HEAD-03.                                                     
           05  FILLER          PIC X(05)  VALUE ' S-NO'.                        
           05  FILLER          PIC X(01)  VALUE SPACES.                         
           05  FILLER          PIC X(30)  VALUE 'RANDOM NO'.                    
           05  FILLER          PIC X(01)  VALUE SPACES.                         
           05  FILLER          PIC X(08)  VALUE 'REGN NO'.                      
           05  FILLER          PIC X(01)  VALUE SPACES.                         
           05  FILLER          PIC X(09)  VALUE 'ALLO SCH'.                     
           05  FILLER          PIC X(01)  VALUE SPACES.                         
           05  FILLER          PIC X(09)  VALUE 'NEW TOWN'.                     
           05  FILLER          PIC X(01)  VALUE SPACES.                         
           05  FILLER          PIC X(10)  VALUE 'FLAT TYPE'.                    
           05  FILLER          PIC X(01)  VALUE SPACES.                         
           05  FILLER          PIC X(09)  VALUE 'ELIG SCH'.                     
           05  FILLER          PIC X(01)  VALUE SPACES.                         
           05  FILLER          PIC X(09)  VALUE 'ALLO CAT'.                     
                                                                                
       01  L085-PR-HEAD-04.                                                     
           05  FILLER                PIC X(132) VALUE ALL '-'.                  
                                                                                
       01  L085-PR-DETAILS-1.                                                   
           05  L085-NUMBER           PIC ZZZZ9  BLANK WHEN ZERO.                
           05  FILLER                PIC X(01)  VALUE SPACES.                   
           05  L085-RANDOM-NO        PIC X(28)  VALUE SPACES.                   
           05  FILLER                PIC X(02)  VALUE SPACES.                   
           05  L085-REF-NO           PIC X(08)  VALUE SPACES.                   
           05  FILLER                PIC X(04)  VALUE SPACES.                   
           05  L085-ALLOC-SCH        PIC X(06)  VALUE SPACES.                   
           05  FILLER                PIC X(04)  VALUE SPACES.                   
           05  L085-NEW-TOWN         PIC X(06)  VALUE SPACES.                   
           05  FILLER                PIC X(04)  VALUE SPACES.                   
           05  L085-FLAT-TYPE        PIC X(07)  VALUE SPACES.                   
           05  FILLER                PIC X(04)  VALUE SPACES.                   
           05  L085-ELIG-SCH         PIC X(06)  VALUE SPACES.                   
           05  FILLER                PIC X(03)  VALUE SPACES.                   
           05  L085-ALLOC-CAT        PIC X(06)  VALUE SPACES.                   
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
                                                                                
           PERFORM 100-INITIALIZATION     THRU 100-EXIT.                        
           PERFORM 200-READ-BP13F740      THRU 200-EXIT.                        
           PERFORM 300-PROCESS-F740       THRU 300-EXIT                         
             UNTIL WS-F740-EOF = 'Y'.                                           
           PERFORM 9990-CLOSE-FILES        THRU 9990-EXIT.                      
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-INITIALIZATION.                                                      
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F740                                                 
                  I-O  BP13K740                                                 
                       BP13K520                                                 
               OUTPUT  P13F740B                                                 
                       BP13L085.                                                
                                                                                
           IF WS-K740-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K740  OPEN ERROR ' WS-K740-STATUS               
              MOVE WS-K740-STATUS TO RETURN-CODE                                
              PERFORM 9990-CLOSE-FILES THRU 9990-EXIT                           
           END-IF.                                                              
                                                                                
           IF WS-K520-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K520  OPEN ERROR ' WS-K520-STATUS               
              MOVE WS-K520-STATUS TO RETURN-CODE                                
              PERFORM 9990-CLOSE-FILES THRU 9990-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATE.                      
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-BP13F740.                                                       
      *-------------------------------------------------------------            
           READ BP13F740 AT END                                                 
                MOVE 'Y'        TO WS-F740-EOF                                  
                GO  TO 200-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1       TO WS-F740-READ.                                         
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PROCESS-F740.                                                        
      *-------------------------------------------------------------            
                                                                                
           PERFORM 600-READ-K740          THRU 600-EXIT.                        
                                                                                
           IF WS-K740-FOUND = 'Y'                                               
              PERFORM 400-WRITE-BP13K740     THRU 400-EXIT                      
              PERFORM 500-WRITE-P13F740B     THRU 500-EXIT                      
           ELSE                                                                 
              PERFORM 700-READ-K520          THRU 700-EXIT                      
              IF WS-K520-FOUND = 'Y'                                            
                 PERFORM 800-WRITE-BP13K520     THRU 800-EXIT                   
                 PERFORM 500-WRITE-P13F740B     THRU 500-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-BP13F740      THRU 200-EXIT.                        
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-WRITE-BP13K740.                                                      
      *-------------------------------------------------------------            
           MOVE 'G'                 TO F740-NUM-RANDOM(28:1).                   
           MOVE 'GR'                TO F740-NUM-REGN(7:2).                      
           MOVE 'PA '               TO F740-CDE-ALLOC-SCH.                      
           MOVE SPACES              TO F740-CDE-SUCC-NT-ZONE.                   
           MOVE SPACES              TO F740-CDE-UNSUCC.                         
           MOVE BP13F740-REC        TO BP13K740-REC.                            
                                                                                
           WRITE BP13K740-REC.                                                  
                                                                                
           EVALUATE WS-K740-STATUS                                              
               WHEN 00                                                          
                    ADD 1        TO WS-K740-WRITE                               
               WHEN 22                                                          
                    ADD 1        TO WS-K740-DUPLICATE                           
                    DISPLAY 'DUPLICATE RECORD FOR BP13K740. KEY IS - '          
                    K740-KEY-FLD                                                
               WHEN OTHER                                                       
                    DISPLAY 'ERROR WRITING TO BP13K740. STATUS IS - '           
                    WS-K740-STATUS                                              
                    PERFORM 9990-CLOSE-FILES THRU 9990-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-WRITE-P13F740B.                                                      
      *-------------------------------------------------------------            
                                                                                
           WRITE P13F740B-REC         FROM BP13F740-REC.                        
           ADD   1                    TO   WS-F740-WRITE.                       
                                                                                
           PERFORM 850-MOVE-PARA      THRU 850-EXIT.                            
           PERFORM 900-PRINT-REPORT   THRU 900-EXIT.                            
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       600-READ-K740.                                                           
      *--------------------------------------------------------*                
                                                                                
           MOVE LOW-VALUES       TO K740-KEY-FLD.                               
           MOVE F740-NUM-RANDOM  TO K740-KEY-FLD.                               
           MOVE 'Y'              TO WS-K740-FOUND.                              
                                                                                
           READ BP13K740.                                                       
                                                                                
           EVALUATE WS-K740-STATUS                                              
              WHEN 00                                                           
                 MOVE 'Y' TO WS-K740-FOUND                                      
              WHEN 23                                                           
                 MOVE 'N' TO WS-K740-FOUND                                      
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN READ OF BP13K740. STATUS IS '                
                          WS-K740-STATUS                                        
                 PERFORM 9990-CLOSE-FILES THRU 9990-EXIT                        
            END-EVALUATE.                                                       
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       700-READ-K520.                                                           
      *--------------------------------------------------------*                
                                                                                
           MOVE LOW-VALUES      TO K520-NUM-REGN.                               
           MOVE F740-NUM-REGN   TO K520-NUM-REGN.                               
           MOVE 'Y'             TO WS-K520-FOUND.                               
                                                                                
           READ BP13K520.                                                       
                                                                                
           EVALUATE WS-K520-STATUS                                              
              WHEN 00                                                           
                 MOVE 'Y' TO WS-K520-FOUND                                      
              WHEN 23                                                           
                 MOVE 'N' TO WS-K520-FOUND                                      
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN READ OF BP13K520. STATUS IS '                
                          WS-K520-STATUS                                        
                 PERFORM 9990-CLOSE-FILES THRU 9990-EXIT                        
            END-EVALUATE.                                                       
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
       800-WRITE-BP13K520.                                                      
      *-------------------------------------------------------------            
           MOVE 'G'                 TO F740-NUM-RANDOM(28:1).                   
           MOVE 'GR'                TO F740-NUM-REGN(7:2).                      
           MOVE 'PA '               TO F740-CDE-ALLOC-SCH.                      
           MOVE SPACES              TO F740-NUM-GRO-RANK.                       
           MOVE SPACES              TO F740-NUM-WARD.                           
           MOVE SPACES              TO F740-CDE-SUCC-NT-ZONE.                   
           MOVE SPACES              TO F740-CDE-UNSUCC.                         
           MOVE BP13F740-REC        TO BP13K520-REC.                            
                                                                                
           WRITE BP13K520-REC.                                                  
           EVALUATE WS-K520-STATUS                                              
               WHEN 00                                                          
                    ADD 1        TO WS-K520-WRITE                               
               WHEN 22                                                          
                    ADD 1        TO WS-K520-DUPLICATE                           
                    DISPLAY 'DUPLICATE RECORD FOR BP13K520. KEY IS - '          
                    K520-NUM-REGN                                               
               WHEN OTHER                                                       
                    DISPLAY 'ERROR WRITING TO BP13K520. STATUS IS - '           
                    WS-K520-STATUS                                              
                    PERFORM 9990-CLOSE-FILES THRU 9990-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
       850-MOVE-PARA.                                                           
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                   TO  L085-PR-DETAILS-1.                 
           INITIALIZE                        L085-PR-DETAILS-1.                 
                                                                                
           MOVE F740-NUM-RANDOM          TO  L085-RANDOM-NO.                    
           MOVE F740-NUM-REF             TO  L085-REF-NO.                       
           MOVE F740-CDE-ALLOC-SCH       TO  L085-ALLOC-SCH.                    
           MOVE F740-CDE-NT1             TO  L085-NEW-TOWN.                     
           MOVE F740-CDE-FLAT-TYPE       TO  L085-FLAT-TYPE.                    
           MOVE F740-NUM-ALLO-CAT        TO  L085-ALLOC-CAT.                    
           MOVE F740-CDE-ELIG-SCH        TO  L085-ELIG-SCH.                     
                                                                                
       850-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-PRINT-REPORT.                                                        
      *-------------------------------------------------------------            
                                                                                
           IF WS-CNT-LINE-ON-PAGE > 55                                          
              PERFORM 910-PRINT-HEADING THRU 910-EXIT                           
              MOVE 5 TO WS-CNT-LINE-ON-PAGE                                     
           END-IF.                                                              
                                                                                
           MOVE WS-CNT-PRINT-L085  TO  L085-NUMBER                              
                                                                                
           WRITE BP13L085-REC  FROM  L085-PR-DETAILS-1 AFTER 1.                 
           ADD   1      TO  WS-CNT-LINE-ON-PAGE                                 
                            WS-CNT-PRINT-L085                                   
                            WS-CNT-WRITE-L085.                                  
           MOVE  SPACES TO  L085-PR-DETAILS-1.                                  
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
       910-PRINT-HEADING.                                                       
      *-------------------------------------------------------------            
                                                                                
           ADD  1                TO     WS-NO-OF-PAGE.                          
           MOVE WS-NO-OF-PAGE    TO     L085-PAGE-NO.                           
           MOVE WS-EDITED-DATE   TO     L085-RUN-DATE.                          
                                                                                
           MOVE SPACES           TO     BP13L085-REC.                           
                                                                                
           WRITE BP13L085-REC    AFTER  PAGE.                                   
           WRITE BP13L085-REC    FROM   L085-PR-HEAD-01 AFTER 1.                
           WRITE BP13L085-REC    FROM   L085-PR-HEAD-02 AFTER 1.                
           WRITE BP13L085-REC    FROM   L085-PR-HEAD-03 AFTER 2.                
           WRITE BP13L085-REC    FROM   L085-PR-HEAD-04 AFTER 1.                
                                                                                
       910-EXIT.                                                                
           EXIT.                                                                
                                                                                
       9990-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           CLOSE BP13F740                                                       
                 BP13K740                                                       
                 BP13K520                                                       
                 P13F740B                                                       
                 BP13L085.                                                      
                                                                                
           IF WS-K740-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K740  CLOSE ERROR ' WS-K740-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K520-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K520  CLOSE ERROR ' WS-K520-STATUS              
           END-IF.                                                              
                                                                                
           DISPLAY '============ BP13C085 STATISTICS =============='.           
           DISPLAY ' '                                                          
           DISPLAY 'BP13F740 RECORDS READ       ' WS-F740-READ                  
           DISPLAY 'DUPLICATE BP13K740 RECORDS  ' WS-K740-DUPLICATE             
           DISPLAY 'BP13K740 RECORDS WRITTEN    ' WS-K740-WRITE                 
           DISPLAY 'DUPLICATE BP13K520 RECORDS  ' WS-K520-DUPLICATE             
           DISPLAY 'BP13K520 RECORDS WRITTEN    ' WS-K520-WRITE                 
           DISPLAY 'P13F740B RECORDS WRITTEN    ' WS-F740-WRITE                 
           DISPLAY ' '                                                          
           DISPLAY '==============================================='.           
           STOP RUN.                                                            
                                                                                
       9990-EXIT.                                                               
           EXIT.                                                                
