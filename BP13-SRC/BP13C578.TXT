       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C578.                                                 
      *AUTHOR.        YPH.                                                      
      *DATE-WRITTEN.  29/11/94.                                                 
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMENCEMENT                *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE  :                                        *                
      *        1.  THIS PROGRAM WILL PRINT OUT THE LIST OF NOT *                
      *            ELIGIBLE REQUEST FOR RFS.                   *                
      *        2.  THIS PROGRAM WILL PRINT OUT THE LIST OF     *                
      *            ELIGIBLE REQUEST AND ALREADY REGISTERED     *                
      *            WITH REGN NO IN SOC MASTER.                 *                
      *                                                        *                
      *     INPUT FILES :                                      *                
      *       1. BP13F520                                      *                
      *       2. BP13K825                                      *                
      *                                                        *                
      *     LISTING:                                           *                
      *       1. NOT ELIGIBLE REQUEST FOR SELECTION (P13L578A) *                
      *       2. ELIGIBLE REQUEST FOR SELECTION (P13L578B) BUT *                
      *          WITH REGN-NO.                                 *                
      *                                                        *                
      * CHG-NO  BY   ON       DESCRIPTION                      *                
      * ---------------------------------                      *                
      * N940014 YPH 291194 - SOC PH9                           *                
      * C9502   YPH 180294 - INCLUDE SHORTLISTED (SL) TAG      *                
      * BP13006 RG  190795 - INCLUDE ALL ESSENTIAL OCCUP DTLS  *                
      *BP130109 LSB 210596 - CHG NUM-REF TO NUM-REGN           *                
      *BP130418 SIA 070798 - CATER FOR YR 2000                 *                
      *BP130652 NOR 200299 - EXPAND F520 RECL FROM 300 TO 500  *                
      * ====================================================== *                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F520 ASSIGN TO BP13F520.                                  
           SELECT BP13K825 ASSIGN TO BP13K825                                   
              ORGANIZATION        IS INDEXED                                    
              ACCESS MODE         IS DYNAMIC                                    
              RECORD KEY          IS K825-KEY-FLD                               
              FILE STATUS         IS K825-STATUS.                               
           SELECT BP13K800 ASSIGN TO BP13K800                                   
              ORGANIZATION        IS INDEXED                                    
              ACCESS MODE         IS RANDOM                                     
              RECORD KEY          IS K800-NUM-REGN                              
              FILE STATUS         IS K800-STATUS.                               
           SELECT P13L578A ASSIGN TO P13L578A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F520         BLOCK CONTAINS 0 RECORDS                            
                            RECORD CONTAINS 500 CHARACTERS                      
                            RECORDING MODE IS F                                 
                            LABEL RECORDS ARE STANDARD.                         
       COPY BP13F520.                                                           
                                                                                
       FD  BP13K825         RECORD CONTAINS 200 CHARACTERS.                     
                                                                                
       COPY BP13K825.                                                           
                                                                                
       FD  BP13K800         RECORD CONTAINS 2000 CHARACTERS.                    
                                                                                
       COPY BP13K800.                                                           
                                                                                
       FD  P13L578A                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED.                                           
       01  PRINT-REC-1                 PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           03  WS-PAGE             PIC 9(4)    VALUE ZEROS.                     
           03  WS-CNT              PIC 9(2)    VALUE ZEROS.                     
           03  WS-SEQ-NO           PIC 9(4)    VALUE ZEROS.                     
           03  WS-PAGE-2           PIC 9(4)    VALUE ZEROS.                     
           03  WS-CNT-2            PIC 9(2)    VALUE ZEROS.                     
           03  WS-SEQ-NO-2         PIC 9(4)    VALUE ZEROS.                     
                                                                                
       01  WS-FLAGS.                                                            
           03  EOF-F520            PIC X       VALUE SPACES.                    
           03  DTL1-SW             PIC X       VALUE SPACES.                    
           03  K800-STATUS         PIC 99      VALUE ZEROS.                     
           03  K825-STATUS         PIC 99      VALUE ZEROS.                     
                                                                                
       01  WS-EDITED-DATE.                                                      
           03  WS-DD               PIC 99.                                      
           03  FILLER              PIC X      VALUE '/'.                        
           03  WS-MM               PIC 99.                                      
           03  FILLER              PIC X      VALUE '/'.                        
           03  WS-YY               PIC 9(4).                                    
                                                                                
       01  WS-CUR-DATE.                                                         
           03  WS-YY               PIC 9(4).                                    
           03  WS-MM               PIC 99.                                      
           03  WS-DD               PIC 99.                                      
                                                                                
       01  L578-PR-HEAD-1.                                                      
           03  L578-ID             PIC X(8).                                    
           03  FILLER              PIC X(6)   VALUE SPACES.                     
           03  FILLER              PIC X(25)  VALUE 'HDB3'.                     
           03  FILLER              PIC X(62)  VALUE                             
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           03  FILLER              PIC X(6)   VALUE 'DATE: '.                   
           03  L578-DATE           PIC X(10).                                   
           03  FILLER              PIC X(10)  VALUE '    PAGE: '.               
           03  L578-PAGE           PIC ZZZ9.                                    
                                                                                
       01  L578-PR-HEAD-2.                                                      
           03  FILLER              PIC X(36)  VALUE SPACES.                     
           03  FILLER              PIC X(95)  VALUE                             
              'LISTING OF INELIGIBLE REQUESTS FOR SELECTION'.                   
                                                                                
       01  L578-PR-HEAD-3.                                                      
           03  FILLER              PIC X(132) VALUE ALL '-'.                    
                                                                                
       01  L578-PR-HEAD-4.                                                      
           03  FILLER              PIC X(33)  VALUE SPACES.                     
           03  FILLER              PIC X(15)  VALUE 'TOTAL CASES : '.           
           03  L578-TOTAL-CASE-NO  PIC ZZZ9.                                    
                                                                                
       01  L578-PR-HEAD-5.                                                      
           03  FILLER                   PIC X(02)  VALUE SPACES.                
           03  FILLER                   PIC X(06) VALUE                         
               'S/NO  '.                                                        
           03  FILLER                   PIC X(60) VALUE                         
               'REFNO                   ZE/NT /FT   CAT'.                       
           03  FILLER                   PIC X(64) VALUE                         
               'EXIST REGN    ZE/NT /FT    DTE-BALLOT   REMARKS'.               
                                                                                
       01  L578-PR-HEAD-6.                                                      
           03  FILLER                   PIC X(08)  VALUE SPACES.                
           03  FILLER                   PIC X(60) VALUE                         
               'NRIC     /NAME            '.                                    
           03  FILLER                   PIC X(64) VALUE                         
               'CUR-RELN      EXIST-HA'.                                        
                                                                                
       01  L578-PR-DETAIL-1.                                                    
           03  FILLER              PIC X(2)    VALUE SPACES.                    
           03  L578-SEQ-NO         PIC ZZZZ9.                                   
           03  FILLER              PIC X(1)    VALUE SPACES.                    
           03  L578-REGN-NEW       PIC X(8).                                    
           03  FILLER              PIC X(16)   VALUE SPACES.                    
           03  L578-ZE             PIC X(2).                                    
           03  FILLER              PIC X       VALUE '/'.                       
           03  L578-NT             PIC X(3).                                    
           03  FILLER              PIC X       VALUE '/'.                       
           03  L578-FT             PIC X(2).                                    
           03  FILLER              PIC X(4)    VALUE SPACES.                    
           03  L578-CAT            PIC X(1).                                    
           03  FILLER              PIC X(22)   VALUE SPACES.                    
           03  L578-EXIST-REGN     PIC X(8).                                    
           03  FILLER              PIC X(6)    VALUE SPACES.                    
           03  L578-ZE-K800        PIC X(2).                                    
           03  FILLER              PIC X       VALUE '/'.                       
           03  L578-NT-K800        PIC X(3).                                    
           03  FILLER              PIC X       VALUE '/'.                       
           03  L578-FT-K800        PIC X(2).                                    
           03  FILLER              PIC X(7)    VALUE SPACES.                    
           03  L578-DTE-BALLOT     PIC X(6).                                    
           03  FILLER              PIC X(4)    VALUE SPACES.                    
           03  L578-REMARK         PIC X(18).                                   
                                                                                
       01  L578-PR-DETAIL-2.                                                    
           03  FILLER                   PIC X(8)    VALUE SPACES.               
           03  L578-NRIC1               PIC X(9).                               
           03  FILLER                   PIC X(1)    VALUE '/'.                  
           03  L578-NAME                PIC X(46).                              
           03  FILLER                   PIC X(4)    VALUE SPACES.               
           03  L578-RELN                PIC X(2).                               
           03  FILLER                   PIC X(12)   VALUE SPACES.               
           03  L578-HA                  PIC X(4).                               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       100-MAIN.                                                                
           OPEN INPUT BP13F520                                                  
                      BP13K825                                                  
                      BP13K800                                                  
               OUTPUT P13L578A.                                                 
                                                                                
           IF K825-STATUS NOT = ZEROS AND 97                                    
              DISPLAY 'OPENING ERROR, K825-STATUS : ' K825-STATUS               
              MOVE K825-STATUS TO RETURN-CODE                                   
              PERFORM  900-CLOSE-ROUTINE THRU 900-EXIT.                         
                                                                                
           IF K800-STATUS NOT = ZEROS AND 97                                    
              DISPLAY 'OPENING ERROR, K800-STATUS : ' K800-STATUS               
              MOVE K800-STATUS TO RETURN-CODE                                   
              PERFORM  900-CLOSE-ROUTINE THRU 900-EXIT.                         
                                                                                
           MOVE ZEROS TO WS-COUNTERS.                                           
           MOVE 60 TO WS-CNT, WS-CNT-2.                                         
           MOVE SPACES TO WS-FLAGS.                                             
                                                                                
           PERFORM 200-READ-F520 THRU 200-EXIT.                                 
                                                                                
           PERFORM 300-PR-REPORT THRU 300-EXIT                                  
                   UNTIL EOF-F520 = 'Y'                                         
           PERFORM 700-PR-TOTAL THRU 700-EXIT.                                  
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       200-READ-F520.                                                           
            READ BP13F520 AT END                                                
                 MOVE 'Y' TO EOF-F520.                                          
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *******************************************************                   
      *                                                     *                   
      *  PRINT OUT THE LISTING OF NOT ELIGIBLE REQUESTS FOR *                   
      *  SELECTION                                          *                   
      *                                                     *                   
      *******************************************************                   
                                                                                
       300-PR-REPORT.                                                           
      *                                                                         
           IF F520-CDE-ELIG-TAG   = 'AS' OR 'DP' OR 'AB' OR 'AI' OR '04'        
                                 OR 'AW' OR 'AA' OR 'AG' OR '05' OR 'SS'        
                                 OR 'RB' OR 'RS' OR 'RI' OR '03' OR 'MR'        
                                 OR 'RW' OR 'RA' OR 'RE' OR 'RJ' OR 'CC'        
                                 OR 'S ' OR ' S' OR 'SL'                        
                PERFORM 400-PRINT-UNELIG-CASE THRU 400-EXIT.                    
                                                                                
            PERFORM 310-START-BP13K825   THRU 310-EXIT.                         
            PERFORM 200-READ-F520        THRU 200-EXIT.                         
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       310-START-BP13K825.                                                      
                                                                                
           MOVE SPACES           TO   BP13K825-REC.                             
           MOVE F520-NUM-REGN    TO   K825-NUM-REGN.                            
           START BP13K825 KEY    NOT  <  K825-KEY-FLD.                          
           IF K825-STATUS = '00'                                                
              MOVE '0'                       TO DTL1-SW                         
              PERFORM 400-PRINT-UNELIG-CASE  THRU 400-EXIT                      
                       UNTIL K825-NUM-REGN NOT = F520-NUM-REGN                  
           ELSE                                                                 
              DISPLAY 'BP13K825 START ERROR FOR REFNO : ' F520-NUM-REGN         
                       ' STATUS IS ' K825-STATUS.                               
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-PRINT-UNELIG-CASE.                                                   
                                                                                
           READ BP13K825  NEXT.                                                 
                                                                                
           IF K825-NUM-REGN NOT = F520-NUM-REGN                                 
              GO TO 400-EXIT.                                                   
                                                                                
           IF K825-NUM-LESSEE-CHECK NOT = 'Y'                                   
              GO TO 400-EXIT.                                                   
                                                                                
           IF WS-CNT > 50                                                       
              PERFORM 800-PR-HEADER THRU 800-EXIT.                              
                                                                                
           IF DTL1-SW = '0'                                                     
              PERFORM 410-PRINT-DTL-LINE1   THRU 410-EXIT                       
              MOVE '1'  TO DTL1-SW                                              
           END-IF                                                               
                                                                                
           MOVE  K825-NUM-NRIC            TO    L578-NRIC1.                     
           MOVE  K825-NME-OCCP            TO    L578-NAME.                      
           MOVE  K825-NUM-RELATIONSHIP    TO    L578-RELN                       
           PERFORM 600-GET-STATUS         THRU  600-EXIT                        
           WRITE PRINT-REC-1 FROM L578-PR-DETAIL-2 AFTER 1.                     
           ADD 1 TO WS-CNT.                                                     
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       410-PRINT-DTL-LINE1.                                                     
                                                                                
           ADD 1                 TO WS-SEQ-NO.                                  
           MOVE WS-SEQ-NO        TO L578-SEQ-NO.                                
           MOVE F520-NUM-REGN    TO L578-REGN-NEW.                              
                                                                                
           MOVE  SPACES                   TO L578-NT.                           
           MOVE  F520-CDE-ZONE            TO L578-ZE.                           
           MOVE  F520-CDE-NT1             TO L578-NT.                           
           MOVE  F520-CDE-FLAT-TYPE       TO L578-FT.                           
           MOVE  F520-CDE-CATEGORY        TO L578-CAT.                          
           MOVE  F520-NUM-EXIST-REGN      TO L578-EXIST-REGN.                   
                                                                                
           PERFORM 500-READ-BP13K800   THRU  500-EXIT.                          
                                                                                
           EVALUATE F520-CDE-ELIG-TAG                                           
               WHEN 'S '                                                        
                     MOVE 'ACCEPTED FLAT(HA1)' TO L578-REMARK                   
               WHEN ' S'                                                        
                     MOVE 'ACCEPTED FLAT(HA2)' TO L578-REMARK                   
               WHEN 'SS'                                                        
                     MOVE 'ACCEPTED FLAT (SS)' TO L578-REMARK                   
               WHEN 'SL'                                                        
                     MOVE 'SHORTLISTED   (SL)' TO L578-REMARK                   
               WHEN '03'                                                        
                     MOVE 'ACCEPTED FLAT (03)' TO L578-REMARK                   
               WHEN 'AG'                                                        
                     MOVE 'ACCEPTED FLAT (AG)' TO L578-REMARK                   
               WHEN 'AS'                                                        
                     MOVE 'ACCEPTED FLAT (AS)' TO L578-REMARK                   
               WHEN 'AB'                                                        
                     MOVE 'ACCEPTED FLAT (AB)' TO L578-REMARK                   
               WHEN 'AI'                                                        
                     MOVE 'ACCEPTED FLAT (AI)' TO L578-REMARK                   
               WHEN 'AW'                                                        
                     MOVE 'ACCEPTED FLAT (AW)' TO L578-REMARK                   
               WHEN 'AA'                                                        
                     MOVE 'ACCEPTED FLAT (AA)' TO L578-REMARK                   
               WHEN 'RB'                                                        
                     MOVE 'REJECTED FLAT (RB)' TO L578-REMARK                   
               WHEN 'RS'                                                        
                     MOVE 'REJECTED FLAT (RS)' TO L578-REMARK                   
               WHEN 'RI'                                                        
                     MOVE 'REJECTED FLAT (RI)' TO L578-REMARK                   
               WHEN 'RW'                                                        
                     MOVE 'REJECTED FLAT (RW)' TO L578-REMARK                   
               WHEN 'RA'                                                        
                     MOVE 'REJECTED FLAT (RA)' TO L578-REMARK                   
               WHEN 'RE'                                                        
                     MOVE 'REJECTED FLAT (RE)' TO L578-REMARK                   
               WHEN 'RJ'                                                        
                     MOVE 'REJECTED FLAT (RJ)' TO L578-REMARK                   
               WHEN 'MR'                                                        
                     MOVE 'MULTIPLE REQUEST  ' TO L578-REMARK                   
               WHEN 'CC'                                                        
                     MOVE 'CANCELLED REQ (CC)' TO L578-REMARK                   
               WHEN OTHER                                                       
                     MOVE SPACES               TO L578-REMARK                   
           END-EVALUATE                                                         
           MOVE SPACES TO PRINT-REC-1.                                          
           WRITE PRINT-REC-1 FROM L578-PR-DETAIL-1 AFTER 2.                     
           ADD 2 TO WS-CNT.                                                     
                                                                                
       410-EXIT.                                                                
            EXIT.                                                               
                                                                                
       500-READ-BP13K800.                                                       
            MOVE F520-NUM-EXIST-REGN      TO K800-NUM-REGN                      
            READ BP13K800                                                       
            IF K800-STATUS NOT = '00'                                           
               MOVE ' - '                 TO L578-HA                            
               DISPLAY 'NO MATCHING BP13K800 RECORD FOR REGNO '                 
                        F520-NUM-EXIST-REGN                                     
            ELSE                                                                
               MOVE K800-NUM-ZONE         TO L578-ZE-K800                       
               MOVE K800-NUM-NEW-TOWN     TO L578-NT-K800                       
               MOVE K800-NUM-FLAT-TYPE    TO L578-FT-K800                       
               MOVE K800-DTE-BALLOT       TO L578-DTE-BALLOT.                   
       500-EXIT.                                                                
            EXIT.                                                               
                                                                                
       600-GET-STATUS.                                                          
            IF K825-NUM-NRIC  = K800-NUM-NRIC1                                  
               MOVE 'HA1 '             TO L578-HA                               
            ELSE                                                                
            IF K825-NUM-NRIC  = K800-NUM-NRIC2                                  
               MOVE 'HA2 '             TO L578-HA                               
            ELSE                                                                
            IF K825-NUM-NRIC  = K800-NUM-NRIC3                                  
               MOVE 'HA3 '             TO L578-HA                               
            ELSE                                                                
            IF K825-NUM-NRIC  = K800-NUM-NRIC4                                  
               MOVE 'HA4 '             TO L578-HA                               
            ELSE                                                                
               MOVE ' -  '             TO L578-HA                               
               DISPLAY 'NO MATCHING NRIC FOR REGNO. '                           
                        F520-NUM-EXIST-REGN.                                    
       600-EXIT.                                                                
            EXIT.                                                               
                                                                                
       700-PR-TOTAL.                                                            
           IF WS-SEQ-NO > 0 THEN                                                
              MOVE WS-SEQ-NO TO L578-TOTAL-CASE-NO                              
              MOVE SPACES TO PRINT-REC-1                                        
              WRITE PRINT-REC-1 FROM L578-PR-HEAD-4 AFTER 2.                    
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
       800-PR-HEADER.                                                           
            ADD 1 TO WS-PAGE.                                                   
            MOVE WS-PAGE TO L578-PAGE.                                          
            MOVE 'P13L578A' TO L578-ID.                                         
            MOVE FUNCTION CURRENT-DATE TO WS-CUR-DATE.                          
            MOVE CORR WS-CUR-DATE TO WS-EDITED-DATE.                            
            MOVE WS-EDITED-DATE TO L578-DATE.                                   
            WRITE PRINT-REC-1 FROM L578-PR-HEAD-1 AFTER PAGE.                   
            WRITE PRINT-REC-1 FROM L578-PR-HEAD-2 AFTER 1.                      
            WRITE PRINT-REC-1 FROM L578-PR-HEAD-5 AFTER 2.                      
            WRITE PRINT-REC-1 FROM L578-PR-HEAD-6 AFTER 1.                      
            WRITE PRINT-REC-1 FROM L578-PR-HEAD-3 AFTER 2.                      
            MOVE 0 TO WS-CNT.                                                   
                                                                                
       800-EXIT.                                                                
            EXIT.                                                               
                                                                                
       900-CLOSE-ROUTINE.                                                       
           CLOSE BP13F520                                                       
                 BP13K825                                                       
                 BP13K800                                                       
                 P13L578A.                                                      
                                                                                
           IF K825-STATUS NOT = ZEROS                                           
              DISPLAY 'CLOSING ERROR, K825-STATUS : ' K825-STATUS               
              MOVE K825-STATUS TO RETURN-CODE.                                  
                                                                                
           IF K800-STATUS NOT = ZEROS                                           
              DISPLAY 'CLOSING ERROR, K800-STATUS : ' K800-STATUS               
              MOVE K800-STATUS TO RETURN-CODE.                                  
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
