       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13CB89.                                                  
       AUTHOR.       ALJOHN MONCADA.                                            
       DATE-WRITTEN. 24/05/2018.                                                
                                                                                
      *****************************************************************         
      *                SYSTEM NAME (BP13)                             *         
      *===============================================================*         
      *  OBJECTIVES :                                                 *         
      *                                                               *         
      *      MATCH SEQUENTIAL INPUT BP13F740 WITH VSAM BP13K757       *         
      *      USING NUM-REGN.                                          *         
      *      IF RECORD FOUND IN BP13K757                              *         
      *         MATCH RECORD FOUND IN BP13K757 WITH VSAM BP13K820     *         
      *         USING NUM-REGN AND UIN FIELDS. PROCESS NUM-EXIST-REGN *         
      *         ALSO IF CDE-ALLOC-SCH = 'GRO'.                        *         
      *         IF RECORD FOUND IN BP13K820                           *         
      *            UPDATE NUM-EMPLY-STATUS, NUM-NS-LT12MTH AND        *         
      *            NUM-EDN-LT12MTH IN BP13K820 BASED ON BP13K757.     *         
      *         ELSE                                                  *         
      *            WRITE RECORDS TO BP13OUT1 FILE                     *         
      *      ELSE                                                     *         
      *         WRITE RECORDS TO BP13OUT2 FILE                        *         
      *                                                               *         
      *===============================================================*         
      * FILES DESCRIPTION :-                                          *         
      *                                                               *         
      *                ACCESS     REC                                 *         
      * DD NAME  I/O/E MODE       LEN  KEY FIELDS   REMARKS           *         
      * -------- ----- ---------  ---- ------------ ----------------- *         
      * BP13F740 I     SEQUENTIAL 0500                                *         
      * BP13K757 I     RANDOM     3000 K757-KEY-FLD                   *         
      * BP13K820 I/O   RANDOM     0400 K820-KEY-FLD                   *         
      * BP13OUT1 O     SEQUENTIAL 0017                                *         
      * BP13OUT2 O     SEQUENTIAL 0008                                *         
      *                                                               *         
      *===============================================================*         
      * MODIFICATIONS :                                               *         
      *                                                               *         
      * CHGE REQ # DATE     AUTHOR REMARKS                            *         
      * ---------- -------- ------ ---------------------------------- *         
      * BP137356   24/05/18 AM25   NEW PROGRAM                        *         
      * BP137783   14/05/19 AM25   CATER FOR NEW FLD DIVORCE-FNLJDGMT *         
      * BP139280   27/10/23 EAA2   EXPAND BP13K757 FROM 3000 TO 4000  *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN      TO BP13F740.                             
                                                                                
           SELECT BP13K757 ASSIGN      TO BP13K757                              
              ORGANIZATION IS INDEXED                                           
              ACCESS MODE  IS RANDOM                                            
              RECORD KEY   IS K757-KEY-FLD                                      
              FILE STATUS  IS WS-K757-STATUS.                                   
                                                                                
           SELECT BP13K820 ASSIGN      TO BP13K820                              
              ORGANIZATION IS INDEXED                                           
              ACCESS MODE  IS RANDOM                                            
              RECORD KEY   IS K820-KEY-FLD                                      
              FILE STATUS  IS WS-K820-STATUS.                                   
                                                                                
           SELECT BP13OUT1 ASSIGN      TO BP13OUT1.                             
                                                                                
           SELECT BP13OUT2 ASSIGN      TO BP13OUT2.                             
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F740                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORD IS STANDARD                                             
           RECORDING MODE IS F.                                                 
       COPY BP13F740.                                                           
                                                                                
       FD  BP13K757                                                             
           RECORD CONTAINS 4000 CHARACTERS.                                     
       COPY BP13K757.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD BP13OUT1                                                              
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 17 CHARACTERS                                        
           LABEL RECORD IS STANDARD                                             
           RECORDING MODE IS F.                                                 
       01  BP13OUT1-REC.                                                        
           05  OUT1-NUM-REGN           PIC X(8).                                
           05  OUT1-NUM-NRIC           PIC X(9).                                
                                                                                
       FD  BP13OUT2                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 8 CHARACTERS                                         
           LABEL RECORD IS STANDARD                                             
           RECORDING MODE IS F.                                                 
       01  BP13OUT2-REC.                                                        
           05  OUT2-NUM-REGN           PIC X(8).                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NUM-REGN             PIC X(8)        VALUE SPACES.            
           05  WS-NUM-NRIC             PIC X(9)        VALUE SPACES.            
           05  WS-EMPLY-STAT           PIC X(3)        VALUE SPACES.            
           05  WS-NS-LT12MTH           PIC X(1)        VALUE SPACE.             
           05  WS-EDN-LT12MTH          PIC X(1)        VALUE SPACE.             
           05  WS-DIVORCE-FNLJDGMT     PIC X(1)        VALUE SPACE.             
           05  WS-K757-STATUS          PIC 9(2)        VALUE ZEROES.            
           05  WS-K820-STATUS          PIC 9(2)        VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F740-CNT-READ        PIC 9(5)        VALUE ZEROES.            
           05  WS-K757-CNT-READ        PIC 9(5)        VALUE ZEROES.            
           05  WS-K820-CNT-READ        PIC 9(5)        VALUE ZEROES.            
           05  WS-K820-CNT-REWRT       PIC 9(5)        VALUE ZEROES.            
           05  WS-OUT1-CNT-WRT         PIC 9(5)        VALUE ZEROES.            
           05  WS-OUT2-CNT-WRT         PIC 9(5)        VALUE ZEROES.            
                                                                                
       01  WS-EOF-FLAGS.                                                        
           05  WS-F740-EOF             PIC X           VALUE 'N'.               
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 2000-READ-BP13F740    THRU 2000-EXIT.                        
           PERFORM 3000-READ-BP13K757    THRU 3000-EXIT                         
              UNTIL WS-F740-EOF = 'Y'.                                          
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN INPUT  BP13F740                                                 
                       BP13K757                                                 
                I-O    BP13K820                                                 
                OUTPUT BP13OUT1                                                 
                       BP13OUT2.                                                
                                                                                
           IF WS-K757-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPEN BP13K757 FAIL STATUS=' WS-K757-STATUS               
              MOVE WS-K757-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPEN BP13K820 FAIL STATUS=' WS-K820-STATUS               
              MOVE WS-K820-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-READ-BP13F740.                                                      
      *****************************************************************         
                                                                                
           READ BP13F740                                                        
              AT END                                                            
                 MOVE 'Y'              TO WS-F740-EOF                           
              NOT AT END                                                        
                 ADD 1                 TO WS-F740-CNT-READ                      
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-READ-BP13K757.                                                      
      *****************************************************************         
                                                                                
           MOVE F740-NUM-REGN          TO K757-NUM-REGN.                        
                                                                                
           READ BP13K757.                                                       
                                                                                
           EVALUATE WS-K757-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K757-CNT-READ                      
                                                                                
                 MOVE K757-NUM-REGN    TO WS-NUM-REGN                           
                 PERFORM 3100-PROCESS-BP13K757 THRU 3100-EXIT                   
                                                                                
                 IF F740-CDE-ALLOC-SCH = 'GRO'                                  
                    MOVE F740-NUM-EXIST-REGN                                    
                                       TO WS-NUM-REGN                           
                    PERFORM 3100-PROCESS-BP13K757 THRU 3100-EXIT                
                 END-IF                                                         
              WHEN 23                                                           
                 MOVE SPACES           TO BP13OUT2-REC                          
                 INITIALIZE BP13OUT2-REC                                        
                                                                                
                 MOVE F740-NUM-REGN    TO OUT2-NUM-REGN                         
                                                                                
                 WRITE BP13OUT2-REC                                             
                 ADD 1                 TO WS-OUT2-CNT-WRT                       
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K757 FAIL STATUS=' WS-K757-STATUS            
                 MOVE WS-K757-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
           PERFORM 2000-READ-BP13F740 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3100-PROCESS-BP13K757.                                                   
      *****************************************************************         
                                                                                
           MOVE WS-NUM-REGN            TO K820-NUM-REGN.                        
                                                                                
           IF (K757-NUM-APPLT1-UIN NOT = SPACES AND LOW-VALUES)                 
              IF K757-NUM-APPLT1-UIN(1:1) = '-'                                 
                 MOVE 'X'              TO K757-NUM-APPLT1-UIN(1:1)              
              END-IF                                                            
                                                                                
              MOVE K757-NUM-APPLT1-UIN TO WS-NUM-NRIC                           
              MOVE K757-NUM-APPLT1-EMPLY-STATUS                                 
                                       TO WS-EMPLY-STAT                         
              MOVE K757-NUM-NS-LT12MTH-HA1                                      
                                       TO WS-NS-LT12MTH                         
              MOVE K757-NUM-EDN-LT12MTH-HA1                                     
                                       TO WS-EDN-LT12MTH                        
              MOVE K757-NUM-DIVORCE-FNLJDGMT-HA1                                
                                       TO WS-DIVORCE-FNLJDGMT                   
                                                                                
              PERFORM 3200-READ-BP13K820 THRU 3200-EXIT                         
           END-IF.                                                              
                                                                                
           IF (K757-NUM-APPLT2-UIN NOT = SPACES AND LOW-VALUES)                 
              IF K757-NUM-APPLT2-UIN(1:1) = '-'                                 
                 MOVE 'X'              TO K757-NUM-APPLT2-UIN(1:1)              
              END-IF                                                            
                                                                                
              MOVE K757-NUM-APPLT2-UIN TO WS-NUM-NRIC                           
              MOVE K757-NUM-APPLT2-EMPLY-STATUS                                 
                                       TO WS-EMPLY-STAT                         
              MOVE K757-NUM-NS-LT12MTH-HA2                                      
                                       TO WS-NS-LT12MTH                         
              MOVE K757-NUM-EDN-LT12MTH-HA2                                     
                                       TO WS-EDN-LT12MTH                        
              MOVE K757-NUM-DIVORCE-FNLJDGMT-HA2                                
                                       TO WS-DIVORCE-FNLJDGMT                   
                                                                                
              PERFORM 3200-READ-BP13K820 THRU 3200-EXIT                         
           END-IF.                                                              
                                                                                
           IF (K757-NUM-APPLT3-UIN NOT = SPACES AND LOW-VALUES)                 
              IF K757-NUM-APPLT3-UIN(1:1) = '-'                                 
                 MOVE 'X'              TO K757-NUM-APPLT3-UIN(1:1)              
              END-IF                                                            
                                                                                
              MOVE K757-NUM-APPLT3-UIN TO WS-NUM-NRIC                           
              MOVE K757-NUM-APPLT3-EMPLY-STATUS                                 
                                       TO WS-EMPLY-STAT                         
              MOVE K757-NUM-NS-LT12MTH-HA3                                      
                                       TO WS-NS-LT12MTH                         
              MOVE K757-NUM-EDN-LT12MTH-HA3                                     
                                       TO WS-EDN-LT12MTH                        
              MOVE K757-NUM-DIVORCE-FNLJDGMT-HA3                                
                                       TO WS-DIVORCE-FNLJDGMT                   
                                                                                
              PERFORM 3200-READ-BP13K820 THRU 3200-EXIT                         
           END-IF.                                                              
                                                                                
           IF (K757-NUM-APPLT4-UIN NOT = SPACES AND LOW-VALUES)                 
              IF K757-NUM-APPLT4-UIN(1:1) = '-'                                 
                 MOVE 'X'              TO K757-NUM-APPLT4-UIN(1:1)              
              END-IF                                                            
                                                                                
              MOVE K757-NUM-APPLT4-UIN TO WS-NUM-NRIC                           
              MOVE K757-NUM-APPLT4-EMPLY-STATUS                                 
                                       TO WS-EMPLY-STAT                         
              MOVE K757-NUM-NS-LT12MTH-HA4                                      
                                       TO WS-NS-LT12MTH                         
              MOVE K757-NUM-EDN-LT12MTH-HA4                                     
                                       TO WS-EDN-LT12MTH                        
              MOVE K757-NUM-DIVORCE-FNLJDGMT-HA4                                
                                       TO WS-DIVORCE-FNLJDGMT                   
                                                                                
              PERFORM 3200-READ-BP13K820 THRU 3200-EXIT                         
           END-IF.                                                              
                                                                                
           IF (K757-NUM-OCCUP1-UIN NOT = SPACES AND LOW-VALUES)                 
              IF K757-NUM-OCCUP1-UIN(1:1) = '-'                                 
                 MOVE 'X'              TO K757-NUM-OCCUP1-UIN(1:1)              
              END-IF                                                            
                                                                                
              MOVE K757-NUM-OCCUP1-UIN TO WS-NUM-NRIC                           
              MOVE K757-NUM-OCCUP1-EMPLY-STATUS                                 
                                       TO WS-EMPLY-STAT                         
              MOVE K757-NUM-NS-LT12MTH-OCC1                                     
                                       TO WS-NS-LT12MTH                         
              MOVE K757-NUM-EDN-LT12MTH-OCC1                                    
                                       TO WS-EDN-LT12MTH                        
              MOVE K757-NUM-DIVORCE-FNLJDGMT-OCC1                               
                                       TO WS-DIVORCE-FNLJDGMT                   
                                                                                
              PERFORM 3200-READ-BP13K820 THRU 3200-EXIT                         
           END-IF.                                                              
                                                                                
           IF (K757-NUM-OCCUP2-UIN NOT = SPACES AND LOW-VALUES)                 
              IF K757-NUM-OCCUP2-UIN(1:1) = '-'                                 
                 MOVE 'X'              TO K757-NUM-OCCUP2-UIN(1:1)              
              END-IF                                                            
                                                                                
              MOVE K757-NUM-OCCUP2-UIN TO WS-NUM-NRIC                           
              MOVE K757-NUM-OCCUP2-EMPLY-STATUS                                 
                                       TO WS-EMPLY-STAT                         
              MOVE K757-NUM-NS-LT12MTH-OCC2                                     
                                       TO WS-NS-LT12MTH                         
              MOVE K757-NUM-EDN-LT12MTH-OCC2                                    
                                       TO WS-EDN-LT12MTH                        
              MOVE K757-NUM-DIVORCE-FNLJDGMT-OCC2                               
                                       TO WS-DIVORCE-FNLJDGMT                   
                                                                                
              PERFORM 3200-READ-BP13K820 THRU 3200-EXIT                         
           END-IF.                                                              
                                                                                
           IF (K757-NUM-OCCUP3-UIN NOT = SPACES AND LOW-VALUES)                 
              IF K757-NUM-OCCUP3-UIN(1:1) = '-'                                 
                 MOVE 'X'              TO K757-NUM-OCCUP3-UIN(1:1)              
              END-IF                                                            
                                                                                
              MOVE K757-NUM-OCCUP3-UIN TO WS-NUM-NRIC                           
              MOVE K757-NUM-OCCUP3-EMPLY-STATUS                                 
                                       TO WS-EMPLY-STAT                         
              MOVE K757-NUM-NS-LT12MTH-OCC3                                     
                                       TO WS-NS-LT12MTH                         
              MOVE K757-NUM-EDN-LT12MTH-OCC3                                    
                                       TO WS-EDN-LT12MTH                        
              MOVE K757-NUM-DIVORCE-FNLJDGMT-OCC3                               
                                       TO WS-DIVORCE-FNLJDGMT                   
                                                                                
              PERFORM 3200-READ-BP13K820 THRU 3200-EXIT                         
           END-IF.                                                              
                                                                                
           IF (K757-NUM-OCCUP4-UIN NOT = SPACES AND LOW-VALUES)                 
              IF K757-NUM-OCCUP4-UIN(1:1) = '-'                                 
                 MOVE 'X'              TO K757-NUM-OCCUP4-UIN(1:1)              
              END-IF                                                            
                                                                                
              MOVE K757-NUM-OCCUP4-UIN TO WS-NUM-NRIC                           
              MOVE K757-NUM-OCCUP4-EMPLY-STATUS                                 
                                       TO WS-EMPLY-STAT                         
              MOVE K757-NUM-NS-LT12MTH-OCC4                                     
                                       TO WS-NS-LT12MTH                         
              MOVE K757-NUM-EDN-LT12MTH-OCC4                                    
                                       TO WS-EDN-LT12MTH                        
              MOVE K757-NUM-DIVORCE-FNLJDGMT-OCC4                               
                                       TO WS-DIVORCE-FNLJDGMT                   
                                                                                
              PERFORM 3200-READ-BP13K820 THRU 3200-EXIT                         
           END-IF.                                                              
                                                                                
           IF (K757-NUM-OCCUP5-UIN NOT = SPACES AND LOW-VALUES)                 
              IF K757-NUM-OCCUP5-UIN(1:1) = '-'                                 
                 MOVE 'X'              TO K757-NUM-OCCUP5-UIN(1:1)              
              END-IF                                                            
                                                                                
              MOVE K757-NUM-OCCUP5-UIN TO WS-NUM-NRIC                           
              MOVE K757-NUM-OCCUP5-EMPLY-STATUS                                 
                                       TO WS-EMPLY-STAT                         
              MOVE K757-NUM-NS-LT12MTH-OCC5                                     
                                       TO WS-NS-LT12MTH                         
              MOVE K757-NUM-EDN-LT12MTH-OCC5                                    
                                       TO WS-EDN-LT12MTH                        
              MOVE K757-NUM-DIVORCE-FNLJDGMT-OCC5                               
                                       TO WS-DIVORCE-FNLJDGMT                   
                                                                                
              PERFORM 3200-READ-BP13K820 THRU 3200-EXIT                         
           END-IF.                                                              
                                                                                
           IF (K757-NUM-OCCUP6-UIN NOT = SPACES AND LOW-VALUES)                 
              IF K757-NUM-OCCUP6-UIN(1:1) = '-'                                 
                 MOVE 'X'              TO K757-NUM-OCCUP6-UIN(1:1)              
              END-IF                                                            
                                                                                
              MOVE K757-NUM-OCCUP6-UIN TO WS-NUM-NRIC                           
              MOVE K757-NUM-OCCUP6-EMPLY-STATUS                                 
                                       TO WS-EMPLY-STAT                         
              MOVE K757-NUM-NS-LT12MTH-OCC6                                     
                                       TO WS-NS-LT12MTH                         
              MOVE K757-NUM-EDN-LT12MTH-OCC6                                    
                                       TO WS-EDN-LT12MTH                        
              MOVE K757-NUM-DIVORCE-FNLJDGMT-OCC6                               
                                       TO WS-DIVORCE-FNLJDGMT                   
                                                                                
              PERFORM 3200-READ-BP13K820 THRU 3200-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE SPACES                 TO WS-NUM-REGN.                          
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3200-READ-BP13K820.                                                      
      *****************************************************************         
                                                                                
           MOVE WS-NUM-NRIC            TO K820-NUM-NRIC.                        
                                                                                
           READ BP13K820.                                                       
                                                                                
           EVALUATE WS-K820-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K820-CNT-READ                      
                                                                                
                 PERFORM 3250-UPDATE-BP13K820 THRU 3250-EXIT                    
              WHEN 23                                                           
                 MOVE SPACES           TO BP13OUT1-REC                          
                 INITIALIZE BP13OUT1-REC                                        
                                                                                
                 MOVE WS-NUM-REGN      TO OUT1-NUM-REGN                         
                 MOVE WS-NUM-NRIC      TO OUT1-NUM-NRIC                         
                                                                                
                 WRITE BP13OUT1-REC                                             
                 ADD 1                 TO WS-OUT1-CNT-WRT                       
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K820 FAIL STATUS=' WS-K820-STATUS            
                 MOVE WS-K820-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
           MOVE SPACES                 TO WS-NUM-NRIC                           
                                          WS-EMPLY-STAT                         
                                          WS-NS-LT12MTH                         
                                          WS-EDN-LT12MTH                        
                                          WS-DIVORCE-FNLJDGMT.                  
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3250-UPDATE-BP13K820.                                                    
      *****************************************************************         
                                                                                
           MOVE WS-EMPLY-STAT          TO K820-NUM-EMPLY-STATUS.                
           MOVE WS-NS-LT12MTH          TO K820-NUM-NS-LT12MTH.                  
           MOVE WS-EDN-LT12MTH         TO K820-NUM-EDN-LT12MTH.                 
           MOVE WS-DIVORCE-FNLJDGMT    TO K820-NUM-DIVORCE-FNLJDGMT.            
                                                                                
           REWRITE BP13K820-REC.                                                
                                                                                
           IF WS-K820-STATUS = ZEROES                                           
              ADD 1                    TO WS-K820-CNT-REWRT                     
           ELSE                                                                 
              DISPLAY 'REWRITE BP13K820 FAIL STATUS=' WS-K820-STATUS            
              MOVE WS-K820-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       3250-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           CLOSE BP13F740                                                       
                 BP13K757                                                       
                 BP13K820                                                       
                 BP13OUT1                                                       
                 BP13OUT2.                                                      
                                                                                
           IF WS-K757-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K757 FAIL STATUS=' WS-K757-STATUS              
              MOVE WS-K757-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K820 FAIL STATUS=' WS-K820-STATUS              
              MOVE WS-K820-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           DISPLAY '*************************'.                                 
           DISPLAY '* PROGRAM BP13CB89      *'.                                 
           DISPLAY '* CONTROL COUNTS        *'.                                 
           DISPLAY '*-----------------------*'.                                 
           DISPLAY '* REC READ F740 : ' WS-F740-CNT-READ ' *'.                  
           DISPLAY '* REC READ K757 : ' WS-K757-CNT-READ ' *'.                  
           DISPLAY '* REC READ K820 : ' WS-K820-CNT-READ ' *'.                  
           DISPLAY '* REC UPDT K820 : ' WS-K820-CNT-REWRT ' *'.                 
           DISPLAY '* REC WRTN OUT1 : ' WS-OUT1-CNT-WRT ' *'.                   
           DISPLAY '* REC WRTN OUT2 : ' WS-OUT2-CNT-WRT ' *'.                   
           DISPLAY '*************************'.                                 
                                                                                
       9000-EXIT.                                                               
           STOP RUN.                                                            
           EXIT.                                                                
                                                                                
      *======================  END OF PROGRAM  ======================*          
