       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C658.                                                 
       AUTHOR.        KAVYA VAKA.                                               
      *DATE-WRITTEN.  30/10/19.                                                 
      **********************************************************                
      *   OBJECTIVE    :                                       *                
      *      SYSTEM TO SEND EMAIL ALERT TO STAFF IF THERE ARE  *                
      *      UNSUCCSSFULLY TRANSMISSION OF APPLICATION FOR     *                
      *      REFUND OF STAMP DUTY RECORD TO IRAS.              *                
      *                                                        *                
      *    INPUT FILE :                                        *                
      *        1. BP13T.K62A.STAMPREF (BP13K62A)               *                
      *    REPORT :                                            *                
      *        1. BP13L658   - 'UNSUCCSSFULLY TRANSMISSION     *                
      *                     OF APPLICATION FOR REFUND OF STAMP *                
      *                     DUTY RECORD TO IRAS.               *                
      * CHG REQ   OIC   DATE   DESCRIPTION                     *                
      * =======   ===   ====   ===========                     *                
      * BP138068  KV5  301019  NEW PROGRAM                     *                
      * BP138068  KTM3 121219  CHANGE CONDITION TO             *                
      *                        K62A-DTE-SENT-TO-IRAS NOT FOUND *                
      * BP138068  KV5  241219  CHANGE THE CONDITIONS TO        *                
      *                        K62A-DTE-SENT-TO-IRAS = SPACES  *                
      *                        OR ZEROS                        *                
      * BP138757  KV8  230721  TO REPLACE KATHIJAH TO SUZANNE  *                
      **********************************************************                
       ENVIRONMENT    DIVISION.                                                 
      *-----------------------*                                                 
       CONFIGURATION  SECTION.                                                  
      *-----------------------*                                                 
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
      *-----------------------*                                                 
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06F120 ASSIGN       TO BM06F120.                            
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS WS-K800-STATUS.                     
                                                                                
           SELECT BP13K62A ASSIGN       TO BP13K62A                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE IS DYNAMIC                               
                           RECORD KEY IS K62A-KEY-FLD                           
                           FILE STATUS IS WS-K62A-STATUS.                       
                                                                                
           SELECT BP13L658 ASSIGN       TO BP13L658.                            
           SELECT MAILL658 ASSIGN       TO MAILL658.                            
                                                                                
                                                                                
       DATA DIVISION.                                                           
      *-----------------------*                                                 
       FILE SECTION.                                                            
      *-----------------------*                                                 
                                                                                
       FD   BM06F120                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 700 CHARACTERS.                                     
       COPY BM06F120.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K62A          RECORD CONTAINS 1200 CHARACTERS.                  
       COPY BP13K62A.                                                           
                                                                                
                                                                                
       FD   BP13L658          BLOCK  CONTAINS   0 RECORDS                       
                              RECORD CONTAINS 132 CHARACTERS                    
                              LABEL RECORDS  OMITTED                            
                              RECORDING MODE IS F.                              
       01  BP13L658-REC       PIC X(132).                                       
                                                                                
       FD   MAILL658                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS  132 CHARACTERS.                                    
       01   MAILL658-REC               PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-K62A-EOF             PIC X(1)  VALUE 'N'.                     
           05  WS-F120-EOF             PIC X(1)  VALUE 'N'.                     
           05  WS-K62A-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K800-STATUS          PIC 9(02) VALUE ZEROES.                  
       01  WS-F120-FND                 PIC X(1)  VALUE 'N'.                     
       01  WS-CURRENT-DATE             PIC X(8).                                
       01  WS-SNO-ZZZZ9                PIC ZZZZ9 VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-K62A             PIC 9(5)  VALUE ZEROES.                  
           05  WS-CNT-F120             PIC 9(5)  VALUE ZEROES.                  
           05  WS-PAGE-CNT             PIC 9(1)  VALUE ZEROES.                  
           05  WS-CNT-L658-WRITE       PIC 9(7)  VALUE ZEROES.                  
                                                                                
       01  WS-L658-HDG1.                                                        
           05  FILLER              PIC X(8)      VALUE 'BP13L658'.              
           05  FILLER              PIC X(39)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
           'S Y S T E M   O F   C O M M I T M E N T'.                           
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE : '.               
           05  L658-DATE           PIC X(10).                                   
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  L658-PAGE           PIC 9(1).                                    
                                                                                
                                                                                
       01  WS-L658-HDG2.                                                        
           05  FILLER              PIC X(33)     VALUE SPACES.                  
           05  FILLER              PIC X(80)     VALUE                          
                 'UNSUCCESSFUL TRANSMISSION OF APPLICATION FOR REFUND OF        
      -          ' STAMP DUTY RECORD TO IRAS'.                                  
                                                                                
       01  WS-L658-HDG3.                                                        
           05  FILLER              PIC X(01) VALUE SPACES.                      
           05  FILLER              PIC X(5)  VALUE '  S/N'.                     
           05  FILLER              PIC X(01) VALUE SPACES.                      
           05  FILLER              PIC X(6)  VALUE 'REGN  '.                    
           05  FILLER              PIC X(3)  VALUE SPACES.                      
           05  FILLER              PIC X(13) VALUE 'SCHEME ACC NO'.             
           05  FILLER              PIC X(01) VALUE SPACES.                      
           05  FILLER              PIC X(17) VALUE 'FLAT CANCELLATION'.         
           05  FILLER              PIC X(01) VALUE SPACES.                      
           05  FILLER              PIC X(15) VALUE 'HDB SEND APP NO'.           
           05  FILLER              PIC X(01) VALUE SPACES.                      
           05  FILLER              PIC X(12) VALUE 'FLAT ADDRESS'.              
           05  FILLER              PIC X(67) VALUE SPACES.                      
                                                                                
       01  WS-L658-DTL.                                                         
           05  FILLER              PIC X(1)  VALUE SPACES.                      
           05  L658-SN             PIC ZZZZ9 VALUE ZEROES.                      
           05  FILLER              PIC X(1)  VALUE SPACES.                      
           05  L658-REGNO          PIC X(8).                                    
           05  FILLER              PIC X(1)  VALUE SPACES.                      
           05  L658-SCHEM-ACC      PIC X(11).                                   
           05  FILLER              PIC X(3)  VALUE SPACES.                      
           05  L658-CANCELATION    PIC X(10).                                   
           05  FILLER              PIC X(08) VALUE SPACES.                      
           05  L658-PRC-REFUND     PIC X(10).                                   
           05  FILLER              PIC X(06) VALUE SPACES.                      
           05  L658-ADDRESS.                                                    
               10 L658-BLK-NO         PIC X(05).                                
               10 FILLER              PIC X(01).                                
               10 L658-STREET-NAME    PIC X(32).                                
               10 FILLER              PIC X(01).                                
               10 L658-UNIT-NO.                                                 
                  15 L658-LEVEL-NO    PIC XX.                                   
                  15 L658-MAIN-UNIT   PIC X(4).                                 
                  15 L658-SUB-UNIT    PIC X(3).                                 
                  15 FILLER           PIC X(02).                                
               10 FILLER              PIC X(01).                                
               10 L658-POSTAL-CODE    PIC X(6).                                 
           05  FILLER              PIC X(12) VALUE SPACES.                      
                                                                                
       01  WS-REPORT-DTL.                                                       
           05  WS-MAIL-RPT1             PIC X(12) VALUE 'HELO SGPHDB1'.         
           05  WS-MAIL-RPT2             PIC X(27)                               
               VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.                                
           05  WS-MAIL-RPT3             PIC X(50)                               
      *        VALUE 'RCPT TO:<KATHIJAH_BEE_ALI_MOHAMED@HDB.GOV.SG>'.           
               VALUE 'RCPT TO:<SUZANNE_SC_HENG@HDB.GOV.SG>'.                    
           05  WS-MAIL-RPT4             PIC X(50)                               
               VALUE 'RCPT TO:<BALASUBRAMANIAM_ARUNASALAM@HDB.GOV.SG>'.         
           05  WS-MAIL-RPT5             PIC X(50)                               
               VALUE 'RCPT TO:<MUHAMMED_SHARIFF_SAHROM@HDB.GOV.SG>'.            
           05  WS-MAIL-RPT6             PIC X(4)                                
               VALUE 'DATA'.                                                    
           05  WS-MAIL-RPT7             PIC X(29)                               
               VALUE 'FROM:Soc System - Email Alert'.                           
           05  WS-MAIL-RPT8             PIC X(50)                               
      *        VALUE 'CC:<KATHIJAH_BEE_ALI_MOHAMED@HDB.GOV.SG>'.                
               VALUE 'CC:<SUZANNE_SC_HENG@HDB.GOV.SG>'.                         
           05  WS-MAIL-RPT9             PIC X(50)                               
               VALUE 'CC:<BALASUBRAMANIAM_ARUNASALAM@HDB.GOV.SG>'.              
           05  WS-MAIL-RPT10            PIC X(50)                               
               VALUE 'CC:<MUHAMMED_SHARIFF_SAHROM@HDB.GOV.SG>'.                 
           05  WS-MAIL-RPT11            PIC X(100)                              
               VALUE 'SUBJECT: UNSUCCESSFUL TRANSMISSION OF APPLICATION         
      -            'FOR REFUND OF STAMP DUTY RECORD TO IRAS'.                   
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-START-BP13K62A     THRU 2000-EXIT.                      
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13K62A                                                 
                       BP13K800                                                 
                       BM06F120                                                 
                OUTPUT BP13L658                                                 
                       MAILL658.                                                
                                                                                
           IF WS-K62A-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K62A - ERROR OPENING : ' WS-K62A-STATUS              
              MOVE WS-K62A-STATUS                   TO RETURN-CODE              
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF                                                               
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR OPENING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 7000-PRINT-HEADER       THRU 7000-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       2000-START-BP13K62A.                                                     
      *************************************************************             
                                                                                
           MOVE SPACES                   TO BP13K62A-REC F120-REC.              
           INITIALIZE                       BP13K62A-REC F120-REC.              
                                                                                
           MOVE LOW-VALUES               TO K62A-NUM-HDB-REF                    
           MOVE 'N'                      TO WS-K62A-EOF                         
                                            WS-F120-EOF.                        
                                                                                
           START BP13K62A KEY >= K62A-NUM-HDB-REF.                              
                                                                                
           EVALUATE WS-K62A-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 2100-READNEXT-BP13K62A THRU 2100-EXIT               
                      UNTIL WS-K62A-EOF = 'Y'                                   
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'BP13K62A START ERROR ' WS-K62A-STATUS              
                    DISPLAY 'K62A KEY: ' K62A-NUM-HDB-REF                       
                    MOVE WS-K62A-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       2100-READNEXT-BP13K62A.                                                  
      *************************************************************             
           READ BP13K62A NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'                 TO WS-K62A-EOF                         
           END-READ.                                                            
                                                                                
           EVALUATE WS-K62A-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    IF ((K62A-DTE-PRC-REFUND = WS-CURRENT-DATE) AND             
                        (K62A-DTE-SENT-TO-IRAS = SPACES OR ZEROS))              
                         PERFORM 2200-READ-BP13K800  THRU 2200-EXIT             
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'BP13K62A READ NEXT ERROR ' WS-K62A-STATUS          
                    DISPLAY 'K62A KEY: ' K62A-NUM-HDB-REF                       
                    MOVE WS-K62A-STATUS  TO RETURN-CODE                         
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       2200-READ-BP13K800.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES        TO K800-NUM-REGN.                                 
           MOVE K62A-NUM-REGN TO K800-NUM-REGN.                                 
           MOVE SPACES TO F120-STREET-NAME F120-BLK-NO F120-UNIT-NO             
                          F120-POSTAL-CODE WS-L658-DTL                          
           MOVE 'N'           TO WS-F120-FND                                    
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS   =  00                                            
              IF WS-F120-EOF = 'Y'                                              
                 CLOSE BM06F120                                                 
                 OPEN INPUT BM06F120                                            
                 MOVE 'N'  TO WS-F120-EOF                                       
              END-IF                                                            
              PERFORM 2300-READ-BM06F120 THRU 2300-EXIT                         
                             UNTIL WS-F120-EOF = 'Y'                            
              PERFORM 2400-MOVE-VALUES   THRU 2400-EXIT                         
           ELSE                                                                 
           IF WS-K800-STATUS  = 23                                              
              DISPLAY 'REC NOTFND IN BP13K800 : ' WS-K800-STATUS                
                      ', REGN IS : ' K800-NUM-REGN                              
           ELSE                                                                 
              DISPLAY 'ERR READING BP13K800, STAT : ' WS-K800-STATUS            
                      ', REGN IS : ' K800-NUM-REGN                              
              MOVE WS-K800-STATUS  TO RETURN-CODE                               
              PERFORM 9000-CLOSE-ROUTINE.                                       
                                                                                
       2200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       2300-READ-BM06F120.                                                      
      ******************************************************************        
                                                                                
           READ BM06F120                                                        
                AT END                                                          
                   MOVE 'Y'         TO WS-F120-EOF                              
                NOT AT END                                                      
                   IF F120-SCH-ACC-NO = K62A-NUM-HDB-REF                        
                      MOVE 'Y'      TO WS-F120-FND                              
                      MOVE 'Y'      TO WS-F120-EOF                              
                   END-IF                                                       
           END-READ.                                                            
                                                                                
       2300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       2400-MOVE-VALUES.                                                        
      ******************************************************************        
                                                                                
               ADD 1                        TO WS-CNT-K62A                      
               MOVE WS-CNT-K62A             TO WS-SNO-ZZZZ9                     
               MOVE WS-SNO-ZZZZ9            TO L658-SN                          
               MOVE K62A-NUM-REGN           TO L658-REGNO                       
               MOVE K62A-NUM-HDB-REF        TO L658-SCHEM-ACC                   
               IF WS-F120-FND = 'Y'                                             
                  MOVE F120-BLK-NO          TO L658-BLK-NO                      
                  MOVE F120-STREET-NAME     TO L658-STREET-NAME                 
      *           MOVE F120-UNIT-NO         TO L658-UNIT-NO                     
                  IF F120-UNIT-NO = SPACES                                      
                     MOVE SPACES            TO F120-UNIT-NO                     
                  ELSE                                                          
                     STRING F120-UNIT-NO(1:2) ' -'                              
                            F120-UNIT-NO(3:7)                                   
                            DELIMITED BY SIZE INTO L658-UNIT-NO                 
                  END-IF                                                        
                  MOVE F120-POSTAL-CODE     TO L658-POSTAL-CODE                 
               END-IF                                                           
               IF K62A-DTE-CANCELLATION = SPACES                                
                  MOVE SPACES               TO L658-CANCELATION                 
               ELSE                                                             
                  STRING K62A-DTE-CANCELLATION(7:2) '/'                         
                         K62A-DTE-CANCELLATION(5:2) '/'                         
                         K62A-DTE-CANCELLATION(1:4) '/'                         
                         DELIMITED BY SIZE INTO L658-CANCELATION                
               END-IF                                                           
                                                                                
               IF K62A-DTE-PRC-REFUND = SPACES                                  
                  MOVE SPACES               TO L658-PRC-REFUND                  
               ELSE                                                             
                  STRING K62A-DTE-PRC-REFUND(7:2) '/'                           
                         K62A-DTE-PRC-REFUND(5:2) '/'                           
                         K62A-DTE-PRC-REFUND(1:4) '/'                           
                         DELIMITED BY SIZE INTO L658-PRC-REFUND                 
               END-IF                                                           
               WRITE BP13L658-REC           FROM WS-L658-DTL                    
               WRITE MAILL658-REC           FROM WS-L658-DTL.                   
                                                                                
       2400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       5000-HEADER-EMAIL.                                                       
      ******************************************************************        
                                                                                
            WRITE MAILL658-REC FROM WS-MAIL-RPT1.                               
            WRITE MAILL658-REC FROM WS-MAIL-RPT2.                               
            WRITE MAILL658-REC FROM WS-MAIL-RPT3.                               
            WRITE MAILL658-REC FROM WS-MAIL-RPT4.                               
            WRITE MAILL658-REC FROM WS-MAIL-RPT5.                               
            WRITE MAILL658-REC FROM WS-MAIL-RPT6.                               
            WRITE MAILL658-REC FROM WS-MAIL-RPT7.                               
            WRITE MAILL658-REC FROM WS-MAIL-RPT8.                               
            WRITE MAILL658-REC FROM WS-MAIL-RPT9.                               
            WRITE MAILL658-REC FROM WS-MAIL-RPT10.                              
            WRITE MAILL658-REC FROM WS-MAIL-RPT11.                              
                                                                                
            MOVE SPACES TO MAILL658-REC.                                        
            WRITE MAILL658-REC.                                                 
                                                                                
            WRITE MAILL658-REC FROM WS-L658-HDG1.                               
            WRITE MAILL658-REC FROM WS-L658-HDG2.                               
            WRITE MAILL658-REC FROM WS-L658-HDG3.                               
                                                                                
            MOVE ALL '='         TO MAILL658-REC(1:132)                         
            WRITE MAILL658-REC.                                                 
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       7000-PRINT-HEADER.                                                       
      ******************************************************************        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)      TO WS-CURRENT-DATE.             
                                                                                
           STRING WS-CURRENT-DATE(7:2) '/'                                      
                  WS-CURRENT-DATE(5:2) '/'                                      
                  WS-CURRENT-DATE(1:4)                                          
              DELIMITED BY SIZE INTO L658-DATE.                                 
                                                                                
           ADD 1                               TO WS-PAGE-CNT.                  
           MOVE ZEROES                         TO L658-PAGE.                    
           MOVE WS-PAGE-CNT                    TO L658-PAGE.                    
                                                                                
           WRITE BP13L658-REC    FROM WS-L658-HDG1 AFTER PAGE.                  
           WRITE BP13L658-REC    FROM WS-L658-HDG2.                             
           WRITE BP13L658-REC    FROM WS-L658-HDG3 AFTER 2.                     
                                                                                
           MOVE ALL '='          TO BP13L658-REC(1:132)                         
           WRITE BP13L658-REC.                                                  
                                                                                
           PERFORM 5000-HEADER-EMAIL           THRU 5000-EXIT.                  
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9000-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C658'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13L658 RECORDS WRITTEN.......... : '            
                    WS-CNT-K62A.                                                
                                                                                
           CLOSE    BP13K62A                                                    
                    BP13K800                                                    
                    BM06F120                                                    
                    BP13L658                                                    
                    MAILL658.                                                   
                                                                                
           IF WS-K62A-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K62A - ERROR CLOSING : ' WS-K62A-STATUS              
           END-IF                                                               
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR CLOSING : ' WS-K800-STATUS              
           END-IF                                                               
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
