      *--------------------------------------------------------*                
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------*                
       PROGRAM-ID. BP13C14A.                                                    
      *AUTHOR. BO JIANG.                                                        
      *DATE-WRITTEN.   05/08/08.                                                
      *--------------------------------------------------------*                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)           *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES  : TO MATCHIMNG OIC (DBS)                *                
      *                                                        *                
      *    INPUT FILES  : BP13F520                             *                
      *                   BP13F800                             *                
      *                   BP13K025                             *                
      *    I-O            BP13K022                             *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY   DATE      DETAILS                         *                
      *-------- ---- --------  --------------------------------*                
      *BP133389 JB8  20080731  NEW PGM,SIMILAR TO BP13C144     *                
      *                        BP13K024 -> BP13K025            *                
      *BP133671 ZDD  20090824  INTIALIZE BP13K022 BEFORE       *                
      *                        WRITING THE RECORD              *                
      *--------------------------------------------------------*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F800   ASSIGN TO BP13F800.                                
           SELECT BP13F520   ASSIGN TO BP13F520.                                
           SELECT BP13K022   ASSIGN TO BP13K022                                 
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K022-KEY-FLD                      
                             ALTERNATE KEY IS K022-NUM-REGN                     
                             FILE STATUS   IS WS-K022-STATUS.                   
                                                                                
           SELECT BP13K025   ASSIGN TO BP13K025                                 
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K025-KEY-FLD                      
                             FILE STATUS   IS WS-K025-STATUS.                   
                                                                                
           SELECT BP13L14A  ASSIGN  TO BP13L14A.                                
                                                                                
      *--------------------------------------------------------*                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------*                
                                                                                
       FD   BP13F800                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13F800.                                                           
                                                                                
       FD   BP13F520                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13F520.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
                                                                                
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K025                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
                                                                                
       COPY BP13K025.                                                           
                                                                                
       FD  BP13L14A                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS  132 CHARACTERS.                                     
                                                                                
       01  BP13L14A-REC         PIC X(132).                                     
                                                                                
      *--------------------------------------------------------*                
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------*                
       01  WORK-AREAS.                                                          
           05 WS-MATCH                  PIC 9(8)    VALUE 0.                    
           05 WS-NOT-MATCH              PIC 9(8)    VALUE 0.                    
           05 WS-READ-F800              PIC 9(8)    VALUE 0.                    
           05 WS-K022-UPDATE            PIC 9(8)    VALUE 0.                    
           05 WS-READ-F520              PIC 9(8)    VALUE 0.                    
           05 WS-K022-STATUS            PIC 9(2)    VALUE 00.                   
           05 WS-K025-STATUS            PIC 9(2)    VALUE 00.                   
                                                                                
           05 WS-NOTFND-K022            PIC 9(8)    VALUE 0.                    
           05 WS-FOUND-K022             PIC 9(8)    VALUE 0.                    
           05 WS-FOUND                  PIC X(1)    VALUE 'N'.                  
                                                                                
           05 WS-CNT-LINE-ON-PAGE       PIC 9(8)    VALUE 66.                   
           05 WS-CNT-PRINT-L14A         PIC 9(8)    VALUE 1.                    
           05 WS-CNT-WRITE-L14A         PIC 9(8)    VALUE 0.                    
           05 WS-NO-OF-PAGE             PIC 9(8)    VALUE 0.                    
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY            PIC 9(4).                                 
           05  WS-SYS-MM              PIC 9(2).                                 
           05  WS-SYS-DD              PIC 9(2).                                 
                                                                                
       01  WS-EDITED-DATE             PIC X(10).                                
                                                                                
      *--------------------------------------------------------*                
      *         BP13L14A  -  R E P O R T    L A Y O U T        *                
      *--------------------------------------------------------*                
                                                                                
       01  L14A-PR-HEAD-01.                                                     
           05  FILLER                   PIC X(14) VALUE 'BP13L14A'.             
           05  FILLER                   PIC X(8)  VALUE 'HDBCAT 3'.             
           05  FILLER                   PIC X(25) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(13) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE: '.               
           05  L14A-PRINT-DATE-1        PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'PAGE: '.               
           05  L14A-PRINT-PAGE          PIC ZZZ9.                               
                                                                                
       01  L14A-PR-HEAD-02.                                                     
           05  FILLER                   PIC X(55) VALUE SPACES.                 
           05  FILLER                   PIC X(73) VALUE                         
                'REPORT ON MATCHING OF OIC'.                                    
                                                                                
       01  L14A-PR-HEAD-03.                                                     
           05  FILLER          PIC X(5)  VALUE ' S-NO'.                         
           05  FILLER          PIC X(2)  VALUE SPACES.                          
           05  FILLER          PIC X(5)  VALUE 'PRJCT'.                         
           05  FILLER          PIC X(2)  VALUE SPACES.                          
           05  FILLER          PIC X(5)  VALUE 'OIC'.                           
           05  FILLER          PIC X(5)  VALUE SPACES.                          
           05  FILLER          PIC X(40) VALUE 'OIC NAME'.                      
           05  FILLER          PIC X(3)  VALUE SPACES.                          
           05  FILLER          PIC X(20) VALUE 'DELETED REF. NO.'.              
           05  FILLER          PIC X(3)  VALUE SPACES.                          
           05  FILLER          PIC X(20) VALUE 'ADDED REGN NO.'.                
           05  FILLER          PIC X(1)  VALUE SPACES.                          
           05  FILLER          PIC X(6)  VALUE 'AL CAT'.                        
           05  FILLER          PIC X(3)  VALUE SPACES.                          
           05  FILLER          PIC X(8)  VALUE 'BNK LOAN'.                      
                                                                                
       01  L14A-PR-HEAD-04.                                                     
           05  FILLER                PIC X(132) VALUE ALL '-'.                  
                                                                                
       01  L14A-PR-DETAILS-1.                                                   
           05  L14A-NUMBER           PIC ZZZZ9.                                 
           05  FILLER                PIC X(3)  VALUE SPACES.                    
           05  L14A-PRJCT            PIC X(3)  VALUE SPACES.                    
           05  FILLER                PIC X(3)  VALUE SPACES.                    
           05  L14A-OIC              PIC X(5)  VALUE SPACES.                    
           05  FILLER                PIC X(5)  VALUE SPACES.                    
           05  L14A-NME-OIC          PIC X(40) VALUE SPACES.                    
           05  FILLER                PIC X(3)  VALUE SPACES.                    
           05  L14A-REF-NO           PIC X(20) VALUE SPACES.                    
           05  FILLER                PIC X(3)  VALUE SPACES.                    
           05  L14A-REG-NO           PIC X(20) VALUE SPACES.                    
           05  FILLER                PIC X(2)  VALUE SPACES.                    
           05  L14A-AL-CAT           PIC X(6)  VALUE SPACES.                    
           05  FILLER                PIC X(4)  VALUE SPACES.                    
           05  L14A-BANK-LOAN        PIC X(1)  VALUE SPACES.                    
                                                                                
      *--------------------------------------------------------*                
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------*                
                                                                                
      *--------------------------------------------------------*                
       0000-MAIN.                                                               
      *--------------------------------------------------------*                
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-F800     THRU 2000-EXIT.                           
           PERFORM 3000-READ-F520     THRU 3000-EXIT.                           
                                                                                
           PERFORM 4000-COMPARE       THRU 4000-EXIT                            
                   UNTIL F800-NUM-REGN = HIGH-VALUES                            
                     AND F520-NUM-REGN = HIGH-VALUES.                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       1000-OPEN-FILES.                                                         
      *--------------------------------------------------------*                
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP13F520                                                 
                       BP13K025                                                 
                I-O    BP13K022                                                 
                OUTPUT BP13L14A.                                                
                                                                                
           EVALUATE WS-K022-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K022-STATUS        TO RETURN-CODE                      
                 DISPLAY 'OPEN BP13K022 FAIL. STATUS=' WS-K022-STATUS           
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K025-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K025-STATUS        TO RETURN-CODE                      
                 DISPLAY 'OPEN BP13K025 FAIL. STATUS=' WS-K025-STATUS           
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATE.                      
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2000-READ-F800.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ BP13F800                                                        
                AT END MOVE HIGH-VALUES TO F800-NUM-REGN                        
                GO TO 2000-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-READ-F800.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       3000-READ-F520.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ BP13F520                                                        
                AT END MOVE HIGH-VALUES TO F520-NUM-REGN                        
                GO TO 3000-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-READ-F520.                                               
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4000-COMPARE.                                                            
      *--------------------------------------------------------*                
                                                                                
           IF F800-NUM-REGN = F520-NUM-REGN                                     
              ADD 1 TO WS-MATCH                                                 
              PERFORM 5000-UPDATE-K022   THRU 5000-EXIT                         
              PERFORM 2000-READ-F800     THRU 2000-EXIT                         
              PERFORM 3000-READ-F520     THRU 3000-EXIT                         
           ELSE                                                                 
              IF F800-NUM-REGN > F520-NUM-REGN                                  
                 PERFORM 3000-READ-F520  THRU 3000-EXIT                         
              ELSE                                                              
                 ADD 1 TO WS-NOT-MATCH                                          
                 PERFORM 2000-READ-F800  THRU 2000-EXIT                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       5000-UPDATE-K022.                                                        
      *--------------------------------------------------------*                
                                                                                
           MOVE SPACES               TO BP13K022-REC.                           
           INITIALIZE                   BP13K022-REC.                           
           MOVE LOW-VALUES           TO K022-KEY-FLD.                           
           MOVE F520-NUM-REF         TO K022-NUM-REGN.                          
                                                                                
           MOVE 'N'                  TO WS-FOUND.                               
                                                                                
           START BP13K022 KEY >= K022-NUM-REGN.                                 
           IF WS-K022-STATUS = 00                                               
                                                                                
              READ BP13K022 NEXT RECORD                                         
              IF K022-NUM-REGN = F520-NUM-REF                                   
                 ADD 1 TO WS-FOUND-K022 WS-K022-UPDATE                          
                 PERFORM 5300-READ-K025      THRU 5300-EXIT                     
                 PERFORM 5100-DELETE-K022    THRU 5100-EXIT                     
                 PERFORM 5200-WRITE-K022     THRU 5200-EXIT                     
                 PERFORM 5400-MOVE-PARA      THRU 5400-EXIT                     
                 PERFORM 6000-WRITE-BP13L14A THRU 6000-EXIT                     
              ELSE                                                              
                  ADD 1 TO WS-NOTFND-K022                                       
              END-IF                                                            
           ELSE                                                                 
               IF WS-K022-STATUS = 23                                           
                  ADD 1 TO WS-NOTFND-K022                                       
               ELSE                                                             
                  DISPLAY 'ERROR WITH START READ BP13K022. STATUS IS '  '       
                           WS-K022-STATUS                                       
                  PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                     
               END-IF                                                           
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       5100-DELETE-K022.                                                        
      *--------------------------------------------------------*                
                                                                                
           DELETE BP13K022.                                                     
                                                                                
           EVALUATE WS-K022-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K022-STATUS        TO RETURN-CODE                      
                 DISPLAY 'DELETE BP13K022 FAIL. STATUS=' WS-K022-STATUS         
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       5200-WRITE-K022.                                                         
      *--------------------------------------------------------*                
                                                                                
                                                                                
           MOVE F800-NUM-REGN TO K022-NUM-REGN.                                 
           MOVE 'BP13C14A'    TO K022-NUM-USERID.                               
                                                                                
           WRITE BP13K022-REC.                                                  
                                                                                
           EVALUATE WS-K022-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K022-STATUS        TO RETURN-CODE                      
                 DISPLAY 'WRITE BP13K022 FAIL. STATUS=' WS-K022-STATUS          
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       5300-READ-K025.                                                          
      *----------------------------------------------------------------*        
                                                                                
           MOVE LOW-VALUES                TO K025-NUM-OIC.                      
           MOVE F800-NUM-DBSS-PRJCT       TO K025-NUM-PRJCT.                    
           MOVE K022-NUM-OIC              TO K025-NUM-OIC.                      
                                                                                
           READ BP13K025.                                                       
                                                                                
           EVALUATE WS-K025-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K025-STATUS        TO RETURN-CODE                      
                 DISPLAY 'READ BP13K025 FAIL. STATUS=' WS-K025-STATUS           
                         ',KEY=' K025-KEY-FLD                                   
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       5400-MOVE-PARA.                                                          
      *----------------------------------------------------------------*        
                                                                                
           MOVE F520-NUM-REF      TO  L14A-REF-NO.                              
           MOVE K022-NUM-REGN     TO  L14A-REG-NO.                              
           MOVE K022-NUM-OIC      TO  L14A-OIC.                                 
           MOVE K025-NUM-PRJCT    TO  L14A-PRJCT.                               
           MOVE K025-NME-OIC      TO  L14A-NME-OIC.                             
           MOVE K022-CDE-ALLO-CAT TO  L14A-AL-CAT.                              
           MOVE K022-CDE-BANKLOAN TO  L14A-BANK-LOAN.                           
                                                                                
       5400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       6000-WRITE-BP13L14A.                                                     
      *----------------------------------------------------------------*        
                                                                                
           IF WS-CNT-LINE-ON-PAGE > 50                                          
              PERFORM 6100-PRINT-HEADING THRU 6100-EXIT                         
              MOVE 1 TO WS-CNT-LINE-ON-PAGE                                     
           END-IF.                                                              
                                                                                
           MOVE WS-CNT-PRINT-L14A  TO    L14A-NUMBER.                           
                                                                                
           WRITE BP13L14A-REC      FROM  L14A-PR-DETAILS-1 AFTER 1.             
                                                                                
           ADD   1                 TO    WS-CNT-LINE-ON-PAGE.                   
           ADD   1                 TO    WS-CNT-PRINT-L14A.                     
           ADD   1                 TO    WS-CNT-WRITE-L14A.                     
                                                                                
           MOVE  SPACES            TO    L14A-PR-DETAILS-1.                     
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       6100-PRINT-HEADING.                                                      
      *----------------------------------------------------------------*        
                                                                                
           ADD  1                TO     WS-NO-OF-PAGE.                          
           MOVE WS-NO-OF-PAGE    TO     L14A-PRINT-PAGE.                        
           MOVE WS-EDITED-DATE   TO     L14A-PRINT-DATE-1.                      
                                                                                
           MOVE SPACES           TO     BP13L14A-REC.                           
                                                                                
           WRITE BP13L14A-REC    AFTER  PAGE.                                   
           WRITE BP13L14A-REC    FROM   L14A-PR-HEAD-01 AFTER 1.                
           WRITE BP13L14A-REC    FROM   L14A-PR-HEAD-02 AFTER 1.                
           WRITE BP13L14A-REC    FROM   L14A-PR-HEAD-03 AFTER 2.                
           WRITE BP13L14A-REC    FROM   L14A-PR-HEAD-04 AFTER 1.                
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       9999-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------*                
                                                                                
           DISPLAY '*********** BP13C14A **************'.                       
           DISPLAY SPACES.                                                      
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECORDS READ    :'.                                   
           DISPLAY '   BP13F800           : '  WS-READ-F800.                    
           DISPLAY '   BP13F520           : '  WS-READ-F520.                    
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECORDS UPDATE  : '  WS-K022-UPDATE.                  
           DISPLAY 'NO MATCH              : '  WS-MATCH.                        
           DISPLAY 'WS-NOT-MATCH          : '  WS-NOT-MATCH.                    
           DISPLAY SPACES.                                                      
           DISPLAY 'FOUND IN K022         : '  WS-FOUND-K022.                   
           DISPLAY 'NOT FOUND IN K022     : '  WS-NOTFND-K022.                  
           DISPLAY '***********************************'.                       
                                                                                
           CLOSE BP13F800                                                       
                 BP13F520                                                       
                 BP13K022                                                       
                 BP13K025                                                       
                 BP13L14A.                                                      
                                                                                
           EVALUATE WS-K022-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K022-STATUS        TO RETURN-CODE                      
                 DISPLAY 'CLOSE BP13K022 FAIL. STATUS=' WS-K022-STATUS          
           END-EVALUATE.                                                        
                                                                                
           EVALUATE WS-K025-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K025-STATUS        TO RETURN-CODE                      
                 DISPLAY 'CLOSE BP13K025 FAIL. STATUS=' WS-K025-STATUS          
           END-EVALUATE.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
