       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD20.                                                 
      *AUTHOR.        EDEN ROSE DINGAL                                          
      *DATE-WRITTEN.  01/04/08.                                                 
      ******************************************************************        
      *                SYSTEM OF COMMITMENT (BP13)                     *        
      ******************************************************************        
      * OBJECTIVE : TO MATCH FD00 WITH F136 (BY REGN)                  *        
      *             AND WRITE RECORDS TO P13F136A                      *        
      *                                                                *        
      * INPUT  FILES : BP13F136                                        *        
      *                BP13FD00                                        *        
      * OUTPUT FILES : P13F136A.                                       *        
      *                                                                *        
      * CHG-NO    BY    ON    - DESCRIPTION                            *        
      * ---------------------------------                              *        
      * BP133209 ED1 01/04/08   NEW PROGRAM                            *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT   BP13F136      ASSIGN TO BP13F136.                           
           SELECT   BP13FD00      ASSIGN TO BP13FD00.                           
           SELECT   P13F136A      ASSIGN TO P13F136A.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   BP13F136                RECORD CONTAINS 150 CHARACTERS              
                                    LABEL RECORDS ARE STANDARD                  
                                    RECORDING MODE IS F.                        
       COPY BP13F136.                                                           
                                                                                
       FD   BP13FD00                RECORD CONTAINS 1600 CHARACTERS             
                                    LABEL RECORDS ARE STANDARD                  
                                    RECORDING MODE IS F.                        
       COPY BP13FD00.                                                           
                                                                                
       FD   P13F136A                RECORD CONTAINS 150 CHARACTERS              
                                    BLOCK CONTAINS 0 RECORDS                    
                                    LABEL RECORDS ARE STANDARD                  
                                    RECORDING MODE IS F.                        
       01 P13F136A-REC  PIC X(150).                                             
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  WS-F136-READ                 PIC 9(07) VALUE ZEROS.              
           05  WS-FD00-READ                 PIC 9(07) VALUE ZEROS.              
           05  WS-F136A-WRITTEN             PIC 9(07) VALUE ZEROS.              
           05  WS-UNMATCH-FD00              PIC 9(07) VALUE ZEROS.              
           05  WS-UNMATCH-F136              PIC 9(07) VALUE ZEROS.              
           05  WS-MATCHED                   PIC 9(07) VALUE ZEROS.              
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CUR-DD                   PIC 9(02) VALUE ZEROES.              
           05  FILLER                      PIC X(01) VALUE '/'.                 
           05  WS-CUR-MM                   PIC 9(02) VALUE ZEROES.              
           05  FILLER                      PIC X(01) VALUE '/'.                 
           05  WS-CUR-CCYY                 PIC 9(04) VALUE ZEROES.              
                                                                                
       01  WS-SYSTEM-DATE-TIME.                                                 
           05  WS-SYS-DATE                  PIC X(08) VALUE SPACES.             
           05  WS-SYS-TIME                  PIC X(08) VALUE SPACES.             
           05  FILLER                       PIC X(05) VALUE SPACES.             
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F136       THRU 2000-EXIT.                     
           PERFORM 2100-READ-BP13FD00       THRU 2100-EXIT.                     
           PERFORM 3000-PROCESS-RECORD      THRU 3000-EXIT                      
             UNTIL F136-NUM-REGN = HIGH-VALUES AND                              
                   FD00-NUM-REGN = HIGH-VALUES.                                 
           PERFORM 9000-CLOSING-ROUTINE     THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT BP13F136                                                  
                      BP13FD00                                                  
               OUTPUT P13F136A.                                                 
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE-TIME.                   
           MOVE WS-SYS-DATE(1:4)      TO WS-CUR-CCYY.                           
           MOVE WS-SYS-DATE(5:2)      TO WS-CUR-MM.                             
           MOVE WS-SYS-DATE(7:2)      TO WS-CUR-DD.                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F136.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F136                                                        
             AT END MOVE HIGH-VALUES TO F136-NUM-REGN                           
             GO TO 2000-EXIT.                                                   
                                                                                
           ADD  1                    TO WS-F136-READ.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2100-READ-BP13FD00.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13FD00                                                        
             AT END MOVE HIGH-VALUES TO  FD00-NUM-REGN                          
             GO TO 2100-EXIT.                                                   
                                                                                
           ADD  1                    TO  WS-FD00-READ.                          
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-RECORD.                                                     
      *-------------------------------------------------------------            
                                                                                
           IF FD00-NUM-REGN = F136-NUM-REGN                                     
              PERFORM 3100-MATCH-REGN        THRU 3100-EXIT                     
                      UNTIL F136-NUM-REGN NOT = FD00-NUM-REGN                   
              PERFORM 2100-READ-BP13FD00     THRU 2100-EXIT                     
                                                                                
           ELSE                                                                 
              IF FD00-NUM-REGN > F136-NUM-REGN                                  
                 ADD   1     TO WS-UNMATCH-F136                                 
                 ADD   1     TO WS-F136A-WRITTEN                                
                 WRITE P13F136A-REC FROM BP13F136-CASHTRAN                      
                 PERFORM 2000-READ-BP13F136 THRU 2000-EXIT                      
              ELSE                                                              
                 ADD   1                     TO WS-UNMATCH-FD00                 
                 PERFORM 2100-READ-BP13FD00  THRU 2100-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-MATCH-REGN.                                                         
      *-------------------------------------------------------------            
                                                                                
           ADD 1          TO WS-MATCHED.                                        
                                                                                
           IF F136-NUM-LOAN-SCH = SPACES OR LOW-VALUES                          
              MOVE FD00-CDE-LOAN-SCH     TO  F136-NUM-LOAN-SCH                  
           END-IF.                                                              
                                                                                
           MOVE BP13F136-CASHTRAN        TO  P13F136A-REC.                      
                                                                                
           WRITE  P13F136A-REC.                                                 
           ADD 1  TO WS-F136A-WRITTEN.                                          
                                                                                
           PERFORM 2000-READ-BP13F136 THRU 2000-EXIT.                           
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '***************************************'.                   
           DISPLAY 'PROGRAM ID     :  BP13CD20'.                                
           DISPLAY 'CURRENT DATE   : ' WS-CURRENT-DATE.                         
           DISPLAY '***************************************'.                   
           DISPLAY 'NO. OF BP13FD00 READ     : ' WS-FD00-READ.                  
           DISPLAY 'NO. OF BP13F136 READ     : ' WS-F136-READ.                  
           DISPLAY 'NO. OF F136 UNMATCHED    : ' WS-UNMATCH-F136.               
           DISPLAY 'NO. OF FD00 UNMATCHED    : ' WS-UNMATCH-FD00.               
           DISPLAY 'NO. OF FD00/F136 MATCHED : ' WS-MATCHED.                    
           DISPLAY 'NO. OF P13F136A  WRITTEN : ' WS-F136A-WRITTEN.              
                                                                                
           CLOSE BP13F136                                                       
                 BP13FD00                                                       
                 P13F136A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
