      *----------------------------------------------------------------*        
       IDENTIFICATION DIVISION.                                                 
      *----------------------------------------------------------------*        
       PROGRAM-ID.    BP13C377.                                                 
       AUTHOR.        SK.                                                       
       DATE-WRITTEN.  20/03/2003.                                               
                                                                                
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                   *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  1. SURRENDER OF STUDIO APARTMENTS             *        
      *                     UPDATE DATE COMPENSATION PAID &            *        
      *                            AMOUNT COMPENSATION                 *        
      *                                                                *        
      *   INPUT FILES :  1.  BP13K690  -  AMORTISE DEFER INCOME        *        
      *                      AG07F630  -  VOUCHER TRANSACTION FILE     *        
      *                                                                *        
      *----------------------------------------------------------------*        
      *                                                                *        
      *CHGE-NO   BY    DATE      DESCRIPTION                           *        
      *--------  ---  ---------- -----------                           *        
      *BP132292  SK   20/03/2003 NEW PGM                               *        
      *BP132996  ED1  27/02/2007 CHANGED READ OF BP13K690 TO STARTBR   *        
      *                          OF FIRST 9-BYTES OF SCH ACC & ADDED   *        
      *                          REPORT BP13L377                       *        
      *BP136398  FNP1 17/08/2016 EXPAND F690 FROM 250 TO 600           *        
      *BP138944  FP6  14/10/2021 EXPAND F690 FROM 600 TO 800           *        
      ******************************************************************        
                                                                                
      *----------------------------------------------------------------*        
       ENVIRONMENT DIVISION.                                                    
      *----------------------------------------------------------------*        
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT AG07F630  ASSIGN        TO AG07F630.                          
           SELECT BP13F690  ASSIGN        TO BP13F690.                          
           SELECT BP13L377  ASSIGN        TO BP13L377.                          
           SELECT P13F690A  ASSIGN        TO P13F690A.                          
                                                                                
           SELECT BP13K690  ASSIGN        TO BP13K690                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K690-KEY-FLD                       
                            FILE STATUS   IS WS-K690-STATUS.                    
                                                                                
      *----------------------------------------------------------------*        
       DATA DIVISION.                                                           
      *----------------------------------------------------------------*        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  AG07F630                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 130 CHARACTERS.                                      
                                                                                
       COPY AG07F630.                                                           
                                                                                
       FD  BP13K690                                                             
           RECORD CONTAINS 800 CHARACTERS.                                      
                                                                                
       COPY BP13K690.                                                           
                                                                                
       FD  BP13F690                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 800 CHARACTERS                                       
           RECORDING MODE F                                                     
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F690.                                                           
                                                                                
       FD  P13F690A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 800 CHARACTERS                                       
           RECORDING MODE F                                                     
           LABEL RECORDS ARE STANDARD.                                          
       01  P13F690A-REC   PIC X(800).                                           
                                                                                
       FD  BP13L377                                                             
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORD IS OMITTED                                              
           RECORDING MODE IS F.                                                 
       01  L377-PRTREC  PIC X(132).                                             
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       WORKING-STORAGE SECTION.                                                 
      *----------------------------------------------------------------*        
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-CNT-K690-READ          PIC 9(8)    VALUE 0.                    
           05 WS-CNT-F630-READ          PIC 9(8)    VALUE 0.                    
           05 WS-CNT-F690-WRITE         PIC 9(8)    VALUE 0.                    
           05 WS-CNT-F690A-WRITE        PIC 9(8)    VALUE 0.                    
           05 WS-CNT-K690-UPDATE        PIC 9(8)    VALUE 0.                    
           05 WS-CNT-K690-NOTFND        PIC 9(8)    VALUE 0.                    
           05 WS-CNT-L377-WRITE         PIC 9(8)    VALUE 0.                    
           05 WS-SNO                    PIC 9(5)    VALUE 0.                    
           05 WS-PAGE-CTR               PIC 9(4)    VALUE 0.                    
           05 WS-LINE-CTR               PIC 9(2)    VALUE 0.                    
                                                                                
       01  WS-SWITCHES.                                                         
           05 WS-F630-EOF               PIC X(1)    VALUE 'N'.                  
           05 WS-K690-EOF               PIC X(1)    VALUE 'N'.                  
           05 WS-K690-FOUND             PIC X(1)    VALUE 'Y'.                  
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-K690-STATUS            PIC 9(2)    VALUE 0.                    
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY              PIC 9(4).                               
           05  WS-SYS-MM                PIC 9(2).                               
           05  WS-SYS-DD                PIC 9(2).                               
                                                                                
       01  WS-EDITED-DATE               PIC X(10).                              
       01  WS-MAX-LINE                  PIC 99    VALUE 58.                     
                                                                                
       01  WS-BLANK-LINE                PIC X(132) VALUE SPACES.                
       01  WS-DASH-LINE                 PIC X(132) VALUE ALL '-'.               
                                                                                
      *---> DETAIL LINES FOR REPORT                                             
       01  L377-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13L377'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  L377-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L377-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
       01  L377-HEADER02.                                                       
           05  FILLER              PIC X(35)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
               'REPORT ON UPDATE OF COMPENSATION AMOUNT'.                       
           05  FILLER              PIC X(14)     VALUE                          
               ' AND DATE PAID'.                                                
                                                                                
       01  L377-HEADER03.                                                       
           05  FILLER              PIC X(07)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'AMORTISE'.              
           05  FILLER              PIC X(08)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'VOUCHER'.               
           05  FILLER              PIC X(13)     VALUE SPACES.                  
           05  FILLER              PIC X(22)     VALUE                          
               '---- COMPENSATION ----'.                                        
           05  FILLER              PIC X(09)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE 'SURRENDER'.             
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(11)     VALUE 'TRANSACTION'.           
                                                                                
       01  L377-HEADER04.                                                       
           05  FILLER              PIC X(03)     VALUE 'S/N'.                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'SCH-ACC'.               
           05  FILLER              PIC X(09)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'SCH-ACC'.               
           05  FILLER              PIC X(09)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'REGN NO'.               
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE 'PAID DATE'.             
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'AMT'.                   
           05  FILLER              PIC X(07)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'AMT BAL'.               
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE 'REFERENCE'.             
                                                                                
       01 L377-DETAILS.                                                         
          05 L377-DTL-SERIAL      PIC ZZZZ9     VALUE ZEROES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 L377-SCH-ACC         PIC X(14)     VALUE SPACES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 L377-SCH-ACC2        PIC X(14)     VALUE SPACES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 L377-REGNO           PIC X(08)     VALUE SPACES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 L377-PAID-DTE        PIC X(10)     VALUE SPACES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 L377-COMPN-AMT       PIC ZZZ,ZZ9.99 VALUE ZEROES.                  
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 L377-SURNDR-BAL      PIC ZZZ,ZZ9.99 VALUE ZEROES.                  
          05 FILLER               PIC X(03)      VALUE SPACES.                  
          05 L377-TRANS-REF       PIC X(19)      VALUE SPACES.                  
                                                                                
      *----------------------------------------------------------------*        
       PROCEDURE DIVISION.                                                      
      *----------------------------------------------------------------*        
                                                                                
      *----------------------------------------------------------------*        
       0000-MAIN-ROUTINE.                                                       
      *----------------------------------------------------------------*        
                                                                                
           PERFORM  1000-OPEN-FILES      THRU  1000-EXIT.                       
           PERFORM  2000-READ-AG07F630   THRU  2000-EXIT.                       
           PERFORM  3000-PROCESS         THRU  3000-EXIT                        
                    UNTIL WS-F630-EOF = 'Y'.                                    
           PERFORM  9000-CLOSE-ROUTINE   THRU  9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       1000-OPEN-FILES.                                                         
      *----------------------------------------------------------------*        
                                                                                
           OPEN INPUT  AG07F630                                                 
                I-O    BP13K690                                                 
                OUTPUT BP13F690                                                 
                       BP13L377                                                 
                       P13F690A.                                                
                                                                                
           EVALUATE WS-K690-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K690-STATUS        TO RETURN-CODE                      
                 DISPLAY 'OPEN BP13K690 FAIL. STATUS=' WS-K690-STATUS           
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATE.                      
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       2000-READ-AG07F630.                                                      
      *----------------------------------------------------------------*        
                                                                                
           READ AG07F630                                                        
                AT END                                                          
                   MOVE 'Y' TO WS-F630-EOF                                      
                NOT AT END                                                      
                   ADD 1 TO WS-CNT-F630-READ                                    
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3000-PROCESS.                                                            
      *----------------------------------------------------------------*        
                                                                                
           IF F630-NUM-TRANS-ID = 'SAPS'                                        
              PERFORM 3100-STARTBR-BP13K690 THRU 3100-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-AG07F630 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3100-STARTBR-BP13K690.                                                   
      *----------------------------------------------------------------*        
           MOVE 'N'                   TO WS-K690-EOF.                           
           MOVE SPACES                TO K690-REC.                              
           MOVE F630-NUM-HDB-REF(1:9) TO K690-NUM-SCH-ACC.                      
                                                                                
           START BP13K690 KEY >= K690-KEY-FLD                                   
                                                                                
           EVALUATE WS-K690-STATUS                                              
              WHEN 00                                                           
                 PERFORM 3200-READNXT-BP13K690 THRU 3200-EXIT                   
                         UNTIL WS-K690-EOF = 'Y' OR                             
                 F630-NUM-HDB-REF(1:9) NOT = K690-NUM-SCH-ACC(1:9)              
                                                                                
              WHEN 23                                                           
                 DISPLAY 'REC NOT FND IN BP13K690'                              
                         ' KEY ' F630-NUM-HDB-REF                               
                 GO TO 3100-EXIT                                                
                                                                                
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN STARTBR BP13K690 - STATUS '                  
                 WS-K690-STATUS ' KEY ' F630-NUM-HDB-REF                        
                 MOVE        WS-K690-STATUS    TO RETURN-CODE                   
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       3200-READNXT-BP13K690.                                                   
      *----------------------------------------------------------------*        
           READ BP13K690 NEXT RECORD                                            
                  AT END MOVE 'Y' TO WS-K690-EOF                                
                     GO TO 3200-EXIT.                                           
                                                                                
           EVALUATE WS-K690-STATUS                                              
              WHEN 00                                                           
              WHEN 22                                                           
                 IF F630-NUM-HDB-REF(1:9) = K690-NUM-SCH-ACC(1:9)               
                    PERFORM 4000-UPDATE-BP13K690  THRU 4000-EXIT                
                 END-IF                                                         
                                                                                
              WHEN 10                                                           
                 MOVE 'Y' TO WS-K690-EOF                                        
                                                                                
              WHEN 23                                                           
                 MOVE 'Y' TO WS-K690-EOF                                        
                 DISPLAY 'REC NOT FND IN BP13K690'                              
                         ' KEY ' F630-NUM-HDB-REF                               
                                                                                
              WHEN OTHER                                                        
                 MOVE 'Y' TO WS-K690-EOF                                        
                 DISPLAY 'ERROR IN READNXT BP13K690 - STATUS '                  
                 WS-K690-STATUS ',KEY ' F630-NUM-HDB-REF                        
                 MOVE        WS-K690-STATUS    TO RETURN-CODE                   
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       4000-UPDATE-BP13K690.                                                    
      *----------------------------------------------------------------*        
      *ERROR CODE                                                      *        
      *                                                                *        
      *WR - NO SURRENDER DATE                                          *        
      *WB - WITH ABORTIVE DATE                                         *        
      *----------------------------------------------------------------*        
                                                                                
           IF K690-AMT-COMPENSATION NOT NUMERIC                                 
              MOVE 0 TO K690-AMT-COMPENSATION                                   
           END-IF.                                                              
                                                                                
           IF K690-AMT-SURRENDER-BAL NOT NUMERIC                                
              MOVE 0 TO K690-AMT-SURRENDER-BAL                                  
           END-IF.                                                              
                                                                                
           IF (K690-DTE-SURRENDER-REQUEST = SPACES OR LOW-VALUES                
                                            OR ZEROS)                           
               MOVE 'WR'     TO K690-TRANS-RMK                                  
               PERFORM 4100-WRITE-F690-ERROR   THRU 4100-EXIT                   
           ELSE                                                                 
              IF (K690-DTE-ABORTIVE NOT = SPACES AND LOW-VALUES                 
                                          AND ZEROS)                            
                  MOVE 'WB'     TO K690-TRANS-RMK                               
                  PERFORM 4100-WRITE-F690-ERROR   THRU 4100-EXIT                
               ELSE                                                             
                  PERFORM 4020-REWRITE-BP13K690   THRU 4020-EXIT                
               END-IF                                                           
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4020-REWRITE-BP13K690.                                                   
      *----------------------------------------------------------------*        
                                                                                
           MOVE F630-DTE-TRANS        TO K690-DTE-COMPENSATION-PD.              
           ADD     F630-AMT-TRANS     TO K690-AMT-COMPENSATION.                 
                                                                                
           COMPUTE K690-AMT-SURRENDER-BAL =                                     
                        K690-AMT-SURRENDER-BAL - F630-AMT-TRANS.                
                                                                                
           MOVE F630-NUM-TRANS-REF     TO K690-NUM-PRS-REF.                     
                                                                                
           REWRITE K690-REC.                                                    
                                                                                
           EVALUATE WS-K690-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO WS-CNT-K690-UPDATE                                    
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN UPDATING BP13K690 - STATUS '                 
                             WS-K690-STATUS                                     
                 MOVE        WS-K690-STATUS    TO RETURN-CODE                   
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           PERFORM 4050-WRITE-F690          THRU 4050-EXIT.                     
                                                                                
           IF F630-NUM-HDB-REF(10:2) NOT = K690-NUM-SCH-ACC(10:2)               
              PERFORM 5000-WRITE-BP13L377      THRU 5000-EXIT                   
           END-IF.                                                              
                                                                                
       4020-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4050-WRITE-F690.                                                         
      *----------------------------------------------------------------*        
                                                                                
           MOVE  K690-REC           TO F690-REC.                                
           MOVE  F630-AMT-TRANS     TO F690-AMT-COMPENSATION.                   
           WRITE F690-REC.                                                      
           ADD 1 TO WS-CNT-F690-WRITE.                                          
                                                                                
       4050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4100-WRITE-F690-ERROR.                                                   
      *----------------------------------------------------------------*        
                                                                                
           MOVE  K690-REC           TO F690-REC.                                
           MOVE  F630-AMT-TRANS     TO F690-AMT-COMPENSATION.                   
           WRITE P13F690A-REC       FROM F690-REC.                              
           ADD 1 TO WS-CNT-F690A-WRITE.                                         
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       5000-WRITE-BP13L377.                                                     
      *----------------------------------------------------------------*        
      *------>  MOVE DETAILS                                                    
           INITIALIZE     L377-DETAILS.                                         
                                                                                
           STRING K690-NUM-SCH-ACC(1:4) '-'                                     
                  K690-NUM-SCH-ACC(5:4) '-'                                     
                  K690-NUM-SCH-ACC(9:1) '-'                                     
                  K690-NUM-SCH-ACC(10:2)                                        
                  DELIMITED BY SIZE INTO L377-SCH-ACC.                          
                                                                                
           STRING F630-NUM-HDB-REF(1:4) '-'                                     
                  F630-NUM-HDB-REF(5:4) '-'                                     
                  F630-NUM-HDB-REF(9:1) '-'                                     
                  F630-NUM-HDB-REF(10:2)                                        
                  DELIMITED BY SIZE INTO L377-SCH-ACC2.                         
                                                                                
           MOVE K690-NUM-REGN               TO L377-REGNO.                      
           MOVE K690-AMT-COMPENSATION       TO L377-COMPN-AMT.                  
           MOVE K690-AMT-SURRENDER-BAL      TO L377-SURNDR-BAL.                 
           MOVE K690-NUM-PRS-REF            TO L377-TRANS-REF.                  
                                                                                
           IF K690-DTE-COMPENSATION-PD NOT = SPACES AND LOW-VALUES              
                                               AND ZEROES                       
              STRING K690-DTE-COMPENSATION-PD(7:2) '/'                          
                     K690-DTE-COMPENSATION-PD(5:2) '/'                          
                     K690-DTE-COMPENSATION-PD(1:4)                              
                     DELIMITED BY SIZE INTO L377-PAID-DTE                       
           END-IF.                                                              
                                                                                
      *------>  PRINT REPORT                                                    
           ADD   1                      TO  WS-SNO.                             
           MOVE  WS-SNO                 TO  L377-DTL-SERIAL.                    
           MOVE  WS-EDITED-DATE         TO  L377-DATE-HDR.                      
                                                                                
           IF WS-LINE-CTR > WS-MAX-LINE OR                                      
              WS-SNO       = 1                                                  
              ADD            1   TO WS-PAGE-CTR                                 
              MOVE ZEROES        TO L377-PAGE-HDR                               
              MOVE WS-PAGE-CTR   TO L377-PAGE-HDR                               
              PERFORM 6000-PRINT-HEADING THRU 6000-EXIT                         
           END-IF.                                                              
                                                                                
           WRITE L377-PRTREC FROM L377-DETAILS.                                 
                                                                                
           ADD   1   TO   WS-LINE-CTR.                                          
           ADD   1   TO   WS-CNT-L377-WRITE.                                    
                                                                                
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------           
       6000-PRINT-HEADING.                                                      
      *--------------------------------------------------------------           
                                                                                
            WRITE L377-PRTREC FROM WS-BLANK-LINE.                               
            WRITE L377-PRTREC FROM WS-BLANK-LINE.                               
            WRITE L377-PRTREC FROM WS-BLANK-LINE.                               
            WRITE L377-PRTREC FROM L377-HEADER01 AFTER PAGE.                    
            WRITE L377-PRTREC FROM L377-HEADER02.                               
            WRITE L377-PRTREC FROM WS-BLANK-LINE.                               
            WRITE L377-PRTREC FROM L377-HEADER03.                               
            WRITE L377-PRTREC FROM L377-HEADER04.                               
            WRITE L377-PRTREC FROM WS-DASH-LINE.                                
                                                                                
            INITIALIZE WS-LINE-CTR.                                             
            MOVE  9 TO WS-LINE-CTR.                                             
                                                                                
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------------------------------------------------*        
       9000-CLOSE-ROUTINE.                                                      
      *----------------------------------------------------------------*        
                                                                                
           CLOSE BP13K690                                                       
                 AG07F630                                                       
                 BP13F690                                                       
                 BP13L377                                                       
                 P13F690A.                                                      
                                                                                
           EVALUATE WS-K690-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K690-STATUS      TO RETURN-CODE                        
                 DISPLAY 'CLOSING BP13K690 ERROR. STATUS='                      
                                             WS-K690-STATUS                     
           END-EVALUATE.                                                        
                                                                                
           DISPLAY '                                    '.                      
           DISPLAY '***** BP13C377 CONTROL TOTALS ***** '.                      
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY '                                    '.                      
           DISPLAY 'NO OF RECORDS READ FROM  (AG07F630) = '                     
                    WS-CNT-F630-READ.                                           
           DISPLAY 'NO OF RECORDS UPDATED    (BP13K690) = '                     
                    WS-CNT-K690-UPDATE.                                         
           DISPLAY 'NO OF RECORDS WRITE      (BP13F690) = '                     
                    WS-CNT-F690-WRITE.                                          
           DISPLAY 'NO OF RECORDS WRITE-ERROR(P13F690A) = '                     
                    WS-CNT-F690A-WRITE.                                         
           DISPLAY 'NO OF RECS IN REPORT     (BP13L377) = '                     
                    WS-CNT-L377-WRITE.                                          
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
