       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C713.                                                 
      *AUTHOR.        RAYATHATHA ASHOK.                                         
      *DATE-WRITTEN.  03/07/92.                                                 
      *============================================================*            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      *============================================================*            
      *  OBJECTIVE : MATCH THE RELEVANT REQUEST FOR THAT ROUND OF  *            
      *              PIGEONHOLING, OUTPUT SUCC AND UNSUCC REQUEST  *            
      *              FOR BP13C522 AND BP13C530 PRINTING REPORTS.   *            
      *                                                            *            
      *  INPUT FILE :  1. BP13F510 ( REQ FOR PIGEONHOLING)         *            
      *                1. BP13K520 ( BALLOT FILE  )                *            
      *                                                            *            
      *  OUTPUT FILE:  1. P13UNSUC (UNSUCC REQ)                    *            
      *                2. P13SUCC  (SUCC REQ)                      *            
      *                                                            *            
      *------------------------------------------------------------*            
      * BP130340 19971115 SSS  CHG CDE-SUCC-NT TO CDE-SUCC-NT-ZONE *            
      * BP130652 19990219 NOR  EXPAND F510 & K520 RECL 300 TO 500  *            
      *============================================================*            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F510 ASSIGN TO  BP13F510.                                 
                                                                                
           SELECT BP13K520 ASSIGN TO  BP13K520                                  
                ACCESS MODE  IS RANDOM                                          
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K520-KEY-FLD                                    
                FILE STATUS  IS BP13K520-STATUS.                                
                                                                                
           SELECT P13UNSUC ASSIGN TO  P13UNSUC.                                 
                                                                                
           SELECT P13SUCC  ASSIGN TO  P13SUCC.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13K520                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13K520.                                                           
                                                                                
       FD  BP13F510                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F510.                                                           
                                                                                
       FD  P13UNSUC                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  P13UNSUC-REC                   PIC X(500).                           
                                                                                
       FD  P13SUCC                                                              
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  P13SUCC-REC                     PIC X(500).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  BP13K520-STATUS                PIC 99         VALUE ZEROS.           
       01  WS-UNSUC-WRITE                 PIC 9(5)       VALUE ZEROS.           
       01  WS-SUCC-WRITE                  PIC 9(5)       VALUE ZEROS.           
       01  WS-BP13F510-READ               PIC 9(5)       VALUE ZEROS.           
       01  WS-BP13K520-READ               PIC 9(5)       VALUE ZEROS.           
       01  WS-F510-EOF                    PIC X          VALUE 'N'.             
           88 F510-EOF                                   VALUE 'Y'.             
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN.                                                                
      *-------------------------------------------------------------            
           PERFORM 100-OPEN-FILE          THRU 100-EXIT.                        
           PERFORM 200-READ-BP13F510      THRU 200-EXIT.                        
           PERFORM 300-PROCESS-BP13K520   THRU 300-EXIT                         
                                          UNTIL F510-EOF.                       
           PERFORM 900-CLOSE-ROUTINE      THRU 900-EXIT.                        
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-OPEN-FILE.                                                           
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F510                                                 
                       BP13K520                                                 
               OUTPUT  P13UNSUC                                                 
                       P13SUCC.                                                 
                                                                                
           IF BP13K520-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR - BP13K520-STATUS : '                      
                                       BP13K520-STATUS                          
              MOVE BP13K520-STATUS       TO   RETURN-CODE                       
              PERFORM 900-CLOSE-ROUTINE  THRU 900-EXIT.                         
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-BP13F510.                                                       
      *-------------------------------------------------------------            
           READ BP13F510                 AT END                                 
           MOVE 'Y'                      TO WS-F510-EOF                         
           GO TO 200-EXIT.                                                      
                                                                                
           ADD 1                         TO WS-BP13F510-READ.                   
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PROCESS-BP13K520.                                                    
      *-------------------------------------------------------------            
           MOVE F510-NUM-RANDOM          TO K520-KEY-FLD.                       
           READ BP13K520.                                                       
           IF BP13K520-STATUS = 00                                              
              ADD 1                      TO WS-BP13K520-READ                    
              IF K520-CDE-SUCC-NT-ZONE = SPACES OR LOW-VALUES                   
                 MOVE BP13K520-REC       TO P13UNSUC-REC                        
                 WRITE P13UNSUC-REC                                             
                 ADD 1                   TO WS-UNSUC-WRITE                      
              ELSE                                                              
                 MOVE BP13K520-REC       TO P13SUCC-REC                         
                 WRITE P13SUCC-REC                                              
                 ADD 1                   TO WS-SUCC-WRITE                       
           ELSE                                                                 
              IF BP13K520-STATUS = 23                                           
                 NEXT SENTENCE                                                  
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K520-STATUS : '                        
                                     BP13K520-STATUS.                           
                                                                                
           PERFORM 200-READ-BP13F510      THRU 200-EXIT.                        
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
      *-------------------------------------------------------------            
            DISPLAY ' '.                                                        
            DISPLAY ' *************** BP13C713 *************** '.               
            DISPLAY ' '.                                                        
            DISPLAY ' NO OF REQUEST READ FROM BP13F510       : '                
                                              WS-BP13F510-READ.                 
            DISPLAY ' NO OF BALLOT  READ FROM BP13K520       : '                
                                              WS-BP13K520-READ.                 
            DISPLAY ' NO OF UNSUCC REQUEST IN PIGEONHOLING   : '                
                                              WS-UNSUC-WRITE.                   
            DISPLAY ' NO OF SUCC REQUEST IN PIGEONHOLING     : '                
                                              WS-SUCC-WRITE.                    
            DISPLAY ' '.                                                        
                                                                                
            CLOSE BP13K520                                                      
                  BP13F510                                                      
                  P13UNSUC                                                      
                  P13SUCC.                                                      
                                                                                
            IF BP13K520-STATUS NOT = 00 AND 97                                  
               DISPLAY 'CLOSING ERROR-BP13F500 ' BP13K520-STATUS                
               MOVE BP13K520-STATUS      TO RETURN-CODE.                        
                                                                                
            STOP RUN.                                                           
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
