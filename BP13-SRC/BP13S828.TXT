    /*=============================================================*/           
    /* PROGRAM ID  :  BP13S828                                     */           
    /* WRITTEN BY  :  PAULO CAMIA LEGASPI                          */           
    /* DATE WRITTEN:  17/11/2009                                   */           
    /* SAS PROGRAM TO PRINT DETAIL REPORT ON NOT BOOK CASES        */           
    /*=============================================================*/           
    /* CHG REQ   DATE   BY   DESCRIPTION                           */           
    /* =======  ====== ====  ===================================== */           
    /* BP133792 301109 PCL3  NEW PROGRAM                           */           
    /* BP133921 130710 PCL3  TO ADD NAME1 AND HA1_INCOME           */           
    /* BP133905 160710 JB8   TO DISPLAY OIC                        */           
    /* BP133905 090910 PCL3  TO OUTPUT P13L828A                    */           
    /* BP134011 221010 PCL3  TO USE BTO COUNT (COL 467)            */           
    /* BP134149 110111 PCL3  TO USE 'PUT' STATEMENT IN PRINTING    */           
    /*                       P13L828A REPORT.                      */           
    /* BP135237 100314 IMC1  TO ADD NEW SMS FND TAG                */           
    /* BP135785 230715 FNP1  PUT SEMICOLON AND HEADER IN P13L828A  */           
    /* BP137734 210319 FNP1  USE NRIC1 WHEN PRINTING               */           
    /* BP138080 051119 SMP3  ADD NUM-QUEUE AND REQUEST-STAT        */           
    /*=============================================================*/           
                                                                                
    OPTIONS  NODATE;                                                            
    %GLOBAL DATE;                                                               
                                                                                
     DATA BP13F740;                                                             
     INFILE BP13F740;                                                           
     INPUT  @123  REGN       $CHAR8.                                            
            @41   NRIC1      $CHAR9.                                            
            @187  NT         $CHAR3.                                            
            @211  FT         $CHAR2.                                            
            @116  HH         $CHAR1.                                            
            @397  SMS_FND    $CHAR1.                                            
            @461  HH_BAL     $CHAR1.                                            
            @235  QUEUE      $CHAR5.                                            
            @260  REQ_ST     $CHAR1.                                            
            @290  BK_ST      $CHAR2.                                            
            @493  AL_TG      $CHAR2.                                            
            @469  BK_TAG     $CHAR1.                                            
            @467  TRIES      $CHAR2.;                                           
                                                                                
     DATA BP13K828;                                                             
     INFILE BP13K828;                                                           
     INPUT  @1    REGN       $CHAR8.                                            
            @9    NRIC       $CHAR9.                                            
            @109  HA_INC     5.                                                 
            @28   HA_NAME    $CHAR66.;                                          
                                                                                
     IF _N_ = 1 THEN                                                            
        DO;                                                                     
          CALL SYMPUT('DATE',PUT(TODAY(),DDMMYY10.));                           
        END;                                                                    
                                                                                
   PROC SORT DATA=BP13F740; BY REGN;                                            
   PROC SORT DATA=BP13K828; BY REGN;                                            
   DATA HAINC; MERGE BP13F740(IN=J) BP13K828(IN=K); BY REGN; IF J;              
                                                                                
   DATA HAINC2;                                                                 
    RETAIN INCOME;                                                              
    RETAIN NAME1;                                                               
    SET HAINC; BY REGN;                                                         
    IF FIRST.REGN THEN DO;                                                      
       INCOME = 0;                                                              
       NAME1  = '                                                  ';           
    END;                                                                        
    INCOME   = INCOME + HA_INC;                                                 
    IF NRIC1 = NRIC THEN NAME1 = HA_NAME;                                       
                                                                                
    SEP = ';';                                                                  
                                                                                
    K023_KEY = REGN;                                                            
    INFILE BP13K023 VSAM KEY = K023_KEY;                                        
    INPUT @;                                                                    
      RC = _IORC_;                                                              
      IF RC = 0 THEN                                                            
         DO;                                                                    
             INPUT @1   OIC_NAME  $CHAR5.;                                      
         END;                                                                   
                                                                                
    IF LAST.REGN THEN OUTPUT;                                                   
   RUN;                                                                         
                                                                                
   PROC SORT DATA=HAINC2 OUT=BYBK;                                              
     BY BK_TAG TRIES;                                                           
   RUN;                                                                         
                                                                                
   OPTIONS MISSING=0 PAGENO=1 PAGESIZE=20000;                                   
     DATA _NULL_; SET BYBK END=EOF;                                             
             FILE P13L828A NOTITLE HEADER=H;                                    
             SEP = ';';                                                         
                PUT REGN     $1-8                                               
                    SEP      $9                                                 
                    NRIC1    $10-18                                             
                    SEP      $19                                                
                    NT       $20-22                                             
                    SEP      $23                                                
                    HH       $24                                                
                    SEP      $25                                                
                    HH_BAL   $26                                                
                    SEP      $27                                                
                    BK_ST    $28-29                                             
                    SEP      $30                                                
                    AL_TG    $31-32                                             
                    SEP      $33                                                
                    BK_TAG   $34                                                
                    SEP      $35                                                
                    TRIES    $36-37                                             
                    SEP      $38                                                
                    INCOME    39-43                                             
                    SEP      $44                                                
                    OIC_NAME  45-49                                             
                    SEP      $50                                                
                    SMS_FND  $51                                                
                    SEP      $52                                                
                    NAME1     53-118                                            
                    SEP      $119                                               
                    FT        120-121                                           
                    SEP      $122                                               
                    QUEUE     123-127                                           
                    SEP      $128                                               
                    REQ_ST    129                                               
                    SEP      $130;                                              
   RETURN;                                                                      
                                                                                
   H:                                                                           
    PUT @1     'REGN;'                                                          
        @10    'NRIC;'                                                          
        @20    'NT;'                                                            
        @24    'HH;'                                                            
        @27    'HH BAL;'                                                        
        @34    'BK ST;'                                                         
        @40    'AL TG;'                                                         
        @46    'BK TAG;'                                                        
        @53    'TRIES;'                                                         
        @59    'INCOME;'                                                        
        @66    'OIC NAME;'                                                      
        @75    'SMS FND;'                                                       
        @83    'NAME;'                                                          
        @120   'FT;'                                                            
        @123   'QUEUE;'                                                         
        @129   'RS;';                                                           
                                                                                
   RETURN;                                                                      
   RUN;                                                                         
                                                                                
   OPTIONS MISSING=0 PAGENO=1 PAGESIZE=60;                                      
   PROC PRINTTO FILE=BP13L828 NEW;                                              
   PROC PRINT DATA=BYBK NOOBS ;                                                 
    VAR REGN SEP NRIC1 SEP NT SEP FT SEP HH SEP HH_BAL SEP BK_ST                
      SEP  AL_TG SEP BK_TAG SEP TRIES SEP INCOME SEP OIC_NAME                   
      SEP  SMS_FND SEP NAME1 SEP QUEUE SEP REQ_ST;                              
                                                                                
TITLE1 "BP13L828   HDB3                            SYSTEM OF COMMITMENT         
                DATE :   &DATE      PAGE : ";                                   
TITLE2 ' BTO UNSUCCESSFUL APPLICANTS (BTO) - NOT FD IN SOC/RESALE';             
                                                                                
   RUN;                                                                         
                                                                                
