       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF88.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  13/06/2013.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *    OBJECTIVE  : AFTER PIGEONHOLING OF SAP, COMPUTE THE     *            
      *                 EXTRA ETHNIC PROPORTION TAKEN BY SAP,      *            
      *                 THEN ADJUST THE ETHNIC PROPORTION FOR      *            
      *                 PA WHICH HAVE TAKEN UP BY SAP.             *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP134914  SMR2  13062013  NEW PROGRAM(SIMILAR TO BP13C766) *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.     IBM-3090.                                           
       OBJECT-COMPUTER.     IBM-3090.                                           
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K010    ASSIGN TO BP13K010                                
                              ORGANIZATION IS INDEXED                           
                              ACCESS MODE  IS SEQUENTIAL                        
                              RECORD KEY   IS K010-KEY-FLD                      
                              FILE STATUS  IS WS-K010-STATUS.                   
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K010                                                             
                              LABEL RECORD IS STANDARD                          
                              RECORD CONTAINS 600 CHARACTERS                    
                              BLOCK CONTAINS 0 RECORDS.                         
           COPY BP13K010.                                                       
                                                                                
                                                                                
        WORKING-STORAGE SECTION.                                                
        01  WS-VARIABLES.                                                       
            05  WS-CNT-K010-READ         PIC 9(8) VALUE 0.                      
            05  WS-CNT-K010-REWRITE      PIC 9(8) VALUE 0.                      
            05  WS-NUM-BAL-SAP           PIC 9(5) VALUE 0.                      
            05  WS-K010-STATUS           PIC 99   VALUE 0.                      
            05  WS-K010-EOF              PIC X    VALUE 'N'.                    
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-CONTROL-RTN.                                                        
                                                                                
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 2000-START-BP13K010-KEY THRU 2000-EXIT.              01790000
           PERFORM 4000-MAIN-ROUTINE     THRU 4000-EXIT                         
             UNTIL WS-K010-EOF = 'Y'.                                           
           PERFORM 9999-CLOSE-FILES      THRU 9999-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-FILES.                                                         
                                                                                
           OPEN I-O    BP13K010.                                                
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K010, STATUS ' WS-K010-STATUS          
              MOVE WS-K010-STATUS        TO RETURN-CODE                         
              GO TO 9999-CLOSE-FILES.                                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                        01780000
       2000-START-BP13K010-KEY.                                         01790000
            MOVE SPACES TO K010-KEY-FLD                                 01800000
            START BP13K010 KEY > K010-KEY-FLD                           01810000
                                                                                
           IF WS-K010-STATUS  = 00                                              
              PERFORM 3000-READ-BP13K010    THRU 3000-EXIT                      
           ELSE                                                                 
              DISPLAY 'START READ BP13K010, STATUS ' WS-K010-STATUS             
              MOVE WS-K010-STATUS        TO RETURN-CODE                         
              GO TO 9999-CLOSE-FILES.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-READ-BP13K010.                                                      
                                                                                
           READ BP13K010 NEXT AT END                                            
                              MOVE 'Y' TO WS-K010-EOF                           
                              GO TO 3000-EXIT.                                  
           ADD 1 TO WS-CNT-K010-READ.                                           
                                                                                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                        01860000
       4000-MAIN-ROUTINE.                                                       
                                                                        01860000
           IF K010-NUM-TOT-SAP IS NOT NUMERIC                                   
              MOVE ZEROES                TO K010-NUM-TOT-SAP                    
           END-IF.                                                              
           IF K010-REG-SAP1 IS NOT NUMERIC                                      
              MOVE ZEROES                TO K010-REG-SAP1                       
           END-IF.                                                              
           IF K010-REG-SAP2 IS NOT NUMERIC                                      
              MOVE ZEROES                TO K010-REG-SAP2                       
           END-IF.                                                              
           IF K010-REG-SAP3 IS NOT NUMERIC                                      
              MOVE ZEROES                TO K010-REG-SAP3                       
           END-IF.                                                              
           IF K010-NUM-MAX-SAP1 IS NOT NUMERIC                                  
              MOVE ZEROES                  TO K010-NUM-MAX-SAP1                 
           END-IF.                                                              
           IF K010-NUM-MAX-SAP2 IS NOT NUMERIC                                  
              MOVE ZEROES                  TO K010-NUM-MAX-SAP2                 
           END-IF.                                                              
           IF K010-NUM-MAX-SAP3 IS NOT NUMERIC                                  
              MOVE ZEROES                  TO K010-NUM-MAX-SAP3                 
           END-IF.                                                              
                                                                                
           COMPUTE WS-NUM-BAL-SAP = K010-NUM-TOT-SAP - K010-REG-SAP1 -          
                                    K010-REG-SAP2    - K010-REG-SAP3.           
                                                                                
           IF WS-NUM-BAL-SAP > 0                                                
              ADD WS-NUM-BAL-SAP         TO K010-NUM-TOT-PA                     
              SUBTRACT WS-NUM-BAL-SAP    FROM K010-NUM-TOT-SAP                  
           END-IF.                                                              
                                                                                
           COMPUTE K010-NUM-MAX-PA1 = K010-NUM-MAX-PA1 +                        
               (K010-NUM-MAX-SAP1 -  K010-REG-SAP1).                            
           COMPUTE K010-NUM-MAX-PA2 = K010-NUM-MAX-PA2 +                        
               (K010-NUM-MAX-SAP2 -  K010-REG-SAP2).                            
           COMPUTE K010-NUM-MAX-PA3 = K010-NUM-MAX-PA3 +                        
               (K010-NUM-MAX-SAP3 -  K010-REG-SAP3).                            
                                                                                
           MOVE K010-REG-SAP1            TO K010-NUM-MAX-SAP1.                  
           MOVE K010-REG-SAP2            TO K010-NUM-MAX-SAP2.                  
           MOVE K010-REG-SAP3            TO K010-NUM-MAX-SAP3.                  
                                                                                
           PERFORM 6000-REWRITE-ROUTINE  THRU 6000-EXIT.                        
           PERFORM 3000-READ-BP13K010    THRU 3000-EXIT.                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       6000-REWRITE-ROUTINE.                                                    
                                                                                
              REWRITE BP13K010-TENDER.                                          
                                                                                
              IF WS-K010-STATUS = 00 OR 02                                      
                 ADD 1                   TO WS-CNT-K010-REWRITE                 
              ELSE                                                              
                 DISPLAY 'ERROR REWRITING BP13K010, STATUS '                    
                                            WS-K010-STATUS                      
                 DISPLAY '                          KEY IS '                    
                                               K010-KEY-FLD                     
                 MOVE WS-K010-STATUS        TO RETURN-CODE                      
                 GO TO 9999-CLOSE-FILES.                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9999-CLOSE-FILES.                                                        
                                                                                
           DISPLAY '>>>>>>>>>>>>>>>> BP13CF88 <<<<<<<<<<<<<<<<<<'.              
           DISPLAY 'UPDATE K010-NUM-MAX-CAT AFTER PIGEONHOLE SAP '.             
           DISPLAY '  '.                                                        
           DISPLAY 'BP13K010 RECORDS READ    : '  WS-CNT-K010-READ.             
           DISPLAY 'BP13K010 RECORDS REWRITE : '  WS-CNT-K010-REWRITE.          
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13K010.                                                      
                                                                                
           IF WS-K010-STATUS NOT = 00                                           
               DISPLAY 'ERROR CLOSING BP13K010, STATUS ' WS-K010-STATUS         
               MOVE WS-K010-STATUS       TO RETURN-CODE.                        
                                                                                
       9999-EXIT.                                                               
           STOP RUN.                                                            
                                                                                
