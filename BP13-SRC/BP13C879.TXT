      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID. BP13C879.                                                    
       AUTHOR. RJB1.                                                            
      *DATE-WRITTEN. 19/10/2015.                                                
                                                                                
      ******************************************************************        
      *                    SYSTEM OF COMMITMENT (BP13)                 *        
      *================================================================*        
      * OBJECTIVE: REPORT ON LIST OF CASES WITH ACCESS TO SOC MASTER   *        
      *================================================================*        
      *  MODIFICATIONS :                                               *        
      *  CHGE REQ #  DATE          AUTHOR         REMARKS              *        
      *  ----------  ----          ------         -------              *        
      *  BP136030    19/10/2015    RJB1           NEW PROGRAM          *        
      ******************************************************************        
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K860   ASSIGN        TO BP13K860                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K860-KEY-FLD                      
                             FILE STATUS   IS WS-K860-STATUS.                   
                                                                                
           SELECT BP13F879 ASSIGN TO BP13F879.                                  
                                                                                
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
                                                                                
           SELECT BP13L879 ASSIGN TO BP13L879.                                  
                                                                                
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13K860                                                            
            RECORD CONTAINS 80 CHARACTERS.                                      
       COPY BP13K860.                                                           
                                                                                
       FD   BP13F879                                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            RECORDING MODE  IS   F.                                             
       COPY BP13F879.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13L879                                                            
            BLOCK  CONTAINS 0   RECORDS                                         
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL  RECORD   IS  OMITTED                                         
            DATA   RECORD   IS  BP13L879-REC                                    
            RECORDING MODE IS F.                                                
       01   BP13L879-REC         PIC X(132).                                    
                                                                                
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
       01  WS-DTE-CCYYMMDD.                                                     
           05  WS-DTE-CC                 PIC X(2).                              
           05  WS-DTE-CURRENT.                                                  
               10  WS-DTE-YY             PIC X(2).                              
               10  WS-DTE-MM             PIC X(2).                              
               10  WS-DTE-DD             PIC X(2).                              
                                                                                
       01  WS-DTE-LOG.                                                          
           05  WS-YYMM-LOG               PIC X(6).                              
           05  WS-DD-LOG                 PIC X(2).                              
                                                                                
       01  WS-DTE-PREV.                                                         
           05  WS-YYMM-PREV              PIC X(6).                              
           05  WS-DD-PREV                PIC X(2).                              
                                                                                
       01  WS-FILE-FLAGS.                                                       
           05  WS-F879-EOF               PIC X     VALUE 'N'.                   
           05  WS-K860-EOF               PIC X     VALUE 'N'.                   
           05  WS-ERROR-SW               PIC X     VALUE 'N'.                   
           05  WS-K860-FOUND-TAG         PIC X     VALUE 'N'.                   
           05  WS-K860-STATUS            PIC 9(2)  VALUE ZEROS.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-L879-CNT-PAGE          PIC 9(5)  VALUE ZEROS.                 
           05  WS-L879-CNT-LINE          PIC 9(5)  VALUE ZEROS.                 
           05  WS-L879-CNT-REC           PIC 9(8)  VALUE ZEROS.                 
           05  WS-F879-CNT-READ          PIC 9(8)  VALUE ZEROS.                 
           05  WS-K860-NFND              PIC 9(8)  VALUE ZEROS.                 
           05  WS-K860-FND               PIC 9(8)  VALUE ZEROS.                 
           05  WS-CNT                    PIC 9(2)  VALUE ZEROS.                 
           05  WS-COUNT                  PIC 9(2)  VALUE ZEROS.                 
           05  WS-MAX-CNT                PIC 9(2)  VALUE ZEROS.                 
                                                                                
       01 WS-SAVE.                                                              
           05  WS-SAVE-LOGIN             PIC X(9)  VALUE SPACES.                
           05  WS-SAVE-ACCESS            PIC X(20) VALUE SPACES.                
           05  WS-SAVE-KEY-FLD           PIC X(27) VALUE SPACES.                
                                                                                
       01 WS-F879-REC5.                                                         
           05  F879-REC5 OCCURS 05 TIMES.                                       
               10  LOGIN-05              PIC  X(09).                            
               10  DTE-05                PIC  X(08).                            
               10  TME-05                PIC  X(08).                            
               10  ACCESS-05             PIC  X(20).                            
               10  CALLING-05            PIC  X(04).                            
               10  CALLED-05             PIC  X(04).                            
                                                                                
       01 WS-F879-REC10.                                                        
           05  F879-REC10 OCCURS 10 TIMES.                                      
               10  LOGIN-10              PIC  X(09).                            
               10  DTE-10                PIC  X(08).                            
               10  TME-10                PIC  X(08).                            
               10  ACCESS-10             PIC  X(20).                            
               10  CALLING-10            PIC  X(04).                            
               10  CALLED-10             PIC  X(04).                            
                                                                                
      *---------------------------------------------------------------          
      *   LAY-OUT FOR BP13L879 REPORT                                           
      *---------------------------------------------------------------          
       01  WS-PAGE-HEADING1.                                                    
           05  FILLER                    PIC X(14) VALUE 'BP13L879'.            
           05  FILLER                    PIC X(42) VALUE 'HDB3'.                
           05  FILLER                    PIC X(44)                              
               VALUE 'SYSTEM OF COMMITMENT'.                                    
           05  FILLER                    PIC X(7)  VALUE 'DATE : '.             
           05  L879-DTE-HDR-DD           PIC X(2).                              
           05  FILLER                    PIC X     VALUE '/'.                   
           05  L879-DTE-HDR-MM           PIC X(2).                              
           05  FILLER                    PIC X     VALUE '/'.                   
           05  L879-DTE-HDR-CC           PIC X(2).                              
           05  L879-DTE-HDR-YY           PIC X(5).                              
           05  FILLER                    PIC X(7)  VALUE 'PAGE : '.             
           05  L879-PAGE-NUM             PIC ZZZ9.                              
           05  FILLER                    PIC X(2)  VALUE SPACES.                
                                                                                
       01  WS-PAGE-HEADING2.                                                    
           05  FILLER                    PIC X(41) VALUE SPACES.                
           05  FILLER                    PIC X(50)                              
               VALUE '                 ACCESS  REPORT '.                        
                                                                                
       01  WS-PAGE-COL-HEADING1.                                                
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(05)                              
               VALUE 'S/NO '.                                                   
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(09)                              
               VALUE 'LOGIN ID'.                                                
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(08)                              
               VALUE 'DATE LOG'.                                                
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(08)                              
               VALUE 'TIME LOG'.                                                
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(20)                              
               VALUE 'ACCESS KEY'.                                              
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(12)                              
               VALUE 'CALLING TRAN'.                                            
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(11)                              
               VALUE 'CALLED TRAN'.                                             
                                                                                
       01  WS-HORIZONTAL-LINE.                                                  
           05  FILLER                    PIC X(132)                             
               VALUE ALL '-'.                                                   
                                                                                
       01  WS-PAGE-DATA-LINE.                                                   
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  L879-SERIAL               PIC ZZZZZ.                             
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  L879-LOGIN                PIC X(09) VALUE SPACES.                
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  L879-DTE-LOG              PIC X(08) VALUE SPACES.                
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  L879-TME-LOG              PIC X(08) VALUE SPACES.                
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  L879-ACCESS               PIC X(20) VALUE SPACES.                
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  L879-CALLING-TRAN         PIC X(12) VALUE SPACES.                
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  L879-CALLED-TRAN          PIC X(11) VALUE SPACES.                
                                                                                
       01  WS-END-OF-REPORT.                                                    
           05  FILLER                    PIC X(55) VALUE SPACES.                
           05  FILLER                    PIC X(21)                              
               VALUE '*** END OF REPORT ***'.                                   
           05  FILLER                    PIC X(56) VALUE SPACES.                
                                                                                
       01  WS-EMPTY-REPORT.                                                     
           05  FILLER                    PIC X(56) VALUE SPACES.                
           05  FILLER                    PIC X(20)                              
               VALUE '*** EMPTY REPORT ***'.                                    
           05  FILLER                    PIC X(56) VALUE SPACES.                
                                                                                
                                                                                
      *------------------------*                                                
       PROCEDURE DIVISION.                                                      
      *------------------------*                                                
      ******************************************************************        
       0000-CONTROL.                                                            
      ******************************************************************        
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-BP13F879 THRU 2000-EXIT.                           
           PERFORM 3000-PROCESS       THRU 3000-EXIT                            
                   UNTIL WS-F879-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-FILES.                                                         
      ******************************************************************        
           OPEN INPUT  BP13F879                                                 
                       BP13K860                                                 
                       SY02F001                                                 
                OUTPUT BP13L879.                                                
                                                                                
           IF WS-K860-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K860 - ERROR OPENING : ' WS-K860-STATUS              
              PERFORM  9000-CLOSE-FILES    THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
                                                                                
           MOVE F001-DTE-CURRENT       TO WS-DTE-CCYYMMDD.                      
           MOVE WS-DTE-DD TO L879-DTE-HDR-DD.                                   
           MOVE WS-DTE-MM TO L879-DTE-HDR-MM.                                   
           MOVE WS-DTE-CC TO L879-DTE-HDR-CC.                                   
           MOVE WS-DTE-YY TO L879-DTE-HDR-YY.                                   
           MOVE F001-DTE-LAST-DAY-PREV TO WS-DTE-PREV.                          
                                                                                
           PERFORM VARYING WS-CNT FROM 1 BY 1                                   
             UNTIL WS-CNT > 5                                                   
             MOVE SPACES               TO F879-REC5(WS-CNT)                     
           END-PERFORM.                                                         
                                                                                
           PERFORM VARYING WS-CNT FROM 1 BY 1                                   
             UNTIL WS-CNT > 10                                                  
             MOVE SPACES               TO F879-REC10(WS-CNT)                    
           END-PERFORM.                                                         
                                                                                
           MOVE ZEROES                 TO WS-COUNT.                             
           PERFORM 7000-PRINT-HEADERS THRU 7000-EXIT.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F879.                                                      
      ******************************************************************        
           READ BP13F879 AT END                                                 
                         MOVE 'Y' TO WS-F879-EOF                                
                         GO TO 2000-EXIT                                        
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F879-CNT-READ.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS.                                                            
      ******************************************************************        
           MOVE F879-DTE-LOG TO  WS-DTE-LOG.                                    
           MOVE 5            TO  WS-MAX-CNT                                     
           IF WS-YYMM-LOG = WS-YYMM-PREV                                        
              IF F879-NUM-LOGIN-ID(1:2) = 'EA'                                  
                 PERFORM 3500-STARTBR-K860                                      
                    THRU 3500-EXIT                                              
              END-IF                                                            
              IF F879-NUM-LOGIN-ID NOT = SPACES AND LOW-VALUES                  
                 IF (F879-NUM-LOGIN-ID = WS-SAVE-LOGIN) AND                     
                    (F879-NUM-ACCESS-KEY = WS-SAVE-ACCESS)                      
                    ADD 1 TO WS-COUNT                                           
                    PERFORM 4000-CHECK-COUNT                                    
                       THRU 4000-EXIT                                           
                    IF WS-COUNT > WS-MAX-CNT                                    
                       PERFORM 5000-WRITE-REPORT                                
                          THRU 5000-EXIT                                        
                       MOVE F879-NUM-LOGIN-ID   TO WS-SAVE-LOGIN                
                       MOVE F879-NUM-ACCESS-KEY TO WS-SAVE-ACCESS               
                       MOVE F879-KEY-FLD        TO WS-SAVE-KEY-FLD              
                       MOVE 0 TO WS-COUNT                                       
                    END-IF                                                      
                 ELSE                                                           
                    MOVE F879-NUM-LOGIN-ID   TO WS-SAVE-LOGIN                   
                    MOVE F879-NUM-ACCESS-KEY TO WS-SAVE-ACCESS                  
                    MOVE F879-KEY-FLD        TO WS-SAVE-KEY-FLD                 
                    MOVE 0 TO WS-COUNT                                          
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F879                                           
              THRU 2000-EXIT.                                                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3500-STARTBR-K860.                                                       
      ******************************************************************        
                                                                                
           MOVE 'N'                        TO WS-K860-EOF.                      
           MOVE SPACES                     TO K860-KEY-FLD.                     
           MOVE F879-NUM-LOGIN-ID(1:8)     TO K860-NUM-USERID.                  
                                                                                
           START BP13K860 KEY >= K860-KEY-FLD.                                  
           EVALUATE WS-K860-STATUS                                              
           WHEN 00                                                              
                PERFORM 3510-READNEXT-K860 THRU 3510-EXIT                       
                   UNTIL K860-NUM-USERID NOT = F879-NUM-LOGIN-ID(1:8)           
                      OR WS-K860-EOF = 'Y'                                      
                IF WS-K860-FOUND-TAG = 'N'                                      
                   ADD 1                   TO WS-K860-NFND                      
                END-IF                                                          
           WHEN 23                                                              
                CONTINUE                                                        
           WHEN OTHER                                                           
                DISPLAY 'ERROR START BP13K860, STATUS  '                        
                        WS-K860-STATUS                                          
                        ',  KEY ' K860-KEY-FLD                                  
                PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3510-READNEXT-K860.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K860 NEXT RECORD                                            
                AT END MOVE 'Y' TO WS-K860-EOF.                                 
                                                                                
           EVALUATE WS-K860-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
                IF (K860-NUM-USERID = F879-NUM-LOGIN-ID(1:8))                   
                   IF K860-NUM-TEAMID = '1' OR '2' OR '3' OR                    
                                        '7' OR '8' OR '9'                       
                      MOVE 10              TO WS-MAX-CNT                        
                   END-IF                                                       
                   MOVE 'Y'                TO WS-K860-FOUND-TAG                 
                   ADD 1                   TO WS-K860-FND                       
                ELSE                                                            
                   MOVE 'Y'                TO WS-K860-EOF                       
                END-IF                                                          
           WHEN 10                                                              
           WHEN 23                                                              
                MOVE 'Y'                   TO WS-K860-EOF                       
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K860, STATUS  '                      
                        WS-K860-STATUS                                          
                        ',  KEY ' K860-KEY-FLD                                  
                PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3510-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       4000-CHECK-COUNT.                                                        
      ******************************************************************        
           IF WS-MAX-CNT = 5                                                    
              IF WS-COUNT <= 5                                                  
                 MOVE F879-NUM-LOGIN-ID         TO LOGIN-05   (WS-COUNT)        
                 MOVE F879-DTE-LOG              TO DTE-05     (WS-COUNT)        
                 MOVE F879-TME-LOG              TO TME-05     (WS-COUNT)        
                 MOVE F879-NUM-ACCESS-KEY       TO ACCESS-05  (WS-COUNT)        
                 MOVE F879-NUM-CALLING-TRANS-ID TO CALLING-05 (WS-COUNT)        
                 MOVE F879-NUM-CALLED-TRANS-ID  TO CALLED-05  (WS-COUNT)        
              END-IF                                                            
           ELSE                                                                 
              IF WS-COUNT <= 10                                                 
                 MOVE F879-NUM-LOGIN-ID         TO LOGIN-10   (WS-COUNT)        
                 MOVE F879-DTE-LOG              TO DTE-10     (WS-COUNT)        
                 MOVE F879-TME-LOG              TO TME-10     (WS-COUNT)        
                 MOVE F879-NUM-ACCESS-KEY       TO ACCESS-10  (WS-COUNT)        
                 MOVE F879-NUM-CALLING-TRANS-ID TO CALLING-10 (WS-COUNT)        
                 MOVE F879-NUM-CALLED-TRANS-ID  TO CALLED-10  (WS-COUNT)        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       5000-WRITE-REPORT.                                                       
      ******************************************************************        
           IF WS-L879-CNT-LINE > 60                                             
              PERFORM 7000-PRINT-HEADERS THRU 7000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-MAX-CNT = 5                                                    
              PERFORM VARYING WS-CNT FROM 1 BY 1 UNTIL WS-CNT > 5               
                 MOVE SPACES               TO WS-PAGE-DATA-LINE                 
                 ADD  1                    TO WS-L879-CNT-REC                   
                 MOVE WS-L879-CNT-REC      TO L879-SERIAL                       
                 MOVE LOGIN-05   (WS-CNT)  TO L879-LOGIN                        
                 MOVE DTE-05     (WS-CNT)  TO L879-DTE-LOG                      
                 MOVE TME-05     (WS-CNT)  TO L879-TME-LOG                      
                 MOVE ACCESS-05  (WS-CNT)  TO L879-ACCESS                       
                 MOVE CALLING-05 (WS-CNT)  TO L879-CALLING-TRAN                 
                 MOVE CALLED-05  (WS-CNT)  TO L879-CALLED-TRAN                  
                 WRITE BP13L879-REC FROM WS-PAGE-DATA-LINE                      
                    AFTER ADVANCING 1 LINE                                      
                 ADD 1                     TO WS-L879-CNT-LINE                  
              END-PERFORM                                                       
           ELSE                                                                 
              PERFORM VARYING WS-CNT FROM 1 BY 1 UNTIL WS-CNT > 10              
                 MOVE SPACES               TO WS-PAGE-DATA-LINE                 
                 ADD  1                    TO WS-L879-CNT-REC                   
                 MOVE WS-L879-CNT-REC      TO L879-SERIAL                       
                 MOVE LOGIN-10   (WS-CNT)  TO L879-LOGIN                        
                 MOVE DTE-10     (WS-CNT)  TO L879-DTE-LOG                      
                 MOVE TME-10     (WS-CNT)  TO L879-TME-LOG                      
                 MOVE ACCESS-10  (WS-CNT)  TO L879-ACCESS                       
                 MOVE CALLING-10 (WS-CNT)  TO L879-CALLING-TRAN                 
                 MOVE CALLED-10  (WS-CNT)  TO L879-CALLED-TRAN                  
                 WRITE BP13L879-REC FROM WS-PAGE-DATA-LINE                      
                    AFTER ADVANCING 1 LINE                                      
                 ADD 1                     TO WS-L879-CNT-LINE                  
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       7000-PRINT-HEADERS.                                                      
      ******************************************************************        
      *----------------------------------------------------------------*        
      *  PRINTS THE PAGE HEADER.                                       *        
      *----------------------------------------------------------------*        
           ADD  1                TO WS-L879-CNT-PAGE.                           
           MOVE WS-L879-CNT-PAGE TO L879-PAGE-NUM.                              
                                                                                
           WRITE BP13L879-REC    FROM WS-PAGE-HEADING1                          
                 AFTER ADVANCING PAGE.                                          
           WRITE BP13L879-REC    FROM WS-PAGE-HEADING2                          
                 AFTER ADVANCING 1 LINE.                                        
           WRITE BP13L879-REC    FROM WS-PAGE-COL-HEADING1                      
                 AFTER ADVANCING 2 LINES.                                       
           WRITE BP13L879-REC    FROM WS-HORIZONTAL-LINE                        
                 AFTER ADVANCING 1 LINE.                                        
                                                                                
           MOVE 5                TO WS-L879-CNT-LINE.                           
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       9000-CLOSE-FILES.                                                        
      ******************************************************************        
           DISPLAY '                                  '.                        
           DISPLAY ' *****  BP13C879  *****           '.                        
           DISPLAY '                                  '.                        
           DISPLAY ' NO. OF REC READ FROM F879  = ' WS-F879-CNT-READ.           
           DISPLAY ' NO. OF REC PRINTED         = ' WS-L879-CNT-REC.            
           DISPLAY ' NO. OF REC NOT FOUND K860  = ' WS-K860-NFND.               
           DISPLAY ' NO. OF REC FOUND K860      = ' WS-K860-FND.                
                                                                                
           CLOSE BP13K860                                                       
                 BP13F879                                                       
                 SY02F001.                                                      
                                                                                
           IF WS-K860-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR, BP13K860-STATUS ' WS-K860-STATUS          
              MOVE 'Y' TO WS-ERROR-SW                                           
              MOVE WS-K860-STATUS TO RETURN-CODE                                
           END-IF.                                                              
                                                                                
           PERFORM 9100-PRINT-END-EMPTY THRU 9100-EXIT.                         
                                                                                
           CLOSE BP13L879.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       9100-PRINT-END-EMPTY.                                                    
      ******************************************************************        
           IF WS-L879-CNT-REC = ZEROES                                          
              IF WS-ERROR-SW = 'Y'                                              
                 PERFORM 7000-PRINT-HEADERS THRU 7000-EXIT                      
              END-IF                                                            
              MOVE  SPACES TO BP13L879-REC                                      
              WRITE BP13L879-REC AFTER ADVANCING 1 LINE                         
              MOVE  SPACES TO BP13L879-REC                                      
              IF WS-ERROR-SW = 'Y'                                              
                 MOVE 'FILE ERROR !!! ' TO   WS-EMPTY-REPORT                    
                 WRITE BP13L879-REC FROM WS-EMPTY-REPORT                        
                       AFTER ADVANCING 1 LINE                                   
              ELSE                                                              
                 WRITE BP13L879-REC FROM WS-EMPTY-REPORT                        
                       AFTER ADVANCING 1 LINE                                   
              END-IF                                                            
           ELSE                                                                 
              MOVE  SPACES TO BP13L879-REC                                      
              WRITE BP13L879-REC AFTER ADVANCING 1 LINE                         
              MOVE  SPACES TO BP13L879-REC                                      
              WRITE BP13L879-REC FROM WS-END-OF-REPORT                          
                    AFTER ADVANCING 1 LINE                                      
           END-IF.                                                              
                                                                                
       9100-EXIT.                                                               
           EXIT.                                                                
      *----------------------------------------------------------------*        
      * >>>>>>>>>>>>>>>>>>>>>>> END OF BP13C879 <<<<<<<<<<<<<<<<<<<<<< *        
      *----------------------------------------------------------------*        
