       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C465.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  15 DEC 2017.                                              
      *========================================================*                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *      1.  UPDATE BP13K482 FOR HYBRID 2ND CON LOAN SERS  *                
      *          CASES                                         *                
      *--------------------------------------------------------*                
      * CHG-NO    BY  DATE    DESCRIPTION                      *                
      * -------  ---  ------  -----------                      *                
      * BP136941 FNP1 151217  NEW PROGRAM                      *                
      *========================================================*                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F410 ASSIGN TO BP13F410.                                  
                                                                                
           SELECT BP13K482  ASSIGN        TO BP13K482                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K482-KEY-FLD                       
                            FILE STATUS   IS K482-STATUS.                       
                                                                                
           SELECT BP17K120  ASSIGN        TO BP17K120                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K120-KEY-FLD                       
                            FILE STATUS   IS K120-STATUS.                       
                                                                                
           SELECT BP17K100  ASSIGN        TO BP17K100                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K100-KEY-FLD                       
                            FILE STATUS   IS K100-STATUS.                       
                                                                                
           SELECT BP17K110  ASSIGN        TO BP17K110                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K110-KEY-FLD                       
                            ALTERNATE KEY IS K110-ALT-KEY1                      
                            FILE STATUS   IS K110-STATUS.                       
                                                                                
           SELECT BP13L465 ASSIGN TO BP13L465.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K482                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13K482.                                                           
                                                                                
       FD  BP17K120                                                             
           RECORD CONTAINS 1600 CHARACTERS.                                     
       COPY BP17K120.                                                           
                                                                                
       FD  BP17K100                                                             
           RECORD CONTAINS  400 CHARACTERS.                                     
       COPY BP17K100.                                                           
                                                                                
       FD  BP17K110                                                             
           RECORD CONTAINS  200 CHARACTERS.                                     
       COPY BP17K110.                                                           
                                                                                
       FD  BP13F410                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           RECORDING MODE IS F                                                  
           LABEL RECORD IS STANDARD.                                            
       COPY BP13F410.                                                           
                                                                                
                                                                                
       FD  BP13L465                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 120 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORD IS STANDARD.                                            
       01  BP13L465-REC    PIC X(120).                                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  K482-STATUS                    PIC 9(2)  VALUE ZERO.                 
       01  K120-STATUS                    PIC 9(2)  VALUE ZERO.                 
       01  K100-STATUS                    PIC 9(2)  VALUE ZERO.                 
       01  K110-STATUS                    PIC 9(2)  VALUE ZERO.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F410-READ          PIC 9(7)    VALUE ZEROES.                   
           05 WS-K120-FND           PIC 9(7)    VALUE ZEROES.                   
           05 WS-K482-READ          PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K482-FND          PIC 9(7)    VALUE ZEROES.                   
           05 WS-K482-UPDATE        PIC 9(7)    VALUE ZEROES.                   
           05 WS-L465-WRITE         PIC 9(7)    VALUE ZEROES.                   
           05 WS-COMP-PSTV          PIC 9(7)    VALUE ZEROES.                   
           05 WS-COMP-NGTV          PIC 9(7)    VALUE ZEROES.                   
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F410-EOF          PIC X       VALUE 'N'.                      
           05  WS-K110-FND          PIC X       VALUE 'N'.                      
           05  WS-K100-FND          PIC X       VALUE 'N'.                      
           05  WS-K482-FND          PIC X(01)   VALUE SPACES.                   
               88  K482-FND                        VALUE 'Y'.                   
           05  WS-K482-EOF          PIC X(01)  VALUE SPACES.                    
               88  K482-EOF                        VALUE 'Y'.                   
                                                                                
       01  L465-HEADER.                                                         
           05 FILLER           PIC X(10) VALUE 'REGN NO'.                       
           05 FILLER           PIC X(13) VALUE 'PURCH FLAT'.                    
           05 FILLER           PIC X(13) VALUE 'DISP FLAT'.                     
           05 FILLER           PIC X(62) VALUE 'ADDRESS'.                       
           05 FILLER           PIC X(06) VALUE 'OIC'.                           
           05 FILLER           PIC X(10) VALUE 'SATP DATE'.                     
           05 FILLER           PIC X(06) VALUE SPACES.                          
                                                                                
       01  L465-DETAILS.                                                        
           05 L465-REGN        PIC X(08) VALUE SPACES.                          
           05 FILLER           PIC X(02) VALUE SPACES.                          
           05 L465-PURCH-FLAT  PIC X(11) VALUE SPACES.                          
           05 FILLER           PIC X(02) VALUE SPACES.                          
           05 L465-DISP-FLAT   PIC X(11) VALUE SPACES.                          
           05 FILLER           PIC X(02) VALUE SPACES.                          
           05 L465-SERS-ADD    PIC X(60) VALUE SPACES.                          
           05 FILLER           PIC X(02) VALUE SPACES.                          
           05 L465-OIC         PIC X(04) VALUE SPACES.                          
           05 FILLER           PIC X(02) VALUE SPACES.                          
           05 L465-DTE-SATP    PIC X(08) VALUE SPACES.                          
           05 FILLER           PIC X(02) VALUE SPACES.                          
           05 FILLER           PIC X(06) VALUE SPACES.                          
                                                                                
      *-------------------------------------------------------*                 
      *    COPY BOOK FOR SUBROUTINE - BP13C913.               *                 
      *-------------------------------------------------------*                 
       COPY P13COMM8.                                                           
                                                                                
      *-------------------------------------------------------*                 
      *    LINKAGE VARIABLES                                  *                 
      *-------------------------------------------------------*                 
       01   WS-LINK-REC.                                                        
            05 WS-LINK-NUM-SCH            PIC X(4).                             
            05 WS-LINK-NUM-ACC            PIC X(4).                             
            05 WS-LINK-NUM-CHK            PIC X(1).                             
                                                                                
                                                                                
       COPY P17CPG01.                                                           
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *------------------------------------------------------------             
       0000-MAIN.                                                               
      *------------------------------------------------------------             
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 1100-READ-BP13F410   THRU 1100-EXIT.                         
           PERFORM 2000-PROCESS-RECORDS THRU 2000-EXIT                          
             UNTIL WS-F410-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       1000-OPEN-ROUTINE.                                                       
      *------------------------------------------------------------             
                                                                                
           OPEN INPUT  BP13F410                                                 
                       BP17K120                                                 
                       BP17K110                                                 
                       BP17K100                                                 
                I-O    BP13K482                                                 
                OUTPUT BP13L465.                                                
                                                                                
           IF K482-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, K482-STATUS ' K482-STATUS                    
              MOVE K482-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF K120-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, K120-STATUS ' K120-STATUS                    
              MOVE K120-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF K110-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, K110-STATUS ' K110-STATUS                    
              MOVE K110-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF K100-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, K100-STATUS ' K100-STATUS                    
              MOVE K100-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           WRITE BP13L465-REC FROM L465-HEADER.                                 
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       1100-READ-BP13F410.                                                      
      *------------------------------------------------------------             
                                                                                
           READ BP13F410 AT END                                                 
              MOVE 'Y' TO WS-F410-EOF                                           
                GO TO 1100-EXIT.                                                
                                                                                
           ADD 1 TO WS-F410-READ.                                               
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       2000-PROCESS-RECORDS.                                                    
      *------------------------------------------------------------             
                                                                                
           MOVE SPACES                   TO K482-KEY-FLD.                       
           MOVE F410-NUM-SCH-ACC         TO K482-NUM-HDBREF-NEW.                
           PERFORM 3000-START-BP13K482   THRU 3000-EXIT.                        
                                                                                
                                                                                
           PERFORM 1100-READ-BP13F410     THRU 1100-EXIT.                       
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3000-START-BP13K482.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE 'N'                   TO WS-K482-EOF.                           
                                                                                
           START BP13K482 KEY >= K482-KEY-FLD.                                  
                                                                                
           IF K482-STATUS = 00 OR 02 OR 22                                      
              PERFORM 3100-READ-NEXT-BP13K482  THRU 3100-EXIT                   
                 UNTIL WS-K482-EOF = 'Y'                                        
           ELSE                                                                 
              IF K482-STATUS = 10 OR 23                                         
                 MOVE 'Y'             TO WS-K482-EOF                            
              ELSE                                                              
                 DISPLAY 'BP13K482 - START ERROR (' K482-STATUS                 
                          '), KEY IS ('  K482-KEY-FLD ')'                       
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-READ-NEXT-BP13K482.                                                 
      *-------------------------------------------------------------            
                                                                                
           READ BP13K482 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y' TO WS-K482-EOF                                         
                GO       TO 3100-EXIT                                           
           END-READ.                                                            
                                                                                
           IF K482-STATUS = 00 OR 02 OR 22                                      
              IF K482-NUM-HDBREF-NEW = F410-NUM-SCH-ACC                         
                 ADD 1 TO CNT-K482-FND                                          
                 PERFORM 3500-READ-BP17K120  THRU 3500-EXIT                     
              ELSE                                                              
                 MOVE 'Y' TO WS-K482-EOF                                        
              END-IF                                                            
           ELSE                                                                 
              IF K482-STATUS = 10 OR 23                                         
                 MOVE 'Y'               TO WS-K482-EOF                          
              ELSE                                                              
                 MOVE 'Y'               TO WS-K482-EOF                          
                 DISPLAY 'BP13K482 - READ NEXT ERROR (' K482-STATUS             
                         ') ,KEY IS ('  K482-KEY-FLD ')'                        
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3500-READ-BP17K120.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES                      TO BP17K120-REC.                    
           INITIALIZE                          BP17K120-REC.                    
                                                                                
                                                                                
           MOVE K482-NUM-HDBREF-DISPOSAL    TO K120-KEY-FLD.                    
                                                                                
           READ BP17K120.                                                       
                                                                                
           EVALUATE K120-STATUS                                                 
           WHEN 00                                                              
           WHEN 02                                                              
              ADD 1 TO WS-K120-FND                                              
              IF K120-AMT-COMPENSATION  > 0                                     
                 ADD 1 TO WS-COMP-PSTV                                          
                 PERFORM 4000-UPDATE-BP13K482  THRU 4000-EXIT                   
              ELSE                                                              
                 PERFORM 5000-WRITE-OUTPUT     THRU 5000-EXIT                   
                 ADD 1 TO WS-COMP-NGTV                                          
              END-IF                                                            
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K120, KEY IS '                   
                       K120-KEY-FLD                                             
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP17K120 : ' K120-STATUS                 
                MOVE K120-STATUS         TO RETURN-CODE                         
                PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       4000-UPDATE-BP13K482.                                                    
      *------------------------------------------------------------             
                                                                                
           MOVE ZEROES             TO K482-AMT-CASH-PROCEEDS-SALES.             
           MOVE F410-DTE-KEY-ISSUE TO K482-DTE-COMPLETE.                        
           MOVE 'N'                TO K482-CDE-LOAN-REDUCTN.                    
           MOVE 'Y'                TO K482-CDE-LOAN-CNVERSN.                    
                                                                                
           REWRITE BP13K482-REC.                                                
                                                                                
           IF K482-STATUS = 00                                                  
              ADD 1                  TO WS-K482-UPDATE                          
           ELSE                                                                 
              DISPLAY 'BP13K482 - REWRITE ERROR (' K482-STATUS ')'              
                      ', KEY (' K482-KEY-FLD ')'                                
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       5000-WRITE-OUTPUT.                                                       
      *------------------------------------------------------------             
           MOVE SPACES TO BP13L465-REC.                                         
           INITIALIZE     BP13L465-REC.                                         
                                                                                
           MOVE F410-NUM-REGN             TO L465-REGN.                         
           MOVE F410-NUM-SCH-ACC          TO L465-PURCH-FLAT.                   
           MOVE K482-NUM-HDBREF-DISPOSAL  TO L465-DISP-FLAT.                    
           PERFORM 5100-GET-ADDRESS       THRU 5100-EXIT.                       
           PERFORM 5200-GET-OIC           THRU 5200-EXIT.                       
                                                                                
           WRITE BP13L465-REC FROM L465-DETAILS.                                
           ADD 1 TO WS-L465-WRITE.                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       5100-GET-ADDRESS.                                                        
      *------------------------------------------------------------             
                                                                                
           MOVE SPACES               TO BP13COMM8-REC.                          
           INITIALIZE                   BP13COMM8-REC.                          
           MOVE F410-NUM-SCH-ACC     TO WS-LINK-REC.                            
                                                                                
           CALL 'BP13C913'  USING WS-LINK-REC,                                  
                                  BP13COMM8-REC.                                
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE'                       
                         COMM8-CDE-SYSERR                                       
              ELSE                                                              
                 DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR             
              END-IF                                                            
              MOVE SPACES        TO BP13COMM8-REC                               
              MOVE SPACES        TO L465-SERS-ADD                               
           ELSE                                                                 
              STRING  COMM8-NUM-BLK ' ' COMM8-NME-STREET                        
                      '# ' COMM8-NUM-LEVEL ' '                                  
                      COMM8-NUM-UNIT-MAIN '-' COMM8-NUM-UNIT-SUB                
                      ' ' COMM8-NUM-POSTAL-CODE                                 
                DELIMITED BY SIZE INTO L465-SERS-ADD                            
              END-STRING                                                        
           END-IF.                                                              
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       5200-GET-OIC.                                                            
      *------------------------------------------------------------             
           PERFORM 5300-READ-BP17K110    THRU 5300-EXIT.                        
           PERFORM 5400-READ-BP17K100    THRU 5400-EXIT.                        
                                                                                
           IF WS-K110-FND = 'N' AND WS-K100-FND = 'N'                           
              MOVE SPACES TO L465-OIC                                           
              GO TO 5200-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE SPACES                    TO WS-PG01-COMMAREA.                  
           INITIALIZE                        WS-PG01-COMMAREA.                  
           MOVE K100-DTE-ANNOUNCE         TO WS-PG01-DTE-ANNOUNCE.              
           MOVE COMM8-NUM-LEVEL           TO WS-PG01-NUM-LVL-NUM.               
           IF (WS-PG01-DTE-ANNOUNCE <= '20140627')                              
              MOVE K110-NUM-OIC-SAU       TO WS-PG01-NUM-OIC1                   
              MOVE K110-NUM-OIC2-SAU      TO WS-PG01-NUM-OIC2                   
              MOVE K110-NUM-OIC3-SAU      TO WS-PG01-NUM-OIC3                   
              MOVE K110-NUM-OIC4-SAU      TO WS-PG01-NUM-OIC4                   
                                                                                
              MOVE K110-NUM-OIC1-SAU-START-LVL                                  
                                          TO WS-PG01-NUM-OIC1-START-LVL         
              MOVE K110-NUM-OIC2-SAU-START-LVL                                  
                                          TO WS-PG01-NUM-OIC2-START-LVL         
              MOVE K110-NUM-OIC3-SAU-START-LVL                                  
                                          TO WS-PG01-NUM-OIC3-START-LVL         
              MOVE K110-NUM-OIC4-SAU-START-LVL                                  
                                          TO WS-PG01-NUM-OIC4-START-LVL         
           END-IF.                                                              
                                                                                
           IF (WS-PG01-DTE-ANNOUNCE > '20140627')                               
              MOVE K110-NUM-OIC-ERU       TO WS-PG01-NUM-OIC1                   
              MOVE K110-NUM-OIC2-ERU      TO WS-PG01-NUM-OIC2                   
              MOVE K110-NUM-OIC3-ERU      TO WS-PG01-NUM-OIC3                   
              MOVE K110-NUM-OIC4-ERU      TO WS-PG01-NUM-OIC4                   
              MOVE K110-NUM-OIC-SAU       TO WS-PG01-NUM-OIC5                   
              MOVE K110-NUM-OIC2-SAU      TO WS-PG01-NUM-OIC6                   
              MOVE K110-NUM-OIC3-SAU      TO WS-PG01-NUM-OIC7                   
              MOVE K110-NUM-OIC4-SAU      TO WS-PG01-NUM-OIC8                   
                                                                                
              MOVE K110-NUM-OIC1-ERU-START-LVL                                  
                                          TO WS-PG01-NUM-OIC1-START-LVL         
              MOVE K110-NUM-OIC2-ERU-START-LVL                                  
                                          TO WS-PG01-NUM-OIC2-START-LVL         
              MOVE K110-NUM-OIC3-ERU-START-LVL                                  
                                          TO WS-PG01-NUM-OIC3-START-LVL         
              MOVE K110-NUM-OIC4-ERU-START-LVL                                  
                                          TO WS-PG01-NUM-OIC4-START-LVL         
                                                                                
              MOVE K110-NUM-OIC1-SAU-START-LVL                                  
                                          TO WS-PG01-NUM-OIC5-START-LVL         
              MOVE K110-NUM-OIC2-SAU-START-LVL                                  
                                          TO WS-PG01-NUM-OIC6-START-LVL         
              MOVE K110-NUM-OIC3-SAU-START-LVL                                  
                                          TO WS-PG01-NUM-OIC7-START-LVL         
              MOVE K110-NUM-OIC4-SAU-START-LVL                                  
                                          TO WS-PG01-NUM-OIC8-START-LVL         
           END-IF.                                                              
           PERFORM 1000-PG01-DETERMINE-OIC THRU 1000-PG01-EXIT.                 
           MOVE WS-PG01-NUM-OIC           TO L465-OIC.                          
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       5300-READ-BP17K110.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES                      TO BP17K110-REC.                    
           INITIALIZE                          BP17K110-REC.                    
                                                                                
           MOVE K120-NUM-BLDNG-GL TO K110-NUM-BLDNG-GL.                         
                                                                                
           READ BP17K110 KEY IS K110-ALT-KEY1.                                  
                                                                                
           EVALUATE K110-STATUS                                                 
           WHEN 00                                                              
           WHEN 02                                                              
              MOVE 'Y'          TO WS-K110-FND                                  
           WHEN 23                                                              
              MOVE 'N'          TO WS-K110-FND                                  
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP17K110 : ' K110-STATUS                 
                MOVE K110-STATUS         TO RETURN-CODE                         
                PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       5400-READ-BP17K100.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES                      TO BP17K100-REC.                    
           INITIALIZE                          BP17K100-REC.                    
                                                                                
           MOVE K110-NUM-SERSREF  TO K100-NUM-SERSREF.                          
                                                                                
           READ BP17K100.                                                       
                                                                                
           EVALUATE K100-STATUS                                                 
           WHEN 00                                                              
              MOVE 'Y'          TO WS-K100-FND                                  
           WHEN 23                                                              
              MOVE 'N'          TO WS-K100-FND                                  
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP17K100 : ' K100-STATUS                 
                MOVE K100-STATUS         TO RETURN-CODE                         
                PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       5400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       9000-CLOSE-ROUTINE.                                                      
      *------------------------------------------------------------             
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '***********BP13C465 END OF JOB*******************'.         
           DISPLAY '*-----------------------------------------------*'.         
           DISPLAY 'NO OF RECORDS READ FROM F410    ' WS-F410-READ.             
           DISPLAY 'NO OF RECORDS FOUND IN BP13K482 ' CNT-K482-FND.             
           DISPLAY 'NO OF RECORDS FOUND IN BP17K120 ' WS-K120-FND.              
           DISPLAY 'NO OF RECORDS COMPENSATION > 0  ' WS-COMP-PSTV.             
           DISPLAY 'NO OF RECORDS COMPENSATION <= 0 ' WS-COMP-NGTV.             
           DISPLAY 'NO OF RECORDS UPDATED IN BP13K482 ' WS-K482-UPDATE.         
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF OUPUT                     ' WS-L465-WRITE.            
                                                                                
                                                                                
           CLOSE BP13F410                                                       
                 BP13K482                                                       
                 BP17K120                                                       
                 BP17K110                                                       
                 BP17K100                                                       
                 BP13L465.                                                      
                                                                                
           IF K482-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, K482-STATUS ' K482-STATUS                   
              MOVE K482-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           IF K120-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, K120-STATUS ' K120-STATUS                   
              MOVE K120-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           IF K110-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, K110-STATUS ' K110-STATUS                   
              MOVE K110-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           IF K100-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, K100-STATUS ' K100-STATUS                   
              MOVE K100-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       COPY BP17PG01.                                                           
