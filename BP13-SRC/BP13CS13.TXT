       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CS13.                                                 
       AUTHOR.        RJB1.                                                     
      *DATE-WRITTEN.  20/04/2017.                                               
      *************************************************************             
      *      SYSTEM OF COMMITMENT (BP13)                          *             
      *===========================================================*             
      *                                                           *             
      *  OBJECTIVES: 1. TO NOTIFY APPLICANTS THRU EMAIL TO DO A   *             
      *                 ECPF WITHDRAWAL.                          *             
      *              2. PROGRAM TO PUT MQ MESSAGE INTO QUEUE      *             
      *                 WHICH CONTAINS CUSTOMER'S DETAILS TO BE   *             
      *                 SENT BY JAVA INTERFACE.                   *             
      *                                                           *             
      *  PROGRAM LOGIC:                                           *             
      *         PROCESS INPUT FILES                               *             
      *         MQCONNECT TO QUEUE MANAGER                        *             
      *         MQOPEN QUEUE                                      *             
      *         MQPUT                                             *             
      *         MQCLOSE QUEUE                                     *             
      *         MQDISCONNECT FROM QUEUE MANAGER                   *             
      *                                                           *             
      *===========================================================*             
      * MODIFICATIONS :                                           *             
      *                                                           *             
      * CHG REQ#    DATE    AUTHOR REMARKS                        *             
      * -------- ---------- ------ ------------------------------ *             
      * BP136748 20/04/2017 RJB1   NEW PROGRAM                    *             
      *************************************************************             
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       SPECIAL-NAMES.                                                           
           SYSIN IS PARM-INPUT.                                                 
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F23B  ASSIGN        TO BP13F23B.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F23B                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 200 CHARACTERS.                                      
           COPY BP13F23B.                                                       
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      *-------------------------------------------------------------            
      *    QUEUE MANAGER                                                        
      *    FOR PRODUCTION, QUEUE MANAGER IS 'MQP1'                              
      *    FOR TESTING,    QUEUE MANAGER IS 'MQT1'                              
      *-------------------------------------------------------------            
       01  WS-QM-NAME                    PIC X(48) VALUE 'MQP1'.                
      *01  WS-QM-NAME                    PIC X(48) VALUE 'MQT1'.                
       01  WS-BP13Q-NAME                 PIC X(48) VALUE SPACES.                
                                                                                
      *-------------------------------------------------------------            
      *    OUTPUT QUEUE NAME TO CRIMSONLOGIC                                    
      *    FOR PROD, QUEUE NAME IS BP13.WIS.MDB.REQUESTQ                        
      *    FOR TESTING, QUEUE NAME IS BP13S.WIS.MDB.REQUESTQ                    
      *-------------------------------------------------------------            
       01  WS-QUEUE-NAME   PIC X(48) VALUE 'BP13.WIS.MDB.REQUESTQ'.             
      *01  WS-QUEUE-NAME   PIC X(48) VALUE 'BP13S.WIS.MDB.REQUESTQ'.            
      *01  WS-QUEUE-NAME   PIC X(48) VALUE 'BP13T.WIS.MDB.REQUESTQ'.            
                                                                                
      *-------------------------------------------------------------            
      *    MESSAGE LENGTH                                                       
      *-------------------------------------------------------------            
       01  WS-MESSAGE-LENGTH             PIC 9(06) VALUE  36212.                
                                                                                
      *-------------------------------------------------------------            
      *    MESSAGE                                                              
      *-------------------------------------------------------------            
       01  WS-SEND-MESSAGE.                                                     
           05  WS-MSG-CDE                PIC X(03).                             
           05  WS-FILLER-01              PIC X(01).                             
           05  WS-MSG-APPT-TYP           PIC X(02).                             
           05  WS-FILLER-02              PIC X(01).                             
           05  WS-MSG-CTR                PIC 9(04).                             
           05  WS-FILLER-03              PIC X(01).                             
           05  WS-MSG OCCURS 200 TIMES.                                         
               10  WS-MSG-NUM-REGN       PIC X(08).                             
               10  WS-FILLER-04          PIC X(01).                             
               10  WS-MSG-NUM-EMAIL      PIC X(50).                             
               10  WS-FILLER-05          PIC X(01).                             
               10  WS-MSG-DTE-BOOK       PIC X(08).                             
               10  WS-FILLER-06          PIC X(01).                             
               10  WS-MSG-DTE-CLOSE      PIC X(08).                             
               10  WS-FILLER-07          PIC X(01).                             
               10  WS-MSG-NUM-BLK        PIC X(05).                             
               10  WS-FILLER-08          PIC X(01).                             
               10  WS-MSG-NUM-LEVEL      PIC X(02).                             
               10  WS-FILLER-09          PIC X(01).                             
               10  WS-MSG-NUM-UNIT-MAIN  PIC X(04).                             
               10  WS-FILLER-10          PIC X(01).                             
               10  WS-MSG-NUM-UNIT-SUB   PIC X(03).                             
               10  WS-FILLER-11          PIC X(01).                             
               10  WS-MSG-NME-STREET     PIC X(32).                             
               10  WS-FILLER-12          PIC X(01).                             
               10  WS-MSG-CDE-POSTAL     PIC X(06).                             
               10  WS-FILLER-13          PIC X(01).                             
               10  WS-MSG-NME-OIC        PIC X(30).                             
               10  WS-FILLER-14          PIC X(01).                             
               10  WS-MSG-DTE-APPT-AUTO  PIC X(08).                             
               10  WS-FILLER-15          PIC X(01).                             
               10  WS-MSG-TME-APPT-AUTO  PIC X(04).                             
               10  WS-FILLER-16          PIC X(01).                             
                                                                                
      *-------------------------------------------------------------            
      *    MQI - TEMPORARY VARIABLES FOR ERROR CODES                            
      *-------------------------------------------------------------            
       01  BP13CS13-REC.                                                        
           05  CS13-REASON               PIC 9(4).                              
           05  CS13-COMPLETION-CODE      PIC 9(4).                              
           05  CS13-ERR-MSG              PIC X(66).                             
           05  FILLER                    PIC X(27).                             
                                                                                
      *-------------------------------------------------------------            
      *    MQI STRUCTURE DECLARATION                                            
      *-------------------------------------------------------------            
       01  MY-MQ-CONSTANTS.                                                     
           COPY CMQV.                                                           
                                                                                
       01  OBJECT-DESCRIPTOR.                                                   
           COPY CMQODV.                                                         
                                                                                
       01  MESSAGE-DESCRIPTOR.                                                  
           COPY CMQMDV.                                                         
                                                                                
      *-------------------------------------------------------------            
      *    PUT MESSAGE OPTIONS                                                  
      *-------------------------------------------------------------            
       01  PMOPTIONS.                                                           
           COPY CMQPMOV.                                                        
                                                                                
      *-------------------------------------------------------------            
      *    VARIABLE DESCRIPTION:                                                
      *    (1) HCONN                                                            
      *        - CONNECTION HANDLE - REPRESENTS THE CONNECTION TO               
      *          THE QUEUE MANAGER. VALUE IS RETURNED BY MQCONN                 
      *          AND SUBSEQUENTLY REFERENCED IN MQOPEN, MQPUT,                  
      *          MQCLOSE AND MQDISC                                             
      *                                                                         
      *    (2) Q-HANDLE                                                         
      *        - ACCESS THAT HAS BEEN ESTABLISHED  TO THE OBJECT                
      *          CONNECTION HANDLE - VALUE IS RETURNED BY MQOPEN                
      *          AND SUBSEQUENTLY REFERENCED IN MQPUT AND MQCLOSE               
      *                                                                         
      *    (3) OBJECT-DESCRIPTOR                                                
      *        - THIS IDENTIFIES THE OBJECT TO BE OPENED                        
      *                                                                         
      *    (4) OPTIONS                                                          
      *        WHEN USED IN MQOPEN                                              
      *        - OPTIONS THAT CONTROL THE ACTION OF MQOPEN                      
      *        - OPTIONS USED IN THIS PROGRAM:                                  
      *          (A) MQOO-OUTPUT - THE QUEUE IS OPENED FOR USE WITH             
      *                            SUBSEQUENT MQPUT CALL                        
      *          (B) MQOO-FAIL-IF-QUIESCING - FORCES MQCALL TO FAIL             
      *                            IF QUEUE MANAGER IS IN QUIESCING             
      *                            STATE (I.E. PREPARING TO SHUTDOWN)           
      *                                                                         
      *        WHEN USED IN MQCLOSE                                             
      *        - OPTIONS THAT CONTROL THE ACTION OF MQCLOSE                     
      *        - OPTIONS USED IN THIS PROGRAM:                                  
      *          (A) MQCO-NONE   - NO OPTIONAL CLOSE PROCESSING                 
      *                            REQUIRED                                     
      *                                                                         
      *    (5) MESSAGE-DESCRIPTOR                                               
      *        - THIS DESCRIBES THE ATTRIBUTES OF THE MESSAGE                   
      *          BEING SENT, AND RECEIVES INFO ABOUT THE MESSAGE                
      *          AFTER THE PUT REQUEST IS COMPLETE                              
      *                                                                         
      *    (6) PMOPTIONS (PUT MESSAGE OPTIONS)                                  
      *        - OPTIONS THAT CONTROL THE ACTION OF MQPUT                       
      *        - OPTION USED IN THIS PROGRAM:                                   
      *          (A) MQMD-EXPIRY - MESSAGE LIFETIME. THE MESSAGE                
      *                            BECOMES ELIGIBLE TO BE DISCARDED             
      *                            IF IT HAS NOT BEEN REMOVED FROM              
      *                            FROM THE QUEUE BEFORE THIS PERIOD            
      *                            OF TIME ELAPSED                              
      *                          - MUST BE SET TO MQEI_UNLIMITED                
      *          (B) MQMD-PERSISTENCE = MQPER-PERSISTENT                        
      *                                                                         
      *    (7) BUFFER-LENGTH                                                    
      *        - LENGTH OF THE MESSAGE IN BUFFER                                
      *                                                                         
      *    (8) BUFFER                                                           
      *        - MESSAGE DATA                                                   
      *-------------------------------------------------------------            
       01  HCONN                         PIC S9(9) BINARY.                      
       01  Q-HANDLE                      PIC S9(9) BINARY.                      
       01  OPTIONS                       PIC S9(9) BINARY.                      
       01  COMPLETION-CODE               PIC S9(9) BINARY.                      
       01  OPEN-CODE                     PIC S9(9) BINARY.                      
       01  CON-REASON                    PIC S9(9) BINARY.                      
       01  REASON                        PIC S9(9) BINARY.                      
       01  MQMSGD                        PIC S9(9) BINARY.                      
       01  BUFFER-LENGTH                 PIC S9(9) BINARY.                      
       01  BUFFER                        PIC X(368012).                         
       01  QM-NAME                       PIC X(48) VALUE SPACES.                
       01  TARGET-QUEUE                  PIC X(48) VALUE SPACES.                
                                                                                
      *-------------------------------------------------------------            
      *  FLAGS / SWITCHES                                                       
      *-------------------------------------------------------------            
       01  WS-FLAGS.                                                            
           05  WS-F23B-EOF               PIC X(01) VALUE 'N'.                   
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-MAIN-PROCESS.                                                       
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 1100-INITIALIZATION     THRU 1100-EXIT.                      
           PERFORM 2000-BP13F23B-READ      THRU 2000-EXIT.                      
                                                                                
           IF WS-F23B-EOF = 'Y'                                                 
              DISPLAY ' *** '                                                   
              DISPLAY ' *** NO RECORDS FOUND IN SMS FILE'                       
              DISPLAY ' *** '                                                   
                                                                                
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 2100-INIT-CONNECTION    THRU 2100-EXIT.                      
                                                                                
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-F23B-EOF = 'Y'.                                     
                                                                                
           DISPLAY 'WS-SEND-MESSAGE : ' WS-SEND-MESSAGE.                        
                                                                                
           PERFORM 5300-MQ-PUT-MESSAGE     THRU 5300-EXIT.                      
           DISPLAY ' *** APPOINTMENT NOTIFICATION SENT!'                        
           PERFORM 5400-MQ-CLOSE           THRU 5400-EXIT.                      
           PERFORM 5500-MQ-DISCONNECT      THRU 5500-EXIT.                      
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           DISPLAY ' ***** STARTING PROGRAM BP13CS13 *****'.                    
           DISPLAY ' '.                                                         
                                                                                
           OPEN INPUT BP13F23B.                                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-INITIALIZATION.                                                     
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   WS-SEND-MESSAGE.                
                                                                                
           MOVE ';'    TO WS-FILLER-01 WS-FILLER-02 WS-FILLER-03.               
                                                                                
           PERFORM VARYING WS-MSG-CTR FROM 1 BY 1                               
                           UNTIL WS-MSG-CTR > 200                               
              MOVE ';' TO WS-FILLER-04(WS-MSG-CTR)                      TR)     
                          WS-FILLER-05(WS-MSG-CTR)                      TR)     
                          WS-FILLER-06(WS-MSG-CTR)                      TR)     
                          WS-FILLER-07(WS-MSG-CTR)                      TR)     
                          WS-FILLER-08(WS-MSG-CTR)                      TR)     
                          WS-FILLER-09(WS-MSG-CTR)                      TR)     
                          WS-FILLER-10(WS-MSG-CTR)                      TR)     
                          WS-FILLER-11(WS-MSG-CTR)                      TR)     
                          WS-FILLER-12(WS-MSG-CTR)                      TR)     
                          WS-FILLER-13(WS-MSG-CTR)                      TR)     
                          WS-FILLER-14(WS-MSG-CTR)                      TR)     
                          WS-FILLER-15(WS-MSG-CTR)                      TR)     
                          WS-FILLER-16(WS-MSG-CTR)                      TR)     
           END-PERFORM.                                                         
                                                                                
           MOVE 'EML'                      TO   WS-MSG-CDE.                     
           MOVE ZEROES                     TO   WS-MSG-CTR.                     
                                                                                
           ACCEPT WS-BP13Q-NAME            FROM PARM-INPUT.                     
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13F23B-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13F23B                                                        
              AT END MOVE 'Y'              TO   WS-F23B-EOF.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2100-INIT-CONNECTION.                                                    
      *=============================================================            
                                                                                
           PERFORM 5100-MQ-CONNECT         THRU 5100-EXIT                       
           PERFORM 5200-MQ-OPEN            THRU 5200-EXIT                       
                                                                                
           MOVE SPACES                     TO   BUFFER.                         
           MOVE F23B-NUM-APPT-TYPE         TO   WS-MSG-APPT-TYP.                
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           IF F23B-NUM-APPT-TYPE = 'V1'                                         
              PERFORM 3300-PASS-MSG-V1     THRU 3300-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 2000-BP13F23B-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3300-PASS-MSG-V1.                                                        
      *=============================================================            
                                                                                
           ADD 1                 TO   WS-MSG-CTR.                               
                                                                                
           MOVE F23B-NUM-EMAIL   TO   WS-MSG-NUM-EMAIL(WS-MSG-CTR).             
           MOVE F23B-NME-OIC     TO   WS-MSG-NME-OIC(WS-MSG-CTR).               
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5100-MQ-CONNECT.                                                         
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    SET THE QUEUE MANAGER NAME                                           
      *-------------------------------------------------------------            
           IF WS-BP13Q-NAME NOT = SPACES AND LOW-VALUES                         
              MOVE WS-BP13Q-NAME           TO   QM-NAME                         
           ELSE                                                                 
              MOVE WS-QM-NAME              TO   QM-NAME                         
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    CALL MQCONN TO CONNECT TO QUEUE MANAGER                              
      *-------------------------------------------------------------            
           CALL 'MQCONN' USING QM-NAME, HCONN,                                  
                               COMPLETION-CODE, CON-REASON.                     
                                                                                
           IF COMPLETION-CODE = MQCC-FAILED                                     
              MOVE CON-REASON              TO   CS13-REASON                     
              MOVE COMPLETION-CODE         TO   CS13-COMPLETION-CODE            
              MOVE 'BP13CS13 MQCONN ERROR' TO   CS13-ERR-MSG                    
              PERFORM 9100-EXIT-MQCONN     THRU 9100-EXIT                       
           END-IF.                                                              
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5200-MQ-OPEN.                                                            
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    USE DEFAULT WS-QUEUE-NAME                                            
      *-------------------------------------------------------------            
           MOVE WS-QUEUE-NAME              TO   MQOD-OBJECTNAME.                
           ADD  MQOO-OUTPUT MQOO-FAIL-IF-QUIESCING GIVING OPTIONS.              
                                                                                
      *-------------------------------------------------------------            
      *    CALL MQOPEN TO OPEN CONNECTION                                       
      *-------------------------------------------------------------            
           CALL 'MQOPEN' USING HCONN, OBJECT-DESCRIPTOR,                        
                               OPTIONS, Q-HANDLE,                               
                               OPEN-CODE, REASON.                               
                                                                                
           IF REASON NOT = MQRC-NONE                                            
              MOVE REASON                  TO   CS13-REASON                     
              MOVE OPEN-CODE               TO   CS13-COMPLETION-CODE            
              MOVE 'MQOPEN ENDED WITH REASON CODE'                              
                                           TO   CS13-ERR-MSG                    
              PERFORM 9100-EXIT-MQCONN     THRU 9100-EXIT                       
           END-IF.                                                              
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5300-MQ-PUT-MESSAGE.                                                     
      *=============================================================            
                                                                                
           MOVE WS-SEND-MESSAGE            TO   BUFFER.                         
                                                                                
      *-------------------------------------------------------------            
      *    SET UP MESSAGE DESCRIPTOR                                            
      *-------------------------------------------------------------            
           MOVE MQEI-UNLIMITED             TO   MQMD-EXPIRY.                    
           MOVE MQPER-PERSISTENT           TO   MQMD-PERSISTENCE.               
           MOVE MQFMT-STRING               TO   MQMD-FORMAT.                    
           MOVE WS-MESSAGE-LENGTH          TO   BUFFER-LENGTH.                  
                                                                                
      *-------------------------------------------------------------            
      *    CALL MQPUT TO PUT MESSAGE TO QUEUE                                   
      *-------------------------------------------------------------            
           CALL 'MQPUT' USING HCONN, Q-HANDLE,                                  
                              MESSAGE-DESCRIPTOR, PMOPTIONS,                    
                              BUFFER-LENGTH, BUFFER,                            
                              COMPLETION-CODE, REASON.                          
                                                                                
           IF REASON NOT = MQRC-NONE                                            
              MOVE REASON                  TO   CS13-REASON                     
              MOVE COMPLETION-CODE         TO   CS13-COMPLETION-CODE            
              MOVE 'BP13CS13 MQPUT ERROR'  TO   CS13-ERR-MSG                    
              PERFORM 9100-EXIT-MQCONN     THRU 9100-EXIT                       
           END-IF.                                                              
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5400-MQ-CLOSE.                                                           
      *=============================================================            
                                                                                
           MOVE MQCO-NONE                  TO   OPTIONS.                        
                                                                                
      *-------------------------------------------------------------            
      *    CALL MQCLOSE TO CLOSE CONNECTION                                     
      *-------------------------------------------------------------            
           CALL 'MQCLOSE' USING HCONN, Q-HANDLE, OPTIONS,                       
                                COMPLETION-CODE, REASON.                        
                                                                                
           IF REASON NOT =  MQRC-NONE                                           
              MOVE REASON                   TO   CS13-REASON                    
              MOVE COMPLETION-CODE          TO   CS13-COMPLETION-CODE           
              MOVE 'BP13CS13 MQCLOSE ERROR' TO   CS13-ERR-MSG                   
              PERFORM 9100-EXIT-MQCONN      THRU 9100-EXIT                      
           END-IF.                                                              
                                                                                
       5400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5500-MQ-DISCONNECT.                                                      
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    CALL MQDISC TO DISCONNECT FROM QUEUE MANAGER                         
      *-------------------------------------------------------------            
           CALL 'MQDISC' USING HCONN, COMPLETION-CODE, REASON.                  
                                                                                
           IF REASON NOT = MQRC-NONE                                            
              MOVE REASON                  TO   CS13-REASON                     
              MOVE COMPLETION-CODE         TO   CS13-COMPLETION-CODE            
              MOVE 'BP13CS13 MQDISC ERROR' TO   CS13-ERR-MSG                    
              PERFORM 9100-EXIT-MQCONN     THRU 9100-EXIT                       
           END-IF.                                                              
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           CLOSE BP13F23B.                                                      
                                                                                
           IF RETURN-CODE NOT = ZERO                                            
              DISPLAY ' *** RETURN-CODE = ' RETURN-CODE ' ERROR'                
           ELSE                                                                 
              DISPLAY ' '                                                       
              DISPLAY ' *** SUCCESSFUL EXECUTION OF BP13CS13.'                  
              DISPLAY ' *** MESSAGE/S PUT INTO QUEUE SUCCESSFULLY!'             
              DISPLAY ' *** Q MANAGER : ' QM-NAME                               
              DISPLAY ' *** TARGET Q  : ' WS-QUEUE-NAME                         
              DISPLAY ' '                                                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9100-EXIT-MQCONN.                                                        
      *=============================================================            
                                                                                
           DISPLAY ' *** PROGRAM ID:  BP13CS13 '                                
           DISPLAY ' *** Q MANAGER : ' QM-NAME                                  
           DISPLAY ' *** TARGET Q  : ' WS-QUEUE-NAME                            
           DISPLAY ' '                                                          
                                                                                
           IF CS13-COMPLETION-CODE NOT = ZEROES AND LOW-VALUES                  
              DISPLAY ' *** MQ COMPLETION CODE: ' CS13-COMPLETION-CODE          
              DISPLAY ' *** MQ REASON         : ' CS13-REASON                   
              DISPLAY ' *** MQ ERROR MSG      : ' CS13-ERR-MSG                  
              MOVE 99                      TO  RETURN-CODE                      
           END-IF.                                                              
                                                                                
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       9100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
      **********           END OF BP13CS13                **********            
      **************************************************************            
