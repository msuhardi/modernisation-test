       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CY73.                                                 
      *AUTHOR.        PAULO CAMIA LEGASPI.                                      
      *DATE-WRITTEN.  03/08/2010.                                               
      * =========================================================== *           
      * FLAT INVENTORY SYSTEM (BP13)                                *           
      * =========================================================== *           
      *                                                             *           
      *  OBJECTIVE  :                                               *           
      *    1. PROGRAM TO CHECK FOR ALLOCATION TAG = 'NE', NOT       *           
      *       ELIGIBLE CASE, IF FOUND DROP ALL RECORDS WITH THE     *           
      *       SAME NRIC.                                            *           
      *                                                             *           
      *    2. PROGRAM CATER FOR 20 RECORDS UNDER THE SAME NRIC.     *           
      *                                                             *           
      *  INPUT FILES:  1. BP13F595                                  *           
      *                                                             *           
      *  OUTPUT FILE:  1. P13F595A                                  *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG REF  BY   ON      DESCRIPTION                           *           
      * -------- --   --      -----------                           *           
      * BP133954 PCL3 030810  NEW PROGRAM                           *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT BP13F595 ASSIGN TO BP13F595.                                  
           SELECT P13F595A ASSIGN TO P13F595A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F595                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F595.                                                           
                                                                                
                                                                                
       FD  P13F595A                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       01  P13F595A-REC        PIC X(500).                                      
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
                                                                                
      *---------------------------------------------------------------*         
      *       SWITCHES    VARIABLE.                                   *         
      *---------------------------------------------------------------*         
       01  WS-SWITCH.                                                           
           05  WS-F595-EOF           PIC X(1)       VALUE 'N'.                  
           05  WS-F595-NE            PIC X(1)       VALUE 'N'.                  
                                                                                
      *---------------------------------------------------------------*         
      *       RECORD-COUNT VARIABLE.                                  *         
      *---------------------------------------------------------------*         
       01  WS-COUNT.                                                            
           05  WS-CNT                PIC 9(2) COMP  VALUE ZEROS.                
           05  WS-F595-READ          PIC 9(5) COMP  VALUE ZEROS.                
           05  WS-CNT-NE             PIC 9(5) COMP  VALUE ZEROS.                
           05  WS-CNT-WRITE          PIC 9(5) COMP  VALUE ZEROS.                
                                                                                
       01  WS-PREV-NRIC              PIC X(9)       VALUE SPACES.               
       01  WS-P13F595-REC.                                                      
           05 WS-F595-REC            PIC X(500)     OCCURS 20.                  
                                                                                
       01  WS-ALLOCSCH-TYPE          PIC X(3)       VALUE SPACES.               
                                                                                
       COPY PRIOSCH.                                                            
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPENING-ROUTINE      THRU 1000-EXIT.                    
           PERFORM 2000-READ-BP13F595        THRU 2000-EXIT.                    
           PERFORM 3000-MAIN-PROCESS         THRU 3000-EXIT                     
             UNTIL WS-F595-EOF = 'Y'.                                           
           PERFORM 9999-CLOSE-ROUTINE        THRU 9999-EXIT.                    
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPENING-ROUTINE.                                                    
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT BP13F595                                                  
               OUTPUT P13F595A.                                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F595.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F595                                                        
             AT END                                                             
                MOVE 'Y'                 TO WS-F595-EOF                         
                GO                       TO 2000-EXIT                           
           END-READ.                                                            
                                                                                
           ADD   1 TO WS-F595-READ.                                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-MAIN-PROCESS.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                          TO WS-P13F595-REC.              
           INITIALIZE                              WS-P13F595-REC.              
                                                                                
           MOVE F595-NUM-NRIC1                  TO WS-PREV-NRIC.                
           MOVE 'N'                             TO WS-F595-NE.                  
           MOVE 00                              TO WS-CNT.                      
                                                                                
           PERFORM 4000-PROCESS-SAME-KEY      THRU 4000-EXIT                    
             UNTIL F595-NUM-NRIC1 NOT = WS-PREV-NRIC                            
                OR WS-F595-EOF = 'Y'.                                           
                                                                                
           IF WS-F595-NE = 'N'                                                  
              PERFORM VARYING WS-CNT FROM 1 BY 1                                
                UNTIL WS-CNT > 20                                               
                                                                                
                IF WS-F595-REC(WS-CNT) NOT = SPACES AND LOW-VALUES              
                   PERFORM 5000-WRITE-P13F595A        THRU 5000-EXIT            
                ELSE                                                            
                   MOVE 21                              TO WS-CNT               
                END-IF                                                          
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-PROCESS-SAME-KEY.                                                   
      *-------------------------------------------------------------            
                                                                                
           PERFORM 4500-GET-ALLO-SCH           THRU 4500-EXIT.                  
                                                                                
           IF BP13F595-REC(493:2)  = 'NE'                                       
      *       WS-ALLOCSCH-TYPE     = 'CCC'                                      
              ADD 1                              TO WS-CNT-NE                   
              MOVE 'Y'                           TO WS-F595-NE                  
           END-IF.                                                              
                                                                                
                                                                                
           IF WS-CNT <= 20                                                      
              ADD 1                              TO WS-CNT                      
              MOVE BP13F595-REC                  TO WS-F595-REC(WS-CNT)         
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F595          THRU 2000-EXIT.                  
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4500-GET-ALLO-SCH.                                                       
      *-------------------------------------------------------------            
                                                                                
           SET WS-ALLOC-PTR TO 1.                                               
                                                                                
           SEARCH WS-ALLOC-SCH                                                  
              AT END MOVE 'XXX' TO WS-ALLOCSCH-TYPE                             
              WHEN F595-CDE-ALLOC-SCH =                                         
                                WS-ALLOC-SCH-DESP(WS-ALLOC-PTR)                 
              MOVE WS-MAIN-SCH-CD(WS-ALLOC-PTR) TO                              
                                      WS-ALLOCSCH-TYPE.                         
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-WRITE-P13F595A.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                   TO P13F595A-REC.                       
           INITIALIZE                       P13F595A-REC.                       
                                                                                
           WRITE P13F595A-REC          FROM WS-F595-REC(WS-CNT).                
           ADD 1                         TO WS-CNT-WRITE.                       
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9999-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '------- BP13CY73 -------------'.                            
           DISPLAY 'NO OF F595 READ      (BP13F595) : '                         
                                               WS-F595-READ.                    
           DISPLAY 'NO OF NE ALLOC-TAG   (BP13F595) : '                         
                                               WS-CNT-NE.                       
           DISPLAY 'NO OF F595 WRITTEN   (BP13F595) : '                         
                                               WS-CNT-WRITE.                    
                                                                                
           CLOSE BP13F595                                                       
                 P13F595A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
