       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC13.                                                 
       AUTHOR.        BALA AMUTHAN.                                             
       DATE-WRITTEN.  10/05/06.                                                 
      *****************************************************************         
      * OBJECTIVE 1)TO ROLL-OVER BALANCES IN PROGRESS PAYMENT MASTER            
      *             MOVE AMT-LOAN-BALANCE TO AMT-LOAN-OPEN-BALANCE,             
      *             CLOSE THE MONTH ACCOUNT AND GET READY FOR NEW MTH.          
      *                                                                         
      *           2)TO ROLL-OVER BALANCES IN ACCOUNT HISTORY (BP13KD60)         
      *             TO BACKUP FILE (BP13KD61).                                  
      *                                                                         
      * SUBROUTINE     - NIL                                                    
      *                                                                         
      *----------------------------------------------------------------         
      * REF NO   DATE    BY   AMENDMENTS/ENHANCEMENTS                           
      * -------  ----    ---  -----------------------                           
      * BP132857 100506  BA2  NEW PGM                                           
      *          060706  SD10 READ BP13FD00                                     
      *          030307  BA3  CHANGE COUNTER FOR NO OF RECORDS READ             
      *                       FROM BP13KD00                                     
      * BP133052 050607  BA3  CHANGE CONDITION LOAN-BAL NOT = ZEROES            
      *                       TO CATER FOR NEGATIVE VALUES                      
      * BP133173 311007  BA3  INCLUDE KD00-AMT-ARR-INTR                         
      * BP133091 161107  RB12 EXPAND KD00 LENGTH FR 1200 TO 1600                
      * BP133209 100308  ED1  INCLUDE REPUDIATION CASES                         
      * BP133545 060309  JB8  TO CATER FOR SUBTRANSFER                          
      * BP133907 210710  JB8  TO UPDATE KD00 EVEN KD00-AMT-LOAN-BAL = 0         
      * BP134154 150411  JB8  TO CATER FOR RE-FINANCING                         
      * BP134707 070313  IMC1 SAVE OPENING BAL OF PREV MONTH                    
      *****************************************************************         
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13FD00  ASSIGN TO BP13FD00.                                 
           SELECT BP13KD00  ASSIGN TO BP13KD00                                  
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS KD00-NUM-REGN                             
                  FILE STATUS      IS KD00-STATUS                               
                  ACCESS           IS RANDOM.                                   
           SELECT BP13KD60  ASSIGN TO BP13KD60                                  
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS KD60-KEY-FLD                              
                  FILE STATUS      IS KD60-STATUS                               
                  ACCESS           IS RANDOM.                                   
           SELECT BP13KD61  ASSIGN TO BP13KD61                                  
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS KD61-KEY-FLD                              
                  FILE STATUS      IS KD61-STATUS                               
                  ACCESS           IS RANDOM.                                   
           SELECT BP13KD65  ASSIGN TO BP13KD65                                  
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS KD65-CDE-FILEID                           
                  FILE STATUS      IS KD65-STATUS                               
                  ACCESS           IS RANDOM.                                   
                                                                                
           SELECT SY02F001  ASSIGN TO SY02F001.                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13FD00                   BLOCK CONTAINS 0 RECORDS                  
                                      RECORD CONTAINS 1600 CHARACTERS           
                                      LABEL RECORD IS STANDARD                  
                                      RECORDING MODE IS F.                      
       COPY BP13FD00.                                                           
                                                                                
       FD  BP13KD00                   BLOCK CONTAINS 0 RECORDS                  
                                      RECORD CONTAINS 1600 CHARACTERS           
                                      LABEL RECORD IS STANDARD.                 
       COPY BP13KD00.                                                           
                                                                                
       FD  BP13KD60                   BLOCK CONTAINS 0 RECORDS                  
                                      RECORD CONTAINS 250 CHARACTERS            
                                      LABEL RECORDS ARE STANDARD.               
       COPY BP13KD60.                                                           
                                                                                
       FD  BP13KD61                   BLOCK CONTAINS 0 RECORDS                  
                                      RECORD CONTAINS 250 CHARACTERS            
                                      LABEL RECORDS ARE STANDARD.               
       COPY BP13KD61.                                                           
                                                                                
       FD  BP13KD65                   BLOCK CONTAINS 0 RECORDS                  
                                      RECORD CONTAINS 200 CHARACTERS            
                                      LABEL RECORDS ARE STANDARD.               
       COPY BP13KD65.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-STATUS.                                                           
           05 KD00-STATUS              PIC 99       VALUE ZEROS.                
           05 KD60-STATUS              PIC 99       VALUE ZEROS.                
           05 KD61-STATUS              PIC 99       VALUE ZEROS.                
           05 KD65-STATUS              PIC 99       VALUE ZEROS.                
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-CNT-FD00-RD           PIC 9(6) COMP VALUE 0.                   
           05 WS-CNT-KD00-RD           PIC 9(6) COMP VALUE 0.                   
           05 WS-CNT-KD00-NOTFND       PIC 9(6) COMP VALUE 0.                   
           05 WS-CNT-KD00-UPD          PIC 9(6) COMP VALUE 0.                   
           05 WS-CNT-KD60-RD           PIC 9(6) COMP VALUE 0.                   
           05 WS-CNT-KD60-WRT          PIC 9(6) COMP VALUE 0.                   
           05 WS-CNT-KD60-UPD          PIC 9(6) COMP VALUE 0.                   
           05 WS-CNT-KD60-DEL          PIC 9(6) COMP VALUE 0.                   
           05 WS-CNT-KD61-WRT          PIC 9(6) COMP VALUE 0.                   
           05 WS-CNT-KD65-UPD          PIC 9(6) COMP VALUE 0.                   
                                                                                
       01  WS-CURR-DATE.                                                        
           02 WS-CURRENT-DATE.                                                  
              05 WS-DATE-CYM.                                                   
                 10 WS-DATE-CCYY       PIC X(4).                                
                 10 WS-DATE-MM         PIC X(2).                                
              05 WS-DATE-DD            PIC X(2).                                
                                                                                
           02 WS-DTE-TODAY REDEFINES WS-CURRENT-DATE.                           
              05 WS-DTE-TODAY-CC       PIC 9(2).                                
              05 WS-DTE-TODAY-YMD.                                              
                 10 WS-DTE-TODAY-YY    PIC 9(2).                                
                 10 WS-DTE-TODAY-MM    PIC 9(2).                                
                 10 WS-DTE-TODAY-DD    PIC 9(2).                                
                                                                                
       01  WS-TMP-DATE.                                                         
           05  WS-TMP-CYM.                                                      
               10  WS-TMP-CCYY         PIC X(4).                                
               10  WS-TMP-MM           PIC X(2).                                
           05  WS-TMP-DD               PIC X(2).                                
                                                                                
       01  WS-CURRENT-DATE1.                                                    
           05  WS-DATE1.                                                        
                10  WS-DATE1-CYM.                                               
                    15  WS-DATE1-CCYY  PIC X(4).                                
                    15  WS-DATE1-MM    PIC X(2).                                
                10  WS-DATE1-DD        PIC X(2).                                
           05  WS-CURRENT-TIME         PIC X(9).                                
           05  WS-TIME REDEFINES WS-CURRENT-TIME.                               
                10  WS-HH              PIC 9(2).                                
                10  WS-MM              PIC 9(2).                                
                10  WS-SS              PIC 9(2).                                
                10  WS-MS              PIC 9(2).                                
                10  WS-GM              PIC 9(1).                                
           05  FILLER                  PIC X(4).                                
                                                                                
       01  WS-GM-CNT                   PIC S9(8) COMP VALUE ZEROS.              
                                                                                
       01  WS-TRANS-DTE.                                                        
           05  WS-TRANS-DTE-CYM.                                                
               10  WS-TRANS-DATE-CCYY  PIC X(4).                                
               10  WS-TRANS-DATE-MM    PIC X(2).                                
           05  WS-TRANS-DATE-DD        PIC X(2) VALUE '01'.                     
                                                                                
       01  WS-DTE-KD60-UPD             PIC X(6).                                
                                                                                
       01  WS-SER-VAR.                                                          
           05  WS-SER-CHR.                                                      
               10 WS-SER-CHR-1          PIC X(01).                              
               10 WS-SER-CHR-2          PIC X(01).                              
               10 WS-SER-CHR-3          PIC X(01).                              
           05  WS-NUM-SER REDEFINES WS-SER-CHR                                  
                                       PIC 9(03).                               
                                                                                
       01  WS-SWITCH.                                                           
           05 WS-FD00-END              PIC X(1) VALUE 'N'.                      
           05 WS-KD00-END              PIC X(1) VALUE 'N'.                      
           05 WS-KD00-FND              PIC X(1) VALUE 'N'.                      
           05 WS-KD60-FND              PIC X(1) VALUE 'N'.                      
           05 WS-WRITE-KD61            PIC X(1) VALUE 'N'.                      
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-START-UP       THRU 1000-EXIT.                          
           PERFORM 2000-MAIN-PROCESS   THRU 2000-EXIT.                          
           PERFORM 9999-WRAP-UP        THRU 9999-EXIT.                          
                                                                                
                                                                                
      ******************************************************************        
       1000-START-UP.                                                           
      ******************************************************************        
                                                                                
            OPEN INPUT BP13FD00                                                 
                       SY02F001                                                 
                I-O    BP13KD00                                                 
                       BP13KD60                                                 
                       BP13KD65                                                 
                       BP13KD61.                                                
                                                                                
           IF  KD00-STATUS NOT = '00' AND '97'                                  
               DISPLAY 'ERROR OPEN BP13KD00 : ' KD00-STATUS                     
               MOVE KD00-STATUS TO RETURN-CODE                                  
               PERFORM 9999-WRAP-UP        THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF  KD60-STATUS NOT = '00' AND '97'                                  
               DISPLAY 'ERROR OPEN BP13KD60 : ' KD60-STATUS                     
               MOVE KD60-STATUS TO RETURN-CODE                                  
               PERFORM 9999-WRAP-UP        THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF  KD61-STATUS NOT = '00' AND '97'                                  
               DISPLAY 'ERROR OPEN BP13KD61 : ' KD61-STATUS                     
               MOVE KD61-STATUS TO RETURN-CODE                                  
               PERFORM 9999-WRAP-UP        THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF  KD65-STATUS NOT = '00' AND '97'                                  
               DISPLAY 'ERROR OPEN BP13KD65 : ' KD65-STATUS                     
               MOVE KD65-STATUS TO RETURN-CODE                                  
               PERFORM 9999-WRAP-UP        THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
                                                                                
       1000-GET-DATE.                                                           
                                                                                
           PERFORM 4000-READ-SY02F001       THRU 4000-EXIT.                     
           MOVE F001-DTE-CURRENT      TO  WS-CURRENT-DATE.                      
                                                                                
           MOVE FUNCTION CURRENT-DATE TO  WS-CURRENT-DATE1.                     
                                                                                
           ADD  1               TO WS-GM-CNT.                                   
           MOVE WS-GM-CNT       TO WS-GM.                                       
                                                                                
       1000-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ******************************************************************        
       2000-MAIN-PROCESS.                                                       
      ******************************************************************        
                                                                                
           PERFORM 3000-READ-BP13FD00       THRU 3000-EXIT.                     
           PERFORM 5000-READ-UPDATE-PROCESS THRU 5000-EXIT                      
                   UNTIL WS-FD00-END = 'Y'.                                     
           PERFORM 5800-READ-BP13KD65       THRU 5800-EXIT.                     
                                                                                
       2000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       3000-READ-BP13FD00.                                                      
      ******************************************************************        
                                                                                
           READ BP13FD00 AT END                                                 
                MOVE 'Y'         TO WS-FD00-END                                 
                GO TO 3000-EXIT.                                                
           ADD 1 TO WS-CNT-FD00-RD.                                             
                                                                                
       3000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       4000-READ-SY02F001.                                                      
      ******************************************************************        
                                                                                
           READ SY02F001 AT END                                                 
                DISPLAY 'RECORD NOT FOUND - SY02F001.'.                         
                                                                                
       4000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       5000-READ-UPDATE-PROCESS.                                                
      ******************************************************************        
           IF  FD00-KEY-FLD = SPACES OR LOW-VALUES                              
               PERFORM 3000-READ-BP13FD00    THRU 3000-EXIT                     
               GO TO 5000-EXIT.                                                 
                                                                                
           IF FD00-DTE-REPUDIATE = LOW-VALUES                                   
              MOVE SPACES TO FD00-DTE-REPUDIATE                                 
           END-IF.                                                              
                                                                                
           IF FD00-DTE-REFINANCE = LOW-VALUES                                   
              MOVE SPACES TO FD00-DTE-REFINANCE                                 
           END-IF.                                                              
                                                                                
           IF FD00-AMT-LOAN-BAL IS NOT NUMERIC                                  
              MOVE ZEROES TO FD00-AMT-LOAN-BAL                                  
           END-IF.                                                              
                                                                                
           PERFORM 5050-UPDATE-BP13KD00          THRU 5050-EXIT                 
                                                                                
           IF FD00-AMT-LOAN-BAL NOT = ZEROES                                    
           OR (FD00-DTE-REPUDIATE(1:6) = F001-DTE-LAST-DAY-PREV(1:6))           
           OR (KD00-DTE-EFF-TRANSFER(1:6) = F001-DTE-LAST-DAY-PREV(1:6))        
           OR (FD00-DTE-REFINANCE(1:6) = F001-DTE-LAST-DAY-PREV(1:6))           
              PERFORM 5100-READ-KD60             THRU 5100-EXIT                 
              IF WS-KD60-FND = 'Y'                                              
                 PERFORM 5200-MOVE-VALUES-KD61   THRU 5200-EXIT                 
                 MOVE 'N'                          TO WS-WRITE-KD61             
                 PERFORM 5300-WRITE-KD61         THRU 5300-EXIT                 
                   UNTIL WS-WRITE-KD61 = 'Y'                                    
              END-IF                                                            
              PERFORM 5500-MOVE-VALUE-KD60       THRU 5500-EXIT                 
           ELSE                                                                 
              PERFORM 5100-READ-KD60             THRU 5100-EXIT                 
              IF WS-KD60-FND = 'Y'                                              
                 PERFORM 5200-MOVE-VALUES-KD61   THRU 5200-EXIT                 
                 MOVE 'N'                          TO WS-WRITE-KD61             
                 PERFORM 5300-WRITE-KD61         THRU 5300-EXIT                 
                   UNTIL WS-WRITE-KD61 = 'Y'                                    
                 PERFORM 5750-DELETE-BP13KD60    THRU 5750-EXIT                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-BP13FD00            THRU 3000-EXIT.                
                                                                                
       5000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       5050-UPDATE-BP13KD00.                                                    
      ******************************************************************        
                                                                                
           MOVE  FD00-KEY-FLD        TO     KD00-KEY-FLD.                       
           READ BP13KD00.                                                       
           IF KD00-STATUS = 00 OR 02                                            
              ADD 1                  TO     WS-CNT-KD00-RD                      
              PERFORM 5060-REWRITE-BP13KD00 THRU 5060-EXIT                      
           ELSE                                                                 
             IF KD00-STATUS = 23                                                
                DISPLAY 'BP13KD00-READ ERROR. REG: ' KD00-NUM-REGN              
                        ' STATUS : ' KD00-STATUS                                
             ELSE                                                               
                DISPLAY 'BP13KD00-READ ERROR. STATUS: '                         
                       KD00-STATUS                                              
                MOVE   KD00-STATUS   TO   RETURN-CODE                           
                PERFORM 9999-WRAP-UP        THRU 9999-EXIT                      
             END-IF                                                             
           END-IF.                                                              
                                                                                
       5050-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       5060-REWRITE-BP13KD00.                                                   
      ******************************************************************        
           IF KD00-AMT-LOAN-BAL IS NOT NUMERIC                                  
              MOVE ZEROS             TO KD00-AMT-LOAN-BAL                       
           END-IF.                                                              
           MOVE KD00-AMT-LOAN-BAL TO KD00-AMT-OPEN-LOAN-BAL                     
                                                                                
           REWRITE BP13KD00-REC.                                                
                                                                                
           IF KD00-STATUS = 00 OR 02                                            
              ADD 1 TO WS-CNT-KD00-UPD                                          
           ELSE                                                                 
              DISPLAY ' BP13KD00-REWRITING ERROR, REGN: ' KD00-NUM-REGN         
                      ' STATUS : ' KD00-STATUS                                  
              MOVE KD00-STATUS           TO   RETURN-CODE                       
              PERFORM 9999-WRAP-UP        THRU 9999-EXIT                        
           END-IF.                                                              
                                                                                
       5060-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       5100-READ-KD60.                                                          
      ******************************************************************        
                                                                                
           MOVE   SPACES             TO     BP13KD60-REC.                       
           INITIALIZE                       BP13KD60-REC.                       
           MOVE  FD00-NUM-REGN       TO     KD60-NUM-REGN.                      
           READ BP13KD60.                                                       
           IF KD60-STATUS = 00 OR 02                                            
              MOVE 'Y'               TO     WS-KD60-FND                         
              ADD 1                  TO     WS-CNT-KD60-RD                      
           ELSE                                                                 
             IF KD60-STATUS = 23                                                
                MOVE 'N'             TO     WS-KD60-FND                         
             ELSE                                                               
                DISPLAY 'BP13KD60-READ ERROR. STATUS: '                         
                       KD60-STATUS                                              
                MOVE   KD60-STATUS   TO   RETURN-CODE                           
                PERFORM 9999-WRAP-UP        THRU 9999-EXIT                      
             END-IF                                                             
           END-IF.                                                              
                                                                                
       5100-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       5200-MOVE-VALUES-KD61.                                                   
      ******************************************************************        
           MOVE SPACES                 TO BP13KD61-REC.                         
           INITIALIZE                     BP13KD61-REC.                         
           MOVE BP13KD60-REC           TO BP13KD61-REC.                         
                                                                                
           MOVE WS-TIME                TO KD61-TME-UPDATE.                      
           MOVE WS-CURRENT-DATE        TO KD61-DTE-UPDATE.                      
           MOVE 'BP13CC13'             TO KD61-NUM-USERID.                      
                                                                                
       5200-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       5300-WRITE-KD61.                                                         
      ******************************************************************        
           WRITE BP13KD61-REC.                                                  
           IF KD61-STATUS = 00 OR 02                                            
              ADD 1    TO WS-CNT-KD61-WRT                                       
              MOVE 'Y' TO WS-WRITE-KD61                                         
           ELSE                                                                 
              IF KD61-STATUS = 22                                               
                 PERFORM 5400-SERIAL-PROCESS  THRU 5400-EXIT                    
              ELSE                                                              
                 DISPLAY ' BP13KD61-WRITING ERROR, STATUS: '                    
                         KD61-STATUS                                            
                 MOVE KD61-STATUS           TO   RETURN-CODE                    
                 PERFORM 9999-WRAP-UP        THRU 9999-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5300-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       5400-SERIAL-PROCESS.                                                     
      ******************************************************************        
           MOVE KD61-NUM-SEQ           TO   WS-SER-CHR.                         
           IF WS-NUM-SER IS NOT NUMERIC                                         
              MOVE ZEROES              TO   WS-NUM-SER                          
           END-IF.                                                              
                                                                                
           ADD +1                      TO   WS-NUM-SER.                         
           MOVE WS-NUM-SER             TO   KD61-NUM-SEQ.                       
                                                                                
       5400-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       5500-MOVE-VALUE-KD60.                                                    
      ******************************************************************        
           PERFORM 5550-CHECK-NUMERIC  THRU 5550-EXIT.                          
           MOVE WS-DATE-CYM            TO WS-TRANS-DTE-CYM.                     
           MOVE WS-TRANS-DTE           TO KD60-DTE-TRANS.                       
           MOVE KD00-NUM-SCH-ACCNT     TO KD60-NUM-SCH-ACCNT.                   
           MOVE KD60-AMT-OPEN-LOAN-BAL TO KD60-AMT-OPEN-LOAN-BAL-PREVMTH        
           MOVE KD00-AMT-LOAN-BAL      TO KD60-AMT-OPEN-LOAN-BAL.               
           MOVE KD00-AMT-ARR-INSTAL    TO KD60-AMT-OPEN-ARR-INST.               
           MOVE KD00-CDE-LOAN-SCH      TO KD60-CDE-LOAN-SCH.                    
           MOVE KD00-AMT-INSTAL        TO KD60-AMT-INST.                        
           MOVE KD00-DTE-INSTAL-EFF    TO KD60-DTE-INST.                        
           MOVE KD00-AMT-ARR-INTR      TO KD60-AMT-OPEN-INTEREST.               
                                                                                
           MOVE WS-TIME          TO KD60-TME-UPDATE.                            
           MOVE WS-CURRENT-DATE  TO KD60-DTE-UPDATE.                            
           MOVE 'BP13CC13'       TO KD60-NUM-USERID.                            
                                                                                
           IF WS-KD60-FND = 'N'                                                 
              MOVE '001'                      TO   KD60-NUM-SEQ                 
              PERFORM   5600-WRITE-BP13KD60   THRU 5600-EXIT                    
           ELSE                                                                 
              PERFORM   5700-REWRITE-BP13KD60 THRU 5700-EXIT                    
           END-IF.                                                              
                                                                                
       5500-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       5550-CHECK-NUMERIC.                                                      
      ******************************************************************        
           IF KD00-AMT-LOAN-BAL IS NOT NUMERIC                                  
              MOVE ZEROES            TO KD00-AMT-LOAN-BAL                       
           END-IF.                                                              
           IF KD00-AMT-ARR-INSTAL IS NOT NUMERIC                                
              MOVE ZEROES            TO KD00-AMT-ARR-INSTAL                     
           END-IF.                                                              
           IF KD00-AMT-INSTAL IS NOT NUMERIC                                    
              MOVE ZEROES            TO KD00-AMT-INSTAL                         
           END-IF.                                                              
           IF KD00-DTE-INSTAL-EFF IS NOT NUMERIC                                
              MOVE ZEROES            TO KD00-DTE-INSTAL-EFF                     
           END-IF.                                                              
           IF KD00-AMT-ARR-INTR IS NOT NUMERIC                                  
              MOVE ZEROES            TO KD00-AMT-ARR-INTR                       
           END-IF.                                                              
                                                                                
       5550-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       5600-WRITE-BP13KD60.                                                     
      ******************************************************************        
           WRITE BP13KD60-REC.                                                  
                                                                                
           IF KD60-STATUS = 00 OR 02                                            
              ADD 1 TO WS-CNT-KD60-WRT                                          
           ELSE                                                                 
              DISPLAY ' BP13KD60-WRINTING ERROR, STATUS: '                      
                       KD60-STATUS                                              
              MOVE KD60-STATUS           TO   RETURN-CODE                       
              PERFORM 9999-WRAP-UP        THRU 9999-EXIT                        
           END-IF.                                                              
                                                                                
       5600-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       5700-REWRITE-BP13KD60.                                                   
      ******************************************************************        
           REWRITE BP13KD60-REC.                                                
                                                                                
           IF KD60-STATUS = 00 OR 02                                            
              ADD 1 TO WS-CNT-KD60-UPD                                          
           ELSE                                                                 
              DISPLAY ' BP13KD60-REWRITING ERROR, STATUS: '                     
                       KD60-STATUS                                              
              MOVE KD60-STATUS           TO   RETURN-CODE                       
              PERFORM 9999-WRAP-UP        THRU 9999-EXIT                        
           END-IF.                                                              
                                                                                
       5700-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       5750-DELETE-BP13KD60.                                                    
      ******************************************************************        
           DELETE  BP13KD60.                                                    
                                                                                
           IF KD60-STATUS = 00 OR 02                                            
              ADD 1 TO WS-CNT-KD60-DEL                                          
           ELSE                                                                 
              DISPLAY ' BP13KD60-REWRITING ERROR, STATUS: '                     
                       KD60-STATUS                                              
              MOVE KD60-STATUS           TO   RETURN-CODE                       
              PERFORM 9999-WRAP-UP        THRU 9999-EXIT                        
           END-IF.                                                              
                                                                                
       5750-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       5800-READ-BP13KD65.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                 TO BP13KD65-REC                          
           INITIALIZE                     BP13KD65-REC.                         
                                                                                
           MOVE 'KD65'                   TO KD65-CDE-FILEID.                    
           READ BP13KD65.                                                       
           IF KD65-STATUS = 00 OR 02                                            
              PERFORM 5900-REWRITE-BP13KD65  THRU 5900-EXIT                     
           ELSE                                                                 
             IF KD65-STATUS = 23                                                
                DISPLAY 'BP13KD65-RECORD NOT FOUND.'                            
                PERFORM 9999-WRAP-UP        THRU 9999-EXIT                      
              ELSE                                                              
                 DISPLAY ' BP13KD65-READ ERROR, STATUS: '                       
                         KD65-STATUS                                            
                 MOVE KD65-STATUS           TO   RETURN-CODE                    
                 PERFORM 9999-WRAP-UP        THRU 9999-EXIT                     
             END-IF                                                             
           END-IF.                                                              
                                                                                
       5800-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       5900-REWRITE-BP13KD65.                                                   
      ******************************************************************        
           MOVE F001-DTE-LAST-DAY-PREV   TO KD65-DTE-BAL-CLOSE.                 
                                                                                
           REWRITE BP13KD65-REC.                                                
           IF KD65-STATUS = 0                                                   
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY ' BP13KD65-REWRITING ERROR, STATUS: '                     
                       KD65-STATUS                                              
              MOVE KD65-STATUS           TO   RETURN-CODE                       
              PERFORM 9999-WRAP-UP        THRU 9999-EXIT                        
           END-IF.                                                              
                                                                                
       5900-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       9999-WRAP-UP.                                                            
      ******************************************************************        
           DISPLAY  SPACES.                                                     
           DISPLAY '******* CONTROL   COUNTS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13CC13'                                      
           DISPLAY  SPACES.                                                     
           DISPLAY 'NO. OF BP13FD00 RECS READ    : ' WS-CNT-FD00-RD.            
           DISPLAY 'NO. OF BP13KD00 RECS READ    : ' WS-CNT-KD00-RD.            
           DISPLAY 'NO. OF BP13KD00 RECS UPDATE  : ' WS-CNT-KD00-UPD.           
           DISPLAY 'NO. OF BP13KD60 RECS READ    : ' WS-CNT-KD60-RD.            
           DISPLAY 'NO. OF BP13KD60 RECS UPDATE  : ' WS-CNT-KD60-UPD.           
           DISPLAY 'NO. OF BP13KD60 RECS DELETE  : ' WS-CNT-KD60-DEL.           
           DISPLAY 'NO. OF BP13KD60 RECS WRITTEN : ' WS-CNT-KD60-WRT.           
           DISPLAY 'NO. OF BP13KD61 RECS WRITTEN : ' WS-CNT-KD61-WRT.           
                                                                                
           CLOSE    BP13KD65                                                    
                    SY02F001                                                    
                    BP13FD00                                                    
                    BP13KD00                                                    
                    BP13KD60                                                    
                    BP13KD61.                                                   
                                                                                
           IF  KD00-STATUS NOT = '00'                                           
               DISPLAY 'ERROR CLOSE BP13KD00 : ' KD00-STATUS                    
               MOVE KD00-STATUS TO RETURN-CODE                                  
           END-IF.                                                              
                                                                                
           IF  KD65-STATUS NOT = '00'                                           
               DISPLAY 'ERROR CLOSE BP13KD65 : ' KD65-STATUS                    
               MOVE KD65-STATUS TO RETURN-CODE                                  
           END-IF.                                                              
                                                                                
           IF  KD60-STATUS NOT = '00'                                           
               DISPLAY 'ERROR CLOSE BP13KD60 : ' KD60-STATUS                    
               MOVE KD60-STATUS TO RETURN-CODE                                  
           END-IF.                                                              
                                                                                
           IF  KD61-STATUS NOT = '00'                                           
               DISPLAY 'ERROR CLOSE BP13KD61 : ' KD61-STATUS                    
               MOVE KD61-STATUS TO RETURN-CODE                                  
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
                                                                                
