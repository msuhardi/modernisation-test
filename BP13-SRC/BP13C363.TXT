      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C363.                                                 
       AUTHOR.        NOR.                                                      
       DATE-WRITTEN.  09/02/1999.                                               
                                                                                
      *****************************************************************         
      *                 SYSTEM OF COMMITMENT (BP13)                   *         
      *****************************************************************         
      * OBJECTIVE : OUTOUT NRIC 1 - 4 FROM SOC MASTER BP13K800        *         
      *           : TO USE NRIC TO MATCH PPOS (BR10K010). IF MATCHED, *         
      *             IF THE PPTY CODE NOT EQUAL TO COMMERCIAL MARKET   *         
      *             OR STALL, UPDATE 'N' TO SOC MASTER IN             *         
      *             SCCC-ELIGIBITY-TAG                                *         
      *                                                               *         
      * SCCC-ELGBTY = Y : ELIGIBLE                                    *         
      *             = N : NOT ELIGIBLE                                *         
      *             = Z : NOT ELIGIBLE FORCE THRU BY SALES UNIT       *         
      *             = R : ELIGIBLE FORCE THRU BY SALES UNIT FOR SPP   *         
      *                   ALLOC-SCH IF FOUND TO HAVE SINGAPOREAN AS   *         
      *                   OCCUPIER                                    *         
      *             = E : ELIGIBLE FORCE THRU BY SALES UNIT. EXEMPTED *         
      *                                                               *         
      * INELIGIBLE CODE : CHSIM - COM STALL IN MARKET                 *         
      * IN PPOS           CSTAL - COM STALL IN FOOD CTE               *         
      *****************************************************************         
      * CHGE REQ    DATE     BY  DESCRIPTION                          *         
      * -------- ---------- ---- ------------------------------------ *         
      * BP130636 09/02/1999 NOR  NEW PGRM                             *         
      * BP132062 14/02/2002 CKK  TO ADD NEW SCCC-ELGBTY CODE, 'E'     *         
      * BP133154 19/07/2007 CLT1 NOT TO MONITOR FOR SCCC-ELGBTY 'N'   *         
      * BP133704 03/05/2011 PCL3 TO CHECK SCCC-ELGBTY-FREEZE.         *         
      *****************************************************************         
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
       CONFIGURATION SECTION.                                                   
      *------------------------*                                                
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
      *------------------------*                                                
       INPUT-OUTPUT SECTION.                                                    
      *------------------------*                                                
       FILE-CONTROL.                                                            
           SELECT BP13K800            ASSIGN BP13K800                           
                                      ORGANIZATION IS INDEXED                   
                                      ACCESS MODE  IS RANDOM                    
                                      RECORD KEY   IS K800-NUM-REGN             
                                      FILE STATUS  IS WS-K800-STATUS.           
                                                                                
           SELECT BP13F800            ASSIGN BP13F800.                          
                                                                                
           SELECT BP13INP             ASSIGN BP13INP.                           
                                                                                
           SELECT BP13INP2            ASSIGN BP13INP2.                          
                                                                                
           SELECT BP13L363            ASSIGN BP13L363.                          
                                                                                
           SELECT P13L363A            ASSIGN P13L363A.                          
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
      *------------------------*                                                
                                                                                
       FD   BP13K800                  RECORD CONTAINS 2000 CHARACTERS.          
       COPY BP13K800.                                                           
                                                                                
       FD   BP13F800                  RECORD CONTAINS 2000 CHARACTERS           
                                      LABEL RECORDS ARE OMITTED                 
                                      RECORDING MODE IS F.                      
       COPY BP13F800.                                                           
                                                                                
       FD   BP13INP                   RECORD CONTAINS 30 CHARACTERS             
                                      LABEL RECORDS ARE STANDARD                
                                      RECORDING MODE IS F.                      
       01   BP13INP-REC.                                                        
            05  INP-REGN              PIC X(08).                                
            05  INP-NRIC              PIC X(09).                                
            05  INP-SCCC-TAG          PIC X(01).                                
            05  FILLER                PIC X(12).                                
                                                                                
       FD   BP13INP2                  RECORD CONTAINS 30 CHARACTERS             
                                      LABEL RECORDS ARE STANDARD                
                                      RECORDING MODE IS F.                      
       01   BP13INP2-REC.                                                       
            05  INP2-REGN             PIC X(08).                                
            05  INP2-NRIC             PIC X(09).                                
            05  INP2-SCCC-TAG         PIC X(01).                                
            05  FILLER                PIC X(12).                                
                                                                                
       FD   BP13L363                  RECORD CONTAINS 132 CHARACTERS            
                                      LABEL RECORDS ARE OMITTED                 
                                      RECORDING MODE IS F.                      
       01   BP13L363-REC              PIC X(132).                               
                                                                                
       FD   P13L363A                  RECORD CONTAINS 132 CHARACTERS            
                                      LABEL RECORDS ARE OMITTED                 
                                      RECORDING MODE IS F.                      
       01   P13L363A-REC              PIC X(132).                               
                                                                                
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
       01  WS-FLAG-N-SWITCH.                                                    
           05  WS-INP-EOF-SW          PIC X(01)  VALUE 'N'.                     
               88  WS-INP-EOF         VALUE 'Y'.                                
           05  WS-K800-FOUND-SW       PIC X(01)  VALUE 'N'.                     
               88  WS-K800-FOUND      VALUE 'Y'.                                
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K800-STATUS         PIC 9(02)  VALUE ZEROS.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-K800-FND-CTR        PIC 9(05)  VALUE ZEROS.                   
           05  WS-K800-NOTFND-CTR     PIC 9(05)  VALUE ZEROS.                   
           05  WS-INP-READ            PIC 9(05)  VALUE ZEROS.                   
           05  WS-INP2-READ           PIC 9(05)  VALUE ZEROS.                   
           05  WS-K800-REWRTN         PIC 9(05)  VALUE ZEROS.                   
           05  WS-F800-WRITE          PIC 9(05)  VALUE ZEROS.                   
           05  WS-NOUPD-CNT           PIC 9(05)  VALUE ZEROS.                   
           05  WS-BYPASS              PIC 9(05)  VALUE ZEROS.                   
           05  WS-LINE-CTR            PIC 9(03)  VALUE ZEROS.                   
           05  WS-LINE2-CTR           PIC 9(03)  VALUE ZEROS.                   
           05  WS-MATCH               PIC 9(05)  VALUE ZEROS.                   
           05  WS-NTFND-INP           PIC 9(05)  VALUE ZEROS.                   
           05  WS-NTFND-INP2          PIC 9(05)  VALUE ZEROS.                   
           05  WS-SN-CTR              PIC 9(05)  VALUE ZEROS.                   
           05  WS-SN-A-CTR            PIC 9(05)  VALUE ZEROS.                   
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-YY              PIC X(04)  VALUE SPACES.                  
           05  WS-SYS-MM              PIC X(02)  VALUE SPACES.                  
           05  WS-SYS-DD              PIC X(02)  VALUE SPACES.                  
                                                                                
       01  RPT-HEADER01.                                                        
           05  FILLER                 PIC X(08)  VALUE 'BP13L363'.              
           05  FILLER                 PIC X(20)  VALUE SPACES.                  
           05  FILLER                 PIC X(39)  VALUE                          
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                 PIC X(10)  VALUE SPACES.                  
           05  FILLER                 PIC X(07)  VALUE 'DATE :'.                
           05  L363-DATE              PIC X(10)  VALUE SPACES.                  
                                                                                
       01  RPT-HEADER02.                                                        
           05  FILLER                 PIC X(28)  VALUE SPACES.                  
           05  FILLER                 PIC X(38)  VALUE                          
               'REPORT ON UPDATE OF SC/CC ELIG TAG "N"'.                        
           05  FILLER                 PIC X(11)  VALUE SPACES.                  
           05  FILLER                 PIC X(07)  VALUE 'PAGE :'.                
           05  L363-PAGE              PIC 9(05)  VALUE ZEROS.                   
                                                                                
       01  RPT-HEADER03.                                                        
           05  FILLER                 PIC X(35)  VALUE SPACES.                  
           05  FILLER                 PIC X(03)  VALUE 'S/N'.                   
           05  FILLER                 PIC X(06)  VALUE SPACES.                  
           05  FILLER                 PIC X(04)  VALUE 'REGN'.                  
           05  FILLER                 PIC X(07)  VALUE SPACES.                  
           05  FILLER                 PIC X(04)  VALUE 'NRIC'.                  
                                                                                
       01  RPT-HEADER04.                                                        
           05  FILLER                 PIC X(34)  VALUE SPACES.                  
           05  FILLER                 PIC X(05)  VALUE '-----'.                 
           05  FILLER                 PIC X(03)  VALUE SPACES.                  
           05  FILLER                 PIC X(08)  VALUE '--------'.              
           05  FILLER                 PIC X(03)  VALUE SPACES.                  
           05  FILLER                 PIC X(09)  VALUE '---------'.             
                                                                                
       01  RPT-DETAILS.                                                         
           05  FILLER                 PIC X(34)  VALUE SPACES.                  
           05  L363-SN                PIC 9(05)  VALUE ZEROS.                   
           05  FILLER                 PIC X(03)  VALUE SPACES.                  
           05  L363-REGN              PIC X(08)  VALUE ALL 'X'.                 
           05  FILLER                 PIC X(03)  VALUE SPACES.                  
           05  L363-NRIC              PIC X(09)  VALUE ALL 'X'.                 
                                                                                
       01  RPT-HEADER01A.                                                       
           05  FILLER                 PIC X(08)  VALUE 'P13L363A'.              
           05  FILLER                 PIC X(20)  VALUE SPACES.                  
           05  FILLER                 PIC X(39)  VALUE                          
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                 PIC X(10)  VALUE SPACES.                  
           05  FILLER                 PIC X(07)  VALUE 'DATE :'.                
           05  L363A-DATE             PIC X(10)  VALUE SPACES.                  
                                                                                
       01  RPT-HEADER02A.                                                       
           05  FILLER                 PIC X(28)  VALUE SPACES.                  
           05  FILLER                 PIC X(38)  VALUE                          
               'REPORT ON UPDATE OF SC/CC ELIG TAG "Y"'.                        
           05  FILLER                 PIC X(11)  VALUE SPACES.                  
           05  FILLER                 PIC X(07)  VALUE 'PAGE :'.                
           05  L363A-PAGE             PIC 9(05)  VALUE ZEROS.                   
                                                                                
      *------------------------*                                                
       PROCEDURE DIVISION.                                                      
      *------------------------*                                                
                                                                                
      *------------------------*                                                
       0000-MAIN.                                                               
      *------------------------*                                                
           PERFORM 1000-OPEN-FILES               THRU  1000-EXIT.               
           PERFORM 2000-READ-BP13INP             THRU  2000-EXIT.               
           PERFORM 2010-READ-BP13INP2            THRU  2010-EXIT.               
           PERFORM 3000-PROCESS                  THRU  3000-EXIT                
             UNTIL INP-REGN = HIGH-VALUE AND                                    
                   INP2-REGN = HIGH-VALUES.                                     
           PERFORM 9000-CLOSE-FILES              THRU  9000-EXIT.               
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       1000-OPEN-FILES.                                                         
      *------------------------*                                                
           OPEN INPUT BP13INP                                                   
                      BP13INP2                                                  
                I-O   BP13K800                                                  
               OUTPUT BP13F800                                                  
                      BP13L363                                                  
                      P13L363A.                                                 
                                                                                
           IF WS-K800-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR OPENING BP13K800, FILE STATUS ('                   
                      WS-K800-STATUS ')'                                        
              MOVE WS-K800-STATUS          TO  RETURN-CODE                      
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       2000-READ-BP13INP.                                                       
      *------------------------*                                                
           READ BP13INP                                                         
             AT END                                                             
                MOVE HIGH-VALUES    TO  INP-REGN                                
                MOVE 'Y'            TO  WS-INP-EOF-SW                           
            NOT AT END                                                          
                ADD   1             TO  WS-INP-READ                             
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       2010-READ-BP13INP2.                                                      
      *------------------------*                                                
           READ BP13INP2                                                        
             AT END                                                             
                MOVE HIGH-VALUES    TO  INP2-REGN                               
            NOT AT END                                                          
                ADD  1              TO  WS-INP2-READ                            
           END-READ.                                                            
                                                                                
       2010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       2020-READ-BP13K800.                                                      
      *------------------------*                                                
           MOVE SPACES               TO  WS-K800-FOUND-SW.                      
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN ZEROS                                                           
                MOVE 'Y'             TO  WS-K800-FOUND-SW                       
                ADD   1              TO  WS-K800-FND-CTR                        
           WHEN 23                                                              
                MOVE 'N'             TO  WS-K800-FOUND-SW                       
                ADD   1              TO  WS-K800-NOTFND-CTR                     
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K800, FILE STATUS ('                 
                        WS-K800-STATUS ')'                                      
                MOVE WS-K800-STATUS  TO  RETURN-CODE                            
                GO TO 9000-CLOSE-FILES                                          
           END-EVALUATE.                                                        
                                                                                
       2020-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       3000-PROCESS.                                                            
      *------------------------*                                                
           IF INP-REGN = INP2-REGN                                              
              ADD  1                              TO  WS-MATCH                  
              PERFORM 2000-READ-BP13INP         THRU  2000-EXIT                 
              PERFORM 2010-READ-BP13INP2        THRU  2010-EXIT                 
           ELSE                                                                 
              IF INP-REGN > INP2-REGN                                           
                 ADD  1                           TO  WS-NTFND-INP2             
                 MOVE SPACES                      TO  K800-NUM-REGN             
                 MOVE INP2-REGN                   TO  K800-NUM-REGN             
                 PERFORM 2020-READ-BP13K800     THRU  2020-EXIT                 
                 PERFORM 3100-UPDATE-YES-ELGBTY THRU  3100-EXIT                 
                 PERFORM 2010-READ-BP13INP2     THRU  2010-EXIT                 
              ELSE                                                              
                 IF INP-REGN < INP2-REGN                                        
                    ADD 1                         TO  WS-NTFND-INP              
                    MOVE SPACES                   TO  K800-NUM-REGN             
                    MOVE INP-REGN                 TO  K800-NUM-REGN             
                    PERFORM 2020-READ-BP13K800    THRU  2020-EXIT               
                    PERFORM 3200-UPDATE-NO-ELGBTY THRU  3200-EXIT               
                    PERFORM 2000-READ-BP13INP     THRU  2000-EXIT               
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       3100-UPDATE-YES-ELGBTY.                                                  
      *------------------------*                                                
           IF WS-K800-FOUND                                                     
              IF K800-NUM-SCCC-ELGBTY = 'Z' OR 'R' OR 'E' OR 'N'                
                  ADD 1 TO WS-NOUPD-CNT                                         
              ELSE                                                              
                  IF K800-NUM-SCCC-ELGBTY-FREEZE NOT = 'Y'                      
                     MOVE 'Y'                TO  K800-NUM-SCCC-ELGBTY           
                     PERFORM 4000-UPDATE-BP13K800 THRU 4000-EXIT                
                     PERFORM 5010-WRITE-REPORT-A  THRU 5010-EXIT                
                  ELSE                                                          
                     ADD 1                   TO WS-NOUPD-CNT                    
                  END-IF                                                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       3200-UPDATE-NO-ELGBTY.                                                   
      *------------------------*                                                
           IF WS-K800-FOUND                                                     
              IF K800-NUM-SCCC-ELGBTY = 'Z' OR 'R' OR 'E'                       
                  ADD 1 TO WS-NOUPD-CNT                                         
              ELSE                                                              
                  IF K800-NUM-SCCC-ELGBTY-FREEZE NOT = 'Y'                      
                     MOVE 'N'             TO  K800-NUM-SCCC-ELGBTY              
                     PERFORM 4000-UPDATE-BP13K800   THRU  4000-EXIT             
                     PERFORM 5000-WRITE-REPORT      THRU  5000-EXIT             
                  ELSE                                                          
                     ADD 1                          TO WS-NOUPD-CNT             
                  END-IF                                                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       4000-UPDATE-BP13K800.                                                    
      *------------------------*                                                
           IF K800-NUM-SCCC-ELGBTY = 'N'                                        
              WRITE BP13F800-MASTER FROM BP13K800-MASTER                        
              ADD 1                 TO   WS-F800-WRITE                          
           END-IF.                                                              
                                                                                
           REWRITE BP13K800-MASTER.                                             
                                                                                
           IF WS-K800-STATUS = ZEROS                                            
              ADD 1              TO  WS-K800-REWRTN                             
           ELSE                                                                 
              DISPLAY 'ERROR REWRITING BP13K800 RECORD, FILE STATUS ('          
                      WS-K800-STATUS                                            
              MOVE WS-K800-STATUS      TO RETURN-CODE                           
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       5000-WRITE-REPORT.                                                       
      *------------------------*                                                
           IF WS-LINE-CTR > 50 OR WS-SN-CTR = ZEROS                             
              ADD   1                          TO  L363-PAGE                    
              MOVE  FUNCTION CURRENT-DATE(1:8) TO WS-SYSTEM-DATE                
              STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-YY                      
                     DELIMITED BY SIZE                                          
                INTO L363-DATE                                                  
              END-STRING                                                        
              MOVE  4                          TO  WS-LINE-CTR                  
                                                                                
              WRITE BP13L363-REC  FROM  RPT-HEADER01 AFTER PAGE                 
              WRITE BP13L363-REC  FROM  RPT-HEADER02 AFTER ADVANCING 1          
              WRITE BP13L363-REC  FROM  RPT-HEADER03 AFTER ADVANCING 2          
              WRITE BP13L363-REC  FROM  RPT-HEADER04 AFTER ADVANCING 1          
                                                                                
           END-IF.                                                              
                                                                                
           ADD  1                   TO  WS-SN-CTR                               
                                        WS-LINE-CTR.                            
           MOVE WS-SN-CTR           TO  L363-SN.                                
           MOVE INP-REGN            TO  L363-REGN.                              
           MOVE INP-NRIC            TO  L363-NRIC.                              
                                                                                
           WRITE BP13L363-REC     FROM  RPT-DETAILS.                            
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       5010-WRITE-REPORT-A.                                                     
      *------------------------*                                                
           IF WS-LINE2-CTR > 50 OR WS-SN-A-CTR = ZEROS                          
              ADD   1                          TO  L363A-PAGE                   
              MOVE  FUNCTION CURRENT-DATE(1:8) TO WS-SYSTEM-DATE                
              STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-YY                      
                     DELIMITED BY SIZE                                          
                INTO L363A-DATE                                                 
              END-STRING                                                        
              MOVE  4                          TO  WS-LINE2-CTR                 
                                                                                
              WRITE P13L363A-REC  FROM  RPT-HEADER01A AFTER PAGE                
              WRITE P13L363A-REC  FROM  RPT-HEADER02A AFTER ADVANCING 1         
              WRITE P13L363A-REC  FROM  RPT-HEADER03  AFTER ADVANCING 2         
              WRITE P13L363A-REC  FROM  RPT-HEADER04  AFTER ADVANCING 1         
                                                                                
           END-IF.                                                              
                                                                                
           ADD  1                   TO  WS-SN-A-CTR                             
                                        WS-LINE2-CTR.                           
           MOVE WS-SN-A-CTR         TO  L363-SN.                                
           MOVE INP2-REGN           TO  L363-REGN.                              
           MOVE INP2-NRIC           TO  L363-NRIC.                              
                                                                                
           WRITE P13L363A-REC     FROM  RPT-DETAILS.                            
                                                                                
       5010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       9000-CLOSE-FILES.                                                        
      *------------------------*                                                
           DISPLAY SPACES.                                                      
           DISPLAY '****************  BP13C363  **********************'.        
           DISPLAY ' NO. OF BP13INP RECORDS READ            : '                 
                   WS-INP-READ.                                                 
           DISPLAY ' NO. OF BP13INP2 RECORDS READ           : '                 
                   WS-INP2-READ.                                                
           DISPLAY ' NO. OF RECORDS MATCHED                 : '                 
                   WS-MATCH.                                                    
           DISPLAY ' NO. OF BP13INP RECORDS NOT IN BP13INP2 : '                 
                   WS-NTFND-INP.                                                
           DISPLAY ' NO. OF BP13INP2 RECORDS NOT IN BP13INP : '                 
                   WS-NTFND-INP2.                                               
           DISPLAY ' NO. OF RECORDS FOUND IN BP13K800       : '                 
                   WS-K800-FND-CTR.                                             
           DISPLAY ' NO. OF RECORDS NOT FOUND IN BP13K800   : '                 
                   WS-K800-NOTFND-CTR.                                          
           DISPLAY ' NO. OF RECORDS REWRITTEN IN K800       : '                 
                   WS-K800-REWRTN.                                              
           DISPLAY ' NO. OF RECORDS WRITE TO F800 INELIG    : '                 
                   WS-F800-WRITE.                                               
           DISPLAY ' NO. OF RECORDS BYPASS (PPTY-TYPE)      : '                 
                   WS-BYPASS.                                                   
           DISPLAY ' NO. OF RECORDS NO UPDATE TO Y OR N     : '                 
                   WS-NOUPD-CNT.                                                
           DISPLAY '****************  --------  **********************'.        
                                                                                
           CLOSE BP13INP                                                        
                 BP13INP2                                                       
                 BP13F800                                                       
                 BP13K800                                                       
                 BP13L363                                                       
                 P13L363A.                                                      
                                                                                
           IF WS-K800-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR CLOSING BP13K800, FILE STATUS ('                   
                      WS-K800-STATUS ')'                                        
              MOVE WS-K800-STATUS        TO  RETURN-CODE                        
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
      **********************  END OF PROGRAM LISTING  *****************         
