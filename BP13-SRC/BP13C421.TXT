      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID. BP13C421.                                                    
       AUTHOR. AJITH KUMAR T.                                                   
       DATE-WRITTEN. 15TH JULY 1999.                                            
                                                                                
      * =========================================================== *           
      *              SYSTEM OF COMMITMENT (BP13)                    *           
      * =========================================================== *           
      *  OBJECTIVE :                                                *           
      *    1.   TO KEEP TRACK OF RETURNS FROM IRAS                  *           
      *                                                             *           
      *  FILES :                                                    *           
      *  INPUT  -    BB14F142  (BB14T.F142.RECVIRAS)                *           
      *  I-O    -    BP13K625  (BP13.K625.STAMPFEE)                 *           
      *              BP13K800  (BP13.K800.APPLN)                    *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG-NO   DATE     BY   DESCRIPTION                          *           
      * -------- -------- ---  -----------                          *           
      * BP130696 15071999 AKT  NEW PGM.                             *           
      * BP130696 03091999 LSB  ELECTRONIC STAMPING VIA IDNET TO IRAS*           
      * BP130696 28091999 LSB  ELECTRONIC STAMPING VIA IDNET TO IRAS*           
      * BP130790 02022000 SCK  TO ADD IN IN-SOURCE DOCUMENTATION    *           
      * BP130821 12042000 RASM TO CATER FOR UPDATING OF MULTIPLE    *           
      *                        RECORDS IN BP13K625; STARTBR USING   *           
      *                        K625-ALT-KEY2 (BATCH NO.)            *           
      * BP132734 14092005 JF4  UPDATE K800-DTE-PRINCIPAL-SALES-AGMT *           
      * BP134381 24102011 JB8  TO REMOVE CHECK ON DOC-TYPE          *           
      * BP135136 19112013 JGO1 TO ADD EMAIL REPORTING                           
      * BP135537 23022015 RJB1 CHANGE THE EMAIL RECIPIENT FROM      *           
      *                        NICKNAME INTO COMPLETE NAME          *           
      * BP136260 17082016 KSJ3 INCLUDE WRITE TO BP13K62K            *           
      * BP137087 17112017 PCL4 TO UPDATE EMAIL RECIPIENTS           *           
      * BP137155 08012018 PCL4 TO UPDATE EMAIL RECIPIENTS           *           
      * BP137333 10082018 DN8  TO WRITE THE DUPLICATE RECORDS TO A              
      *                        FLAT FILE AND REWRITE THE BP13K62K               
      * BP137167 03122018 DN8  TO CHECK THE K62K-STATUS '02' WHILE              
      *                        WRITING THE RECORD.                              
      * BP138757 23072021 KV8  TO REPLACE KATHIJAH TO SUZANNE       *           
      * BP139159 28062022 KAC1 REMOVED NORMALA AND AFIQAH           *           
      *=============================================================*           
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
                                                                                
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BB14F142     ASSIGN   TO       BB14F142                       
                  ORGANIZATION     IS  SEQUENTIAL.                              
                                                                                
           SELECT BP13K625     ASSIGN   TO      BP13K625                        
                  ORGANIZATION     IS  INDEXED                                  
                  ACCESS MODE      IS  SEQUENTIAL                               
                  RECORD KEY       IS  K625-KEY-FLD                             
                  ALTERNATE RECORD KEY IS K625-ALT-KEY2                         
                                   WITH DUPLICATES                              
                  FILE STATUS      IS  WS-K625-STATUS.                          
                                                                                
           SELECT BP13K800     ASSIGN   TO      BP13K800                        
                  ORGANIZATION     IS  INDEXED                                  
                  ACCESS MODE      IS  RANDOM                                   
                  RECORD KEY       IS  K800-NUM-REGN                            
                  FILE STATUS      IS  BP13K800-STATUS.                         
                                                                                
           SELECT BP13IRPT  ASSIGN TO BP13IRPT.                                 
                                                                                
           SELECT BP13K62K     ASSIGN   TO      BP13K62K                        
                  ORGANIZATION     IS  INDEXED                                  
                  ACCESS MODE      IS  DYNAMIC                                  
                  RECORD KEY       IS  K62K-KEY-FLD                             
                  FILE STATUS      IS  WS-K62K-STATUS.                          
                                                                                
           SELECT BP13F421     ASSIGN   TO       BP13F421                       
                  ORGANIZATION     IS  SEQUENTIAL.                              
                                                                                
      *---------------*                                                         
       DATA DIVISION.                                                           
      *---------------*                                                         
                                                                                
      *---------------*                                                         
       FILE SECTION.                                                            
      *---------------*                                                         
                                                                                
       FD   BB14F142                                                            
            BLOCK  CONTAINS  0   RECORDS                                        
            RECORD CONTAINS  200 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BB14F142.                                                           
                                                                                
       FD   BP13K625                                                            
            BLOCK  CONTAINS  0   RECORDS                                        
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BP13K625.                                                           
                                                                                
       FD   BP13K800                                                            
            BLOCK  CONTAINS  0   RECORDS                                        
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD  BP13IRPT                   RECORD CONTAINS 130 CHARACTERS            
                                      RECORDING MODE IS F                       
                                      LABEL RECORD IS OMITTED.                  
       01 IRPT-REC                      PIC X(130).                             
                                                                                
       FD   BP13K62K                                                            
            BLOCK  CONTAINS  0   RECORDS                                        
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BP13K62K.                                                           
                                                                                
       FD   BP13F421                                                            
            BLOCK  CONTAINS  0   RECORDS                                        
            RECORD CONTAINS  500 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01 BP13F421-REC                  PIC X(500).                             
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K625-STATUS         PIC X(02).                                
           05  WS-K62K-STATUS         PIC X(02).                                
           05  BP13K800-STATUS        PIC 9(02).                                
                                                                                
       01  WS-EOF-VARIABLES.                                                    
           05  WS-K625-EOF            PIC X VALUE 'N'.                          
           05  WS-F142-EOF            PIC X VALUE 'N'.                          
                                                                                
       01  BP13K800-FND-TAG           PIC X(01) VALUE SPACE.                    
           88  BP13K800-FOUND         VALUE 'Y'.                                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F142-TOT-READ       PIC 9(08) VALUE ZEROES.                   
           05  WS-F142-BAT-READ       PIC 9(08) VALUE ZEROES.                   
           05  WS-F142-DET-READ       PIC 9(08) VALUE ZEROES.                   
           05  WS-K625-WRITE          PIC 9(08) VALUE ZEROES.                   
           05  WS-K62K-WRITE          PIC 9(08) VALUE ZEROES.                   
           05  WS-K62K-REWRITE        PIC 9(08) VALUE ZEROES.                   
           05  WS-F421-WRITE          PIC 9(08) VALUE ZEROES.                   
           05  WS-K625-READ           PIC 9(08) VALUE ZEROES.                   
           05  WS-K625-NOT-FND        PIC 9(08) VALUE ZEROES.                   
           05  WS-K800-UPD-CNT        PIC 9(08) VALUE ZEROES.                   
           05  WS-SERIAL-NUMBER       PIC 9(04) VALUE ZEROES.                   
                                                                                
       01  WS-DATE.                                                             
           05  WS-CCYY                PIC 9(04).                                
           05  WS-MM                  PIC 9(02).                                
           05  WS-DD                  PIC 9(02).                                
                                                                                
       01  WS-DATE1.                                                            
           05  WS-CC1                 PIC X(02).                                
           05  WS-YY1                 PIC X(02).                                
           05  WS-MM1                 PIC X(02).                                
           05  WS-DD1                 PIC X(02).                                
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NUM-BATCH           PIC X(20).                                
           05  WS-DTE-PROCESSED       PIC X(08).                                
                                                                                
      *VARIABLES FOR EMAIL                                                      
       01  WS-HDR-LINES.                                                        
           05 WS-SUBJ                   PIC X(9)                                
              VALUE 'SUBJECT:'.                                                 
           05 WS-SUBJ2                  PIC X(31)                               
              VALUE 'UPDATED IRAS PROCESS DATE      '.                          
                                                                                
       01  MAIL-HDR1.                                                           
           05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB1'.                 
           05  FILLER          PIC X(88)  VALUE SPACES.                         
                                                                                
       01  MAIL-HDR2.                                                           
           05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
           05  MAIL-SENDID     PIC X(04)  VALUE 'OPCP'.                         
           05  FILLER          PIC X(09)  VALUE '@SGPHDB1>'.                    
           05  FILLER          PIC X(76)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL1A.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  RCPT-ID         PIC X(26)  VALUE                                 
                               'BALASUBRAMANIAM_ARUNASALAM'.                    
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
                                                                                
       01  MAIL-DTL1B.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  RCPT-ID         PIC X(34)  VALUE                                 
                               'NOORJAHAN_SHAIK_MOHAMED_GULEMKADER'.            
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
                                                                                
       01  MAIL-DTL1D.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  RCPT-ID PIC X(23)  VALUE 'MUHAMMED_SHARIFF_SAHROM'.              
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
                                                                                
       01  MAIL-DTL1E.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
      *    05  RCPT-ID PIC X(24) VALUE 'KATHIJAH_BEE_ALI_MOHAMED'.              
           05  RCPT-ID PIC X(15) VALUE 'SUZANNE_SC_HENG'.                       
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
                                                                                
       01  MAIL-DTL1G.                                                          
           05  FILLER          PIC X(26)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL2.                                                           
           05  FILLER          PIC X(04)  VALUE 'DATA'.                         
           05  FILLER          PIC X(96)  VALUE SPACES.                         
                                                                                
                                                                                
       01  MAIL-DTL3.                                                           
           05  FILLER          PIC X(05)  VALUE 'FROM:'.                        
           05  MAIL-SENDMAILID PIC X(26)  VALUE                                 
               'SOC - EMAIL ALERT'.                                             
           05  FILLER          PIC X(69)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL4A.                                                          
           05  FILLER          PIC X(04)  VALUE 'TO:<'.                         
           05  RCPT-ID         PIC X(26)  VALUE                                 
                               'BALASUBRAMANIAM_ARUNASALAM'.                    
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
                                                                                
       01  MAIL-DTL4B.                                                          
           05  FILLER          PIC X(04)  VALUE 'TO:<'.                         
           05  RCPT-ID         PIC X(34)  VALUE                                 
                               'NOORJAHAN_SHAIK_MOHAMED_GULEMKADER'.            
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
                                                                                
       01  MAIL-DTL4D.                                                          
           05  FILLER          PIC X(04)  VALUE 'TO:<'.                         
           05  RCPT-ID  PIC X(23)  VALUE 'MUHAMMED_SHARIFF_SAHROM'.             
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
                                                                                
       01  MAIL-DTL4E.                                                          
           05  FILLER          PIC X(04)  VALUE 'TO:<'.                         
      *    05  RCPT-ID PIC X(24) VALUE 'KATHIJAH_BEE_ALI_MOHAMED'.              
           05  RCPT-ID PIC X(15) VALUE 'SUZANNE_SC_HENG'.                       
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
                                                                                
       01  MAIL-DTL4G.                                                          
           05  FILLER          PIC X(21)  VALUE SPACES.                         
                                                                                
       01  MAIL-TRAILER.                                                        
           05  FILLER          PIC X(01)  VALUE '.'.                            
                                                                                
      *VARIABLES FOR REPORT                                                     
       01  WS-RPT-HDR1.                                                         
           05 WS-HDR-HDB-SENT       PIC X(15) VALUE 'DATE HDB SENT:'.           
           05 FILLER                PIC X(03) VALUE SPACES.                     
           05 WS-HDR-DTE-SENT       PIC X(08) VALUE SPACES.                     
                                                                                
       01  WS-RPT-DASH              PIC X(130) VALUE ALL '-'.                   
       01  WS-RPT-HDR2.                                                         
           05 WS-HDR-SER-NUM        PIC X(04) VALUE 'S/N '.                     
           05 FILLER                PIC X(03) VALUE SPACES.                     
           05 WS-HDR-BTCH-NUM       PIC X(20) VALUE 'BATCH NUMBER'.             
           05 FILLER                PIC X(03) VALUE SPACES.                     
                                                                                
       01  WS-RPT-DTL.                                                          
           05 WS-DTL-SER-NUM        PIC ZZZ9.                                   
           05 FILLER                PIC X(03) VALUE SPACES.                     
           05 WS-DTL-BTCH-NUM       PIC X(20) VALUE SPACES.                     
           05 FILLER                PIC X(03) VALUE SPACES.                     
                                                                                
       01  WS-RPT-END.                                                          
           05 FILLER                PIC X(55) VALUE ALL '-'.                    
           05 FILLER                PIC X(20) VALUE                             
              '    END OF REPORT   '.                                           
           05 FILLER                PIC X(55) VALUE ALL '-'.                    
                                                                                
      *-----------------------*                                                 
       PROCEDURE DIVISION.                                                      
      *-----------------------*                                                 
                                                                                
      *================================================================*        
       0000-CONTROL.                                                            
      *================================================================*        
      *---------------------------------                                        
      *   OPENS PROCESSS AND CLOSES FILES                                       
      *----------------------------------                                       
                                                                                
            PERFORM  1000-OPEN-FILES    THRU     1000-EXIT.                     
            PERFORM  2000-READ-BB14F142 THRU     2000-EXIT                      
                     UNTIL WS-F142-EOF = 'Y'.                                   
            PERFORM  6000-SUMMARY       THRU     6000-EXIT.                     
            PERFORM  9000-CLOSE-FILES   THRU     9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       1000-OPEN-FILES.                                                         
      *================================================================*        
      *------------------------------------                                     
      *   OPEN  FILES & CHECKS FILE STATUS                                      
      *------------------------------------                                     
           OPEN                                                                 
                   INPUT       BB14F142                                         
                   I-O         BP13K625                                         
                               BP13K62K                                         
                               BP13K800                                         
                   OUTPUT      BP13IRPT                                         
                               BP13F421.                                        
                                                                                
           IF    WS-K625-STATUS = '00' OR '97'                                  
                 CONTINUE                                                       
           ELSE                                                                 
                 DISPLAY 'ERROR WHILE OPENING BP13K625' WS-K625-STATUS          
                 PERFORM  9000-CLOSE-FILES THRU     9000-EXIT                   
           END-IF.                                                              
                                                                                
           IF    WS-K62K-STATUS = '00' OR '97'                                  
                 CONTINUE                                                       
           ELSE                                                                 
                 DISPLAY 'ERROR WHILE OPENING BP13K62K' WS-K62K-STATUS          
                 PERFORM  9000-CLOSE-FILES THRU     9000-EXIT                   
           END-IF.                                                              
                                                                                
           IF BP13K800-STATUS NOT = ZEROES AND 97                               
              DISPLAY 'BP13K800 FILE OPEN ERROR, STATUS '                       
                                                      BP13K800-STATUS           
              MOVE BP13K800-STATUS         TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE    FUNCTION   CURRENT-DATE   TO     WS-DATE.                    
           MOVE               WS-DATE        TO     WS-DATE1.                   
           MOVE               WS-DATE        TO     WS-HDR-DTE-SENT.            
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       2000-READ-BB14F142.                                                      
      *================================================================*        
      *-------------------*                                                     
      *   READ INPUT FILE *                                                     
      *-------------------*                                                     
            READ  BB14F142 AT END MOVE 'Y' TO    WS-F142-EOF                    
                  GO TO    2000-EXIT.                                           
                                                                                
            ADD   1  TO    WS-F142-TOT-READ.                                    
                                                                                
            IF F142-NUM-REC-TYP OF BB14F142-BATCH = 'BATCH'                     
               DISPLAY 'BATCH REC : ' F142-NUM-BATCH(1:13)                      
               MOVE F142-NUM-BATCH       TO  WS-NUM-BATCH                       
               MOVE F142-NUM-BATCH       TO  WS-DTL-BTCH-NUM                    
               MOVE F142-DTE-BATCH-PROC  TO  WS-DTE-PROCESSED                   
               ADD  1                    TO  WS-F142-BAT-READ                   
               PERFORM 5000-FORMAT-EMAIL THRU 5000-EXIT                         
               GO TO 2000-EXIT                                                  
            END-IF.                                                             
                                                                                
            DISPLAY 'DETAIL REC : ' BB14F142-DETAIL(1:40).                      
            ADD  1   TO  WS-F142-DET-READ.                                      
            PERFORM  3000-UPDATE-BP13K625   THRU 3000-EXIT.                     
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       3000-UPDATE-BP13K625.                                                    
      *================================================================*        
      *---------------------------------                                        
      *   UPDATES BP13K625                                                      
      *----------------------------------                                       
           MOVE WS-NUM-BATCH(1:13)     TO   K625-ALT-KEY2.                      
           MOVE 'N'                    TO   WS-K625-EOF.                        
           PERFORM 3100-START-BP13K625 THRU 3100-EXIT.                          
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       3100-START-BP13K625.                                                     
      *================================================================*        
            START BP13K625 KEY = K625-ALT-KEY2.                                 
                                                                                
            EVALUATE  WS-K625-STATUS                                            
               WHEN '00'                                                        
                     PERFORM 3200-READNEXT-BP13K625 THRU 3200-EXIT              
                             UNTIL WS-K625-EOF = 'Y'                            
               WHEN '10'                                                        
               WHEN '23'                                                        
                     DISPLAY 'BATCH NUMBER NOT FOUND IN BP13K625 '              
                              WS-NUM-BATCH(1:13)                                
                     ADD 1     TO WS-K625-NOT-FND                               
                     GO TO 3100-EXIT                                            
               WHEN OTHER                                                       
                    DISPLAY 'ERROR WHILE STARTING BROWSE-BP13K625 '             
                    MOVE WS-K625-STATUS TO RETURN-CODE                          
                    PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                     
            END-EVALUATE.                                                       
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       3200-READNEXT-BP13K625.                                                  
      *================================================================*        
           READ BP13K625 NEXT AT END MOVE 'Y' TO WS-K625-EOF                    
              GO TO 3200-EXIT.                                                  
                                                                                
           IF K625-ALT-KEY2 NOT = WS-NUM-BATCH(1:13)                            
              MOVE 'Y'  TO WS-K625-EOF                                          
              GO TO 3200-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF (F142-NUM-HDB-REF  NOT = K625-NUM-HDB-REF) OR                     
              (F142-NUM-DOC-TYPE NOT = K625-NUM-DOC)                            
              GO TO 3200-EXIT                                                   
           END-IF.                                                              
                                                                                
           ADD 1 TO WS-K625-READ.                                               
                                                                                
           MOVE WS-DTE-PROCESSED      TO  K625-DTE-IRAS-PROCESSED.              
           MOVE F142-NUM-DOC-REF      TO  K625-NUM-IRAS-DOC-REF.                
                                                                                
           REWRITE BP13K625-REC.                                                
                                                                                
           EVALUATE WS-K625-STATUS                                              
               WHEN '00'                                                        
                     ADD 1 TO WS-K625-WRITE                                     
               WHEN OTHER                                                       
                     DISPLAY 'ERROR WHILE REWRITING TO BP13K625'                
                     MOVE WS-K625-STATUS TO  RETURN-CODE                        
                     PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
           IF K625-NUM-BATCH-TYPE = 'SA'                                        
              MOVE SPACES          TO   BP13K800-MASTER                         
              INITIALIZE                BP13K800-MASTER                         
              MOVE K625-NUM-REGN   TO   K800-NUM-REGN                           
                                                                                
              PERFORM 4000-READ-BP13K800        THRU 4000-EXIT                  
              IF BP13K800-FOUND                                                 
                 DISPLAY 'REGN     = ' K800-NUM-REGN                            
                 DISPLAY 'DTE-RAISE= ' K800-DTE-RAISE-AO                        
                 DISPLAY 'DTE-PRIN = ' K800-DTE-PRINCIPAL-SALES-AGMT            
                 MOVE K800-DTE-RAISE-AO         TO                              
                                          K800-DTE-PRINCIPAL-SALES-AGMT         
                 DISPLAY 'DTE-RAISE= ' K800-DTE-RAISE-AO                        
                 DISPLAY 'DTE-PRIN = ' K800-DTE-PRINCIPAL-SALES-AGMT            
                 PERFORM 4200-REWRITE-BP13K800  THRU 4200-EXIT                  
                 ADD   1                        TO   WS-K800-UPD-CNT            
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF K625-NUM-DOC     = 'ALE' AND                                      
              K625-NUM-DOC-TYP = 'O'   AND                                      
              K625-NUM-DOC-CNT =  1                                             
                                                                                
      *** CR 7333                                                               
                 MOVE K625-KEY-FLD TO K62K-KEY-FLD                              
                 READ BP13K62K KEY IS K62K-KEY-FLD                              
                   EVALUATE WS-K62K-STATUS                                      
                     WHEN '00'                                                  
                      DISPLAY 'DUPLICATE RECORD:' K62K-KEY-FLD                  
                      MOVE  BP13K62K-REC           TO   BP13F421-REC            
                      WRITE BP13F421-REC                                        
                      ADD 1                        TO   WS-F421-WRITE           
                      MOVE    BP13K625-REC         TO   BP13K62K-REC            
                      REWRITE BP13K62K-REC                                      
                      ADD   1                      TO   WS-K62K-REWRITE         
                     WHEN '23'                                                  
                      ADD   1                      TO   WS-K62K-WRITE           
                      MOVE  BP13K625-REC           TO   BP13K62K-REC            
                      WRITE BP13K62K-REC                                        
                       IF WS-K62K-STATUS NOT = '00' AND '02' AND '22'           
                          DISPLAY 'ERROR WHILE WRITING K62K'                    
                                                      K62K-KEY-FLD              
      *                   PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT              
                       END-IF                                                   
                     WHEN OTHER                                                 
                      DISPLAY 'ERROR WHILE READING BP13K62K'                    
                      PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                  
                   END-EVALUATE                                                 
      *** CR 7333                                                               
                                                                                
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       4000-READ-BP13K800.                                                      
      *================================================================*        
           READ BP13K800.                                                       
                                                                                
           EVALUATE BP13K800-STATUS                                             
              WHEN 00                                                           
                 MOVE 'Y'               TO   BP13K800-FND-TAG                   
              WHEN 23                                                           
                 MOVE SPACE             TO   BP13K800-FND-TAG                   
              WHEN OTHER                                                        
                 DISPLAY 'BP13K800 FILE READ ERROR, STATUS '                    
                                                    BP13K800-STATUS             
                 MOVE BP13K800-STATUS   TO   RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES                                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       4200-REWRITE-BP13K800.                                                   
      *================================================================*        
           REWRITE BP13K800-MASTER.                                             
                                                                                
           EVALUATE BP13K800-STATUS                                             
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'BP13K800 FILE REWRITE ERROR, STATUS '                 
                                                    BP13K800-STATUS             
                 MOVE BP13K800-STATUS   TO   RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES                                       
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------                                                
       5000-FORMAT-EMAIL.                                                       
      *-------------------------                                                
                                                                                
            IF WS-SERIAL-NUMBER = 0                                             
               PERFORM 5100-WRITE-HEADER THRU 5100-EXIT                         
            END-IF                                                              
            ADD 1 TO WS-SERIAL-NUMBER.                                          
            MOVE WS-SERIAL-NUMBER TO WS-DTL-SER-NUM.                            
            INITIALIZE IRPT-REC.                                                
            WRITE IRPT-REC FROM  WS-RPT-DTL.                                    
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------                                                
       5100-WRITE-HEADER.                                                       
      *-------------------------                                                
                                                                                
            PERFORM 5200-WRITE-EMAIL-HEADER THRU 5200-EXIT.                     
            INITIALIZE IRPT-REC.                                                
            WRITE IRPT-REC FROM  WS-RPT-HDR1.                                   
            INITIALIZE IRPT-REC.                                                
            WRITE IRPT-REC FROM  WS-RPT-DASH.                                   
            INITIALIZE IRPT-REC.                                                
            WRITE IRPT-REC FROM  WS-RPT-HDR2.                                   
            INITIALIZE IRPT-REC.                                                
            WRITE IRPT-REC FROM  WS-RPT-DASH.                                   
                                                                                
       5100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------                                                
       5200-WRITE-EMAIL-HEADER.                                                 
      *-------------------------                                                
                                                                                
            INITIALIZE IRPT-REC.                                                
            WRITE IRPT-REC    FROM MAIL-HDR1.                                   
            INITIALIZE IRPT-REC.                                                
            WRITE IRPT-REC    FROM MAIL-HDR2.                                   
            INITIALIZE IRPT-REC.                                                
            WRITE IRPT-REC    FROM MAIL-DTL1A.                                  
            INITIALIZE IRPT-REC.                                                
            WRITE IRPT-REC    FROM MAIL-DTL1B.                                  
            INITIALIZE IRPT-REC.                                                
            WRITE IRPT-REC    FROM MAIL-DTL1D.                                  
            INITIALIZE IRPT-REC.                                                
            WRITE IRPT-REC    FROM MAIL-DTL1E.                                  
            INITIALIZE IRPT-REC.                                                
            WRITE IRPT-REC    FROM MAIL-DTL2.                                   
            INITIALIZE IRPT-REC.                                                
            WRITE IRPT-REC    FROM MAIL-DTL3.                                   
            INITIALIZE IRPT-REC.                                                
            WRITE IRPT-REC    FROM MAIL-DTL4A.                                  
            INITIALIZE IRPT-REC.                                                
            WRITE IRPT-REC    FROM MAIL-DTL4B.                                  
            INITIALIZE IRPT-REC.                                                
            WRITE IRPT-REC    FROM MAIL-DTL4D.                                  
            INITIALIZE IRPT-REC.                                                
            WRITE IRPT-REC    FROM MAIL-DTL4E.                                  
            INITIALIZE IRPT-REC.                                                
            WRITE IRPT-REC    FROM WS-HDR-LINES.                                
            INITIALIZE IRPT-REC.                                                
            WRITE IRPT-REC.                                                     
                                                                                
       5200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------                                                
       6000-SUMMARY.                                                            
      *-------------------------                                                
                                                                                
            INITIALIZE IRPT-REC.                                                
            WRITE IRPT-REC    FROM WS-RPT-END.                                  
            INITIALIZE IRPT-REC.                                                
            WRITE IRPT-REC.                                                     
            INITIALIZE IRPT-REC.                                                
            STRING 'TOTAL RECORDS PROCESSED: ' WS-F142-TOT-READ                 
               DELIMITED BY SIZE INTO IRPT-REC.                                 
            WRITE IRPT-REC.                                                     
            INITIALIZE IRPT-REC.                                                
            STRING 'BATCH RECORDS PROCESSED: ' WS-F142-BAT-READ                 
               DELIMITED BY SIZE INTO IRPT-REC.                                 
            WRITE IRPT-REC.                                                     
                                                                                
            INITIALIZE IRPT-REC.                                                
            WRITE IRPT-REC    FROM MAIL-TRAILER.                                
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       9000-CLOSE-FILES.                                                        
      *================================================================*        
           CLOSE     BP13K625                                                   
                     BP13K62K                                                   
                     BP13K800                                                   
                     BB14F142                                                   
                     BP13IRPT                                                   
                     BP13F421.                                                  
                                                                                
           DISPLAY    '**** BP13C421 ****'.                                     
           DISPLAY    'F142 TOT REC READ : '        WS-F142-TOT-READ.           
           DISPLAY    'F142 BAT REC READ : '        WS-F142-BAT-READ.           
           DISPLAY    'F142 DET REC READ : '        WS-F142-DET-READ.           
           DISPLAY    'K625 RECORDS READ : '        WS-K625-READ.               
           DISPLAY    'K625 RECORDS NOT FOUND : '   WS-K625-NOT-FND.            
           DISPLAY    'K625 RECORDS REWRITTEN : '   WS-K625-WRITE.              
           DISPLAY    'K62K RECORDS WRITTEN   : '   WS-K62K-WRITE.              
           DISPLAY    'K62K RECORDS REWRITTEN : '   WS-K62K-REWRITE.            
           DISPLAY    'F421 RECORDS WRITTEN   : '   WS-F421-WRITE.              
           DISPLAY    'K800 RECORDS UPDATED   : '   WS-K800-UPD-CNT.            
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
