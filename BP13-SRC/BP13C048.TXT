       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C048.                                                 
      *AUTHOR.        SK.                                                       
      *DATE-WRITTEN.  14/08/2003.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *                                                             *           
      *  OBJECTIVE   :  MATCHING PROGRAM                            *           
      *                                                             *           
      *  INPUT FILES :  BP13F800                                    *           
      *                 BP13OUT                                     *           
      *  OUT FILE    :  P13F800A                                    *           
      *------------------------------------------------------------ *           
      * CHG REF  DATE     BY    DESCRIPTION                         *           
      *--------  -------- ----- -----------                         *           
      * BP132391 20030814 SK    NEW PGM                             *           
      * BP132558 20040712 SK    TO CATER FOR ANY BALLOT DATE        *           
      * BP132691 20050428 SK    CATER MUTIPLE ATTEMP OF BE/BTO      *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *-------------------------------------------------------------            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F800  ASSIGN TO BP13F800.                                 
           SELECT BP13OUT   ASSIGN TO BP13OUT.                                  
           SELECT P13F800A  ASSIGN TO P13F800A.                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD   BP13F800                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13F800.                                                           
                                                                                
       FD   BP13OUT                                                             
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 100 CHARACTERS.                                     
                                                                                
       01   BP13OUT-REC.                                                        
            05 OUT-CAN-NUM-REGN      PIC X(08).                                 
            05 OUT-CAN-NUM-NRIC      PIC X(09).                                 
            05 OUT-CAN-DTE-BALLOT    PIC X(06).                                 
            05 OUT-CAN-NUM-ALLOCAT   PIC X(03).                                 
            05 OUT-CAN-NUM-FTYPE     PIC X(02).                                 
            05 OUT-CAN-NUMBER        PIC 9(02).                                 
            05 OUT-BTO-NUMBER        PIC 9(02).                                 
            05 OUT-BE-NUMBER         PIC 9(02).                                 
            05 FILLER                PIC X(66).                                 
                                                                                
       FD   P13F800A                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 2000 CHARACTERS.                                    
       01   P13F800A-REC     PIC X(2000).                                       
                                                                                
      *-------------------------------------------------------------            
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-MATCH            PIC 9(8)    VALUE 0.                         
           05  WS-NOTMATCH         PIC 9(8)    VALUE 0.                         
           05  WS-READ-F800        PIC 9(8)    VALUE 0.                         
           05  WS-READ-OUT         PIC 9(8)    VALUE 0.                         
           05  WS-WRITE-F800       PIC 9(8)    VALUE 0.                         
           05  WS-NUM-REGN         PIC X(8)    VALUE SPACES.                    
           05  WS-NRIC-MAX         PIC X(9)    VALUE SPACES.                    
           05  WS-NUM-MAX          PIC 9(2)    VALUE ZEROS.                     
           05  WS-NUM-MAX-BE       PIC 9(2)    VALUE ZEROS.                     
           05  WS-NUM-MAX-BTO      PIC 9(2)    VALUE ZEROS.                     
                                                                                
       01  WS-DATE                 PIC X(8)    VALUE SPACES.                    
                                                                                
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT                            
           PERFORM 2000-READ-F800     THRU 2000-EXIT                            
           PERFORM 2100-READ-OUT      THRU 2100-EXIT                            
                                                                                
           PERFORM 3000-COMPARE       THRU 3000-EXIT                            
                   UNTIL F800-NUM-REGN = HIGH-VALUES                            
                     AND OUT-CAN-NUM-REGN = HIGH-VALUES                         
                                                                                
           PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP13OUT                                                  
                OUTPUT P13F800A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-DATE.                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-F800.                                                          
      *-------------------------------------------------------------            
                                                                                
           READ BP13F800                                                        
              AT END MOVE HIGH-VALUES TO F800-NUM-REGN                          
              GO TO 2000-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-READ-F800.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2100-READ-OUT.                                                           
      *-------------------------------------------------------------            
                                                                                
           READ BP13OUT                                                         
              AT END MOVE HIGH-VALUES TO OUT-CAN-NUM-REGN                       
              GO TO 2100-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-READ-OUT.                                                
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-COMPARE.                                                            
      *-------------------------------------------------------------            
                                                                                
           IF F800-NUM-REGN = OUT-CAN-NUM-REGN                                  
              MOVE  SPACES            TO P13F800A-REC                           
              INITIALIZE                 P13F800A-REC                           
              MOVE  BP13F800-MASTER   TO P13F800A-REC                           
              ADD 1      TO WS-MATCH                                            
              IF OUT-CAN-NUMBER NOT NUMERIC                                     
                 MOVE 0 TO OUT-CAN-NUMBER                                       
              END-IF                                                            
              IF OUT-CAN-NUMBER = 01                                            
                 MOVE OUT-CAN-DTE-BALLOT TO P13F800A-REC(1965:6)                
              END-IF                                                            
              MOVE OUT-CAN-NUMBER      TO WS-NUM-MAX                            
              MOVE OUT-BE-NUMBER       TO WS-NUM-MAX-BE                         
              MOVE OUT-BTO-NUMBER      TO WS-NUM-MAX-BTO                        
              MOVE OUT-CAN-NUM-NRIC    TO WS-NRIC-MAX                           
              PERFORM 3500-READ-NEXT   THRU 3500-EXIT                           
                UNTIL OUT-CAN-NUM-REGN NOT = F800-NUM-REGN                      
                                                                                
              MOVE WS-NRIC-MAX             TO P13F800A-REC(1962:9)              
              MOVE OUT-CAN-NUM-ALLOCAT     TO P13F800A-REC(1973:3)              
              MOVE WS-NUM-MAX              TO P13F800A-REC(1971:2)              
              MOVE WS-NUM-MAX-BE           TO P13F800A-REC(1997:2)              
              MOVE WS-NUM-MAX-BTO          TO P13F800A-REC(1999:2)              
              PERFORM 4000-WRITE-RECORDS   THRU 4000-EXIT                       
              PERFORM 2000-READ-F800       THRU 2000-EXIT                       
           ELSE                                                                 
              IF F800-NUM-REGN > OUT-CAN-NUM-REGN                               
                 PERFORM 2100-READ-OUT       THRU 2100-EXIT                     
              ELSE                                                              
                 ADD 1 TO WS-NOTMATCH                                           
                 MOVE  SPACES           TO P13F800A-REC                         
                 INITIALIZE                P13F800A-REC                         
                 MOVE  BP13F800-MASTER  TO P13F800A-REC                         
                 MOVE F800-NUM-NRIC1    TO P13F800A-REC(1962:9)                 
                 MOVE '00'              TO P13F800A-REC(1971:2)                 
                 MOVE F800-NUM-ALLO-CAT TO P13F800A-REC(1973:3)                 
                 MOVE '0000'            TO P13F800A-REC(1997:4)                 
                 PERFORM 4000-WRITE-RECORDS     THRU 4000-EXIT                  
                 PERFORM 2000-READ-F800         THRU 2000-EXIT                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3500-READ-NEXT.                                                          
      *-------------------------------------------------------------            
                                                                                
           IF OUT-CAN-NUMBER > WS-NUM-MAX                                       
              MOVE OUT-CAN-NUMBER   TO WS-NUM-MAX                               
              MOVE OUT-CAN-NUM-NRIC TO WS-NRIC-MAX                              
           END-IF.                                                              
                                                                                
           IF OUT-BE-NUMBER > WS-NUM-MAX-BE                                     
              MOVE OUT-BE-NUMBER    TO WS-NUM-MAX-BE                            
           END-IF.                                                              
                                                                                
           IF OUT-BTO-NUMBER > WS-NUM-MAX-BTO                                   
              MOVE OUT-BTO-NUMBER    TO WS-NUM-MAX-BTO                          
           END-IF.                                                              
                                                                                
           PERFORM 2100-READ-OUT        THRU 2100-EXIT.                         
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-WRITE-RECORDS.                                                      
      *-------------------------------------------------------------            
                                                                                
           WRITE P13F800A-REC.                                                  
           ADD 1 TO WS-WRITE-F800.                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9999-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '--- BP13C048 CONTROL LIST ---------------'.                 
           DISPLAY '                     RUN DATE : ' WS-DATE.                  
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF REC READ FROM BP13F800  : ' WS-READ-F800.             
           DISPLAY 'NO OF REC READ FROM BP13OUT   : ' WS-READ-OUT.              
           DISPLAY 'NO OF REC WRITTEN TO P13F800A : ' WS-WRITE-F800.            
           DISPLAY 'NO OF MATCH                   : ' WS-MATCH.                 
           DISPLAY 'NO OF NOTMATCH                : ' WS-NOTMATCH.              
           DISPLAY '------------------------------------------'.                
                                                                                
           CLOSE BP13F800                                                       
                 BP13OUT                                                        
                 P13F800A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
