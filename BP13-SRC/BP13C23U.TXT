      *--------------------------------------------------------------*          
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------------*          
       PROGRAM-ID.    BP13C23U.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  FEB 18 2010.                                              
                                                                                
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                   *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  1.  TO EXTRACT BP13F200 CANCELLED AND SHORT-  *        
      *                      LISTED CASES                              *        
      *                                                                *        
      *   INPUT  FILE :  1.  BP13F200  -  BOOKING APPOINTMENT FILE     *        
      *                  2.  BP13K800  -  SOC MASTER          FILE     *        
      *                                                                *        
      *   OUTPUT FILE :  1.  P13F200O  -  BK APPOINTMENT FILE EXTRACT  *        
      *                  2.  P13L23UA  -  LIST OF CANCELLED REGN NO    *        
      *                  3.  P13L23UB  -  LIST OF SHORTLISTED CASES    *        
      *                                                                *        
      *   LISTINGS    :  1.  CONTROL LIST                              *        
      *                                                                *        
      *   CHGE-NO   BY     DATE    DESCRIPTION                         *        
      *   --------  ----  ------   ---------------------------------   *        
      *   BP133813  ESA1  180210   NEW PROGRAM                         *        
      ******************************************************************        
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F200  ASSIGN       TO BP13F200.                           
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS WS-K800-STATUS.                     
           SELECT P13F200O  ASSIGN       TO P13F200O.                           
           SELECT P13L23UA  ASSIGN       TO P13L23UA.                           
           SELECT P13L23UB  ASSIGN       TO P13L23UB.                           
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F200                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13F200.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   P13F200O                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 300 CHARACTERS.                                     
       01  P13F200O-REC     PIC X(300).                                         
                                                                                
       FD   P13L23UA                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   P13L23UA-REC    PIC X(132).                                         
                                                                                
       FD   P13L23UB                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   P13L23UB-REC    PIC X(132).                                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILE-STATUS.                                                         
           05 WS-K800-STATUS            PIC 9(02) VALUE 00.                     
                                                                                
       01  WS-ACCUMULATORS.                                                     
           05 WS-F200-READ              PIC 9(08) VALUE ZEROES.                 
           05 WS-F200-DELETE            PIC 9(08) VALUE ZEROES.                 
           05 WS-K800-READ              PIC 9(08) VALUE ZEROES.                 
           05 WS-F200-WRITE             PIC 9(08) VALUE ZEROES.                 
           05 WS-MAX-LINESA             PIC 9(02) VALUE 60.                     
           05 WS-LINE-COUNTA            PIC 9(02) VALUE 65.                     
           05 WS-MAX-LINESB             PIC 9(02) VALUE 60.                     
           05 WS-LINE-COUNTB            PIC 9(02) VALUE 65.                     
           05 WS-L23UA-PRINTED          PIC 9(08) VALUE ZEROES.                 
           05 WS-L23UB-PRINTED          PIC 9(08) VALUE ZEROES.                 
           05 WS-PAGE-NOA               PIC 9(05) VALUE ZEROES.                 
           05 WS-PAGE-NOB               PIC 9(05) VALUE ZEROES.                 
                                                                                
       01  WS-RUN-DATE                  PIC X(08).                              
                                                                                
       01  WS-SWITCHES.                                                         
           05 WS-F200-EOF-SW            PIC X(01) VALUE SPACES.                 
              88 C-EOF-F200                 VALUE 'Y'.                          
                                                                                
       01  WS-DTE-CANCEL.                                                       
           05 WS-CANCEL-CC              PIC X(02).                              
           05 WS-CANCEL-YY              PIC X(02).                              
           05 WS-CANCEL-MM              PIC X(02).                              
           05 WS-CANCEL-DD              PIC X(02).                              
                                                                                
       01  WS-EDIT-DTE-CANCEL.                                                  
           05 WS-CANCEL-DD              PIC X(02).                              
           05 FILLER                    PIC X(01) VALUE '/'.                    
           05 WS-CANCEL-MM              PIC X(02).                              
           05 FILLER                    PIC X(01) VALUE '/'.                    
           05 WS-CANCEL-CC              PIC X(02).                              
           05 WS-CANCEL-YY              PIC X(02).                              
                                                                                
       01  WS-DATE.                                                             
           05 WS-CCYY                   PIC 9(04).                              
           05 WS-MM                     PIC 9(02).                              
           05 WS-DD                     PIC 9(02).                              
                                                                                
       01  WS-EDIT-DATE.                                                        
           05 WS-EDIT-DD                PIC 9(02).                              
           05 FILLER                    PIC X(01) VALUE '/'.                    
           05 WS-EDIT-MM                PIC 9(02).                              
           05 FILLER                    PIC X(01) VALUE '/'.                    
           05 WS-EDIT-CCYY              PIC 9(04).                              
                                                                                
                                                                                
      ******************************************************************        
      *             REPORT LAYOUT FOR CANCELLED CASES                           
      ******************************************************************        
       01  L23UA-HDG1.                                                          
           05 FILLER                    PIC X(14) VALUE 'P13C23UA'.             
           05 FILLER                    PIC X(08) VALUE 'HDB3'.                 
           05 FILLER                    PIC X(18) VALUE SPACES.                 
           05 FILLER                    PIC X(39) VALUE                         
              'S Y S T E M   O F   C O M M I T M E N T'.                        
           05 FILLER                    PIC X(16) VALUE SPACES.                 
           05 FILLER                    PIC X(07) VALUE 'DATE: '.               
           05 L23UA-DATE                PIC X(10) VALUE SPACES.                 
           05 FILLER                    PIC X(03) VALUE SPACES.                 
           05 FILLER                    PIC X(07) VALUE 'PAGE: '.               
           05 L23UA-PAGE                PIC ZZZZ9.                              
                                                                                
       01  L23UA-HDG2.                                                          
           05 FILLER                    PIC X(45) VALUE SPACES.                 
           05 FILLER                    PIC X(31) VALUE                         
             'LIST OF CANCELLATION CASES'.                                      
           05 FILLER                    PIC X(19) VALUE SPACES.                 
           05 FILLER                    PIC X(34) VALUE                         
             'USER: SALES UNIT (ALLOCATION TEAM)'.                              
                                                                                
       01  L23UA-HDG3.                                                          
           05 FILLER                    PIC X(10) VALUE SPACES.                 
           05 FILLER                    PIC X(06) VALUE 'SER NO'.               
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 FILLER                    PIC X(07) VALUE 'REGN NO'.              
           05 FILLER                    PIC X(03) VALUE SPACES.                 
           05 FILLER                    PIC X(11) VALUE 'CANCEL DATE'.          
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 FILLER                    PIC X(13) VALUE 'CANCEL REASON'.        
                                                                                
       01  L23UA-HDG4.                                                          
           05 FILLER                    PIC X(10) VALUE SPACES.                 
           05 FILLER                    PIC X(06) VALUE '------'.               
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 FILLER                    PIC X(07) VALUE '-------'.              
           05 FILLER                    PIC X(03) VALUE SPACES.                 
           05 FILLER                    PIC X(11) VALUE '-----------'.          
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 FILLER                    PIC X(13) VALUE '-------------'.        
                                                                                
       01  L23UA-DETAIL-REC.                                                    
           05 FILLER                    PIC X(10) VALUE SPACES.                 
           05 L23UA-SER-NO              PIC ZZ9   VALUE ZEROES.                 
           05 FILLER                    PIC X(05) VALUE SPACES.                 
           05 L23UA-NUM-REGN            PIC X(08) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 L23UA-DTE-CANCEL          PIC X(10) VALUE SPACES.                 
           05 FILLER                    PIC X(03) VALUE SPACES.                 
           05 L23UA-NUM-RC              PIC X(02) VALUE SPACES.                 
                                                                                
                                                                                
      ******************************************************************        
      *             REPORT LAYOUT FOR SHORTLISTED CASES                         
      ******************************************************************        
       01  L23UB-HDG1.                                                          
           05 FILLER                    PIC X(14) VALUE 'P13L23UB'.             
           05 FILLER                    PIC X(08) VALUE 'HDB3'.                 
           05 FILLER                    PIC X(18) VALUE SPACES.                 
           05 FILLER                    PIC X(39) VALUE                         
              'S Y S T E M   O F   C O M M I T M E N T'.                        
           05 FILLER                    PIC X(18) VALUE SPACES.                 
           05 FILLER                    PIC X(07) VALUE 'DATE: '.               
           05 L23UB-DATE                PIC X(10) VALUE SPACES.                 
           05 FILLER                    PIC X(01) VALUE SPACES.                 
           05 FILLER                    PIC X(07) VALUE 'PAGE: '.               
           05 L23UB-PAGE                PIC ZZZZ9.                              
                                                                                
       01  L23UB-HDG2.                                                          
           05 FILLER                    PIC X(32) VALUE SPACES.                 
           05 FILLER                    PIC X(51) VALUE                         
           'CASES WITH SCHEME ACCOUNT NUMBER IN SOC MASTER'.                    
           05 FILLER                    PIC X(14) VALUE SPACES.                 
           05 FILLER                    PIC X(34) VALUE                         
             'USER: SALES UNIT (ALLOCATION TEAM)'.                              
                                                                                
       01  L23UB-HDG3.                                                          
           05 FILLER                    PIC X(43) VALUE                         
              'S/N  REGN-NO    ZONE  FLAT-TYPE  BALLOT-QTR'.                    
           05 FILLER                    PIC X(28) VALUE                         
              ' SCHEME ACCOUNT APPLN-STATUS'.                                   
                                                                                
       01  L23UB-HDG4.                                                          
           05 FILLER                    PIC X(71) VALUE ALL '-'.                
                                                                                
       01  L23UB-DETAIL-REC.                                                    
           05 L23UB-SER-NO              PIC ZZ9   VALUE ZEROES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 L23UB-REGNO               PIC X(8)  VALUE SPACES.                 
           05 FILLER                    PIC X(03) VALUE SPACES.                 
           05 L23UB-ZONE                PIC X(2)  VALUE SPACES.                 
           05 FILLER                    PIC X(04) VALUE SPACES.                 
           05 L23UB-FT                  PIC X(2)  VALUE SPACES.                 
           05 FILLER                    PIC X(09) VALUE SPACES.                 
           05 L23UB-BAL1                PIC X(6)  VALUE SPACES.                 
           05 FILLER                    PIC X(05) VALUE SPACES.                 
           05 L23UB-SCH-ACC             PIC X(11) VALUE SPACES.                 
           05 FILLER                    PIC X(04) VALUE SPACES.                 
           05 L23UB-APPL-STAT           PIC X     VALUE SPACES.                 
                                                                                
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
      ***********                                                               
       0000-MAIN.                                                               
      ***********                                                               
                                                                                
            PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                        
            PERFORM 2000-READ-F200       THRU 2000-EXIT.                        
            PERFORM 2500-PROCESS-RECORD  THRU 2500-EXIT                         
                       UNTIL C-EOF-F200.                                        
            PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *******************                                                       
       1000-OPEN-ROUTINE.                                                       
      *******************                                                       
                                                                                
           OPEN INPUT BP13F200                                                  
                      BP13K800                                                  
               OUTPUT P13F200O                                                  
                      P13L23UA                                                  
                      P13L23UB.                                                 
                                                                                
           IF WS-K800-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR OPENING BP13K800 FILE, STATUS '                    
                       WS-K800-STATUS                                           
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           MOVE   WS-DD   TO   WS-EDIT-DD.                                      
           MOVE   WS-MM   TO   WS-EDIT-MM.                                      
           MOVE   WS-CCYY TO   WS-EDIT-CCYY.                                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************                                                          
       2000-READ-F200.                                                          
      ****************                                                          
                                                                                
           READ BP13F200                                                        
              AT END MOVE 'Y'       TO  WS-F200-EOF-SW                          
              GO TO 2000-EXIT                                                   
              END-READ.                                                         
                                                                                
           ADD +1                   TO WS-F200-READ.                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ***********************                                                   
       2500-PROCESS-RECORD.                                                     
      ***********************                                                   
                                                                                
           MOVE F200-NUM-REGN      TO K800-NUM-REGN.                            
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN  00                                                             
                 ADD +1                   TO WS-K800-READ                       
                 IF K800-NUM-STATUS NOT = 'C'                                   
                    IF K800-NUM-SCH-ACC = SPACES OR LOW-VALUES                  
                       WRITE P13F200O-REC  FROM BP13F200-REC                    
                       ADD +1               TO WS-F200-WRITE                    
                    ELSE                                                        
                       PERFORM 4000-WRITE-REPORT THRU 4000-EXIT                 
                    END-IF                                                      
                 ELSE                                                           
                    ADD +1               TO WS-F200-DELETE                      
                    PERFORM 3000-WRITE-REPORT THRU 3000-EXIT                    
                 END-IF                                                         
           WHEN  23                                                             
                 ADD +1                   TO WS-F200-DELETE                     
                 DISPLAY 'REGN NOT FOUND IN K800, REGN # : '                    
                          F200-NUM-REGN                                         
           WHEN  OTHER                                                          
                 DISPLAY 'ERROR READING BP13K800 FILE, STATUS : '               
                          WS-K800-STATUS                                        
           END-EVALUATE.                                                        
                                                                                
           PERFORM 2000-READ-F200     THRU 2000-EXIT.                           
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************                                                        
       3000-WRITE-REPORT.                                                       
      ******************                                                        
                                                                                
           IF ( WS-LINE-COUNTA         < WS-MAX-LINESA        )                 
                CONTINUE                                                        
           ELSE                                                                 
              PERFORM 3500-WRITE-HEADING  THRU 3500-EXIT                        
           END-IF.                                                              
                                                                                
           ADD  1                            TO WS-L23UA-PRINTED.               
           MOVE WS-L23UA-PRINTED             TO L23UA-SER-NO.                   
                                                                                
           MOVE    K800-DTE-CANCEL           TO WS-DTE-CANCEL.                  
           MOVE    CORR WS-DTE-CANCEL        TO WS-EDIT-DTE-CANCEL.             
           MOVE    WS-EDIT-DTE-CANCEL        TO L23UA-DTE-CANCEL.               
           MOVE    K800-NUM-REGN             TO L23UA-NUM-REGN.                 
           MOVE    K800-NUM-CANCEL-REASON    TO L23UA-NUM-RC.                   
                                                                                
           WRITE P13L23UA-REC  FROM L23UA-DETAIL-REC AFTER 1.                   
           ADD   1                           TO WS-LINE-COUNTA.                 
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *********************                                                     
       3500-WRITE-HEADING.                                                      
      *********************                                                     
                                                                                
           MOVE  WS-EDIT-DATE          TO L23UA-DATE.                           
           ADD   1                     TO WS-PAGE-NOA.                          
           MOVE  WS-PAGE-NOA           TO L23UA-PAGE.                           
                                                                                
           WRITE P13L23UA-REC  FROM L23UA-HDG1 AFTER ADVANCING PAGE.            
           WRITE P13L23UA-REC  FROM L23UA-HDG2 AFTER ADVANCING 1.               
           WRITE P13L23UA-REC  FROM L23UA-HDG3 AFTER ADVANCING 2.               
           WRITE P13L23UA-REC  FROM L23UA-HDG4 AFTER ADVANCING 1.               
                                                                                
           MOVE SPACES  TO P13L23UA-REC.                                        
           WRITE P13L23UA-REC.                                                  
           MOVE  5      TO WS-LINE-COUNTA.                                      
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************                                                        
       4000-WRITE-REPORT.                                                       
      ******************                                                        
                                                                                
           IF ( WS-LINE-COUNTB         < WS-MAX-LINESB        )                 
                CONTINUE                                                        
           ELSE                                                                 
              PERFORM 4500-WRITE-HEADING  THRU 4500-EXIT                        
           END-IF.                                                              
                                                                                
           ADD  1                            TO WS-L23UB-PRINTED.               
           MOVE WS-L23UB-PRINTED             TO L23UB-SER-NO.                   
                                                                                
           MOVE    K800-NUM-REGN             TO L23UB-REGNO.                    
           MOVE    K800-NUM-ZONE             TO L23UB-ZONE.                     
           MOVE    K800-NUM-FLAT-TYPE        TO L23UB-FT.                       
           MOVE    K800-DTE-BALLOT           TO L23UB-BAL1.                     
           MOVE    K800-NUM-SCH-ACC          TO L23UB-SCH-ACC.                  
           MOVE    K800-NUM-STATUS           TO L23UB-APPL-STAT.                
                                                                                
           WRITE P13L23UB-REC FROM L23UB-DETAIL-REC AFTER 1.                    
           ADD   1                           TO WS-LINE-COUNTB.                 
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      **********************                                                    
       4500-WRITE-HEADING.                                                      
      **********************                                                    
                                                                                
           MOVE  WS-EDIT-DATE          TO L23UB-DATE.                           
           ADD   1                     TO WS-PAGE-NOB.                          
           MOVE  WS-PAGE-NOB           TO L23UB-PAGE.                           
                                                                                
           WRITE P13L23UB-REC FROM L23UB-HDG1 AFTER ADVANCING PAGE.             
           WRITE P13L23UB-REC FROM L23UB-HDG2 AFTER ADVANCING 1.                
           WRITE P13L23UB-REC FROM L23UB-HDG3 AFTER ADVANCING 2.                
           WRITE P13L23UB-REC FROM L23UB-HDG4 AFTER ADVANCING 1.                
                                                                                
           MOVE SPACES  TO P13L23UB-REC.                                        
           WRITE P13L23UB-REC.                                                  
           MOVE  6      TO WS-LINE-COUNTB.                                      
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ********************                                                      
       9000-CLOSE-ROUTINE.                                                      
      ********************                                                      
                                                                                
           CLOSE BP13F200                                                       
                 BP13K800                                                       
                 P13F200O                                                       
                 P13L23UA                                                       
                 P13L23UB.                                                      
                                                                                
           IF WS-K800-STATUS NOT = ZEROS                                        
              DISPLAY 'ERROR CLOSING BP13K800 FILE, STATUS '                    
                       WS-K800-STATUS                                           
           END-IF.                                                              
                                                                                
           DISPLAY '                                       '.                   
           DISPLAY ' *****  BP13C23U CONTROL TOTALS ****** '.                   
           DISPLAY ' RUN DATE : ' WS-EDIT-DATE                                  
           DISPLAY '                                       '.                   
           DISPLAY ' RECORDS READ    FROM BP13F200  = ' WS-F200-READ.           
           DISPLAY ' RECORDS READ    FROM BP13K800  = ' WS-K800-READ.           
           DISPLAY ' RECORDS DELETED FROM BP13F200  = ' WS-F200-DELETE.         
           DISPLAY ' RECORDS WRITTEN TO   P13F200O  = ' WS-F200-WRITE.          
           DISPLAY ' RECORDS WRITTEN TO   P13L23UA  = ' WS-L23UA-PRINTED.       
           DISPLAY ' RECORDS WRITTEN TO   P13L23UB  = ' WS-L23UB-PRINTED.       
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ***************** E N D  O F  P R O G R A M *****************             
