       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C23I.                                                 
       AUTHOR.        RACHELLE SAN BUENAVENTURA.                                
       DATE-WRITTEN.  18/04/2008.                                               
      * ========================================================= *             
      *            SORT OUT F223 APPMT SCHEDULES FOR              *             
      *          SCHEDULING OF ADDITIONAL QUEUE NUMBERS           *             
      * ========================================================= *             
      *  PURPOSE OF THIS PROGRAM: SORT OUT BP13F223 APPOINTMENT   *             
      *                           SCHEDULES AS SPECIFIED ONLINE,  *             
      *                           FOR ADDING ADDITIONAL QUEUE     *             
      *                           NUMBERS IN BP13K225 (BP13C21M)  *             
      *                                                           *             
      *  INPUT        :  BP13F205                                 *             
      *                  BP13K223                                 *             
      *  I-O          :  BP13F223                                 *             
                                                                  *             
      * ----------------------------------------------------------*             
      * CHG REF  DATE     BY   DESCRIPTION                        *             
      * -------- -------- --- ------------------------------------*             
      * BP133307 18/04/08 RB12 NEW PROGRAM                        *             
      * ==========================================================*             
                                                                                
       ENVIRONMENT    DIVISION.                                                 
       CONFIGURATION  SECTION.                                                  
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT BP13F205  ASSIGN        TO BP13F205.                          
                                                                                
           SELECT BP13K223  ASSIGN        TO BP13K223                           
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K223-KEY-FLD                       
                            ACCESS MODE   IS DYNAMIC                            
                            FILE STATUS   IS K223-FILE-STATUS.                  
                                                                                
           SELECT BP13F223  ASSIGN        TO BP13F223.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F205                                                            
            RECORD CONTAINS 80 CHARACTERS                                       
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F205.                                                           
                                                                                
       FD   BP13F223                                                            
            RECORD CONTAINS 80 CHARACTERS                                       
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   F223-REC                      PIC X(80).                            
                                                                                
       FD   BP13K223                                                            
            RECORD CONTAINS 80 CHARACTERS.                                      
       COPY BP13K223.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-K223-READ-CNT               PIC 9(8) VALUE ZEROES.                
       01  WS-F223-WRITE-CNT              PIC 9(8) VALUE ZEROES.                
       01  K223-FILE-STATUS               PIC 9(2) VALUE 0.                     
       01  WS-DTE-CURRENT.                                                      
           05  WS-DTE-CCYY                PIC 9(4) VALUE ZEROES.                
           05  WS-DTE-MM                  PIC 9(2) VALUE ZEROES.                
           05  WS-DTE-DD                  PIC 9(2) VALUE ZEROES.                
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-K223-EOF-FLAG           PIC X VALUE 'N'.                      
               88 WS-K223-EOF-YES               VALUE 'Y'.                      
               88 WS-K223-EOF-NO                VALUE 'N'.                      
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 1000-PRE-PROCESS         THRU 1000-EXIT.                     
           PERFORM 2000-PROCESS-K223        THRU 2000-EXIT.                     
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-PRE-PROCESS.                                                        
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F205                                                 
                       BP13K223                                                 
                OUTPUT BP13F223.                                                
                                                                                
           IF K223-FILE-STATUS NOT = 00 AND 97                                  
              DISPLAY 'ERROR OPENING BP13K223 : ' K223-FILE-STATUS              
              MOVE     K223-FILE-STATUS    TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT.                      
                                                                                
           READ BP13F205.                                                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-PROCESS-K223.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES             TO  K223-KEY-FLD.                            
                                                                                
           MOVE F205-NUM-NT-ZONE   TO  K223-NUM-SALES-MODE.                     
                                                                                
           MOVE F205-DTE-START     TO  K223-DTE-APPOINTMENT.                    
                                                                                
           START BP13K223 KEY IS GREATER OR EQUAL K223-KEY-FLD                  
              END-START.                                                        
                                                                                
           EVALUATE K223-FILE-STATUS                                            
                                                                                
           WHEN 00                                                              
              PERFORM 2110-READNEXT-K223 THRU 2110-EXIT                         
                UNTIL WS-K223-EOF-YES                                           
                                                                                
           WHEN 10                                                              
              DISPLAY 'K223 END OF FILE'                                        
                                                                                
           WHEN 23                                                              
              DISPLAY 'K223 KEY NOT FOUND IN START OF K223: '                   
                          K223-KEY-FLD                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR STARTING BP13K223'                                 
              DISPLAY 'K223-FILE-STATUS: ' K223-FILE-STATUS                     
                                                                                
           END-EVALUATE.                                                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2110-READNEXT-K223.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K223 NEXT RECORD                                            
             AT END                                                             
                SET WS-K223-EOF-YES TO TRUE                                     
                GO TO 2110-EXIT.                                                
                                                                                
           ADD 1 TO WS-K223-READ-CNT.                                           
                                                                                
           IF K223-NUM-SALES-MODE = F205-NUM-NT-ZONE AND                        
              K223-DTE-APPOINTMENT >= F205-DTE-START                            
              WRITE F223-REC FROM BP13K223-REC                                  
              ADD 1 TO WS-F223-WRITE-CNT                                        
           ELSE                                                                 
              IF K223-NUM-SALES-MODE NOT = F205-NUM-NT-ZONE                     
                SET WS-K223-EOF-YES TO TRUE                                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2110-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '********* BP13C23I ************'.                           
           DISPLAY '                               '.                           
           DISPLAY '***** SORT OUT BP13F223 *******'.                           
           DISPLAY 'SELECTION TYPE PROCESSED    : ' F205-NUM-NT-ZONE.           
           DISPLAY 'RECS READ   FROM BP13K223   : ' WS-K223-READ-CNT.           
           DISPLAY 'RECS WRITTEN  TO BP13F223   : ' WS-F223-WRITE-CNT.          
           DISPLAY '                               '.                           
                                                                                
           CLOSE   BP13F205                                                     
                   BP13F223                                                     
                   BP13K223.                                                    
                                                                                
           IF K223-FILE-STATUS NOT = 00                                         
              DISPLAY 'ERROR CLOSING BP13K223 : ' K223-FILE-STATUS              
              MOVE     K223-FILE-STATUS    TO RETURN-CODE.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
