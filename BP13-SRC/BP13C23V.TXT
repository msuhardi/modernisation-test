      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C23V.                                                 
      *AUTHOR.        MDT2.                                                     
      *DATE-WRITTEN.  31 AUG 2012.                                              
                                                                                
      * ========================================================== *            
      *  SYSTEM OF COMMITMENT (BP13)                               *            
      * ========================================================== *            
      *  OBJECTIVE :                                               *            
      *    1. BATCH PROGRAM TO UPDATE BP13K800 USING THE BP13F800  *            
      *       AND PRINT OUT A REPORT ON RECORDS CANCELLED WITH ALL *            
      *       CASES THAT ARE 2 WEEKS BEFORE THE RUN DATE           *            
      *                                                            *            
      *    INPUT: SY02F001                                         *            
      *           BP13K800                                         *            
      *           BP13F800                                         *            
      *                                                            *            
      *    OUTPUT: P13L23VA                                        *            
      *            P13L23VB                                        *            
      *                                                            *            
      * ========================================================== *            
      * ---------------------------------------------------------- *            
      * CHGE NO  BY   DATE      DESCRIPTION                        *            
      * -------- ---- --------  -----------                        *            
      * BP134202 MDT2 20120831  NEW RELEASE.                       *            
      * BP135498 ESA1 20150930  TO ADD UPDATE IN HISTORY FILES     *            
      * BP137200 FNP1 20180308  ADD WRITE TO BP13F595              *            
      * BP137681 ESA1 20190219  TO CATER FOR ROF                   *            
      * BP137326 FNP1 20181206  BLANK OFF OTHER DETAILS WHEN CREA- *            
      *                         TING 'TE' FOR F915                              
      * BP137918 KV6  20190709  TO CATER FOR REMOVAL OF DTE-DEFER- *            
      *                         EXPIRY & NUM-DEFER-ZONE-FT         *            
      * BP13XXXX EL27 20190816  TO CATER FOR OBF                   *            
      * BP13XXXX EL27 20200709  DEFAULT OBF CANCEL REASON IF NO TAG*            
      **************************************************************            
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
      ************************                                                  
       CONFIGURATION SECTION.                                                   
      ************************                                                  
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
                                                                                
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS K800-STATUS.                               
                                                                                
                                                                                
           SELECT BP13K840 ASSIGN TO BP13K840                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K840-KEY-FLD                               
                  FILE STATUS     IS K840-STATUS.                               
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K820-KEY-FLD                               
                  FILE STATUS     IS K820-STATUS.                               
                                                                                
           SELECT BP13K848 ASSIGN TO BP13K848                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K848-KEY-FLD                               
                  FILE STATUS     IS K848-STATUS.                               
                                                                                
           SELECT BP13K810 ASSIGN TO BP13K810                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K810-KEY-FLD                               
                  FILE STATUS     IS K810-STATUS.                               
                                                                                
           SELECT P13L23VA ASSIGN TO P13L23VA.                                  
           SELECT P13L23VB ASSIGN TO P13L23VB.                                  
           SELECT BP13F313 ASSIGN TO BP13F313.                                  
           SELECT BP13F915 ASSIGN TO BP13F915.                                  
                                                                                
                                                                                
      ****************                                                          
       DATA DIVISION.                                                           
      ****************                                                          
                                                                                
      ****************                                                          
       FILE SECTION.                                                            
      ****************                                                          
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13F800                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K840                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K840.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K848                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K848.                                                           
                                                                                
       FD   BP13K810                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K810.                                                           
                                                                                
       FD   P13L23VA                                                            
            RECORDING MODE    IS  F                                             
            RECORD CONTAINS  132  CHARACTERS.                                   
       01   P13L23VA-REC     PIC X(132).                                        
                                                                                
       FD   P13L23VB                                                            
            RECORDING MODE    IS  F                                             
            RECORD CONTAINS  132  CHARACTERS.                                   
       01   P13L23VB-REC     PIC X(132).                                        
                                                                                
       FD   BP13F313                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       COPY BP13K313.                                                           
                                                                                
       FD   BP13F915                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 800 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       COPY BP13F915.                                                           
                                                                                
      ************************                                                  
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  WS-K800-NOT-FOUND       PIC X     VALUE SPACE.                       
       01  WS-K820-FOUND           PIC X     VALUE SPACE.                       
       01  WS-TOT-F800-READ        PIC 9(08) VALUE ZEROES.                      
       01  WS-TOT-K800-READ        PIC 9(08) VALUE ZEROES.                      
       01  WS-TOT-K800-REWRITE     PIC 9(08) VALUE ZEROES.                      
       01  WS-CNT-K840-UPD         PIC 9(8)  VALUE ZEROES.                      
       01  WS-CNT-K893-UPD         PIC 9(8)  VALUE ZEROES.                      
       01  WS-CNT-K840-WRITE       PIC 9(8)  VALUE ZEROES.                      
       01  WS-CNT-K848-WRITE       PIC 9(8)  VALUE ZEROES.                      
       01  WS-CNT-K810-WRITE       PIC 9(8)  VALUE ZEROES.                      
       01  WS-CNT-F313-WRITE       PIC 9(8)  VALUE ZEROES.                      
       01  WS-CNT-F915-WRITE       PIC 9(8)  VALUE ZEROES.                      
       01  WS-F800-EOF             PIC X     VALUE SPACE.                       
       01  WS-ONE-WEEK-SW          PIC X     VALUE SPACE.                       
       01  WS-NAME                 PIC X(66) VALUE SPACES.                      
       01  WS-NOUPDATE             PIC X     VALUE SPACE.                       
       01  WS-UPDATE-BP13K800-SW   PIC X VALUE SPACE.                           
       01  WS-NRIC                 PIC X(9)  VALUE SPACES.                      
       01 WS-NUM-REGN              PIC X(8)  VALUE SPACES.                      
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-K820-EOF               PIC X(01)   VALUE 'N'.                 
           05  WS-K820-NTFND             PIC X(01)   VALUE 'N'.                 
           05  WS-REC-PROCESS            PIC X(01)   VALUE 'N'.                 
           05  WS-K820-FND               PIC 9(06)  VALUE ZEROES.               
           05  WS-K820-NOTFND            PIC 9(06)  VALUE ZEROES.               
                                                                                
       01  WS-DATE.                                                             
           05  WS-CCYY             PIC 9(4)  VALUE 0000.                        
           05  WS-MM               PIC 99    VALUE 00.                          
           05  WS-DD               PIC 99    VALUE 00.                          
       01  WS-CURRENT-DATE         PIC X(08) VALUE SPACES.                      
                                                                                
       01  WS-SYSTEM-TIME.                                                      
           05 WS-HH                       PIC 9(2).                             
           05 WS-MM                       PIC 9(2).                             
           05 WS-SS                       PIC 9(2).                             
           05 WS-MI                       PIC 9(2).                             
                                                                                
       01  WS-INT-DATE             PIC 9(07).                                   
       01  WS-RANGE-DATE           PIC 9(08).                                   
       01  WS-CURRENT-WEEK         PIC 9(08).                                   
       01  WS-WEEK-BEFORE          PIC 9(08).                                   
       01  WS-CTR-DAYS             PIC 9(02).                                   
                                                                                
       01  WS-F800-DTE             PIC X(08).                                   
       01  WS-F800-DTE1  REDEFINES WS-F800-DTE.                                 
           05 WS-DTE1-CC           PIC 9(02).                                   
           05 WS-DTE1-YY           PIC 9(02).                                   
           05 WS-DTE1-MM           PIC 9(02).                                   
           05 WS-DTE1-DD           PIC 9(02).                                   
                                                                                
       01  K800-STATUS             PIC 9(2)  VALUE ZEROES.                      
       01  K840-STATUS             PIC 9(2)  VALUE ZEROES.                      
       01  K820-STATUS             PIC 9(2)  VALUE ZEROES.                      
       01  K848-STATUS             PIC 9(2)  VALUE ZEROES.                      
       01  K810-STATUS             PIC 9(2)  VALUE ZEROES.                      
                                                                                
       01  WS-FILE-STATUS.                                                      
           05 WS-FILE-STATUS-OK       PIC 9(02) VALUE 00.                       
           05 WS-FILE-STATUS-OPEN     PIC 9(02) VALUE 97.                       
           05 WS-FILE-STATUS-NOTFND   PIC 9(02) VALUE 23.                       
           05 WS-FILE-STATUS-EOF      PIC 9(02) VALUE 10.                       
                                                                                
       01  WS-LINE-CNT             PIC 9(2)  VALUE 60.                          
       01  WS-PAGE-CNT             PIC 9(5)  VALUE ZEROES.                      
       01  WS-SNO                  PIC 9(6)  VALUE ZEROES.                      
       01  WS-LINE-CNT1            PIC 9(2)  VALUE 60.                          
       01  WS-PAGE-CNT1            PIC 9(5)  VALUE ZEROES.                      
       01  WS-SNO1                 PIC 9(6)  VALUE ZEROES.                      
                                                                                
      *-------------------------------------------------------------            
      * EXCEPTION LAYOUT                                                        
      *-------------------------------------------------------------            
       01  L23X-HEADER-1.                                                       
           05 FILLER                     PIC X(08)  VALUE 'P13L23VB'.           
           05 FILLER                     PIC X(44)  VALUE SPACES.               
           05 FILLER                     PIC X(24)  VALUE                       
              'HDB SYSTEM OF COMMITMENT'.                                       
           05 FILLER                     PIC X(26)  VALUE SPACES.               
           05 FILLER                     PIC X(6)   VALUE 'DATE: '.             
           05 L23X-DATE                  PIC X(10)  VALUE SPACES.               
           05 FILLER                     PIC XX     VALUE SPACES.               
           05 FILLER                     PIC X(6)   VALUE 'PAGE: '.             
           05 L23X-PAGE                  PIC Z(04)9.                            
                                                                                
       01  L23X-HEADER-2.                                                       
           05 FILLER                     PIC X(56)  VALUE SPACES.               
           05 FILLER                     PIC X(16)  VALUE                       
              'EXCEPTION REPORT'.                                               
           05 FILLER                     PIC X(60)  VALUE SPACES.               
                                                                                
       01  L23X-HEADER-3.                                                       
           05 FILLER                     PIC X(31)  VALUE                       
               '  S/NO   REGN         ALLOC TAG'.                               
           05 FILLER                     PIC X(101) VALUE SPACES.               
                                                                                
       01  L23X-HEADER-4.                                                       
           05 FILLER                     PIC X(132) VALUE SPACES.               
                                                                                
       01  L23X-DETAIL-LINES.                                                   
           02  L23X-DETAIL.                                                     
               05 L23X-SNO               PIC Z(05)9.                            
               05 FILLER                 PIC X(03)  VALUE SPACES.               
               05 L23X-NUM-REGN          PIC X(08)  VALUE SPACES.               
               05 FILLER                 PIC X(05)  VALUE SPACES.               
               05 L23X-NUM-ALLOC-TAG     PIC X(02)  VALUE SPACES.               
               05 FILLER                 PIC X(109) VALUE SPACES.               
                                                                                
      *-------------------------------------------------------------            
      * REPORT LAYOUT                                                           
      *-------------------------------------------------------------            
       01  L23V-HEADER-1.                                                       
           05 FILLER                     PIC X(08)  VALUE 'P13L23VA'.           
           05 FILLER                     PIC X(44)  VALUE SPACES.               
           05 FILLER                     PIC X(24)  VALUE                       
              'HDB SYSTEM OF COMMITMENT'.                                       
           05 FILLER                     PIC X(26)  VALUE SPACES.               
           05 FILLER                     PIC X(6)   VALUE 'DATE: '.             
           05 L23V-DATE                  PIC X(10)  VALUE SPACES.               
           05 FILLER                     PIC X(02)  VALUE SPACES.               
           05 FILLER                     PIC X(6)   VALUE 'PAGE: '.             
           05 L23V-PAGE                  PIC Z(04)9.                            
                                                                                
       01  L23V-HEADER-2.                                                       
           05 FILLER                     PIC X(45)  VALUE SPACES.               
           05 FILLER                     PIC X(41)  VALUE                       
              'CONTROL LISTING OF APPLICATIONS CANCELLED'.                      
           05 FILLER                     PIC X(46)  VALUE SPACES.               
                                                                                
       01  L23V-HEADER-3.                                                       
           05 FILLER                     PIC X(29)  VALUE                       
               '  S/NO   REGN       NRIC1    '.                                 
           05 FILLER                     PIC X(03)  VALUE SPACES.               
           05 FILLER                     PIC X(09)  VALUE 'NRIC2'.              
           05 FILLER                     PIC X(03)  VALUE SPACES.               
           05 FILLER                     PIC X(16)  VALUE                       
               'BOOKING APP DATE'.                                              
           05 FILLER                     PIC X(03)  VALUE SPACES.               
           05 FILLER                     PIC X(11)  VALUE                       
               'BALLOT DATE'.                                                   
           05 FILLER                     PIC X(03)  VALUE SPACES.               
           05 FILLER                     PIC X(09)  VALUE                       
               'ALLOC TAG'.                                                     
           05 FILLER                     PIC X(05)  VALUE SPACES.               
           05 FILLER                     PIC X(13)  VALUE                       
               'CANCEL REASON'.                                                 
                                                                                
       01  L23V-HEADER-4.                                                       
           05 FILLER                     PIC X(132) VALUE SPACES.               
                                                                                
       01  L23V-DETAIL-LINES.                                                   
           02  L23V-DETAIL-1.                                                   
               05 L23V-SNO               PIC Z(05)9.                            
               05 FILLER                 PIC X(03)  VALUE SPACES.               
               05 L23V-NUM-REGN          PIC X(08)  VALUE SPACES.               
               05 FILLER                 PIC X(03)  VALUE SPACES.               
               05 L23V-NUM-NRIC1         PIC X(09)  VALUE SPACES.               
               05 FILLER                 PIC X(03)  VALUE SPACES.               
               05 L23V-NUM-NRIC2         PIC X(09)  VALUE SPACES.               
               05 FILLER                 PIC X(03)  VALUE SPACES.               
               05 L23V-DTE-BK-APPT       PIC X(08)  VALUE SPACES.               
               05 FILLER                 PIC X(11)  VALUE SPACES.               
               05 L23V-DTE-BALLOT        PIC X(06)  VALUE SPACES.               
               05 FILLER                 PIC X(08)  VALUE SPACES.               
               05 L23V-NUM-ALLOC-TAG     PIC X(02)  VALUE SPACES.               
               05 FILLER                 PIC X(12)  VALUE SPACES.               
               05 L23V-NUM-CANCEL-RSN    PIC X(02)  VALUE SPACES.               
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *--------*                                                                
       0000-MAIN.                                                               
      *--------*                                                                
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-SY02F001       THRU 2000-EXIT.                     
           PERFORM 2100-READ-BP13F800       THRU 2100-EXIT.                     
           PERFORM 3000-PROCESS-REC         THRU 3000-EXIT                      
                   UNTIL WS-F800-EOF = 'Y'.                                     
                                                                                
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------*                                                          
       1000-OPEN-FILES.                                                         
      *--------------*                                                          
                                                                                
           OPEN INPUT  SY02F001                                                 
                       BP13F800                                                 
                I-O    BP13K800                                                 
                       BP13K840                                                 
                       BP13K820                                                 
                       BP13K848                                                 
                       BP13K810                                                 
                OUTPUT P13L23VA                                                 
                       P13L23VB                                                 
                       BP13F915                                                 
                       BP13F313.                                                
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING - BP13K800 : ' K800-STATUS                 
              MOVE K800-STATUS              TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU  9000-EXIT                     
           END-IF.                                                              
                                                                                
           IF K840-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING - BP13K840 : ' K840-STATUS                 
              MOVE K840-STATUS              TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU  9000-EXIT                     
           END-IF.                                                              
                                                                                
           IF K820-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING - BP13K820 : ' K820-STATUS                 
              MOVE K820-STATUS              TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU  9000-EXIT                     
           END-IF.                                                              
                                                                                
           IF K848-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING - BP13K848 : ' K848-STATUS                 
              MOVE K848-STATUS              TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU  9000-EXIT                     
           END-IF.                                                              
                                                                                
           IF K810-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING - BP13K810 : ' K810-STATUS                 
              MOVE K810-STATUS              TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU  9000-EXIT                     
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       2000-READ-SY02F001.                                                      
      *--------------------------------------------------------------*          
                                                                                
           READ SY02F001 AT END                                                 
                DISPLAY '*** EMPTY SY02F001 FILE ***'                           
                MOVE 88                       TO RETURN-CODE                    
                PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT.                   
                                                                                
           MOVE F001-DTE-CURRENT              TO WS-CURRENT-DATE.               
           STRING  WS-CURRENT-DATE(7:2) '/'                                     
                   WS-CURRENT-DATE(5:2) '/'                                     
                   WS-CURRENT-DATE(1:4)                                         
                   DELIMITED BY SIZE          INTO L23V-DATE.                   
           MOVE    L23V-DATE                  TO L23X-DATE.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       2100-READ-BP13F800.                                                      
      *-----------------*                                                       
                                                                                
           READ BP13F800 AT END                                                 
                MOVE 'Y'                      TO WS-F800-EOF.                   
                                                                                
           IF WS-F800-EOF NOT = 'Y'                                             
              ADD 1                           TO WS-TOT-F800-READ               
           ELSE                                                                 
              IF WS-TOT-F800-READ = 0                                           
                 DISPLAY '*** EMPTY BP13F800 FILE ***'                          
                 PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           IF  (F800-NUM-STATUS  = 'C')                      OR                 
              ((F800-NUM-ALLOC-TAG = 'NE')     AND                              
               (F800-DTE-BK-APPT = SPACES))                  OR                 
              ((F800-NUM-ALLO-CAT NOT = 'BTO') AND                              
               (F800-NUM-ALLO-CAT NOT = 'ROF') AND                              
               (F800-NUM-ALLO-CAT NOT = 'OBF') AND                              
               (F800-NUM-ALLO-CAT NOT = 'SBF')               OR                 
              ((F800-NUM-SCH-ACC NOT = SPACES) AND                              
               (F800-NUM-SCH-ACC NOT = LOW-VALUES))          OR                 
               (F800-DTE-BK-APPT = SPACES OR LOW-VALUES))                       
               CONTINUE                                                         
           ELSE                                                                 
              PERFORM 3100-VALIDATE-RECORDS THRU 3100-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 2100-READ-BP13F800       THRU 2100-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3100-VALIDATE-RECORDS.                                                   
      *-----------------*                                                       
           MOVE 'N'                         TO WS-ONE-WEEK-SW.                  
           PERFORM 3200-FILTER-DATE         THRU 3200-EXIT                      
           IF WS-ONE-WEEK-SW   = 'Y'                                            
              PERFORM 3400-READ-BP13K800    THRU 3400-EXIT                      
              IF WS-UPDATE-BP13K800-SW = 'Y'                                    
                 PERFORM 4200-MOVE-RPRT-DTLS THRU 4200-EXIT                     
                 PERFORM 4300-PRINT-LISTING THRU 4300-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3200-FILTER-DATE.                                                        
      *-----------------*                                                       
                                                                                
           MOVE F001-DTE-CURRENT            TO WS-RANGE-DATE.                   
           MOVE 14                          TO WS-CTR-DAYS.                     
           PERFORM 3300-COMPUTE-DATE        THRU 3300-EXIT.                     
           MOVE WS-RANGE-DATE               TO WS-CURRENT-WEEK.                 
                                                                                
           MOVE 21                          TO WS-CTR-DAYS.                     
           PERFORM 3300-COMPUTE-DATE        THRU 3300-EXIT.                     
           MOVE WS-RANGE-DATE               TO WS-WEEK-BEFORE.                  
                                                                                
           MOVE F800-DTE-BK-APPT            TO WS-F800-DTE.                     
           IF WS-F800-DTE1 >= WS-WEEK-BEFORE AND                                
              WS-F800-DTE1 <  WS-CURRENT-WEEK                                   
              MOVE  'Y'                     TO WS-ONE-WEEK-SW                   
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3300-COMPUTE-DATE.                                                       
      *-----------------*                                                       
                                                                                
           COMPUTE WS-INT-DATE   =                                              
              FUNCTION INTEGER-OF-DATE(WS-RANGE-DATE).                          
                                                                                
           COMPUTE WS-INT-DATE   = WS-INT-DATE - WS-CTR-DAYS.                   
                                                                                
           COMPUTE WS-RANGE-DATE =                                              
              FUNCTION DATE-OF-INTEGER(WS-INT-DATE).                            
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3400-READ-BP13K800.                                                      
      *-----------------*                                                       
                                                                                
           MOVE SPACES                        TO BP13K800-MASTER.               
           INITIALIZE                            BP13K800-MASTER.               
           MOVE SPACES                        TO WS-UPDATE-BP13K800-SW.         
                                                                                
           MOVE F800-NUM-REGN                 TO K800-NUM-REGN.                 
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE K800-STATUS                                                 
                                                                                
           WHEN WS-FILE-STATUS-OK                                               
                ADD   1                         TO WS-TOT-K800-READ             
                PERFORM 3500-VALDTE-ALLOC-TAG   THRU 3500-EXIT                  
                IF WS-UPDATE-BP13K800-SW = 'Y'                                  
                   PERFORM 5000-WRITE-K800-IMAGE THRU 5000-EXIT                 
                   PERFORM 5100-WRITE-K810       THRU 5100-EXIT                 
                   PERFORM 3600-MOVE-BP13K800   THRU 3600-EXIT                  
                   PERFORM 3700-REWRTE-BP13K800 THRU 3700-EXIT                  
                END-IF                                                          
                                                                                
           WHEN WS-FILE-STATUS-NOTFND                                           
           WHEN WS-FILE-STATUS-EOF                                              
                DISPLAY 'RECORD NOT FOUND IN BP13K800'                          
                DISPLAY 'K800-NUM-REGN                 = ' K800-NUM-REGN        
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING  BP13K800, STAT = ' K800-STATUS          
                DISPLAY 'K800-NUM-REGN                 = ' K800-NUM-REGN        
                MOVE     K800-STATUS             TO  RETURN-CODE                
                PERFORM  9000-CLOSE-ROUTINE      THRU 9000-EXIT                 
                                                                                
           END-EVALUATE.                                                        
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3500-VALDTE-ALLOC-TAG.                                                   
      *---------------------*                                                   
                                                                                
           MOVE 'Y'                         TO WS-UPDATE-BP13K800-SW.           
           EVALUATE K800-NUM-ALLOC-TAG                                          
              WHEN 'NF'                                                         
                 MOVE 'G1'                  TO K800-NUM-CANCEL-REASON           
              WHEN 'NC'                                                         
                 MOVE 'G2'                  TO K800-NUM-CANCEL-REASON           
              WHEN 'NI'                                                         
                 MOVE 'G3'                  TO K800-NUM-CANCEL-REASON           
              WHEN 'FN'                                                         
                 MOVE '27'                  TO K800-NUM-CANCEL-REASON           
              WHEN 'SF'                                                         
                 MOVE 'G4'                  TO K800-NUM-CANCEL-REASON           
              WHEN 'SC'                                                         
                 MOVE 'G5'                  TO K800-NUM-CANCEL-REASON           
              WHEN 'SI'                                                         
                 MOVE 'G6'                  TO K800-NUM-CANCEL-REASON           
              WHEN OTHER                                                        
                 IF K800-NUM-ALLO-CAT = 'OBF'                                   
                    MOVE 'G3'               TO K800-NUM-CANCEL-REASON           
                 ELSE                                                           
                    MOVE 'N'                TO WS-UPDATE-BP13K800-SW            
                    PERFORM 4000-PRNT-EXCEPTION THRU 4000-EXIT                  
                 END-IF                                                         
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3600-MOVE-BP13K800.                                                      
      *---------------------*                                                   
                                                                                
           MOVE SPACES                           TO K800-NUM-HALF-IDP.          
           MOVE 'C'                              TO K800-NUM-STATUS.            
           MOVE WS-CURRENT-DATE                  TO K800-DTE-CANCEL.            
           MOVE WS-CURRENT-DATE                  TO K800-DTE-UPDATE.            
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3700-REWRTE-BP13K800.                                                    
      *---------------------*                                                   
                                                                                
           REWRITE  BP13K800-MASTER                                             
           EVALUATE K800-STATUS                                                 
                                                                                
           WHEN WS-FILE-STATUS-OK                                               
                ADD   1                          TO WS-TOT-K800-REWRITE         
                MOVE SPACES            TO  BP13K313-REC                         
                MOVE '04'              TO  K313-NUM-TRAN-TYPE                   
                MOVE F800-NUM-REGN     TO  K313-NUM-REGN                        
                WRITE BP13K313-REC                                              
                ADD   1 TO WS-CNT-F313-WRITE                                    
                PERFORM 6000-PROCESS-CDB  THRU 6000-EXIT                        
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR UPDATING BP13K800, STAT = ' K800-STATUS          
                DISPLAY 'K800-NUM-REGN                 = ' K800-NUM-REGN        
                MOVE     K800-STATUS             TO  RETURN-CODE                
                PERFORM  9000-CLOSE-ROUTINE      THRU 9000-EXIT                 
                                                                                
           END-EVALUATE.                                                        
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       4000-PRNT-EXCEPTION.                                                     
      *-----------------*                                                       
                                                                                
           IF WS-LINE-CNT1 > 55                                                 
              PERFORM 4100-PRINT-HEADERX     THRU 4100-EXIT                     
           END-IF.                                                              
                                                                                
           ADD 1 TO WS-SNO1.                                                    
           MOVE WS-SNO1                      TO L23X-SNO.                       
                                                                                
           MOVE K800-NUM-REGN                TO L23X-NUM-REGN.                  
           MOVE K800-NUM-ALLOC-TAG           TO L23X-NUM-ALLOC-TAG.             
                                                                                
           WRITE P13L23VB-REC  FROM L23X-DETAIL AFTER 2.                        
           ADD   2                            TO WS-LINE-CNT1.                  
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       4100-PRINT-HEADERX.                                                      
      *-----------------*                                                       
                                                                                
           ADD   1                          TO WS-PAGE-CNT1.                    
           MOVE  WS-PAGE-CNT1               TO L23X-PAGE.                       
                                                                                
           WRITE P13L23VB-REC            FROM L23X-HEADER-1 AFTER PAGE.         
           WRITE P13L23VB-REC            FROM L23X-HEADER-2.                    
           WRITE P13L23VB-REC            FROM L23X-HEADER-3 AFTER 2.            
           WRITE P13L23VB-REC            FROM L23X-HEADER-4.                    
                                                                                
           MOVE  5                       TO WS-LINE-CNT1.                       
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4200-MOVE-RPRT-DTLS.                                                     
      *-------------------*                                                     
                                                                                
           MOVE K800-NUM-REGN                    TO L23V-NUM-REGN.              
           MOVE K800-NUM-NRIC1                   TO L23V-NUM-NRIC1.             
           MOVE K800-NUM-NRIC2                   TO L23V-NUM-NRIC2.             
           MOVE K800-DTE-BALLOT                  TO L23V-DTE-BALLOT.            
           MOVE K800-DTE-BK-APPT                 TO L23V-DTE-BK-APPT.           
           MOVE K800-NUM-ALLOC-TAG               TO L23V-NUM-ALLOC-TAG.         
           MOVE K800-NUM-CANCEL-REASON           TO L23V-NUM-CANCEL-RSN.        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       4300-PRINT-LISTING.                                                      
      *-----------------*                                                       
                                                                                
           IF WS-LINE-CNT  > 55                                                 
              PERFORM 4400-PRINT-HEADER     THRU 4400-EXIT                      
           END-IF.                                                              
                                                                                
           ADD   1                          TO WS-SNO.                          
           MOVE  WS-SNO                     TO L23V-SNO.                        
                                                                                
           WRITE P13L23VA-REC  FROM L23V-DETAIL-1 AFTER 2.                      
           ADD   2                          TO WS-LINE-CNT.                     
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       4400-PRINT-HEADER.                                                       
      *-----------------*                                                       
                                                                                
           ADD   1                  TO WS-PAGE-CNT.                             
           MOVE  WS-PAGE-CNT        TO L23V-PAGE.                               
                                                                                
           WRITE P13L23VA-REC       FROM L23V-HEADER-1 AFTER PAGE.              
           WRITE P13L23VA-REC       FROM L23V-HEADER-2.                         
           WRITE P13L23VA-REC       FROM L23V-HEADER-3 AFTER 2.                 
           WRITE P13L23VA-REC       FROM L23V-HEADER-4.                         
                                                                                
           MOVE  5                  TO WS-LINE-CNT.                             
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       5000-WRITE-K800-IMAGE.                                                   
      *--------------------------------------------------------------*          
                                                                                
           MOVE BP13K800-MASTER TO BP13K848-MASTER.                             
           MOVE 'C23V'          TO K848-NUM-TRANS-BKIMAGE.                      
                                                                                
           MOVE F800-NUM-USERID TO K848-NUM-USERID.                             
           MOVE F001-DTE-CURRENT TO K848-DTE-BKIMAGE.                           
                                                                                
           MOVE FUNCTION CURRENT-DATE(9:8) TO WS-SYSTEM-TIME.                   
                                                                                
           MOVE WS-SYSTEM-TIME  TO K848-TME-BKIMAGE.                            
           MOVE K848-NUM-REGN   TO K848-NUM-REGN-BKIMAGE.                       
                                                                                
           WRITE BP13K848-MASTER.                                               
                                                                                
           EVALUATE K848-STATUS                                                 
               WHEN 00                                                          
                    ADD 1 TO WS-CNT-K848-WRITE                                  
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR WRITING BP13K848. STATUS IS '                
                            K848-STATUS                                         
                    PERFORM  9000-CLOSE-ROUTINE      THRU 9000-EXIT             
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------*          
       5100-WRITE-K810.                                                         
      *--------------------------------------------------------------*          
                                                                                
           MOVE SPACES                 TO BP13K810-MASTER.                      
           INITIALIZE                     BP13K810-MASTER.                      
                                                                                
           MOVE K800-NUM-REGN          TO K810-NUM-REGN                         
                                          WS-NUM-REGN.                          
           MOVE F001-DTE-CURRENT       TO K810-DTE-UPDATE.                      
           MOVE FUNCTION CURRENT-DATE(9:8) TO WS-SYSTEM-TIME.                   
                                                                                
           MOVE WS-SYSTEM-TIME(1:7)    TO K810-TME-UPDATE.                      
           MOVE SPACES                 TO K810-NUM-REF.                         
           MOVE 'C'                    TO K810-NUM-STATUS                       
           MOVE K800-NUM-NRIC1         TO K810-NUM-NRIC1.                       
           MOVE K800-NUM-NRIC2         TO K810-NUM-NRIC2.                       
           MOVE K800-NUM-NRIC3         TO K810-NUM-NRIC3.                       
           MOVE K800-NUM-NRIC4         TO K810-NUM-NRIC4.                       
           MOVE K800-NUM-ZONE          TO K810-NUM-ZONE.                        
           MOVE K800-NUM-FLAT-TYPE     TO K810-NUM-FLAT-TYPE.                   
           MOVE K800-NUM-NEW-TOWN      TO K810-NUM-NEW-TOWN.                    
           MOVE K800-NUM-IDP-SCHEME    TO K810-NUM-IDP-SCHEME.                  
           MOVE K800-NUM-ALLO-SCHEME   TO K810-NUM-ALLO-SCHEME.                 
           MOVE K800-NUM-ELIG-SCHEME   TO K810-NUM-ELIG-SCHEME.                 
           MOVE K800-NUM-CAT           TO K810-NUM-CAT.                         
           MOVE K800-NUM-HOUSEHOLD     TO K810-NUM-HOUSEHOLD.                   
           MOVE SPACES                 TO K810-DTE-CHANGE-ZONE-FT.              
           MOVE K800-QTY-JT-BALLOT     TO K810-QTY-JT-BALLOT.                   
           MOVE K800-NUM-JT-BALLOT-MTF TO K810-NUM-JT-BALLOT.                   
           MOVE K800-NUM-SCH-ACC       TO K810-NUM-SCH-ACC.                     
           MOVE K800-NUM-CUR-REGN      TO K810-NUM-CUR-REGN.                    
           MOVE SPACES                 TO K810-NUM-DEFER-TAG.                   
           MOVE K800-NUM-WAIT-TIME     TO K810-NUM-WAIT-TIME.                   
           MOVE K800-NUM-ZONE-FT-QUEUE TO K810-NUM-ZONE-FT-QUEUE.               
           MOVE K800-NUM-5YR-OCCUPN    TO K810-NUM-5YR-OCCUPN.                  
           MOVE K800-NUM-PARENT-CHILD  TO K810-NUM-PARENT-CHILD.                
           MOVE K800-NUM-HOLDING-TYPE  TO K810-NUM-HOLDING-TYPE.                
           MOVE K800-NUM-SHORTLIST-PA-HHTY                                      
                                       TO K810-NUM-SHORTLIST-PA-HHTY.           
           MOVE K800-NUM-SHORTLIST-PA-CAT                                       
                                       TO K810-NUM-SHORTLIST-PA-CAT.            
           MOVE K800-NUM-SHORTLIST-PRIO-HHTY                                    
                                       TO K810-NUM-SHORTLIST-PRIO-HHTY.         
           MOVE K800-NUM-SHORTLIST-PRIO-CAT                                     
                                       TO K810-NUM-SHORTLIST-PRIO-CAT.          
           MOVE K800-DTE-BALLOT        TO K810-DTE-BALLOT.                      
                                                                                
                                                                                
           MOVE K800-TME-QUEUE         TO K810-TME-QUEUE.                       
           MOVE K800-TME-QUEUE         TO K810-TME-QUEUE.                       
           MOVE K800-NUM-JT-BALLOT-REGN TO K810-NUM-JT-BALLOT-REGN-1.           
           MOVE SPACES                 TO K810-NUM-JT-BALLOT-REGN-2.            
           MOVE SPACES                 TO K810-NUM-JT-BALLOT-REGN-3.            
           MOVE SPACES                 TO K810-NUM-JT-BALLOT-REGN-4.            
           MOVE SPACES                 TO K810-NUM-JT-BALLOT-REGN-5.            
           MOVE SPACES                 TO K810-NUM-JT-BALLOT-REGN-6.            
           MOVE K800-NUM-LOAN-TAG      TO K810-NUM-LOAN-TAG.                    
           MOVE F800-NUM-USERID        TO K810-NUM-USERID.                      
                                                                                
           IF  K800-NUM-NRIC1      NOT = SPACES AND LOW-VALUES                  
               MOVE K800-NUM-NRIC1     TO WS-NRIC                               
               PERFORM 5200-READ-BP13K820 THRU 5200-EXIT                        
               MOVE K820-NME-OCCP      TO K810-NME-HA1                          
           ELSE                                                                 
               MOVE SPACES             TO K810-NME-HA1.                         
                                                                                
           IF  (K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES) AND                 
               (K800-NUM-NRIC2(1:1) NOT = '#')                                  
               MOVE K800-NUM-NRIC2     TO WS-NRIC                               
               PERFORM 5200-READ-BP13K820 THRU 5200-EXIT                        
               MOVE K820-NME-OCCP      TO K810-NME-HA2                          
           ELSE                                                                 
               MOVE SPACES             TO K810-NME-HA2.                         
                                                                                
           IF  (K800-NUM-NRIC3      NOT = SPACES AND LOW-VALUES) AND            
               (K800-NUM-NRIC3(1:1) NOT = '#')                                  
               MOVE K800-NUM-NRIC3     TO WS-NRIC                               
               PERFORM 5200-READ-BP13K820 THRU 5200-EXIT                        
               MOVE K820-NME-OCCP      TO K810-NME-HA3                          
           ELSE                                                                 
               MOVE SPACES             TO K810-NME-HA3.                         
                                                                                
           IF  (K800-NUM-NRIC4      NOT = SPACES AND LOW-VALUES) AND            
               (K800-NUM-NRIC4(1:1) NOT = '#')                                  
               MOVE K800-NUM-NRIC4     TO WS-NRIC                               
               PERFORM 5200-READ-BP13K820 THRU 5200-EXIT                        
               MOVE K820-NME-OCCP      TO K810-NME-HA4                          
           ELSE                                                                 
               MOVE SPACES             TO K810-NME-HA4.                         
                                                                                
           MOVE 'C23V'                 TO K810-NUM-SCREENID.                    
                                                                                
           WRITE BP13K810-MASTER.                                               
                                                                                
           EVALUATE K810-STATUS                                                 
               WHEN 00                                                          
                    ADD 1 TO WS-CNT-K810-WRITE                                  
               WHEN OTHER                                                       
                    DISPLAY 'ERROR WRITING BP13K810. STATUS IS '                
                            K810-STATUS                                         
                    DISPLAY K810-KEY-FLD ' KEY 810'                             
                    PERFORM 9000-CLOSE-ROUTINE      THRU  9000-EXIT             
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------*          
       5200-READ-BP13K820.                                                      
      *--------------------------------------------------------------*          
                                                                                
            MOVE LOW-VALUES            TO K820-KEY-FLD.                         
            MOVE WS-NUM-REGN           TO K820-NUM-REGN.                        
            MOVE WS-NRIC               TO K820-NUM-NRIC.                        
                                                                                
            READ BP13K820.                                                      
                                                                                
            EVALUATE K820-STATUS                                                
               WHEN 00                                                          
               WHEN 02                                                          
                    MOVE 'Y' TO WS-K820-FOUND                                   
               WHEN 23                                                          
                    DISPLAY 'NOT FOUND IN BP13K820, KEY '                       
                            K820-KEY-FLD                                        
               WHEN OTHER                                                       
                    DISPLAY 'ERROR WRITING BP13K820. STATUS IS '                
                            K820-STATUS                                         
                    PERFORM 9000-CLOSE-ROUTINE      THRU  9000-EXIT             
            END-EVALUATE.                                                       
                                                                                
       5200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       6000-PROCESS-CDB.                                                        
      *************************************************************             
           MOVE SPACES                   TO BP13K820-REC.                       
           INITIALIZE                       BP13K820-REC.                       
                                                                                
           MOVE 'N'                      TO WS-K820-EOF                         
                                            WS-K820-NTFND                       
                                            WS-REC-PROCESS.                     
                                                                                
           MOVE K800-NUM-REGN            TO K820-NUM-REGN.                      
                                                                                
           START BP13K820 KEY >= K820-KEY-FLD.                                  
                                                                                
           EVALUATE K820-STATUS                                                 
               WHEN 00                                                          
                    PERFORM 6100-READNEXT-BP13K820 THRU 6100-EXIT               
                      UNTIL K820-NUM-REGN NOT = K800-NUM-REGN                   
                         OR WS-K820-EOF = 'Y'                                   
                         OR WS-K820-NTFND = 'Y'                                 
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1                TO WS-K820-NOTFND                      
               WHEN OTHER                                                       
                    DISPLAY 'BP13K820 START BROWSE ERROR : '                    
                            K820-STATUS                                         
                    MOVE K820-STATUS  TO RETURN-CODE                            
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       6100-READNEXT-BP13K820.                                                  
      *************************************************************             
           MOVE SPACES                   TO BP13K820-REC.                       
           INITIALIZE                       BP13K820-REC.                       
                                                                                
           READ BP13K820 NEXT RECORD.                                           
                                                                                
           EVALUATE K820-STATUS                                                 
               WHEN 00                                                          
                    IF K820-NUM-REGN = K800-NUM-REGN                            
                       ADD 1             TO WS-K820-FND                         
                       MOVE 'Y'          TO WS-REC-PROCESS                      
                                                                                
                       PERFORM 6200-WRITE-BP13F915 THRU 6200-EXIT               
                    ELSE                                                        
                       IF WS-REC-PROCESS = 'N'                                  
                          ADD 1          TO WS-K820-NOTFND                      
                       END-IF                                                   
                                                                                
                       MOVE 'Y'          TO WS-K820-NTFND                       
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1                TO WS-K820-NOTFND                      
                    MOVE 'Y'             TO WS-K820-NTFND                       
               WHEN OTHER                                                       
                    DISPLAY 'BP13K820 READ NEXT ERROR : ' K820-STATUS           
                    MOVE K820-STATUS  TO RETURN-CODE                            
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       6100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       6200-WRITE-BP13F915.                                                     
      *************************************************************             
           IF K820-NUM-CSTMR-SOURCE NOT = SPACES AND LOW-VALUES                 
              MOVE SPACES                   TO BP13F915-REC                     
              INITIALIZE                       BP13F915-REC                     
                                                                                
              MOVE  'TE'                    TO F915-TXN-TYPE                    
              MOVE  'P'                     TO F915-CSTMR-TYPE                  
              MOVE  'BP13'                  TO F915-NUM-UPDATE-SOURCE           
              MOVE  'BP13C23V'              TO F915-NUM-UPDATE-USERID           
              MOVE  K820-NUM-CSTMR-SOURCE   TO F915-NUM-CSTMR-SOURCE            
                                                                                
              IF K820-NUM-NRIC = K800-NUM-NRIC1                                 
                 MOVE '011'                 TO F915-NUM-ROLE                    
              ELSE                                                              
                 IF (K820-NUM-NRIC = K800-NUM-NRIC2) OR                         
                    (K820-NUM-NRIC = K800-NUM-NRIC3) OR                         
                    (K820-NUM-NRIC = K800-NUM-NRIC4)                            
                     MOVE '012'             TO F915-NUM-ROLE                    
                 ELSE                                                           
                     MOVE '013'             TO F915-NUM-ROLE                    
                 END-IF                                                         
              END-IF                                                            
                                                                                
              MOVE  K820-NUM-REGN           TO F915-NUM-BSNS-REF                
                                                                                
              STRING K800-DTE-CANCEL(1:4) '-'                                   
                     K800-DTE-CANCEL(5:2) '-'                                   
                     K800-DTE-CANCEL(7:2)                                       
                     DELIMITED BY SIZE                                          
                INTO F915-DTE-ROLE-END                                          
                                                                                
              STRING WS-CURRENT-DATE(1:4) '-'                                   
                     WS-CURRENT-DATE(5:2) '-'                                   
                     WS-CURRENT-DATE(7:2)                                       
                     DELIMITED BY SIZE                                          
                INTO F915-DTE-UPDATE-SOURCE                                     
                                                                                
              WRITE BP13F915-REC                                                
                                                                                
              ADD 1                         TO WS-CNT-F915-WRITE                
                                                                                
           END-IF.                                                              
                                                                                
       6200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------*                                                       
       9000-CLOSE-ROUTINE.                                                      
      *-----------------*                                                       
                                                                                
           DISPLAY '**********************'.                                    
           DISPLAY 'PROGRAM-ID : BP13C23V '.                                    
           DISPLAY '**********************'.                                    
           DISPLAY ' '.                                                         
           DISPLAY 'BATCH RUN DATE IS : ' F001-DTE-CURRENT                      
           DISPLAY ' '                                                          
           DISPLAY 'DATES TO BE CAPTURED FROM  : ' WS-WEEK-BEFORE               
           DISPLAY 'DATES TO BE CAPTURED UNTIL : ' WS-CURRENT-WEEK              
           DISPLAY ' '                                                          
           DISPLAY 'NO OF RECS READ FROM  BP13F800  : '                         
                          WS-TOT-F800-READ.                                     
           DISPLAY 'NO OF RECS FOUND IN   BP13K800  : '                         
                          WS-TOT-K800-READ.                                     
           DISPLAY 'NO OF RECS UPDATED IN BP13K800  : '                         
                          WS-TOT-K800-REWRITE.                                  
           DISPLAY 'NO OF RECS WRITTEN IN BP13K840  : '                         
                          WS-CNT-K840-WRITE.                                    
           DISPLAY 'NO OF RECS WRITTEN IN BP13K848  : '                         
                          WS-CNT-K848-WRITE.                                    
           DISPLAY 'NO OF RECS WRITTEN IN BP13K810  : '                         
                          WS-CNT-K810-WRITE.                                    
           DISPLAY 'NO OF RECS WRITTEN IN BP13F313  : '                         
                          WS-CNT-F313-WRITE.                                    
           DISPLAY 'NO OF RECS WRITTEN IN BP13F915  : '                         
                          WS-CNT-F915-WRITE.                                    
                                                                                
           CLOSE SY02F001                                                       
                 BP13K800                                                       
                 BP13K840                                                       
                 BP13K820                                                       
                 BP13K848                                                       
                 BP13K810                                                       
                 BP13F800                                                       
                 P13L23VA                                                       
                 P13L23VB                                                       
                 BP13F915                                                       
                 BP13F313.                                                      
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING - BP13K800 : ' K800-STATUS                 
              MOVE K800-STATUS                TO RETURN-CODE                    
              PERFORM 9000-CLOSE-ROUTINE      THRU  9000-EXIT                   
           END-IF.                                                              
                                                                                
           IF K840-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING - BP13K840 : ' K840-STATUS                 
              MOVE K840-STATUS                TO RETURN-CODE                    
              PERFORM 9000-CLOSE-ROUTINE      THRU  9000-EXIT                   
           END-IF.                                                              
                                                                                
           IF K820-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING - BP13K820 : ' K820-STATUS                 
              MOVE K820-STATUS                TO RETURN-CODE                    
              PERFORM 9000-CLOSE-ROUTINE      THRU  9000-EXIT                   
           END-IF.                                                              
                                                                                
           IF K820-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING - BP13K820 : ' K820-STATUS                 
              MOVE K820-STATUS                TO RETURN-CODE                    
              PERFORM 9000-CLOSE-ROUTINE      THRU  9000-EXIT                   
           END-IF.                                                              
                                                                                
           IF K848-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING - BP13K848 : ' K848-STATUS                 
              MOVE K848-STATUS                TO RETURN-CODE                    
              PERFORM 9000-CLOSE-ROUTINE      THRU  9000-EXIT                   
           END-IF.                                                              
                                                                                
           IF K810-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING - BP13K810 : ' K810-STATUS                 
              MOVE K810-STATUS                TO RETURN-CODE                    
              PERFORM 9000-CLOSE-ROUTINE      THRU  9000-EXIT                   
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
