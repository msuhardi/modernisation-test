       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C22D.                                                  
      *AUTHOR.       LMS.                                                       
      *DATE-WRITTEN. 20/01/06.                                                  
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *  OBJECTIVE :                                               *            
      *      1.  TO EXTRACT RECORDS FROM BP13K230 AND BP13K240 BY  *            
      *          ALLOCATION DATE, ZONE/NT AND FLAT TYPE.           *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REQ  BY   DATE   DESCRIPTION                           *            
      * -------- ---- ------ -----------                           *            
      * BP132562 LMS  060121 NEW PROGRAM.                          *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13K060   ASSIGN       TO BP13K060                           
                             ORGANIZATION IS INDEXED                            
                             ACCESS MODE  IS RANDOM                             
                             RECORD KEY   IS K060-KEY-FLD                       
                             FILE STATUS  IS WS-K060-STATUS.                    
                                                                                
           SELECT BP13F208   ASSIGN       TO BP13F208.                          
           SELECT BP13F230   ASSIGN       TO BP13F230.                          
           SELECT BP13F240   ASSIGN       TO BP13F240.                          
           SELECT P13F230A   ASSIGN       TO P13F230A.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F230                                                            
            RECORD CONTAINS 450 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13K230.                                                           
                                                                                
       FD   BP13F240                                                            
            RECORD CONTAINS 450 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13K240.                                                           
                                                                                
       FD   BP13F208                                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F208.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS  25 CHARACTERS.                                     
       COPY BP13K060.                                                           
                                                                                
       FD   P13F230A                                                            
            RECORD CONTAINS 450 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       01   P13F230A-REC           PIC X(450).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-F230-READ            PIC 9(8)  VALUE 0.                           
       01  WS-F240-READ            PIC 9(8)  VALUE 0.                           
       01  WS-F230A-WRITE          PIC 9(8)  VALUE 0.                           
       01  WS-K060-STATUS          PIC 9(2).                                    
       01  WS-EOF-CTL              PIC X     VALUE SPACES.                      
                                                                                
       01  WS-CONTROL-FIELDS.                                                   
           05  WS-F230-CTRL-FLDS.                                               
               10  WS-F230-DTE-ALLOC         PIC X(8).                          
               10  WS-F230-NUM-NT-ZONE       PIC X(20).                         
               10  WS-F230-NUM-FT            PIC X(2).                          
           05  WS-F240-CTRL-FLDS.                                               
               10  WS-F240-DTE-ALLOC         PIC X(8).                          
               10  WS-F240-NUM-NT-ZONE       PIC X(20).                         
               10  WS-F240-NUM-FT            PIC X(2).                          
           05  WS-F208-CTRL-FLDS.                                               
               10  WS-F208-DTE-ALLOCN        PIC X(8).                          
               10  WS-F208-NUM-NT-ZONE       PIC X(20).                         
               10  WS-F208-NUM-FT            PIC X(2).                          
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN.                                                                
      *-------------------------------------------------------------            
           PERFORM 100-OPEN-FILES    THRU 100-EXIT.                             
           PERFORM 200-READ-F208-REC THRU 200-EXIT.                             
           PERFORM 300-PROCESS-REC   THRU 300-EXIT                              
             UNTIL WS-EOF-CTL = 'Y'.                                            
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F208                                                 
                       BP13K060                                                 
                OUTPUT P13F230A.                                                
                                                                                
           IF (WS-K060-STATUS NOT = ZEROS AND 97)                               
              DISPLAY 'BP13K060 OPEN ERROR, STATUS IS ' WS-K060-STATUS          
              MOVE     WS-K060-STATUS TO RETURN-CODE                            
              GO TO    900-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-F208-REC.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES              TO WS-F208-CTRL-FLDS.                       
                                                                                
           READ BP13F208 AT END                                                 
                MOVE 'Y' TO WS-EOF-CTL                                          
                GO TO 200-EXIT                                                  
           END-READ.                                                            
                                                                                
           MOVE F208-DTE-ALLOCN     TO WS-F208-DTE-ALLOCN.                      
                                                                                
           MOVE 02                  TO K060-SERIAL-NO.                          
           MOVE F208-NUM-NT-ZONE    TO K060-CODE.                               
           READ BP13K060 KEY IS K060-KEY-FLD.                                   
           EVALUATE WS-K060-STATUS                                              
           WHEN ZEROS                                                           
             MOVE K060-DESC       TO WS-F208-NUM-NT-ZONE                        
           WHEN OTHER                                                           
             DISPLAY 'BP13K060 READ ERROR, STATUS IS ' WS-K060-STATUS           
             DISPLAY 'BP13K060 RECORD KEY IS ' K060-KEY-FLD                     
             GO TO   900-CLOSE-ROUTINE                                          
           END-EVALUATE                                                         
           IF WS-F208-NUM-NT-ZONE = 'KALLANG/WHAMPOA'                           
              MOVE 'KALLANG/WP/NOVENA' TO WS-F208-NUM-NT-ZONE                   
           END-IF.                                                              
                                                                                
           MOVE F208-NUM-FLAT-TYPE  TO WS-F208-NUM-FT.                          
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS-REC.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F230                                                 
                       BP13F240.                                                
                                                                                
           PERFORM 400-READ-F230-REC THRU 400-EXIT.                             
           PERFORM 450-PROCESS-F230  THRU 450-EXIT                              
                   UNTIL K230-CDE-INTV = HIGH-VALUE.                            
                                                                                
           PERFORM 500-READ-F240-REC THRU 500-EXIT.                             
           PERFORM 550-PROCESS-F240  THRU 550-EXIT                              
                   UNTIL K240-CDE-INTV = HIGH-VALUE.                            
                                                                                
           CLOSE BP13F230                                                       
                 BP13F240.                                                      
                                                                                
           PERFORM 200-READ-F208-REC THRU 200-EXIT.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       400-READ-F230-REC.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES               TO WS-F230-CTRL-FLDS.                      
                                                                                
           READ BP13F230 AT END                                                 
                MOVE HIGH-VALUE TO K230-CDE-INTV                                
                GO TO 400-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F230-READ.                                               
                                                                                
           MOVE K230-DTE-ALLOC    TO WS-F230-DTE-ALLOC.                         
                                                                                
           MOVE K230-NUM-NEW-TOWN TO WS-F230-NUM-NT-ZONE.                       
                                                                                
           EVALUATE K230-NUM-FLAT-TYPE                                          
              WHEN '03'                                                         
              WHEN '3D'                                                         
              WHEN '3P'                                                         
                 MOVE '3 ' TO WS-F230-NUM-FT                                    
              WHEN '04'                                                         
              WHEN '4D'                                                         
              WHEN '4P'                                                         
                 MOVE '4 ' TO WS-F230-NUM-FT                                    
              WHEN '05'                                                         
              WHEN '5D'                                                         
              WHEN '5P'                                                         
                 MOVE '5 ' TO WS-F230-NUM-FT                                    
            END-EVALUATE.                                                       
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       450-PROCESS-F230.                                                        
      *-------------------------------------------------------------            
           IF WS-F208-CTRL-FLDS = WS-F230-CTRL-FLDS                             
              MOVE K230-DETAIL-REC  TO P13F230A-REC                             
              WRITE P13F230A-REC                                                
              ADD  1                TO  WS-F230A-WRITE                          
              PERFORM 400-READ-F230-REC THRU 400-EXIT                           
           ELSE                                                                 
              IF WS-F208-CTRL-FLDS > WS-F230-CTRL-FLDS                          
                 PERFORM 400-READ-F230-REC THRU 400-EXIT                        
              ELSE                                                              
                 MOVE HIGH-VALUE TO K230-CDE-INTV                               
              END-IF                                                            
           END-IF.                                                              
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       500-READ-F240-REC.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES               TO WS-F240-CTRL-FLDS.                      
           READ BP13F240 AT END                                                 
                MOVE HIGH-VALUE TO K240-CDE-INTV                                
                GO TO 500-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F240-READ.                                               
                                                                                
           MOVE K240-DTE-ALLOC    TO WS-F240-DTE-ALLOC.                         
                                                                                
           MOVE K240-NUM-NEW-TOWN TO WS-F240-NUM-NT-ZONE.                       
                                                                                
           EVALUATE K240-NUM-FLAT-TYPE                                          
              WHEN '03'                                                         
              WHEN '3D'                                                         
              WHEN '3P'                                                         
                 MOVE '3 ' TO WS-F240-NUM-FT                                    
              WHEN '04'                                                         
              WHEN '4D'                                                         
              WHEN '4P'                                                         
                 MOVE '4 ' TO WS-F240-NUM-FT                                    
              WHEN '05'                                                         
              WHEN '5D'                                                         
              WHEN '5P'                                                         
                 MOVE '5 ' TO WS-F240-NUM-FT                                    
            END-EVALUATE.                                                       
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       550-PROCESS-F240.                                                        
      *-------------------------------------------------------------            
           IF WS-F208-CTRL-FLDS = WS-F240-CTRL-FLDS                             
              MOVE K240-HEADER-REC  TO P13F230A-REC                             
              WRITE P13F230A-REC                                                
              ADD  1                TO  WS-F230A-WRITE                          
              PERFORM 500-READ-F240-REC THRU 500-EXIT                           
           ELSE                                                                 
              IF WS-F208-CTRL-FLDS > WS-F240-CTRL-FLDS                          
                 PERFORM 500-READ-F240-REC THRU 500-EXIT                        
              ELSE                                                              
                 MOVE HIGH-VALUE TO K240-CDE-INTV                               
                 GO TO 550-EXIT                                                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
       550-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
      *-------------------------------------------------------------            
           DISPLAY '*-------------- BP13C22D --------------*'.                  
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS READ FROM  F230  : ' WS-F230-READ.            
           DISPLAY 'NO OF RECORDS READ FROM  F240  : ' WS-F240-READ.            
           DISPLAY 'NO OF RECORDS WRITTEN TO F230A : ' WS-F230A-WRITE.          
                                                                                
           CLOSE BP13F208                                                       
                 BP13K060                                                       
                 P13F230A.                                                      
                                                                                
           IF WS-K060-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K060 CLOSE ERROR, STATUS IS ' WS-K060-STATUS         
              MOVE WS-K060-STATUS TO RETURN-CODE                                
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
