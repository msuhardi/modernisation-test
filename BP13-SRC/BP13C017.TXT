      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
                                                                                
       PROGRAM-ID.    BP13C017.                                                 
      *AUTHOR.        ELGER DE GUZMAN                                           
      *DATE-WRITTEN.  13/05/13.                                                 
      *DATE-COMPILED.                                                           
      *REMARKS.                                                                 
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME  :    SYSTEM OF COMMITMENT   (SOC)               *        
      *                                                                *        
      *   SYSTEM ID    :    BP13                                       *        
      *                                                                *        
      *   OBJECTIVE    :    TO CREATE REPORT OF REGISTRATION NUMBER    *        
      *                     THAT OUTRIGHT OF PURCHASE LETTER IS PRINTED*        
      *                                                                *        
      *   INPUT FILES  :    BP13F310                                   *        
      *                :    BP13K800                                   *        
      *                :    BP13K410                                   *        
      *                :    BP13K820                                   *        
      *   OUTPUT FILES :    BP13F130                                   *        
      *                                                                *        
      *   REPORT       :    1. BP13L017                                *        
      *                                                                *        
      *   MODIFICATION HISTORY:                                        *        
      *   =====================                                        *        
      *   CHGE-NO  BY    DATE   DESCRIPTION                            *        
      *   -------  ---  ------  -------------------------------------  *        
      *   BP134919 ESD1 13/05/13 NEW PROGRAM.                          *        
      *   BP134919 ESD1 13/05/13 ADDED BP13K410 TO READ IF NOT FOUND IN*        
      *                          BP13K800.                             *        
      ******************************************************************        
      ******************************************************************        
       ENVIRONMENT    DIVISION.                                                 
      ******************************************************************        
      *================================================================*        
       CONFIGURATION  SECTION.                                                  
      *================================================================*        
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
      *================================================================*        
       INPUT-OUTPUT SECTION.                                                    
      *================================================================*        
      *----------------------------------------------------------------*        
       FILE-CONTROL.                                                            
      *----------------------------------------------------------------*        
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K800-NUM-REGN                                   
                  ACCESS MODE IS RANDOM                                         
                  FILE STATUS IS WS-K800-STATUS.                                
                                                                                
           SELECT BP13K410 ASSIGN TO BP13K410                                   
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K410-NUM-REGN                                   
                  ACCESS MODE IS RANDOM                                         
                  FILE STATUS IS WS-K410-STATUS.                                
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K820-KEY-FLD                                    
                  ACCESS MODE IS RANDOM                                         
                  FILE STATUS IS WS-K820-STATUS.                                
                                                                                
           SELECT BP13F310  ASSIGN       TO BP13F310.                           
           SELECT BP13L017  ASSIGN       TO BP13L017.                           
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
      *================================================================*        
       FILE SECTION.                                                            
      *================================================================*        
                                                                                
       FD  BP13K800                                                             
                                   RECORD CONTAINS 2000 CHARACTERS.             
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K410                                                             
                                   RECORD CONTAINS 2000 CHARACTERS.             
       COPY BP13K410.                                                           
                                                                                
       FD  BP13K820                                                             
                                   RECORD CONTAINS 400  CHARACTERS.             
       COPY BP13K820.                                                           
                                                                                
       FD  BP13F310                                                             
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13F310.                                                           
                                                                                
       FD   BP13L017                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   L017-REC            PIC X(132).                                     
                                                                                
      *================================================================*        
       WORKING-STORAGE SECTION.                                                 
      *================================================================*        
      *----------------------------------------------------------------*        
      *     ERROR VARIABLES                                            *        
      *----------------------------------------------------------------*        
       01  WS-ERR-MSG                     PIC X(80).                            
                                                                                
      *----------------------------------------------------------------*        
      *     FLAG VARIABLES                                             *        
      *----------------------------------------------------------------*        
       01  WS-FLAGS.                                                            
           05 WS-K800-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K410-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K820-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-BP13F310-EOF        PIC X(1) VALUE 'N'.                        
       01  WS-CD                          PIC S9(7)V99   VALUE ZEROES.          
       01  WS-NO-F130-WR                  PIC 9(5)  VALUE ZEROES.               
                                                                                
      *----------------------------------------------------------------*        
      *    COUNTER VARIABLES                                           *        
      *----------------------------------------------------------------*        
       01  WS-COUNTERS.                                                         
           05  WS-CNT-BP13F310-READ       PIC 9(8)   VALUE ZEROES.              
           05  WS-CNT-L017-WRITE          PIC 9(8)   VALUE ZEROES.              
                                                                                
      *----------------------------------------------------------------*        
      *    PRINTING VARIABLES                                          *        
      *----------------------------------------------------------------*        
       01  WS-PRINT-VARIABLES.                                                  
           05  WS-SEQ-CNT                 PIC 9(5)   VALUE ZEROES.              
           05  WS-PAGE-CNT                PIC 9(5)   VALUE 0.                   
           05  WS-LINE-CNT                PIC 9(2)   VALUE 0.                   
                                                                                
      *----------------------------------------------------------------*        
      *  MISCELLEANEOUS VARIABLES                                      *        
      *----------------------------------------------------------------*        
       01  WS-STRING-POINTER              PIC 9(2).                             
       01  WS-CTR                         PIC 9(2).                             
       01  WS-CTR2                        PIC 9(2).                             
       01  WS-NUM-NRIC1                   PIC X(9).                             
       01  WS-NUM-NRIC2                   PIC X(9).                             
       01  WS-NUM-NRIC3                   PIC X(9).                             
       01  WS-NUM-NRIC4                   PIC X(9).                             
       01  WS-ARRAY.                                                            
           05  WS-MAIN-ARRAY.                                                   
               10  WS-CHAR-ARRAY  OCCURS 5 TIMES PIC X.                         
           05  WS-TXT-ARRAY.                                                    
               10  WS-CHAR-TXT    OCCURS 5 TIMES PIC X.                         
       01  WS-TEMP-NAME                   PIC X(40) VALUE SPACES.               
       01  WS-CURRENT-DATE.                                                     
               10  WS-CURR-YY             PIC X(4).                             
               10  WS-CURR-MM             PIC X(2).                             
               10  WS-CURR-DD             PIC X(2).                             
                                                                                
      *----------------------------------------------------------------*        
      *    REPORT LAYOUT OF SUMMARY AGEING REPORT.                     *        
      *----------------------------------------------------------------*        
       01  L017-PRT-HDR-1.                                                      
           05  FILLER                     PIC X      VALUE SPACE.               
           05  FILLER                     PIC X(8)   VALUE 'BP13L017'.          
           05  FILLER                     PIC X(13)  VALUE SPACES.              
           05  FILLER                     PIC X(4)   VALUE 'HDB3'.              
           05  FILLER                     PIC X(14)  VALUE SPACES.              
           05  FILLER                     PIC X(39)                             
               VALUE 'S Y S T E M   O F   C O M M I T M E N T'.                 
           05  FILLER                     PIC X(10)  VALUE SPACES.              
           05  FILLER                     PIC X(7)   VALUE 'DATE : '.           
           05  L017-PRT-DATE              PIC X(10)  VALUE SPACES.              
           05  FILLER                     PIC X(10)  VALUE SPACES.              
           05  FILLER                     PIC X(9)   VALUE 'PAGE   : '.         
           05  L017-PRT-PAGE              PIC ZZ9.                              
                                                                                
       01  L017-PRT-HDR-2.                                                      
           05  FILLER                     PIC X(49)  VALUE SPACES.              
           05  FILLER                     PIC X(30)                             
           VALUE 'OUTRIGHT PURCHASED LETTER '.                                  
                                                                                
       01  L017-PRT-HDR-3.                                                      
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(03)  VALUE 'S/N'.               
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(08)  VALUE 'REGN-NO '.          
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  FILLER                     PIC X(05)                             
               VALUE 'NAMES'.                                                   
           05  FILLER                     PIC X(38)  VALUE SPACES.              
           05  FILLER                     PIC X(3)                              
               VALUE 'BLK'.                                                     
           05  FILLER                     PIC X(05)  VALUE SPACES.              
           05  FILLER                     PIC X(07)                             
               VALUE 'ADDRESS'.                                                 
           05  FILLER                     PIC X(25)  VALUE SPACES.              
           05  FILLER                     PIC X(06)                             
               VALUE 'POSTAL'.                                                  
                                                                                
       01  L017-PRT-DTL-1.                                                      
           05  FILLER                     PIC X(01)  VALUE SPACES.              
           05  L017-SERIAL-NO             PIC ZZZZ9.                            
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  L017-REGN-NO               PIC X(08).                            
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  L017-NAME-1                PIC X(39)  VALUE SPACES.              
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  L017-BLK                   PIC X(05)  VALUE SPACES.              
           05  FILLER                     PIC X(04)  VALUE SPACES.              
           05  L017-ADDRESS               PIC X(30)  VALUE SPACES.              
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  L017-POSTAL                PIC X(06)  VALUE SPACES.              
                                                                                
       01  L017-PRT-DTL-2.                                                      
           05  FILLER                     PIC X(20)  VALUE SPACES.              
           05  L017-NAME-2                PIC X(40)  VALUE SPACES.              
                                                                                
       01  L017-PRT-DTL-3.                                                      
           05  FILLER                     PIC X(20)  VALUE SPACES.              
           05  L017-NAME-3                PIC X(40)  VALUE SPACES.              
                                                                                
       01  L017-PRT-DTL-4.                                                      
           05  FILLER                     PIC X(20)  VALUE SPACES.              
           05  L017-NAME-4                PIC X(40)  VALUE SPACES.              
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      *================================================================*        
       0000-MAIN-ROUTINE.                                                       
      *================================================================*        
                                                                                
           PERFORM 1000-INITIALIZATION      THRU 1000-EXIT.                     
           PERFORM 2000-PROCESS-ROUTINE     THRU 2000-EXIT                      
                   UNTIL WS-BP13F310-EOF = 'Y'.                                 
           PERFORM 9999-CLOSE-FILES         THRU 9999-EXIT.                     
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       1000-INITIALIZATION.                                                     
      *================================================================*        
           OPEN INPUT  BP13F310                                                 
                       BP13K800                                                 
                       BP13K410                                                 
                       BP13K820                                                 
                OUTPUT BP13L017.                                                
                                                                                
           IF WS-K800-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K800-STATUS ' WS-K800-STATUS              
              MOVE WS-K800-STATUS       TO RETURN-CODE                          
              MOVE 'OPENING ERROR, K800-STATUS ' TO WS-ERR-MSG                  
              PERFORM 9999-CLOSE-FILES         THRU 9999-EXIT                   
           END-IF.                                                              
                                                                                
           IF WS-K410-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K410-STATUS ' WS-K410-STATUS              
              MOVE WS-K410-STATUS       TO RETURN-CODE                          
              MOVE 'OPENING ERROR, K410-STATUS ' TO WS-ERR-MSG                  
              PERFORM 9999-CLOSE-FILES         THRU 9999-EXIT                   
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K820-STATUS ' WS-K820-STATUS              
              MOVE WS-K820-STATUS       TO RETURN-CODE                          
              MOVE 'OPENING ERROR, K820-STATUS ' TO WS-ERR-MSG                  
              PERFORM 9999-CLOSE-FILES         THRU 9999-EXIT                   
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO    WS-CURRENT-DATE.              
           STRING WS-CURR-DD '/' WS-CURR-MM '/'   WS-CURR-YY                    
                  DELIMITED BY SIZE INTO L017-PRT-DATE.                         
                                                                                
           READ  BP13F310 AT END                                                
                 MOVE 'Y'          TO  WS-BP13F310-EOF.                         
                                                                                
           IF WS-BP13F310-EOF = 'Y'                                             
              STRING 'NO PRINT FILE RECORDS FOUND'                              
                     DELIMITED BY SIZE INTO WS-ERR-MSG                          
           ELSE                                                                 
              ADD 1                        TO WS-CNT-BP13F310-READ              
              PERFORM 5000-PRINT-L017-HDG  THRU 5000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       2000-PROCESS-ROUTINE.                                                    
      *================================================================*        
                                                                                
           MOVE F310-REGN-NO  TO K800-NUM-REGN                                  
                                 K820-NUM-REGN                                  
                                                                                
           READ  BP13K800.                                                      
                                                                                
           IF WS-K800-STATUS = 00                                               
              ADD     1                 TO WS-SEQ-CNT                           
              INITIALIZE  L017-PRT-DTL-1 L017-PRT-DTL-2 L017-PRT-DTL-3          
                          L017-PRT-DTL-4                                        
              IF WS-SEQ-CNT  > 12                                               
                 MOVE 1   TO  WS-SEQ-CNT                                        
                 PERFORM 5000-PRINT-L017-HDG  THRU 5000-EXIT                    
              END-IF                                                            
              MOVE K800-NUM-NRIC1   TO  WS-NUM-NRIC1                            
              MOVE K800-NUM-NRIC2   TO  WS-NUM-NRIC2                            
              MOVE K800-NUM-NRIC3   TO  WS-NUM-NRIC3                            
              MOVE K800-NUM-NRIC4   TO  WS-NUM-NRIC4                            
              PERFORM 3000-GET-DETAILS  THRU 3000-EXIT                          
           ELSE                                                                 
              IF WS-K800-STATUS = 23                                            
                 PERFORM 2500-READ-BP13K410 THRU 2500-EXIT                      
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13K800,STATUS ' WS-K800-STATUS           
                 MOVE WS-K800-STATUS        TO RETURN-CODE                      
                 MOVE 'ERROR READ BP13K800,STATUS ' TO WS-ERR-MSG               
                 PERFORM 9999-CLOSE-FILES   THRU 9999-EXIT                      
           END-IF.                                                              
                                                                                
                                                                                
           READ  BP13F310 AT END                                                
                 MOVE 'Y'        TO WS-BP13F310-EOF                             
                 GO  TO 2000-EXIT                                               
                 END-READ.                                                      
                                                                                
           ADD   1                   TO WS-CNT-BP13F310-READ.                   
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       2500-READ-BP13K410.                                                      
      *================================================================*        
                                                                                
           MOVE F310-REGN-NO  TO K410-NUM-REGN                                  
                                                                                
           READ  BP13K410.                                                      
                                                                                
           IF WS-K410-STATUS = 00                                               
              ADD     1                 TO WS-SEQ-CNT                           
              INITIALIZE  L017-PRT-DTL-1 L017-PRT-DTL-2 L017-PRT-DTL-3          
                          L017-PRT-DTL-4                                        
              IF WS-SEQ-CNT  > 12                                               
                 MOVE 1   TO  WS-SEQ-CNT                                        
                 PERFORM 5000-PRINT-L017-HDG  THRU 5000-EXIT                    
              END-IF                                                            
              PERFORM 3500-GET-K410DTL  THRU 3500-EXIT                          
           ELSE                                                                 
              DISPLAY 'ERROR READ BP13K410,STATUS ' WS-K410-STATUS              
              MOVE WS-K410-STATUS        TO RETURN-CODE                         
              MOVE 'ERROR READ BP13K410,STATUS ' TO WS-ERR-MSG                  
              PERFORM 9999-CLOSE-FILES   THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3000-GET-DETAILS.                                                        
      *================================================================*        
                                                                                
            MOVE    SPACES                TO WS-MAIN-ARRAY WS-TXT-ARRAY         
            MOVE    WS-CNT-BP13F310-READ  TO L017-SERIAL-NO                     
            MOVE    F310-REGN-NO          TO L017-REGN-NO                       
            MOVE    F310-NUM-BLK          TO L017-BLK                           
            MOVE    F310-NUM-POSTAL       TO L017-POSTAL                        
            MOVE    F310-NUM-MAIN         TO WS-MAIN-ARRAY                      
            MOVE    0                     TO WS-CTR2                            
                                                                                
            PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 5                 
               IF  WS-CHAR-ARRAY(WS-CTR)  =  ' '                                
                   CONTINUE                                                     
               ELSE                                                             
                   ADD  1  TO WS-CTR2                                           
                   MOVE WS-CHAR-ARRAY(WS-CTR) TO WS-CHAR-TXT(WS-CTR2)           
               END-IF                                                           
            END-PERFORM                                                         
                                                                                
            STRING  F310-NME-STREET ' '                                         
                    F310-NUM-LEVEL '-'                                          
                    WS-TXT-ARRAY                                                
            DELIMITED BY '   '  INTO L017-ADDRESS                               
                                                                                
            IF WS-NUM-NRIC1 NOT = SPACES AND LOW-VALUES                         
               MOVE SPACES        TO WS-TEMP-NAME                               
               MOVE WS-NUM-NRIC1  TO K820-NUM-NRIC                              
               PERFORM 4000-READ-BP13K820 THRU 4000-EXIT                        
               MOVE WS-TEMP-NAME  TO L017-NAME-1                                
               WRITE L017-REC   FROM L017-PRT-DTL-1                             
            ELSE                                                                
               DISPLAY 'NO NRIC FOUND FOR REGN     ' F310-REGN-NO               
               MOVE 'NO NRIC FOUND' TO WS-ERR-MSG                               
               PERFORM 9999-CLOSE-FILES   THRU 9999-EXIT                        
            END-IF.                                                             
                                                                                
            IF WS-NUM-NRIC2 NOT = SPACES AND LOW-VALUES                         
               MOVE SPACES          TO WS-TEMP-NAME                             
               MOVE WS-NUM-NRIC2    TO K820-NUM-NRIC                            
               PERFORM 4000-READ-BP13K820 THRU 4000-EXIT                        
               MOVE WS-TEMP-NAME  TO L017-NAME-2                                
               WRITE L017-REC   FROM L017-PRT-DTL-2                             
            ELSE                                                                
               MOVE SPACES      TO L017-NAME-2                                  
               WRITE L017-REC   FROM L017-PRT-DTL-2                             
            END-IF.                                                             
                                                                                
            IF WS-NUM-NRIC3 NOT = SPACES AND LOW-VALUES                         
               MOVE SPACES          TO WS-TEMP-NAME                             
               MOVE WS-NUM-NRIC3  TO K820-NUM-NRIC                              
               PERFORM 4000-READ-BP13K820 THRU 4000-EXIT                        
               MOVE WS-TEMP-NAME  TO L017-NAME-3                                
               WRITE L017-REC   FROM L017-PRT-DTL-3                             
            ELSE                                                                
               MOVE SPACES      TO L017-NAME-3                                  
               WRITE L017-REC   FROM L017-PRT-DTL-3                             
            END-IF.                                                             
                                                                                
            IF WS-NUM-NRIC4 NOT = SPACES AND LOW-VALUES                         
               MOVE SPACES          TO WS-TEMP-NAME                             
               MOVE WS-NUM-NRIC4  TO K820-NUM-NRIC                              
               PERFORM 4000-READ-BP13K820 THRU 4000-EXIT                        
               MOVE WS-TEMP-NAME  TO L017-NAME-4                                
               WRITE L017-REC   FROM L017-PRT-DTL-4                             
            ELSE                                                                
               MOVE SPACES      TO L017-NAME-4                                  
               WRITE L017-REC   FROM L017-PRT-DTL-4                             
            END-IF.                                                             
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3500-GET-K410DTL.                                                        
      *================================================================*        
                                                                                
            MOVE    SPACES                TO WS-MAIN-ARRAY WS-TXT-ARRAY         
            MOVE    WS-CNT-BP13F310-READ  TO L017-SERIAL-NO                     
            MOVE    F310-REGN-NO          TO L017-REGN-NO                       
            MOVE    F310-NUM-BLK          TO L017-BLK                           
            MOVE    F310-NUM-POSTAL       TO L017-POSTAL                        
            MOVE    F310-NUM-MAIN         TO WS-MAIN-ARRAY                      
            MOVE    0                     TO WS-CTR2                            
                                                                                
            PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 5                 
               IF  WS-CHAR-ARRAY(WS-CTR)  =  ' '                                
                   CONTINUE                                                     
               ELSE                                                             
                   ADD  1  TO WS-CTR2                                           
                   MOVE WS-CHAR-ARRAY(WS-CTR) TO WS-CHAR-TXT(WS-CTR2)           
               END-IF                                                           
            END-PERFORM                                                         
                                                                                
            STRING  F310-NME-STREET ' '                                         
                    F310-NUM-LEVEL '-'                                          
                    WS-TXT-ARRAY                                                
            DELIMITED BY '   '  INTO L017-ADDRESS                               
                                                                                
            IF K410-NAME1 NOT = SPACES AND LOW-VALUES                           
               MOVE K410-NAME1  TO L017-NAME-1                                  
               WRITE L017-REC   FROM L017-PRT-DTL-1                             
            ELSE                                                                
               DISPLAY 'NO NRIC FOUND FOR REGN     ' F310-REGN-NO               
               MOVE 'NO NRIC FOUND' TO WS-ERR-MSG                               
               PERFORM 9999-CLOSE-FILES   THRU 9999-EXIT                        
            END-IF.                                                             
                                                                                
            IF K410-NAME2  NOT = SPACES AND LOW-VALUES                          
               MOVE K410-NAME2    TO L017-NAME-2                                
               WRITE L017-REC   FROM L017-PRT-DTL-2                             
            ELSE                                                                
               MOVE SPACES      TO L017-NAME-2                                  
               WRITE L017-REC   FROM L017-PRT-DTL-2                             
            END-IF.                                                             
                                                                                
            IF K410-NAME3  NOT = SPACES AND LOW-VALUES                          
               MOVE K410-NAME3  TO L017-NAME-3                                  
               WRITE L017-REC   FROM L017-PRT-DTL-3                             
            ELSE                                                                
               MOVE SPACES      TO L017-NAME-3                                  
               WRITE L017-REC   FROM L017-PRT-DTL-3                             
            END-IF.                                                             
                                                                                
            IF K410-NAME4   NOT = SPACES AND LOW-VALUES                         
               MOVE  K410-NAME4 TO L017-NAME-4                                  
               WRITE L017-REC   FROM L017-PRT-DTL-4                             
            ELSE                                                                
               MOVE SPACES      TO L017-NAME-4                                  
               WRITE L017-REC   FROM L017-PRT-DTL-4                             
            END-IF.                                                             
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       4000-READ-BP13K820.                                                      
      *================================================================*        
                                                                                
           READ  BP13K820.                                                      
                                                                                
           IF WS-K820-STATUS = 00                                               
              MOVE  K820-NME-OCCP  TO   WS-TEMP-NAME                            
           ELSE                                                                 
              IF WS-K820-STATUS = 23                                            
                 CONTINUE                                                       
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13K800,STATUS ' WS-K800-STATUS           
                 MOVE WS-K800-STATUS        TO RETURN-CODE                      
                 MOVE 'ERROR READ BP13K800,STATUS ' TO WS-ERR-MSG               
                 PERFORM 9999-CLOSE-FILES   THRU 9999-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       5000-PRINT-L017-HDG.                                                     
      *================================================================*        
           ADD   1                   TO WS-PAGE-CNT.                            
           MOVE  WS-PAGE-CNT         TO L017-PRT-PAGE.                          
                                                                                
           WRITE L017-REC    FROM L017-PRT-HDR-1   AFTER PAGE.                  
           WRITE L017-REC    FROM L017-PRT-HDR-2   AFTER 2.                     
           MOVE  SPACES      TO L017-REC.                                       
           WRITE L017-REC.                                                      
           WRITE L017-REC    FROM L017-PRT-HDR-3   AFTER 2.                     
           MOVE  SPACES      TO L017-REC.                                       
           WRITE L017-REC.                                                      
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       9999-CLOSE-FILES.                                                        
      *================================================================*        
            CLOSE   BP13F310                                                    
                    BP13K800                                                    
                    BP13K410                                                    
                    BP13L017.                                                   
                                                                                
           IF WS-K800-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'CLOSING ERROR, K800-STATUS ' WS-K800-STATUS              
              MOVE WS-K800-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K410-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'CLOSING ERROR, K410-STATUS ' WS-K410-STATUS              
              MOVE WS-K410-STATUS       TO RETURN-CODE.                         
                                                                                
           IF WS-K820-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'CLOSING ERROR, K820-STATUS ' WS-K820-STATUS              
              MOVE WS-K820-STATUS       TO RETURN-CODE.                         
                                                                                
                                                                                
            DISPLAY '***************************************'                   
            DISPLAY '*    BP13C017  CONTROL TOTAL          *'                   
            DISPLAY '***************************************'                   
            DISPLAY '* NO. OF INPUT READ : ' WS-CNT-BP13F310-READ.              
            DISPLAY '***************************************'                   
            DISPLAY '* REMARKS: ' WS-ERR-MSG.                                   
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
