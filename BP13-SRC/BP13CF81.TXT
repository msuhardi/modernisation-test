       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF81.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  08/02/2013.                                               
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : TO CALCULATE FOR K712-NUM-REG-PPS     *                
      *                                                        *                
      * ====================================================== *                
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                  *                
      * --------  ----  --------  ---------------------------- *                
      * BP134782  SMR2  08022013  SIMILAR TO BP13C54A          *                
      * BP134914  SMR2  13062013  INCLUDE PREGNANT IN PPS-ELIG *                
      * BP135458  SMR2  04082014  EXPAND BP13K712 TO 350       *                
      * BP135492  SMR2  16092014  CATER FOR AST & MCPS QUOTA   *                
      * ====================================================== *                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
           SELECT BP13F713 ASSIGN TO BP13F713.                                  
                                                                                
           SELECT BP13K712 ASSIGN TO BP13K712                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K712-KEY-FLD                               
                  FILE STATUS     IS WS-K712-STATUS.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F740                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F740.                                                           
                                                                                
       FD  BP13F713                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 20 CHARACTERS                                        
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F713.                                                           
                                                                                
       FD  BP13K712                                                             
           RECORD CONTAINS 350 CHARACTERS.                                      
       COPY BP13K712.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       COPY PRIOSCH.                                                            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05 WS-K712-STATUS              PIC 99    VALUE ZEROS.                
                                                                                
       01  WS-COUNTER.                                                          
           05 WS-F740-READ                PIC 9(8)  VALUE ZEROS.                
           05 WS-K712-WRITE               PIC 9(8)  VALUE ZEROS.                
           05 WS-K712-REWRITE             PIC 9(8)  VALUE ZEROS.                
                                                                                
       01  WS-SYS-DATE.                                                         
           05 WS-CC                       PIC 99.                               
           05 WS-SYS-YYMMDD.                                                    
              10 WS-YY                    PIC 99.                               
              10 WS-MM                    PIC 99.                               
              10 WS-DD                    PIC 99.                               
                                                                                
       01  WS-SYS-TIME                    PIC 9(8).                             
                                                                                
       01  WS-EOF-F740                    PIC 9     VALUE 0.                    
           88 EOF-F740                    VALUE 1.                              
                                                                                
       01  WS-K712-KEY-FLD.                                                     
           05  WS-K712-CDE-NT-ZONE        PIC X(3).                             
           05  WS-K712-CDE-FLAT-TY        PIC X(2).                             
           05  WS-K712-CDE-COHORT         PIC X(2).                             
           05  WS-K712-CDE-ALLOC-SCH      PIC X(3).                             
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       000-MAIN-ROUTINE.                                                        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 250-READ-F713      THRU 250-EXIT.                            
           PERFORM 200-READ-F740      THRU 200-EXIT.                            
           PERFORM 300-PROCESS-F740   THRU 300-EXIT                             
             UNTIL EOF-F740.                                                    
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       100-INITIALIZATION.                                                      
      ******************************************************************        
           OPEN INPUT  BP13F740                                                 
                       BP13F713                                                 
                I-O    BP13K712.                                                
                                                                                
           IF WS-K712-STATUS NOT = 00 AND 97                                    
              MOVE WS-K712-STATUS TO RETURN-CODE                                
              DISPLAY 'ERROR OPENING BP13K712'                                  
              GO TO 999-CLOSE-FILES.                                            
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYS-DATE.                           
           ACCEPT WS-SYS-TIME              FROM TIME.                           
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       200-READ-F740.                                                           
      ******************************************************************        
           READ BP13F740 AT END                                                 
                MOVE 1   TO WS-EOF-F740                                         
                GO TO 200-EXIT.                                                 
                                                                                
           ADD  1  TO WS-F740-READ.                                             
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       250-READ-F713.                                                           
      ******************************************************************        
                                                                                
           READ BP13F713 AT END                                                 
                DISPLAY 'BP13F713 IS EMPTY'                                     
                GO TO 999-CLOSE-FILES.                                          
                                                                                
       250-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       300-PROCESS-F740.                                                        
      ******************************************************************        
           MOVE F740-CDE-NT1               TO WS-K712-CDE-NT-ZONE.              
           MOVE F740-CDE-FLAT-TYPE         TO WS-K712-CDE-FLAT-TY.              
           MOVE F713-CDE-COHORT            TO WS-K712-CDE-COHORT                
                                                                                
           PERFORM 310-GET-ALLOC-SCH       THRU 310-EXIT.                       
           IF WS-K712-CDE-ALLOC-SCH = 'XXX'                                     
               DISPLAY 'INVALID ALLOC SCHEME F740-REGN = '                      
                        F740-NUM-REGN                                           
               DISPLAY 'INVALID ALLOC SCHEME = '                                
                        F740-CDE-ALLOC-SCH                                      
               PERFORM 200-READ-F740  THRU 200-EXIT                             
               GO TO 300-EXIT.                                                  
                                                                                
           IF  F713-CDE-COHORT = 'PA'                                           
               MOVE 'PA ' TO WS-K712-CDE-ALLOC-SCH                              
               PERFORM 330-UPDATE-BP13K712 THRU 330-EXIT                        
           ELSE                                                                 
           IF  F713-CDE-COHORT = 'CC'                                           
               IF WS-K712-CDE-ALLOC-SCH = 'CCC'                                 
                  PERFORM 330-UPDATE-BP13K712 THRU 330-EXIT                     
               END-IF                                                           
           ELSE                                                                 
           IF  F713-CDE-COHORT = 'MT'                                           
               IF WS-K712-CDE-ALLOC-SCH NOT = 'PA '                             
                  IF WS-K712-CDE-ALLOC-SCH = 'CCC'                              
                     MOVE 'CC'  TO WS-K712-CDE-COHORT                           
                  END-IF                                                        
                  PERFORM 330-UPDATE-BP13K712 THRU 330-EXIT                     
               END-IF.                                                          
                                                                                
           PERFORM 200-READ-F740           THRU 200-EXIT.                       
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       310-GET-ALLOC-SCH.                                                       
      ******************************************************************        
                                                                                
           SET WS-ALLOC-PTR  TO 1.                                              
           SEARCH WS-ALLOC-SCH                                                  
               AT END MOVE 'XXX'             TO WS-K712-CDE-ALLOC-SCH           
             WHEN F740-CDE-ALLOC-SCH = WS-ALLOC-SCH-DESP(WS-ALLOC-PTR)          
                  MOVE WS-MAIN-SCH-CD(WS-ALLOC-PTR)                             
                    TO WS-K712-CDE-ALLOC-SCH.                                   
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       320-CAL-TOT-REG.                                                         
      ******************************************************************        
           EVALUATE F740-CDE-BALLOT-HOUSEHOLD                                   
           WHEN 'T'                                                             
           WHEN 'H'                                                             
           WHEN 'F'                                                             
              IF F740-NUM-PPS-ELIG-TAG = 'Y' OR 'E'                             
                 ADD 1      TO K712-NUM-REG-PPS                                 
              ELSE                                                              
                 IF (F740-NUM-MCPS NOT = SPACES AND LOW-VALUES) AND             
                    (F740-CDE-RESIDENT-HA1 NOT = 'H' AND 'M' AND 'P')           
                    ADD 1   TO K712-NUM-REG-MCPS-FT                             
                 END-IF                                                         
              END-IF                                                            
           WHEN 'G'                                                             
              IF F740-NUM-AST = 'Y'                                             
                 ADD 1      TO K712-NUM-REG-AST                                 
              ELSE                                                              
                 IF (F740-NUM-MCPS NOT = SPACES AND LOW-VALUES) AND             
                    (F740-CDE-RESIDENT-HA1 NOT = 'H' AND 'M' AND 'P')           
                    ADD 1   TO K712-NUM-REG-MCPS-ST                             
                 END-IF                                                         
              END-IF                                                            
           END-EVALUATE.                                                        
                                                                                
           MOVE WS-SYS-DATE TO K712-DTE-UPDATE.                                 
           MOVE WS-SYS-TIME TO K712-TME-SYSTEM.                                 
           MOVE 'P13CF81'   TO K712-USERID.                                     
                                                                                
       320-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       330-UPDATE-BP13K712.                                                     
      ******************************************************************        
           MOVE WS-K712-KEY-FLD TO K712-KEY-FLD.                                
                                                                                
           READ BP13K712 KEY IS K712-KEY-FLD.                                   
                                                                                
           IF WS-K712-STATUS = 00 OR 97                                         
              PERFORM 320-CAL-TOT-REG                                           
                 THRU 320-EXIT                                                  
              REWRITE BP13K712-REC                                              
              ADD 1 TO WS-K712-REWRITE                                          
           ELSE                                                                 
           IF WS-K712-STATUS = 23                                               
              MOVE SPACES TO BP13K712-REC                                       
              INITIALIZE BP13K712-REC                                           
              MOVE WS-K712-KEY-FLD TO K712-KEY-FLD                              
              PERFORM 320-CAL-TOT-REG                                           
                 THRU 320-EXIT                                                  
              WRITE BP13K712-REC                                                
              ADD 1 TO WS-K712-WRITE                                            
           ELSE                                                                 
              MOVE WS-K712-STATUS TO RETURN-CODE                                
              DISPLAY 'ERROR WRITING IN BP13K712, '                             
                       K712-KEY-FLD                                             
              GO TO 999-CLOSE-FILES.                                            
                                                                                
       330-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       999-CLOSE-FILES.                                                         
      ******************************************************************        
           CLOSE BP13F740                                                       
                 BP13F713                                                       
                 BP13K712.                                                      
                                                                                
           IF WS-K712-STATUS NOT = 00                                           
              MOVE WS-K712-STATUS TO RETURN-CODE                                
              DISPLAY 'ERROR CLOSING BP13K712, ' WS-K712-STATUS.                
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13CF81 ================*'.              
           DISPLAY ' NO. OF F740 RECORDS READ      = ' WS-F740-READ.            
           DISPLAY ' NO. OF K712 RECORDS WRITTEN   = ' WS-K712-WRITE.           
           DISPLAY ' NO. OF K712 RECORDS REWRITTEN = ' WS-K712-REWRITE.         
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
                                                                                
