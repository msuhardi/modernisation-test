       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC09.                                                 
       AUTHOR.        SHARON DUMDUM.                                            
       DATE-WRITTEN.  26 APRIL 2006.                                            
      **************************************************************            
      *  OBJECTIVES - TO UPDATE THE CPFRATE IN DBSS BY HDBREF.     *            
      *               (SUM UP INDIVIDUAL CPFRATE IN NCN MASTER     *            
      *                BY HDBREF BEFORE MATCHING PROCESS)          *            
      *                                                            *            
      *  SUBROUTINE     - NIL                                      *            
      *                                                            *            
      *  IDENTICAL PGM  - AB03C229                                 *            
      **************************************************************            
      *  REF NO     DATE    BY  AMENDMENTS/ENHANCEMENTS            *            
      * --------  -------- ---- -----------------------            *            
      * BP132772  26042006 SD10 NEW PROGRAM                        *            
      * BP133091  17102007 RB12 EXPAND KD00 LENGTH FR 1200 TO 1600 *            
      * BP135093  18122013 IMC1 CHANGE B04F003 TO AB02F503         *            
      **************************************************************            
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT AB02F503  ASSIGN TO    AB02F503.                              
                                                                                
           SELECT BP13KD00  ASSIGN TO    BP13FD00                               
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS KD00-KEY-FLD                        
                            FILE STATUS  IS KD00-STATUS                         
                            ACCESS MODE  IS RANDOM.                             
                                                                                
           SELECT BP13FD00  ASSIGN TO    BP13FD00.                              
                                                                                
           SELECT BP13FD72  ASSIGN TO    BP13FD72.                              
                                                                                
           SELECT BP13LC09  ASSIGN TO    BP13LC09.                              
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       COPY AB02F503.                                                           
                                                                                
       FD  BP13KD00                    LABEL RECORDS ARE STANDARD               
                                       BLOCK  CONTAINS    0 RECORDS             
                                       RECORD CONTAINS 1600 CHARACTERS.         
       COPY BP13KD00.                                                           
                                                                                
       FD  BP13FD00                    LABEL RECORDS ARE STANDARD               
                                       BLOCK  CONTAINS    0 RECORDS             
                                       RECORD CONTAINS 1600 CHARACTERS          
                                       RECORDING MODE IS F.                     
       COPY BP13FD00.                                                           
                                                                                
       FD  BP13FD72                    LABEL RECORDS ARE STANDARD               
                                       BLOCK  CONTAINS    0 RECORDS             
                                       RECORD CONTAINS  400 CHARACTERS          
                                       RECORDING MODE IS F.                     
       COPY BP13FD72.                                                           
                                                                                
       FD   BP13LC09                   BLOCK  CONTAINS    0 RECORDS             
                                       RECORD CONTAINS  132 CHARACTERS          
                                       LABEL  RECORD   IS   STANDARD            
                                       RECORDING MODE  IS   F.                  
       01  BP13LC09-REC      PIC X(132).                                        
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-F503-EOF                   PIC 9      VALUE ZERO.                 
       01  WS-FD00-EOF                   PIC 9      VALUE ZERO.                 
       01  WS-KD00-EOF                   PIC 9      VALUE ZERO.                 
       01  FD00-STATUS                   PIC 9(2)   VALUE ZERO.                 
       01  KD00-STATUS                   PIC 9(2)   VALUE ZERO.                 
       01  WS-COUNTERS.                                                         
           05  WS-F503-READ              PIC 9(7)   VALUE ZERO.                 
           05  WS-FD00-READ              PIC 9(7)   VALUE ZERO.                 
           05  WS-KD00-READ              PIC 9(7)   VALUE ZERO.                 
           05  WS-CNT-READ-KD00          PIC 9(7)   VALUE ZERO.                 
           05  WS-CNT-UPD-KD00           PIC 9(7)   VALUE ZERO.                 
           05  WS-FD72-WRITE             PIC 9(7)   VALUE ZERO.                 
           05  WS-MATCH                  PIC 9(6)   VALUE ZERO.                 
           05  WS-UNMATCH                PIC 9(6)   VALUE ZERO.                 
           05  WS-ZERORISE               PIC 9(6)   VALUE ZERO.                 
           05  WS-BYPASS                 PIC 9(6)   VALUE ZERO.                 
           05  WS-BYPASS-ZERORISE        PIC 9(6)   VALUE ZERO.                 
           05  WS-PAGE                   PIC 9(4)   VALUE ZERO.                 
           05  WS-SNO                    PIC 9(4)   VALUE ZERO.                 
           05  WS-LINE                   PIC 9(2)   VALUE 60.                   
                                                                                
       01  SAVE-HDB-REF.                                                        
           05  SAVE-SCHACC               PIC X(9)   VALUE SPACES.               
           05  SAVE-LESSEE               PIC X(2)   VALUE SPACES.               
                                                                                
       01  WS-CURR-DATE.                                                        
           05  WS-CURR-CYM.                                                     
               10  WS-CURR-CY            PIC 9(4)    VALUE ZEROES.              
               10  WS-CURR-MM            PIC 9(2)    VALUE ZEROES.              
           05  WS-CURR-DD                PIC 9(2)    VALUE ZEROES.              
                                                                                
       01  WS-PREV-DATE.                                                        
           05  WS-PREV-CYM.                                                     
               10  WS-PREV-CY            PIC 9(4)    VALUE ZEROES.              
               10  WS-PREV-MM            PIC 9(2)    VALUE ZEROES.              
                                                                                
       01  WS-OLD-CPF-RATE               PIC 9(5)V99  VALUE ZERO COMP-3.        
       01  WS-NEW-CPF-RATE               PIC 9(5)V99  VALUE ZERO COMP-3.        
                                                                                
       01  WS-FD72-REC.                                                         
           05  WS-FD72-NME-LESSEE1       PIC X(66)   VALUE SPACES.              
           05  WS-FD72-NUM-CPF-ACCT1     PIC X(9)    VALUE SPACES.              
           05  WS-FD72-AMT-CPF-RATE1     PIC 9(4)V99 VALUE ZEROES.              
           05  WS-FD72-NME-LESSEE2       PIC X(66)   VALUE SPACES.              
           05  WS-FD72-NUM-CPF-ACCT2     PIC X(9)    VALUE SPACES.              
           05  WS-FD72-AMT-CPF-RATE2     PIC 9(4)V99 VALUE ZEROES.              
           05  WS-FD72-NME-LESSEE3       PIC X(66)   VALUE SPACES.              
           05  WS-FD72-NUM-CPF-ACCT3     PIC X(9)    VALUE SPACES.              
           05  WS-FD72-AMT-CPF-RATE3     PIC 9(4)V99 VALUE ZEROES.              
           05  WS-FD72-NME-LESSEE4       PIC X(66)   VALUE SPACES.              
           05  WS-FD72-NUM-CPF-ACCT4     PIC X(9)    VALUE SPACES.              
           05  WS-FD72-AMT-CPF-RATE4     PIC 9(4)V99 VALUE ZEROES.              
           05  WS-FD72-AMT-CPF-TOTAL     PIC 9(5)V99 VALUE ZEROES.              
                                                                                
      *--------------------------------------------------------------           
      *   REPORT LAYOUT                                                         
      *--------------------------------------------------------------           
       01  LC09-PR-HEAD-01.                                                     
           05  LC09-RPTNAME        PIC X(8)      VALUE 'BP13LC09'.              
           05  FILLER              PIC X(5)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  LC09-DATE           PIC X(10).                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  LC09-PRT-PAGE       PIC ZZZZ9 .                                  
                                                                                
       01  LC09-PR-HEAD-02.                                                     
           05  FILLER              PIC X(47)     VALUE SPACES .                 
           05  FILLER              PIC X(56)     VALUE                          
               'REPORT ON UNMATCHED CPF'.                                       
                                                                                
       01  LC09-PR-HEAD-03.                                                     
           05  FILLER              PIC X(04)     VALUE                          
               'SNO'.                                                           
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE                          
               'REGN NO'.                                                       
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(11)     VALUE                          
               'SCH ACCT NO'.                                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE                          
               'CPF RATE1'.                                                     
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE                          
               'CPF RATE2'.                                                     
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE                          
               'CPF RATE3'.                                                     
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE                          
               'CPF RATE4'.                                                     
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE                          
               'TOTAL'.                                                         
                                                                                
       01  LC09-PR-HEAD-04.                                                     
           05  FILLER              PIC X(04)     VALUE                          
               '----'.                                                          
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE                          
               '--------'.                                                      
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(11)     VALUE                          
               '-----------'.                                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE                          
               '---------'.                                                     
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE                          
               '---------'.                                                     
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE                          
               '---------'.                                                     
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE                          
               '---------'.                                                     
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE                          
               '--------'.                                                      
                                                                                
       01  LC09-PR-DTL.                                                         
           05  LC09-PRT-SNO        PIC ZZZ9  VALUE ZEROES.                      
           05  FILLER              PIC X(05) VALUE SPACES.                      
           05  LC09-REGN-NO        PIC X(08) VALUE SPACES.                      
           05  FILLER              PIC X(05) VALUE SPACES.                      
           05  LC09-SCH-ACCT       PIC X(11) VALUE SPACES.                      
           05  FILLER              PIC X(04) VALUE SPACES.                      
           05  LC09-AMT-CPF-RATE1  PIC Z,ZZ9.99.                                
           05  FILLER              PIC X(04) VALUE SPACES.                      
           05  LC09-AMT-CPF-RATE2  PIC Z,ZZ9.99.                                
           05  FILLER              PIC X(04) VALUE SPACES.                      
           05  LC09-AMT-CPF-RATE3  PIC Z,ZZ9.99.                                
           05  FILLER              PIC X(04) VALUE SPACES.                      
           05  LC09-AMT-CPF-RATE4  PIC Z,ZZ9.99.                                
           05  FILLER              PIC X(03) VALUE SPACES.                      
           05  LC09-TOTAL-AMT-CPF-RATE                                          
                                   PIC ZZ,ZZ9.99.                               
                                                                                
                                                                                
      *===================*                                                     
       PROCEDURE DIVISION.                                                      
      *===================*                                                     
                                                                                
      *==========*                                                              
       0000-MAIN.                                                               
      *==========*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 2000-READ-NCN         THRU 2000-EXIT.                        
           PERFORM 3000-READ-BP13FD00    THRU 3000-EXIT.                        
           PERFORM 4000-MATCH            THRU 4000-EXIT                         
                   UNTIL FD00-NUM-SCH-ACCNT = HIGH-VALUE                        
                     AND SAVE-HDB-REF       = HIGH-VALUE.                       
           PERFORM 9999-CLOSE-FILES.                                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================*                                                        
       1000-OPEN-FILES.                                                         
      *================*                                                        
                                                                                
           OPEN INPUT   AB02F503                                                
                        BP13FD00                                                
                I-O     BP13KD00                                                
                OUTPUT  BP13FD72                                                
                        BP13LC09.                                               
                                                                                
           IF KD00-STATUS NOT = 00 AND 97                                       
              MOVE KD00-STATUS TO RETURN-CODE                                   
              DISPLAY 'OPEN FILE FAILED FOR BP13KD00 ' KD00-STATUS              
              GO TO 9999-CLOSE-FILES.                                           
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CURR-DATE.                     
                                                                                
           MOVE WS-CURR-CY  TO   WS-PREV-CY.                                    
           COMPUTE WS-PREV-MM =  WS-CURR-MM - 1.                                
           IF WS-PREV-MM = 0                                                    
              MOVE 12       TO   WS-PREV-MM                                     
              SUBTRACT 1    FROM WS-PREV-CY                                     
           END-IF.                                                              
                                                                                
           DISPLAY 'CURR-DTE :' WS-CURR-DATE.                                   
           DISPLAY 'PREV-DTE :' WS-PREV-DATE.                                   
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *==============*                                                          
       2000-READ-NCN.                                                           
      *==============*                                                          
                                                                                
           READ AB02F503                                                        
                AT END MOVE 1            TO WS-F503-EOF                         
                       MOVE HIGH-VALUE   TO SAVE-HDB-REF                        
                       GO TO 2000-EXIT.                                         
                                                                                
           IF F503-GEN-KEY = SPACES                                             
              GO TO 2000-READ-NCN.                                              
                                                                                
           MOVE F503-SCHACC         TO SAVE-SCHACC                              
           MOVE F503-LESSEE         TO SAVE-LESSEE.                             
           ADD 1 TO WS-F503-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *===================*                                                     
       3000-READ-BP13FD00.                                                      
      *===================*                                                     
                                                                                
           READ BP13FD00                                                        
                AT END MOVE 1            TO WS-FD00-EOF                         
                       MOVE HIGH-VALUE   TO FD00-NUM-SCH-ACCNT                  
                       GO TO 3000-EXIT.                                         
                                                                                
           ADD 1 TO WS-FD00-READ.                                               
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *===========*                                                             
       4000-MATCH.                                                              
      *===========*                                                             
                                                                                
           IF FD00-NUM-SCH-ACCNT < SAVE-HDB-REF                                 
              PERFORM 5000-ZERORISE-DBSS THRU 5000-EXIT                         
              PERFORM 3000-READ-BP13FD00 THRU 3000-EXIT                         
           ELSE                                                                 
              IF FD00-NUM-SCH-ACCNT = SAVE-HDB-REF                              
                 PERFORM 4100-PROCESS-NCN-REC  THRU 4100-EXIT                   
                 PERFORM 4300-MATCH-DBSS       THRU 4300-EXIT                   
                 PERFORM 3000-READ-BP13FD00    THRU 3000-EXIT                   
              ELSE                                                              
                 ADD 1 TO WS-UNMATCH                                            
                 PERFORM 2000-READ-NCN   THRU 2000-EXIT.                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=====================*                                                   
       4100-PROCESS-NCN-REC.                                                    
      *=====================*                                                   
                                                                                
           IF WS-F503-EOF = 1                                                   
              GO TO 4100-EXIT.                                                  
                                                                                
           IF SAVE-HDB-REF = FD00-NUM-SCH-ACCNT                                 
              ADD F503-INSTAL       TO   WS-NEW-CPF-RATE                        
              PERFORM 4200-MOVE-WS-FD72  THRU 4200-EXIT                         
              PERFORM 2000-READ-NCN      THRU 2000-EXIT                         
              GO TO 4100-PROCESS-NCN-REC.                                       
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *==================*                                                      
       4200-MOVE-WS-FD72.                                                       
      *==================*                                                      
                                                                                
           IF WS-FD72-NME-LESSEE1 = SPACES                                      
              MOVE F503-NAME               TO  WS-FD72-NME-LESSEE1              
              MOVE F503-CPFREF             TO  WS-FD72-NUM-CPF-ACCT1            
              MOVE F503-INSTAL             TO  WS-FD72-AMT-CPF-RATE1            
           ELSE                                                                 
              IF WS-FD72-NME-LESSEE2 = SPACES                                   
                 MOVE F503-NAME            TO  WS-FD72-NME-LESSEE2              
                 MOVE F503-CPFREF          TO  WS-FD72-NUM-CPF-ACCT2            
                 MOVE F503-INSTAL          TO  WS-FD72-AMT-CPF-RATE2            
              ELSE                                                              
                 IF WS-FD72-NME-LESSEE3 = SPACES                                
                    MOVE F503-NAME         TO  WS-FD72-NME-LESSEE3              
                    MOVE F503-CPFREF       TO  WS-FD72-NUM-CPF-ACCT3            
                    MOVE F503-INSTAL       TO  WS-FD72-AMT-CPF-RATE3            
                 ELSE                                                           
                    MOVE F503-NAME         TO  WS-FD72-NME-LESSEE4              
                    MOVE F503-CPFREF       TO  WS-FD72-NUM-CPF-ACCT4            
                    MOVE F503-INSTAL       TO  WS-FD72-AMT-CPF-RATE4.           
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================*                                                        
       4300-MATCH-DBSS.                                                         
      *================*                                                        
                                                                                
           ADD 1 TO WS-MATCH.                                                   
           MOVE FD00-AMT-CPF-RATE      TO WS-OLD-CPF-RATE.                      
                                                                                
           IF WS-NEW-CPF-RATE > FD00-AMT-INSTAL                                 
              PERFORM 4400-WRITE-FD72  THRU  4400-EXIT.                         
                                                                                
           MOVE WS-NEW-CPF-RATE        TO FD00-AMT-CPF-RATE                     
           MOVE '1'                    TO FD00-CDE-CPF-STATUS                   
           PERFORM 6000-UPDATE-BP13KD00   THRU 6000-EXIT                        
                                                                                
           MOVE ZEROES  TO WS-NEW-CPF-RATE.                                     
           MOVE SPACES  TO WS-FD72-REC.                                         
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================*                                                        
       4400-WRITE-FD72.                                                         
      *================*                                                        
                                                                                
           MOVE SPACES                   TO  BP13FD72-REC.                      
           INITIALIZE                        BP13FD72-REC.                      
                                                                                
           MOVE FD00-NUM-SCH-ACCNT       TO  FD72-NUM-HDB-REF.                  
           MOVE FD00-AMT-INSTAL          TO  FD72-AMT-INST.                     
           MOVE WS-OLD-CPF-RATE          TO  FD72-AMT-CPF-RATE.                 
           MOVE WS-NEW-CPF-RATE          TO  FD72-AMT-CPF-RATE-NEW.             
                                                                                
           IF WS-FD72-NME-LESSEE1 NOT = SPACES                                  
              MOVE WS-FD72-NME-LESSEE1    TO FD72-NME-LESSEE1                   
              MOVE WS-FD72-NUM-CPF-ACCT1  TO FD72-NUM-CPF-ACCT1                 
              MOVE WS-FD72-AMT-CPF-RATE1  TO FD72-AMT-CPF-RATE1.                
                                                                                
           IF WS-FD72-NME-LESSEE2 NOT = SPACES                                  
              MOVE WS-FD72-NME-LESSEE2    TO FD72-NME-LESSEE2                   
              MOVE WS-FD72-NUM-CPF-ACCT2  TO FD72-NUM-CPF-ACCT2                 
              MOVE WS-FD72-AMT-CPF-RATE2  TO FD72-AMT-CPF-RATE2.                
                                                                                
           IF WS-FD72-NME-LESSEE3 NOT = SPACES                                  
              MOVE WS-FD72-NME-LESSEE3    TO FD72-NME-LESSEE3                   
              MOVE WS-FD72-NUM-CPF-ACCT3  TO FD72-NUM-CPF-ACCT3                 
              MOVE WS-FD72-AMT-CPF-RATE3  TO FD72-AMT-CPF-RATE3.                
                                                                                
           IF WS-FD72-NME-LESSEE4 NOT = SPACES                                  
              MOVE WS-FD72-NME-LESSEE4    TO FD72-NME-LESSEE4                   
              MOVE WS-FD72-NUM-CPF-ACCT4  TO FD72-NUM-CPF-ACCT4                 
              MOVE WS-FD72-AMT-CPF-RATE4  TO FD72-AMT-CPF-RATE4.                
                                                                                
           WRITE BP13FD72-REC.                                                  
           ADD 1 TO WS-FD72-WRITE.                                              
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *===================*                                                     
       5000-ZERORISE-DBSS.                                                      
      *===================*                                                     
                                                                                
           IF FD00-CDE-ACCT-STAT = 'C'                                          
              MOVE '0' TO FD00-CDE-CPF-STATUS                                   
              PERFORM 6000-UPDATE-BP13KD00   THRU 6000-EXIT                     
              PERFORM 7000-PRINT-UNMATCHED   THRU 7000-EXIT                     
              ADD 1 TO WS-ZERORISE                                              
           ELSE                                                                 
              ADD 1 TO WS-BYPASS-ZERORISE.                                      
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=====================*                                                   
       6000-UPDATE-BP13KD00.                                                    
      *=====================*                                                   
                                                                                
           MOVE SPACES             TO KD00-KEY-FLD.                             
           MOVE FD00-NUM-REGN      TO KD00-NUM-REGN.                            
           READ BP13KD00 KEY IS KD00-KEY-FLD.                                   
                                                                                
           IF KD00-STATUS = 0                                                   
              ADD 1 TO WS-CNT-READ-KD00                                         
           ELSE                                                                 
              IF KD00-STATUS = 23                                               
                 DISPLAY 'RECORD NOT FOUND IN BP13KD00 ' KD00-NUM-REGN          
              ELSE                                                              
                 DISPLAY 'READ FAILED FOR BP13KD00 ' KD00-NUM-REGN              
                         'STATUS IS ' KD00-STATUS                               
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE FD00-AMT-CPF-RATE    TO KD00-AMT-CPF-RATE.                      
           MOVE FD00-CDE-CPF-STATUS  TO KD00-CDE-CPF-STATUS.                    
                                                                                
           REWRITE BP13KD00-REC.                                                
           IF KD00-STATUS = 0                                                   
              ADD 1 TO WS-CNT-UPD-KD00                                          
           ELSE                                                                 
              DISPLAY 'REWRITE FAILED FOR BP13KD00 ' KD00-NUM-REGN              
                      'STATUS IS ' KD00-STATUS                                  
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=====================*                                                   
       7000-PRINT-UNMATCHED.                                                    
      *=====================*                                                   
                                                                                
           IF WS-LINE > 50                                                      
              ADD 1 TO WS-PAGE                                                  
              MOVE WS-PAGE TO LC09-PRT-PAGE                                     
              WRITE BP13LC09-REC FROM LC09-PR-HEAD-01  AFTER PAGE               
              WRITE BP13LC09-REC FROM LC09-PR-HEAD-02  AFTER 2                  
              WRITE BP13LC09-REC FROM LC09-PR-HEAD-03  AFTER 2                  
              WRITE BP13LC09-REC FROM LC09-PR-HEAD-04                           
              MOVE SPACES TO BP13LC09-REC                                       
              WRITE BP13LC09-REC                                                
              MOVE  5 TO WS-LINE                                                
           END-IF.                                                              
                                                                                
           MOVE FD00-NUM-REGN         TO LC09-REGN-NO.                          
           MOVE FD00-NUM-SCH-ACCNT    TO LC09-SCH-ACCT.                         
           MOVE WS-FD72-AMT-CPF-RATE1 TO LC09-AMT-CPF-RATE1.                    
           MOVE WS-FD72-AMT-CPF-RATE2 TO LC09-AMT-CPF-RATE2.                    
           MOVE WS-FD72-AMT-CPF-RATE3 TO LC09-AMT-CPF-RATE3.                    
           MOVE WS-FD72-AMT-CPF-RATE4 TO LC09-AMT-CPF-RATE4.                    
           COMPUTE WS-FD72-AMT-CPF-TOTAL =                                      
                   WS-FD72-AMT-CPF-RATE1 +                                      
                   WS-FD72-AMT-CPF-RATE2 +                                      
                   WS-FD72-AMT-CPF-RATE3 +                                      
                   WS-FD72-AMT-CPF-RATE4.                                       
           MOVE WS-FD72-AMT-CPF-TOTAL TO LC09-TOTAL-AMT-CPF-RATE.               
                                                                                
           ADD 1              TO WS-SNO.                                        
           MOVE WS-SNO        TO LC09-PRT-SNO.                                  
           WRITE BP13LC09-REC FROM LC09-PR-DTL.                                 
           ADD 1 TO WS-LINE.                                                    
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=================*                                                       
       9999-CLOSE-FILES.                                                        
      *=================*                                                       
                                                                                
           DISPLAY  SPACES.                                                     
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13C229'.                                     
           DISPLAY  SPACES.                                                     
           DISPLAY 'TOTAL AB02F503 RECS READ          = ' WS-F503-READ.         
           DISPLAY 'TOTAL BP13FD00 RECS READ         = ' WS-FD00-READ.          
           DISPLAY 'TOTAL BP13KD00 RECS READ         = '                        
                    WS-CNT-READ-KD00.                                           
           DISPLAY 'TOTAL BP13KD00 RECS UPDATED      = ' WS-CNT-UPD-KD00        
           DISPLAY 'TOTAL BP13FD72 RECS WRITE        = ' WS-FD72-WRITE.         
           DISPLAY  SPACES.                                                     
           DISPLAY 'TOTAL DBSS RECS ZERORISE          = ' WS-ZERORISE.          
           DISPLAY 'TOTAL DBSS RECS BYPASS (NS)       = ' WS-BYPASS.            
           DISPLAY 'TOTAL DBSS RECS BYPASS (AS NOT C) = '                       
                    WS-BYPASS-ZERORISE.                                         
           DISPLAY 'DBSS RECS MATCH WITH NCN          = ' WS-MATCH.             
           DISPLAY 'NCN RECS NOT FOUND IN DBSS        = ' WS-UNMATCH.           
                                                                                
           CLOSE  AB02F503                                                      
                  BP13FD00                                                      
                  BP13KD00                                                      
                  BP13FD72                                                      
                  BP13LC09.                                                     
                                                                                
           IF KD00-STATUS NOT = 0                                               
              DISPLAY 'CLOSE FAILED FOR BP13KD00 ' KD00-STATUS.                 
                                                                                
           STOP RUN.                                                            
                                                                                
