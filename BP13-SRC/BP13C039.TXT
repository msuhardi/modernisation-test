       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C039.                                                 
       AUTHOR.        MTDULLONA.                                                
      *DATE-WRITTEN.  FEB2004.                                                  
      ***************************************************************           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      ***************************************************************           
      * OBJECTIVE: PRINT DEBARMENT REPORT FOR MARRIED CHILD PRIORITY*           
      *            (NO DEBARMENT)                                   *           
      *-------------------------------------------------------------*           
      * INPUT FILES: BP13F310                                       *           
      *              BP13K853                                       *           
      *              BP13K800                                       *           
      *              BP13K820                                       *           
      *              BP04K010                                       *           
      * OUTPUT FILE: BP13L039 (REPORT)                              *           
      *-------------------------------------------------------------*           
      * CHG REF  OIC  DATE    DESCRIPTION                           *           
      *--------- ---  ------  --------------------------------------*           
      * BP132463 MTD  090204  NEW PROGRAM.                          *           
      * BP132496 PGS1 120404  TO INCLUDE THE SCH-ACC-NO FOR CHECKING*           
      *                       THE NRIC IN K010 AND CHECK ALL THE ICS*           
      ***************************************************************           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS WS-K800-STATUS.                     
                                                                                
           SELECT BP13K853  ASSIGN       TO BP13K853                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K853-KEY-FLD                        
                            FILE STATUS  IS WS-K853-STATUS.                     
                                                                                
           SELECT BP13K820  ASSIGN       TO BP13K820                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K820-KEY-FLD                        
                            FILE STATUS  IS WS-K820-STATUS.                     
                                                                                
           SELECT BP04K010  ASSIGN       TO BP04K010                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K010-PRIME-KEY                      
                            FILE STATUS  IS WS-K010-STATUS.                     
                                                                                
           SELECT BP13F310  ASSIGN TO BP13F310.                                 
                                                                                
           SELECT BP13L039  ASSIGN TO BP13L039.                                 
                                                                                
       DATA DIVISION.                                                           
      *--------------------------------------------------------------           
       FILE SECTION.                                                            
      *--------------------------------------------------------------           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K853                                                             
           RECORD CONTAINS 250 CHARACTERS.                                      
       COPY BP13K853.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BP04K010                                                             
           RECORD CONTAINS 300 CHARACTERS.                                      
       COPY P04K010.                                                            
                                                                                
       FD  BP13F310                                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       COPY BP13F310.                                                           
                                                                                
       FD  BP13L039                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS OMITTED                                                
           RECORDING MODE IS F.                                                 
       01  BP13L039-REC                   PIC X(132).                           
                                                                                
      *--------------------------------------------------------------           
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------           
       01  WS-WORK-VAR.                                                         
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-DATE         PIC X(08) VALUE SPACES.                  
               10  FILLER              PIC X(13) VALUE SPACES.                  
           05  WS-DISP-DATE.                                                    
               10  WS-DISP-DD          PIC X(02) VALUE SPACES.                  
               10  FILLER              PIC X(01) VALUE '/'.                     
               10  WS-DISP-MM          PIC X(02) VALUE SPACES.                  
               10  FILLER              PIC X(01) VALUE '/'.                     
               10  WS-DISP-CCYY        PIC X(04) VALUE SPACES.                  
           05  WS-SUB                  PIC 9(02) VALUE ZEROES.                  
           05  WS-K853-TABLE           OCCURS 10.                               
               10  WS-K853-NUM-NRIC    PIC X(09).                               
               10  WS-K853-NME-OCCP    PIC X(66).                               
               10  WS-K853-DEB-REF     PIC X(13).                               
           05  WS-RETCODE              PIC -ZZZZ.                               
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K800-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K853-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K820-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K010-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-END-OF-F310-SW       PIC X(01) VALUE SPACES.                  
               88  C-END-OF-F310       VALUE 'Y'.                               
           05  WS-K800-REGN-FND-SW     PIC X(01) VALUE SPACES.                  
               88  C-K800-REGN-FND     VALUE 'Y'.                               
           05  WS-END-OF-K853-SW       PIC X(01) VALUE SPACES.                  
               88  C-END-OF-K853       VALUE 'Y'.                               
           05  WS-END-OF-K010-SW       PIC X(01) VALUE SPACES.                  
               88  C-END-OF-K010       VALUE 'Y'.                               
           05  WS-PRT-REPORT-SW        PIC X(01) VALUE SPACES.                  
               88  C-PRT-REPORT        VALUE 'Y'.                               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F310-READ-CTR        PIC 9(06) VALUE ZEROES.                  
           05  WS-K800-READ-CTR        PIC 9(06) VALUE ZEROES.                  
           05  WS-PRT-CTR              PIC 9(06) VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(02) VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(04) VALUE ZEROES.                  
           05  WS-SNO-CTR              PIC 9(04) VALUE ZEROES.                  
                                                                                
      *--------------------------------------------------------------           
      *   REPORT LAYOUT                                                         
      *--------------------------------------------------------------           
       01 L039-HEADER-1.                                                        
          05 FILLER              PIC X(08)  VALUE 'BP13L039'.                   
          05 FILLER              PIC X(05)  VALUE SPACES .                      
          05 FILLER              PIC X(08)  VALUE 'HDBCAT3'.                    
          05 FILLER              PIC X(22)  VALUE SPACES.                       
          05 FILLER              PIC X(40)  VALUE                               
             'S Y S T E M   O F   C O M M I T M E N T'.                         
          05 FILLER              PIC X(28)  VALUE SPACES.                       
          05 FILLER              PIC X(07)  VALUE 'DATE :'.                     
          05 L039-DATE           PIC X(10)  VALUE SPACES.                       
                                                                                
       01 L039-HEADER-2.                                                        
          05 FILLER              PIC X(41)  VALUE SPACES .                      
          05 FILLER              PIC X(43)  VALUE                               
             'DEBARMENT REPORT FOR MARRIED CHILD PRIORITY'.                     
          05 FILLER              PIC X(27)  VALUE SPACES .                      
          05 FILLER              PIC X(07)  VALUE 'PAGE : '.                    
          05 L039-PAGE           PIC ZZZ9 .                                     
                                                                                
       01 L039-HEADER-2A.                                                       
          05 FILLER              PIC X(22)  VALUE                               
             'S/NO  NT    FT   QUEUE'.                                          
          05 FILLER              PIC X(110) VALUE SPACES .                      
                                                                                
       01 L039-HEADER-3.                                                        
          05 FILLER              PIC X(55)  VALUE                               
             '     REGN NO. NAME OF APPLICANTS          NRIC NO.  NAM'.         
          05 FILLER              PIC X(55)  VALUE                               
             'E OF PARENTS/MARRIED CHILD NRIC NO.  DEB-REF       RSN '.         
          05 FILLER              PIC X(17)  VALUE                               
             'EFF-DATE   PERIOD'.                                               
                                                                                
       01 L039-HEADER-3A.                                                       
          05 FILLER              PIC X(15)  VALUE                               
             '   SCH-ACC-NO'.                                                   
          05 FILLER              PIC X(115) VALUE SPACES .                      
                                                                                
       01 L039-DETAIL-0.                                                        
          10 L039-SNO            PIC ZZ99.                                      
          10 FILLER              PIC X(02)  VALUE SPACES.                       
          10 L039-NEW-TOWN       PIC X(03)  VALUE SPACES.                       
          10 FILLER              PIC X(03)  VALUE SPACES.                       
          10 L039-FLAT-TYPE      PIC X(02)  VALUE SPACES.                       
          10 FILLER              PIC X(03)  VALUE SPACES.                       
          10 L039-QUEUE          PIC X(05)  VALUE SPACES.                       
          10 FILLER              PIC X(110) VALUE SPACES.                       
                                                                                
       01 L039-DETAIL.                                                          
          05 L039-HDB-REF.                                                      
             10 FILLER           PIC X(02).                                     
             10 L039-SCH-ACC     PIC X(11).                                     
          05 L039-SEQ-REGN  REDEFINES L039-HDB-REF.                             
             10 FILLER           PIC X(05).                                     
             10 L039-NUM-REGN    PIC X(08).                                     
          05 FILLER              PIC X(01)  VALUE SPACES.                       
          05 L039-NME-APPL       PIC X(27)  VALUE SPACES.                       
          05 FILLER              PIC X(01)  VALUE SPACES.                       
          05 L039-NUM-NRIC-APPL  PIC X(09)  VALUE SPACES.                       
          05 FILLER              PIC X(01)  VALUE SPACES.                       
          05 L039-NME-MCP        PIC X(27)  VALUE SPACES.                       
          05 FILLER              PIC X(03)  VALUE SPACES.                       
          05 L039-NUM-NRIC-MCP   PIC X(09)  VALUE SPACES.                       
          05 FILLER              PIC X(01)  VALUE SPACES.                       
          05 L039-DEB-REF        PIC X(13)  VALUE SPACES.                       
          05 FILLER              PIC X(01)  VALUE SPACES.                       
          05 L039-DEB-CODE       PIC X(02)  VALUE SPACES.                       
          05 FILLER              PIC X(02)  VALUE SPACES.                       
          05 L039-EFF-DATE       PIC X(10)  VALUE SPACES.                       
          05 FILLER              PIC X(01)  VALUE SPACES.                       
          05 L039-PERIOD         PIC X(03)  VALUE SPACES.                       
                                                                                
       01  L039-LINE             PIC X(132)    VALUE ALL '-'.                   
                                                                                
      *--------------------------------------------------------------           
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------------           
                                                                                
      *--------------------------------*                                        
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 1000-INITIALIZATION    THRU 1000-EXIT.                       
           PERFORM 3000-PROCESS-RECORDS   THRU 3000-EXIT                        
             UNTIL C-END-OF-F310.                                               
           PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       1000-INITIALIZATION.                                                     
      *--------------------------------*                                        
                                                                                
           OPEN INPUT    BP13F310                                               
                         BP13K800                                               
                         BP13K820                                               
                         BP13K853                                               
                         BP04K010                                               
                OUTPUT   BP13L039.                                              
                                                                                
           IF WS-K853-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR-BP13K853-STATUS ' WS-K853-STATUS              
              MOVE WS-K853-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR-BP13K800-STATUS ' WS-K800-STATUS              
              MOVE WS-K800-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR-BP13K820-STATUS ' WS-K820-STATUS              
              MOVE WS-K820-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR-BP04K010-STATUS ' WS-K010-STATUS              
              MOVE WS-K010-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-SYSTEM-DATE.                       
           MOVE WS-SYS-DATE(1:4)       TO WS-DISP-CCYY.                         
           MOVE WS-SYS-DATE(5:2)       TO WS-DISP-MM.                           
           MOVE WS-SYS-DATE(7:2)       TO WS-DISP-DD.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3000-PROCESS-RECORDS.                                                    
      *--------------------------------*                                        
                                                                                
           READ BP13F310 AT END                                                 
                MOVE 'Y'     TO WS-END-OF-F310-SW                               
                GO TO 3000-EXIT.                                                
                                                                                
           ADD 1   TO WS-F310-READ-CTR.                                         
                                                                                
           MOVE SPACES          TO WS-K800-REGN-FND-SW.                         
           MOVE SPACES          TO BP13K800-MASTER.                             
           INITIALIZE              BP13K800-MASTER.                             
           MOVE F310-REGN-NO    TO K800-NUM-REGN.                               
           PERFORM 3700-READ-BP13K800         THRU 3700-EXIT.                   
           IF C-K800-REGN-FND                                                   
              IF (K800-NUM-ALLO-SCHEME = 'MCP') AND                             
                 (K800-DTE-BALLOT = '200311') AND                               
                 (K800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES)                 
                 ADD 1        TO WS-K800-READ-CTR                               
                 PERFORM 3100-CHECK-RECORD         THRU 3100-EXIT               
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3100-CHECK-RECORD.                                                       
      *--------------------------------*                                        
                                                                                
      **-- READ BP13K853 TO GET PARENT-CHILD                                    
           MOVE SPACES                   TO WS-END-OF-K853-SW                   
                                            WS-PRT-REPORT-SW.                   
           PERFORM VARYING WS-SUB FROM 1 BY 1 UNTIL WS-SUB > 10                 
              MOVE SPACES        TO WS-K853-NME-OCCP (WS-SUB)                   
                                    WS-K853-NUM-NRIC (WS-SUB)                   
                                    WS-K853-DEB-REF (WS-SUB)                    
           END-PERFORM.                                                         
                                                                                
           MOVE ZEROES                   TO WS-SUB.                             
                                                                                
           MOVE SPACES                   TO K853-KEY-FLD.                       
           MOVE K800-NUM-REGN            TO K853-NUM-REGN.                      
           PERFORM 3110-START-BP13K853      THRU 3110-EXIT.                     
           PERFORM 3120-READNXT-BP13K853    THRU 3120-EXIT                      
             UNTIL K800-NUM-REGN NOT = K853-NUM-REGN                            
                OR C-END-OF-K853                                                
                OR WS-SUB > 10.                                                 
                                                                                
           IF C-PRT-REPORT                                                      
              ADD 1   TO WS-PRT-CTR                                             
              PERFORM 3300-PRINT-REPORT       THRU 3300-EXIT                    
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3110-START-BP13K853.                                                     
      *--------------------------------*                                        
                                                                                
           START BP13K853 KEY NOT LESS THAN K853-KEY-FLD.                       
           EVALUATE WS-K853-STATUS                                              
              WHEN 00                                                           
              WHEN 22                                                           
                   CONTINUE                                                     
              WHEN 10                                                           
              WHEN 23                                                           
                   DISPLAY 'RECORD NOT FOUND IN BP13K853, KEY: '                
                            K853-KEY-FLD                                        
                   MOVE 'Y'        TO WS-END-OF-K853-SW                         
                   MOVE SPACES     TO BP13K853-REC                              
              WHEN OTHER                                                        
                   DISPLAY 'START ERROR BP13K853,STATUS ' WS-K853-STATUS        
                   MOVE WS-K853-STATUS        TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3120-READNXT-BP13K853.                                                   
      *--------------------------------*                                        
                                                                                
           READ BP13K853 NEXT AT END                                            
                MOVE 'Y'     TO WS-END-OF-K853-SW                               
                GO TO 3120-EXIT.                                                
                                                                                
           EVALUATE WS-K853-STATUS                                              
              WHEN 00                                                           
              WHEN 22                                                           
                   IF K800-NUM-REGN = K853-NUM-REGN                             
                      PERFORM 3200-CHECK-BP04K010    THRU 3200-EXIT             
                   END-IF                                                       
              WHEN 10                                                           
              WHEN 23                                                           
                   MOVE 'Y'       TO WS-END-OF-K853-SW                          
              WHEN OTHER                                                        
                   DISPLAY 'READ ERROR BP13K853,STATUS ' WS-K853-STATUS         
                   MOVE WS-K853-STATUS        TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3120-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3200-CHECK-BP04K010.                                                     
      *--------------------------------*                                        
                                                                                
           MOVE SPACES         TO WS-END-OF-K010-SW.                            
                                                                                
           MOVE SPACES         TO K010-PRIME-KEY.                               
           MOVE K853-NUM-NRIC  TO K010-PRIME-KEY(1:9).                          
           PERFORM 3210-START-BP04K010      THRU 3210-EXIT.                     
           PERFORM 3220-READNXT-BP04K010    THRU 3220-EXIT                      
             UNTIL K853-NUM-NRIC NOT = K010-PRIME-KEY(1:9)                      
                OR C-END-OF-K010.                                               
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------*                                        
       3210-START-BP04K010.                                                     
      *--------------------------------*                                        
                                                                                
           START BP04K010 KEY NOT LESS THAN K010-PRIME-KEY.                     
           EVALUATE WS-K010-STATUS                                              
              WHEN 00                                                           
              WHEN 22                                                           
                   CONTINUE                                                     
              WHEN 10                                                           
              WHEN 23                                                           
                   DISPLAY 'RECORD NOT FOUND IN BP04K010, KEY: '                
                            K010-PRIME-KEY                                      
                   MOVE 'Y'        TO WS-END-OF-K010-SW                         
                   MOVE SPACES     TO P04K010-REC                               
              WHEN OTHER                                                        
                   DISPLAY 'START ERROR BP04K010,STATUS ' WS-K010-STATUS        
                   MOVE WS-K010-STATUS        TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3220-READNXT-BP04K010.                                                   
      *--------------------------------*                                        
                                                                                
           READ BP04K010 NEXT AT END                                            
                MOVE 'Y'     TO WS-END-OF-K010-SW                               
                GO TO 3220-EXIT.                                                
                                                                                
           EVALUATE WS-K010-STATUS                                              
              WHEN 00                                                           
              WHEN 22                                                           
                   IF K853-NUM-NRIC = K010-PRIME-KEY(1:9)                       
                      IF (K853-NUM-NRIC = K010-PRIME-KEY(1:9))   AND            
                         (K010-REASON-CODE NOT = 'MA')           AND            
                         (K800-NUM-SCH-ACC(1:9) = K010-EX-SCH-ACCTNO)           
                                                                                
                         MOVE 'Y'        TO WS-PRT-REPORT-SW                    
                         PERFORM 3230-SAVE-K853        THRU 3230-EXIT           
                      END-IF                                                    
                   ELSE                                                         
                      MOVE 'Y'       TO WS-END-OF-K010-SW                       
                   END-IF                                                       
              WHEN 10                                                           
              WHEN 23                                                           
                   MOVE 'Y'       TO WS-END-OF-K010-SW                          
              WHEN OTHER                                                        
                   DISPLAY 'READ ERROR BP04K010,STATUS ' WS-K010-STATUS         
                   MOVE WS-K010-STATUS        TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3220-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3230-SAVE-K853.                                                          
      *--------------------------------*                                        
                                                                                
           ADD 1       TO WS-SUB.                                               
           MOVE K853-NUM-NRIC     TO WS-K853-NUM-NRIC (WS-SUB).                 
           MOVE K853-NME-OCCP     TO WS-K853-NME-OCCP (WS-SUB).                 
           MOVE K853-DEB-REF-NO   TO WS-K853-DEB-REF (WS-SUB).                  
                                                                                
       3230-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3300-PRINT-REPORT.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 3400-CHECK-LINE-CTR          THRU 3400-EXIT.                 
                                                                                
           MOVE SPACES    TO L039-DETAIL.                                       
           WRITE BP13L039-REC  FROM L039-DETAIL.                                
           ADD 1          TO WS-LINE-CTR.                                       
                                                                                
           PERFORM 3400-CHECK-LINE-CTR          THRU 3400-EXIT.                 
                                                                                
           MOVE SPACES                TO L039-DETAIL.                           
           ADD 1                      TO WS-SNO-CTR.                            
           MOVE WS-SNO-CTR            TO L039-SNO.                              
           MOVE K800-NUM-NEW-TOWN     TO L039-NEW-TOWN.                         
           MOVE K800-NUM-FLAT-TYPE    TO L039-FLAT-TYPE.                        
           MOVE K800-NUM-NT-FT-QUEUE  TO L039-QUEUE.                            
                                                                                
           WRITE BP13L039-REC  FROM L039-DETAIL-0.                              
           ADD 1          TO WS-LINE-CTR.                                       
                                                                                
           PERFORM 3400-CHECK-LINE-CTR          THRU 3400-EXIT.                 
                                                                                
           MOVE SPACES                TO L039-DETAIL.                           
           MOVE K800-NUM-REGN         TO L039-NUM-REGN.                         
           MOVE K800-NUM-NRIC1        TO L039-NUM-NRIC-APPL.                    
      **-- GET NAME OF APPLICANT/MCP (1)                                        
           MOVE SPACES                TO K820-KEY-FLD.                          
           MOVE K800-NUM-REGN         TO K820-NUM-REGN.                         
           MOVE K800-NUM-NRIC1        TO K820-NUM-NRIC                          
           PERFORM 3600-READ-BP13K820         THRU 3600-EXIT.                   
           MOVE K820-NME-OCCP         TO L039-NME-APPL.                         
                                                                                
           MOVE WS-K853-NUM-NRIC(1)   TO L039-NUM-NRIC-MCP                      
           MOVE WS-K853-NME-OCCP(1)   TO L039-NME-MCP.                          
           MOVE WS-K853-DEB-REF(1)    TO L039-DEB-REF.                          
                                                                                
           WRITE BP13L039-REC  FROM L039-DETAIL.                                
           ADD 1          TO WS-LINE-CTR.                                       
                                                                                
      **-- GET NAME OF APPLICANT/MCP (2)                                        
           MOVE SPACES                   TO L039-DETAIL.                        
           MOVE K800-NUM-SCH-ACC         TO L039-SCH-ACC.                       
           IF K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES                        
              MOVE SPACES                TO K820-KEY-FLD                        
              MOVE K800-NUM-REGN         TO K820-NUM-REGN                       
              MOVE K800-NUM-NRIC2        TO K820-NUM-NRIC                       
              PERFORM 3600-READ-BP13K820         THRU 3600-EXIT                 
              MOVE K800-NUM-NRIC2        TO L039-NUM-NRIC-APPL                  
              MOVE K820-NME-OCCP         TO L039-NME-APPL                       
           END-IF.                                                              
           IF WS-K853-NUM-NRIC(2) NOT = SPACES AND LOW-VALUES                   
              MOVE WS-K853-NUM-NRIC(2)   TO L039-NUM-NRIC-MCP                   
              MOVE WS-K853-NME-OCCP(2)   TO L039-NME-MCP                        
              MOVE WS-K853-DEB-REF(2)    TO L039-DEB-REF                        
           END-IF.                                                              
           IF L039-DETAIL NOT = SPACES AND LOW-VALUES                           
              PERFORM 3400-CHECK-LINE-CTR      THRU 3400-EXIT                   
              WRITE BP13L039-REC  FROM L039-DETAIL                              
              ADD 1          TO WS-LINE-CTR                                     
           END-IF.                                                              
                                                                                
      **-- GET NAME OF APPLICANT/MCP (3)                                        
           MOVE SPACES                   TO L039-DETAIL                         
           IF K800-NUM-NRIC3 NOT = SPACES AND LOW-VALUES                        
              MOVE SPACES                TO K820-KEY-FLD                        
              MOVE K800-NUM-REGN         TO K820-NUM-REGN                       
              MOVE K800-NUM-NRIC3        TO K820-NUM-NRIC                       
              PERFORM 3600-READ-BP13K820         THRU 3600-EXIT                 
              MOVE K800-NUM-NRIC3        TO L039-NUM-NRIC-APPL                  
              MOVE K820-NME-OCCP         TO L039-NME-APPL                       
           END-IF.                                                              
           IF WS-K853-NUM-NRIC(3) NOT = SPACES AND LOW-VALUES                   
              MOVE WS-K853-NUM-NRIC(3)   TO L039-NUM-NRIC-MCP                   
              MOVE WS-K853-NME-OCCP(3)   TO L039-NME-MCP                        
              MOVE WS-K853-DEB-REF(3)    TO L039-DEB-REF                        
           END-IF.                                                              
           IF L039-DETAIL NOT = SPACES AND LOW-VALUES                           
              PERFORM 3400-CHECK-LINE-CTR      THRU 3400-EXIT                   
              WRITE BP13L039-REC  FROM L039-DETAIL                              
              ADD 1          TO WS-LINE-CTR                                     
           END-IF.                                                              
                                                                                
      **-- GET NAME OF APPLICANT/MCP (4)                                        
           MOVE SPACES                   TO L039-DETAIL                         
           IF K800-NUM-NRIC4 NOT = SPACES AND LOW-VALUES                        
              MOVE SPACES                TO K820-KEY-FLD                        
              MOVE K800-NUM-REGN         TO K820-NUM-REGN                       
              MOVE K800-NUM-NRIC4        TO K820-NUM-NRIC                       
              PERFORM 3600-READ-BP13K820         THRU 3600-EXIT                 
              MOVE K800-NUM-NRIC4        TO L039-NUM-NRIC-APPL                  
              MOVE K820-NME-OCCP         TO L039-NME-APPL                       
           END-IF.                                                              
           IF WS-K853-NUM-NRIC(4) NOT = SPACES AND LOW-VALUES                   
              MOVE WS-K853-NUM-NRIC(4)   TO L039-NUM-NRIC-MCP                   
              MOVE WS-K853-NME-OCCP(4)   TO L039-NME-MCP                        
              MOVE WS-K853-DEB-REF(4)    TO L039-DEB-REF                        
           END-IF.                                                              
           IF L039-DETAIL NOT = SPACES AND LOW-VALUES                           
              PERFORM 3400-CHECK-LINE-CTR      THRU 3400-EXIT                   
              WRITE BP13L039-REC  FROM L039-DETAIL                              
              ADD 1          TO WS-LINE-CTR                                     
           END-IF.                                                              
                                                                                
      **-- GET REMAINING PARENT/CHILD                                           
           MOVE SPACES                   TO L039-DETAIL                         
           PERFORM VARYING WS-SUB FROM 5 BY 1 UNTIL WS-SUB > 10                 
              IF WS-K853-NUM-NRIC(WS-SUB) NOT = SPACES AND LOW-VALUES           
                 PERFORM 3400-CHECK-LINE-CTR      THRU 3400-EXIT                
                 MOVE SPACES                   TO L039-DETAIL                   
                 MOVE WS-K853-NUM-NRIC(WS-SUB) TO L039-NUM-NRIC-MCP             
                 MOVE WS-K853-NME-OCCP(WS-SUB) TO L039-NME-MCP                  
                 MOVE WS-K853-DEB-REF(WS-SUB)  TO L039-DEB-REF                  
                 WRITE BP13L039-REC  FROM L039-DETAIL                           
                 ADD 1          TO WS-LINE-CTR                                  
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3400-CHECK-LINE-CTR.                                                     
      *--------------------------------*                                        
                                                                                
           IF WS-LINE-CTR > 55  OR                                              
              WS-LINE-CTR = 0                                                   
              PERFORM 3500-PRINT-PAGE-BREAK   THRU 3500-EXIT                    
           END-IF.                                                              
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3500-PRINT-PAGE-BREAK.                                                   
      *--------------------------------*                                        
                                                                                
           ADD   1   TO WS-PAGE-CTR.                                            
           MOVE WS-PAGE-CTR    TO L039-PAGE.                                    
           MOVE WS-DISP-DATE   TO L039-DATE.                                    
           WRITE BP13L039-REC  FROM L039-HEADER-1 AFTER PAGE.                   
           WRITE BP13L039-REC  FROM L039-HEADER-2.                              
           WRITE BP13L039-REC  FROM L039-LINE    AFTER 2.                       
           WRITE BP13L039-REC  FROM L039-HEADER-2A.                             
           WRITE BP13L039-REC  FROM L039-HEADER-3.                              
           WRITE BP13L039-REC  FROM L039-HEADER-3A.                             
           WRITE BP13L039-REC  FROM L039-LINE.                                  
           MOVE  8        TO   WS-LINE-CTR.                                     
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3600-READ-BP13K820.                                                      
      *--------------------------------*                                        
                                                                                
           READ BP13K820.                                                       
           EVALUATE WS-K820-STATUS                                              
              WHEN 00                                                           
                   CONTINUE                                                     
              WHEN 23                                                           
                   DISPLAY 'RECORD NOT FOUND IN BP13K820, KEY: '                
                            K820-KEY-FLD                                        
                   MOVE SPACES     TO BP13K820-REC                              
              WHEN OTHER                                                        
                   DISPLAY 'READ ERROR BP13K820,STATUS ' WS-K820-STATUS         
                   MOVE WS-K820-STATUS        TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3700-READ-BP13K800.                                                      
      *--------------------------------*                                        
                                                                                
           READ BP13K800.                                                       
           EVALUATE WS-K800-STATUS                                              
              WHEN 00                                                           
                   MOVE 'Y'      TO WS-K800-REGN-FND-SW                         
              WHEN 23                                                           
                   DISPLAY 'RECORD NOT FOUND IN BP13K800, KEY: '                
                            K800-NUM-REGN                                       
              WHEN OTHER                                                        
                   DISPLAY 'READ ERROR BP13K800,STATUS ' WS-K800-STATUS         
                   MOVE WS-K800-STATUS        TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       9000-CLOSE-FILES.                                                        
      *--------------------------------*                                        
                                                                                
           DISPLAY 'PROGRAM ID   : BP13C039'.                                   
           DISPLAY 'CURRENT DATE : ' WS-DISP-DATE.                              
           DISPLAY SPACES.                                                      
           DISPLAY '*=============== COUNTERS ===============*'.                
           DISPLAY 'BP13F310 RECORDS READ       : ' WS-F310-READ-CTR.           
           DISPLAY 'BP13K800 RECORDS READ(MCP)  : ' WS-K800-READ-CTR.           
           DISPLAY 'NO. OF RECORDS PRINTED      : ' WS-PRT-CTR.                 
                                                                                
           CLOSE BP13K853                                                       
                 BP13K820                                                       
                 BP13K800                                                       
                 BP04K010                                                       
                 BP13F310                                                       
                 BP13L039.                                                      
                                                                                
            IF WS-K853-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR-BP13K853-STATUS ' WS-K853-STATUS            
            END-IF.                                                             
                                                                                
            IF WS-K800-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR-BP13K800-STATUS ' WS-K800-STATUS            
            END-IF.                                                             
                                                                                
            IF WS-K820-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR-BP13K820-STATUS ' WS-K820-STATUS            
            END-IF.                                                             
                                                                                
            IF WS-K010-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR-BP04K010-STATUS ' WS-K010-STATUS            
            END-IF.                                                             
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
