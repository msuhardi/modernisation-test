       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CH22.                                                 
       AUTHOR.        JOHN BARCEGA.                                             
      *DATE-WRITTEN.  22/02/2013.                                               
      * =========================================================== *           
      *              SYSTEM OF COMMITMENT (BP13)                    *           
      * =========================================================== *           
      *   OBJECTIVE  :  EXTRACT REGN NOS WITH PCD OF 1 YR FROM   .  *           
      *                 CURRENT DATE                                *           
      *                                                             *           
      *   INPUT      :  BM06F100 -- BLOCK FILE                      *           
      *                 BM06K110 -- FLAT INV FILE                   *           
      *   OUTPUT     :  BP13FOUT -- REGN NOS WITH 1 YR FROM COMPLTN *           
      *                                                             *           
      *   AMENDMENTS:                                               *           
      *    REF      DATE     BY          DESCRIPTIONS               *           
      *****************************************************************         
      * BP134765 22/02/2013 JGO1     NEW PROGRAM                    *           
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06F100  ASSIGN       TO BM06F100.                           
                                                                                
           SELECT BM06K110  ASSIGN       TO BM06K110                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K110-KEY-FLD                        
                            ALTERNATE KEY IS K110-AIX1                          
                            FILE STATUS  IS K110-STATUS.                        
                                                                                
           SELECT BP13FOUT  ASSIGN TO BP13FOUT.                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BM06F100                   BLOCK CONTAINS 0 RECORDS                  
                                      RECORD CONTAINS 500 CHARACTERS            
                                      LABEL RECORD IS STANDARD                  
                                      RECORDING MODE IS F.                      
       COPY BM06F100.                                                           
                                                                                
       FD  BM06K110                                                             
                                      RECORD CONTAINS 500 CHARACTERS.           
       COPY BM06K110.                                                           
                                                                                
       FD  BP13FOUT                   RECORD CONTAINS 50 CHARACTERS             
                                      RECORDING MODE IS F                       
                                      LABEL RECORD IS OMITTED.                  
       01 BP13FOUT-REC            PIC X(50).                                    
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-STATUS.                                                           
           05 K100-STATUS              PIC 99       VALUE ZEROS.                
           05 FOUT-STATUS              PIC 99       VALUE ZEROS.                
           05 K110-STATUS              PIC 99       VALUE ZEROS.                
           05 WS-F100-END              PIC X        VALUE 'N'.                  
           05 WS-PCD-1YR               PIC X        VALUE 'N'.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-CNT-F100-RD           PIC 9(6) COMP VALUE 0.                   
           05 WS-CNT-FOUT-WRITE        PIC 9(6) COMP VALUE 0.                   
                                                                                
       01  WS-FLAGS.                                                            
           05 WS-QUARTER               PIC X(2) VALUE SPACES.                   
              88 WS-Q1                 VALUE '01' '02' '03'.                    
              88 WS-Q2                 VALUE '04' '05' '06'.                    
              88 WS-Q3                 VALUE '07' '08' '09'.                    
              88 WS-Q4                 VALUE '10' '11' '02'.                    
                                                                                
       01  WS-ACCEPT-DT-NUM            PIC 9(8).                                
                                                                                
       01  WS-CURRENT-DATE1.                                                    
           05  WS-DATE1.                                                        
                10  WS-DATE1-CYM.                                               
                    15  WS-DATE1-CCYY  PIC X(4).                                
                    15  WS-DATE1-MM    PIC X(2).                                
                10  WS-DATE1-DD        PIC X(2).                                
           05  WS-CURRENT-TIME         PIC X(9).                                
           05  WS-TIME REDEFINES WS-CURRENT-TIME.                               
                10  WS-HH              PIC 9(2).                                
                10  WS-MM              PIC 9(2).                                
                10  WS-SS              PIC 9(2).                                
                10  WS-MS              PIC 9(2).                                
                10  WS-GM              PIC 9(1).                                
           05  FILLER                  PIC X(4).                                
                                                                                
       01  CS-DATE-VARIABLES.                                                   
           05  WS-CHECK-DATE.                                                   
               10  WS-CHK-CCYY               PIC 9(4).                          
               10  WS-CHK-MM                 PIC 9(2).                          
               10  WS-CHK-DD                 PIC 9(2).                          
           05  WS-DATE-PLUS1.                                                   
               10  WS-PLS-CCYY               PIC 9(4).                          
               10  WS-PLS-MM                 PIC 9(2).                          
               10  WS-PLS-DD                 PIC 9(2).                          
                                                                                
       01 WS-FILE-LAYOUT.                                                       
           05 WS-BFOUT-REGN-NO               PIC X(8).                          
           05 WS-BFOUT-PACD.                                                    
              10 WS-BFOUT-PACD-QTR           PIC X(6).                          
              10 WS-BFOUT-PACD-DD            PIC X(2).                          
           05 FILLER                         PIC X(34).                         
                                                                                
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-START-UP       THRU 1000-EXIT.                          
           PERFORM 2000-MAIN-PROCESS   THRU 2000-EXIT                           
                   UNTIL WS-F100-END = 'Y' .                                    
           PERFORM 9999-WRAP-UP        THRU 9999-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-START-UP.                                                           
      ******************************************************************        
                                                                                
            OPEN INPUT  BM06F100                                                
                        BM06K110                                                
                 OUTPUT BP13FOUT.                                               
                                                                                
           IF  K110-STATUS NOT = '00' AND '97'                                  
               DISPLAY 'ERROR OPEN BM06K110 : ' K110-STATUS                     
               MOVE K110-STATUS TO RETURN-CODE                                  
               PERFORM 9999-WRAP-UP        THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE TO  WS-CURRENT-DATE1.                     
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-MAIN-PROCESS.                                                       
      ******************************************************************        
                                                                                
           PERFORM 3000-READ-BM06F100       THRU 3000-EXIT.                     
           PERFORM 3100-CHECK-PCD           THRU 3100-EXIT.                     
           IF WS-PCD-1YR = 'Y'                                                  
              PERFORM 3200-READ-BM06K110       THRU 3200-EXIT                   
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-READ-BM06F100.                                                      
      ******************************************************************        
                                                                                
           READ BM06F100 AT END                                                 
                MOVE 'Y'         TO WS-F100-END                                 
                GO TO 2000-EXIT.                                                
                ADD 1 TO WS-CNT-F100-RD.                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3100-CHECK-PCD.                                                          
      ******************************************************************        
                                                                                
           MOVE  'N'                    TO WS-PCD-1YR.                          
           MOVE  F100-DTE-PACD-QTR(1:4) TO WS-CHK-CCYY.                         
           MOVE  01                     TO WS-CHK-DD.                           
           MOVE  F100-DTE-PACD-QTR(5:2) TO WS-CHK-MM.                           
                                                                                
           IF F100-NUM-PACD = 'Q' OR SPACES OR LOW-VALUES                       
              MOVE WS-CHK-MM TO WS-QUARTER                                      
              EVALUATE TRUE                                                     
               WHEN WS-Q1                                                       
                    MOVE '01' TO WS-CHK-MM                                      
               WHEN WS-Q2                                                       
                    MOVE '04' TO WS-CHK-MM                                      
               WHEN WS-Q3                                                       
                    MOVE '07' TO WS-CHK-MM                                      
               WHEN WS-Q4                                                       
                    MOVE '10' TO WS-CHK-MM                                      
              END-EVALUATE                                                      
           ELSE                                                                 
              IF F100-NUM-PACD = 'M'                                            
                    CONTINUE                                                    
           END-IF.                                                              
                                                                                
           MOVE WS-DATE1   TO WS-DATE-PLUS1.                                    
           ADD  1          TO WS-PLS-CCYY.                                      
           IF WS-DATE-PLUS1 = WS-CHECK-DATE                                     
              MOVE 'Y'     TO WS-PCD-1YR                                        
           END-IF.                                                              
                                                                                
                                                                                
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3200-READ-BM06K110.                                                      
      ******************************************************************        
                                                                                
            INITIALIZE K110-REC.                                                
            MOVE F100-KEY-FLD  TO K110-AIX1.                                    
            READ BM06K110 KEY IS K110-AIX1.                                     
            IF K110-STATUS = '00'                                               
              PERFORM 3300-WRITE-OUTPUT        THRU 3300-EXIT                   
            ELSE                                                                
              DISPLAY 'K110 REC NOT FOUND:' F100-KEY-FLD                        
            END-IF.                                                             
                                                                                
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       3300-WRITE-OUTPUT.                                                       
      ******************************************************************        
                                                                                
           INITIALIZE WS-FILE-LAYOUT.                                           
           MOVE K110-REGN-NO  TO WS-BFOUT-REGN-NO.                              
           MOVE F100-DTE-PACD TO WS-BFOUT-PACD.                                 
           WRITE BP13FOUT-REC FROM WS-FILE-LAYOUT.                              
           ADD 1 TO WS-CNT-FOUT-WRITE.                                          
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-WRAP-UP.                                                            
      ******************************************************************        
           DISPLAY  SPACES.                                                     
           DISPLAY '******* CONTROL   COUNTS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13CH22'                                      
           DISPLAY  SPACES.                                                     
           DISPLAY 'NO. OF BM06K100 RECS READ    : ' WS-CNT-F100-RD.            
           DISPLAY 'NO. OF BP13FOUT RECS WRITTEN : ' WS-CNT-FOUT-WRITE.         
                                                                                
           CLOSE    BM06F100                                                    
                    BP13FOUT                                                    
                    BM06K110.                                                   
                                                                                
           IF  K110-STATUS NOT = '00'                                           
               DISPLAY 'ERROR CLOSE BM06K110 : ' K110-STATUS                    
               MOVE K110-STATUS TO RETURN-CODE                                  
           END-IF.                                                              
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
