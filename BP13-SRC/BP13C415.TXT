      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13C415.                                                 
      *AUTHOR.        MCC.                                                      
      *DATE WRITTEN.  30/10/01.                                                 
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                   *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  1.  TO PRINT WEEKLY REPORT ON CONTRA CASES    *        
      *                      FOR FOLLOW-UP ON ADDITIONAL MORTGAGE      *        
      *                      LOAN GRANTED.                             *        
      *                                                                *        
      *   INPUT  FILE :  1.  BP13F481  -  CONTRA FILE                  *        
      *                  2.  BP13K410  -  SALES ORDER FILE             *        
      *                                                                *        
      *   OUTPUT FILE :  1.  BP13L415  -  LIST OF CONTRA CASES         *        
      *                                                                *        
      *                                                                *        
      *   CHGE-NO   BY     DATE    DESCRIPTION                         *        
      *   -------   ---   ------   ---------------------------------   *        
      *  BP132041   MCC   30102001 NEW PGM                             *        
      *  BP133374   CCC   05092008 EXCLUDE CASES WITH SALES DATE ON &  *        
      *                            BEFORE 01/12/2005.                  *        
      *  BP134671   PCL4  09012013 TO CATER FOR BUY-SELL TAG AND CSM   *        
      ******************************************************************        
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F481  ASSIGN       TO BP13F481.                           
           SELECT BP13K410  ASSIGN       TO BP13K410                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K410-NUM-REGN                       
                            FILE STATUS  IS WS-K410-STATUS.                     
                                                                                
           SELECT BP13K022  ASSIGN       TO BP13K022                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K022-KEY-FLD                        
                            ALTERNATE RECORD KEY IS K022-NUM-REGN               
                            FILE STATUS  IS WS-K022-STATUS.                     
                                                                                
           SELECT  BP13K800 ASSIGN        TO BP13K800                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-REGN                      
                            FILE STATUS   IS WS-K800-STATUS.                    
                                                                                
           SELECT  BP13K893  ASSIGN       TO BP13K893                           
                             ACCESS MODE  IS DYNAMIC                            
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS K893-KEY-FLD                       
                             FILE STATUS  IS WS-K893-STATUS.                    
                                                                                
                                                                                
           SELECT BP13L415  ASSIGN        TO BP13L415.                          
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F481                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13F481.                                                           
                                                                                
       FD   BP13K410                                                            
            RECORD CONTAINS 2000  CHARACTERS.                                   
       COPY BP13K410.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000  CHARACTERS.                                   
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K893                                                            
            RECORD CONTAINS 2050 CHARACTERS.                                    
       COPY BP13K893.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP13L415                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   BP13L415-REC    PIC X(132).                                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILE-STATUS.                                                         
           05 WS-K410-STATUS            PIC 9(02)   VALUE 00.                   
           05 WS-K022-STATUS            PIC 9(02)   VALUE 00.                   
           05 WS-K800-STATUS            PIC 9(02)   VALUE 00.                   
           05 WS-K893-STATUS            PIC 9(02)   VALUE 00.                   
                                                                                
       01  WS-ACCUMULATORS.                                                     
           05 WS-F481-READ              PIC 9(08)   VALUE ZEROES.               
           05 WS-K410-READ              PIC 9(08)   VALUE ZEROES.               
           05 WS-K410-NOTFND            PIC 9(08)   VALUE ZEROES.               
           05 WS-MAX-LINES              PIC 9(02)   VALUE 55.                   
           05 WS-LINE-COUNT             PIC 9(02)   VALUE 65.                   
           05 WS-L415-PRINTED           PIC 9(08)   VALUE ZEROES.               
           05 WS-PAGE-NO                PIC 9(05)   VALUE ZEROES.               
                                                                                
       01  WS-SWITCHES.                                                         
           05 WS-F481-EOF-SW            PIC X(01)   VALUE SPACES.               
              88 WS-F481-EOF                        VALUE 'Y'.                  
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-CONTRA-AMT             PIC 9(7)V99 VALUE ZEROES.               
                                                                                
       01  WS-DATE.                                                             
           05 WS-YY                     PIC 9(04).                              
           05 WS-MM                     PIC 9(02).                              
           05 WS-DD                     PIC 9(02).                              
                                                                                
       01  WS-EDIT-DATE.                                                        
           05 WS-EDIT-DD                PIC 9(02).                              
           05 FILLER                    PIC X(01)   VALUE '/'.                  
           05 WS-EDIT-MM                PIC 9(02).                              
           05 FILLER                    PIC X(01)   VALUE '/'.                  
           05 WS-EDIT-YY                PIC 9(04).                              
                                                                                
      *----------------------------------------------------------------*        
      *      R E P O R T    L A Y O U T                                         
      *----------------------------------------------------------------*        
       01  L415-HDG1.                                                           
           05 FILLER                    PIC X(14)  VALUE 'BP13L415'.            
           05 FILLER                    PIC X(08)  VALUE 'HDB3'.                
           05 FILLER                    PIC X(18)  VALUE SPACES.                
           05 FILLER                    PIC X(39)  VALUE                        
              'S Y S T E M   O F   C O M M I T M E N T'.                        
           05 FILLER                    PIC X(18)  VALUE SPACES.                
           05 FILLER                    PIC X(07)  VALUE 'DATE: '.              
           05 L415-DATE                 PIC X(10)  VALUE SPACES.                
           05 FILLER                    PIC X(01)  VALUE SPACES.                
           05 FILLER                    PIC X(07)  VALUE 'PAGE: '.              
           05 L415-PAGE                 PIC ZZZZ9.                              
                                                                                
       01  L415-HDG2.                                                           
           05 FILLER                    PIC X(45)  VALUE SPACES.                
           05 FILLER                    PIC X(27)  VALUE                        
           'SALES UNIT - AGREEMENT TEAM'.                                       
           05 FILLER                    PIC X(60)  VALUE SPACES.                
                                                                                
       01  L415-HDG3.                                                           
           05 FILLER                    PIC X(36)  VALUE SPACES.                
           05 FILLER                    PIC X(46)  VALUE                        
           'WEEKLY REPORT ON CONTRA CASES FOR FOLLOW-UP ON'.                    
           05 FILLER                    PIC X(50)  VALUE SPACES.                
                                                                                
       01  L415-HDG4.                                                           
           05 FILLER                    PIC X(43)  VALUE SPACES.                
           05 FILLER                    PIC X(32)  VALUE                        
           'ADDITIONAL MORTGAGE LOAN GRANTED'.                                  
           05 FILLER                    PIC X(57)  VALUE SPACES.                
                                                                                
       01  L415-HDG5.                                                           
           05 FILLER                    PIC X(26)  VALUE                        
           ' TO   :     AGREEMENT TEAM'.                                        
           05 FILLER                    PIC X(106) VALUE SPACES.                
                                                                                
       01  L415-HDG6.                                                           
           05 FILLER                    PIC X(23)  VALUE                        
           ' CC   :     HAD FINANCE'.                                           
           05 FILLER                    PIC X(109) VALUE SPACES.                
                                                                                
       01  L415-HDG7.                                                           
           05 FILLER                    PIC X(12)  VALUE                        
           ' ATTN :     '.                                                      
           05 FILLER                    PIC X(120) VALUE SPACES.                
                                                                                
       01  L415-HDG8.                                                           
           05 FILLER                    PIC X(65)  VALUE SPACES.                
           05 FILLER                    PIC X(10)  VALUE                        
           'ADDITIONAL'.                                                        
                                                                                
       01  L415-HDG9.                                                           
           05 FILLER                    PIC X(130) VALUE                        
           ' S/N    REGN NO      SCHACC NO       SO DATE     SALE DATE          
      -    '  MORTGAGE LOAN($)   B/S   CSM'.                                    
                                                                                
       01  L415-HDG10.                                                          
           05 FILLER                    PIC X(130) VALUE ALL '-'.               
                                                                                
       01  L415-DETAIL-REC.                                                     
           05 L415-SER-NO               PIC Z,ZZ9  VALUE ZEROES.                
           05 FILLER                    PIC X(03)  VALUE SPACES.                
           05 L415-REGNO                PIC X(08)  VALUE SPACES.                
           05 FILLER                    PIC X(03)  VALUE SPACES.                
           05 L415-SCHACC               PIC X(14)  VALUE SPACES.                
           05 FILLER                    PIC X(03)  VALUE SPACES.                
           05 L415-SO-DATE              PIC X(10)  VALUE SPACES.                
           05 FILLER                    PIC X(03)  VALUE SPACES.                
           05 L415-SALE-DATE            PIC X(10)  VALUE SPACES.                
           05 FILLER                    PIC X(06)  VALUE SPACES.                
           05 L415-MORT-LOAN            PIC ZZZ,ZZ9.99 VALUE ZEROES.            
           05 FILLER                    PIC X(07)  VALUE SPACES.                
           05 L415-BUY-SELL             PIC X(01)  VALUE SPACES.                
           05 FILLER                    PIC X(04)  VALUE SPACES.                
           05 L415-CSM-NAME             PIC X(30)  VALUE SPACES.                
                                                                                
       01  L415-EMPTY-RPT.                                                      
           05 FILLER                    PIC X(49)  VALUE SPACES.                
           05 FILLER                    PIC X(20)  VALUE                        
           '*** EMPTY REPORT ***'.                                              
                                                                                
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
            PERFORM 1000-OPEN-ROUTINE    THRU  1000-EXIT.                       
            PERFORM 2000-READ-F481       THRU  2000-EXIT.                       
            PERFORM 3000-PROCESS-ROUTINE THRU  3000-EXIT                        
                                         UNTIL WS-F481-EOF.                     
            PERFORM 9000-CLOSE-ROUTINE   THRU  9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *------------------*                                                      
       1000-OPEN-ROUTINE.                                                       
      *------------------*                                                      
           OPEN INPUT BP13F481                                                  
                      BP13K410                                                  
                      BP13K800                                                  
                      BP13K893                                                  
                      BP13K022                                                  
               OUTPUT BP13L415.                                                 
                                                                                
           IF WS-K410-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR OPENING BP13K410 FILE, STATUS '                    
                       WS-K410-STATUS                                           
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR OPENING BP13K800 FILE, STATUS '                    
                       WS-K800-STATUS                                           
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K893-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR OPENING BP13K893 FILE, STATUS '                    
                       WS-K893-STATUS                                           
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR OPENING BP13K022 FILE, STATUS '                    
                       WS-K022-STATUS                                           
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-DATE.                          
           MOVE   WS-DD   TO   WS-EDIT-DD.                                      
           MOVE   WS-MM   TO   WS-EDIT-MM.                                      
           MOVE   WS-YY   TO   WS-EDIT-YY.                                      
                                                                                
           PERFORM 4500-WRITE-HEADING THRU 4500-EXIT.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------*                                                         
       2000-READ-F481.                                                          
      *---------------*                                                         
           READ BP13F481                                                        
                AT END MOVE 'Y'     TO  WS-F481-EOF-SW                          
                GO TO 2000-EXIT.                                                
                                                                                
           ADD +1                   TO  WS-F481-READ.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------*                                                   
       3000-PROCESS-ROUTINE.                                                    
      *---------------------*                                                   
           MOVE F481-NUM-REGN      TO K410-NUM-REGN.                            
                                                                                
           READ BP13K410.                                                       
                                                                                
           EVALUATE WS-K410-STATUS                                              
           WHEN 00                                                              
                ADD +1                   TO WS-K410-READ                        
                IF K410-DTE-SO-EFFECTIVE > '20051201' AND                       
                   K410-DTE-KEY-ISSUE   >= '20100101'                           
                   PERFORM 4000-WRITE-REPORT THRU 4000-EXIT                     
                END-IF                                                          
           WHEN 23                                                              
                ADD +1                   TO WS-K410-NOTFND                      
                DISPLAY 'REGN NOT FOUND IN K410, REGN # : '                     
                          F481-NUM-REGN                                         
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K410 FILE, STATUS : '                
                          WS-K410-STATUS                                        
           END-EVALUATE.                                                        
                                                                                
           PERFORM 2000-READ-F481       THRU  2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *------------------*                                                      
       4000-WRITE-REPORT.                                                       
      *------------------*                                                      
           IF WS-LINE-COUNT > WS-MAX-LINES                                      
              PERFORM 4500-WRITE-HEADING  THRU 4500-EXIT                        
           END-IF.                                                              
                                                                                
           PERFORM 4100-READ-BP13K800      THRU 4100-EXIT.                      
           PERFORM 4200-GET-CSM-NAME       THRU 4200-EXIT.                      
                                                                                
           ADD  1                            TO WS-L415-PRINTED.                
           MOVE WS-L415-PRINTED              TO L415-SER-NO.                    
                                                                                
           MOVE    F481-NUM-REGN             TO L415-REGNO.                     
           STRING  F481-SCH '-' F481-ACC '-' F481-LESSEE-CODE                   
                   DELIMITED BY SIZE INTO    L415-SCHACC.                       
                                                                                
           STRING  K410-DTE-SO-EXECUTION(7:2) '/'                               
                   K410-DTE-SO-EXECUTION(5:2) '/'                               
                   K410-DTE-SO-EXECUTION(1:4)                                   
                   DELIMITED BY SIZE INTO    L415-SO-DATE.                      
                                                                                
           STRING  K410-DTE-SO-EFFECTIVE(7:2) '/'                               
                   K410-DTE-SO-EFFECTIVE(5:2) '/'                               
                   K410-DTE-SO-EFFECTIVE(1:4)                                   
                   DELIMITED BY SIZE INTO    L415-SALE-DATE.                    
                                                                                
           MOVE    K410-AMT-RESALE-CONTRA    TO WS-CONTRA-AMT.                  
           MOVE    WS-CONTRA-AMT             TO L415-MORT-LOAN.                 
                                                                                
           WRITE BP13L415-REC  FROM L415-DETAIL-REC AFTER 1.                    
           ADD   1                           TO WS-LINE-COUNT.                  
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------*                                                      
       4100-READ-BP13K800.                                                      
      *------------------*                                                      
                                                                                
           MOVE SPACES                             TO BP13K800-MASTER.          
           INITIALIZE                                 BP13K800-MASTER.          
                                                                                
           MOVE F481-NUM-REGN                      TO K800-NUM-REGN.            
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    MOVE K800-NUM-BUY-SELL-TAG     TO L415-BUY-SELL             
               WHEN 23                                                          
                    PERFORM 4400-START-BP13K893    THRU 4400-EXIT               
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K800 : ' WS-K800-STATUS          
                            ' REGN = ' K800-NUM-REGN                            
                    PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT              
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       4200-GET-CSM-NAME.                                                       
      *------------------*                                                      
                                                                                
           MOVE SPACES                               TO BP13K022-REC.           
           INITIALIZE                                   BP13K022-REC.           
                                                                                
           MOVE F481-NUM-REGN                        TO K022-NUM-REGN.          
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K022-STATUS                                              
               WHEN 00                                                          
                    MOVE K022-NUM-OIC                TO L415-CSM-NAME           
                                                                                
               WHEN 23                                                          
                    MOVE SPACES                      TO L415-CSM-NAME           
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K022 : ' WS-K022-STATUS          
                            ' REGN NO = ' K022-NUM-REGN                         
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------*                                                    
       4400-START-BP13K893.                                                     
      *--------------------*                                                    
                                                                                
           MOVE SPACES                           TO BP13K893-MASTER.            
           INITIALIZE                               BP13K893-MASTER.            
                                                                                
           MOVE SPACES                           TO K893-KEY-FLD.               
           MOVE F481-NUM-REGN                    TO K893-NUM-REGN-HIST.         
                                                                                
           START BP13K893 KEY >= K893-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K893-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 4450-READ-BP13K893          THRU 4450-EXIT          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR STARTING BP13K893: ' WS-K893-STATUS          
                            ' REGN = ' K893-NUM-REGN-HIST                       
                    PERFORM 9000-CLOSE-ROUTINE          THRU 9000-EXIT          
           END-EVALUATE.                                                        
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4450-READ-BP13K893.                                                      
      *-------------------*                                                     
                                                                                
            READ  BP13K893 NEXT AT END                                          
                  MOVE SPACES               TO K893-KEY-FLD.                    
                                                                                
            EVALUATE WS-K893-STATUS                                             
                WHEN 00                                                         
                WHEN 02                                                         
                     IF K893-NUM-REGN-HIST = F481-NUM-REGN                      
                        MOVE K893-NUM-BUY-SELL-TAG    TO L415-BUY-SELL          
                     END-IF                                                     
                WHEN 23                                                         
                     CONTINUE                                                   
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K893 : ' WS-K893-STATUS         
                             ' REGN = ' K893-NUM-REGN-HIST                      
                     PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT              
            END-EVALUATE.                                                       
                                                                                
       4450-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4500-WRITE-HEADING.                                                      
      *-------------------*                                                     
           MOVE  WS-EDIT-DATE          TO L415-DATE.                            
           ADD   1                     TO WS-PAGE-NO.                           
           MOVE  WS-PAGE-NO            TO L415-PAGE.                            
                                                                                
           WRITE BP13L415-REC  FROM L415-HDG1 AFTER ADVANCING PAGE.             
           WRITE BP13L415-REC  FROM L415-HDG2 AFTER ADVANCING 1.                
           WRITE BP13L415-REC  FROM L415-HDG3 AFTER ADVANCING 1.                
           WRITE BP13L415-REC  FROM L415-HDG4 AFTER ADVANCING 1.                
                                                                                
           IF WS-PAGE-NO = 1                                                    
              WRITE BP13L415-REC  FROM L415-HDG5 AFTER ADVANCING 2              
              WRITE BP13L415-REC  FROM L415-HDG6 AFTER ADVANCING 2              
              WRITE BP13L415-REC  FROM L415-HDG7 AFTER ADVANCING 1              
           END-IF.                                                              
                                                                                
           WRITE BP13L415-REC  FROM L415-HDG8  AFTER ADVANCING 2.               
           WRITE BP13L415-REC  FROM L415-HDG9  AFTER ADVANCING 1.               
           WRITE BP13L415-REC  FROM L415-HDG10 AFTER ADVANCING 1.               
                                                                                
           MOVE SPACES  TO BP13L415-REC.                                        
           WRITE BP13L415-REC.                                                  
           MOVE  14     TO WS-LINE-COUNT.                                       
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------*                                                     
       9000-CLOSE-ROUTINE.                                                      
      *-------------------*                                                     
           IF WS-L415-PRINTED = ZEROES                                          
              MOVE SPACES  TO BP13L415-REC                                      
              WRITE BP13L415-REC FROM L415-EMPTY-RPT AFTER ADVANCING 5          
           END-IF.                                                              
                                                                                
           CLOSE BP13F481                                                       
                 BP13K410                                                       
                 BP13K800                                                       
                 BP13K893                                                       
                 BP13K022                                                       
                 BP13L415.                                                      
                                                                                
           IF WS-K410-STATUS NOT = ZEROS                                        
              DISPLAY 'ERROR CLOSING BP13K410 FILE, STATUS '                    
                       WS-K410-STATUS                                           
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = ZEROS                                        
              DISPLAY 'ERROR CLOSING BP13K800 FILE, STATUS '                    
                       WS-K800-STATUS                                           
           END-IF.                                                              
                                                                                
           IF WS-K893-STATUS NOT = ZEROS                                        
              DISPLAY 'ERROR CLOSING BP13K893 FILE, STATUS '                    
                       WS-K893-STATUS                                           
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = ZEROS                                        
              DISPLAY 'ERROR CLOSING BP13K022 FILE, STATUS '                    
                       WS-K022-STATUS                                           
           END-IF.                                                              
                                                                                
           DISPLAY '                                       '.                   
           DISPLAY ' *****  BP13C415 CONTROL TOTALS ****** '.                   
           DISPLAY ' RUN DATE : ' WS-DATE  (7:2) '/'                            
                                  WS-DATE  (5:2) '/'                            
                                  WS-DATE  (1:4).                               
           DISPLAY '                                       '.                   
           DISPLAY ' RECORDS READ    FROM BP13F481  = ' WS-F481-READ.           
           DISPLAY ' RECORDS READ    FROM BP13K410  = ' WS-K410-READ.           
           DISPLAY ' RECORDS NOTFND  IN   BP13K410  = ' WS-K410-NOTFND.         
           DISPLAY ' RECORDS WRITTEN TO   BP13L415  = ' WS-L415-PRINTED.        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
**************************** END OF PROGRAM ****************************        
