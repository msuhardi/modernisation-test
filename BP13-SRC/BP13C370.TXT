      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13C370.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  30 JUN 2010.                                              
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : TO DELETE BP13K421 RECPRD AFTER CLOSING     *           
      *                 CEREMONY                                    *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BP13F421 -- SORT CC FILE                              *           
      *                                                             *           
      *   I-O FILES :                                               *           
      *   1.  BP13K421 -- CC INFO                                   *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP133926 30/06/2010 ESA1 NEW PROGRAM                        *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K421 ASSIGN TO BP13K421                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K421-KEY-FLD                               
                  FILE STATUS     IS BP13K421-STATUS.                           
                                                                                
           SELECT BP13F421 ASSIGN TO BP13F421.                                  
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13K421                                                            
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K421.                                                           
                                                                                
       FD   BP13F421                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  450 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F421.                                                           
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  CNT-F421-READ          PIC 9(08) VALUE ZEROES.                       
       01  CNT-K421-READ          PIC 9(08) VALUE ZEROES.                       
       01  WS-F421-EOF            PIC X     VALUE SPACE.                        
       01  BP13K421-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  CNT-K421-FND           PIC 9(08) VALUE ZEROES.                       
       01  CNT-K421-NOTFND        PIC 9(08) VALUE ZEROES.                       
       01  CNT-K421-DELETE        PIC 9(08) VALUE ZEROES.                       
       01  CNT-K421-DELETE-ERROR  PIC 9(08) VALUE ZEROES.                       
       01  WS-DATE                PIC X(08) VALUE SPACES.                       
       01  WS-NUM-REGN            PIC X(08) VALUE SPACES.                       
                                                                                
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-F421    THRU 2000-EXIT.                            
           PERFORM 3000-PROCESS-REC  THRU 3000-EXIT                             
                   UNTIL WS-F421-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13F421                                                 
                I-O    BP13K421.                                                
                                                                                
           IF BP13K421-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING - BP13K421 : ' BP13K421-STATUS             
              MOVE BP13K421-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-F421.                                                          
      *---------------*                                                         
                                                                                
           READ BP13F421 AT END                                                 
                MOVE HIGH-VALUES TO BP13F421-REC                                
                MOVE 'Y' TO WS-F421-EOF                                         
                GO TO 2000-EXIT.                                                
           ADD 1 TO CNT-F421-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           PERFORM 3100-READ-BP13K421  THRU 3100-EXIT.                          
                                                                                
           PERFORM 2000-READ-F421      THRU 2000-EXIT.                          
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------*                                                     
       3100-READ-BP13K421.                                                      
      *-------------------*                                                     
           MOVE SPACES            TO K421-KEY-FLD.                              
           MOVE F421-KEY-FLD      TO K421-KEY-FLD.                              
                                                                                
           READ BP13K421.                                                       
                                                                                
           IF BP13K421-STATUS = 00                                              
              ADD 1    TO CNT-K421-READ                                         
              IF WS-DATE > K421-DTE-COMPLETION-CEREMONY                         
                 PERFORM 3200-DELETE-BP13K421  THRU 3200-EXIT                   
              END-IF                                                            
           ELSE                                                                 
              IF BP13K421-STATUS = 23                                           
                 ADD 1        TO CNT-K421-NOTFND                                
                 MOVE SPACES  TO BP13K421-REC                                   
                 INITIALIZE      BP13K421-REC                                   
                 DISPLAY 'RECORD NOT FOUND IN BP13K421 : '                      
                          F421-NUM-REGN                                         
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13K421 : ' BP13K421-STATUS          
                 MOVE BP13K421-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------*                                                   
       3200-DELETE-BP13K421.                                                    
      *---------------------*                                                   
                                                                                
           DELETE BP13K421.                                                     
                                                                                
           EVALUATE BP13K421-STATUS                                             
               WHEN 00                                                          
                    ADD 1 TO CNT-K421-DELETE                                    
               WHEN OTHER                                                       
                    ADD 1 TO CNT-K421-DELETE-ERROR                              
                    DISPLAY 'ERROR DELETING BP13K421-REC ...'                   
                    MOVE BP13K421-STATUS TO RETURN-CODE                         
                    PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13C370 *************'.                            
           DISPLAY 'NO OF RECS READ FROM  BP13F421 : ' CNT-F421-READ.           
           DISPLAY 'NO OF RECS READ FROM  BP13K421 : ' CNT-K421-READ.           
           DISPLAY 'NO OF RECS DELETED IN BP13K421 : ' CNT-K421-DELETE.         
           DISPLAY 'NO OF DELETE ERROR IN BP13K421 : '                          
                    CNT-K421-DELETE-ERROR.                                      
                                                                                
           CLOSE      BP13F421                                                  
                      BP13K421.                                                 
                                                                                
           IF BP13K421-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSING ERROR - BP13K421 : ' BP13K421-STATUS             
              MOVE BP13K421-STATUS TO RETURN-CODE.                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
