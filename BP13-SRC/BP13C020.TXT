       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C020.                                                 
      *AUTHOR.        CKK7.                                                     
      *DATE-WRITTEN.  23OCT2002.                                                
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)           *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES :                                        *                
      *                                                        *                
      *    1. PROGRAM FOR GENERATING REPORT FOR FIN ON THE     *                
      *       LISTING OF FLAT SCHEDULED FOR TP ON THE 23RD     *                
      *       TO THE LAST DAY OF THE MONTH.                    *                
      *                                                        *                
      *    INPUT  FILES : BP13F454 - APPOINTMENT FILE          *                
      *                 : SY02F001 - DATE FILE                 *                
      *                                                        *                
      *    LISTINGS     : BP13L020 - REPORT ON SCHEDULED TP    *                
      *--------------------------------------------------------*                
      * CHG-NO  BY    DATE    DETAILS                          *                
      * ======  ==   ======== ================================ *                
      *BP132231 CKK  20021025 NEW PGM                          *                
      *BP132375 SK   20030714 WRITE OUTFILE  (P13L020A)        *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F454 ASSIGN TO BP13F454.                                  
           SELECT BP13F410 ASSIGN TO BP13F410.                                  
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
           SELECT P13L020A ASSIGN TO P13L020A.                                  
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                ACCESS MODE       IS RANDOM                                     
                ORGANIZATION      IS INDEXED                                    
                RECORD KEY        IS K110-KEY-FLD                               
                FILE STATUS       IS BM06K110-STATUS.                           
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                ACCESS            IS RANDOM                                     
                ORGANIZATION      IS INDEXED                                    
                RECORD KEY        IS K800-NUM-REGN                              
                FILE STATUS       IS BP13K800-STATUS.                           
                                                                                
           SELECT BP13L020 ASSIGN TO BP13L020.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F454                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13F454.                                                           
                                                                                
       FD   BP13F410                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13F410.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD   P13L020A                                                            
            RECORD CONTAINS 133 CHARACTERS                                      
            RECORDING MODE    F.                                                
       01   P13L020A-REC    PIC X(132).                                         
                                                                                
       FD   BP13L020                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE    F.                                                
       01   BP13L020-REC    PIC X(132).                                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F454-EOF-SW                 PIC X(1)  VALUE 'N'.              
           05  WS-F410-EOF                    PIC X(1)  VALUE 'N'.              
           05  WS-SA-CASE                     PIC X(1)  VALUE 'N'.              
           05  WS-FOUND-F410                  PIC X(1)  VALUE SPACES.           
           05  BM06K110-STATUS                PIC 99    VALUE ZEROES.           
           05  BP13K800-STATUS                PIC 99    VALUE ZEROES.           
                                                                                
       01  WS-RECORD-COUNTERS.                                                  
           05  WS-CNT-READ-F454               PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-READ-F410               PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-MATCH-F410              PIC 9(6)  VALUE ZEROS.            
           05  WS-CANCEL                      PIC 9(6)  VALUE ZEROS.            
           05  WS-L020-WRT-CTR                PIC 9(6)  VALUE 0.                
                                                                                
       01  WS-L020-CONTROLS.                                                    
           05  WS-L020-LINECTR                PIC 9(2)  VALUE 60.               
           05  WS-L020-PAGECTR                PIC 9(5)  VALUE ZEROS.            
           05  WS-L020-SNO                    PIC 9(4)  VALUE 0.                
           05  WS-CNT-LETTERS                 PIC 9(4)  VALUE ZEROES.           
                                                                                
       01  WS-MISCELLANEOUS-VARS.                                               
           05  WS-BLANKS                      PIC X(132) VALUE SPACES.          
           05  WS-CTR                         PIC 9(2)   VALUE 0.               
           05  WS-TOTAL-PRICE                 PIC 9(10)  VALUE 0.               
                                                                                
       01  WS-TIME-DATES.                                                       
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY.                                                 
                   15  WS-SYS-CC              PIC 9(2).                         
                   15  WS-SYS-YY              PIC 9(2).                         
               10  WS-SYS-MM                  PIC 9(2).                         
               10  WS-SYS-DD                  PIC 9(2).                         
                                                                                
           05  WS-EDITED-DATE.                                                  
               10  WS-EDITED-DD               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-MM               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-YY               PIC 9(4).                         
                                                                                
      *============================================================*            
      *         BP13L020 -   R E P O R T    L A Y O U T            *            
      *============================================================*            
                                                                                
       01  L020-HEADER-01.                                                      
           05  FILLER              PIC X(08)     VALUE 'BP13L020'.              
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(25)     VALUE SPACES.                  
           05  FILLER              PIC X(41)     VALUE                          
               'S Y S T E M    O F    C O M M I T M E N T'.                     
           05  FILLER              PIC X(12)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE :'.                
           05  L020-RUN-DATE       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L020-PAGE-NO        PIC ZZZZ9 .                                  
                                                                                
       01  L020-HEADER-02.                                                      
           05  FILLER              PIC X(38)     VALUE SPACES.                  
           05  FILLER              PIC X(55)     VALUE                          
           'LISTING OF FLATS SCHEDULED FOR TP ON 23RD TO END OF MTH'.           
                                                                                
       01  L020-HEADER-03.                                                      
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE ' S/N '.                 
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'SCH-ACC-NO'.            
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(17)                                    
                                            VALUE 'SCHEDULED TP DATE'.          
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(13)     VALUE 'SELLING PRICE'.         
                                                                                
       01  L020-HEADER-04.                                                      
           05  FILLER              PIC X(130)    VALUE ALL '-'.                 
                                                                                
       01  L020-SPACES.                                                         
           05  FILLER              PIC X(130)    VALUE ALL ' '.                 
                                                                                
       01  L020-DETAIL.                                                         
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  L020-SNO            PIC ZZZ9      VALUE ZEROES.                  
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  L020-SCH-ACC-NO     PIC X(11)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE SPACES.                  
           05  L020-TP-DATE        PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE SPACES.                  
           05  L020-SELLING-PRICE  PIC $$99,999.99  VALUE ZEROES.               
                                                                                
       01  L020-FOOTER-05.                                                      
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'TOTAL   '.              
           05  L020-TOTAL-CASES    PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(06)     VALUE ' CASES'.                
           05  FILLER              PIC X(22)     VALUE SPACES.                  
           05  L020-TOTAL-PRICE    PIC $$99,999,999.99 VALUE ZEROES.            
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                     
                                                                                
           PERFORM 3000-PROCESS-F454                                            
              THRU 3000-EXIT                                                    
             UNTIL (WS-F454-EOF-SW = 'Y').                                      
                                                                                
           PERFORM 5000-PRINT-FOOTER        THRU 5000-EXIT.                     
           PERFORM 9999-CLOSE-ROUTINE       THRU 9999-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F454                                                 
                       BM06K110                                                 
                       BP13K800                                                 
                       SY02F001                                                 
                       BP13F410                                                 
                OUTPUT P13L020A                                                 
                       BP13L020.                                                
                                                                                
           IF BM06K110-STATUS NOT = '00' AND NOT = '97'                         
              DISPLAY ' OPENING ERROR,BM06K110-STATUS' BM06K110-STATUS          
              MOVE BM06K110-STATUS TO RETURN-CODE                               
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                        
                                                                                
           IF BP13K800-STATUS NOT = '00' AND NOT = '97'                         
              DISPLAY ' OPENING ERROR,BP13K800-STATUS' BP13K800-STATUS          
              MOVE BP13K800-STATUS TO RETURN-CODE                               
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-SYSTEM-DATE.                 
                                                                                
           MOVE   WS-SYS-DD           TO WS-EDITED-DD.                          
           MOVE   WS-SYS-MM           TO WS-EDITED-MM.                          
           MOVE   WS-SYS-CCYY         TO WS-EDITED-YY.                          
                                                                                
           MOVE   WS-EDITED-DATE      TO L020-RUN-DATE.                         
                                                                                
           READ SY02F001                                                        
           AT END                                                               
              DISPLAY 'EMPTY FILE - SY02F001 '                                  
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       2000-READ-F454.                                                          
      ****************************************************************          
                                                                                
           READ BP13F454                                                        
              AT END MOVE 'Y'   TO WS-F454-EOF-SW                               
                     GO TO 2000-EXIT.                                           
                                                                                
           ADD  +1                 TO WS-CNT-READ-F454.                         
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       2500-READ-K800.                                                          
      ***************************************************************           
                                                                                
           INITIALIZE BP13K800-MASTER.                                          
           MOVE 'N'            TO WS-SA-CASE.                                   
                                                                                
           MOVE F454-NUM-REGN  TO  K800-NUM-REGN.                               
           READ BP13K800.                                                       
                                                                                
           EVALUATE BP13K800-STATUS                                             
           WHEN 00                                                              
             IF  K800-NUM-IDP-SCHEME = 'SA '                                    
                 MOVE 'Y'      TO  WS-SA-CASE                                   
             END-IF                                                             
           WHEN 23                                                              
             CONTINUE                                                           
           WHEN OTHER                                                           
             DISPLAY 'ERROR READING BP13K800 = ' BP13K800-STATUS                
             DISPLAY '      K800-KEY-FLD     = ' K800-NUM-REGN                  
             PERFORM  9999-CLOSE-ROUTINE THRU 9999-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3000-PROCESS-F454.                                                       
      ****************************************************************          
                                                                                
           PERFORM  2000-READ-F454    THRU 2000-EXIT.                           
           MOVE 'N'  TO WS-FOUND-F410.                                          
           PERFORM  3600-READ-F410    THRU 3600-EXIT                            
                 UNTIL F410-NUM-REGN > F454-NUM-REGN OR                         
                 WS-FOUND-F410 = 'Y' OR WS-F410-EOF = 'Y'.                      
                                                                                
           IF WS-FOUND-F410 NOT = 'Y'                                           
              PERFORM 2500-READ-K800  THRU 2500-EXIT                            
                                                                                
              IF (WS-F454-EOF-SW NOT = 'Y' AND                                  
                  WS-SA-CASE NOT = 'Y' AND                                      
                 (F454-DTE-APPT(1:4) = F001-DTE-CURRENT(1:4) AND                
                  F454-DTE-APPT(5:2) = F001-DTE-CURRENT(5:2) AND                
                  F454-DTE-APPT(7:2) > F001-DTE-CURRENT(7:2)) AND               
                 (F454-CDE-APPT-TYPE = 'TP' OR                                  
                  F454-CDE-APPT-TYPE = 'SP' OR                                  
                  F454-CDE-APPT-TYPE = 'SR'))                                   
                                                                                
                 MOVE F454-SCH-ACCT      TO L020-SCH-ACC-NO                     
                 MOVE F454-DTE-APPT(1:4) TO WS-EDITED-YY                        
                 MOVE F454-DTE-APPT(5:2) TO WS-EDITED-MM                        
                 MOVE F454-DTE-APPT(7:2) TO WS-EDITED-DD                        
                 MOVE WS-EDITED-DATE     TO L020-TP-DATE                        
                                                                                
                PERFORM 3500-READ-BM06K110 THRU 3500-EXIT                       
                PERFORM 4000-PRINT-RPT     THRU 4000-EXIT                       
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       3500-READ-BM06K110.                                                      
      *************************************************************             
           MOVE F454-SCH-ACCT     TO K110-KEY-FLD                               
           READ BM06K110                                                        
           IF BM06K110-STATUS = 00                                              
               MOVE K110-CURR-SELLING-PRICE TO L020-SELLING-PRICE               
               ADD K110-CURR-SELLING-PRICE TO WS-TOTAL-PRICE                    
           ELSE                                                                 
               DISPLAY 'READ ERROR,BM06K110-STATUS' BM06K110-STATUS             
               MOVE ZEROES        TO L020-SELLING-PRICE.                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       3600-READ-F410.                                                          
      *************************************************************             
                                                                                
           READ BP13F410                                                        
              AT END MOVE 'Y'   TO WS-F410-EOF                                  
              GO TO 3600-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD  1       TO WS-CNT-READ-F410.                                    
                                                                                
           IF F410-NUM-REGN = F454-NUM-REGN                                     
              MOVE 'Y'  TO WS-FOUND-F410                                        
              ADD  1    TO WS-CNT-MATCH-F410                                    
           END-IF.                                                              
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       4000-PRINT-RPT.                                                          
      *************************************************************             
                                                                                
           ADD 1                        TO WS-L020-SNO.                         
           MOVE WS-L020-SNO             TO L020-SNO.                            
                                                                                
           IF WS-L020-LINECTR >= 55                                             
              ADD                  1 TO WS-L020-PAGECTR                         
              MOVE WS-L020-PAGECTR   TO L020-PAGE-NO                            
              WRITE BP13L020-REC   FROM L020-HEADER-01 AFTER PAGE               
              WRITE BP13L020-REC   FROM L020-HEADER-02                          
              WRITE BP13L020-REC   FROM L020-HEADER-03                          
              WRITE BP13L020-REC   FROM L020-HEADER-04                          
              WRITE BP13L020-REC   FROM L020-DETAIL                             
              MOVE                 5 TO WS-L020-LINECTR                         
           ELSE                                                                 
              ADD                  1 TO WS-L020-LINECTR                         
              WRITE BP13L020-REC   FROM L020-DETAIL                             
           END-IF.                                                              
                                                                                
           MOVE SPACES            TO P13L020A-REC.                              
           INITIALIZE                P13L020A-REC.                              
           WRITE P13L020A-REC     FROM L020-DETAIL.                             
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       5000-PRINT-FOOTER.                                                       
      *************************************************************             
                                                                                
           MOVE L020-SNO TO L020-TOTAL-CASES.                                   
           MOVE WS-TOTAL-PRICE TO L020-TOTAL-PRICE.                             
                                                                                
           IF WS-L020-LINECTR >= 52                                             
              ADD                  1 TO WS-L020-PAGECTR                         
              MOVE WS-L020-PAGECTR   TO L020-PAGE-NO                            
              WRITE BP13L020-REC   FROM L020-HEADER-01 AFTER PAGE               
              WRITE BP13L020-REC   FROM L020-HEADER-02                          
              WRITE BP13L020-REC   FROM L020-HEADER-03                          
              WRITE BP13L020-REC   FROM L020-HEADER-04                          
              WRITE BP13L020-REC   FROM L020-SPACES                             
              WRITE BP13L020-REC   FROM L020-FOOTER-05                          
           ELSE                                                                 
              ADD                  1 TO WS-L020-LINECTR                         
              WRITE BP13L020-REC   FROM L020-SPACES                             
              WRITE BP13L020-REC   FROM L020-FOOTER-05                          
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           CLOSE BP13F454                                                       
                 BM06K110                                                       
                 BP13K800                                                       
                 SY02F001                                                       
                 BP13F410                                                       
                 P13L020A                                                       
                 BP13L020.                                                      
                                                                                
           IF BM06K110-STATUS NOT = 0                                           
            DISPLAY ' CLOSING ERROR, BM06K110-STATUS:' BM06K110-STATUS          
            MOVE BM06K110-STATUS TO RETURN-CODE .                               
                                                                                
           IF BP13K800-STATUS NOT = 0                                           
            DISPLAY ' CLOSING ERROR, BP13K800-STATUS:' BP13K800-STATUS          
            MOVE BP13K800-STATUS TO RETURN-CODE .                               
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'RUN DATE : ' WS-EDITED-DATE.                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13C020 CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13F454 RECORDS READ        = ' WS-CNT-READ-F454.          
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF MATCH WITH BP13F410   = ' WS-CNT-MATCH-F410.         
           DISPLAY '*==============================================*'.          
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
                                                                                
