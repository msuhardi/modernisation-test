      *-------------------------*                                               
       IDENTIFICATION DIVISION.                                                 
      *-------------------------*                                               
       PROGRAM-ID.    BP13C125.                                                 
       AUTHOR.        YEONG TAT CHEONG.                                         
       DATE-WRITTEN.  20/5/89.                                                  
                                                                                
      ************************************************************              
      *  OBJECTIVE : TO PROCESS TRANSACTION FILE                 *              
      *              (LEASE STAMP FEE/LEASE REGN FEE) INTO       *              
      *              STAMP FEE SYSTEM (SFS)                      *              
      *              FOR POSTING                                 *              
      ************************************************************              
      *  CHG-NO   BY    ON         DESCRIPTION                   *              
      *  ------   ---   --------   ----------------------------- *              
      *  SOC-PH8  JEP   14/07/93   USE BP13K820 FILE INSTEAD OF  *              
      *                                P10K040  FILE             *              
      *  BP130504 MPA   04/06/98   CHANGE BP13F128 TO B05F005    *              
      *  BP130418 RHB   25/08/98   TO CATER FOR Y2000.           *              
      *  BP130998 CLT   15/05/01   TO PASS LEASE STAMP FEE &     *              
      *                            LEASE REGN FEE OF SA FLAT     *              
      *                            REMOVE READING OF BP13K820    *              
      *  BP132742 CLT   05/04/06   TO PASS NRIC FOR BANK CASE    *              
      *  BP132969 MTSR  13/02/07   TO PASS SFTT CODE             *              
      ************************************************************              
                                                                                
      *-------------------------*                                               
       ENVIRONMENT DIVISION.                                                    
      *-------------------------*                                               
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F136 ASSIGN TO BP13F136.                                  
                                                                                
           SELECT B05F005  ASSIGN TO B05F005.                                   
                                                                                
                                                                                
      *-------------------------*                                               
       DATA DIVISION.                                                           
      *-------------------------*                                               
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F136                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13F136.                                                           
                                                                                
       FD  B05F005                                                              
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 160 CHARACTERS.                                      
       COPY B05F005.                                                            
                                                                                
                                                                                
      *-------------------------*                                               
       WORKING-STORAGE SECTION.                                                 
      *-------------------------*                                               
                                                                                
       01  WS-INDICATORS.                                                       
           05 WS-F136-EOF          PIC X       VALUE 'N'.                       
           05 WS-CNT-F136          PIC 9999    VALUE 0.                         
           05 WS-CNT-F005          PIC 9999    VALUE 0.                         
           05 WS-CNT-BYPASS        PIC 9999    VALUE 0.                         
           05 WS-CUR-DATE          PIC 9(8).                                    
                                                                                
       01  WS-NUM-VOUCHER.                                                      
           05 FILLER               PIC X(2)    VALUE 'SO'.                      
           05 WS-NUM-VOUCHER-MM    PIC X(2).                                    
           05 WS-NUM-VOUCHER-YY    PIC X(2).                                    
                                                                                
       01  WS-NUM-JR.                                                           
           05 FILLER               PIC X(1)    VALUE '0'.                       
           05 WS-NUM-JR-MM         PIC X(2).                                    
           05 WS-NUM-JR-YY         PIC X(2).                                    
                                                                                
       01  WS-DTE-POST.                                                         
           05 WS-DTE-POST-CC       PIC X(2).                                    
           05 WS-DTE-POST-YY       PIC X(2).                                    
           05 WS-DTE-POST-MM       PIC X(2).                                    
           05 WS-DTE-POST-DD       PIC X(2).                                    
                                                                                
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
      *-------------------*                                                     
       MAIN-PROGRAM.                                                            
      *-------------------*                                                     
           PERFORM 000-OPEN-FILES            THRU  000-EXIT.                    
           PERFORM 100-READ-BP13F136         THRU  100-EXIT.                    
           PERFORM 200-ACCESS-FILE-B05F005   THRU  200-EXIT                     
                       UNTIL WS-F136-EOF = 'Y'.                                 
           PERFORM 500-CLOSE-ROUTINE.                                           
                                                                                
       MAIN-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------*                                                     
       000-OPEN-FILES.                                                          
      *-------------------*                                                     
           OPEN INPUT   BP13F136                                                
               OUTPUT   B05F005.                                                
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------*                                                     
       100-READ-BP13F136.                                                       
      *-------------------*                                                     
           READ BP13F136                                                        
                         AT END MOVE 'Y' TO WS-F136-EOF                         
                         GO TO 100-EXIT.                                        
                                                                                
           ADD 1 TO WS-CNT-F136.                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       200-ACCESS-FILE-B05F005.                                                 
      *------------------------*                                                
           IF (F136-CDE-TRANS-TYPE   = '23'   AND                               
               F136-CDE-PAYMENT-TYPE = 'LSE') OR                                
              (F136-CDE-TRANS-TYPE   = '31'   AND                               
               F136-CDE-PAYMENT-TYPE = 'LRF')                                   
              PERFORM 220-WRITE-AB05F005 THRU 220-EXIT.                         
                                                                                
           PERFORM 100-READ-BP13F136 THRU  100-EXIT.                            
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       220-WRITE-AB05F005.                                                      
      *------------------------*                                                
           MOVE SPACES TO F005-RECEIPT-REC.                                     
                                                                                
           MOVE F136-NUM-SCH-ACCT         TO  F005-HDB-REF-SF.                  
           MOVE '8'                       TO  F005-VR-TYPE.                     
           MOVE 'SO'                      TO  F005-SCHEDULE-NO(1:2).            
           MOVE '04'                      TO  F005-SYSTEM-CODE.                 
           MOVE '/'                       TO  F005-SCHEDULE-NO(8:1).            
                                                                                
           MOVE '0000000104250'           TO  F005-VOTE-NO                      
           MOVE F136-AMT-RECEIPT          TO  F005-TOTAL-AMT.                   
           MOVE 'C'                       TO  F005-CODE.                        
           MOVE SPACES                    TO  F005-NAME.                        
                                                                                
           MOVE F136-DTE-POST             TO  F005-RECEIPT-DATE                 
                                              WS-DTE-POST.                      
           MOVE F136-DTE-POST(1:6)        TO  F005-ACC-MTH-YR.                  
                                                                                
           MOVE WS-DTE-POST-YY            TO  F005-SCHEDULE-NO(9:2)             
                                                                                
           MOVE WS-DTE-POST-MM            TO  WS-NUM-VOUCHER-MM.                
           MOVE WS-DTE-POST-YY            TO  WS-NUM-VOUCHER-YY                 
           MOVE WS-NUM-VOUCHER            TO  F005-RECEIPT-NO.                  
                                                                                
           MOVE WS-DTE-POST-MM            TO  WS-NUM-JR-MM                      
           MOVE WS-DTE-POST-YY            TO  WS-NUM-JR-YY.                     
           MOVE WS-NUM-JR                 TO  F005-SCHEDULE-NO(3:5).            
                                                                                
           IF F136-NUM-JRNO = 'BANK LOAN'                                       
              MOVE F136-NUM-NRIC          TO  F005-VR-NO                        
           END-IF.                                                              
                                                                                
           MOVE 'NS'                      TO  F005-CDE-SFTT.                    
           MOVE 'BP13'                    TO  F005-CDE-SYSTEM.                  
                                                                                
           WRITE F005-RECEIPT-REC.                                              
           ADD 1 TO WS-CNT-F005.                                                
                                                                                
       220-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       500-CLOSE-ROUTINE.                                                       
      *------------------------*                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CUR-DATE.                      
                                                                                
           DISPLAY 'PROGRAM-ID : BP13C125 '.                                    
           DISPLAY 'RUN DATE   : ' WS-CUR-DATE.                                 
           DISPLAY 'BP13F128-RECORDS READ    : ' WS-CNT-F136.                   
           DISPLAY 'B05F005-RECORDS WRITTEN  : ' WS-CNT-F005.                   
                                                                                
           CLOSE BP13F136                                                       
                 B05F005.                                                       
                                                                                
           STOP RUN.                                                            
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
