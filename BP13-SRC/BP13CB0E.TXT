      **************************                                        00010000
       IDENTIFICATION DIVISION.                                         00020000
      **************************                                        00030000
       PROGRAM-ID.    BP13CB0E.                                         00040000
      *AUTHOR.        MARGE LAM KO.                                     00050000
      *DATE-WRITTEN.  09 SEP 2015.                                      00060000
                                                                        00070000
      * =========================================================== *   00080000
      * SYSTEM OF COMMITMENT (BP13)                                 *   00090000
      * =========================================================== *   00100000
      *   OBJECTIVE   : THIS PROGRAM PROCESSES RECOVERY TRANSACTIONS*   00110000
      *                 FROM AC28 (COLL TRANS FILE)                 *   00120000
      *                 DEPENDING ON GRANT_ASMNT.TXT-CNCLTN-REASON, *   00130000
      *                 DROP FROM LIVE TABLES TO EX TABLES          *   00140000
      *                 AND CREATE TO GRANT_RCVRY TABLE             *   00150000
      *                                                             *   00160000
      *   INPUT FILES :                                             *   00170000
      *   1.  AC28F526 -- COLLECTION SYSTEM FILE                    *   00180000
      *                                                             *   00190000
      *   OUTPUT FILES:                                             *   00200000
      *   1.  BP13K130 -- FILE FOR FINANCIAL HISTORY                *   00210000
      *   2.  BP13LB0E -- CONTROL LISTING                           *   00220000
      *   3.  BP13F52A -- MISMATCH FILE                             *   00230000
      *                                                             *   00240000
      *   TABLES:                                                   *   00250000
      *   1.  GRANT_ASMNT                                           *   00260000
      *   2.  GRANT_ASMNT_PERSON                                    *   00270000
      *   3.  EX_GRANT_ASMNT                                        *   00280000
      *   4.  EX_GRANT_ASMNT_PERSON                                 *   00290000
      *   5.  GRANT_RCVRY                                           *   00300000
      *   6.  GRANT_UPDATE_LOG                                      *   00310000
      *                                                             *   00320000
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *   00330000
      * -------- ---------- ---- ---------------------------------- *   00340000
      * BP135978 09/10/2015 MRN1 NEW PROGRAM                        *   00350000
      * BP136147 21/03/2016 MRN1 CATER FOR SUG, AHG/SHG             *   00360000
      * BP136148 29/04/2016 MRN1 NEW FIELDS AND NEW TABLE FOR UPD LOG   00370000
      * BP136309 21/07/2016 MRN1 CATER FOR INTEREST                 *   00380000
      * BP136401 22/09/2016 MRN1 CATER FOR NEW RECEIPTING CODES     *   00390000
      *                          6014J(AHG), 6021Z(SHG), 6254D(INT) *   00400000
      *                          AND DO NOT WRITE BP13K130 FOR      *   00410000
      *                          6020H(AHG), 6011E(SHG), 6015B(SUG) *   00420000
      * BP136585 09/03/2017 MRN1 DO NOT WRITE BP13K130 FOR          *   00430000
      *                          6014J(AHG), 6021Z(SHG), 6254D(INT) *   00440000
      * BP136622 03/04/2017 MRN1 NEW FIELD GRANT EXCESS             *   00450000
      * BP137143 17/01/2018 MRN1 MOVE YEAR TO THE DTE POST IN THE RPT   00460000
      * BP137965 02/10/2019 SRG1 CATER FOR RCPTCD                   *   00470000
      * BP138095 27/11/2019 KR13 ADD EHG.                           *   00480000
      * BP138502 23/12/2020 PP11 CATER TO DROP ONLY ACTIVE NRIC CASES   00490000
      *                          FROM GRANT PERSON TABLE            *   00500000
      * BP138680 11/03/2021 PP11 CATER TO UPDATE EX GRANT ASMNT PERSON  00510000
      *                          IF CASES ALREADY DROPED            *   00520000
      * BP138231 10/01/2023 PP11 ADD HGT REG PHG TUG ATG FSG  AND   *   00530000
      *                          NOT TO FETCH FROM EX GRANT PERSON      00540000
      *                          IF CASES AVAILABLE IN GRANT PERSON     00550000
      * BP139474 20/01/2023 PP11 CATER TO REMOVE THE CONDITION CHECK*   00551000
      *                          RC01                                   00552000
      * BP138212 30/03/2023 PP11 CATER TO CHANGE SRG GRANT RECOVERY *   00553005
      *                          TRANS TYPE AS 'GR RECY'35              00554000
      * =========================================================== *   00560000
                                                                        00570000
       ENVIRONMENT DIVISION.                                            00580000
                                                                        00590000
       CONFIGURATION SECTION.                                           00600000
                                                                        00610000
       SOURCE-COMPUTER. IBM-3090.                                       00620000
       OBJECT-COMPUTER. IBM-3090.                                       00630000
                                                                        00640000
       INPUT-OUTPUT SECTION.                                            00650000
                                                                        00660000
       FILE-CONTROL.                                                    00670000
                                                                        00680000
           SELECT AC28F526   ASSIGN       TO AC28F526.                  00690000
                                                                        00700000
           SELECT BP13K130 ASSIGN TO BP13K130                           00710000
                ACCESS MODE       IS RANDOM                             00720000
                ORGANIZATION      IS INDEXED                            00730000
                RECORD KEY        IS K130-KEY-FLD                       00740000
                FILE STATUS       IS BP13K130-STATUS.                   00750000
                                                                        00760000
           SELECT BP13F52A   ASSIGN       TO BP13F52A.                  00770000
                                                                        00780000
           SELECT BP13LB0E   ASSIGN       TO BP13LB0E.                  00790000
                                                                        00800000
       DATA DIVISION.                                                   00810000
                                                                        00820000
       FILE SECTION.                                                    00830000
                                                                        00840000
       FD  AC28F526                                                     00850000
           BLOCK     CONTAINS 0   RECORDS                               00860000
           RECORD    CONTAINS 130 CHARACTERS                            00870000
           LABEL     RECORDS  ARE STANDARD                              00880000
           RECORDING MODE     IS  F.                                    00890000
       COPY AC28F526.                                                   00900000
                                                                        00910000
       FD  BP13K130                                                     00920000
           RECORD    CONTAINS 150 CHARACTERS.                           00930000
       COPY BP13K130.                                                   00940000
                                                                        00950000
       FD   BP13F52A                                                    00960000
            RECORD CONTAINS 130 CHARACTERS                              00970000
            LABEL RECORDS ARE OMITTED                                   00980000
            RECORDING MODE IS F.                                        00990000
       COPY BP13F52A.                                                   01000000
                                                                        01010000
       FD   BP13LB0E                                                    01020000
            RECORD CONTAINS   132 CHARACTERS                            01030000
            LABEL RECORDS ARE OMITTED                                   01040000
            RECORDING MODE IS F.                                        01050000
       01   PRINT-REC             PIC X(132).                           01060000
                                                                        01070000
      *************************                                         01080000
       WORKING-STORAGE SECTION.                                         01090000
      *************************                                         01100000
                                                                        01110000
       01  WS-LINE-CNT            PIC 99    VALUE 66.                   01120000
       01  WS-PAGE-CNT            PIC 9(5)  VALUE ZERO.                 01130000
       01  CNT-KIV-CASES          PIC 9(4)  VALUE ZERO.                 01140000
       01  WS-AMT-KIV-TOTAL       PIC 9(6)V99 VALUE ZEROES.             01150000
       01  CNT-K130-WRITE         PIC 9(05) VALUE ZEROES.               01160000
       01  CNT-F526-READ          PIC 9(05) VALUE ZEROES.               01170000
       01  CNT-ASMNT-UPD          PIC 9(05) VALUE ZEROES.               01180000
       01  CNT-EX-GRANT-UPD       PIC 9(05) VALUE ZEROES.               01190000
       01  CNT-RCVRY-UPD          PIC 9(05) VALUE ZEROES.               01200000
       01  CNT-LINE-ON-PAGE       PIC 9(05) VALUE ZEROES.               01210000
       01  CNT-PRT-CASES          PIC 9(05) VALUE ZEROES.               01220000
       01  CNT-PAGE               PIC 9(05) VALUE ZEROES.               01230000
       01  WS-EX-GRANT-INS        PIC 9(05) VALUE ZEROES.               01240000
       01  WS-EX-PRSN-INS         PIC 9(05) VALUE ZEROES.               01250000
       01  WS-RCVRY-INS           PIC 9(05) VALUE ZEROES.               01260000
       01  WS-GRANT-DELETE        PIC 9(05) VALUE ZEROES.               01270000
       01  WS-PERSON-DELETE       PIC 9(05) VALUE ZEROES.               01280000
       01  WS-F52A-WRITE          PIC 9(05) VALUE ZEROES.               01290000
       01  WS-GRNT-UPD-LOG        PIC 9(05) VALUE ZEROES.               01300000
       01  WS-F526-EOF            PIC X     VALUE SPACE.                01310000
       01  WS-FD10-EOF            PIC X     VALUE SPACE.                01320000
       01  WS-CURSOR1-EOF         PIC X     VALUE 'N'.                  01330000
       01  WS-CURSOR2-EOF         PIC X     VALUE 'N'.                  01340000
       01  WS-CURSOR3-EOF         PIC X     VALUE 'N'.                  01350000
       01  WS-CURSOR4-EOF         PIC X     VALUE 'N'.                  01360000
       01  WS-CURSOR5-EOF         PIC X     VALUE 'N'.                  01370000
       01  WS-GRANT-ASMNT-FND     PIC X     VALUE SPACE.                01380000
       01  WS-RCVRY-FND           PIC X     VALUE SPACE.                01390000
       01  WS-UPD-INTRST          PIC X     VALUE SPACE.                01400000
       01  WS-EX-PROCESSED        PIC X     VALUE SPACE.                01410000
       01  WS-GRNT-PRSN-FND       PIC X     VALUE SPACE.                01420000
       01  BP13K130-STATUS        PIC 9(02) VALUE ZEROES.               01430000
       01  WS-REGNO-UPDATED       PIC X     VALUE SPACE.                01440000
       01  WS-PREV-NUM-REGN       PIC X(08) VALUE SPACES.               01450000
       01  WS-DATE                PIC X(08) VALUE SPACES.               01460000
       01  WS-TIMESTAMP           PIC X(26) VALUE SPACES.               01470000
       01  WS-TIMESTAMP-LOG       PIC X(26) VALUE SPACES.               01480000
       01  WS-TIME-CREATE         PIC X(26) VALUE SPACES.               01490000
       01  WS-SQLCODE             PIC ---9.                             01500000
       01  WS-RETURN-CODE         PIC 9(02) VALUE ZEROES.               01510000
       01  WS-DTE-CCYYMMDD.                                             01520000
           05 WS-DTE-CCYY                   PIC X(04) VALUE SPACES.     01530000
           05 WS-DTE-MM                     PIC X(02) VALUE SPACES.     01540000
           05 WS-DTE-DD                     PIC X(02) VALUE SPACES.     01550000
       01  WS-TIME-HHMMSS.                                              01560000
           05 WS-TIME-HH                    PIC X(02) VALUE SPACES.     01570000
           05 WS-TIME-MM                    PIC X(02) VALUE SPACES.     01580000
           05 WS-TIME-SS                    PIC X(02) VALUE SPACES.     01590000
           05 WS-TIME-MS                    PIC X(02) VALUE SPACES.     01600000
                                                                        01610000
       01  WS-LOG-VARIABLE.                                             01620000
           05  WS-BEFORE-IMAGE        PIC X(600).                       01630000
           05  WS-AFTER-IMAGE         PIC X(600).                       01640000
           05  WS-SOURCE              PIC X(30).                        01650000
           05  WS-RGSTRN              PIC X(8).                         01660000
           05  WS-ACTION              PIC X(1).                         01670000
           05  WS-GRNT-TYPE           PIC X(6).                         01680000
                                                                        01690000
       01  WS-SQL-STATUS.                                               01700000
           05 WS-SQL-OK                PIC S9(03) VALUE +000.           01710000
           05 WS-SQL-NFND              PIC S9(03) VALUE +100.           01720000
           05 WS-SQL-DUPREC            PIC S9(03) VALUE -803.           01730000
                                                                        01740000
       01  LB0E-PR-HEAD-01.                                             01750000
           05  FILLER              PIC X(8)      VALUE 'BP13LB0E'.      01760000
           05  FILLER              PIC X(4)      VALUE SPACES.          01770000
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.      01780000
           05  FILLER              PIC X(24)     VALUE SPACES.          01790000
           05  FILLER              PIC X(20)     VALUE                  01800000
                                    'S Y S T E M   O F   '.             01810000
           05  FILLER              PIC X(19)     VALUE                  01820000
                                    'C O M M I T M E N T'.              01830000
           05  FILLER              PIC X(15)     VALUE SPACES.          01840000
           05  FILLER              PIC X(7)      VALUE 'DATE :'.        01850000
           05  LB0E-DATE           PIC X(10)     VALUE SPACES.          01860000
           05  FILLER              PIC X(4)      VALUE SPACES.          01870000
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.       01880000
           05  LB0E-PAGE           PIC ZZZZ9 .                          01890000
                                                                        01900000
       01  LB0E-PR-HEAD-02.                                             01910000
           05  FILLER              PIC X(39)     VALUE SPACES .         01920000
           05  FILLER              PIC X(53)     VALUE                  01930000
               ' ERROR LIST OF TRANSACTIONS IN THE COLLN-TRANS FILE'.   01940000
                                                                        01950000
       01  LB0E-PR-HEAD-03.                                             01960000
           05  FILLER              PIC X(28)                            01970000
                  VALUE '   S/NO     SCH-ACCNT NO    '.                 01980000
           05  FILLER              PIC X(34)                            01990000
                  VALUE 'REGN NO       AMOUNT    RECEIPT NO'.           02000000
           05  FILLER              PIC X(39)                            02010000
                  VALUE '      DATE POST   PAY TYPE       REMARK'.      02020000
           05  FILLER              PIC X(25)     VALUE SPACES.          02030000
           05  FILLER              PIC X(6)     VALUE 'H-CNT '.         02040000
                                                                        02050000
       01  LB0E-PR-DETAILS.                                             02060000
           05  LB0E-SNO            PIC ZZZZZ9 .                         02070000
           05  FILLER              PIC X(06)     VALUE SPACES .         02080000
           05  LB0E-NUM-SCHACCNT   PIC X(11).                           02090000
           05  FILLER              PIC X(04)     VALUE SPACES.          02100000
           05  LB0E-ORIG-REG-NO    PIC X(8).                            02110000
           05  FILLER              PIC X(4)      VALUE SPACES .         02120000
           05  LB0E-AMT            PIC $$$$$$9.99DB.                    02130000
           05  FILLER              PIC X(02)     VALUE SPACES .         02140000
           05  LB0E-NUM-RECEIPT    PIC X(12) .                          02150000
           05  FILLER              PIC X(04)     VALUE SPACES.          02160000
           05  LB0E-DTE-POST.                                           02170000
               10  LB0E-POST-DD    PIC X(2)      VALUE SPACES.          02180000
               10  LB0E-POST-FIL1  PIC X         VALUE '/'.             02190000
               10  LB0E-POST-MM    PIC X(2)      VALUE SPACES.          02200000
               10  LB0E-POST-FIL2  PIC X         VALUE '/'.             02210000
               10  LB0E-POST-YYYY  PIC X(4)      VALUE SPACES.          02220000
           05  FILLER              PIC X(05)     VALUE SPACES .         02230000
           05  LB0E-PAYMENT-TYPE   PIC X(03).                           02240000
           05  FILLER              PIC X(03)     VALUE SPACES .         02250000
           05  LB0E-REMARK         PIC X(35).                           02260000
                                                                        02270000
      *--------------------------------------------------------------*  02280000
      *                          SQLCA                               *  02290000
      *--------------------------------------------------------------*  02300000
           EXEC SQL INCLUDE SQLCA          END-EXEC.                    02310000
           EXEC SQL INCLUDE P13VGASM       END-EXEC.                    02320000
           EXEC SQL INCLUDE P13VGAPN       END-EXEC.                    02330000
           EXEC SQL INCLUDE P13VGRCV       END-EXEC.                    02340000
           EXEC SQL INCLUDE P13VGXAP       END-EXEC.                    02350000
           EXEC SQL INCLUDE P13VGXAS       END-EXEC.                    02360000
           EXEC SQL INCLUDE P13VGUPD       END-EXEC.                    02370000
                                                                        02380000
      ********************                                              02390000
       PROCEDURE DIVISION.                                              02400000
      ********************                                              02410000
                                                                        02420000
      *----------*                                                      02430000
       0000-MAIN.                                                       02440000
      *----------*                                                      02450000
                                                                        02460000
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                   02470000
           PERFORM 1200-READ-AC28F526 THRU 1200-EXIT.                   02480000
           PERFORM 1500-PROCESS-REC   THRU 1500-EXIT                    02490000
                   UNTIL WS-F526-EOF = 'Y'.                             02500000
           PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT.                   02510000
                                                                        02520000
       0000-EXIT.                                                       02530000
           EXIT.                                                        02540000
                                                                        02550000
      *----------------*                                                02560000
       1000-OPEN-FILES.                                                 02570000
      *----------------*                                                02580000
                                                                        02590000
           OPEN INPUT  AC28F526                                         02600000
                I-O    BP13K130                                         02610000
                OUTPUT BP13F52A                                         02620000
                       BP13LB0E.                                        02630000
                                                                        02640000
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                       02650000
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)        02660000
              DELIMITED BY SIZE INTO LB0E-DATE.                         02670000
                                                                        02680000
           ACCEPT WS-DTE-CCYYMMDD   FROM   DATE YYYYMMDD.               02690000
           ACCEPT WS-TIME-HHMMSS    FROM   TIME.                        02700000
                                                                        02710000
           PERFORM 1100-SET-DB2-TIMESTAMP THRU 1100-EXIT.               02720000
                                                                        02730000
       1000-EXIT.                                                       02740000
           EXIT.                                                        02750000
                                                                        02760000
      *----------------------------------------------------------------*02770000
       1100-SET-DB2-TIMESTAMP.                                          02780000
      *----------------------------------------------------------------*02790000
            EXEC SQL                                                    02800000
                 SET :WS-TIMESTAMP = CURRENT TIMESTAMP                  02810000
            END-EXEC.                                                   02820000
            IF SQLCODE NOT = 000                                        02830000
               MOVE SQLCODE          TO WS-SQLCODE                      02840000
               DISPLAY 'SQL ERROR SET DB2 TIMESTAMP'                    02850000
               DISPLAY 'SQL ERROR CODE  : '  WS-SQLCODE                 02860000
               PERFORM 9999-SQL-ERROR THRU 9999-EXIT                    02870000
            END-IF.                                                     02880000
                                                                        02890000
       1100-EXIT.                                                       02900000
           EXIT.                                                        02910000
                                                                        02920000
      *-------------------*                                             02930000
       1200-READ-AC28F526.                                              02940000
      *-------------------*                                             02950000
                                                                        02960000
           READ AC28F526 AT END                                         02970000
                MOVE 'Y' TO WS-F526-EOF                                 02980000
                GO TO 1200-EXIT.                                        02990000
                                                                        03000000
           ADD 1 TO CNT-F526-READ.                                      03010000
                                                                        03020000
       1200-EXIT.                                                       03030000
           EXIT.                                                        03040000
                                                                        03050000
      *-----------------*                                               03060000
       1500-PROCESS-REC.                                                03070000
      *-----------------*                                               03080000
           MOVE 'N'               TO WS-GRANT-ASMNT-FND.                03090000
                                                                        03100000
           IF F526-NUM-REF NOT = WS-PREV-NUM-REGN                       03110000
              MOVE 'N' TO WS-REGNO-UPDATED                              03120000
           END-IF.                                                      03130000
                                                                        03140000
           IF F526-NUM-RCPTCD = '0520C' OR '6023C' OR '6254D' OR 'INT  '03150000
              PERFORM 8100-UPD-RCVRY-INTRST  THRU 8100-EXIT             03160000
              PERFORM 3300-WRITE-TO-BP13K130 THRU 3300-EXIT             03170000
           ELSE                                                         03180000
              PERFORM 2100-READ-GRANT-ASMNT THRU 2100-EXIT              03190000
              IF WS-GRANT-ASMNT-FND = 'Y'                               03200000
                    PERFORM 4000-UPDATE-GRANT-ASMNT  THRU 4000-EXIT     03220000
                    PERFORM 4100-PROCESS-GRANT-ASMNT THRU 4100-EXIT     03230000
                    PERFORM 3300-WRITE-TO-BP13K130   THRU 3300-EXIT     03240000
              ELSE                                                      03260000
                 MOVE 'N' TO WS-EX-PROCESSED                            03270000
                 PERFORM 2200-READ-UPD-EX-GRNT-ASMNT THRU 2200-EXIT     03280000
                 IF WS-EX-PROCESSED = 'Y'                               03290000
                   PERFORM 3300-WRITE-TO-BP13K130 THRU 3300-EXIT        03300000
                 ELSE                                                   03310000
                   MOVE 'NO RECOVERY TO BE PROCESSED' TO LB0E-REMARK    03320000
                   PERFORM 6000-PRINT-ERROR-RPT THRU 6000-EXIT          03330000
                 END-IF                                                 03340000
              END-IF                                                    03350000
              IF WS-GRANT-ASMNT-FND = 'N' AND                           03360000
                 WS-EX-PROCESSED = 'N'                                  03370000
                 PERFORM 3500-WRITE-TO-BP13F52A THRU 3500-EXIT          03380000
              END-IF                                                    03390000
           END-IF.                                                      03400000
                                                                        03410000
           MOVE F526-NUM-REF TO WS-PREV-NUM-REGN.                       03420000
           PERFORM 1200-READ-AC28F526 THRU 1200-EXIT.                   03430000
                                                                        03440000
       1500-EXIT.                                                       03450000
           EXIT.                                                        03460000
                                                                        03470000
      *--------------------*                                            03480000
       2100-READ-GRANT-ASMNT.                                           03490000
      *--------------------*                                            03500000
           MOVE F526-NUM-REF     TO NUM-RGSTRN OF DCLGRANT-ASMNT.       03510000
           EVALUATE F526-NUM-RCPTCD                                     03520000
            WHEN '6019Z'                                                03530000
            WHEN '6013I'                                                03540000
              MOVE 'SRG   '      TO NUM-GRANT-TYPE OF DCLGRANT-ASMNT    03550000
            WHEN '6015B'                                                03560000
              MOVE 'SUG   '      TO NUM-GRANT-TYPE OF DCLGRANT-ASMNT    03570000
            WHEN '6020H'                                                03580000
            WHEN '6014J'                                                03590000
              MOVE 'AHG   '      TO NUM-GRANT-TYPE OF DCLGRANT-ASMNT    03600000
            WHEN '6011E'                                                03610000
            WHEN '6021Z'                                                03620000
              MOVE 'SHG   '      TO NUM-GRANT-TYPE OF DCLGRANT-ASMNT    03630000
            WHEN 'AHG  '                                                03640000
            WHEN 'SHG  '                                                03650000
            WHEN 'SRG  '                                                03660000
            WHEN 'SUG  '                                                03670000
            WHEN 'EHG  '                                                03680000
            WHEN 'HGT  '                                                03690000
            WHEN 'REG  '                                                03700000
            WHEN 'PHG  '                                                03710000
            WHEN 'TUG  '                                                03720000
            WHEN 'ATG  '                                                03730000
            WHEN 'FSG  '                                                03740000
              MOVE F526-NUM-RCPTCD                                      03750000
                                 TO NUM-GRANT-TYPE OF DCLGRANT-ASMNT    03760000
           END-EVALUATE.                                                03770000
                                                                        03780000
           MOVE 'N'              TO WS-GRANT-ASMNT-FND.                 03790000
                                                                        03800000
           EXEC SQL                                                     03810000
              SELECT NUM_RGSTRN,                                        03820000
                     NUM_GRANT_TYPE,                                    03830000
                     NUM_ELGBL_GRANT,                                   03840000
                     NUM_GRANT_AMOUNT_ELGBL,                            03850000
                     NUM_GRANT_STATUS,                                  03860000
                     DTE_APRV,                                          03870000
                     DTE_CANCEL,                                        03880000
                     DTE_REJECT,                                        03890000
                     DTE_SUBMIT,                                        03900000
                     DTE_WTHDRW,                                        03910000
                     NUM_RJCTN_REASON,                                  03920000
                     NUM_WTDRWL_REASON,                                 03930000
                     TXT_CNCLTN_REASON,                                 03940000
                     DTE_GRANT_RQST,                                    03950000
                     NUM_GRANT_DOC_RCV,                                 03960000
                     DTE_ACK,                                           03970000
                     DTE_GRANT_FORM_ISSUE,                              03980000
                     DTE_GRANT_OPT_OUT_FORM_ISSUE,                      03990000
                     NUM_GRANT_OPT_OUT_REASON,                          04000000
                     DTE_GRANT_OPT_OUT_REASON,                          04010000
                     NUM_ALCTN_SCHEME,                                  04020000
                     NUM_HSHLD_CTZNSP,                                  04030000
                     NUM_WRKNG_CHILD_ASMNT,                             04040000
                     QTY_MONTH_WORK,                                    04050000
                     NUM_APPEAL_ATCHMT,                                 04060000
                     NUM_PARENT_ADRS_BLDNG,                             04070000
                     NUM_PARENT_ADRS_LEVEL,                             04080000
                     NUM_PARENT_ADRS_UNIT,                              04090000
                     TXT_PARENT_ADRS1,                                  04100000
                     TXT_PARENT_ADRS2,                                  04110000
                     NUM_PARENT_ADRS_POSTAL,                            04120000
                     DTE_PYMNT,                                         04130000
                     DTE_CPF_ACK_GRANT_RCV,                             04140000
                     NUM_GRANT_CREDIT,                                  04150000
                     TME_APNTMT,                                        04160000
                     DTE_APNTMT,                                        04170000
                     TME_CREATE_ASMNT,                                  04180000
                     NUM_UPDATE_USERID,                                 04190000
                     TME_UPDATE,                                        04200000
                     DTE_UPDATE,                                        04210000
                     NUM_SOURCE_SYSTEM,                                 04220000
                     NUM_ACNT,                                          04230000
                     NUM_SCHEME,                                        04240000
                     NUM_TNCY,                                          04250000
                     NUM_HALF_GRANT                                     04260000
              INTO  :DCLGRANT-ASMNT.NUM-RGSTRN,                         04270000
                    :DCLGRANT-ASMNT.NUM-GRANT-TYPE,                     04280000
                    :DCLGRANT-ASMNT.NUM-ELGBL-GRANT,                    04290000
                    :DCLGRANT-ASMNT.NUM-GRANT-AMOUNT-ELGBL,             04300000
                    :DCLGRANT-ASMNT.NUM-GRANT-STATUS,                   04310000
                    :DCLGRANT-ASMNT.DTE-APRV,                           04320000
                    :DCLGRANT-ASMNT.DTE-CANCEL,                         04330000
                    :DCLGRANT-ASMNT.DTE-REJECT,                         04340000
                    :DCLGRANT-ASMNT.DTE-SUBMIT,                         04350000
                    :DCLGRANT-ASMNT.DTE-WTHDRW,                         04360000
                    :DCLGRANT-ASMNT.NUM-RJCTN-REASON,                   04370000
                    :DCLGRANT-ASMNT.NUM-WTDRWL-REASON,                  04380000
                    :DCLGRANT-ASMNT.TXT-CNCLTN-REASON,                  04390000
                    :DCLGRANT-ASMNT.DTE-GRANT-RQST,                     04400000
                    :DCLGRANT-ASMNT.NUM-GRANT-DOC-RCV,                  04410000
                    :DCLGRANT-ASMNT.DTE-ACK,                            04420000
                    :DCLGRANT-ASMNT.DTE-GRANT-FORM-ISSUE,               04430000
                    :DCLGRANT-ASMNT.DTE-GRANT-OPT-OUT-FORM-ISSUE,       04440000
                    :DCLGRANT-ASMNT.NUM-GRANT-OPT-OUT-REASON,           04450000
                    :DCLGRANT-ASMNT.DTE-GRANT-OPT-OUT-REASON,           04460000
                    :DCLGRANT-ASMNT.NUM-ALCTN-SCHEME,                   04470000
                    :DCLGRANT-ASMNT.NUM-HSHLD-CTZNSP,                   04480000
                    :DCLGRANT-ASMNT.NUM-WRKNG-CHILD-ASMNT,              04490000
                    :DCLGRANT-ASMNT.QTY-MONTH-WORK,                     04500000
                    :DCLGRANT-ASMNT.NUM-APPEAL-ATCHMT,                  04510000
                    :DCLGRANT-ASMNT.NUM-PARENT-ADRS-BLDNG,              04520000
                    :DCLGRANT-ASMNT.NUM-PARENT-ADRS-LEVEL,              04530000
                    :DCLGRANT-ASMNT.NUM-PARENT-ADRS-UNIT,               04540000
                    :DCLGRANT-ASMNT.TXT-PARENT-ADRS1,                   04550000
                    :DCLGRANT-ASMNT.TXT-PARENT-ADRS2,                   04560000
                    :DCLGRANT-ASMNT.NUM-PARENT-ADRS-POSTAL,             04570000
                    :DCLGRANT-ASMNT.DTE-PYMNT,                          04580000
                    :DCLGRANT-ASMNT.DTE-CPF-ACK-GRANT-RCV,              04590000
                    :DCLGRANT-ASMNT.NUM-GRANT-CREDIT,                   04600000
                    :DCLGRANT-ASMNT.TME-APNTMT,                         04610000
                    :DCLGRANT-ASMNT.DTE-APNTMT,                         04620000
                    :DCLGRANT-ASMNT.TME-CREATE-ASMNT,                   04630000
                    :DCLGRANT-ASMNT.NUM-UPDATE-USERID,                  04640000
                    :DCLGRANT-ASMNT.TME-UPDATE,                         04650000
                    :DCLGRANT-ASMNT.DTE-UPDATE,                         04660000
                    :DCLGRANT-ASMNT.NUM-SOURCE-SYSTEM,                  04670000
                    :DCLGRANT-ASMNT.NUM-ACNT,                           04680000
                    :DCLGRANT-ASMNT.NUM-SCHEME,                         04690000
                    :DCLGRANT-ASMNT.NUM-TNCY,                           04700000
                    :DCLGRANT-ASMNT.NUM-HALF-GRANT                      04710000
              FROM                                                      04720000
                GRANT_ASMNT                                             04730000
              WHERE NUM_RGSTRN       = :DCLGRANT-ASMNT.NUM-RGSTRN       04740000
               AND  NUM_GRANT_TYPE   = :DCLGRANT-ASMNT.NUM-GRANT-TYPE   04750000
           END-EXEC.                                                    04770000
                                                                        04780000
           EVALUATE SQLCODE                                             04790000
           WHEN WS-SQL-OK                                               04800000
                MOVE 'Y' TO WS-GRANT-ASMNT-FND                          04810000
                                                                        04820000
           WHEN WS-SQL-NFND                                             04830000
                MOVE 'N' TO WS-GRANT-ASMNT-FND                          04840000
                MOVE SQLCODE   TO WS-SQLCODE                            04850000
                DISPLAY 'CASE NOTFND IN GRANT_ASMNT'                    04860000
                DISPLAY 'SQLCODE IS : ' WS-SQLCODE                      04870000
                DISPLAY 'REGNO : ' NUM-RGSTRN OF DCLGRANT-ASMNT         04880000
                DISPLAY 'GRANT TYPE: ' NUM-GRANT-TYPE OF DCLGRANT-ASMNT 04890000
                DISPLAY SPACES                                          04900000
                                                                        04910000
           WHEN OTHER                                                   04920000
                MOVE SQLCODE   TO WS-SQLCODE                            04930000
                DISPLAY 'ERROR READING GRANT_ASMNT'                     04940000
                DISPLAY 'SQLCODE IS : ' WS-SQLCODE                      04950000
                DISPLAY SPACES                                          04960000
                PERFORM 9999-SQL-ERROR      THRU 9999-EXIT              04970000
           END-EVALUATE.                                                04980000
                                                                        04990000
       2100-EXIT.                                                       05000000
           EXIT.                                                        05010000
                                                                        05020000
      *================================================================*05030000
       2200-READ-UPD-EX-GRNT-ASMNT.                                     05040000
      *================================================================*05050000
           PERFORM 2210-OPEN-CURSOR2    THRU 2210-EXIT.                 05060000
           PERFORM 2220-FETCH-CURSOR2   THRU 2220-EXIT                  05070000
                    UNTIL WS-CURSOR2-EOF = 'Y'.                         05080000
           PERFORM 2230-CLOSE-CURSOR2   THRU 2230-EXIT.                 05090000
                                                                        05100000
       2200-EXIT.                                                       05110000
           EXIT.                                                        05120000
                                                                        05130000
      *================================================================*05140000
       2210-OPEN-CURSOR2.                                               05150000
      *================================================================*05160000
           INITIALIZE SQLCODE.                                          05170000
           MOVE 'N'                   TO WS-CURSOR2-EOF.                05180000
           MOVE F526-NUM-REF     TO NUM-RGSTRN OF DCLEX-GRANT-ASMNT.    05190000
                                                                        05200000
           EVALUATE F526-NUM-RCPTCD                                     05210000
            WHEN '6019Z'                                                05220000
            WHEN '6013I'                                                05230000
              MOVE 'SRG   '      TO NUM-GRANT-TYPE OF DCLEX-GRANT-ASMNT 05240000
            WHEN '6015B'                                                05250000
              MOVE 'SUG   '      TO NUM-GRANT-TYPE OF DCLEX-GRANT-ASMNT 05260000
            WHEN '6020H'                                                05270000
            WHEN '6014J'                                                05280000
              MOVE 'AHG   '      TO NUM-GRANT-TYPE OF DCLEX-GRANT-ASMNT 05290000
            WHEN '6011E'                                                05300000
            WHEN '6021Z'                                                05310000
              MOVE 'SHG   '      TO NUM-GRANT-TYPE OF DCLEX-GRANT-ASMNT 05320000
            WHEN 'AHG  '                                                05330000
            WHEN 'SHG  '                                                05340000
            WHEN 'SRG  '                                                05350000
            WHEN 'SUG  '                                                05360000
            WHEN 'EHG  '                                                05370000
            WHEN 'HGT  '                                                05380000
            WHEN 'REG  '                                                05390000
            WHEN 'PHG  '                                                05400000
            WHEN 'TUG  '                                                05410000
            WHEN 'ATG  '                                                05420000
            WHEN 'FSG  '                                                05430000
              MOVE F526-NUM-RCPTCD                                      05440000
                                 TO NUM-GRANT-TYPE OF DCLEX-GRANT-ASMNT 05450000
           END-EVALUATE.                                                05460000
                                                                        05470000
      *------------------------------------------*                      05480000
      *  DECLARE CURSOR2 - EX GRANT ASMNT                               05490000
      *------------------------------------------*                      05500000
           EXEC SQL                                                     05510000
             DECLARE CURSOR2 CURSOR FOR                                 05520000
               SELECT NUM_RGSTRN,                                       05530000
                      NUM_GRANT_TYPE,                                   05540000
                      TME_CREATE,                                       05550000
                      NUM_ELGBL_GRANT,                                  05560000
                      NUM_GRANT_AMOUNT_ELGBL,                           05570000
                      NUM_GRANT_STATUS,                                 05580000
                      DTE_APRV,                                         05590000
                      DTE_CANCEL,                                       05600000
                      DTE_REJECT,                                       05610000
                      DTE_SUBMIT,                                       05620000
                      DTE_WTHDRW,                                       05630000
                      NUM_RJCTN_REASON,                                 05640000
                      NUM_WTDRWL_REASON,                                05650000
                      TXT_CNCLTN_REASON,                                05660000
                      DTE_GRANT_RQST,                                   05670000
                      NUM_GRANT_DOC_RCV,                                05680000
                      DTE_ACK,                                          05690000
                      DTE_GRANT_FORM_ISSUE,                             05700000
                      DTE_GRANT_OPT_OUT_FORM_ISSUE,                     05710000
                      NUM_GRANT_OPT_OUT_REASON,                         05720000
                      DTE_GRANT_OPT_OUT_REASON,                         05730000
                      NUM_ALCTN_SCHEME,                                 05740000
                      NUM_HSHLD_CTZNSP,                                 05750000
                      NUM_WRKNG_CHILD_ASMNT,                            05760000
                      QTY_MONTH_WORK,                                   05770000
                      NUM_APPEAL_ATCHMT,                                05780000
                      NUM_PARENT_ADRS_BLDNG,                            05790000
                      NUM_PARENT_ADRS_LEVEL,                            05800000
                      NUM_PARENT_ADRS_UNIT,                             05810000
                      TXT_PARENT_ADRS1,                                 05820000
                      TXT_PARENT_ADRS2,                                 05830000
                      NUM_PARENT_ADRS_POSTAL,                           05840000
                      DTE_PYMNT,                                        05850000
                      DTE_CPF_ACK_GRANT_RCV,                            05860000
                      NUM_GRANT_CREDIT,                                 05870000
                      TME_APNTMT,                                       05880000
                      DTE_APNTMT,                                       05890000
                      TME_CREATE_ASMNT,                                 05900000
                      NUM_UPDATE_USERID,                                05910000
                      TME_UPDATE,                                       05920000
                      DTE_UPDATE,                                       05930000
                      NUM_SOURCE_SYSTEM,                                05940000
                      NUM_ACNT,                                         05950000
                      NUM_SCHEME,                                       05960000
                      NUM_TNCY,                                         05970000
                      NUM_HALF_GRANT                                    05980000
               FROM                                                     05990000
                 EX_GRANT_ASMNT                                         06000000
               WHERE                                                    06010000
                 NUM_RGSTRN     = :DCLEX-GRANT-ASMNT.NUM-RGSTRN AND     06020000
                 NUM_GRANT_TYPE = :DCLEX-GRANT-ASMNT.NUM-GRANT-TYPE AND 06030000
                 (TXT_CNCLTN_REASON IN ('RC00') OR                      06040000
                  TXT_CNCLTN_REASON IN ('RC01') OR                      06050000
                  TXT_CNCLTN_REASON IN ('RC02') OR                      06060000
                  TXT_CNCLTN_REASON IN ('GR  '))                        06070000
           END-EXEC.                                                    06080000
                                                                        06090000
      *----------------------------------------*                        06100000
      *  OPEN CURSOR2 - EX GRANT ASMNT         *                        06110000
      *----------------------------------------*                        06120000
           EXEC SQL                                                     06130000
             OPEN CURSOR2                                               06140000
           END-EXEC.                                                    06150000
                                                                        06160000
           EVALUATE SQLCODE                                             06170000
           WHEN 0                                                       06180000
             CONTINUE                                                   06190000
           WHEN OTHER                                                   06200000
             MOVE SQLCODE            TO WS-SQLCODE                      06210000
             DISPLAY 'OPEN CURSOR2 ERROR'                               06220000
             GO TO 9999-SQL-ERROR                                       06230000
           END-EVALUATE.                                                06240000
                                                                        06250000
       2210-EXIT.                                                       06260000
            EXIT.                                                       06270000
                                                                        06280000
      *================================================================*06290000
       2220-FETCH-CURSOR2.                                              06300000
      *================================================================*06310000
                                                                        06320000
           EXEC SQL                                                     06330000
             FETCH CURSOR2                                              06340000
               INTO                                                     06350000
                 :DCLEX-GRANT-ASMNT.NUM-RGSTRN,                         06360000
                 :DCLEX-GRANT-ASMNT.NUM-GRANT-TYPE,                     06370000
                 :DCLEX-GRANT-ASMNT.TME-CREATE,                         06380000
                 :DCLEX-GRANT-ASMNT.NUM-ELGBL-GRANT,                    06390000
                 :DCLEX-GRANT-ASMNT.NUM-GRANT-AMOUNT-ELGBL,             06400000
                 :DCLEX-GRANT-ASMNT.NUM-GRANT-STATUS,                   06410000
                 :DCLEX-GRANT-ASMNT.DTE-APRV,                           06420000
                 :DCLEX-GRANT-ASMNT.DTE-CANCEL,                         06430000
                 :DCLEX-GRANT-ASMNT.DTE-REJECT,                         06440000
                 :DCLEX-GRANT-ASMNT.DTE-SUBMIT,                         06450000
                 :DCLEX-GRANT-ASMNT.DTE-WTHDRW,                         06460000
                 :DCLEX-GRANT-ASMNT.NUM-RJCTN-REASON,                   06470000
                 :DCLEX-GRANT-ASMNT.NUM-WTDRWL-REASON,                  06480000
                 :DCLEX-GRANT-ASMNT.TXT-CNCLTN-REASON,                  06490000
                 :DCLEX-GRANT-ASMNT.DTE-GRANT-RQST,                     06500000
                 :DCLEX-GRANT-ASMNT.NUM-GRANT-DOC-RCV,                  06510000
                 :DCLEX-GRANT-ASMNT.DTE-ACK,                            06520000
                 :DCLEX-GRANT-ASMNT.DTE-GRANT-FORM-ISSUE,               06530000
                 :DCLEX-GRANT-ASMNT.DTE-GRANT-OPT-OUT-FORM-ISSUE,       06540000
                 :DCLEX-GRANT-ASMNT.NUM-GRANT-OPT-OUT-REASON,           06550000
                 :DCLEX-GRANT-ASMNT.DTE-GRANT-OPT-OUT-REASON,           06560000
                 :DCLEX-GRANT-ASMNT.NUM-ALCTN-SCHEME,                   06570000
                 :DCLEX-GRANT-ASMNT.NUM-HSHLD-CTZNSP,                   06580000
                 :DCLEX-GRANT-ASMNT.NUM-WRKNG-CHILD-ASMNT,              06590000
                 :DCLEX-GRANT-ASMNT.QTY-MONTH-WORK,                     06600000
                 :DCLEX-GRANT-ASMNT.NUM-APPEAL-ATCHMT,                  06610000
                 :DCLEX-GRANT-ASMNT.NUM-PARENT-ADRS-BLDNG,              06620000
                 :DCLEX-GRANT-ASMNT.NUM-PARENT-ADRS-LEVEL,              06630000
                 :DCLEX-GRANT-ASMNT.NUM-PARENT-ADRS-UNIT,               06640000
                 :DCLEX-GRANT-ASMNT.TXT-PARENT-ADRS1,                   06650000
                 :DCLEX-GRANT-ASMNT.TXT-PARENT-ADRS2,                   06660000
                 :DCLEX-GRANT-ASMNT.NUM-PARENT-ADRS-POSTAL,             06670000
                 :DCLEX-GRANT-ASMNT.DTE-PYMNT,                          06680000
                 :DCLEX-GRANT-ASMNT.DTE-CPF-ACK-GRANT-RCV,              06690000
                 :DCLEX-GRANT-ASMNT.NUM-GRANT-CREDIT,                   06700000
                 :DCLEX-GRANT-ASMNT.TME-APNTMT,                         06710000
                 :DCLEX-GRANT-ASMNT.DTE-APNTMT,                         06720000
                 :DCLEX-GRANT-ASMNT.TME-CREATE-ASMNT,                   06730000
                 :DCLEX-GRANT-ASMNT.NUM-UPDATE-USERID,                  06740000
                 :DCLEX-GRANT-ASMNT.TME-UPDATE,                         06750000
                 :DCLEX-GRANT-ASMNT.DTE-UPDATE,                         06760000
                 :DCLEX-GRANT-ASMNT.NUM-SOURCE-SYSTEM,                  06770000
                 :DCLEX-GRANT-ASMNT.NUM-ACNT,                           06780000
                 :DCLEX-GRANT-ASMNT.NUM-SCHEME,                         06790000
                 :DCLEX-GRANT-ASMNT.NUM-TNCY,                           06800000
                 :DCLEX-GRANT-ASMNT.NUM-HALF-GRANT                      06810000
           END-EXEC.                                                    06820000
                                                                        06830000
           EVALUATE SQLCODE                                             06840000
           WHEN WS-SQL-OK                                               06850000
                IF TXT-CNCLTN-REASON OF DCLEX-GRANT-ASMNT = 'RC01' OR   06860000
                                                            'RC02'      06870000
                   MOVE TME-CREATE OF DCLEX-GRANT-ASMNT                 06880000
                                                    TO WS-TIME-CREATE   06890000
                   PERFORM 7000-UPD-EX-GRANT-ASMNT  THRU 7000-EXIT      06900000
                   PERFORM 2500-READ-UPD-GRNT-RCVRY THRU 2500-EXIT      06910000
                ELSE                                                    06920000
                                                                        06930000
                   PERFORM 5000-PROCESS-PERSON     THRU 5000-EXIT       06940000
                                                                        06950000
                   IF WS-GRNT-PRSN-FND = 'Y'                            06960000
                      MOVE 'Y'  TO WS-EX-PROCESSED                      06970000
                   END-IF                                               06980000
                END-IF                                                  06990000
           WHEN WS-SQL-NFND                                             07000000
             MOVE 'Y'                  TO WS-CURSOR2-EOF                07010000
           WHEN OTHER                                                   07020000
             MOVE SQLCODE            TO WS-SQLCODE                      07030000
             DISPLAY 'FETCH CURSOR2 ERROR'                              07040000
             GO TO 9999-SQL-ERROR                                       07050000
           END-EVALUATE.                                                07060000
                                                                        07070000
       2220-EXIT.                                                       07080000
            EXIT.                                                       07090000
                                                                        07100000
      *---------------------------------------------------------------* 07110000
       2230-CLOSE-CURSOR2.                                              07120000
      *---------------------------------------------------------------* 07130000
           INITIALIZE SQLCODE.                                          07140000
                                                                        07150000
      *-----------------------------------*                             07160000
      * CLOSE CURSOR2 - EX GRANT ASMNT                                  07170000
      *-----------------------------------*                             07180000
           EXEC SQL CLOSE CURSOR2                                       07190000
           END-EXEC.                                                    07200000
                                                                        07210000
           EVALUATE SQLCODE                                             07220000
              WHEN 0                                                    07230000
                   CONTINUE                                             07240000
              WHEN OTHER                                                07250000
                MOVE SQLCODE TO WS-SQLCODE                              07260000
                DISPLAY 'CLOSE CURSOR2 ERROR'                           07270000
                GO TO 9999-SQL-ERROR                                    07280000
           END-EVALUATE.                                                07290000
                                                                        07300000
       2230-EXIT.                                                       07310000
           EXIT.                                                        07320000
                                                                        07330000
      *================================================================*07340000
       2500-READ-UPD-GRNT-RCVRY.                                        07350000
      *================================================================*07360000
           PERFORM 2510-OPEN-CURSOR3    THRU 2510-EXIT.                 07370000
           PERFORM 2520-FETCH-CURSOR3   THRU 2520-EXIT                  07380000
                   UNTIL WS-CURSOR3-EOF = 'Y'.                          07390000
           PERFORM 2530-CLOSE-CURSOR3   THRU 2530-EXIT.                 07400000
                                                                        07410000
       2500-EXIT.                                                       07420000
           EXIT.                                                        07430000
                                                                        07440000
      *================================================================*07450000
       2510-OPEN-CURSOR3.                                               07460000
      *================================================================*07470000
           INITIALIZE SQLCODE.                                          07480000
           MOVE 'N'              TO WS-CURSOR3-EOF.                     07490000
           MOVE F526-NUM-REF     TO NUM-RGSTRN OF DCLGRANT-RCVRY.       07500000
           MOVE F526-NUM-UIN     TO NUM-IDNTY-NO OF DCLGRANT-RCVRY.     07510000
           MOVE WS-TIME-CREATE   TO TME-CREATE OF DCLGRANT-RCVRY.       07520000
           EVALUATE F526-NUM-RCPTCD                                     07530000
            WHEN '6019Z'                                                07540000
            WHEN '6013I'                                                07550000
              MOVE 'SRG   '      TO NUM-GRANT-TYPE OF DCLGRANT-RCVRY    07560000
            WHEN '6015B'                                                07570000
              MOVE 'SUG   '      TO NUM-GRANT-TYPE OF DCLGRANT-RCVRY    07580000
            WHEN '6020H'                                                07590000
            WHEN '6014J'                                                07600000
              MOVE 'AHG   '      TO NUM-GRANT-TYPE OF DCLGRANT-RCVRY    07610000
            WHEN '6011E'                                                07620000
            WHEN '6021Z'                                                07630000
              MOVE 'SHG   '      TO NUM-GRANT-TYPE OF DCLGRANT-RCVRY    07640000
            WHEN 'AHG  '                                                07650000
            WHEN 'SHG  '                                                07660000
            WHEN 'SRG  '                                                07670000
            WHEN 'SUG  '                                                07680000
            WHEN 'EHG  '                                                07690000
            WHEN 'HGT  '                                                07700000
            WHEN 'REG  '                                                07710000
            WHEN 'PHG  '                                                07720000
            WHEN 'TUG  '                                                07730000
            WHEN 'ATG  '                                                07740000
            WHEN 'FSG  '                                                07750000
              MOVE F526-NUM-RCPTCD                                      07760000
                                 TO NUM-GRANT-TYPE OF DCLGRANT-RCVRY    07770000
           END-EVALUATE.                                                07780000
           MOVE 'N'              TO WS-RCVRY-FND.                       07790000
                                                                        07800000
      *------------------------------------------*                      07810000
      *  DECLARE CURSOR3 - GRANT RCVRY                                  07820000
      *------------------------------------------*                      07830000
           EXEC SQL                                                     07840000
             DECLARE CURSOR3 CURSOR FOR                                 07850000
               SELECT NUM_RGSTRN,                                       07860000
                      NUM_GRANT_TYPE,                                   07870000
                      TME_CREATE,                                       07880000
                      NUM_IDNTY_NO,                                     07890000
                      NUM_IDNTY_NO_TYPE,                                07900000
                      AMT_GRANT_RCVR,                                   07910000
                      AMT_GRANT_RCVR_INTRST,                            07920000
                      DTE_RQST_RCVR,                                    07930000
                      DTE_RCV_RCVR_GRANT,                               07940000
                      DTE_ACK_GRANT_RCVRY_CPF,                          07950000
                      DTE_GRANT_ABORT,                                  07960000
                      DTE_GRANT_RPDT,                                   07970000
                      NUM_TRNSFR,                                       07980000
                      DTE_TRNSFR,                                       07990000
                      NUM_ACNT,                                         08000000
                      NUM_SCHEME,                                       08010000
                      NUM_TNCY,                                         08020000
                      DTE_CPF_GRANT_RQST,                               08030000
                      DTE_CPF_GRANT_RCV,                                08040000
                      DTE_UNUSED_GRANT_RQST,                            08050000
                      DTE_UNUSED_GRANT_RCV,                             08060000
                      DTE_USED_GRANT_RQST,                              08070000
                      DTE_USED_GRANT_RCV,                               08080000
                      NUM_UPDATE_USERID,                                08090000
                      DTE_UPDATE,                                       08100000
                      TME_UPDATE                                        08110000
               FROM                                                     08120000
                 GRANT_RCVRY                                            08130000
               WHERE                                                    08140000
                 NUM_RGSTRN       = :DCLGRANT-RCVRY.NUM-RGSTRN AND      08150000
                 NUM_GRANT_TYPE   = :DCLGRANT-RCVRY.NUM-GRANT-TYPE AND  08160000
                 TME_CREATE       = :DCLGRANT-RCVRY.TME-CREATE AND      08170000
                 NUM_IDNTY_NO     = :DCLGRANT-RCVRY.NUM-IDNTY-NO        08180000
           END-EXEC.                                                    08190000
                                                                        08200000
      *----------------------------------------*                        08210000
      *  OPEN CURSOR3 - GRANT RCVRY            *                        08220000
      *----------------------------------------*                        08230000
           EXEC SQL                                                     08240000
             OPEN CURSOR3                                               08250000
           END-EXEC.                                                    08260000
                                                                        08270000
           EVALUATE SQLCODE                                             08280000
           WHEN 0                                                       08290000
             CONTINUE                                                   08300000
           WHEN OTHER                                                   08310000
             MOVE SQLCODE            TO WS-SQLCODE                      08320000
             DISPLAY 'OPEN CURSOR3 ERROR'                               08330000
             GO TO 9999-SQL-ERROR                                       08340000
           END-EVALUATE.                                                08350000
                                                                        08360000
       2510-EXIT.                                                       08370000
            EXIT.                                                       08380000
                                                                        08390000
      *================================================================*08400000
       2520-FETCH-CURSOR3.                                              08410000
      *================================================================*08420000
                                                                        08430000
           EXEC SQL                                                     08440000
             FETCH CURSOR3                                              08450000
               INTO                                                     08460000
                 :DCLGRANT-RCVRY.NUM-RGSTRN,                            08470000
                 :DCLGRANT-RCVRY.NUM-GRANT-TYPE,                        08480000
                 :DCLGRANT-RCVRY.TME-CREATE,                            08490000
                 :DCLGRANT-RCVRY.NUM-IDNTY-NO,                          08500000
                 :DCLGRANT-RCVRY.NUM-IDNTY-NO-TYPE,                     08510000
                 :DCLGRANT-RCVRY.AMT-GRANT-RCVR,                        08520000
                 :DCLGRANT-RCVRY.AMT-GRANT-RCVR-INTRST,                 08530000
                 :DCLGRANT-RCVRY.DTE-RQST-RCVR,                         08540000
                 :DCLGRANT-RCVRY.DTE-RCV-RCVR-GRANT,                    08550000
                 :DCLGRANT-RCVRY.DTE-ACK-GRANT-RCVRY-CPF,               08560000
                 :DCLGRANT-RCVRY.DTE-GRANT-ABORT,                       08570000
                 :DCLGRANT-RCVRY.DTE-GRANT-RPDT,                        08580000
                 :DCLGRANT-RCVRY.NUM-TRNSFR,                            08590000
                 :DCLGRANT-RCVRY.DTE-TRNSFR,                            08600000
                 :DCLGRANT-RCVRY.NUM-ACNT,                              08610000
                 :DCLGRANT-RCVRY.NUM-SCHEME,                            08620000
                 :DCLGRANT-RCVRY.NUM-TNCY,                              08630000
                 :DCLGRANT-RCVRY.DTE-CPF-GRANT-RQST,                    08640000
                 :DCLGRANT-RCVRY.DTE-CPF-GRANT-RCV,                     08650000
                 :DCLGRANT-RCVRY.DTE-UNUSED-GRANT-RQST,                 08660000
                 :DCLGRANT-RCVRY.DTE-UNUSED-GRANT-RCV,                  08670000
                 :DCLGRANT-RCVRY.DTE-USED-GRANT-RQST,                   08680000
                 :DCLGRANT-RCVRY.DTE-USED-GRANT-RCV,                    08690000
                 :DCLGRANT-RCVRY.NUM-UPDATE-USERID,                     08700000
                 :DCLGRANT-RCVRY.DTE-UPDATE,                            08710000
                 :DCLGRANT-RCVRY.TME-UPDATE                             08720000
           END-EXEC.                                                    08730000
                                                                        08740000
           EVALUATE SQLCODE                                             08750000
           WHEN WS-SQL-OK                                               08760000
                PERFORM 7100-UPD-GRANT-RCVRY THRU 7100-EXIT             08770000
           WHEN WS-SQL-NFND                                             08780000
             MOVE 'Y'                  TO WS-CURSOR3-EOF                08790000
           WHEN OTHER                                                   08800000
             MOVE SQLCODE            TO WS-SQLCODE                      08810000
             DISPLAY 'FETCH CURSOR3 ERROR'                              08820000
             GO TO 9999-SQL-ERROR                                       08830000
           END-EVALUATE.                                                08840000
                                                                        08850000
       2520-EXIT.                                                       08860000
            EXIT.                                                       08870000
                                                                        08880000
      *---------------------------------------------------------------* 08890000
       2530-CLOSE-CURSOR3.                                              08900000
      *---------------------------------------------------------------* 08910000
           INITIALIZE SQLCODE.                                          08920000
                                                                        08930000
      *-----------------------------------*                             08940000
      * CLOSE CURSOR3 - GRANT RCVRY                                     08950000
      *-----------------------------------*                             08960000
           EXEC SQL CLOSE CURSOR3                                       08970000
           END-EXEC.                                                    08980000
                                                                        08990000
           EVALUATE SQLCODE                                             09000000
              WHEN 0                                                    09010000
                   CONTINUE                                             09020000
              WHEN OTHER                                                09030000
                MOVE SQLCODE TO WS-SQLCODE                              09040000
                DISPLAY 'CLOSE CURSOR3 ERROR'                           09050000
                GO TO 9999-SQL-ERROR                                    09060000
           END-EVALUATE.                                                09070000
                                                                        09080000
       2530-EXIT.                                                       09090000
           EXIT.                                                        09100000
                                                                        09110000
      **********************************************************        09120000
       3300-WRITE-TO-BP13K130.                                          09130000
      **********************************************************        09140000
                                                                        09150000
           IF F526-NUM-RCPTCD = '6015B' OR '6020H' OR '6011E' OR        09160000
                                '6014J' OR '6021Z' OR '6254D'           09170000
              GO TO 3300-EXIT                                           09180000
           END-IF.                                                      09190000
                                                                        09200000
           MOVE SPACES                 TO  BP13K130-CDHIST.             09210000
           IF F526-NUM-RCPTCD = 'SRG  '                                 09220000
              MOVE '35'                   TO  K130-TRANS-TYPE           09230005
           ELSE                                                         09230100
              MOVE '11'                   TO  K130-TRANS-TYPE           09231005
           END-IF.                                                      09232000
           MOVE F526-NUM-REF           TO  K130-NUM-ORIG-REGN.          09240000
           MOVE F526-DTE-TXN           TO  K130-DTE-POST                09250000
                                           K130-DTE-TRANS.              09260000
           MOVE F526-NUM-UIN           TO  K130-NUM-NRIC.               09270000
           MOVE F526-NUM-RCPT          TO  K130-NUM-RECPT-JRNO.         09280000
           IF F526-AMT-TXN IS NOT NUMERIC                               09290000
              MOVE ZEROS               TO    F526-AMT-TXN               09300000
           END-IF.                                                      09310000
           IF F526-NUM-RCPTCD = '0520C' OR '6023C' OR '6254D' OR 'INT  '09320000
              MOVE F526-AMT-TXN        TO    K130-AMT-CASH              09330000
           ELSE                                                         09340000
              COMPUTE  K130-AMT-CASH =  F526-AMT-TXN * -1               09350000
           END-IF.                                                      09360000
           MOVE F526-NUM-CRDR          TO  K130-AMT-TYP.                09370000
           EVALUATE F526-NUM-RCPTCD                                     09380000
            WHEN '0520C'                                                09390000
            WHEN '6023C'                                                09400000
            WHEN '6254D'                                                09410000
              MOVE 'INT   '      TO  K130-PAYMENT-TYPE                  09420000
            WHEN '6019Z'                                                09430000
            WHEN '6013I'                                                09440000
              MOVE 'SRG   '      TO  K130-PAYMENT-TYPE                  09450000
            WHEN '6015B'                                                09460000
              MOVE 'SUG   '      TO  K130-PAYMENT-TYPE                  09470000
            WHEN '6020H'                                                09480000
            WHEN '6014J'                                                09490000
              MOVE 'AHG   '      TO  K130-PAYMENT-TYPE                  09500000
            WHEN '6011E'                                                09510000
            WHEN '6021Z'                                                09520000
              MOVE 'SHG   '      TO  K130-PAYMENT-TYPE                  09530000
            WHEN 'AHG  '                                                09540000
            WHEN 'INT  '                                                09550000
            WHEN 'SHG  '                                                09560000
            WHEN 'SRG  '                                                09570000
            WHEN 'SUG  '                                                09580000
            WHEN 'EHG  '                                                09590000
            WHEN 'HGT  '                                                09600000
            WHEN 'REG  '                                                09610000
            WHEN 'PHG  '                                                09620000
            WHEN 'TUG  '                                                09630000
            WHEN 'ATG  '                                                09640000
            WHEN 'FSG  '                                                09650000
              MOVE F526-NUM-RCPTCD                                      09660000
                                 TO K130-PAYMENT-TYPE                   09670000
           END-EVALUATE.                                                09680000
           MOVE F526-NUM-HDBREF        TO  K130-NUM-SCH-ACCT.           09690000
           MOVE ZEROS                  TO  K130-COUNT-HISTORY.          09700000
           MOVE F526-NUM-RCPTCD        TO  K130-NUM-RECEIPT-CODE.       09710000
                                                                        09720000
           WRITE BP13K130-CDHIST.                                       09730000
           IF BP13K130-STATUS > 0 AND NOT = 22 AND NOT = 02             09740000
               DISPLAY ' WRITE ERROR AT BP13K130 '                      09750000
               DISPLAY ' BP13K130-STATUS IS ' BP13K130-STATUS           09760000
               DISPLAY ' REGN NO IS: ' F526-NUM-REF                     09770000
               MOVE BP13K130-STATUS TO RETURN-CODE                      09780000
               PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                  09790000
           ELSE                                                         09800000
               IF BP13K130-STATUS  = 22                                 09810000
                  PERFORM 3400-WRITE-HISTORY THRU 3400-EXIT             09820000
                    UNTIL BP13K130-STATUS = 0.                          09830000
                                                                        09840000
           ADD 1 TO CNT-K130-WRITE.                                     09850000
                                                                        09860000
       3300-EXIT.                                                       09870000
           EXIT.                                                        09880000
                                                                        09890000
      **********************************************************        09900000
       3400-WRITE-HISTORY.                                              09910000
      **********************************************************        09920000
                                                                        09930000
           ADD 1     TO  K130-COUNT-HISTORY                             09940000
           WRITE BP13K130-CDHIST                                        09950000
           IF BP13K130-STATUS NOT = 0                                   09960000
              GO TO 3400-EXIT.                                          09970000
                                                                        09980000
       3400-EXIT.                                                       09990000
           EXIT.                                                        10000000
                                                                        10010000
      ******************************************************************10020000
       3500-WRITE-TO-BP13F52A.                                          10030000
      ******************************************************************10040000
                                                                        10050000
           MOVE  SPACES                TO    BP13F52A-REC.              10060000
           INITIALIZE                        BP13F52A-REC.              10070000
           MOVE F526-NUM-HDBREF        TO    F52A-NUM-HDBREF.           10080000
           MOVE F526-DTE-TXN           TO    F52A-DTE-TXN.              10090000
           MOVE F526-NUM-TXN-ID        TO    F52A-NUM-TXN-ID.           10100000
           MOVE F526-NUM-RCPT          TO    F52A-NUM-RCPT.             10110000
           MOVE F526-AMT-TXN           TO    F52A-AMT-TXN.              10120000
           MOVE F526-NUM-CRDR          TO    F52A-NUM-CRDR.             10130000
           MOVE F526-TXT-DESC-1        TO    F52A-TXT-DESC-1.           10140000
           MOVE F526-NUM-GL-REF        TO    F52A-NUM-GL-REF.           10150000
           MOVE F526-NUM-UIN           TO    BP13F52A-REC(104:9).       10160000
           MOVE F526-NUM-REF           TO    F52A-NUM-REF.              10170000
                                                                        10180000
           WRITE BP13F52A-REC.                                          10190000
           ADD 1 TO WS-F52A-WRITE.                                      10200000
                                                                        10210000
       3500-EXIT.                                                       10220000
           EXIT.                                                        10230000
                                                                        10240000
      *----------------------*                                          10250000
       4000-UPDATE-GRANT-ASMNT.                                         10260000
      *----------------------*                                          10270000
           MOVE DCLGRANT-ASMNT TO WS-BEFORE-IMAGE.                      10280000
           MOVE F526-NUM-REF TO NUM-RGSTRN        OF DCLGRANT-ASMNT.    10290000
           EVALUATE F526-NUM-RCPTCD                                     10300000
            WHEN '6019Z'                                                10310000
            WHEN '6013I'                                                10320000
              MOVE 'SRG   '      TO NUM-GRANT-TYPE    OF DCLGRANT-ASMNT 10330000
            WHEN '6015B'                                                10340000
              MOVE 'SUG   '      TO NUM-GRANT-TYPE    OF DCLGRANT-ASMNT 10350000
            WHEN '6020H'                                                10360000
            WHEN '6014J'                                                10370000
              MOVE 'AHG   '      TO NUM-GRANT-TYPE    OF DCLGRANT-ASMNT 10380000
            WHEN '6011E'                                                10390000
            WHEN '6021Z'                                                10400000
              MOVE 'SHG   '      TO NUM-GRANT-TYPE    OF DCLGRANT-ASMNT 10410000
            WHEN 'AHG  '                                                10420000
            WHEN 'SHG  '                                                10430000
            WHEN 'SRG  '                                                10440000
            WHEN 'SUG  '                                                10450000
            WHEN 'EHG  '                                                10460000
            WHEN 'HGT  '                                                10470000
            WHEN 'REG  '                                                10480000
            WHEN 'PHG  '                                                10490000
            WHEN 'TUG  '                                                10500000
            WHEN 'ATG  '                                                10510000
            WHEN 'FSG  '                                                10520000
              MOVE F526-NUM-RCPTCD                                      10530000
                                 TO NUM-GRANT-TYPE    OF DCLGRANT-ASMNT 10540000
           END-EVALUATE.                                                10550000
                                                                        10560000
           MOVE 'RC00'       TO TXT-CNCLTN-REASON OF DCLGRANT-ASMNT.    10570000
                                                                        10580000
           MOVE 'BP13CB0E '  TO NUM-UPDATE-USERID OF DCLGRANT-ASMNT.    10590000
                                                                        10600000
           STRING WS-DTE-CCYYMMDD(1:4) '-'                              10610000
                  WS-DTE-CCYYMMDD(5:2) '-'                              10620000
                  WS-DTE-CCYYMMDD(7:2)                                  10630000
                  DELIMITED BY SIZE INTO DTE-UPDATE OF DCLGRANT-ASMNT.  10640000
                                                                        10650000
           STRING WS-DTE-CCYYMMDD(1:4) '-'                              10660000
                  WS-DTE-CCYYMMDD(5:2) '-'                              10670000
                  WS-DTE-CCYYMMDD(7:2)                                  10680000
                  DELIMITED BY SIZE INTO DTE-CANCEL OF DCLGRANT-ASMNT.  10690000
                                                                        10700000
           STRING WS-TIME-HHMMSS(1:2) '.'                               10710000
                  WS-TIME-HHMMSS(3:2) '.'                               10720000
                  WS-TIME-HHMMSS(5:2)                                   10730000
                  DELIMITED BY SIZE INTO TME-UPDATE OF DCLGRANT-ASMNT.  10740000
                                                                        10750000
           EXEC SQL                                                     10760000
              UPDATE GRANT_ASMNT                                        10770000
              SET TXT_CNCLTN_REASON = :DCLGRANT-ASMNT.TXT-CNCLTN-REASON,10780000
                  NUM_UPDATE_USERID = :DCLGRANT-ASMNT.NUM-UPDATE-USERID,10790000
                  TME_UPDATE        = :DCLGRANT-ASMNT.TME-UPDATE,       10800000
                  DTE_UPDATE        = :DCLGRANT-ASMNT.DTE-UPDATE,       10810000
                  DTE_CANCEL        = :DCLGRANT-ASMNT.DTE-CANCEL        10820000
              WHERE NUM_RGSTRN      = :DCLGRANT-ASMNT.NUM-RGSTRN        10830000
               AND  NUM_GRANT_TYPE  = :DCLGRANT-ASMNT.NUM-GRANT-TYPE    10840000
           END-EXEC.                                                    10850000
                                                                        10860000
           EVALUATE SQLCODE                                             10870000
           WHEN WS-SQL-OK                                               10880000
                ADD 1       TO CNT-ASMNT-UPD                            10890000
                MOVE 'Y'    TO WS-REGNO-UPDATED                         10900000
                MOVE 'GRANT_ASMNT'  TO WS-SOURCE                        10910000
                MOVE 'U'            TO WS-ACTION                        10920000
                MOVE F526-NUM-REF   TO WS-RGSTRN                        10930000
                MOVE NUM-GRANT-TYPE OF DCLGRANT-ASMNT TO WS-GRNT-TYPE   10940000
                MOVE DCLGRANT-ASMNT TO WS-AFTER-IMAGE                   10950000
                PERFORM 6400-INSERT-TO-LOG     THRU 6400-EXIT           10960000
           WHEN WS-SQL-NFND                                             10970000
                MOVE SQLCODE   TO WS-SQLCODE                            10980000
                DISPLAY 'CASE NOTFND IN GRANT_ASMNT'                    10990000
                DISPLAY 'SQLCODE IS : ' WS-SQLCODE ' '                  11000000
                        'REGNO : ' NUM-RGSTRN OF DCLGRANT-ASMNT ' '     11010000
                        'GRANT TYPE: ' NUM-GRANT-TYPE OF DCLGRANT-ASMNT 11020000
                DISPLAY SPACES                                          11030000
                MOVE 'GRANT_ASMNT NOT UPDATED, CASE NOT FOUND'          11040000
                                                     TO LB0E-REMARK     11050000
                PERFORM 6000-PRINT-ERROR-RPT THRU 6000-EXIT             11060000
                                                                        11070000
           WHEN OTHER                                                   11080000
                MOVE SQLCODE   TO WS-SQLCODE                            11090000
                DISPLAY 'ERROR UPDATING GRANT_ASMNT'                    11100000
                DISPLAY 'SQLCODE IS : ' WS-SQLCODE                      11110000
                DISPLAY SPACES                                          11120000
                PERFORM 9999-SQL-ERROR      THRU 9999-EXIT              11130000
           END-EVALUATE.                                                11140000
                                                                        11150000
       4000-EXIT.                                                       11160000
           EXIT.                                                        11170000
                                                                        11180000
      *------------------------*                                        11190000
       4100-PROCESS-GRANT-ASMNT.                                        11200000
      *------------------------*                                        11210000
           MOVE F526-NUM-REF     TO NUM-RGSTRN OF DCLGRANT-ASMNT.       11220000
           EVALUATE F526-NUM-RCPTCD                                     11230000
            WHEN '6019Z'                                                11240000
            WHEN '6013I'                                                11250000
              MOVE 'SRG   '      TO NUM-GRANT-TYPE OF DCLGRANT-ASMNT    11260000
            WHEN '6015B'                                                11270000
              MOVE 'SUG   '      TO NUM-GRANT-TYPE OF DCLGRANT-ASMNT    11280000
            WHEN '6020H'                                                11290000
            WHEN '6014J'                                                11300000
              MOVE 'AHG   '      TO NUM-GRANT-TYPE OF DCLGRANT-ASMNT    11310000
            WHEN '6011E'                                                11320000
            WHEN '6021Z'                                                11330000
              MOVE 'SHG   '      TO NUM-GRANT-TYPE OF DCLGRANT-ASMNT    11340000
            WHEN 'AHG  '                                                11350000
            WHEN 'SHG  '                                                11360000
            WHEN 'SRG  '                                                11370000
            WHEN 'SUG  '                                                11380000
            WHEN 'EHG  '                                                11390000
            WHEN 'HGT  '                                                11400000
            WHEN 'REG  '                                                11410000
            WHEN 'PHG  '                                                11420000
            WHEN 'TUG  '                                                11430000
            WHEN 'ATG  '                                                11440000
            WHEN 'FSG  '                                                11450000
              MOVE F526-NUM-RCPTCD                                      11460000
                                 TO NUM-GRANT-TYPE OF DCLGRANT-ASMNT    11470000
           END-EVALUATE.                                                11480000
                                                                        11490000
           EXEC SQL                                                     11500000
              SELECT NUM_RGSTRN,                                        11510000
                     NUM_GRANT_TYPE,                                    11520000
                     NUM_ELGBL_GRANT,                                   11530000
                     NUM_GRANT_AMOUNT_ELGBL,                            11540000
                     NUM_GRANT_STATUS,                                  11550000
                     DTE_APRV,                                          11560000
                     DTE_CANCEL,                                        11570000
                     DTE_REJECT,                                        11580000
                     DTE_SUBMIT,                                        11590000
                     DTE_WTHDRW,                                        11600000
                     NUM_RJCTN_REASON,                                  11610000
                     NUM_WTDRWL_REASON,                                 11620000
                     TXT_CNCLTN_REASON,                                 11630000
                     DTE_GRANT_RQST,                                    11640000
                     NUM_GRANT_DOC_RCV,                                 11650000
                     DTE_ACK,                                           11660000
                     DTE_GRANT_FORM_ISSUE,                              11670000
                     DTE_GRANT_OPT_OUT_FORM_ISSUE,                      11680000
                     NUM_GRANT_OPT_OUT_REASON,                          11690000
                     DTE_GRANT_OPT_OUT_REASON,                          11700000
                     NUM_ALCTN_SCHEME,                                  11710000
                     NUM_HSHLD_CTZNSP,                                  11720000
                     NUM_WRKNG_CHILD_ASMNT,                             11730000
                     QTY_MONTH_WORK,                                    11740000
                     NUM_APPEAL_ATCHMT,                                 11750000
                     NUM_PARENT_ADRS_BLDNG,                             11760000
                     NUM_PARENT_ADRS_LEVEL,                             11770000
                     NUM_PARENT_ADRS_UNIT,                              11780000
                     TXT_PARENT_ADRS1,                                  11790000
                     TXT_PARENT_ADRS2,                                  11800000
                     NUM_PARENT_ADRS_POSTAL,                            11810000
                     DTE_PYMNT,                                         11820000
                     DTE_CPF_ACK_GRANT_RCV,                             11830000
                     NUM_GRANT_CREDIT,                                  11840000
                     TME_APNTMT,                                        11850000
                     DTE_APNTMT,                                        11860000
                     TME_CREATE_ASMNT,                                  11870000
                     NUM_UPDATE_USERID,                                 11880000
                     TME_UPDATE,                                        11890000
                     DTE_UPDATE,                                        11900000
                     NUM_SOURCE_SYSTEM,                                 11910000
                     NUM_ACNT,                                          11920000
                     NUM_SCHEME,                                        11930000
                     NUM_TNCY,                                          11940000
                     NUM_HALF_GRANT                                     11950000
              INTO  :DCLGRANT-ASMNT.NUM-RGSTRN,                         11960000
                    :DCLGRANT-ASMNT.NUM-GRANT-TYPE,                     11970000
                    :DCLGRANT-ASMNT.NUM-ELGBL-GRANT,                    11980000
                    :DCLGRANT-ASMNT.NUM-GRANT-AMOUNT-ELGBL,             11990000
                    :DCLGRANT-ASMNT.NUM-GRANT-STATUS,                   12000000
                    :DCLGRANT-ASMNT.DTE-APRV,                           12010000
                    :DCLGRANT-ASMNT.DTE-CANCEL,                         12020000
                    :DCLGRANT-ASMNT.DTE-REJECT,                         12030000
                    :DCLGRANT-ASMNT.DTE-SUBMIT,                         12040000
                    :DCLGRANT-ASMNT.DTE-WTHDRW,                         12050000
                    :DCLGRANT-ASMNT.NUM-RJCTN-REASON,                   12060000
                    :DCLGRANT-ASMNT.NUM-WTDRWL-REASON,                  12070000
                    :DCLGRANT-ASMNT.TXT-CNCLTN-REASON,                  12080000
                    :DCLGRANT-ASMNT.DTE-GRANT-RQST,                     12090000
                    :DCLGRANT-ASMNT.NUM-GRANT-DOC-RCV,                  12100000
                    :DCLGRANT-ASMNT.DTE-ACK,                            12110000
                    :DCLGRANT-ASMNT.DTE-GRANT-FORM-ISSUE,               12120000
                    :DCLGRANT-ASMNT.DTE-GRANT-OPT-OUT-FORM-ISSUE,       12130000
                    :DCLGRANT-ASMNT.NUM-GRANT-OPT-OUT-REASON,           12140000
                    :DCLGRANT-ASMNT.DTE-GRANT-OPT-OUT-REASON,           12150000
                    :DCLGRANT-ASMNT.NUM-ALCTN-SCHEME,                   12160000
                    :DCLGRANT-ASMNT.NUM-HSHLD-CTZNSP,                   12170000
                    :DCLGRANT-ASMNT.NUM-WRKNG-CHILD-ASMNT,              12180000
                    :DCLGRANT-ASMNT.QTY-MONTH-WORK,                     12190000
                    :DCLGRANT-ASMNT.NUM-APPEAL-ATCHMT,                  12200000
                    :DCLGRANT-ASMNT.NUM-PARENT-ADRS-BLDNG,              12210000
                    :DCLGRANT-ASMNT.NUM-PARENT-ADRS-LEVEL,              12220000
                    :DCLGRANT-ASMNT.NUM-PARENT-ADRS-UNIT,               12230000
                    :DCLGRANT-ASMNT.TXT-PARENT-ADRS1,                   12240000
                    :DCLGRANT-ASMNT.TXT-PARENT-ADRS2,                   12250000
                    :DCLGRANT-ASMNT.NUM-PARENT-ADRS-POSTAL,             12260000
                    :DCLGRANT-ASMNT.DTE-PYMNT,                          12270000
                    :DCLGRANT-ASMNT.DTE-CPF-ACK-GRANT-RCV,              12280000
                    :DCLGRANT-ASMNT.NUM-GRANT-CREDIT,                   12290000
                    :DCLGRANT-ASMNT.TME-APNTMT,                         12300000
                    :DCLGRANT-ASMNT.DTE-APNTMT,                         12310000
                    :DCLGRANT-ASMNT.TME-CREATE-ASMNT,                   12320000
                    :DCLGRANT-ASMNT.NUM-UPDATE-USERID,                  12330000
                    :DCLGRANT-ASMNT.TME-UPDATE,                         12340000
                    :DCLGRANT-ASMNT.DTE-UPDATE,                         12350000
                    :DCLGRANT-ASMNT.NUM-SOURCE-SYSTEM,                  12360000
                    :DCLGRANT-ASMNT.NUM-ACNT,                           12370000
                    :DCLGRANT-ASMNT.NUM-SCHEME,                         12380000
                    :DCLGRANT-ASMNT.NUM-TNCY,                           12390000
                    :DCLGRANT-ASMNT.NUM-HALF-GRANT                      12400000
              FROM                                                      12410000
                GRANT_ASMNT                                             12420000
              WHERE NUM_RGSTRN       = :DCLGRANT-ASMNT.NUM-RGSTRN       12430000
               AND  NUM_GRANT_TYPE   = :DCLGRANT-ASMNT.NUM-GRANT-TYPE   12440000
           END-EXEC.                                                    12450000
                                                                        12460000
           EVALUATE SQLCODE                                             12470000
           WHEN WS-SQL-OK                                               12480000
                MOVE 'Y' TO WS-GRANT-ASMNT-FND                          12490000
                PERFORM 4200-INSERT-TO-EX-GRANT THRU 4200-EXIT          12500000
                PERFORM 4300-DELETE-GRANT-ASMNT THRU 4300-EXIT          12510000
                PERFORM 5000-PROCESS-PERSON     THRU 5000-EXIT          12520000
           WHEN WS-SQL-NFND                                             12530000
                MOVE SQLCODE   TO WS-SQLCODE                            12540000
                DISPLAY 'CASE NOTFND IN GRANT_ASMNT'                    12550000
                DISPLAY 'SQLCODE IS : ' WS-SQLCODE                      12560000
                DISPLAY 'REGNO : ' NUM-RGSTRN OF DCLGRANT-ASMNT         12570000
                DISPLAY 'GRANT TYPE: ' NUM-GRANT-TYPE OF DCLGRANT-ASMNT 12580000
                DISPLAY SPACES                                          12590000
                                                                        12600000
           WHEN OTHER                                                   12610000
                MOVE SQLCODE   TO WS-SQLCODE                            12620000
                DISPLAY 'ERROR READING GRANT_ASMNT'                     12630000
                DISPLAY 'SQLCODE IS : ' WS-SQLCODE                      12640000
                DISPLAY SPACES                                          12650000
                PERFORM 9999-SQL-ERROR      THRU 9999-EXIT              12660000
           END-EVALUATE.                                                12670000
                                                                        12680000
       4100-EXIT.                                                       12690000
           EXIT.                                                        12700000
                                                                        12710000
      *----------------------*                                          12720000
       4200-INSERT-TO-EX-GRANT.                                         12730000
      *----------------------*                                          12740000
           MOVE NUM-RGSTRN OF DCLGRANT-ASMNT                            12750000
                    TO NUM-RGSTRN OF DCLEX-GRANT-ASMNT.                 12760000
           MOVE NUM-GRANT-TYPE OF DCLGRANT-ASMNT                        12770000
                    TO NUM-GRANT-TYPE OF DCLEX-GRANT-ASMNT.             12780000
           MOVE NUM-ELGBL-GRANT OF DCLGRANT-ASMNT                       12790000
                    TO NUM-ELGBL-GRANT OF DCLEX-GRANT-ASMNT.            12800000
           MOVE NUM-GRANT-AMOUNT-ELGBL OF DCLGRANT-ASMNT                12810000
                    TO NUM-GRANT-AMOUNT-ELGBL OF DCLEX-GRANT-ASMNT.     12820000
           MOVE NUM-GRANT-STATUS OF DCLGRANT-ASMNT                      12830000
                    TO NUM-GRANT-STATUS OF DCLEX-GRANT-ASMNT.           12840000
           MOVE DTE-APRV OF DCLGRANT-ASMNT                              12850000
                    TO DTE-APRV OF DCLEX-GRANT-ASMNT.                   12860000
           MOVE DTE-CANCEL OF DCLGRANT-ASMNT                            12870000
                    TO DTE-CANCEL OF DCLEX-GRANT-ASMNT.                 12880000
           MOVE DTE-REJECT OF DCLGRANT-ASMNT                            12890000
                    TO DTE-REJECT OF DCLEX-GRANT-ASMNT.                 12900000
           MOVE DTE-SUBMIT OF DCLGRANT-ASMNT                            12910000
                    TO DTE-SUBMIT OF DCLEX-GRANT-ASMNT.                 12920000
           MOVE DTE-WTHDRW OF DCLGRANT-ASMNT                            12930000
                    TO DTE-WTHDRW OF DCLEX-GRANT-ASMNT.                 12940000
           MOVE NUM-RJCTN-REASON OF DCLGRANT-ASMNT                      12950000
                    TO NUM-RJCTN-REASON OF DCLEX-GRANT-ASMNT.           12960000
           MOVE NUM-WTDRWL-REASON OF DCLGRANT-ASMNT                     12970000
                    TO NUM-WTDRWL-REASON OF DCLEX-GRANT-ASMNT.          12980000
           MOVE TXT-CNCLTN-REASON OF DCLGRANT-ASMNT                     12990000
                    TO TXT-CNCLTN-REASON OF DCLEX-GRANT-ASMNT.          13000000
           MOVE DTE-GRANT-RQST OF DCLGRANT-ASMNT                        13010000
                    TO DTE-GRANT-RQST OF DCLEX-GRANT-ASMNT.             13020000
           MOVE NUM-GRANT-DOC-RCV OF DCLGRANT-ASMNT                     13030000
                    TO NUM-GRANT-DOC-RCV OF DCLEX-GRANT-ASMNT.          13040000
           MOVE DTE-ACK OF DCLGRANT-ASMNT                               13050000
                    TO DTE-ACK OF DCLEX-GRANT-ASMNT.                    13060000
           MOVE DTE-GRANT-FORM-ISSUE OF DCLGRANT-ASMNT                  13070000
                    TO DTE-GRANT-FORM-ISSUE OF DCLEX-GRANT-ASMNT.       13080000
           MOVE DTE-GRANT-OPT-OUT-FORM-ISSUE OF DCLGRANT-ASMNT          13090000
                 TO DTE-GRANT-OPT-OUT-FORM-ISSUE OF DCLEX-GRANT-ASMNT.  13100000
           MOVE NUM-GRANT-OPT-OUT-REASON OF DCLGRANT-ASMNT              13110000
                 TO NUM-GRANT-OPT-OUT-REASON OF DCLEX-GRANT-ASMNT.      13120000
           MOVE DTE-GRANT-OPT-OUT-REASON OF DCLGRANT-ASMNT              13130000
                 TO DTE-GRANT-OPT-OUT-REASON OF DCLEX-GRANT-ASMNT.      13140000
           MOVE NUM-ALCTN-SCHEME OF DCLGRANT-ASMNT                      13150000
                 TO NUM-ALCTN-SCHEME OF DCLEX-GRANT-ASMNT.              13160000
           MOVE NUM-HSHLD-CTZNSP OF DCLGRANT-ASMNT                      13170000
                 TO NUM-HSHLD-CTZNSP OF DCLEX-GRANT-ASMNT.              13180000
           MOVE NUM-WRKNG-CHILD-ASMNT OF DCLGRANT-ASMNT                 13190000
                 TO NUM-WRKNG-CHILD-ASMNT OF DCLEX-GRANT-ASMNT.         13200000
           MOVE QTY-MONTH-WORK OF DCLGRANT-ASMNT                        13210000
                 TO QTY-MONTH-WORK OF DCLEX-GRANT-ASMNT.                13220000
           MOVE NUM-APPEAL-ATCHMT OF DCLGRANT-ASMNT                     13230000
                 TO NUM-APPEAL-ATCHMT OF DCLEX-GRANT-ASMNT.             13240000
           MOVE NUM-PARENT-ADRS-BLDNG OF DCLGRANT-ASMNT                 13250000
                 TO NUM-PARENT-ADRS-BLDNG OF DCLEX-GRANT-ASMNT.         13260000
           MOVE NUM-PARENT-ADRS-LEVEL OF DCLGRANT-ASMNT                 13270000
                 TO NUM-PARENT-ADRS-LEVEL OF DCLEX-GRANT-ASMNT.         13280000
           MOVE NUM-PARENT-ADRS-UNIT OF DCLGRANT-ASMNT                  13290000
                 TO NUM-PARENT-ADRS-UNIT OF DCLEX-GRANT-ASMNT.          13300000
           MOVE TXT-PARENT-ADRS1 OF DCLGRANT-ASMNT                      13310000
                 TO TXT-PARENT-ADRS1 OF DCLEX-GRANT-ASMNT.              13320000
           MOVE TXT-PARENT-ADRS2 OF DCLGRANT-ASMNT                      13330000
                 TO TXT-PARENT-ADRS2 OF DCLEX-GRANT-ASMNT.              13340000
           MOVE NUM-PARENT-ADRS-POSTAL OF DCLGRANT-ASMNT                13350000
                 TO NUM-PARENT-ADRS-POSTAL OF DCLEX-GRANT-ASMNT.        13360000
           MOVE DTE-PYMNT OF DCLGRANT-ASMNT                             13370000
                 TO DTE-PYMNT OF DCLEX-GRANT-ASMNT.                     13380000
           MOVE DTE-CPF-ACK-GRANT-RCV OF DCLGRANT-ASMNT                 13390000
                 TO DTE-CPF-ACK-GRANT-RCV OF DCLEX-GRANT-ASMNT.         13400000
           MOVE NUM-GRANT-CREDIT OF DCLGRANT-ASMNT                      13410000
                 TO NUM-GRANT-CREDIT OF DCLEX-GRANT-ASMNT.              13420000
           MOVE TME-APNTMT OF DCLGRANT-ASMNT                            13430000
                 TO TME-APNTMT OF DCLEX-GRANT-ASMNT.                    13440000
           MOVE DTE-APNTMT OF DCLGRANT-ASMNT                            13450000
                 TO DTE-APNTMT OF DCLEX-GRANT-ASMNT.                    13460000
           MOVE TME-CREATE-ASMNT OF DCLGRANT-ASMNT                      13470000
                 TO TME-CREATE-ASMNT OF DCLEX-GRANT-ASMNT.              13480000
           MOVE NUM-SOURCE-SYSTEM OF DCLGRANT-ASMNT                     13490000
                 TO NUM-SOURCE-SYSTEM OF DCLEX-GRANT-ASMNT.             13500000
           MOVE NUM-ACNT OF DCLGRANT-ASMNT                              13510000
                 TO NUM-ACNT OF DCLEX-GRANT-ASMNT.                      13520000
           MOVE NUM-SCHEME OF DCLGRANT-ASMNT                            13530000
                 TO NUM-SCHEME OF DCLEX-GRANT-ASMNT.                    13540000
           MOVE NUM-TNCY OF DCLGRANT-ASMNT                              13550000
                 TO NUM-TNCY OF DCLEX-GRANT-ASMNT.                      13560000
           MOVE NUM-HALF-GRANT OF DCLGRANT-ASMNT                        13570000
                 TO NUM-HALF-GRANT OF DCLEX-GRANT-ASMNT.                13580000
                                                                        13590000
           MOVE 'BP13CB0E ' TO NUM-UPDATE-USERID OF DCLEX-GRANT-ASMNT.  13600000
                                                                        13610000
           STRING WS-DTE-CCYYMMDD(1:4) '-'                              13620000
                  WS-DTE-CCYYMMDD(5:2) '-'                              13630000
                  WS-DTE-CCYYMMDD(7:2)                                  13640000
                 DELIMITED BY SIZE INTO DTE-UPDATE OF DCLEX-GRANT-ASMNT.13650000
                                                                        13660000
           STRING WS-TIME-HHMMSS(1:2) '.'                               13670000
                  WS-TIME-HHMMSS(3:2) '.'                               13680000
                  WS-TIME-HHMMSS(5:2)                                   13690000
                 DELIMITED BY SIZE INTO TME-UPDATE OF DCLEX-GRANT-ASMNT.13700000
                                                                        13710000
            MOVE WS-TIMESTAMP TO TME-CREATE OF DCLEX-GRANT-ASMNT.       13720000
                                                                        13730000
            EXEC SQL                                                    13740000
                 INSERT INTO EX_GRANT_ASMNT                             13750000
                        ( NUM_RGSTRN,                                   13760000
                          NUM_GRANT_TYPE,                               13770000
                          TME_CREATE,                                   13780000
                          NUM_ELGBL_GRANT,                              13790000
                          NUM_GRANT_AMOUNT_ELGBL,                       13800000
                          NUM_GRANT_STATUS,                             13810000
                          DTE_APRV,                                     13820000
                          DTE_CANCEL,                                   13830000
                          DTE_REJECT,                                   13840000
                          DTE_SUBMIT,                                   13850000
                          DTE_WTHDRW,                                   13860000
                          NUM_RJCTN_REASON,                             13870000
                          NUM_WTDRWL_REASON,                            13880000
                          TXT_CNCLTN_REASON,                            13890000
                          DTE_GRANT_RQST,                               13900000
                          NUM_GRANT_DOC_RCV,                            13910000
                          DTE_ACK,                                      13920000
                          DTE_GRANT_FORM_ISSUE,                         13930000
                          DTE_GRANT_OPT_OUT_FORM_ISSUE,                 13940000
                          NUM_GRANT_OPT_OUT_REASON,                     13950000
                          DTE_GRANT_OPT_OUT_REASON,                     13960000
                          NUM_ALCTN_SCHEME,                             13970000
                          NUM_HSHLD_CTZNSP,                             13980000
                          NUM_WRKNG_CHILD_ASMNT,                        13990000
                          QTY_MONTH_WORK,                               14000000
                          NUM_APPEAL_ATCHMT,                            14010000
                          NUM_PARENT_ADRS_BLDNG,                        14020000
                          NUM_PARENT_ADRS_LEVEL,                        14030000
                          NUM_PARENT_ADRS_UNIT,                         14040000
                          TXT_PARENT_ADRS1,                             14050000
                          TXT_PARENT_ADRS2,                             14060000
                          NUM_PARENT_ADRS_POSTAL,                       14070000
                          DTE_PYMNT,                                    14080000
                          DTE_CPF_ACK_GRANT_RCV,                        14090000
                          NUM_GRANT_CREDIT,                             14100000
                          TME_APNTMT,                                   14110000
                          DTE_APNTMT,                                   14120000
                          TME_CREATE_ASMNT,                             14130000
                          NUM_UPDATE_USERID,                            14140000
                          TME_UPDATE,                                   14150000
                          DTE_UPDATE )                                  14160000
                 VALUES ( :DCLEX-GRANT-ASMNT.NUM-RGSTRN,                14170000
                          :DCLEX-GRANT-ASMNT.NUM-GRANT-TYPE,            14180000
                          :DCLEX-GRANT-ASMNT.TME-CREATE,                14190000
                          :DCLEX-GRANT-ASMNT.NUM-ELGBL-GRANT,           14200000
                          :DCLEX-GRANT-ASMNT.NUM-GRANT-AMOUNT-ELGBL,    14210000
                          :DCLEX-GRANT-ASMNT.NUM-GRANT-STATUS,          14220000
                          :DCLEX-GRANT-ASMNT.DTE-APRV,                  14230000
                          :DCLEX-GRANT-ASMNT.DTE-CANCEL,                14240000
                          :DCLEX-GRANT-ASMNT.DTE-REJECT,                14250000
                          :DCLEX-GRANT-ASMNT.DTE-SUBMIT,                14260000
                          :DCLEX-GRANT-ASMNT.DTE-WTHDRW,                14270000
                          :DCLEX-GRANT-ASMNT.NUM-RJCTN-REASON,          14280000
                          :DCLEX-GRANT-ASMNT.NUM-WTDRWL-REASON,         14290000
                          :DCLEX-GRANT-ASMNT.TXT-CNCLTN-REASON,         14300000
                          :DCLEX-GRANT-ASMNT.DTE-GRANT-RQST,            14310000
                          :DCLEX-GRANT-ASMNT.NUM-GRANT-DOC-RCV,         14320000
                          :DCLEX-GRANT-ASMNT.DTE-ACK,                   14330000
                          :DCLEX-GRANT-ASMNT.DTE-GRANT-FORM-ISSUE,      14340000
                        :DCLEX-GRANT-ASMNT.DTE-GRANT-OPT-OUT-FORM-ISSUE,14350000
                          :DCLEX-GRANT-ASMNT.NUM-GRANT-OPT-OUT-REASON,  14360000
                          :DCLEX-GRANT-ASMNT.DTE-GRANT-OPT-OUT-REASON,  14370000
                          :DCLEX-GRANT-ASMNT.NUM-ALCTN-SCHEME,          14380000
                          :DCLEX-GRANT-ASMNT.NUM-HSHLD-CTZNSP,          14390000
                          :DCLEX-GRANT-ASMNT.NUM-WRKNG-CHILD-ASMNT,     14400000
                          :DCLEX-GRANT-ASMNT.QTY-MONTH-WORK,            14410000
                          :DCLEX-GRANT-ASMNT.NUM-APPEAL-ATCHMT,         14420000
                          :DCLEX-GRANT-ASMNT.NUM-PARENT-ADRS-BLDNG,     14430000
                          :DCLEX-GRANT-ASMNT.NUM-PARENT-ADRS-LEVEL,     14440000
                          :DCLEX-GRANT-ASMNT.NUM-PARENT-ADRS-UNIT,      14450000
                          :DCLEX-GRANT-ASMNT.TXT-PARENT-ADRS1,          14460000
                          :DCLEX-GRANT-ASMNT.TXT-PARENT-ADRS2,          14470000
                          :DCLEX-GRANT-ASMNT.NUM-PARENT-ADRS-POSTAL,    14480000
                          :DCLEX-GRANT-ASMNT.DTE-PYMNT,                 14490000
                          :DCLEX-GRANT-ASMNT.DTE-CPF-ACK-GRANT-RCV,     14500000
                          :DCLEX-GRANT-ASMNT.NUM-GRANT-CREDIT,          14510000
                          :DCLEX-GRANT-ASMNT.TME-APNTMT,                14520000
                          :DCLEX-GRANT-ASMNT.DTE-APNTMT,                14530000
                          :DCLEX-GRANT-ASMNT.TME-CREATE-ASMNT,          14540000
                          :DCLEX-GRANT-ASMNT.NUM-UPDATE-USERID,         14550000
                          :DCLEX-GRANT-ASMNT.TME-UPDATE,                14560000
                          :DCLEX-GRANT-ASMNT.DTE-UPDATE )               14570000
            END-EXEC.                                                   14580000
                                                                        14590000
            EVALUATE SQLCODE                                            14600000
               WHEN 000                                                 14610000
                    ADD 1                 TO WS-EX-GRANT-INS            14620000
                    MOVE 'EX_GRANT_ASMNT'  TO WS-SOURCE                 14630000
                    MOVE 'C'            TO WS-ACTION                    14640000
                    MOVE NUM-RGSTRN OF DCLEX-GRANT-ASMNT TO WS-RGSTRN   14650000
                    MOVE NUM-GRANT-TYPE OF DCLEX-GRANT-ASMNT            14660000
                                                  TO WS-GRNT-TYPE       14670000
                    MOVE SPACES            TO WS-BEFORE-IMAGE           14680000
                    MOVE DCLEX-GRANT-ASMNT TO WS-AFTER-IMAGE            14690000
                    PERFORM 6400-INSERT-TO-LOG     THRU 6400-EXIT       14700000
                                                                        14710000
               WHEN -803                                                14720000
                    DISPLAY 'RECORD ALREADY EXIST IN EX_GRANT_ASMNT'    14730000
                    DISPLAY NUM-RGSTRN OF DCLEX-GRANT-ASMNT ' '         14740000
                            NUM-GRANT-TYPE OF DCLEX-GRANT-ASMNT         14750000
                                                                        14760000
               WHEN OTHER                                               14770000
                    MOVE SQLCODE          TO WS-SQLCODE                 14780000
                    DISPLAY 'SQL ERROR (INSERT) IN EX_GRANT_ASMNT'      14790000
                    DISPLAY 'SQL ERROR CODE  : '  WS-SQLCODE            14800000
                    PERFORM 9999-SQL-ERROR      THRU 9999-EXIT          14810000
            END-EVALUATE.                                               14820000
                                                                        14830000
       4200-EXIT.                                                       14840000
           EXIT.                                                        14850000
                                                                        14860000
      *----------------------*                                          14870000
       4300-DELETE-GRANT-ASMNT.                                         14880000
      *----------------------*                                          14890000
            MOVE DCLGRANT-ASMNT                   TO WS-BEFORE-IMAGE.   14900000
            MOVE NUM-RGSTRN OF DCLGRANT-ASMNT     TO WS-RGSTRN.         14910000
            MOVE NUM-GRANT-TYPE OF DCLGRANT-ASMNT TO WS-GRNT-TYPE.      14920000
                                                                        14930000
            EXEC SQL                                                    14940000
                 DELETE FROM GRANT_ASMNT                                14950000
                 WHERE NUM_RGSTRN      = :DCLGRANT-ASMNT.NUM-RGSTRN     14960000
                 AND   NUM_GRANT_TYPE  = :DCLGRANT-ASMNT.NUM-GRANT-TYPE 14970000
            END-EXEC.                                                   14980000
                                                                        14990000
            EVALUATE SQLCODE                                            15000000
               WHEN 000                                                 15010000
                    ADD 1                 TO WS-GRANT-DELETE            15020000
                    MOVE 'GRANT_ASMNT'  TO WS-SOURCE                    15030000
                    MOVE 'D'            TO WS-ACTION                    15040000
                    MOVE SPACES         TO WS-AFTER-IMAGE               15050000
                    PERFORM 6400-INSERT-TO-LOG     THRU 6400-EXIT       15060000
                                                                        15070000
               WHEN 100                                                 15080000
                    DISPLAY 'RECORD NOT EXIST IN GRANT_ASMNT'           15090000
                    DISPLAY NUM-RGSTRN OF DCLGRANT-ASMNT ' '            15100000
                            NUM-GRANT-TYPE OF DCLGRANT-ASMNT            15110000
                    MOVE 'CANNOT DELETE FROM GRANT_ASMNT, CASE NOT FND' 15120000
                                                         TO LB0E-REMARK 15130000
                    PERFORM 6000-PRINT-ERROR-RPT THRU 6000-EXIT         15140000
                                                                        15150000
               WHEN OTHER                                               15160000
                    MOVE SQLCODE          TO WS-SQLCODE                 15170000
                    DISPLAY 'SQL ERROR (DELETE) IN GRANT_ASMNT TABLE '  15180000
                    DISPLAY 'SQL ERROR CODE  : '  WS-SQLCODE            15190000
            END-EVALUATE.                                               15200000
                                                                        15210000
       4300-EXIT.                                                       15220000
           EXIT.                                                        15230000
                                                                        15240000
      *================================================================*15250000
       5000-PROCESS-PERSON.                                             15260000
      *================================================================*15270000
           MOVE  'N'       TO  WS-GRNT-PRSN-FND.                        15280000
                                                                        15290000
           PERFORM 5100-OPEN-CURSOR1    THRU 5100-EXIT.                 15300000
           PERFORM 5200-FETCH-CURSOR1   THRU 5200-EXIT                  15310000
                   UNTIL WS-CURSOR1-EOF = 'Y'.                          15320000
           PERFORM 5300-CLOSE-CURSOR1   THRU 5300-EXIT.                 15330000
                                                                        15340000
       5000-EXIT.                                                       15350000
           EXIT.                                                        15360000
                                                                        15370000
      *================================================================*15380000
       5100-OPEN-CURSOR1.                                               15390000
      *================================================================*15400000
           INITIALIZE SQLCODE.                                          15410000
           MOVE 'N'                   TO WS-CURSOR1-EOF.                15420000
                                                                        15430000
           MOVE F526-NUM-REF TO NUM-RGSTRN OF DCLGRANT-ASMNT-PERSON.    15440000
           EVALUATE F526-NUM-RCPTCD                                     15450000
            WHEN '6019Z'                                                15460000
            WHEN '6013I'                                                15470000
              MOVE 'SRG   '  TO NUM-GRANT-TYPE OF DCLGRANT-ASMNT-PERSON 15480000
            WHEN '6015B'                                                15490000
              MOVE 'SUG   '  TO NUM-GRANT-TYPE OF DCLGRANT-ASMNT-PERSON 15500000
            WHEN '6020H'                                                15510000
            WHEN '6014J'                                                15520000
              MOVE 'AHG   '  TO NUM-GRANT-TYPE OF DCLGRANT-ASMNT-PERSON 15530000
            WHEN '6011E'                                                15540000
            WHEN '6021Z'                                                15550000
              MOVE 'SHG   '  TO NUM-GRANT-TYPE OF DCLGRANT-ASMNT-PERSON 15560000
            WHEN 'AHG  '                                                15570000
            WHEN 'SHG  '                                                15580000
            WHEN 'SRG  '                                                15590000
            WHEN 'SUG  '                                                15600000
            WHEN 'EHG  '                                                15610000
            WHEN 'HGT  '                                                15620000
            WHEN 'REG  '                                                15630000
            WHEN 'PHG  '                                                15640000
            WHEN 'TUG  '                                                15650000
            WHEN 'ATG  '                                                15660000
            WHEN 'FSG  '                                                15670000
              MOVE F526-NUM-RCPTCD                                      15680000
                             TO NUM-GRANT-TYPE OF DCLGRANT-ASMNT-PERSON 15690000
           END-EVALUATE.                                                15700000
                                                                        15710000
           MOVE F526-NUM-UIN                                            15720000
                             TO NUM-IDNTY-NO OF DCLGRANT-ASMNT-PERSON   15730000
                                                                        15740000
      *------------------------------------------*                      15750000
      *  DECLARE CURSOR1 - GRANT ASSESSMENT PERSON                      15760000
      *------------------------------------------*                      15770000
           EXEC SQL                                                     15780000
             DECLARE CURSOR1 CURSOR FOR                                 15790000
               SELECT NUM_RGSTRN,                                       15800000
                      NUM_GRANT_TYPE,                                   15810000
                      NUM_IDNTY_NO,                                     15820000
                      NUM_IDNTY_NO_TYPE,                                15830000
                      NUM_CSTMR_SOURCE,                                 15840000
                      NUM_CPF_ACNT,                                     15850000
                      AMT_GRANT,                                        15860000
                      AMT_GRANT_USE,                                    15870000
                      NUM_ROLE_TAG,                                     15880000
                      NUM_RLTNSP,                                       15890000
                      NME_PERSON,                                       15900000
                      NUM_GENDER,                                       15910000
                      NUM_CTZNSP,                                       15920000
                      NUM_MRTL_STATUS,                                  15930000
                      AMT_INCOME,                                       15940000
                      NUM_1YR_CNTNS_EMPLY,                              15950000
                      NUM_DEBAR,                                        15960000
                      NUM_DEBAR_REASON,                                 15970000
                      NUM_RCVRY,                                        15980000
                      NUM_UPDATE_USERID,                                15990000
                      DTE_UPDATE,                                       16000000
                      TME_UPDATE,                                       16010000
                      AMT_GRANT_THRSHD,                                 16020000
                      AMT_GRANT_EXCESS                                  16030000
               FROM                                                     16040000
                 GRANT_ASMNT_PERSON                                     16050000
               WHERE                                                    16060000
                 NUM_RGSTRN = :DCLGRANT-ASMNT-PERSON.NUM-RGSTRN AND     16070000
                 NUM_GRANT_TYPE = :DCLGRANT-ASMNT-PERSON.NUM-GRANT-TYPE 16080000
                                                                AND     16090000
                 NUM_IDNTY_NO   = :DCLGRANT-ASMNT-PERSON.NUM-IDNTY-NO   16100000
               ORDER BY NUM_IDNTY_NO                                    16110000
           END-EXEC.                                                    16120000
                                                                        16130000
      *----------------------------------------*                        16140000
      *  OPEN CURSOR1 - GRANT ASSESSMENT PERSON *                       16150000
      *----------------------------------------*                        16160000
           EXEC SQL                                                     16170000
             OPEN CURSOR1                                               16180000
           END-EXEC.                                                    16190000
                                                                        16200000
           EVALUATE SQLCODE                                             16210000
           WHEN 0                                                       16220000
             CONTINUE                                                   16230000
           WHEN OTHER                                                   16240000
             MOVE SQLCODE            TO WS-SQLCODE                      16250000
             DISPLAY 'OPEN CURSOR1 ERROR'                               16260000
             GO TO 9999-SQL-ERROR                                       16270000
           END-EVALUATE.                                                16280000
                                                                        16290000
       5100-EXIT.                                                       16300000
            EXIT.                                                       16310000
                                                                        16320000
      *================================================================*16330000
       5200-FETCH-CURSOR1.                                              16340000
      *================================================================*16350000
                                                                        16360000
           EXEC SQL                                                     16370000
             FETCH CURSOR1                                              16380000
               INTO                                                     16390000
                 :DCLGRANT-ASMNT-PERSON.NUM-RGSTRN,                     16400000
                 :DCLGRANT-ASMNT-PERSON.NUM-GRANT-TYPE,                 16410000
                 :DCLGRANT-ASMNT-PERSON.NUM-IDNTY-NO,                   16420000
                 :DCLGRANT-ASMNT-PERSON.NUM-IDNTY-NO-TYPE,              16430000
                 :DCLGRANT-ASMNT-PERSON.NUM-CSTMR-SOURCE,               16440000
                 :DCLGRANT-ASMNT-PERSON.NUM-CPF-ACNT,                   16450000
                 :DCLGRANT-ASMNT-PERSON.AMT-GRANT,                      16460000
                 :DCLGRANT-ASMNT-PERSON.AMT-GRANT-USE,                  16470000
                 :DCLGRANT-ASMNT-PERSON.NUM-ROLE-TAG,                   16480000
                 :DCLGRANT-ASMNT-PERSON.NUM-RLTNSP,                     16490000
                 :DCLGRANT-ASMNT-PERSON.NME-PERSON,                     16500000
                 :DCLGRANT-ASMNT-PERSON.NUM-GENDER,                     16510000
                 :DCLGRANT-ASMNT-PERSON.NUM-CTZNSP,                     16520000
                 :DCLGRANT-ASMNT-PERSON.NUM-MRTL-STATUS,                16530000
                 :DCLGRANT-ASMNT-PERSON.AMT-INCOME,                     16540000
                 :DCLGRANT-ASMNT-PERSON.NUM-1YR-CNTNS-EMPLY,            16550000
                 :DCLGRANT-ASMNT-PERSON.NUM-DEBAR,                      16560000
                 :DCLGRANT-ASMNT-PERSON.NUM-DEBAR-REASON,               16570000
                 :DCLGRANT-ASMNT-PERSON.NUM-RCVRY,                      16580000
                 :DCLGRANT-ASMNT-PERSON.NUM-UPDATE-USERID,              16590000
                 :DCLGRANT-ASMNT-PERSON.DTE-UPDATE,                     16600000
                 :DCLGRANT-ASMNT-PERSON.TME-UPDATE,                     16610000
                 :DCLGRANT-ASMNT-PERSON.AMT-GRANT-THRSHD,               16620000
                 :DCLGRANT-ASMNT-PERSON.AMT-GRANT-EXCESS                16630000
           END-EXEC.                                                    16640000
                                                                        16650000
           EVALUATE SQLCODE                                             16660000
           WHEN WS-SQL-OK                                               16670000
             PERFORM 5400-INSERT-TO-EX-PERSON THRU 5400-EXIT            16680000
             IF AMT-GRANT OF DCLGRANT-ASMNT-PERSON NOT = ZEROES         16690000
                PERFORM 5500-INSERT-TO-RECOVERY  THRU 5500-EXIT         16700000
             END-IF                                                     16710000
             PERFORM 5600-DELETE-GRANT-PERSON THRU 5600-EXIT            16720000
                                                                        16730000
             MOVE 'Y'  TO WS-GRNT-PRSN-FND                              16740000
                                                                        16750000
           WHEN WS-SQL-NFND                                             16760000
             IF WS-GRNT-PRSN-FND = 'N'                                  16770000
                PERFORM 5700-EX-PROCESS-PERSON      THRU 5700-EXIT      16780000
             END-IF                                                     16790000
             MOVE 'Y'                  TO WS-CURSOR1-EOF                16800000
           WHEN OTHER                                                   16810000
             MOVE SQLCODE            TO WS-SQLCODE                      16820000
             DISPLAY 'FETCH CURSOR1 ERROR'                              16830000
             GO TO 9999-SQL-ERROR                                       16840000
           END-EVALUATE.                                                16850000
                                                                        16860000
       5200-EXIT.                                                       16870000
            EXIT.                                                       16880000
                                                                        16890000
      *---------------------------------------------------------------* 16900000
       5300-CLOSE-CURSOR1.                                              16910000
      *---------------------------------------------------------------* 16920000
           INITIALIZE SQLCODE.                                          16930000
                                                                        16940000
      *-----------------------------------*                             16950000
      * CLOSE CURSOR1 - GRANT ASSESSMENT PERSON                         16960000
      *-----------------------------------*                             16970000
           EXEC SQL CLOSE CURSOR1                                       16980000
           END-EXEC.                                                    16990000
                                                                        17000000
           EVALUATE SQLCODE                                             17010000
              WHEN 0                                                    17020000
                   CONTINUE                                             17030000
              WHEN OTHER                                                17040000
                MOVE SQLCODE TO WS-SQLCODE                              17050000
                DISPLAY 'CLOSE CURSOR1 ERROR'                           17060000
                GO TO 9999-SQL-ERROR                                    17070000
           END-EVALUATE.                                                17080000
                                                                        17090000
       5300-EXIT.                                                       17100000
           EXIT.                                                        17110000
                                                                        17120000
      *------------------------*                                        17130000
       5400-INSERT-TO-EX-PERSON.                                        17140000
      *------------------------*                                        17150000
           MOVE NUM-RGSTRN OF DCLGRANT-ASMNT-PERSON                     17160000
                TO NUM-RGSTRN OF DCLEX-GRANT-ASMNT-PERSON.              17170000
           MOVE NUM-GRANT-TYPE OF DCLGRANT-ASMNT-PERSON                 17180000
                TO NUM-GRANT-TYPE OF DCLEX-GRANT-ASMNT-PERSON.          17190000
           MOVE NUM-IDNTY-NO OF DCLGRANT-ASMNT-PERSON                   17200000
                TO NUM-IDNTY-NO OF DCLEX-GRANT-ASMNT-PERSON.            17210000
           MOVE NUM-IDNTY-NO-TYPE OF DCLGRANT-ASMNT-PERSON              17220000
                TO NUM-IDNTY-NO-TYPE OF DCLEX-GRANT-ASMNT-PERSON.       17230000
           MOVE NUM-CSTMR-SOURCE OF DCLGRANT-ASMNT-PERSON               17240000
                TO NUM-CSTMR-SOURCE OF DCLEX-GRANT-ASMNT-PERSON.        17250000
           MOVE NUM-CPF-ACNT OF DCLGRANT-ASMNT-PERSON                   17260000
                TO NUM-CPF-ACNT OF DCLEX-GRANT-ASMNT-PERSON.            17270000
           MOVE AMT-GRANT OF DCLGRANT-ASMNT-PERSON                      17280000
                TO AMT-GRANT OF DCLEX-GRANT-ASMNT-PERSON.               17290000
           MOVE AMT-GRANT-USE OF DCLGRANT-ASMNT-PERSON                  17300000
                TO AMT-GRANT-USE OF DCLEX-GRANT-ASMNT-PERSON.           17310000
           MOVE NUM-ROLE-TAG OF DCLGRANT-ASMNT-PERSON                   17320000
                TO NUM-ROLE-TAG OF DCLEX-GRANT-ASMNT-PERSON.            17330000
           MOVE NUM-RLTNSP OF DCLGRANT-ASMNT-PERSON                     17340000
                TO NUM-RLTNSP OF DCLEX-GRANT-ASMNT-PERSON.              17350000
           MOVE NME-PERSON OF DCLGRANT-ASMNT-PERSON                     17360000
                TO NME-PERSON OF DCLEX-GRANT-ASMNT-PERSON.              17370000
           MOVE NUM-GENDER OF DCLGRANT-ASMNT-PERSON                     17380000
                TO NUM-GENDER OF DCLEX-GRANT-ASMNT-PERSON.              17390000
           MOVE NUM-CTZNSP OF DCLGRANT-ASMNT-PERSON                     17400000
                TO NUM-CTZNSP OF DCLEX-GRANT-ASMNT-PERSON.              17410000
           MOVE NUM-MRTL-STATUS OF DCLGRANT-ASMNT-PERSON                17420000
                TO NUM-MRTL-STATUS OF DCLEX-GRANT-ASMNT-PERSON.         17430000
           MOVE AMT-INCOME OF DCLGRANT-ASMNT-PERSON                     17440000
                TO AMT-INCOME OF DCLEX-GRANT-ASMNT-PERSON.              17450000
           MOVE NUM-1YR-CNTNS-EMPLY OF DCLGRANT-ASMNT-PERSON            17460000
                TO NUM-1YR-CNTNS-EMPLY OF DCLEX-GRANT-ASMNT-PERSON.     17470000
           MOVE NUM-DEBAR OF DCLGRANT-ASMNT-PERSON                      17480000
                TO NUM-DEBAR OF DCLEX-GRANT-ASMNT-PERSON.               17490000
           MOVE NUM-DEBAR-REASON OF DCLGRANT-ASMNT-PERSON               17500000
                TO NUM-DEBAR-REASON OF DCLEX-GRANT-ASMNT-PERSON.        17510000
           MOVE NUM-RCVRY OF DCLGRANT-ASMNT-PERSON                      17520000
                TO NUM-RCVRY OF DCLEX-GRANT-ASMNT-PERSON.               17530000
           MOVE AMT-GRANT-THRSHD OF DCLGRANT-ASMNT-PERSON               17540000
                TO AMT-GRANT-THRSHD OF DCLEX-GRANT-ASMNT-PERSON.        17550000
           MOVE AMT-GRANT-EXCESS OF DCLGRANT-ASMNT-PERSON               17560000
                TO AMT-GRANT-EXCESS OF DCLEX-GRANT-ASMNT-PERSON.        17570000
                                                                        17580000
           MOVE 'BP13CB0E '                                             17590000
                   TO NUM-UPDATE-USERID OF DCLEX-GRANT-ASMNT-PERSON.    17600000
                                                                        17610000
           STRING WS-DTE-CCYYMMDD(1:4) '-'                              17620000
                  WS-DTE-CCYYMMDD(5:2) '-'                              17630000
                  WS-DTE-CCYYMMDD(7:2)                                  17640000
                  DELIMITED BY SIZE                                     17650000
                  INTO DTE-UPDATE OF DCLEX-GRANT-ASMNT-PERSON.          17660000
                                                                        17670000
           STRING WS-TIME-HHMMSS(1:2) '.'                               17680000
                  WS-TIME-HHMMSS(3:2) '.'                               17690000
                  WS-TIME-HHMMSS(5:2)                                   17700000
                  DELIMITED BY SIZE                                     17710000
                  INTO TME-UPDATE OF DCLEX-GRANT-ASMNT-PERSON.          17720000
                                                                        17730000
            MOVE WS-TIMESTAMP TO TME-CREATE OF DCLEX-GRANT-ASMNT-PERSON.17740000
                                                                        17750000
            EXEC SQL                                                    17760000
                 INSERT INTO EX_GRANT_ASMNT_PERSON                      17770000
                        ( NUM_RGSTRN,                                   17780000
                          NUM_GRANT_TYPE,                               17790000
                          TME_CREATE,                                   17800000
                          NUM_IDNTY_NO,                                 17810000
                          NUM_IDNTY_NO_TYPE,                            17820000
                          NUM_CSTMR_SOURCE,                             17830000
                          NUM_CPF_ACNT,                                 17840000
                          AMT_GRANT,                                    17850000
                          AMT_GRANT_USE,                                17860000
                          NUM_ROLE_TAG,                                 17870000
                          NUM_RLTNSP,                                   17880000
                          NME_PERSON,                                   17890000
                          NUM_GENDER,                                   17900000
                          NUM_CTZNSP,                                   17910000
                          NUM_MRTL_STATUS,                              17920000
                          AMT_INCOME,                                   17930000
                          NUM_1YR_CNTNS_EMPLY,                          17940000
                          NUM_DEBAR,                                    17950000
                          NUM_DEBAR_REASON,                             17960000
                          NUM_RCVRY,                                    17970000
                          NUM_UPDATE_USERID,                            17980000
                          DTE_UPDATE,                                   17990000
                          TME_UPDATE,                                   18000000
                          AMT_GRANT_THRSHD,                             18010000
                          AMT_GRANT_EXCESS )                            18020000
                 VALUES ( :DCLEX-GRANT-ASMNT-PERSON.NUM-RGSTRN,         18030000
                          :DCLEX-GRANT-ASMNT-PERSON.NUM-GRANT-TYPE,     18040000
                          :DCLEX-GRANT-ASMNT-PERSON.TME-CREATE,         18050000
                          :DCLEX-GRANT-ASMNT-PERSON.NUM-IDNTY-NO,       18060000
                          :DCLEX-GRANT-ASMNT-PERSON.NUM-IDNTY-NO-TYPE,  18070000
                          :DCLEX-GRANT-ASMNT-PERSON.NUM-CSTMR-SOURCE,   18080000
                          :DCLEX-GRANT-ASMNT-PERSON.NUM-CPF-ACNT,       18090000
                          :DCLEX-GRANT-ASMNT-PERSON.AMT-GRANT,          18100000
                          :DCLEX-GRANT-ASMNT-PERSON.AMT-GRANT-USE,      18110000
                          :DCLEX-GRANT-ASMNT-PERSON.NUM-ROLE-TAG,       18120000
                          :DCLEX-GRANT-ASMNT-PERSON.NUM-RLTNSP,         18130000
                          :DCLEX-GRANT-ASMNT-PERSON.NME-PERSON,         18140000
                          :DCLEX-GRANT-ASMNT-PERSON.NUM-GENDER,         18150000
                          :DCLEX-GRANT-ASMNT-PERSON.NUM-CTZNSP,         18160000
                          :DCLEX-GRANT-ASMNT-PERSON.NUM-MRTL-STATUS,    18170000
                          :DCLEX-GRANT-ASMNT-PERSON.AMT-INCOME,         18180000
                          :DCLEX-GRANT-ASMNT-PERSON.NUM-1YR-CNTNS-EMPLY,18190000
                          :DCLEX-GRANT-ASMNT-PERSON.NUM-DEBAR,          18200000
                          :DCLEX-GRANT-ASMNT-PERSON.NUM-DEBAR-REASON,   18210000
                          :DCLEX-GRANT-ASMNT-PERSON.NUM-RCVRY,          18220000
                          :DCLEX-GRANT-ASMNT-PERSON.NUM-UPDATE-USERID,  18230000
                          :DCLEX-GRANT-ASMNT-PERSON.DTE-UPDATE,         18240000
                          :DCLEX-GRANT-ASMNT-PERSON.TME-UPDATE,         18250000
                          :DCLEX-GRANT-ASMNT-PERSON.AMT-GRANT-THRSHD,   18260000
                          :DCLEX-GRANT-ASMNT-PERSON.AMT-GRANT-EXCESS )  18270000
            END-EXEC.                                                   18280000
                                                                        18290000
            EVALUATE SQLCODE                                            18300000
               WHEN 000                                                 18310000
                    ADD 1                 TO WS-EX-PRSN-INS             18320000
                    MOVE 'EX_GRANT_ASMNT_PERSON'  TO WS-SOURCE          18330000
                    MOVE 'C'            TO WS-ACTION                    18340000
                    MOVE NUM-RGSTRN OF DCLEX-GRANT-ASMNT-PERSON         18350000
                                                  TO WS-RGSTRN          18360000
                    MOVE NUM-GRANT-TYPE OF DCLEX-GRANT-ASMNT-PERSON     18370000
                                                  TO WS-GRNT-TYPE       18380000
                    MOVE SPACES                   TO WS-BEFORE-IMAGE    18390000
                    MOVE DCLEX-GRANT-ASMNT-PERSON TO WS-AFTER-IMAGE     18400000
                    PERFORM 6400-INSERT-TO-LOG     THRU 6400-EXIT       18410000
                                                                        18420000
               WHEN -803                                                18430000
                    DISPLAY 'RECORD EXIST IN EX_GRANT_ASMNT_PERSON'     18440000
                    DISPLAY NUM-RGSTRN OF DCLEX-GRANT-ASMNT-PERSON ' '  18450000
                         NUM-GRANT-TYPE OF DCLEX-GRANT-ASMNT-PERSON ' ' 18460000
                         NUM-IDNTY-NO OF DCLEX-GRANT-ASMNT-PERSON       18470000
                                                                        18480000
               WHEN OTHER                                               18490000
                    MOVE SQLCODE          TO WS-SQLCODE                 18500000
                    DISPLAY 'SQL ERR (INSERT) IN EX_GRANT_ASMNT_PERSON' 18510000
                    DISPLAY 'SQL ERROR CODE  : '  WS-SQLCODE            18520000
                    PERFORM 9999-SQL-ERROR      THRU 9999-EXIT          18530000
            END-EVALUATE.                                               18540000
                                                                        18550000
       5400-EXIT.                                                       18560000
           EXIT.                                                        18570000
                                                                        18580000
      *------------------------*                                        18590000
       5500-INSERT-TO-RECOVERY.                                         18600000
      *------------------------*                                        18610000
           MOVE NUM-RGSTRN OF DCLGRANT-ASMNT-PERSON                     18620000
                             TO NUM-RGSTRN OF DCLGRANT-RCVRY.           18630000
           MOVE NUM-GRANT-TYPE OF DCLGRANT-ASMNT-PERSON                 18640000
                             TO NUM-GRANT-TYPE OF DCLGRANT-RCVRY.       18650000
           MOVE NUM-IDNTY-NO OF DCLGRANT-ASMNT-PERSON                   18660000
                             TO NUM-IDNTY-NO OF DCLGRANT-RCVRY.         18670000
           MOVE NUM-IDNTY-NO-TYPE OF DCLGRANT-ASMNT-PERSON              18680000
                             TO NUM-IDNTY-NO-TYPE OF DCLGRANT-RCVRY.    18690000
                                                                        18700000
           DISPLAY 'F526-AMT-TXN1 : 'F526-AMT-TXN                       18710000
           IF F526-NUM-UIN = NUM-IDNTY-NO OF DCLGRANT-ASMNT-PERSON      18720000
              IF (F526-NUM-RCPTCD = '6019Z' OR '6013I' OR               18730000
                                    '6015B' OR '6020H' OR '6011E' OR    18740000
                                    '6014J' OR '6021Z') AND             18750000
                 (F526-NUM-CRDR = 'C')                                  18760000
           DISPLAY 'F526-AMT-TXN2C : 'F526-AMT-TXN                      18770000
                 MOVE F526-AMT-TXN TO AMT-GRANT-RCVR OF DCLGRANT-RCVRY  18780000
              ELSE                                                      18790000
                IF (F526-NUM-RCPTCD = 'AHG  ' OR 'SHG  ' OR 'SRG  ' OR  18800000
                                      'SUG  ' OR 'EHG  ' OR 'HGT  ' OR  18810000
                                      'REG  ' OR 'PHG  ' OR 'TUG  ' OR  18820000
                                      'ATG  ' OR 'FSG  ') AND           18830000
                   (F526-NUM-CRDR = 'D')                                18840000
           DISPLAY 'F526-AMT-TXN3D : 'F526-AMT-TXN                      18850000
                   MOVE F526-AMT-TXN TO AMT-GRANT-RCVR OF DCLGRANT-RCVRY18860000
                ELSE                                                    18870000
           DISPLAY 'F526-AMT-TXN4  : 'F526-AMT-TXN                      18880000
                   MOVE ZEROES TO AMT-GRANT-RCVR OF DCLGRANT-RCVRY      18890000
                END-IF                                                  18900000
              END-IF                                                    18910000
              IF (F526-NUM-RCPTCD = '0520C' OR '6023C' OR '6254D') AND  18920000
                 (F526-NUM-CRDR = 'C')                                  18930000
           DISPLAY 'F526-AMT-TXN5C : 'F526-AMT-TXN                      18940000
                 MOVE F526-AMT-TXN TO AMT-GRANT-RCVR-INTRST             18950000
                                                      OF DCLGRANT-RCVRY 18960000
              ELSE                                                      18970000
                IF (F526-NUM-RCPTCD = 'INT  ' AND F526-NUM-CRDR = 'D')  18980000
                   MOVE F526-AMT-TXN TO AMT-GRANT-RCVR-INTRST           18990000
                                                      OF DCLGRANT-RCVRY 19000000
           DISPLAY 'F526-AMT-TXN6D : 'F526-AMT-TXN                      19010000
                ELSE                                                    19020000
           DISPLAY 'F526-AMT-TXN7  : 'F526-AMT-TXN                      19030000
                   MOVE ZEROES TO AMT-GRANT-RCVR-INTRST                 19040000
                                                      OF DCLGRANT-RCVRY 19050000
                END-IF                                                  19060000
              END-IF                                                    19070000
           ELSE                                                         19080000
              MOVE ZEROES TO AMT-GRANT-RCVR        OF DCLGRANT-RCVRY    19090000
                             AMT-GRANT-RCVR-INTRST OF DCLGRANT-RCVRY    19100000
           END-IF.                                                      19110000
                                                                        19120000
           IF F526-DTE-TXN = SPACES OR LOW-VALUES                       19130000
              MOVE '0001-01-01' TO DTE-RCV-RCVR-GRANT OF DCLGRANT-RCVRY 19140000
           ELSE                                                         19150000
              STRING F526-DTE-TXN(1:4) '-'                              19160000
                     F526-DTE-TXN(5:2) '-'                              19170000
                     F526-DTE-TXN(7:2) DELIMITED BY SIZE                19180000
                     INTO DTE-RCV-RCVR-GRANT OF DCLGRANT-RCVRY          19190000
           END-IF.                                                      19200000
                                                                        19210000
           MOVE WS-TIMESTAMP TO TME-CREATE OF DCLGRANT-RCVRY            19220000
                                WS-TIME-CREATE                          19230000
                                                                        19240000
           MOVE 'BP13CB0E '                                             19250000
                   TO NUM-UPDATE-USERID OF DCLGRANT-RCVRY.              19260000
                                                                        19270000
           STRING WS-DTE-CCYYMMDD(1:4) '-'                              19280000
                  WS-DTE-CCYYMMDD(5:2) '-'                              19290000
                  WS-DTE-CCYYMMDD(7:2)                                  19300000
                  DELIMITED BY SIZE                                     19310000
                  INTO DTE-UPDATE OF DCLGRANT-RCVRY.                    19320000
                                                                        19330000
           STRING WS-TIME-HHMMSS(1:2) '.'                               19340000
                  WS-TIME-HHMMSS(3:2) '.'                               19350000
                  WS-TIME-HHMMSS(5:2)                                   19360000
                  DELIMITED BY SIZE                                     19370000
                  INTO TME-UPDATE OF DCLGRANT-RCVRY.                    19380000
                                                                        19390000
            MOVE ZEROES TO AMT-GRANT-RCVR-INTRST OF DCLGRANT-RCVRY.     19400000
            MOVE '0001-01-01' TO DTE-TRNSFR OF DCLGRANT-RCVRY           19410000
                              DTE-RQST-RCVR OF DCLGRANT-RCVRY           19420000
                              DTE-ACK-GRANT-RCVRY-CPF OF DCLGRANT-RCVRY 19430000
                              DTE-GRANT-ABORT OF DCLGRANT-RCVRY         19440000
                              DTE-GRANT-RPDT OF DCLGRANT-RCVRY          19450000
                              DTE-CPF-GRANT-RQST OF DCLGRANT-RCVRY      19460000
                              DTE-CPF-GRANT-RCV OF DCLGRANT-RCVRY       19470000
                              DTE-UNUSED-GRANT-RQST OF DCLGRANT-RCVRY   19480000
                              DTE-UNUSED-GRANT-RCV OF DCLGRANT-RCVRY    19490000
                              DTE-USED-GRANT-RQST OF DCLGRANT-RCVRY     19500000
                              DTE-USED-GRANT-RCV OF DCLGRANT-RCVRY.     19510000
                                                                        19520000
            MOVE SPACES    TO NUM-TRNSFR OF DCLGRANT-RCVRY              19530000
                              NUM-ACNT OF DCLGRANT-RCVRY                19540000
                              NUM-SCHEME OF DCLGRANT-RCVRY              19550000
                              NUM-TNCY OF DCLGRANT-RCVRY.               19560000
                                                                        19570000
            EXEC SQL                                                    19580000
                 INSERT INTO GRANT_RCVRY                                19590000
                        ( NUM_RGSTRN,                                   19600000
                          NUM_GRANT_TYPE,                               19610000
                          TME_CREATE,                                   19620000
                          NUM_IDNTY_NO,                                 19630000
                          NUM_IDNTY_NO_TYPE,                            19640000
                          AMT_GRANT_RCVR,                               19650000
                          AMT_GRANT_RCVR_INTRST,                        19660000
                          DTE_RQST_RCVR,                                19670000
                          DTE_RCV_RCVR_GRANT,                           19680000
                          DTE_ACK_GRANT_RCVRY_CPF,                      19690000
                          DTE_GRANT_ABORT,                              19700000
                          DTE_GRANT_RPDT,                               19710000
                          NUM_TRNSFR,                                   19720000
                          DTE_TRNSFR,                                   19730000
                          NUM_ACNT,                                     19740000
                          NUM_SCHEME,                                   19750000
                          NUM_TNCY,                                     19760000
                          DTE_CPF_GRANT_RQST,                           19770000
                          DTE_CPF_GRANT_RCV,                            19780000
                          DTE_UNUSED_GRANT_RQST,                        19790000
                          DTE_UNUSED_GRANT_RCV,                         19800000
                          DTE_USED_GRANT_RQST,                          19810000
                          DTE_USED_GRANT_RCV,                           19820000
                          NUM_UPDATE_USERID,                            19830000
                          DTE_UPDATE,                                   19840000
                          TME_UPDATE )                                  19850000
                 VALUES ( :DCLGRANT-RCVRY.NUM-RGSTRN,                   19860000
                          :DCLGRANT-RCVRY.NUM-GRANT-TYPE,               19870000
                          :DCLGRANT-RCVRY.TME-CREATE,                   19880000
                          :DCLGRANT-RCVRY.NUM-IDNTY-NO,                 19890000
                          :DCLGRANT-RCVRY.NUM-IDNTY-NO-TYPE,            19900000
                          :DCLGRANT-RCVRY.AMT-GRANT-RCVR,               19910000
                          :DCLGRANT-RCVRY.AMT-GRANT-RCVR-INTRST,        19920000
                          :DCLGRANT-RCVRY.DTE-RQST-RCVR,                19930000
                          :DCLGRANT-RCVRY.DTE-RCV-RCVR-GRANT,           19940000
                          :DCLGRANT-RCVRY.DTE-ACK-GRANT-RCVRY-CPF,      19950000
                          :DCLGRANT-RCVRY.DTE-GRANT-ABORT,              19960000
                          :DCLGRANT-RCVRY.DTE-GRANT-RPDT,               19970000
                          :DCLGRANT-RCVRY.NUM-TRNSFR,                   19980000
                          :DCLGRANT-RCVRY.DTE-TRNSFR,                   19990000
                          :DCLGRANT-RCVRY.NUM-ACNT,                     20000000
                          :DCLGRANT-RCVRY.NUM-SCHEME,                   20010000
                          :DCLGRANT-RCVRY.NUM-TNCY,                     20020000
                          :DCLGRANT-RCVRY.DTE-CPF-GRANT-RQST,           20030000
                          :DCLGRANT-RCVRY.DTE-CPF-GRANT-RCV,            20040000
                          :DCLGRANT-RCVRY.DTE-UNUSED-GRANT-RQST,        20050000
                          :DCLGRANT-RCVRY.DTE-UNUSED-GRANT-RCV,         20060000
                          :DCLGRANT-RCVRY.DTE-USED-GRANT-RQST,          20070000
                          :DCLGRANT-RCVRY.DTE-USED-GRANT-RCV,           20080000
                          :DCLGRANT-RCVRY.NUM-UPDATE-USERID,            20090000
                          :DCLGRANT-RCVRY.DTE-UPDATE,                   20100000
                          :DCLGRANT-RCVRY.TME-UPDATE )                  20110000
            END-EXEC.                                                   20120000
                                                                        20130000
            EVALUATE SQLCODE                                            20140000
               WHEN 000                                                 20150000
                    ADD 1                 TO WS-RCVRY-INS               20160000
                    MOVE 'GRANT_RCVRY'  TO WS-SOURCE                    20170000
                    MOVE 'C'            TO WS-ACTION                    20180000
                    MOVE NUM-RGSTRN OF DCLGRANT-RCVRY TO WS-RGSTRN      20190000
                    MOVE NUM-GRANT-TYPE OF DCLGRANT-RCVRY               20200000
                                                  TO WS-GRNT-TYPE       20210000
                    MOVE SPACES         TO WS-BEFORE-IMAGE              20220000
                    MOVE DCLGRANT-RCVRY TO WS-AFTER-IMAGE               20230000
                    PERFORM 6400-INSERT-TO-LOG     THRU 6400-EXIT       20240000
                                                                        20250000
               WHEN -803                                                20260000
                    DISPLAY 'RECORD EXIST IN GRANT_RCVRY'               20270000
                    DISPLAY NUM-RGSTRN OF DCLGRANT-RCVRY ' '            20280000
                            NUM-GRANT-TYPE OF DCLGRANT-RCVRY ' '        20290000
                            NUM-IDNTY-NO OF DCLGRANT-RCVRY              20300000
                                                                        20310000
               WHEN OTHER                                               20320000
                    MOVE SQLCODE          TO WS-SQLCODE                 20330000
                    DISPLAY 'SQL ERR (INSERT) IN GRANT_RCVRY'           20340000
                    DISPLAY 'SQL ERROR CODE  : '  WS-SQLCODE            20350000
                    PERFORM 9999-SQL-ERROR      THRU 9999-EXIT          20360000
            END-EVALUATE.                                               20370000
                                                                        20380000
       5500-EXIT.                                                       20390000
           EXIT.                                                        20400000
                                                                        20410000
      *------------------------*                                        20420000
       5600-DELETE-GRANT-PERSON.                                        20430000
      *------------------------*                                        20440000
           MOVE DCLGRANT-ASMNT-PERSON               TO WS-BEFORE-IMAGE. 20450000
           MOVE NUM-RGSTRN OF DCLGRANT-ASMNT-PERSON     TO WS-RGSTRN.   20460000
           MOVE NUM-GRANT-TYPE OF DCLGRANT-ASMNT-PERSON TO WS-GRNT-TYPE 20470000
                                                                        20480000
            EXEC SQL                                                    20490000
              DELETE FROM GRANT_ASMNT_PERSON                            20500000
              WHERE NUM_RGSTRN   = :DCLGRANT-ASMNT-PERSON.NUM-RGSTRN    20510000
              AND NUM_GRANT_TYPE = :DCLGRANT-ASMNT-PERSON.NUM-GRANT-TYPE20520000
              AND NUM_IDNTY_NO   = :DCLGRANT-ASMNT-PERSON.NUM-IDNTY-NO  20530000
            END-EXEC.                                                   20540000
                                                                        20550000
            EVALUATE SQLCODE                                            20560000
               WHEN 000                                                 20570000
                    ADD 1                 TO WS-PERSON-DELETE           20580000
                    MOVE 'GRANT_ASMNT_PERSON'  TO WS-SOURCE             20590000
                    MOVE 'D'                   TO WS-ACTION             20600000
                    MOVE SPACES                TO WS-AFTER-IMAGE        20610000
                    PERFORM 6400-INSERT-TO-LOG THRU 6400-EXIT           20620000
                                                                        20630000
               WHEN 100                                                 20640000
                    DISPLAY 'RECORD NOT EXIST IN GRANT_ASMNT_PERSON'    20650000
                    DISPLAY NUM-RGSTRN OF DCLGRANT-ASMNT-PERSON ' '     20660000
                            NUM-GRANT-TYPE OF DCLGRANT-ASMNT-PERSON     20670000
                                                                        20680000
               WHEN OTHER                                               20690000
                    MOVE SQLCODE          TO WS-SQLCODE                 20700000
                    DISPLAY 'SQL ERROR (DELETE) IN GRANT_ASMNT_PERSON'  20710000
                    DISPLAY 'SQL ERROR CODE  : '  WS-SQLCODE            20720000
            END-EVALUATE.                                               20730000
                                                                        20740000
       5600-EXIT.                                                       20750000
           EXIT.                                                        20760000
                                                                        20770000
      *================================================================*20780000
       5700-EX-PROCESS-PERSON.                                          20790000
      *================================================================*20800000
           MOVE  'N'       TO  WS-GRNT-PRSN-FND.                        20810000
                                                                        20820000
           PERFORM 5720-OPEN-CURSOR5    THRU 5720-EXIT.                 20830000
           PERFORM 5740-FETCH-CURSOR5   THRU 5740-EXIT                  20840000
                   UNTIL WS-CURSOR5-EOF = 'Y'.                          20850000
           PERFORM 5760-CLOSE-CURSOR5   THRU 5760-EXIT.                 20860000
                                                                        20870000
       5700-EXIT.                                                       20880000
           EXIT.                                                        20890000
                                                                        20900000
      *================================================================*20910000
       5720-OPEN-CURSOR5.                                               20920000
      *================================================================*20930000
           INITIALIZE SQLCODE.                                          20940000
           MOVE 'N'                   TO WS-CURSOR5-EOF.                20950000
                                                                        20960000
           MOVE F526-NUM-REF TO NUM-RGSTRN OF DCLEX-GRANT-ASMNT-PERSON. 20970000
           EVALUATE F526-NUM-RCPTCD                                     20980000
            WHEN '6019Z'                                                20990000
            WHEN '6013I'                                                21000000
              MOVE 'SRG   '  TO NUM-GRANT-TYPE OF                       21010000
                                              DCLEX-GRANT-ASMNT-PERSON  21020000
            WHEN '6015B'                                                21030000
              MOVE 'SUG   '  TO NUM-GRANT-TYPE OF                       21040000
                                              DCLEX-GRANT-ASMNT-PERSON  21050000
            WHEN '6020H'                                                21060000
            WHEN '6014J'                                                21070000
              MOVE 'AHG   '  TO NUM-GRANT-TYPE OF                       21080000
                                              DCLEX-GRANT-ASMNT-PERSON  21090000
            WHEN '6011E'                                                21100000
            WHEN '6021Z'                                                21110000
              MOVE 'SHG   '  TO NUM-GRANT-TYPE OF                       21120000
                                              DCLEX-GRANT-ASMNT-PERSON  21130000
            WHEN 'AHG  '                                                21140000
            WHEN 'SHG  '                                                21150000
            WHEN 'SRG  '                                                21160000
            WHEN 'SUG  '                                                21170000
            WHEN 'EHG  '                                                21180000
            WHEN 'HGT  '                                                21190000
            WHEN 'REG  '                                                21200000
            WHEN 'PHG  '                                                21210000
            WHEN 'TUG  '                                                21220000
            WHEN 'ATG  '                                                21230000
            WHEN 'FSG  '                                                21240000
              MOVE F526-NUM-RCPTCD                                      21250000
                             TO NUM-GRANT-TYPE OF                       21260000
                                              DCLEX-GRANT-ASMNT-PERSON  21270000
           END-EVALUATE.                                                21280000
                                                                        21290000
           MOVE F526-NUM-UIN                                            21300000
                            TO NUM-IDNTY-NO OF DCLEX-GRANT-ASMNT-PERSON.21310000
                                                                        21320000
      *------------------------------------------*                      21330000
      *  DECLARE CURSOR5 - GRANT ASSESSMENT PERSON                      21340000
      *------------------------------------------*                      21350000
           EXEC SQL                                                     21360000
             DECLARE CURSOR5 CURSOR FOR                                 21370000
               SELECT NUM_RGSTRN,                                       21380000
                      NUM_GRANT_TYPE,                                   21390000
                      TME_CREATE,                                       21400000
                      NUM_IDNTY_NO,                                     21410000
                      NUM_IDNTY_NO_TYPE,                                21420000
                      NUM_CSTMR_SOURCE,                                 21430000
                      NUM_CPF_ACNT,                                     21440000
                      AMT_GRANT,                                        21450000
                      AMT_GRANT_USE,                                    21460000
                      NUM_ROLE_TAG,                                     21470000
                      NUM_RLTNSP,                                       21480000
                      NME_PERSON,                                       21490000
                      NUM_GENDER,                                       21500000
                      NUM_CTZNSP,                                       21510000
                      NUM_MRTL_STATUS,                                  21520000
                      AMT_INCOME,                                       21530000
                      NUM_1YR_CNTNS_EMPLY,                              21540000
                      NUM_DEBAR,                                        21550000
                      NUM_DEBAR_REASON,                                 21560000
                      NUM_RCVRY,                                        21570000
                      NUM_UPDATE_USERID,                                21580000
                      DTE_UPDATE,                                       21590000
                      TME_UPDATE,                                       21600000
                      AMT_GRANT_THRSHD,                                 21610000
                      AMT_GRANT_EXCESS                                  21620000
               FROM                                                     21630000
                 EX_GRANT_ASMNT_PERSON                                  21640000
               WHERE                                                    21650000
                 NUM_RGSTRN = :DCLEX-GRANT-ASMNT-PERSON.NUM-RGSTRN AND  21660000
                 NUM_GRANT_TYPE =                                       21670000
                              :DCLEX-GRANT-ASMNT-PERSON.NUM-GRANT-TYPE  21680000
                                                                AND     21690000
                 NUM_IDNTY_NO = :DCLEX-GRANT-ASMNT-PERSON.NUM-IDNTY-NO  21700000
               ORDER BY NUM_IDNTY_NO                                    21710000
           END-EXEC.                                                    21720000
                                                                        21730000
      *----------------------------------------*                        21740000
      *  OPEN CURSOR5 - GRANT ASSESSMENT PERSON *                       21750000
      *----------------------------------------*                        21760000
           EXEC SQL                                                     21770000
             OPEN CURSOR5                                               21780000
           END-EXEC.                                                    21790000
                                                                        21800000
           EVALUATE SQLCODE                                             21810000
           WHEN 0                                                       21820000
             CONTINUE                                                   21830000
           WHEN OTHER                                                   21840000
             MOVE SQLCODE            TO WS-SQLCODE                      21850000
             DISPLAY 'OPEN CURSOR5 ERROR'                               21860000
             GO TO 9999-SQL-ERROR                                       21870000
           END-EVALUATE.                                                21880000
                                                                        21890000
       5720-EXIT.                                                       21900000
            EXIT.                                                       21910000
                                                                        21920000
      *================================================================*21930000
       5740-FETCH-CURSOR5.                                              21940000
      *================================================================*21950000
                                                                        21960000
           EXEC SQL                                                     21970000
             FETCH CURSOR5                                              21980000
               INTO                                                     21990000
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-RGSTRN,                  22000000
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-GRANT-TYPE,              22010000
                 :DCLEX-GRANT-ASMNT-PERSON.TME-CREATE,                  22020000
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-IDNTY-NO,                22030000
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-IDNTY-NO-TYPE,           22040000
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-CSTMR-SOURCE,            22050000
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-CPF-ACNT,                22060000
                 :DCLEX-GRANT-ASMNT-PERSON.AMT-GRANT,                   22070000
                 :DCLEX-GRANT-ASMNT-PERSON.AMT-GRANT-USE,               22080000
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-ROLE-TAG,                22090000
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-RLTNSP,                  22100000
                 :DCLEX-GRANT-ASMNT-PERSON.NME-PERSON,                  22110000
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-GENDER,                  22120000
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-CTZNSP,                  22130000
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-MRTL-STATUS,             22140000
                 :DCLEX-GRANT-ASMNT-PERSON.AMT-INCOME,                  22150000
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-1YR-CNTNS-EMPLY,         22160000
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-DEBAR,                   22170000
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-DEBAR-REASON,            22180000
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-RCVRY,                   22190000
                 :DCLEX-GRANT-ASMNT-PERSON.NUM-UPDATE-USERID,           22200000
                 :DCLEX-GRANT-ASMNT-PERSON.DTE-UPDATE,                  22210000
                 :DCLEX-GRANT-ASMNT-PERSON.TME-UPDATE,                  22220000
                 :DCLEX-GRANT-ASMNT-PERSON.AMT-GRANT-THRSHD,            22230000
                 :DCLEX-GRANT-ASMNT-PERSON.AMT-GRANT-EXCESS             22240000
           END-EXEC.                                                    22250000
                                                                        22260000
           EVALUATE SQLCODE                                             22270000
           WHEN WS-SQL-OK                                               22280000
             IF AMT-GRANT OF DCLEX-GRANT-ASMNT-PERSON NOT = ZEROES      22290000
                MOVE TME-CREATE OF DCLEX-GRANT-ASMNT-PERSON             22300000
                                                   TO WS-TIME-CREATE    22310000
                PERFORM 2500-READ-UPD-GRNT-RCVRY THRU 2500-EXIT         22320000
                MOVE 'Y'  TO WS-GRNT-PRSN-FND                           22330000
             END-IF                                                     22340000
                                                                        22350000
                                                                        22360000
           WHEN WS-SQL-NFND                                             22370000
             MOVE 'Y'                  TO WS-CURSOR5-EOF                22380000
           WHEN OTHER                                                   22390000
             MOVE SQLCODE            TO WS-SQLCODE                      22400000
             DISPLAY 'FETCH CURSOR5 ERROR'                              22410000
             GO TO 9999-SQL-ERROR                                       22420000
           END-EVALUATE.                                                22430000
                                                                        22440000
       5740-EXIT.                                                       22450000
            EXIT.                                                       22460000
                                                                        22470000
      *---------------------------------------------------------------* 22480000
       5760-CLOSE-CURSOR5.                                              22490000
      *---------------------------------------------------------------* 22500000
           INITIALIZE SQLCODE.                                          22510000
                                                                        22520000
      *-----------------------------------*                             22530000
      * CLOSE CURSOR5 - GRANT ASSESSMENT PERSON                         22540000
      *-----------------------------------*                             22550000
           EXEC SQL CLOSE CURSOR5                                       22560000
           END-EXEC.                                                    22570000
                                                                        22580000
           EVALUATE SQLCODE                                             22590000
              WHEN 0                                                    22600000
                   CONTINUE                                             22610000
              WHEN OTHER                                                22620000
                MOVE SQLCODE TO WS-SQLCODE                              22630000
                DISPLAY 'CLOSE CURSOR5 ERROR'                           22640000
                GO TO 9999-SQL-ERROR                                    22650000
           END-EVALUATE.                                                22660000
                                                                        22670000
       5760-EXIT.                                                       22680000
           EXIT.                                                        22690000
                                                                        22700000
      *---------------------*                                           22710000
       6000-PRINT-ERROR-RPT.                                            22720000
      *---------------------*                                           22730000
                                                                        22740000
           IF CNT-LINE-ON-PAGE > 46                                     22750000
              MOVE 1 TO CNT-LINE-ON-PAGE                                22760000
              PERFORM 6100-REPORT-HEADING THRU 6100-EXIT                22770000
           ELSE                                                         22780000
              ADD 1 TO CNT-LINE-ON-PAGE                                 22790000
           END-IF.                                                      22800000
                                                                        22810000
           IF CNT-LINE-ON-PAGE = 1                                      22820000
              PERFORM 6100-REPORT-HEADING THRU 6100-EXIT                22830000
           END-IF.                                                      22840000
                                                                        22850000
           ADD 1 TO CNT-PRT-CASES.                                      22860000
                                                                        22870000
           MOVE CNT-PRT-CASES         TO LB0E-SNO.                      22880000
                                                                        22890000
           MOVE F526-NUM-REF          TO LB0E-ORIG-REG-NO               22900000
                                                                        22910000
           MOVE F526-DTE-TXN-CC       TO LB0E-POST-YYYY(1:2).           22920000
           MOVE F526-DTE-TXN-YY       TO LB0E-POST-YYYY(3:2).           22930000
           MOVE F526-DTE-TXN-MM       TO LB0E-POST-MM.                  22940000
           MOVE F526-DTE-TXN-DD       TO LB0E-POST-DD.                  22950000
           MOVE F526-NUM-RCPT         TO LB0E-NUM-RECEIPT.              22960000
           MOVE F526-NUM-RCPT         TO LB0E-NUM-RECEIPT.              22970000
                                                                        22980000
           EVALUATE F526-NUM-RCPTCD                                     22990000
            WHEN '0520C'                                                23000000
            WHEN '6023C'                                                23010000
            WHEN '6254D'                                                23020000
              MOVE 'INT   '      TO LB0E-PAYMENT-TYPE                   23030000
            WHEN '6019Z'                                                23040000
            WHEN '6013I'                                                23050000
              MOVE 'SRG   '      TO LB0E-PAYMENT-TYPE                   23060000
            WHEN '6015B'                                                23070000
              MOVE 'SUG   '      TO LB0E-PAYMENT-TYPE                   23080000
            WHEN '6020H'                                                23090000
            WHEN '6014J'                                                23100000
              MOVE 'AHG   '      TO LB0E-PAYMENT-TYPE                   23110000
            WHEN '6011E'                                                23120000
            WHEN '6021Z'                                                23130000
              MOVE 'SHG   '      TO LB0E-PAYMENT-TYPE                   23140000
            WHEN 'AHG  '                                                23150000
            WHEN 'INT  '                                                23160000
            WHEN 'SHG  '                                                23170000
            WHEN 'SRG  '                                                23180000
            WHEN 'SUG  '                                                23190000
            WHEN 'EHG  '                                                23200000
            WHEN 'HGT  '                                                23210000
            WHEN 'REG  '                                                23220000
            WHEN 'PHG  '                                                23230000
            WHEN 'TUG  '                                                23240000
            WHEN 'ATG  '                                                23250000
            WHEN 'FSG  '                                                23260000
              MOVE F526-NUM-RCPTCD                                      23270000
                                 TO LB0E-PAYMENT-TYPE                   23280000
           END-EVALUATE.                                                23290000
                                                                        23300000
           MOVE F526-AMT-TXN          TO LB0E-AMT.                      23310000
           WRITE PRINT-REC          FROM LB0E-PR-DETAILS.               23320000
                                                                        23330000
       6000-EXIT.                                                       23340000
           EXIT.                                                        23350000
                                                                        23360000
      *--------------------*                                            23370000
       6100-REPORT-HEADING.                                             23380000
      *--------------------*                                            23390000
                                                                        23400000
            ADD 1           TO CNT-PAGE.                                23410000
            MOVE CNT-PAGE   TO LB0E-PAGE.                               23420000
            MOVE SPACES     TO PRINT-REC.                               23430000
            WRITE PRINT-REC AFTER PAGE.                                 23440000
            WRITE PRINT-REC FROM LB0E-PR-HEAD-01 AFTER 2.               23450000
            WRITE PRINT-REC FROM LB0E-PR-HEAD-02 AFTER 3.               23460000
            WRITE PRINT-REC FROM LB0E-PR-HEAD-03 AFTER 3.               23470000
            MOVE ALL '-'    TO PRINT-REC.                               23480000
            WRITE PRINT-REC.                                            23490000
            MOVE SPACES     TO PRINT-REC.                               23500000
            WRITE PRINT-REC AFTER 1.                                    23510000
                                                                        23520000
       6100-EXIT.                                                       23530000
           EXIT.                                                        23540000
                                                                        23550000
      *--------------------*                                            23560000
       6400-INSERT-TO-LOG.                                              23570000
      *--------------------*                                            23580000
            EXEC SQL                                                    23590000
                 SET :WS-TIMESTAMP-LOG = CURRENT TIMESTAMP              23600000
            END-EXEC.                                                   23610000
            IF SQLCODE NOT = 000                                        23620000
               MOVE SQLCODE          TO WS-SQLCODE                      23630000
               DISPLAY 'SQL ERROR SET DB2 TIMESTAMP'                    23640000
               DISPLAY 'SQL ERROR CODE  : '  WS-SQLCODE                 23650000
               PERFORM 9999-SQL-ERROR THRU 9999-EXIT                    23660000
            END-IF.                                                     23670000
                                                                        23680000
           MOVE WS-TIMESTAMP-LOG TO TME-UPDATE OF DCLGRANT-UPDATE-LOG.  23690000
           MOVE 'BP13CB0E'  TO NUM-UPDATE-USERID OF DCLGRANT-UPDATE-LOG.23700000
           MOVE WS-SOURCE   TO NUM-SOURCE-RECORD OF DCLGRANT-UPDATE-LOG.23710000
           MOVE WS-ACTION     TO NUM-ACTION OF DCLGRANT-UPDATE-LOG.     23720000
           MOVE WS-RGSTRN     TO NUM-RGSTRN OF DCLGRANT-UPDATE-LOG.     23730000
           MOVE WS-GRNT-TYPE TO NUM-GRANT-TYPE OF DCLGRANT-UPDATE-LOG.  23740000
           MOVE WS-BEFORE-IMAGE TO TXT-BEFORE-IMAGE-TEXT                23750000
                                              OF DCLGRANT-UPDATE-LOG.   23760000
           MOVE LENGTH OF WS-BEFORE-IMAGE TO TXT-BEFORE-IMAGE-LEN       23770000
                                              OF DCLGRANT-UPDATE-LOG.   23780000
           MOVE WS-AFTER-IMAGE TO TXT-AFTER-IMAGE-TEXT                  23790000
                                              OF DCLGRANT-UPDATE-LOG.   23800000
           MOVE LENGTH OF WS-AFTER-IMAGE TO TXT-AFTER-IMAGE-LEN         23810000
                                              OF DCLGRANT-UPDATE-LOG.   23820000
            EXEC SQL                                                    23830000
                 INSERT INTO GRANT_UPDATE_LOG                           23840000
                        ( TME_UPDATE,                                   23850000
                          NUM_UPDATE_USERID,                            23860000
                          NUM_SOURCE_RECORD,                            23870000
                          NUM_ACTION,                                   23880000
                          NUM_RGSTRN,                                   23890000
                          NUM_GRANT_TYPE,                               23900000
                          TXT_BEFORE_IMAGE,                             23910000
                          TXT_AFTER_IMAGE )                             23920000
                 VALUES ( :DCLGRANT-UPDATE-LOG.TME-UPDATE,              23930000
                          :DCLGRANT-UPDATE-LOG.NUM-UPDATE-USERID,       23940000
                          :DCLGRANT-UPDATE-LOG.NUM-SOURCE-RECORD,       23950000
                          :DCLGRANT-UPDATE-LOG.NUM-ACTION,              23960000
                          :DCLGRANT-UPDATE-LOG.NUM-RGSTRN,              23970000
                          :DCLGRANT-UPDATE-LOG.NUM-GRANT-TYPE,          23980000
                          :DCLGRANT-UPDATE-LOG.TXT-BEFORE-IMAGE,        23990000
                          :DCLGRANT-UPDATE-LOG.TXT-AFTER-IMAGE )        24000000
            END-EXEC.                                                   24010000
                                                                        24020000
            EVALUATE SQLCODE                                            24030000
               WHEN 000                                                 24040000
                    ADD 1                 TO WS-GRNT-UPD-LOG            24050000
                                                                        24060000
               WHEN -803                                                24070000
                    CONTINUE                                            24080000
                                                                        24090000
               WHEN OTHER                                               24100000
                    MOVE SQLCODE          TO WS-SQLCODE                 24110000
                    DISPLAY 'SQL ERROR (INSERT) IN GRANT_UPDATE_LOG'    24120000
                    DISPLAY 'SQL ERROR CODE  : '  WS-SQLCODE            24130000
                    PERFORM 9999-SQL-ERROR      THRU 9999-EXIT          24140000
            END-EVALUATE.                                               24150000
                                                                        24160000
       6400-EXIT.                                                       24170000
           EXIT.                                                        24180000
                                                                        24190000
      *----------------------*                                          24200000
       7000-UPD-EX-GRANT-ASMNT.                                         24210000
      *----------------------*                                          24220000
           MOVE DCLEX-GRANT-ASMNT TO WS-BEFORE-IMAGE.                   24230000
           MOVE F526-NUM-REF TO NUM-RGSTRN        OF DCLEX-GRANT-ASMNT. 24240000
           EVALUATE F526-NUM-RCPTCD                                     24250000
            WHEN '6019Z'                                                24260000
            WHEN '6013I'                                                24270000
              MOVE 'SRG   '  TO NUM-GRANT-TYPE    OF DCLEX-GRANT-ASMNT  24280000
            WHEN '6015B'                                                24290000
              MOVE 'SUG   '  TO NUM-GRANT-TYPE    OF DCLEX-GRANT-ASMNT  24300000
            WHEN '6020H'                                                24310000
            WHEN '6014J'                                                24320000
              MOVE 'AHG   '  TO NUM-GRANT-TYPE    OF DCLEX-GRANT-ASMNT  24330000
            WHEN '6011E'                                                24340000
            WHEN '6021Z'                                                24350000
              MOVE 'SHG   '  TO NUM-GRANT-TYPE    OF DCLEX-GRANT-ASMNT  24360000
            WHEN 'AHG  '                                                24370000
            WHEN 'SHG  '                                                24380000
            WHEN 'SRG  '                                                24390000
            WHEN 'SUG  '                                                24400000
            WHEN 'EHG  '                                                24410000
            WHEN 'HGT  '                                                24420000
            WHEN 'REG  '                                                24430000
            WHEN 'PHG  '                                                24440000
            WHEN 'TUG  '                                                24450000
            WHEN 'ATG  '                                                24460000
            WHEN 'FSG  '                                                24470000
              MOVE F526-NUM-RCPTCD                                      24480000
                             TO NUM-GRANT-TYPE    OF DCLEX-GRANT-ASMNT  24490000
           END-EVALUATE.                                                24500000
           IF TXT-CNCLTN-REASON OF DCLEX-GRANT-ASMNT = 'RC01'           24510000
              MOVE 'RC00'    TO TXT-CNCLTN-REASON OF DCLEX-GRANT-ASMNT. 24520000
           IF TXT-CNCLTN-REASON OF DCLEX-GRANT-ASMNT = 'RC02'           24530000
              MOVE 'GR'      TO TXT-CNCLTN-REASON OF DCLEX-GRANT-ASMNT. 24540000
                                                                        24550000
           MOVE 'BP13CB0E '  TO NUM-UPDATE-USERID OF DCLEX-GRANT-ASMNT. 24560000
                                                                        24570000
           STRING WS-DTE-CCYYMMDD(1:4) '-'                              24580000
                  WS-DTE-CCYYMMDD(5:2) '-'                              24590000
                  WS-DTE-CCYYMMDD(7:2)                                  24600000
                 DELIMITED BY SIZE INTO DTE-UPDATE OF DCLEX-GRANT-ASMNT.24610000
                                                                        24620000
           STRING WS-DTE-CCYYMMDD(1:4) '-'                              24630000
                  WS-DTE-CCYYMMDD(5:2) '-'                              24640000
                  WS-DTE-CCYYMMDD(7:2)                                  24650000
                 DELIMITED BY SIZE INTO DTE-CANCEL OF DCLEX-GRANT-ASMNT.24660000
                                                                        24670000
           STRING WS-TIME-HHMMSS(1:2) '.'                               24680000
                  WS-TIME-HHMMSS(3:2) '.'                               24690000
                  WS-TIME-HHMMSS(5:2)                                   24700000
                 DELIMITED BY SIZE INTO TME-UPDATE OF DCLEX-GRANT-ASMNT.24710000
                                                                        24720000
           EXEC SQL                                                     24730000
              UPDATE EX_GRANT_ASMNT                                     24740000
              SET TXT_CNCLTN_REASON =                                   24750000
                                 :DCLEX-GRANT-ASMNT.TXT-CNCLTN-REASON,  24760000
                  NUM_UPDATE_USERID =                                   24770000
                                 :DCLEX-GRANT-ASMNT.NUM-UPDATE-USERID,  24780000
                  TME_UPDATE   = :DCLEX-GRANT-ASMNT.TME-UPDATE,         24790000
                  DTE_UPDATE   = :DCLEX-GRANT-ASMNT.DTE-UPDATE,         24800000
                  DTE_CANCEL   = :DCLEX-GRANT-ASMNT.DTE-CANCEL          24810000
              WHERE NUM_RGSTRN = :DCLEX-GRANT-ASMNT.NUM-RGSTRN          24820000
               AND  NUM_GRANT_TYPE = :DCLEX-GRANT-ASMNT.NUM-GRANT-TYPE  24830000
               AND  TME_CREATE = :DCLEX-GRANT-ASMNT.TME-CREATE          24840000
           END-EXEC.                                                    24850000
                                                                        24860000
           EVALUATE SQLCODE                                             24870000
           WHEN WS-SQL-OK                                               24880000
                ADD 1       TO CNT-EX-GRANT-UPD                         24890000
                MOVE 'Y'    TO WS-EX-PROCESSED                          24900000
                               WS-REGNO-UPDATED                         24910000
                MOVE 'EX_GRANT_ASMNT'  TO WS-SOURCE                     24920000
                MOVE 'U'            TO WS-ACTION                        24930000
                MOVE NUM-RGSTRN OF DCLEX-GRANT-ASMNT TO WS-RGSTRN       24940000
                MOVE NUM-GRANT-TYPE OF DCLEX-GRANT-ASMNT TO WS-GRNT-TYPE24950000
                MOVE DCLEX-GRANT-ASMNT TO WS-AFTER-IMAGE                24960000
                PERFORM 6400-INSERT-TO-LOG     THRU 6400-EXIT           24970000
           WHEN WS-SQL-NFND                                             24980000
                MOVE SQLCODE   TO WS-SQLCODE                            24990000
                DISPLAY 'CASE NOTFND IN EX_GRANT_ASMNT'                 25000000
                DISPLAY 'SQLCODE IS : ' WS-SQLCODE ' '                  25010000
                        'REGNO : ' NUM-RGSTRN OF DCLEX-GRANT-ASMNT ' '  25020000
                       'GRNT TYPE: ' NUM-GRANT-TYPE OF DCLEX-GRANT-ASMNT25030000
                DISPLAY SPACES                                          25040000
                MOVE 'EX_GRANT_ASMNT NOT UPDATED, CASE NOT FOUND'       25050000
                                                        TO LB0E-REMARK  25060000
                PERFORM 6000-PRINT-ERROR-RPT THRU 6000-EXIT             25070000
           WHEN OTHER                                                   25080000
                MOVE SQLCODE   TO WS-SQLCODE                            25090000
                DISPLAY 'ERROR UPDATING EX_GRANT_ASMNT'                 25100000
                DISPLAY 'SQLCODE IS : ' WS-SQLCODE                      25110000
                DISPLAY SPACES                                          25120000
                PERFORM 9999-SQL-ERROR      THRU 9999-EXIT              25130000
           END-EVALUATE.                                                25140000
                                                                        25150000
       7000-EXIT.                                                       25160000
           EXIT.                                                        25170000
                                                                        25180000
      *----------------------*                                          25190000
       7100-UPD-GRANT-RCVRY.                                            25200000
      *----------------------*                                          25210000
           MOVE DCLGRANT-RCVRY TO WS-BEFORE-IMAGE.                      25220000
           IF F526-NUM-RCPTCD = '6019Z' OR '6013I' OR                   25230000
                                '6015B' OR '6020H' OR '6011E' OR        25240000
                                '6014J' OR '6021Z'                      25250000
              IF F526-NUM-CRDR = 'C'                                    25260000
                 ADD F526-AMT-TXN TO AMT-GRANT-RCVR OF DCLGRANT-RCVRY   25270000
              END-IF                                                    25280000
           ELSE                                                         25290000
             IF F526-NUM-RCPTCD = 'AHG  ' OR 'SHG  ' OR 'SUG  ' OR      25300000
                                  'SRG  ' OR 'EHG  ' OR 'HGT  ' OR      25310000
                                  'REG  ' OR 'PHG  ' OR 'TUG  ' OR      25320000
                                  'ATG  ' OR 'FSG  '                    25330000
                IF F526-NUM-CRDR = 'D'                                  25340000
                   ADD F526-AMT-TXN TO AMT-GRANT-RCVR OF DCLGRANT-RCVRY 25350000
                END-IF                                                  25360000
             END-IF                                                     25370000
           END-IF.                                                      25380000
                                                                        25390000
           IF F526-DTE-TXN = SPACES OR LOW-VALUES                       25400000
              MOVE '0001-01-01' TO DTE-RCV-RCVR-GRANT OF DCLGRANT-RCVRY 25410000
           ELSE                                                         25420000
              STRING F526-DTE-TXN(1:4) '-'                              25430000
                     F526-DTE-TXN(5:2) '-'                              25440000
                     F526-DTE-TXN(7:2) DELIMITED BY SIZE                25450000
                     INTO DTE-RCV-RCVR-GRANT OF DCLGRANT-RCVRY          25460000
           END-IF.                                                      25470000
                                                                        25480000
           MOVE 'BP13CB0E '                                             25490000
                   TO NUM-UPDATE-USERID OF DCLGRANT-RCVRY.              25500000
                                                                        25510000
           STRING WS-DTE-CCYYMMDD(1:4) '-'                              25520000
                  WS-DTE-CCYYMMDD(5:2) '-'                              25530000
                  WS-DTE-CCYYMMDD(7:2)                                  25540000
                 DELIMITED BY SIZE INTO DTE-UPDATE OF DCLGRANT-RCVRY.   25550000
                                                                        25560000
           STRING WS-TIME-HHMMSS(1:2) '.'                               25570000
                  WS-TIME-HHMMSS(3:2) '.'                               25580000
                  WS-TIME-HHMMSS(5:2)                                   25590000
                 DELIMITED BY SIZE INTO TME-UPDATE OF DCLGRANT-RCVRY.   25600000
                                                                        25610000
           EXEC SQL                                                     25620000
              UPDATE GRANT_RCVRY                                        25630000
              SET AMT_GRANT_RCVR    = :DCLGRANT-RCVRY.AMT-GRANT-RCVR,   25640000
                DTE_RCV_RCVR_GRANT = :DCLGRANT-RCVRY.DTE-RCV-RCVR-GRANT,25650000
                NUM_UPDATE_USERID = :DCLGRANT-RCVRY.NUM-UPDATE-USERID,  25660000
                TME_UPDATE        = :DCLGRANT-RCVRY.TME-UPDATE,         25670000
                DTE_UPDATE        = :DCLGRANT-RCVRY.DTE-UPDATE          25680000
              WHERE NUM_RGSTRN      = :DCLGRANT-RCVRY.NUM-RGSTRN        25690000
               AND  NUM_GRANT_TYPE  = :DCLGRANT-RCVRY.NUM-GRANT-TYPE    25700000
               AND  TME_CREATE      = :DCLGRANT-RCVRY.TME-CREATE        25710000
               AND  NUM_IDNTY_NO    = :DCLGRANT-RCVRY.NUM-IDNTY-NO      25720000
           END-EXEC.                                                    25730000
                                                                        25740000
           EVALUATE SQLCODE                                             25750000
           WHEN WS-SQL-OK                                               25760000
                ADD 1       TO CNT-RCVRY-UPD                            25770000
                MOVE 'Y'    TO WS-EX-PROCESSED                          25780000
                MOVE 'GRANT_RCVRY'  TO WS-SOURCE                        25790000
                MOVE 'U'            TO WS-ACTION                        25800000
                MOVE NUM-RGSTRN OF DCLGRANT-RCVRY TO WS-RGSTRN          25810000
                MOVE NUM-GRANT-TYPE OF DCLGRANT-RCVRY TO WS-GRNT-TYPE   25820000
                MOVE DCLGRANT-RCVRY TO WS-AFTER-IMAGE                   25830000
                PERFORM 6400-INSERT-TO-LOG     THRU 6400-EXIT           25840000
           WHEN WS-SQL-NFND                                             25850000
                MOVE SQLCODE   TO WS-SQLCODE                            25860000
                DISPLAY 'CASE NOTFND IN GRANT_RCVRY'                    25870000
                DISPLAY 'SQLCODE IS : ' WS-SQLCODE ' '                  25880000
                        'REGNO : ' NUM-RGSTRN OF DCLGRANT-RCVRY ' '     25890000
                       'GRNT TYPE: ' NUM-GRANT-TYPE OF DCLGRANT-RCVRY   25900000
                DISPLAY SPACES                                          25910000
                MOVE 'GRANT_RCVRY NOT UPDATED, CASE NOT FOUND'          25920000
                                                     TO LB0E-REMARK     25930000
                PERFORM 6000-PRINT-ERROR-RPT THRU 6000-EXIT             25940000
           WHEN OTHER                                                   25950000
                MOVE SQLCODE   TO WS-SQLCODE                            25960000
                DISPLAY 'ERROR UPDATING GRANT_RCVRY'                    25970000
                DISPLAY 'SQLCODE IS : ' WS-SQLCODE                      25980000
                DISPLAY SPACES                                          25990000
                PERFORM 9999-SQL-ERROR      THRU 9999-EXIT              26000000
           END-EVALUATE.                                                26010000
                                                                        26020000
       7100-EXIT.                                                       26030000
           EXIT.                                                        26040000
                                                                        26050000
      *------------------------*                                        26060000
       8100-UPD-RCVRY-INTRST.                                           26070000
      *------------------------*                                        26080000
           MOVE 'N' TO WS-UPD-INTRST.                                   26090000
                                                                        26100000
           PERFORM 8110-OPEN-CURSOR4    THRU 8110-EXIT.                 26110000
           PERFORM 8150-FETCH-CURSOR4   THRU 8150-EXIT                  26120000
                   UNTIL WS-CURSOR4-EOF = 'Y' OR WS-UPD-INTRST = 'Y'    26130000
           PERFORM 8180-CLOSE-CURSOR4   THRU 8180-EXIT.                 26140000
                                                                        26150000
       8100-EXIT.                                                       26160000
           EXIT.                                                        26170000
                                                                        26180000
      *================================================================*26190000
       8110-OPEN-CURSOR4.                                               26200000
      *================================================================*26210000
           INITIALIZE SQLCODE.                                          26220000
           MOVE 'N'                   TO WS-CURSOR4-EOF.                26230000
                                                                        26240000
           MOVE F526-NUM-REF TO NUM-RGSTRN OF DCLGRANT-RCVRY.           26250000
           MOVE F526-NUM-UIN TO NUM-IDNTY-NO OF DCLGRANT-RCVRY.         26260000
      *------------------------------------------*                      26270000
      *  DECLARE CURSOR4 - GRANT_RCVRY                                  26280000
      *  RETRIEVE RECORD WITH LATEST DATE AND TIME                      26290000
      *------------------------------------------*                      26300000
           EXEC SQL                                                     26310000
             DECLARE CURSOR4 CURSOR FOR                                 26320000
               SELECT NUM_RGSTRN,                                       26330000
                      NUM_GRANT_TYPE,                                   26340000
                      TME_CREATE,                                       26350000
                      NUM_IDNTY_NO,                                     26360000
                      NUM_IDNTY_NO_TYPE,                                26370000
                      AMT_GRANT_RCVR,                                   26380000
                      AMT_GRANT_RCVR_INTRST,                            26390000
                      DTE_RQST_RCVR,                                    26400000
                      DTE_RCV_RCVR_GRANT,                               26410000
                      DTE_ACK_GRANT_RCVRY_CPF,                          26420000
                      DTE_GRANT_ABORT,                                  26430000
                      DTE_GRANT_RPDT,                                   26440000
                      NUM_TRNSFR,                                       26450000
                      DTE_TRNSFR,                                       26460000
                      NUM_ACNT,                                         26470000
                      NUM_SCHEME,                                       26480000
                      NUM_TNCY,                                         26490000
                      DTE_CPF_GRANT_RQST,                               26500000
                      DTE_CPF_GRANT_RCV,                                26510000
                      DTE_UNUSED_GRANT_RQST,                            26520000
                      DTE_UNUSED_GRANT_RCV,                             26530000
                      DTE_USED_GRANT_RQST,                              26540000
                      DTE_USED_GRANT_RCV,                               26550000
                      NUM_UPDATE_USERID,                                26560000
                      DTE_UPDATE,                                       26570000
                      TME_UPDATE                                        26580000
               FROM                                                     26590000
                 GRANT_RCVRY                                            26600000
               WHERE                                                    26610000
                 NUM_RGSTRN = :DCLGRANT-RCVRY.NUM-RGSTRN AND            26620000
                 NUM_IDNTY_NO = :DCLGRANT-RCVRY.NUM-IDNTY-NO AND        26630000
                 DTE_UPDATE IN (SELECT MAX(DTE_UPDATE)                  26640000
                                FROM GRANT_RCVRY                        26650000
                                WHERE NUM_RGSTRN =                      26660000
                                        :DCLGRANT-RCVRY.NUM-RGSTRN AND  26670000
                                      NUM_IDNTY_NO =                    26680000
                                        :DCLGRANT-RCVRY.NUM-IDNTY-NO    26690000
                                GROUP BY NUM_RGSTRN, NUM_IDNTY_NO) AND  26700000
                 TME_UPDATE IN (SELECT MAX(TME_UPDATE)                  26710000
                                FROM GRANT_RCVRY                        26720000
                                WHERE NUM_RGSTRN =                      26730000
                                        :DCLGRANT-RCVRY.NUM-RGSTRN AND  26740000
                                      NUM_IDNTY_NO =                    26750000
                                        :DCLGRANT-RCVRY.NUM-IDNTY-NO    26760000
                         GROUP BY NUM_RGSTRN, NUM_IDNTY_NO, DTE_UPDATE) 26770000
           END-EXEC.                                                    26780000
                                                                        26790000
      *----------------------------------------*                        26800000
      *  OPEN CURSOR4 - GRANT_RCVRY                                     26810000
      *----------------------------------------*                        26820000
           EXEC SQL                                                     26830000
             OPEN CURSOR4                                               26840000
           END-EXEC.                                                    26850000
                                                                        26860000
           EVALUATE SQLCODE                                             26870000
           WHEN 0                                                       26880000
             CONTINUE                                                   26890000
           WHEN OTHER                                                   26900000
             MOVE SQLCODE            TO WS-SQLCODE                      26910000
             DISPLAY 'OPEN CURSOR4 ERROR'                               26920000
             GO TO 9999-SQL-ERROR                                       26930000
           END-EVALUATE.                                                26940000
                                                                        26950000
       8110-EXIT.                                                       26960000
            EXIT.                                                       26970000
                                                                        26980000
      *================================================================*26990000
       8150-FETCH-CURSOR4.                                              27000000
      *================================================================*27010000
                                                                        27020000
           EXEC SQL                                                     27030000
             FETCH CURSOR4                                              27040000
               INTO                                                     27050000
                 :DCLGRANT-RCVRY.NUM-RGSTRN,                            27060000
                 :DCLGRANT-RCVRY.NUM-GRANT-TYPE,                        27070000
                 :DCLGRANT-RCVRY.TME-CREATE,                            27080000
                 :DCLGRANT-RCVRY.NUM-IDNTY-NO,                          27090000
                 :DCLGRANT-RCVRY.NUM-IDNTY-NO-TYPE,                     27100000
                 :DCLGRANT-RCVRY.AMT-GRANT-RCVR,                        27110000
                 :DCLGRANT-RCVRY.AMT-GRANT-RCVR-INTRST,                 27120000
                 :DCLGRANT-RCVRY.DTE-RQST-RCVR,                         27130000
                 :DCLGRANT-RCVRY.DTE-RCV-RCVR-GRANT,                    27140000
                 :DCLGRANT-RCVRY.DTE-ACK-GRANT-RCVRY-CPF,               27150000
                 :DCLGRANT-RCVRY.DTE-GRANT-ABORT,                       27160000
                 :DCLGRANT-RCVRY.DTE-GRANT-RPDT,                        27170000
                 :DCLGRANT-RCVRY.NUM-TRNSFR,                            27180000
                 :DCLGRANT-RCVRY.DTE-TRNSFR,                            27190000
                 :DCLGRANT-RCVRY.NUM-ACNT,                              27200000
                 :DCLGRANT-RCVRY.NUM-SCHEME,                            27210000
                 :DCLGRANT-RCVRY.NUM-TNCY,                              27220000
                 :DCLGRANT-RCVRY.DTE-CPF-GRANT-RQST,                    27230000
                 :DCLGRANT-RCVRY.DTE-CPF-GRANT-RCV,                     27240000
                 :DCLGRANT-RCVRY.DTE-UNUSED-GRANT-RQST,                 27250000
                 :DCLGRANT-RCVRY.DTE-UNUSED-GRANT-RCV,                  27260000
                 :DCLGRANT-RCVRY.DTE-USED-GRANT-RQST,                   27270000
                 :DCLGRANT-RCVRY.DTE-USED-GRANT-RCV,                    27280000
                 :DCLGRANT-RCVRY.NUM-UPDATE-USERID,                     27290000
                 :DCLGRANT-RCVRY.DTE-UPDATE,                            27300000
                 :DCLGRANT-RCVRY.TME-UPDATE                             27310000
           END-EXEC.                                                    27320000
                                                                        27330000
           EVALUATE SQLCODE                                             27340000
           WHEN WS-SQL-OK                                               27350000
                PERFORM 8200-UPDATE-RCVRY-INTEREST THRU 8200-EXIT       27360000
           WHEN WS-SQL-NFND                                             27370000
             MOVE 'Y'                  TO WS-CURSOR4-EOF                27380000
           WHEN OTHER                                                   27390000
             MOVE SQLCODE            TO WS-SQLCODE                      27400000
             DISPLAY 'FETCH CURSOR1 ERROR'                              27410000
             GO TO 9999-SQL-ERROR                                       27420000
           END-EVALUATE.                                                27430000
                                                                        27440000
       8150-EXIT.                                                       27450000
            EXIT.                                                       27460000
                                                                        27470000
      *---------------------------------------------------------------* 27480000
       8180-CLOSE-CURSOR4.                                              27490000
      *---------------------------------------------------------------* 27500000
           INITIALIZE SQLCODE.                                          27510000
                                                                        27520000
      *-----------------------------------*                             27530000
      * CLOSE CURSOR4 - GRANT_RCVRY                                     27540000
      *-----------------------------------*                             27550000
           EXEC SQL CLOSE CURSOR4                                       27560000
           END-EXEC.                                                    27570000
                                                                        27580000
           EVALUATE SQLCODE                                             27590000
              WHEN 0                                                    27600000
                   CONTINUE                                             27610000
              WHEN OTHER                                                27620000
                MOVE SQLCODE TO WS-SQLCODE                              27630000
                DISPLAY 'CLOSE CURSOR4 ERROR'                           27640000
                GO TO 9999-SQL-ERROR                                    27650000
           END-EVALUATE.                                                27660000
                                                                        27670000
       8180-EXIT.                                                       27680000
           EXIT.                                                        27690000
                                                                        27700000
      *--------------------------*                                      27710000
       8200-UPDATE-RCVRY-INTEREST.                                      27720000
      *--------------------------*                                      27730000
           MOVE DCLGRANT-RCVRY TO WS-BEFORE-IMAGE.                      27740000
                                                                        27750000
           ADD F526-AMT-TXN TO AMT-GRANT-RCVR-INTRST OF DCLGRANT-RCVRY. 27760000
                                                                        27770000
           IF F526-DTE-TXN = SPACES OR LOW-VALUES                       27780000
              MOVE '0001-01-01' TO DTE-RCV-RCVR-GRANT OF DCLGRANT-RCVRY 27790000
           ELSE                                                         27800000
              STRING F526-DTE-TXN(1:4) '-'                              27810000
                     F526-DTE-TXN(5:2) '-'                              27820000
                     F526-DTE-TXN(7:2) DELIMITED BY SIZE                27830000
                     INTO DTE-RCV-RCVR-GRANT OF DCLGRANT-RCVRY          27840000
           END-IF.                                                      27850000
                                                                        27860000
           MOVE 'BP13CB0E '                                             27870000
                   TO NUM-UPDATE-USERID OF DCLGRANT-RCVRY.              27880000
                                                                        27890000
           STRING WS-DTE-CCYYMMDD(1:4) '-'                              27900000
                  WS-DTE-CCYYMMDD(5:2) '-'                              27910000
                  WS-DTE-CCYYMMDD(7:2)                                  27920000
                 DELIMITED BY SIZE INTO DTE-UPDATE OF DCLGRANT-RCVRY.   27930000
                                                                        27940000
           STRING WS-TIME-HHMMSS(1:2) '.'                               27950000
                  WS-TIME-HHMMSS(3:2) '.'                               27960000
                  WS-TIME-HHMMSS(5:2)                                   27970000
                 DELIMITED BY SIZE INTO TME-UPDATE OF DCLGRANT-RCVRY.   27980000
                                                                        27990000
           EXEC SQL                                                     28000000
              UPDATE GRANT_RCVRY                                        28010000
              SET AMT_GRANT_RCVR_INTRST =                               28020000
                                 :DCLGRANT-RCVRY.AMT-GRANT-RCVR-INTRST, 28030000
                DTE_RCV_RCVR_GRANT = :DCLGRANT-RCVRY.DTE-RCV-RCVR-GRANT,28040000
                NUM_UPDATE_USERID = :DCLGRANT-RCVRY.NUM-UPDATE-USERID,  28050000
                TME_UPDATE        = :DCLGRANT-RCVRY.TME-UPDATE,         28060000
                DTE_UPDATE        = :DCLGRANT-RCVRY.DTE-UPDATE          28070000
              WHERE NUM_RGSTRN      = :DCLGRANT-RCVRY.NUM-RGSTRN        28080000
               AND  NUM_GRANT_TYPE  = :DCLGRANT-RCVRY.NUM-GRANT-TYPE    28090000
               AND  TME_CREATE      = :DCLGRANT-RCVRY.TME-CREATE        28100000
               AND  NUM_IDNTY_NO    = :DCLGRANT-RCVRY.NUM-IDNTY-NO      28110000
           END-EXEC.                                                    28120000
                                                                        28130000
           EVALUATE SQLCODE                                             28140000
           WHEN WS-SQL-OK                                               28150000
                ADD 1       TO CNT-RCVRY-UPD                            28160000
                MOVE 'Y'    TO WS-UPD-INTRST                            28170000
                MOVE 'GRANT_RCVRY'  TO WS-SOURCE                        28180000
                MOVE 'U'            TO WS-ACTION                        28190000
                MOVE NUM-RGSTRN OF DCLGRANT-RCVRY TO WS-RGSTRN          28200000
                MOVE NUM-GRANT-TYPE OF DCLGRANT-RCVRY TO WS-GRNT-TYPE   28210000
                MOVE DCLGRANT-RCVRY TO WS-AFTER-IMAGE                   28220000
                PERFORM 6400-INSERT-TO-LOG     THRU 6400-EXIT           28230000
           WHEN WS-SQL-NFND                                             28240000
                MOVE SQLCODE   TO WS-SQLCODE                            28250000
                DISPLAY 'CASE NOTFND IN GRANT_RCVRY'                    28260000
                DISPLAY 'SQLCODE IS : ' WS-SQLCODE ' '                  28270000
                        'REGNO : ' NUM-RGSTRN OF DCLGRANT-RCVRY ' '     28280000
                       'GRNT TYPE: ' NUM-GRANT-TYPE OF DCLGRANT-RCVRY   28290000
                DISPLAY SPACES                                          28300000
                MOVE 'GRANT_RCVRY NOT UPDATED, CASE NOT FOUND'          28310000
                                                     TO LB0E-REMARK     28320000
                PERFORM 6000-PRINT-ERROR-RPT THRU 6000-EXIT             28330000
           WHEN OTHER                                                   28340000
                MOVE SQLCODE   TO WS-SQLCODE                            28350000
                DISPLAY 'ERROR UPDATING GRANT_RCVRY'                    28360000
                DISPLAY 'SQLCODE IS : ' WS-SQLCODE                      28370000
                DISPLAY SPACES                                          28380000
                PERFORM 9999-SQL-ERROR      THRU 9999-EXIT              28390000
           END-EVALUATE.                                                28400000
                                                                        28410000
       8200-EXIT.                                                       28420000
           EXIT.                                                        28430000
                                                                        28440000
      *-----------------*                                               28450000
       9000-CLOSE-FILES.                                                28460000
      *-----------------*                                               28470000
                                                                        28480000
           DISPLAY '******  BP13CB0E *************'.                    28490000
           DISPLAY 'RECS READ FROM  AC28F526     : ' CNT-F526-READ.     28500000
           DISPLAY 'RECS WRITTEN TO BP13K130     : ' CNT-K130-WRITE.    28510000
           DISPLAY 'RECS UPDATED TO ASMNT TBLE   : ' CNT-ASMNT-UPD.     28520000
           DISPLAY 'RECS MOVED TO EX GRANT ASMT  : ' WS-EX-GRANT-INS.   28530000
           DISPLAY 'RECS MOVED TO EX GRANT PRSN  : ' WS-EX-PRSN-INS.    28540000
           DISPLAY 'RECS CREATED TO GRANT RCVRY  : ' WS-RCVRY-INS.      28550000
           DISPLAY 'RECS UPDATED TO EX GRANT ASMT: ' CNT-EX-GRANT-UPD   28560000
           DISPLAY 'RECS UPDATED TO GRANT RCVRY  : ' CNT-RCVRY-UPD.     28570000
           DISPLAY 'RECS WRITTEN TO UPDATE LOG   : ' WS-GRNT-UPD-LOG.   28580000
           DISPLAY 'NO OF RECS WRITTEN (BP13F52A): ' WS-F52A-WRITE.     28590000
           DISPLAY 'RECS WRITTEN TO BP13LB0E     : ' CNT-PRT-CASES.     28600000
                                                                        28610000
           CLOSE AC28F526                                               28620000
                 BP13LB0E                                               28630000
                 BP13F52A                                               28640000
                 BP13K130.                                              28650000
                                                                        28660000
           STOP RUN.                                                    28670000
                                                                        28680000
       9000-EXIT.                                                       28690000
            EXIT.                                                       28700000
                                                                        28710000
      *---------------------------------------------------------------- 28720000
       9999-SQL-ERROR.                                                  28730000
      *---------------------------------------------------------------- 28740000
           CALL 'DBATIAR'  USING SQLCA.                                 28750000
           EXEC SQL ROLLBACK               END-EXEC.                    28760000
           MOVE 99                         TO    WS-RETURN-CODE.        28770000
           PERFORM 9000-CLOSE-FILES        THRU  9000-EXIT.             28780000
                                                                        28790000
       9999-EXIT.                                                       28800000
           EXIT.                                                        28810000
