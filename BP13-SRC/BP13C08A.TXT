       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C08A.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  12/04/2012.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT  (BP13)                               *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  TO DUPLICATE RECORDS FOR MGPS APPLICATION   *            
      *                                                            *            
      *  INPUT      :  BP13F740  BP13.F740.MATUREBA.MGP            *            
      *  I-O        :  BP13K740  BP13.K740.MATUREBA                *            
      *  OUTPUT     :  P13F740A  BP13.F740.MATUREBA.MGPSPA         *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP134483  SMR2  12042012  NEW PROGRAM                      *            
      * BP135170  SMR2  03122013  DELETE WRITING INTO BP13K740     *            
      * BP136096  SMR2  18012016  CATER FOR FTS/STS                *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
           SELECT BP13K740 ASSIGN TO BP13K740                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K740-KEY-FLD                               
                  FILE STATUS     IS WS-K740-STATUS.                            
           SELECT P13F740A ASSIGN TO P13F740A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F740                                                            
            RECORD CONTAINS 500  CHARACTERS                                     
            BLOCK CONTAINS    0  RECORDS                                        
            RECORDING MODE   IS  F                                              
            LABEL RECORDS        STANDARD.                                      
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K740                                                            
            RECORD CONTAINS 500  CHARACTERS.                                    
       COPY BP13K740.                                                           
                                                                                
       FD   P13F740A                                                            
            RECORD CONTAINS 500  CHARACTERS                                     
            BLOCK CONTAINS    0  RECORDS                                        
            RECORDING MODE   IS  F                                              
            LABEL RECORDS        STANDARD.                                      
       01   P13F740A-REC         PIC X(500).                                    
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-INDICATORS.                                                  
           05  WS-F740-EOF                PIC X(01) VALUE 'N'.                  
           05  WS-F740-READ               PIC 9(06) VALUE ZEROES.               
           05  WS-K740-FOUND              PIC X(01) VALUE 'N'.                  
           05  WS-K740-STATUS             PIC 9(02) VALUE ZEROES.               
           05  WS-740A-WRITE              PIC 9(06) VALUE ZEROES.               
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY                PIC 9(4).                             
           05  WS-SYS-MM                  PIC 9(2).                             
           05  WS-SYS-DD                  PIC 9(2).                             
                                                                                
       01  WS-EDITED-DATE                 PIC X(10).                            
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F740      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-F740       THRU 3000-EXIT                       
             UNTIL WS-F740-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F740                                                 
                       BP13K740                                                 
               OUTPUT  P13F740A.                                                
                                                                                
           IF WS-K740-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K740 OPEN ERROR ' WS-K740-STATUS                
              MOVE WS-K740-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-SYSTEM-DATE.                   
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F740.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F740 AT END                                                 
                MOVE 'Y'                   TO WS-F740-EOF                       
                GO TO 2000-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1                           TO WS-F740-READ.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-F740.                                                       
      *-------------------------------------------------------------            
                                                                                
           PERFORM 4000-READ-K740          THRU 4000-EXIT.                      
                                                                                
           IF WS-K740-FOUND = 'Y'                                               
              PERFORM 6000-WRITE-P13F740A  THRU 6000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F740      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4000-READ-K740.                                                          
      *--------------------------------------------------------*                
                                                                                
           MOVE LOW-VALUES                 TO K740-KEY-FLD.                     
           MOVE F740-KEY-FLD               TO K740-KEY-FLD.                     
                                                                                
           READ BP13K740.                                                       
                                                                                
           EVALUATE WS-K740-STATUS                                              
           WHEN ZEROES                                                          
              MOVE 'Y'                     TO WS-K740-FOUND                     
           WHEN 23                                                              
              MOVE 'N'                     TO WS-K740-FOUND                     
           WHEN OTHER                                                           
              DISPLAY 'ERROR IN READ OF BP13K740. STATUS IS '                   
                      WS-K740-STATUS                                            
              MOVE WS-K740-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-WRITE-P13F740A.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE 'M'                        TO F740-NUM-RANDOM(28:1).            
           IF F740-CDE-ALLOC-SCH NOT = 'FTS' AND 'STS'                          
              MOVE 'PA '                   TO F740-CDE-ALLOC-SCH                
           END-IF.                                                              
           MOVE SPACES                     TO F740-NUM-MGPS.                    
           MOVE SPACES                     TO F740-NUM-JB-REGN.                 
           MOVE SPACES                     TO F740-CDE-SUCC-NT-ZONE.            
           MOVE SPACES                     TO F740-CDE-UNSUCC.                  
                                                                                
           WRITE P13F740A-REC              FROM BP13F740-REC.                   
           ADD 1                           TO WS-740A-WRITE.                    
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '==============================================='.           
           DISPLAY '      PROGRAM : BP13C08A       ' WS-EDITED-DATE.            
           DISPLAY '==============================================='.           
           DISPLAY ' '.                                                         
           DISPLAY 'BP13F740 RECORDS READ       ' WS-F740-READ.                 
           DISPLAY 'P13F740A RECORDS WRITTEN    ' WS-740A-WRITE.                
           DISPLAY ' '.                                                         
           DISPLAY '==============================================='.           
                                                                                
           CLOSE BP13F740                                                       
                 BP13K740                                                       
                 P13F740A.                                                      
                                                                                
           IF WS-K740-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K740 CLOSE ERROR ' WS-K740-STATUS               
              MOVE WS-K740-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
