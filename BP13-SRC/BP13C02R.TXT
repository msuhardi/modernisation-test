       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C02R.                                                 
      *AUTHOR.        MICHAEL ARRIOLA.                                          
      *DATE-WRITTEN.  31/10/2008.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVES: TO DETERMINE ETHNIC TYPE OF THE APPLICATION    *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF   BY    DATE      DETAILS                          *            
      * -------   ====  ========= ================================ *            
      * BP133464  MSD1  20080624  NEW PGM                          *            
      * BP134532  IL5   20120723  ADD COND TO CHECK FOR SINGLE     *            
      *                           APPLICANT WITH DIV/SEP/WID MOTHER*            
      * BP135396  SMR2  20140623  ADD SINGLE AS AN ALLOWED MARITAL *            
      *                           STATUS OF MOTHER WHEN HOUSEHOLD  *            
      *                           CONSISTS OF SINGLE APPL & MOTHER *            
      * BP135414  SMR2  20140819  INITIALIZE WS-DECODE-CAT         *            
      * BP137897  FNP1  20190521  FOLLOW HA ETHNIC GROUP IF NOT    *            
      *                           GIVEN THE OPTION TO CHOOSE       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F730  ASSIGN        TO BP13F730.                          
           SELECT BP13K730  ASSIGN        TO BP13K730                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K730-NUM-REGN                      
                            FILE STATUS   IS BP13K730-STATUS.                   
           SELECT BP13K825  ASSIGN        TO BP13K825                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K825-KEY-FLD                       
                            FILE STATUS   IS BP13K825-STATUS.                   
           SELECT BP13L02R  ASSIGN        TO BP13L02R.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F730                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F730.                                                           
                                                                                
       FD   BP13K730                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K730.                                                           
                                                                                
       FD   BP13K825                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K825.                                                           
                                                                                
       FD   BP13L02R                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
       01   BP13L02R-REC          PIC X(132).                                   
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01 WS-FILE-STATUS.                                                       
          05 BP13K730-STATUS      PIC 9(02) VALUE ZEROES.                       
          05 BP13K825-STATUS      PIC 9(02) VALUE ZEROES.                       
          05 WS-K825-EOF          PIC X     VALUE 'N'.                          
          05 WS-K825-FND          PIC X     VALUE 'N'.                          
          05 WS-K730-FND          PIC X     VALUE 'N'.                          
                                                                                
       01 WS-COUNTERS.                                                          
          05 WS-F730-READ         PIC 9(08) VALUE ZEROES.                       
          05 WS-K730-FOUND        PIC 9(08) VALUE ZEROES.                       
          05 WS-K730-UPD          PIC 9(08) VALUE ZEROES.                       
                                                                                
       01 WS-SWITCHES.                                                          
          05 WS-HUSBAND-FND       PIC X(1).                                     
          05 WS-FATHER-FND        PIC X(1).                                     
                                                                                
       01 WS-VARIABLES.                                                         
          05 WS-SUB               PIC 9.                                        
          05 WS-OCCP-CNT          PIC 9.                                        
          05 WS-DECODE-CAT        PIC X(1).                                     
          05 WS-OCCP-TABLE OCCURS 8.                                            
             10 WS-REGN           PIC X(8).                                     
             10 WS-NRIC           PIC X(9).                                     
             10 WS-CAT            PIC X(1).                                     
             10 WS-SEX            PIC X(1).                                     
             10 WS-STAT           PIC X(1).                                     
             10 WS-RELN           PIC X(2).                                     
          05 WS-REGN-CAT          PIC X(1).                                     
                                                                                
          05 WS-SNO               PIC 9(04) VALUE ZERO.                         
          05 WS-DATE              PIC X(08) VALUE SPACES.                       
          05 WS-PAGENO            PIC 9(03) VALUE ZEROES.                       
          05 WS-LINE-CNT          PIC 9(02) VALUE 56.                           
                                                                                
      ***************************                                               
      * PRINT AREA                                                              
      ***************************                                               
       01 L02R-HEADER1.                                                         
          05 FILLER             PIC X(08) VALUE 'BP13L02R'.                     
          05 FILLER             PIC X(06) VALUE SPACES.                         
          05 FILLER             PIC X(04) VALUE 'HDB3'.                         
          05 FILLER             PIC X(38) VALUE SPACES.                         
          05 FILLER             PIC X(20) VALUE 'SYSTEM OF COMMITMENT'.         
          05 FILLER             PIC X(26) VALUE SPACES.                         
          05 FILLER             PIC X(07) VALUE 'DATE : '.                      
          05 L02R-DATE          PIC X(10).                                      
          05 FILLER             PIC X(02) VALUE SPACES.                         
          05 FILLER             PIC X(07) VALUE 'PAGE : '.                      
          05 L02R-PAGE          PIC Z(04).                                      
                                                                                
       01 L02R-HEADER2.                                                         
          05 FILLER             PIC X(50) VALUE SPACES.                         
          05 FILLER             PIC X(40) VALUE                                 
             'REPORT ON UPDATE OF ETHNIC GROUP'.                                
                                                                                
       01 L02R-HEADER3.                                                         
          05 FILLER             PIC X(10) VALUE SPACES.                         
          05 FILLER             PIC X(06) VALUE 'SEQ NO'.                       
          05 FILLER             PIC X(05) VALUE SPACES.                         
          05 FILLER             PIC X(12) VALUE 'REGISTRATION'.                 
          05 FILLER             PIC X(04) VALUE SPACES.                         
          05 FILLER             PIC X(10) VALUE 'OLD ETHNIC'.                   
          05 FILLER             PIC X(10) VALUE SPACES.                         
          05 FILLER             PIC X(10) VALUE 'NEW ETHNIC'.                   
          05 FILLER             PIC X(05) VALUE SPACES.                         
          05 FILLER             PIC X(04) VALUE 'CAT1'.                         
          05 FILLER             PIC X(02) VALUE SPACES.                         
          05 FILLER             PIC X(04) VALUE 'CAT2'.                         
          05 FILLER             PIC X(02) VALUE SPACES.                         
          05 FILLER             PIC X(04) VALUE 'CAT3'.                         
          05 FILLER             PIC X(02) VALUE SPACES.                         
          05 FILLER             PIC X(04) VALUE 'CAT4'.                         
          05 FILLER             PIC X(02) VALUE SPACES.                         
          05 FILLER             PIC X(04) VALUE 'CAT5'.                         
          05 FILLER             PIC X(02) VALUE SPACES.                         
          05 FILLER             PIC X(04) VALUE 'CAT6'.                         
          05 FILLER             PIC X(02) VALUE SPACES.                         
          05 FILLER             PIC X(04) VALUE 'CAT7'.                         
          05 FILLER             PIC X(02) VALUE SPACES.                         
          05 FILLER             PIC X(04) VALUE 'CAT8'.                         
                                                                                
       01 L02R-DASH-LINE        PIC X(132) VALUE ALL '-'.                       
                                                                                
       01 L02R-DETAILS.                                                         
          05 FILLER             PIC X(10) VALUE SPACES.                         
          05 L02R-SNO           PIC Z(04).                                      
          05 FILLER             PIC X(09) VALUE SPACES.                         
          05 L02R-REGN          PIC X(08).                                      
          05 FILLER             PIC X(10) VALUE SPACES.                         
          05 L02R-OLD-CAT       PIC X(01).                                      
          05 FILLER             PIC X(19) VALUE SPACES.                         
          05 L02R-NEW-CAT       PIC X(01).                                      
          05 FILLER             PIC X(13) VALUE SPACES.                         
          05 L02R-CAT-1         PIC X(01).                                      
          05 FILLER             PIC X(05) VALUE SPACES.                         
          05 L02R-CAT-2         PIC X(01).                                      
          05 FILLER             PIC X(05) VALUE SPACES.                         
          05 L02R-CAT-3         PIC X(01).                                      
          05 FILLER             PIC X(05) VALUE SPACES.                         
          05 L02R-CAT-4         PIC X(01).                                      
          05 FILLER             PIC X(05) VALUE SPACES.                         
          05 L02R-CAT-5         PIC X(01).                                      
          05 FILLER             PIC X(05) VALUE SPACES.                         
          05 L02R-CAT-6         PIC X(01).                                      
          05 FILLER             PIC X(05) VALUE SPACES.                         
          05 L02R-CAT-7         PIC X(01).                                      
          05 FILLER             PIC X(05) VALUE SPACES.                         
          05 L02R-CAT-8         PIC X(01).                                      
                                                                                
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13F730  THRU 2000-EXIT.                          
           PERFORM 3000-PROCESS-DATA   THRU 3000-EXIT                           
             UNTIL F730-NUM-REGN = HIGH-VALUES.                                 
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT BP13F730                                                  
                      BP13K825                                                  
                I-O   BP13K730                                                  
               OUTPUT BP13L02R.                                                 
                                                                                
           IF BP13K730-STATUS  NOT = 00 AND 97                                  
              DISPLAY 'ERROR OPENING - BP13K730 : ' BP13K730-STATUS             
              MOVE     BP13K730-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BP13K825-STATUS  NOT = 00 AND 97                                  
              DISPLAY 'ERROR OPENING - BP13K825 : ' BP13K825-STATUS             
              MOVE     BP13K825-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)     TO WS-DATE.                      
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                           DELIMITED BY SIZE INTO L02R-DATE.                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F730.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F730 AT END                                                 
             MOVE HIGH-VALUES TO F730-NUM-REGN                                  
             GO TO 2000-EXIT.                                                   
                                                                                
           ADD 1              TO WS-F730-READ.                                  
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2100-READ-BP13K730.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE F730-NUM-REGN         TO K730-NUM-REGN.                         
                                                                                
           READ BP13K730.                                                       
           EVALUATE BP13K730-STATUS                                             
             WHEN 00                                                            
                MOVE 'Y'               TO WS-K730-FND                           
                ADD   1                TO WS-K730-FOUND                         
                MOVE K730-CDE-CATEGORY TO WS-REGN-CAT                           
             WHEN 23                                                            
                DISPLAY 'REC NOT FOUND IN BP13K730'                             
                      ', K730-NUM-REGN: ' F730-NUM-REGN                         
             WHEN OTHER                                                         
                DISPLAY 'BP13K730 READ ERROR,STATUS : ' BP13K730-STATUS         
                      ', K730 KEY FLD : ' F730-NUM-REGN                         
                MOVE BP13K730-STATUS       TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE 1    TO WS-OCCP-CNT                                             
           MOVE 'N'  TO WS-K730-FND                                             
                        WS-K825-FND   WS-FATHER-FND                             
                        WS-K825-EOF   WS-HUSBAND-FND.                           
           PERFORM VARYING WS-SUB FROM 1 BY 1 UNTIL WS-SUB > 8                  
              MOVE SPACES         TO  WS-OCCP-TABLE(WS-SUB)                     
           END-PERFORM.                                                         
                                                                                
           PERFORM 2100-READ-BP13K730       THRU 2100-EXIT.                     
                                                                                
           IF WS-K730-FND = 'Y'                                                 
              PERFORM 3100-BROWSE-BP13K825  THRU 3100-EXIT                      
              IF WS-K825-FND = 'Y'                                              
                 PERFORM 3200-DETERMINE-CAT THRU 3200-EXIT                      
                    VARYING WS-SUB FROM 2 BY 1                                  
                      UNTIL WS-SUB > WS-OCCP-CNT                                
                 IF WS-REGN-CAT NOT = K730-CDE-CATEGORY                         
                    PERFORM 3400-GEN-RPT     THRU 3400-EXIT                     
                    PERFORM 3600-UPDATE-K730 THRU 3600-EXIT.                    
                                                                                
           PERFORM 2000-READ-BP13F730        THRU 2000-EXIT.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-BROWSE-BP13K825.                                                    
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES            TO  K825-KEY-FLD.                             
           MOVE K730-NUM-REGN     TO  K825-NUM-REGN.                            
                                                                                
           START BP13K825 KEY >= K825-KEY-FLD.                                  
           EVALUATE BP13K825-STATUS                                             
           WHEN 00                                                              
                PERFORM 4000-READNEXT-BP13K825 THRU 4000-EXIT                   
                  UNTIL K825-NUM-REGN NOT = K730-NUM-REGN                       
                     OR WS-K825-EOF = 'Y'                                       
           WHEN 10                                                              
           WHEN 23                                                              
                DISPLAY 'BP13K825 RECORD NOT FOUND  = ' K825-KEY-FLD            
           WHEN OTHER                                                           
                DISPLAY 'ERROR START BP13K825, STATUS  '                        
                        BP13K825-STATUS                                         
                        ',  REGN ' K825-NUM-REGN                                
                MOVE BP13K825-STATUS TO RETURN-CODE                             
                PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3200-DETERMINE-CAT.                                                      
      *-------------------------------------------------------------            
                                                                                
      * 0)   APPLICANT WITH CHILD                                               
           IF (WS-RELN(WS-SUB) = '41' OR '39') AND                              
              (WS-STAT(1) = '1' OR '3' OR '4') AND                              
              (K730-CDE-ELIG-SCH NOT = 'FS ')                                   
                 MOVE WS-CAT(1)      TO WS-REGN-CAT                             
           ELSE                                                                 
      * 1)   HUSBAND OF APPLICANT                                               
           IF WS-RELN(WS-SUB) = '20' AND WS-SEX(WS-SUB) = '1'                   
              MOVE 'Y'               TO WS-HUSBAND-FND                          
              IF WS-FATHER-FND = 'Y'                                            
                 MOVE SPACES         TO WS-REGN-CAT                             
              ELSE                                                              
                 MOVE WS-CAT(WS-SUB) TO WS-REGN-CAT                             
              END-IF                                                            
           ELSE                                                                 
      * 2)   FIANCE OF APPLICANT                                                
           IF WS-RELN(WS-SUB) = '09' AND WS-SEX(WS-SUB) = '1'                   
              AND K730-CDE-ELIG-SCH = 'FS '                                     
                 MOVE WS-CAT(WS-SUB) TO WS-REGN-CAT                             
           ELSE                                                                 
      * 3)   FATHER OF APPLICANT                                                
           IF WS-RELN(WS-SUB) = '02' AND WS-SEX(WS-SUB) = '1'                   
              MOVE 'Y'               TO WS-FATHER-FND                           
      * 3.1) FATHER OF SINGLE APPLICANT                                         
              IF WS-STAT(1) = '1'                                               
                 MOVE WS-CAT(1)      TO WS-REGN-CAT                             
              ELSE                                                              
      * 3.2) FATHER OF MARRIED FEMALE APPLICANT                                 
                 IF WS-HUSBAND-FND = 'Y'                                        
                    MOVE SPACES      TO WS-REGN-CAT                             
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
      * 4)   MOTHER OF SINGLE APPLICANT (MS = S/W/D/P)                          
           IF WS-RELN(WS-SUB) = '12' AND WS-SEX(WS-SUB) = '2' AND               
              (WS-STAT(WS-SUB) = '1' OR '3' OR '4' OR '5') AND                  
               WS-STAT(1) = '1' AND (K730-CDE-ELIG-SCH NOT = 'FS ')             
                 MOVE WS-CAT(1)      TO WS-REGN-CAT                             
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3400-GEN-RPT.                                                            
      *-------------------------------------------------------------            
           IF WS-LINE-CNT > 55                                                  
              PERFORM 3500-PRINT-HEADER THRU 3500-EXIT.                         
                                                                                
           MOVE SPACES                    TO L02R-DETAILS.                      
                                                                                
           ADD                          1 TO WS-SNO.                            
           MOVE WS-SNO                    TO L02R-SNO.                          
                                                                                
           MOVE K730-NUM-REGN             TO L02R-REGN                          
           MOVE K730-CDE-CATEGORY         TO L02R-OLD-CAT                       
           MOVE WS-REGN-CAT               TO L02R-NEW-CAT                       
                                                                                
           MOVE WS-CAT(1)                 TO L02R-CAT-1                         
           MOVE WS-CAT(2)                 TO L02R-CAT-2                         
           MOVE WS-CAT(3)                 TO L02R-CAT-3                         
           MOVE WS-CAT(4)                 TO L02R-CAT-4                         
           MOVE WS-CAT(5)                 TO L02R-CAT-5                         
           MOVE WS-CAT(6)                 TO L02R-CAT-6                         
           MOVE WS-CAT(7)                 TO L02R-CAT-7                         
           MOVE WS-CAT(8)                 TO L02R-CAT-8                         
                                                                                
           WRITE BP13L02R-REC           FROM L02R-DETAILS.                      
           ADD                          1 TO WS-LINE-CNT.                       
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3500-PRINT-HEADER.                                                       
      *-------------------------------------------------------------            
           ADD   1            TO WS-PAGENO.                                     
           MOVE WS-PAGENO     TO L02R-PAGE.                                     
                                                                                
                                                                                
           WRITE BP13L02R-REC  FROM L02R-HEADER1   AFTER PAGE                   
           WRITE BP13L02R-REC  FROM L02R-HEADER2   AFTER 1                      
           WRITE BP13L02R-REC  FROM L02R-HEADER3   AFTER 2                      
           WRITE BP13L02R-REC  FROM L02R-DASH-LINE AFTER 1                      
           MOVE   5              TO WS-LINE-CNT.                                
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3600-UPDATE-K730.                                                        
      *-------------------------------------------------------------            
                                                                                
           MOVE WS-REGN-CAT  TO K730-CDE-CATEGORY.                              
                                                                                
           REWRITE BP13K730-REC.                                                
           EVALUATE BP13K730-STATUS                                             
           WHEN 00                                                              
               ADD 1 TO WS-K730-UPD                                             
           WHEN OTHER                                                           
               DISPLAY 'BP13K730 REWRITE ERROR STATUS ' BP13K730-STATUS         
                       ' KEY IS ' K730-NUM-REGN                                 
               MOVE BP13K730-STATUS          TO RETURN-CODE                     
               PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-READNEXT-BP13K825.                                                  
      *-------------------------------------------------------------            
                                                                                
           READ BP13K825 NEXT RECORD.                                           
                                                                                
           EVALUATE BP13K825-STATUS                                             
           WHEN 00                                                              
           WHEN 02                                                              
                IF K825-NUM-REGN = K730-NUM-REGN                                
                   PERFORM 5000-MOVE-TO-TABLE THRU 5000-EXIT                    
                END-IF                                                          
           WHEN 10                                                              
           WHEN 23                                                              
                MOVE 'Y'        TO WS-K825-EOF                                  
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K825, STATUS '                       
                        BP13K825-STATUS ',  REGN ' K825-NUM-REGN                
                MOVE BP13K825-STATUS          TO RETURN-CODE                    
                PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-MOVE-TO-TABLE.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                     TO WS-DECODE-CAT.                    
           MOVE  'Y'                       TO WS-K825-FND.                      
                                                                                
           EVALUATE K825-NUM-ETHNIC                                             
              WHEN '0'                                                          
                 MOVE '2'                 TO WS-DECODE-CAT                      
              WHEN '5'                                                          
                 MOVE '1'                 TO WS-DECODE-CAT                      
              WHEN '6'                                                          
                 MOVE '3'                 TO WS-DECODE-CAT                      
              WHEN '7'                                                          
                 MOVE '4'                 TO WS-DECODE-CAT                      
           END-EVALUATE.                                                        
                                                                                
           IF K825-NUM-RELATIONSHIP = '00'                                      
              MOVE K825-NUM-REGN           TO WS-REGN(1)                        
              MOVE K825-NUM-NRIC           TO WS-NRIC(1)                        
              MOVE K825-NUM-SEX            TO WS-SEX(1)                         
              MOVE K825-NUM-RELATIONSHIP   TO WS-RELN(1)                        
              MOVE K825-NUM-MARITAL-STATUS TO WS-STAT(1)                        
      *       MOVE K825-NUM-ETHNIC         TO WS-CAT(1)                         
              MOVE WS-DECODE-CAT           TO WS-CAT(1)                         
                                              WS-REGN-CAT                       
           ELSE                                                                 
              ADD                        1 TO WS-OCCP-CNT                       
              MOVE K825-NUM-REGN           TO WS-REGN(WS-OCCP-CNT)              
              MOVE K825-NUM-NRIC           TO WS-NRIC(WS-OCCP-CNT)              
              MOVE K825-NUM-SEX            TO WS-SEX (WS-OCCP-CNT)              
              MOVE K825-NUM-RELATIONSHIP   TO WS-RELN(WS-OCCP-CNT)              
              MOVE K825-NUM-MARITAL-STATUS TO WS-STAT(WS-OCCP-CNT)              
      *       MOVE K825-NUM-ETHNIC         TO WS-CAT (WS-OCCP-CNT).             
              MOVE WS-DECODE-CAT           TO WS-CAT (WS-OCCP-CNT).             
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' BP13C02R CONTROL TOTAL '.                                  
           DISPLAY '------------------------------------------'.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECS READ FROM BP13F730  : ' WS-F730-READ.            
           DISPLAY 'NO OF RECS FOUND     BP13K730  : ' WS-K730-FOUND.           
           DISPLAY 'NO OF RECS UPDATED   BP13K730  : ' WS-K730-UPD.             
                                                                                
           CLOSE BP13F730                                                       
                 BP13K825                                                       
                 BP13K730                                                       
                 BP13L02R.                                                      
                                                                                
           IF BP13K730-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13K730 : ' BP13K730-STATUS             
              MOVE BP13K730-STATUS TO RETURN-CODE.                              
                                                                                
           IF BP13K825-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13K825 : ' BP13K825-STATUS             
              MOVE BP13K825-STATUS TO RETURN-CODE.                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
