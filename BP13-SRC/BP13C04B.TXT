      *--------------------------------------------------------*                
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------*                
       PROGRAM-ID. BP13C04B.                                                    
      *AUTHOR.     SK.                                                          
      *DATE-WRITTEN. 17/07/2004.                                                
      *--------------------------------------------------------*                
      **********************************************************                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *                                                        *                
      *      1. TO MATCH BP13F830 WITH AB03F089 (PIDB)         *                
      *         TO GET ESTATE NAME, ESTATE CODE FROM BM06K510  *                
      *                                                        *                
      * INPUT  : BP13F830                                      *                
      *          AB03F089                                      *                
      *          BM06K510                                      *                
      *                                                        *                
      * OUTPUT : P13F830A                                      *                
      *          P13F830B                                      *                
      *                                                        *                
      * CHG-REG# BY   ON          DESCRIPTION                  *                
      * -------- ---- ----------  -----------------------------*                
      * BP132558 SK   17/07/2004  NEW PRG                      *                
      **********************************************************                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F830   ASSIGN TO BP13F830.                                
           SELECT AB03F089   ASSIGN TO AB03F089.                                
           SELECT P13F830A   ASSIGN TO P13F830A.                                
           SELECT P13F830B   ASSIGN TO P13F830B.                                
                                                                                
           SELECT BM06K510  ASSIGN        TO BM06K510                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K510-ESTCDE                        
                            FILE STATUS   IS WS-K510-STATUS.                    
                                                                                
      *--------------------------------------------------------*                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------*                
                                                                                
       FD  BM06K510                                                             
           RECORD CONTAINS 60 CHARACTERS.                                       
       COPY BM06K510.                                                           
                                                                                
       FD   BP13F830                                                            
            RECORD CONTAINS 250 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13F830.                                                           
                                                                                
       FD   AB03F089                                                            
            RECORD CONTAINS 250 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY AB03F089.                                                           
                                                                                
       FD   P13F830A                                                            
            RECORD CONTAINS 300  CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       01   P13F830A-REC               PIC X(300).                              
                                                                                
       FD   P13F830B                                                            
            RECORD CONTAINS 250  CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       01   P13F830B-REC               PIC X(250).                              
      *--------------------------------------------------------*                
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------*                
       01  WORK-AREAS.                                                          
           05 WS-READ-F830              PIC 9(8)    VALUE 0.                    
           05 WS-READ-F089              PIC 9(8)    VALUE 0.                    
           05 WS-MATCH                  PIC 9(8)    VALUE 0.                    
           05 WS-NOT-MATCH              PIC 9(8)    VALUE 0.                    
           05 WS-WRITE-F830A            PIC 9(8)    VALUE 0.                    
           05 WS-WRITE-F830B            PIC 9(8)    VALUE 0.                    
           05 WS-K510-STATUS            PIC 9(2)    VALUE 0.                    
           05 WS-ESTNAME                PIC X(27)   VALUE SPACES.               
                                                                                
      *--------------------------------------------------------*                
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------*                
                                                                                
      *--------------------------------------------------------*                
       0000-MAIN.                                                               
      *--------------------------------------------------------*                
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-F830     THRU 2000-EXIT.                           
           PERFORM 3000-READ-F089     THRU 3000-EXIT.                           
                                                                                
           PERFORM 4000-COMPARE       THRU 4000-EXIT                            
                   UNTIL F830-NME-STREET = HIGH-VALUES                          
                     AND F089-NME-STREET = HIGH-VALUES.                         
                                                                                
           PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       1000-OPEN-FILES.                                                         
      *--------------------------------------------------------*                
                                                                                
           OPEN INPUT  BP13F830                                                 
                       AB03F089                                                 
                       BM06K510                                                 
           OUTPUT      P13F830A                                                 
                       P13F830B.                                                
                                                                                
           EVALUATE WS-K510-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'OPEN BM06K510 FAIL. STATUS - '                        
                                            WS-K510-STATUS                      
                 MOVE  WS-K510-STATUS       TO RETURN-CODE                      
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2000-READ-F830.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ BP13F830                                                        
                AT END                                                          
                   MOVE HIGH-VALUES TO F830-NME-STREET                          
                NOT AT END                                                      
                   ADD 1 TO WS-READ-F830                                        
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       3000-READ-F089.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ AB03F089                                                        
                AT END                                                          
                   MOVE HIGH-VALUES TO F089-NME-STREET                          
                NOT AT END                                                      
                   ADD 1 TO WS-READ-F089                                        
           END-READ.                                                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4000-COMPARE.                                                            
      *--------------------------------------------------------*                
                                                                                
           MOVE SPACES TO WS-ESTNAME.                                           
                                                                                
           IF F089-NME-STREET  = F830-NME-STREET                                
              ADD 1 TO WS-MATCH                                                 
              MOVE BP13F830-REC TO P13F830A-REC                                 
              PERFORM 4500-READ-K510  THRU 4500-EXIT                            
              MOVE F089-NUM-TOWN(1:3)   TO  P13F830A-REC(251:3)                 
              MOVE K510-ESTCDE          TO  P13F830A-REC(254:3)                 
              MOVE WS-ESTNAME           TO  P13F830A-REC(257:27)                
              WRITE P13F830A-REC                                                
              ADD 1 TO WS-WRITE-F830A                                           
              PERFORM 2000-READ-F830   THRU 2000-EXIT                           
              PERFORM 3000-READ-F089   THRU 3000-EXIT                           
           ELSE                                                                 
              ADD 1 TO WS-NOT-MATCH                                             
              IF F089-NME-STREET > F830-NME-STREET                              
                 ADD 1 TO WS-WRITE-F830B                                        
                 WRITE P13F830B-REC FROM BP13F830-REC                           
                 PERFORM 2000-READ-F830   THRU 2000-EXIT                        
              ELSE                                                              
                 PERFORM 3000-READ-F089   THRU 3000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4500-READ-K510.                                                          
      *--------------------------------------------------------*                
                                                                                
           MOVE F089-NUM-ESTATE-CIM  TO K510-ESTCDE                             
           READ BM06K510.                                                       
           EVALUATE WS-K510-STATUS                                              
              WHEN 00                                                           
                   MOVE K510-ESTATE-NAME TO WS-ESTNAME                          
              WHEN 23                                                           
                   MOVE SPACES TO WS-ESTNAME                                    
              WHEN OTHER                                                        
                 DISPLAY 'READ BM06K510 FAIL. STATUS - '                        
                                            WS-K510-STATUS                      
                 MOVE  WS-K510-STATUS       TO RETURN-CODE                      
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       9999-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------*                
                                                                                
           DISPLAY '*********** BP13C04B *************'.                        
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF RECORDS READ (BP13F830) :' WS-READ-F830.             
           DISPLAY 'NO. OF RECORDS READ (AB03F089) :' WS-READ-F089.             
           DISPLAY 'NO. OF RECORDS WRITE(P13F830A) :' WS-WRITE-F830A.           
           DISPLAY 'NO. OF RECORDS WRITE(P13F830B) :' WS-WRITE-F830B.           
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE       BP13F830                                                 
                       AB03F089                                                 
                       BM06K510                                                 
                       P13F830A                                                 
                       P13F830B.                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
