       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C21J.                                                 
       AUTHOR.        RACHELLE SAN BUENAVENTURA.                                
       DATE-WRITTEN.  20/09/2007.                                               
      * ========================================================= *             
      *              ADD APPOINTMENT SCHEDULES IN K225            *             
      * ========================================================= *             
      *  PURPOSE OF THIS PROGRAM: CREATE APPOINTMENT SCHEDULES    *             
      *                           IN BP13K225 FROM BOOKING APPMT  *             
      *                           DETAILS FLAT FILE BP13F223      *             
      *                                                           *             
      *  INPUT        :  BP13F223                                 *             
      *                  BP13K800                                 *             
      *                  BP13K820                                 *             
      *  I-O          :  BP13K225                                 *             
      *                  BP13F200                                 *             
      *  OUTPUT       :  BP13F225                                 *             
      *                                                           *             
      * ----------------------------------------------------------*             
      * CHG REF  DATE     BY   DESCRIPTION                        *             
      * -------- -------- --- ------------------------------------*             
      * BP133161 20/09/07 RB12 NEW PROGRAM                        *             
      * BP133147 05/10/07 RB12 REMOVE SETTING OF EOF WHEN STAGE 2 *             
      *                        QUEUE NUMBER IS ENCOUNTERED        *             
      * BP133071 17/09/08 RB12 CATER FOR SALES MODE 3R1           *             
      * BP133430 15/10/08 RB12 CATER FOR WIH                      *             
      * ==========================================================*             
                                                                                
       ENVIRONMENT    DIVISION.                                                 
       CONFIGURATION  SECTION.                                                  
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT BP13K225  ASSIGN        TO BP13K225                           
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K225-KEY-FLD                       
                            ALTERNATE KEY IS K225-ALT-KEY                       
                            ACCESS MODE   IS DYNAMIC                            
                            FILE STATUS   IS K225-FILE-STATUS.                  
                                                                                
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-REGN                      
                            ACCESS MODE   IS RANDOM                             
                            FILE STATUS   IS K800-FILE-STATUS.                  
                                                                                
           SELECT BP13K820  ASSIGN        TO BP13K820                           
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K820-KEY-FLD                       
                            ACCESS MODE   IS RANDOM                             
                            FILE STATUS   IS K820-FILE-STATUS.                  
                                                                                
           SELECT BP13F200  ASSIGN        TO BP13F200.                          
                                                                                
           SELECT BP13F223  ASSIGN        TO BP13F223.                          
                                                                                
           SELECT BP13F225  ASSIGN        TO BP13F225.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13K225                                                            
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13K225.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13F223                                                            
            RECORD CONTAINS 80 CHARACTERS                                       
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13K223.                                                           
                                                                                
       FD   BP13F200                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13F200.                                                           
                                                                                
       FD   BP13F225                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13F225.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  K225-FILE-STATUS               PIC 9(2) VALUE 0.                     
       01  K800-FILE-STATUS               PIC 9(2) VALUE 0.                     
       01  K820-FILE-STATUS               PIC 9(2) VALUE 0.                     
                                                                                
       01  WS-SAVE-DATE                   PIC X(8) VALUE SPACES.                
       01  WS-PREFIX                      PIC X    VALUE SPACES.                
       01  WS-PREFIX2                     PIC X    VALUE SPACES.                
       01  WS-CTR                         PIC 9(2) VALUE ZEROES.                
                                                                                
       01  WS-SAVE-SALES-MODE             PIC X(3) VALUE SPACES.                
       01  WS-SAVE-SALES-MODE-CNT         PIC 9(5) VALUE ZEROES.                
                                                                                
       01  WS-QUEUE-NUM REDEFINES WS-SAVE-SALES-MODE-CNT.                       
           05 FILLER                      PIC X.                                
           05 WS-QUEUE-NUM-DIGITS         PIC X(4).                             
                                                                                
       01  WS-SAVE-QUEUE                  PIC X(5) VALUE SPACES.                
       01  WS-SAVE-QUEUE-REDEF REDEFINES WS-SAVE-QUEUE.                         
           05 FILLER                      PIC X.                                
           05 WS-SAVE-QUEUE-NUM           PIC 9(4).                             
                                                                                
       01  WS-K223-NUM-CASE-ALPHA         PIC X(2).                             
       01  WS-K223-NUM-CASE REDEFINES WS-K223-NUM-CASE-ALPHA                    
                                          PIC 9(2).                             
                                                                                
       01  WS-F223-READ-CNT               PIC 9(8) VALUE ZEROES.                
       01  WS-K225-KEY-FND                PIC 9(8) VALUE ZEROES.                
       01  WS-K225-WRITE-CNT              PIC 9(8) VALUE ZEROES.                
       01  WS-K225-WRITE-ERR              PIC 9(8) VALUE ZEROES.                
       01  WS-F225-WRITE-CNT              PIC 9(8) VALUE ZEROES.                
       01  WS-K800-READ-ERR               PIC 9(8) VALUE ZEROES.                
       01  WS-K800-READ-CNT               PIC 9(8) VALUE ZEROES.                
       01  WS-K800-REWRITE-CNT            PIC 9(8) VALUE ZEROES.                
       01  WS-K800-REWRITE-ERR            PIC 9(8) VALUE ZEROES.                
       01  WS-F200-REWRITE-CNT            PIC 9(8) VALUE ZEROES.                
       01  WS-K820-READ-ERR               PIC 9(8) VALUE ZEROES.                
       01  WS-K820-READ-CNT               PIC 9(8) VALUE ZEROES.                
       01  WS-F200-READ-CNT               PIC 9(8) VALUE ZEROES.                
                                                                                
       01  WS-DTE-CURRENT.                                                      
           05  WS-DTE-CCYY                PIC 9(4) VALUE ZEROES.                
           05  WS-DTE-MM                  PIC 9(2) VALUE ZEROES.                
           05  WS-DTE-DD                  PIC 9(2) VALUE ZEROES.                
                                                                                
       01  WS-APP-INFO.                                                         
           05  WS-NME-HA1                 PIC X(30).                            
           05  WS-NUM-TEL                 PIC X(8).                             
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F200-EOF-FLAG           PIC X VALUE 'N'.                      
               88 WS-F200-EOF-YES               VALUE 'Y'.                      
               88 WS-F200-EOF-NO                VALUE 'N'.                      
           05  WS-F223-EOF-FLAG           PIC X VALUE 'N'.                      
               88 WS-F223-EOF-YES               VALUE 'Y'.                      
               88 WS-F223-EOF-NO                VALUE 'N'.                      
           05  WS-K225-EOF-FLAG           PIC X VALUE 'N'.                      
               88 WS-K225-EOF-YES               VALUE 'Y'.                      
               88 WS-K225-EOF-NO                VALUE 'N'.                      
           05  WS-K225-KEY-FOUND-FLAG     PIC X VALUE 'N'.                      
               88 WS-K225-KEY-FOUND             VALUE 'Y'.                      
               88 WS-K225-KEY-NOT-FOUND         VALUE 'N'.                      
           05  WS-K800-KEY-FOUND-FLAG     PIC X VALUE 'N'.                      
               88 WS-K800-KEY-FOUND             VALUE 'Y'.                      
               88 WS-K800-KEY-NOT-FOUND         VALUE 'N'.                      
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 1000-PRE-PROCESS         THRU 1000-EXIT.                     
           PERFORM 1500-READ-BP13F223       THRU 1500-EXIT.                     
           PERFORM 2100-FIND-LAST-QUEUE     THRU 2100-EXIT.                     
           PERFORM 2000-PROCESS-F223        THRU 2000-EXIT                      
             UNTIL WS-F223-EOF-YES.                                             
           PERFORM 3500-READ-F200           THRU 3500-EXIT.                     
           PERFORM 4000-PROCESS-MASTER      THRU 4000-EXIT                      
             UNTIL WS-F200-EOF-YES.                                             
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-PRE-PROCESS.                                                        
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F223                                                 
                       BP13K820                                                 
                I-O    BP13K225                                                 
                       BP13K800                                                 
                       BP13F200                                                 
                OUTPUT BP13F225.                                                
                                                                                
           IF K225-FILE-STATUS NOT = 00 AND 97                                  
              DISPLAY 'ERROR OPENING BP13K225 : ' K225-FILE-STATUS              
              MOVE     K225-FILE-STATUS    TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT.                      
                                                                                
           IF K800-FILE-STATUS NOT = 00 AND 97                                  
              DISPLAY 'ERROR OPENING BP13K800 : ' K800-FILE-STATUS              
              MOVE     K800-FILE-STATUS    TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT.                      
                                                                                
           IF K820-FILE-STATUS NOT = 00 AND 97                                  
              DISPLAY 'ERROR OPENING BP13K820 : ' K820-FILE-STATUS              
              MOVE     K820-FILE-STATUS    TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1500-READ-BP13F223.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F223 AT END                                                 
                MOVE 'Y' TO WS-F223-EOF-FLAG                                    
                GO TO 1500-EXIT.                                                
                                                                                
           ADD 1 TO WS-F223-READ-CNT.                                           
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-PROCESS-F223.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF K223-NUM-SALES-MODE = SPACES OR LOW-VALUES                        
              GO TO 2000-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE K223-NUM-CASES TO WS-K223-NUM-CASE-ALPHA                        
                                                                                
           PERFORM 3000-CREATE-K225 VARYING WS-CTR FROM 1 BY 1                  
             UNTIL WS-CTR > WS-K223-NUM-CASE.                                   
                                                                                
           PERFORM 1500-READ-BP13F223 THRU 1500-EXIT.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2100-FIND-LAST-QUEUE.                                                    
      *-------------------------------------------------------------            
                                                                                
           EVALUATE K223-NUM-SALES-MODE                                         
             WHEN 'WIH'                                                         
                MOVE '2' TO WS-PREFIX2                                          
             WHEN '3R '                                                         
                MOVE 'T' TO WS-PREFIX2                                          
             WHEN '3R1'                                                         
                MOVE 'Y' TO WS-PREFIX2                                          
           END-EVALUATE.                                                        
                                                                                
           SET WS-K225-EOF-NO        TO TRUE.                                   
                                                                                
           MOVE K223-NUM-SALES-MODE  TO K225-ALT-KEY.                           
                                                                                
           START BP13K225 KEY IS GREATER OR EQUAL K225-ALT-KEY                  
              END-START.                                                        
                                                                                
           EVALUATE K225-FILE-STATUS                                            
                                                                                
           WHEN 00                                                              
              PERFORM 2110-READNXT-K225 THRU 2110-EXIT                          
                UNTIL WS-K225-EOF-YES                                           
              MOVE WS-SAVE-QUEUE-NUM TO WS-SAVE-SALES-MODE-CNT                  
                                                                                
           WHEN 10                                                              
              DISPLAY 'K225 END OF FILE'                                        
                                                                                
           WHEN 23                                                              
              DISPLAY 'K225 KEY NOT FOUND IN START OF K225: '                   
                          K225-ALT-KEY                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR STARTING BP13K225'                                 
              DISPLAY 'K225-FILE-STATUS: ' K225-FILE-STATUS                     
                                                                                
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2110-READNXT-K225.                                                       
      *-------------------------------------------------------------            
                                                                                
           READ BP13K225 NEXT RECORD                                            
             AT END                                                             
                SET WS-K225-EOF-YES TO TRUE                                     
                GO TO 2110-EXIT.                                                
                                                                                
           IF K225-NUM-SALES-MODE NOT EQUAL K223-NUM-SALES-MODE                 
              SET WS-K225-EOF-YES TO TRUE                                       
              GO TO 2110-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF WS-PREFIX2 NOT EQUAL K225-NUM-QUEUE(1:1)                          
              MOVE K225-NUM-QUEUE      TO WS-SAVE-QUEUE                         
              MOVE K225-NUM-QUEUE(1:1) TO WS-PREFIX                             
           END-IF.                                                              
                                                                                
       2110-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-CREATE-K225.                                                        
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES TO BP13K225-REC.                                         
           INITIALIZE     BP13K225-REC.                                         
                                                                                
           ADD 1 TO WS-SAVE-SALES-MODE-CNT.                                     
                                                                                
           PERFORM 3100-CREATE-QUEUE-NUM THRU 3100-EXIT.                        
                                                                                
           STRING '##' K225-NUM-QUEUE                                           
              DELIMITED BY SIZE     INTO K225-NUM-NRIC.                         
                                                                                
           MOVE K223-NUM-SALES-MODE   TO K225-NUM-SALES-MODE.                   
           MOVE K223-DTE-APPOINTMENT  TO K225-DTE-APPOINTMENT                   
                                         WS-SAVE-DATE.                          
           MOVE K223-TME-APPOINTMENT  TO K225-TME-APPOINTMENT.                  
                                                                                
           MOVE 'BP13C21J'            TO K225-NUM-USERID.                       
           MOVE FUNCTION CURRENT-DATE TO K225-DTE-UPDATE.                       
           ACCEPT K225-TME-UPDATE   FROM TIME.                                  
                                                                                
           WRITE BP13K225-REC.                                                  
                                                                                
           IF K225-FILE-STATUS EQUAL 00 OR 02                                   
              ADD 1 TO WS-K225-WRITE-CNT                                        
           ELSE                                                                 
              DISPLAY 'ERROR WRITING K225 - ' K225-FILE-STATUS                  
                      ' APPMT NOT WRITTEN: ' K225-KEY-FLD K225-ALT-KEY          
              ADD 1 TO WS-K225-WRITE-ERR                                        
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3100-CREATE-QUEUE-NUM.                                                   
      *-------------------------------------------------------------            
            EVALUATE K223-NUM-SALES-MODE                                        
                WHEN '3R '                                                      
                   MOVE 'R' TO WS-PREFIX                                        
                WHEN '3R1'                                                      
                   MOVE 'S' TO WS-PREFIX                                        
            END-EVALUATE.                                                       
                                                                                
            IF K223-NUM-SALES-MODE = 'WIH'                                      
               MOVE WS-SAVE-SALES-MODE-CNT TO K225-NUM-QUEUE                    
            ELSE                                                                
               STRING WS-PREFIX WS-QUEUE-NUM-DIGITS                             
                  DELIMITED BY SIZE INTO K225-NUM-QUEUE                         
            END-IF.                                                             
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3500-READ-F200.                                                          
      *-------------------------------------------------------------            
           READ BP13F200                                                        
             AT END                                                             
                SET WS-F200-EOF-YES TO TRUE                                     
                GO TO 3500-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F200-READ-CNT.                                           
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4000-PROCESS-MASTER.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES               TO K225-KEY-FLD.                           
                                                                                
           EVALUATE F200-NUM-SELTYPE                                            
              WHEN '3R '                                                        
                STRING 'R' F200-NUM-HHTY-QUEUE-SERIAL(2:4)                      
                  DELIMITED BY SIZE                                             
                     INTO K225-NUM-QUEUE                                        
              WHEN '3R1'                                                        
                STRING 'S' F200-NUM-HHTY-QUEUE-SERIAL(2:4)                      
                  DELIMITED BY SIZE                                             
                     INTO K225-NUM-QUEUE                                        
              WHEN 'WIH'                                                        
                MOVE F200-NUM-HHTY-QUEUE-SERIAL TO K225-NUM-QUEUE               
           END-EVALUATE.                                                        
                                                                                
           READ BP13K225.                                                       
                                                                                
           EVALUATE K225-FILE-STATUS                                            
                                                                                
           WHEN 00                                                              
           WHEN 02                                                              
              PERFORM 4100-PROCESS-K225 THRU 4100-EXIT                          
                                                                                
           WHEN 23                                                              
              DISPLAY 'K225 KEY NOT FOUND IN READING K225: '                    
                          K225-KEY-FLD                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K225'                                  
              DISPLAY 'K225-FILE-STATUS: ' K225-FILE-STATUS                     
                                                                                
           END-EVALUATE.                                                        
                                                                                
           SET WS-K225-KEY-NOT-FOUND TO TRUE.                                   
                                                                                
           PERFORM 3500-READ-F200 THRU 3500-EXIT.                               
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4100-PROCESS-K225.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF K225-NUM-REGN = LOW-VALUES OR SPACES                              
                                                                                
              SET WS-K225-KEY-FOUND     TO   TRUE                               
              SET WS-K800-KEY-NOT-FOUND TO   TRUE                               
              PERFORM 4110-UPDATE-FILES THRU 4110-EXIT                          
              ADD 1 TO WS-K225-KEY-FND                                          
                                                                                
           ELSE                                                                 
              SET WS-K225-KEY-NOT-FOUND TO TRUE                                 
                                                                                
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4110-UPDATE-FILES.                                                       
      *-------------------------------------------------------------            
                                                                                
           PERFORM 4120-READ-BP13K800       THRU 4120-EXIT.                     
                                                                                
           IF WS-K800-KEY-FOUND                                                 
              PERFORM 4122-REWRITE-BP13K800 THRU 4122-EXIT                      
              PERFORM 4130-WRITE-BP13F225   THRU 4130-EXIT                      
              PERFORM 4123-REWRITE-BP13F200 THRU 4123-EXIT                      
           END-IF.                                                              
                                                                                
       4110-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4120-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE F200-NUM-REGN TO K800-NUM-REGN.                                 
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE K800-FILE-STATUS                                            
                                                                                
              WHEN 00                                                           
                ADD 1 TO WS-K800-READ-CNT                                       
                SET WS-K800-KEY-FOUND TO TRUE                                   
                PERFORM 4121-READ-BP13K820    THRU 4121-EXIT                    
                                                                                
              WHEN 23                                                           
                DISPLAY 'KEY NOT FOUND IN READING K800: '                       
                         K800-NUM-REGN                                          
                ADD 1 TO WS-K800-READ-ERR                                       
                                                                                
              WHEN OTHER                                                        
                DISPLAY 'ERROR READING BP13K800: ' K800-FILE-STATUS             
                MOVE     K800-FILE-STATUS TO RETURN-CODE                        
                PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                     
                                                                                
           END-EVALUATE.                                                        
                                                                                
       4120-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4121-READ-BP13K820.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                 TO WS-APP-INFO.                          
                                                                                
           MOVE K800-NUM-REGN          TO K820-NUM-REGN.                        
                                                                                
           MOVE K800-NUM-NRIC1         TO K820-NUM-NRIC.                        
                                                                                
           READ BP13K820.                                                       
                                                                                
           EVALUATE K820-FILE-STATUS                                            
                                                                                
              WHEN 00                                                           
                MOVE K820-NME-OCCP(1:30)      TO WS-NME-HA1                     
                IF K820-NUM-HP-PGR NOT EQUAL SPACES AND LOW-VALUES              
                   MOVE K820-NUM-HP-PGR       TO WS-NUM-TEL                     
                ELSE                                                            
                   IF K820-NUM-PHONE-OFF NOT EQUAL SPACES                       
                                         AND LOW-VALUES                         
                      MOVE K820-NUM-PHONE-OFF TO WS-NUM-TEL                     
                   ELSE                                                         
                      IF K820-NUM-PHONE NOT EQUAL SPACES                        
                                         AND LOW-VALUES                         
                         MOVE K820-NUM-PHONE  TO WS-NUM-TEL                     
                      END-IF                                                    
                   END-IF                                                       
                END-IF                                                          
                ADD 1 TO WS-K820-READ-CNT                                       
              WHEN 23                                                           
                DISPLAY 'KEY NOT FOUND IN READING K820: '                       
                         K820-KEY-FLD                                           
                ADD 1 TO WS-K820-READ-ERR                                       
                                                                                
              WHEN OTHER                                                        
                DISPLAY 'ERROR READING BP13K820: ' K820-FILE-STATUS             
                MOVE     K820-FILE-STATUS TO RETURN-CODE                        
                PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                     
                                                                                
           END-EVALUATE.                                                        
                                                                                
       4121-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4122-REWRITE-BP13K800.                                                   
      *-------------------------------------------------------------            
                                                                                
           IF K800-NUM-STATUS NOT = 'C'                                         
              MOVE K225-DTE-APPOINTMENT  TO K800-DTE-BK-APPT                    
              MOVE K225-TME-APPOINTMENT  TO K800-TME-BK-APPT                    
              MOVE K225-NUM-QUEUE        TO K800-NUM-NT-FT-QUEUE                
                                                                                
              MOVE FUNCTION CURRENT-DATE TO K800-DTE-UPDATE                     
              MOVE 'BP13C21J'            TO K800-NUM-USERID                     
                                                                                
              REWRITE BP13K800-MASTER                                           
                                                                                
              IF K800-FILE-STATUS EQUAL 00                                      
                 ADD 1                   TO WS-K800-REWRITE-CNT                 
              ELSE                                                              
                 DISPLAY 'ERROR REWRITING K800 - ' K800-FILE-STATUS             
                        ' K800 REC NOT UPDATED: ' K800-NUM-REGN                 
                 ADD 1                   TO WS-K800-REWRITE-ERR                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4122-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4123-REWRITE-BP13F200.                                                   
      *-------------------------------------------------------------            
                                                                                
           MOVE K225-DTE-APPOINTMENT  TO F200-DTE-BKAPPMT-DATE.                 
           MOVE K225-TME-APPOINTMENT  TO F200-TME-BKAPPMT.                      
           MOVE K225-NUM-QUEUE        TO F200-NUM-HHTY-QUEUE-SERIAL.            
                                                                                
           REWRITE BP13F200-REC.                                                
                                                                                
           ADD 1 TO WS-F200-REWRITE-CNT.                                        
                                                                                
       4123-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4130-WRITE-BP13F225.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE BP13K225-REC          TO BP13F225-REC.                          
                                                                                
           MOVE F200-NUM-REGN         TO F225-NUM-REGN.                         
           MOVE K800-NUM-NRIC1        TO F225-NUM-NRIC.                         
           MOVE WS-NME-HA1            TO F225-NME-HA1.                          
           MOVE WS-NUM-TEL            TO F225-NUM-TEL.                          
           MOVE K800-NUM-CAT          TO F225-NUM-CAT.                          
           MOVE K800-DTE-BALLOT(1:4)  TO F225-DTE-YEAR-LAUNCH.                  
           MOVE K800-DTE-BALLOT(5:2)  TO F225-DTE-MONTH-LAUNCH.                 
                                                                                
           MOVE FUNCTION CURRENT-DATE TO F225-DTE-UPDATE.                       
           ACCEPT F225-TME-UPDATE   FROM TIME.                                  
           MOVE 'BP13C21J'            TO F225-NUM-USERID.                       
                                                                                
           WRITE BP13F225-REC.                                                  
                                                                                
           ADD 1                      TO WS-F225-WRITE-CNT.                     
                                                                                
       4130-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '********* BP13C21J ************'.                           
           DISPLAY '                               '.                           
           DISPLAY '***** UPDATE APPOINTMENT ******'.                           
           DISPLAY 'RECS READ   FROM BP13F223   : ' WS-F223-READ-CNT.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS WRITTEN  IN BP13K225   : ' WS-K225-WRITE-CNT.          
           DISPLAY 'RECS ERROR WRITE BP13K225   : ' WS-K225-WRITE-ERR.          
           DISPLAY '                               '.                           
           DISPLAY '***** UPDATE NRIC DETAILS *****'.                           
           DISPLAY 'RECS READ FROM  BP13F200    : ' WS-F200-READ-CNT.           
           DISPLAY '                              '.                            
           DISPLAY 'KEYS FOUND IN BP13K225      : ' WS-K225-KEY-FND.            
           DISPLAY 'RECS WRITTEN  IN BP13F225   : ' WS-F225-WRITE-CNT.          
           DISPLAY '                              '.                            
           DISPLAY 'RECS READ    FROM BP13K800  : ' WS-K800-READ-CNT.           
           DISPLAY 'RECS NOTFND    IN BP13K800  : ' WS-K800-READ-ERR.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS READ   FROM BP13K820   : ' WS-K820-READ-CNT.           
           DISPLAY 'RECS NOTFND   IN BP13K820   : ' WS-K820-READ-ERR.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS REWRITTEN IN BP13K800  : ' WS-K800-REWRITE-CNT.        
           DISPLAY 'REWRITE ERRORS IN BP13K800  : ' WS-K800-REWRITE-ERR.        
           DISPLAY '                              '.                            
           DISPLAY 'RECS WRITTEN  IN BP13F200   : ' WS-F200-REWRITE-CNT.        
                                                                                
           CLOSE   BP13F200                                                     
                   BP13F223                                                     
                   BP13F225                                                     
                   BP13K225                                                     
                   BP13K800                                                     
                   BP13K820.                                                    
                                                                                
           IF K225-FILE-STATUS NOT = 00                                         
              DISPLAY 'ERROR CLOSING BP13K225 : ' K225-FILE-STATUS              
              MOVE     K225-FILE-STATUS    TO RETURN-CODE.                      
                                                                                
           IF K800-FILE-STATUS NOT = 00                                         
              DISPLAY 'ERROR CLOSING BP13K800 : ' K800-FILE-STATUS              
              MOVE     K800-FILE-STATUS    TO RETURN-CODE.                      
                                                                                
           IF K820-FILE-STATUS NOT = 00                                         
              DISPLAY 'ERROR CLOSING BP13K820 : ' K820-FILE-STATUS              
              MOVE     K820-FILE-STATUS    TO RETURN-CODE.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
