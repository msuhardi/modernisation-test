      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13CB0M.                                                 
      *AUTHOR.        MARGE LAM KO.                                             
      *DATE-WRITTEN.  03 MAY 2017.                                              
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : THIS PROGRAM RETRIEVES RECORDS THAT HAVE    *           
      *                 PROCESS DATE = CURRENT DATE                 *           
      *                                                             *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BP13F800 -- MASTER FILE                               *           
      *   2.  BP13F893 -- HISTORY FILE                              *           
      *   2.  BP13FD00 -- HISTORY FILE                              *           
      *                                                             *           
      *   OUTPUT FILES:                                             *           
      *   1.  P13F800A -- OUTPUT FOR BP13F800                       *           
      *   2.  P13F893A -- OUTPUT FOR BP13F893                       *           
      *   2.  P13FD00A -- OUTPUT FOR BP13FD00                       *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP136622 03/05/2017 MRN1 NEW PROGRAM                        *           
      * BP136844 16/10/2017 MRN1 INCLUDE COMPARISON TO DATE YESTERDAY           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
           SELECT BP13F893 ASSIGN TO BP13F893.                                  
           SELECT BP13FD00 ASSIGN TO BP13FD00.                                  
           SELECT P13F800A ASSIGN TO P13F800A.                                  
           SELECT P13F893A ASSIGN TO P13F893A.                                  
           SELECT P13FD00A ASSIGN TO P13FD00A.                                  
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F800                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  2000 CHARACTERS                                    
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13K800.                                                   00055007
                                                                                
       FD   BP13F893                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  2050 CHARACTERS                                    
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13K893.                                                   00055007
                                                                                
       FD   BP13FD00                                                            
            RECORD CONTAINS 1600 CHARACTERS                                     
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
       COPY BP13KD00.                                                           
                                                                                
       FD   P13F800A                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  2000 CHARACTERS                                    
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                   00055007
                                                                                
       FD   P13F893A                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  2050 CHARACTERS                                    
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F893.                                                   00055007
                                                                                
       FD   P13FD00A                                                            
            RECORD CONTAINS 1600 CHARACTERS                                     
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
       COPY BP13FD00.                                                           
                                                                                
       COPY AV02COMM.                                                           
                                                                                
      ************************                                                  
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  WS-INDICATORS.                                                       
           05  WS-EOF-F800                PIC X       VALUE SPACE.              
           05  WS-EOF-F893                PIC X       VALUE SPACE.              
           05  WS-EOF-FD00                PIC X       VALUE SPACE.              
                                                                                
       01  WS-COUNTERS.                                                         
           05  CNT-F800-READ              PIC 9(5)    VALUE ZEROES.             
           05  CNT-F893-READ              PIC 9(5)    VALUE ZEROES.             
           05  CNT-FD00-READ              PIC 9(5)    VALUE ZEROES.             
           05  CNT-F800-WRITE             PIC 9(5)    VALUE ZEROES.             
           05  CNT-F893-WRITE             PIC 9(5)    VALUE ZEROES.             
           05  CNT-FD00-WRITE             PIC 9(5)    VALUE ZEROES.             
                                                                                
       01  WS-CURR-DATE.                                                        
           05 WS-CURRENT-DATE             PIC 9(8).                             
           05 WS-CURRENT-TIME             PIC 9(8).                             
                                                                                
       01  WS-DTE-YESTERDAY               PIC 9(8).                             
       01  WS-INTEGER                     PIC 9(7).                             
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-F800    THRU 2000-EXIT                             
                   UNTIL WS-EOF-F800 = 'Y'                                      
           PERFORM 3000-READ-F893    THRU 3000-EXIT                             
                   UNTIL WS-EOF-F893 = 'Y'                                      
           PERFORM 4000-READ-FD00    THRU 4000-EXIT                             
                   UNTIL WS-EOF-FD00 = 'Y'                                      
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP13F893                                                 
                       BP13FD00                                                 
                OUTPUT P13F800A                                                 
                       P13F893A                                                 
                       P13FD00A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:16) TO WS-CURR-DATE.                    
                                                                                
           COMPUTE WS-INTEGER =                                                 
                   FUNCTION INTEGER-OF-DATE(WS-CURRENT-DATE).                   
           SUBTRACT 1 FROM WS-INTEGER.                                          
           COMPUTE WS-DTE-YESTERDAY =                                           
                   FUNCTION DATE-OF-INTEGER(WS-INTEGER).                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-F800.                                                          
      *---------------*                                                         
                                                                                
           READ BP13F800 AT END                                                 
                MOVE HIGH-VALUES TO BP13K800-MASTER                             
                MOVE 'Y' TO WS-EOF-F800                                         
                GO TO 2000-EXIT.                                                
                                                                                
           MOVE K800-DTE-AHG-PROCESS TO WS-IN-DATE.                             
           MOVE 'CD' TO WS-IN-OPTION.                                           
           CALL 'AV02C001' USING LINK-REC                                       
           IF WS-RETURN NOT = '000'                                             
              MOVE '00010101' TO K800-DTE-AHG-PROCESS                           
           END-IF.                                                              
                                                                                
           MOVE K800-DTE-SHG-PROCESS TO WS-IN-DATE.                             
           MOVE 'CD' TO WS-IN-OPTION.                                           
           CALL 'AV02C001' USING LINK-REC                                       
           IF WS-RETURN NOT = '000'                                             
              MOVE '00010101' TO K800-DTE-SHG-PROCESS                           
           END-IF.                                                              
                                                                                
           ADD 1 TO CNT-F800-READ.                                              
                                                                                
           IF (K800-DTE-AHG-PROCESS = WS-CURRENT-DATE OR                        
               K800-DTE-SHG-PROCESS = WS-CURRENT-DATE) OR                       
              (K800-DTE-AHG-PROCESS = WS-DTE-YESTERDAY OR                       
               K800-DTE-SHG-PROCESS = WS-DTE-YESTERDAY)                         
              WRITE BP13F800-MASTER FROM BP13K800-MASTER                        
              ADD 1 TO CNT-F800-WRITE                                           
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       3000-READ-F893.                                                          
      *---------------*                                                         
                                                                                
           READ BP13F893 AT END                                                 
                MOVE HIGH-VALUES TO BP13K893-MASTER                             
                MOVE 'Y' TO WS-EOF-F893                                         
                GO TO 3000-EXIT.                                                
                                                                                
           MOVE K893-DTE-AHG-PROCESS TO WS-IN-DATE.                             
           MOVE 'CD' TO WS-IN-OPTION.                                           
           CALL 'AV02C001' USING LINK-REC                                       
           IF WS-RETURN NOT = '000'                                             
              MOVE '00010101' TO K893-DTE-AHG-PROCESS                           
           END-IF.                                                              
                                                                                
           MOVE K893-DTE-SHG-PROCESS TO WS-IN-DATE.                             
           MOVE 'CD' TO WS-IN-OPTION.                                           
           CALL 'AV02C001' USING LINK-REC                                       
           IF WS-RETURN NOT = '000'                                             
              MOVE '00010101' TO K893-DTE-SHG-PROCESS                           
           END-IF.                                                              
                                                                                
           ADD 1 TO CNT-F800-READ.                                              
                                                                                
           IF (K893-DTE-AHG-PROCESS = WS-CURRENT-DATE OR                        
               K893-DTE-SHG-PROCESS = WS-CURRENT-DATE) OR                       
              (K893-DTE-AHG-PROCESS = WS-DTE-YESTERDAY OR                       
               K893-DTE-SHG-PROCESS = WS-DTE-YESTERDAY)                         
              WRITE BP13F893-MASTER FROM BP13K893-MASTER                        
              ADD 1 TO CNT-F893-WRITE                                           
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       4000-READ-FD00.                                                          
      *---------------*                                                         
                                                                                
           READ BP13FD00 AT END                                                 
                MOVE HIGH-VALUES TO BP13KD00-REC                                
                MOVE 'Y' TO WS-EOF-FD00                                         
                GO TO 4000-EXIT.                                                
                                                                                
           MOVE KD00-DTE-HSGNT-APPROVE TO WS-IN-DATE.                           
           MOVE 'CD' TO WS-IN-OPTION.                                           
           CALL 'AV02C001' USING LINK-REC                                       
           IF WS-RETURN NOT = '000'                                             
              MOVE '00010101' TO KD00-DTE-HSGNT-APPROVE                         
           END-IF.                                                              
                                                                                
           MOVE KD00-DTE-HSGNT-REJECT TO WS-IN-DATE.                            
           MOVE 'CD' TO WS-IN-OPTION.                                           
           CALL 'AV02C001' USING LINK-REC                                       
           IF WS-RETURN NOT = '000'                                             
              MOVE '00010101' TO KD00-DTE-HSGNT-REJECT                          
           END-IF.                                                              
                                                                                
           MOVE KD00-DTE-HSGNT-APPROVE-TOPUP TO WS-IN-DATE.                     
           MOVE 'CD' TO WS-IN-OPTION.                                           
           CALL 'AV02C001' USING LINK-REC                                       
           IF WS-RETURN NOT = '000'                                             
              MOVE '00010101' TO KD00-DTE-HSGNT-APPROVE-TOPUP                   
           END-IF.                                                              
                                                                                
           MOVE KD00-DTE-HSGNT-REJECT-TOPUP TO WS-IN-DATE.                      
           MOVE 'CD' TO WS-IN-OPTION.                                           
           CALL 'AV02C001' USING LINK-REC                                       
           IF WS-RETURN NOT = '000'                                             
              MOVE '00010101' TO KD00-DTE-HSGNT-REJECT-TOPUP                    
           END-IF.                                                              
                                                                                
           ADD 1 TO CNT-FD00-READ.                                              
                                                                                
           IF (KD00-DTE-HSGNT-APPROVE = WS-CURRENT-DATE OR                      
               KD00-DTE-HSGNT-REJECT = WS-CURRENT-DATE OR                       
               KD00-DTE-HSGNT-APPROVE-TOPUP = WS-CURRENT-DATE OR                
               KD00-DTE-HSGNT-REJECT-TOPUP = WS-CURRENT-DATE) OR                
              (KD00-DTE-HSGNT-APPROVE = WS-DTE-YESTERDAY OR                     
               KD00-DTE-HSGNT-REJECT = WS-DTE-YESTERDAY OR                      
               KD00-DTE-HSGNT-APPROVE-TOPUP = WS-DTE-YESTERDAY OR               
               KD00-DTE-HSGNT-REJECT-TOPUP = WS-DTE-YESTERDAY)                  
              WRITE BP13FD00-REC FROM BP13KD00-REC                              
              ADD 1 TO CNT-FD00-WRITE                                           
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13CB0M *************'.                            
           DISPLAY 'NO OF RECS READ FROM  BP13F800  : ' CNT-F800-READ.          
           DISPLAY 'NO OF RECS WRITTEN TO P13F800A  : ' CNT-F800-WRITE.         
           DISPLAY 'NO OF RECS READ FROM  BP13F893  : ' CNT-F893-READ.          
           DISPLAY 'NO OF RECS WRITTEN TO P13F893A  : ' CNT-F893-WRITE.         
           DISPLAY 'NO OF RECS READ FROM  BP13FD00  : ' CNT-FD00-READ.          
           DISPLAY 'NO OF RECS WRITTEN TO P13FD00A  : ' CNT-FD00-WRITE.         
                                                                                
           CLOSE BP13F800                                                       
                 BP13F893                                                       
                 BP13FD00                                                       
                 P13F800A                                                       
                 P13F893A                                                       
                 P13FD00A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
