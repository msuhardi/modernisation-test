       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD87.                                                 
      **************************************************************            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      **************************************************************            
      * OBJECTIVE : MATCH BP13F595 WITH EC OCCUPIER/MASTER         *            
      *             BP18K310/BP18K300.                             *            
      *             IF NRIC MATCHED, PUT TO FOUND FILE             *            
      *             IF NRIC NOT MATCH, PUT TO NOT FOUND FILE       *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY   DESCRIPTION                         *            
      * -------- -------- ---- -----------                         *            
      * BP134247 20110608 JB8   NEW PRGM                           *            
      * BP135635 20150120 JGO1  ADD READING OF K440 AND K450       *            
      **************************************************************            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F595          ASSIGN BP13F595.                            
           SELECT BP18K310          ASSIGN BP18K310                             
                              ORGANIZATION IS INDEXED                           
                               ACCESS MODE IS DYNAMIC                           
                                RECORD KEY IS K310-KEY-FLD                      
                          ALTERNATE RECORD IS K310-NUM-NRIC                     
                               FILE STATUS IS WS-K310-STATUS.                   
           SELECT BP18K300          ASSIGN BP18K300                             
                              ORGANIZATION IS INDEXED                           
                               ACCESS MODE IS RANDOM                            
                                RECORD KEY IS K300-KEY-FLD                      
                               FILE STATUS IS WS-K300-STATUS.                   
           SELECT BP18K450          ASSIGN BP18K450                             
                              ORGANIZATION IS INDEXED                           
                               ACCESS MODE IS DYNAMIC                           
                                RECORD KEY IS K450-KEY-FLD                      
                          ALTERNATE RECORD IS K450-NUM-NRIC                     
                               FILE STATUS IS WS-K450-STATUS.                   
           SELECT BP18K440          ASSIGN BP18K440                             
                              ORGANIZATION IS INDEXED                           
                               ACCESS MODE IS RANDOM                            
                                RECORD KEY IS K440-KEY-FLD                      
                               FILE STATUS IS WS-K440-STATUS.                   
           SELECT P13F595F          ASSIGN P13F595F.                            
           SELECT P13F595N          ASSIGN P13F595N.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F595                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL  RECORDS  ARE STANDARD                                        
            RECORDING MODE IS F.                                                
       COPY BP13F595.                                                           
                                                                                
       FD   BP18K310                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP18K310.                                                           
                                                                                
       FD   BP18K300                                                            
            RECORD CONTAINS 800 CHARACTERS.                                     
       COPY BP18K300.                                                           
                                                                                
       FD   BP18K440                                                            
            RECORD CONTAINS 800 CHARACTERS.                                     
       COPY BP18K440.                                                           
                                                                                
       FD   BP18K450                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP18K450.                                                           
                                                                                
       FD   P13F595F                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL  RECORDS  ARE OMITTED                                         
            RECORDING MODE IS F.                                                
       01   P13F595F-REC            PIC X(500).                                 
                                                                                
       FD   P13F595N                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL  RECORDS  ARE OMITTED                                         
            RECORDING MODE IS F.                                                
       01   P13F595N-REC            PIC X(500).                                 
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FLAGS.                                                            
           05  WS-F595-EOF                  PIC X(01)  VALUE 'N'.               
               88  F595-EOF                            VALUE 'Y'.               
           05  WS-K311-EOF                  PIC X(01)  VALUE 'N'.               
               88  K311-EOF                            VALUE 'Y'.               
           05  WS-K451-EOF                  PIC X(01)  VALUE 'N'.               
               88  K451-EOF                            VALUE 'Y'.               
           05  WS-K310-STATUS               PIC 9(02)  VALUE ZEROES.            
           05  WS-K300-STATUS               PIC 9(02)  VALUE ZEROES.            
           05  WS-K450-STATUS               PIC 9(02)  VALUE ZEROES.            
           05  WS-K440-STATUS               PIC 9(02)  VALUE ZEROES.            
           05  WS-EC-FND                    PIC X(01)  VALUE 'N'.               
               88  EC-FND                              VALUE 'Y'.               
           05  WS-I                         PIC 9(02)  VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  CNT-F595-READ                PIC 9(05)  VALUE ZEROS.             
           05  CNT-F595F-WRITE              PIC 9(05)  VALUE ZEROS.             
           05  CNT-F595N-WRITE              PIC 9(05)  VALUE ZEROS.             
                                                                                
       01  WS-NRIC-LIST.                                                        
           05  WS-NRIC OCCURS 2 TIMES       PIC X(09)  VALUE SPACES.            
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES           THRU  1000-EXIT.                   
           PERFORM 2000-READ-BP13F595        THRU  2000-EXIT.                   
           PERFORM 3000-PROCESS              THRU  3000-EXIT                    
             UNTIL F595-EOF.                                                    
           PERFORM 9000-CLOSE-FILES          THRU  9000-EXIT.                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT BP13F595                                                  
                      BP18K310                                                  
                      BP18K300                                                  
                      BP18K450                                                  
                      BP18K440                                                  
               OUTPUT P13F595F                                                  
                      P13F595N.                                                 
                                                                                
           IF WS-K310-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K310 - OPEN ERROR (' WS-K310-STATUS ')'              
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           IF WS-K300-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K300 - OPEN ERROR (' WS-K300-STATUS ')'              
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F595.                                                      
      *-------------------------------------------------------------            
           READ BP13F595                                                        
             AT END                                                             
                MOVE 'Y'                    TO  WS-F595-EOF                     
            NOT AT END                                                          
                ADD   1                     TO  CNT-F595-READ                   
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS.                                                            
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                   TO WS-EC-FND.                          
                                                                                
           MOVE SPACES                   TO WS-NRIC-LIST.                       
           MOVE F595-NUM-NRIC1           TO WS-NRIC(1).                         
           MOVE F595-NUM-NRIC2           TO WS-NRIC(2).                         
                                                                                
           PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > 2 OR EC-FND            
              IF WS-NRIC(WS-I) NOT = SPACES AND LOW-VALUES                      
                 MOVE WS-NRIC(WS-I)                    TO K310-NUM-NRIC         
                 PERFORM 8001-START-BP18K311         THRU 8001-EXIT             
                 IF NOT K311-EOF                                                
                    PERFORM 8002-READ-NEXT-BP18K311  THRU 8002-EXIT             
                    PERFORM UNTIL K311-EOF OR EC-FND                            
                       IF K310-NUM-NRIC = WS-NRIC(WS-I)                         
                          MOVE K310-NUM-REGN           TO K300-NUM-REGN         
                          PERFORM 8003-READ-BP18K300 THRU 8003-EXIT             
                          IF K300-DTE-OPTION > F595-DTE-BALLOT                  
                             MOVE 'Y'                  TO WS-EC-FND             
                          END-IF                                                
                          PERFORM 8002-READ-NEXT-BP18K311 THRU 8002-EXIT        
                       ELSE                                                     
                          MOVE 'Y'                     TO WS-K311-EOF           
                       END-IF                                                   
                    END-PERFORM                                                 
                 END-IF                                                         
                 IF K311-EOF AND NOT EC-FND                                     
                 MOVE WS-NRIC(WS-I)                    TO K450-NUM-NRIC         
                 PERFORM 8005-START-BP18K451         THRU 8005-EXIT             
                  IF NOT K451-EOF                                               
                    PERFORM 8006-READ-NEXT-BP18K451  THRU 8006-EXIT             
                    PERFORM UNTIL K451-EOF OR EC-FND                            
                       IF K450-NUM-NRIC = WS-NRIC(WS-I)                         
                          MOVE K450-NUM-REGN           TO K440-NUM-REGN         
                          PERFORM 8007-READ-BP18K440 THRU 8007-EXIT             
                          IF K440-DTE-INDVDL-TOP > F595-DTE-BALLOT              
                             MOVE 'Y'                  TO WS-EC-FND             
                          END-IF                                                
                          PERFORM 8006-READ-NEXT-BP18K451 THRU 8006-EXIT        
                       ELSE                                                     
                          MOVE 'Y'                     TO WS-K451-EOF           
                       END-IF                                                   
                    END-PERFORM                                                 
                  END-IF                                                        
                 END-IF                                                         
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           IF EC-FND                                                            
              WRITE P13F595F-REC FROM BP13F595-REC                              
              ADD 1 TO CNT-F595F-WRITE                                          
           ELSE                                                                 
              WRITE P13F595N-REC FROM BP13F595-REC                              
              ADD 1 TO CNT-F595N-WRITE                                          
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F595      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       8001-START-BP18K311.                                                     
      *--------------------------------------------------------*                
                                                                                
           MOVE 'N'              TO WS-K311-EOF                                 
                                                                                
           START BP18K310 KEY >= K310-NUM-NRIC.                                 
                                                                                
           EVALUATE WS-K310-STATUS                                              
               WHEN 00                                                          
                    CONTINUE                                                    
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE 'Y'                   TO WS-K311-EOF                   
               WHEN OTHER                                                       
                    DISPLAY 'BP18K310 - START ERROR(' WS-K310-STATUS ')'        
                    MOVE WS-K310-STATUS        TO RETURN-CODE                   
                    GO TO 9000-CLOSE-FILES                                      
           END-EVALUATE.                                                        
                                                                                
       8001-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       8002-READ-NEXT-BP18K311.                                                 
      *--------------------------------------------------------*                
                                                                                
           READ BP18K310 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y' TO WS-K311-EOF                                         
           END-READ.                                                            
                                                                                
       8002-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       8003-READ-BP18K300.                                                      
      *--------------------------------------------------------*                
                                                                                
           READ BP18K300                                                        
                                                                                
           IF WS-K300-STATUS = 00 OR 02                                         
              IF K300-DTE-OPTION = SPACES OR LOW-VALUES                         
                 MOVE K300-DTE-APLCTN   TO K300-DTE-OPTION                      
              END-IF                                                            
           ELSE                                                                 
              IF WS-K300-STATUS = 23                                            
                 MOVE SPACES            TO K300-DTE-OPTION                      
              ELSE                                                              
                 DISPLAY 'BP18K300 - READ ERROR (' WS-K300-STATUS ')'           
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8003-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       8005-START-BP18K451.                                                     
      *--------------------------------------------------------*                
                                                                                
           MOVE 'N'              TO WS-K451-EOF                                 
                                                                                
           START BP18K450 KEY >= K450-NUM-NRIC.                                 
                                                                                
           EVALUATE WS-K450-STATUS                                              
               WHEN 00                                                          
                    CONTINUE                                                    
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE 'Y'                   TO WS-K451-EOF                   
               WHEN OTHER                                                       
                    DISPLAY 'BP18K450 - START ERROR(' WS-K450-STATUS ')'        
                    MOVE WS-K450-STATUS        TO RETURN-CODE                   
                    GO TO 9000-CLOSE-FILES                                      
           END-EVALUATE.                                                        
                                                                                
       8005-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       8006-READ-NEXT-BP18K451.                                                 
      *--------------------------------------------------------*                
                                                                                
           READ BP18K450 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y' TO WS-K451-EOF                                         
           END-READ.                                                            
                                                                                
       8006-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       8007-READ-BP18K440.                                                      
      *--------------------------------------------------------*                
                                                                                
           READ BP18K440                                                        
                                                                                
           IF WS-K440-STATUS = 00 OR 02                                         
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-K300-STATUS = 23                                            
                 MOVE SPACES            TO K440-DTE-INDVDL-TOP                  
              ELSE                                                              
                 DISPLAY 'BP18K440 - READ ERROR (' WS-K440-STATUS ')'           
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8007-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           DISPLAY SPACES.                                                      
           DISPLAY '------- BP13CD87 ------'.                                   
           DISPLAY ' RECORDS READ    IN BP13F595 : ' CNT-F595-READ.             
           DISPLAY ' RECORDS WRITTEN TO P13F595F : ' CNT-F595F-WRITE.           
           DISPLAY ' RECORDS WRITTEN TO P13F595N : ' CNT-F595N-WRITE.           
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE BP13F595                                                       
                 BP18K300                                                       
                 BP18K310                                                       
                 BP18K440                                                       
                 BP18K450                                                       
                 P13F595F                                                       
                 P13F595N.                                                      
                                                                                
           IF WS-K310-STATUS NOT = 00                                           
              DISPLAY 'BP13K310 - CLOSE ERROR (' WS-K310-STATUS ')'             
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           IF WS-K300-STATUS NOT = 00                                           
              DISPLAY 'BP13K300 - CLOSE ERROR (' WS-K300-STATUS ')'             
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           IF WS-K450-STATUS NOT = 00                                           
              DISPLAY 'BP13K450 - CLOSE ERROR (' WS-K450-STATUS ')'             
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           IF WS-K440-STATUS NOT = 00                                           
              DISPLAY 'BP13K440 - CLOSE ERROR (' WS-K440-STATUS ')'             
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
