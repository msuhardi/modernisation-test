      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13C21S.                                                 
      *AUTHOR.        RACHELLE SAN BUENAVENTURA.                                
      *DATE-WRITTEN.  12 JUNE 2009.                                             
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : TO UPDATE KH30-NUM-ACCEPT                   *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BP13F310 -- TRANSACTION FILE                          *           
      *   2.  BP13K800 -- SOC MASTER FILE                           *           
      *   2.  BP13K060 -- CODE TABLE                                *           
      *                                                             *           
      *   I-O FILES :                                               *           
      *   1.  BP13KH30 -- SUPPLY FILE                               *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP133627 12/06/2009 RB12 NEW PROGRAM                        *           
      * BP133851 26/03/2010 ESA1 TO CATER FOR PAIRED UNITS          *           
      *                          TO ADD READING OF BP13KQ30         *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13KH30 ASSIGN TO BP13KH30                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KH30-KEY-FLD                               
                  FILE STATUS     IS BP13KH30-STATUS.                           
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS BP13K800-STATUS.                           
                                                                                
           SELECT BP13K060 ASSIGN TO BP13K060                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K060-KEY-FLD                               
                  FILE STATUS     IS BP13K060-STATUS.                           
                                                                                
           SELECT BP13KQ30 ASSIGN TO BP13KQ30                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KQ30-KEY-FLD                               
                  ALTERNATE RECORD KEY IS KQ30-ALT-KEY2                         
                  ALTERNATE RECORD KEY IS KQ30-ALT-KEY3                         
                  FILE STATUS     IS BP13KQ30-STATUS.                           
                                                                                
           SELECT BP13F310 ASSIGN TO BP13F310.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13KH30                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13KH30.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS   25 CHARACTERS.                                    
       COPY BP13K060.                                                           
                                                                                
       FD   BP13KQ30                                                            
            RECORD CONTAINS  100 CHARACTERS.                                    
       COPY BP13KQ30.                                                           
                                                                                
       FD   BP13F310                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  150 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F310.                                                           
                                                                                
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  WS-CURR-DATE           PIC X(08) VALUE ZEROES.                       
       01  WS-KH30-ERR-CNT        PIC 9(08) VALUE ZEROES.                       
       01  WS-KH30-FND-SW         PIC X     VALUE SPACES.                       
       01  WS-KH30-READ-ERR       PIC 9(08) VALUE ZEROES.                       
       01  WS-KH30-READ-CNT       PIC 9(08) VALUE ZEROES.                       
       01  WS-KH30-REWRITE-CNT    PIC 9(08) VALUE ZEROES.                       
       01  WS-KH30-REWRITE-ERR    PIC 9(08) VALUE ZEROES.                       
       01  WS-K800-READ-ERR       PIC 9(08) VALUE ZEROES.                       
       01  WS-K800-READ-CNT       PIC 9(08) VALUE ZEROES.                       
       01  WS-K060-READ-ERR       PIC 9(08) VALUE ZEROES.                       
       01  WS-K060-READ-CNT       PIC 9(08) VALUE ZEROES.                       
       01  WS-F310-READ-CNT       PIC 9(08) VALUE ZEROES.                       
       01  WS-F310-EOF            PIC X     VALUE SPACES.                       
       01  WS-FLAG                PIC X     VALUE SPACES.                       
       01  WS-PAIR-FLAG           PIC X     VALUE SPACES.                       
       01  BP13KH30-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13K800-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13K060-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13KQ30-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  WS-START-QUEUE         PIC X(05) VALUE ZEROES.                       
       01  WS-START-QUEUE-NO REDEFINES WS-START-QUEUE.                          
           05  WS-START-QUEUE-NUM PIC 9(05).                                    
       01  WS-END-QUEUE           PIC X(05) VALUE ZEROES.                       
       01  WS-END-QUEUE-NO REDEFINES WS-END-QUEUE.                              
           05  WS-END-QUEUE-NUM   PIC 9(05).                                    
       01  WS-RATE                PIC 9(02)V99 VALUE ZEROES.                    
       01  WS-CTR                 PIC 9(02) VALUE ZEROES.                       
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-F310    THRU 2000-EXIT.                            
           PERFORM 3000-PROCESS-REC  THRU 3000-EXIT                             
                   UNTIL WS-F310-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13F310                                                 
                       BP13K060                                                 
                       BP13K800                                                 
                       BP13KQ30                                                 
                I-O    BP13KH30.                                                
                                                                                
           IF BP13K060-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K060 : ' BP13K060-STATUS             
              MOVE BP13K060-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K800-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K800 : ' BP13K800-STATUS             
              MOVE BP13K800-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13KH30-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KH30 : ' BP13KH30-STATUS             
              MOVE BP13KH30-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13KQ30-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KQ30 : ' BP13KQ30-STATUS             
              MOVE BP13KQ30-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           ACCEPT WS-CURR-DATE FROM DATE YYYYMMDD.                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-F310.                                                          
      *---------------*                                                         
                                                                                
           READ BP13F310 AT END                                                 
                MOVE HIGH-VALUES TO BP13F310-TRANS-REC                          
                MOVE 'Y' TO WS-F310-EOF                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F310-READ-CNT.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           MOVE SPACES       TO BP13K800-MASTER.                                
           INITIALIZE           BP13K800-MASTER.                                
           MOVE F310-REGN-NO TO K800-NUM-REGN.                                  
                                                                                
           PERFORM 3100-READ-BP13K800      THRU 3100-EXIT.                      
                                                                                
           IF K800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES AND ZEROES           
              PERFORM 3200-UPDATE-BP13KH30 THRU 3200-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F310          THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3100-READ-BP13K800.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13K800 KEY IS K800-NUM-REGN.                                  
                                                                                
           IF BP13K800-STATUS = 0                                               
              DISPLAY 'RECORD FOUND KEY IS ' K800-NUM-REGN                      
              ADD   1  TO WS-K800-READ-CNT                                      
           ELSE                                                                 
           IF BP13K800-STATUS = 23                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K800:' K800-NUM-REGN             
              ADD   1     TO WS-K800-READ-ERR                                   
           ELSE                                                                 
           IF BP13K800-STATUS NOT = 0                                           
              MOVE '4'  TO RETURN-CODE                                          
              DISPLAY  'READ ERROR IN BP13K800 ' K800-NUM-REGN                  
              ADD   1   TO WS-K800-READ-ERR                                     
              GO        TO 9000-CLOSE-FILES.                                    
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       3200-UPDATE-BP13KH30.                                                    
      *--------------------*                                                    
                                                                                
           MOVE SPACES                   TO   BP13KH30-REC.                     
           INITIALIZE                         BP13KH30-REC.                     
                                                                                
           MOVE K800-NUM-ALLO-CAT        TO   KH30-NUM-SELECTION-TYPE.          
           MOVE K800-DTE-BALLOT          TO   KH30-DTE-BALLOT.                  
           PERFORM 3300-DECODE-NEW-TOWN  THRU 3300-EXIT.                        
           PERFORM 3400-DECODE-FLAT-TYPE THRU 3400-EXIT.                        
                                                                                
           PERFORM 3500-READ-BP13KH30    THRU 3500-EXIT.                        
           PERFORM 3600-UPD-QUEUE-RANGE  THRU 3600-EXIT.                        
           PERFORM 3700-REWRITE-BP13KH30 THRU 3700-EXIT.                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       3300-DECODE-NEW-TOWN.                                                    
      *--------------------*                                                    
                                                                                
           MOVE SPACES             TO K060-KEY-FLD.                             
           MOVE 02                 TO K060-SERIAL-NO.                           
           MOVE K800-NUM-NEW-TOWN  TO K060-CODE.                                
                                                                                
           READ BP13K060 KEY IS K060-KEY-FLD.                                   
                                                                                
           IF BP13K060-STATUS = 0                                               
              MOVE K060-DESC TO KH30-NUM-NEW-TOWN                               
              ADD   1  TO WS-K060-READ-CNT                                      
           ELSE                                                                 
           IF BP13K060-STATUS = 23                                              
              MOVE SPACES    TO KH30-NUM-NEW-TOWN                               
              DISPLAY 'ESTATE NOT FOUND IN BP13K060:' K060-KEY-FLD              
              ADD   1     TO WS-K060-READ-ERR                                   
              GO          TO 3200-EXIT                                          
           ELSE                                                                 
           IF BP13K060-STATUS NOT = 0                                           
              MOVE '4'  TO RETURN-CODE                                          
              DISPLAY  'READ ERROR IN BP13K060 ' K060-KEY-FLD                   
              ADD   1   TO WS-K060-READ-ERR                                     
              GO        TO 9000-CLOSE-FILES.                                    
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3400-DECODE-FLAT-TYPE.                                                   
      *---------------------*                                                   
                                                                                
           EVALUATE K800-NUM-FLAT-TYPE(1:1)                                     
              WHEN '1'                                                          
                 IF K800-NUM-FLAT-TYPE(2:1) = 'A'                               
                    PERFORM 3800-READ-BP13KQ30   THRU 3800-EXIT                 
                    IF WS-PAIR-FLAG = 'Y'                                       
                       MOVE SPACES                TO BP13K800-MASTER            
                       INITIALIZE                    BP13K800-MASTER            
                       MOVE KQ30-NUM-REGN-PAIR    TO K800-NUM-REGN              
                       PERFORM 3100-READ-BP13K800 THRU 3100-EXIT                
                       IF K800-NUM-FLAT-TYPE = '4 '                             
                          MOVE '4-ROOM-SP' TO KH30-NUM-FLAT-TYPE                
                       ELSE                                                     
                          IF K800-NUM-FLAT-TYPE = '5 '                          
                             MOVE '5-ROOM-SP' TO KH30-NUM-FLAT-TYPE             
                          END-IF                                                
                       END-IF                                                   
                    ELSE                                                        
                       MOVE 'SA       ' TO KH30-NUM-FLAT-TYPE                   
                    END-IF                                                      
                 ELSE                                                           
                    MOVE '1-ROOM   ' TO KH30-NUM-FLAT-TYPE                      
                 END-IF                                                         
              WHEN '2'                                                          
                 IF K800-NUM-FLAT-TYPE(2:1) = 'A'                               
                    PERFORM 3800-READ-BP13KQ30   THRU 3800-EXIT                 
                    IF WS-PAIR-FLAG = 'Y'                                       
                       MOVE SPACES                TO BP13K800-MASTER            
                       INITIALIZE                    BP13K800-MASTER            
                       MOVE KQ30-NUM-REGN-PAIR    TO K800-NUM-REGN              
                       PERFORM 3100-READ-BP13K800 THRU 3100-EXIT                
                       IF K800-NUM-FLAT-TYPE = '4 '                             
                          MOVE '4-ROOM-SP' TO KH30-NUM-FLAT-TYPE                
                       ELSE                                                     
                          IF K800-NUM-FLAT-TYPE = '5 '                          
                             MOVE '5-ROOM-SP' TO KH30-NUM-FLAT-TYPE             
                          END-IF                                                
                       END-IF                                                   
                    ELSE                                                        
                       MOVE 'SA       ' TO KH30-NUM-FLAT-TYPE                   
                    END-IF                                                      
                 ELSE                                                           
                    MOVE '2-ROOM   ' TO KH30-NUM-FLAT-TYPE                      
                 END-IF                                                         
              WHEN '3'                                                          
                 MOVE '3-ROOM   '    TO KH30-NUM-FLAT-TYPE                      
              WHEN '4'                                                          
                 PERFORM 3900-READ-BP13KQ30   THRU 3900-EXIT                    
                 IF WS-PAIR-FLAG = 'Y'                                          
                    MOVE '4-ROOM-SA'    TO KH30-NUM-FLAT-TYPE                   
                 ELSE                                                           
                    MOVE '4-ROOM   '    TO KH30-NUM-FLAT-TYPE                   
                 END-IF                                                         
              WHEN '5'                                                          
                 PERFORM 3900-READ-BP13KQ30   THRU 3900-EXIT                    
                 IF WS-PAIR-FLAG = 'Y'                                          
                    MOVE '5-ROOM-SA'    TO KH30-NUM-FLAT-TYPE                   
                 ELSE                                                           
                    MOVE '5-ROOM   '    TO KH30-NUM-FLAT-TYPE                   
                 END-IF                                                         
              WHEN '6'                                                          
              WHEN 'E'                                                          
                 MOVE 'EXECUTIVE'    TO KH30-NUM-FLAT-TYPE                      
              WHEN OTHER                                                        
                 MOVE SPACES         TO KH30-NUM-FLAT-TYPE                      
                 DISPLAY 'ERROR - FLAT TYPE NOT SUPPORTED: '                    
                                        K800-NUM-FLAT-TYPE                      
                 GO TO 3200-EXIT                                                
           END-EVALUATE.                                                        
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3500-READ-BP13KH30.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13KH30 KEY IS KH30-KEY-FLD.                                   
                                                                                
           IF BP13KH30-STATUS = 0                                               
              ADD   1  TO WS-KH30-READ-CNT                                      
           ELSE                                                                 
           IF BP13KH30-STATUS = 23                                              
              DISPLAY 'RECORD NOT FOUND IN BP13KH30:' KH30-KEY-FLD              
              ADD   1     TO WS-KH30-READ-ERR                                   
              GO          TO 3200-EXIT                                          
           ELSE                                                                 
           IF BP13KH30-STATUS NOT = 0                                           
              MOVE '4'  TO RETURN-CODE                                          
              DISPLAY  'READ ERROR IN BP13KH30 ' KH30-KEY-FLD                   
              ADD   1   TO WS-KH30-READ-ERR                                     
              GO        TO 9000-CLOSE-FILES.                                    
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       3600-UPD-QUEUE-RANGE.                                                    
      *--------------------*                                                    
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1                                   
            UNTIL WS-CTR > 80                                                   
              IF KH30-NUM-SUPPLY-PCT(WS-CTR) > ZEROES                           
                IF  (K800-NUM-NT-FT-QUEUE(2:4) >=                               
                                    KH30-NUM-START-QUEUE(WS-CTR)(2:4))          
                AND (K800-NUM-NT-FT-QUEUE(2:4) <=                               
                                    KH30-NUM-END-QUEUE(WS-CTR)(2:4))            
                   ADD 1 TO KH30-NUM-ACCEPT(WS-CTR)                             
                   MOVE 81 TO WS-CTR                                            
                END-IF                                                          
              ELSE                                                              
                MOVE 81 TO WS-CTR                                               
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       3700-REWRITE-BP13KH30.                                                   
      *--------------------*                                                    
                                                                                
           MOVE 'P13C21S'         TO   KH30-NUM-USERID.                         
           ACCEPT KH30-DTE-UPDATE FROM DATE YYYYMMDD.                           
                                                                                
           REWRITE BP13KH30-REC.                                                
                                                                                
           IF BP13KH30-STATUS = 0                                               
              ADD   1  TO WS-KH30-REWRITE-CNT                                   
           ELSE                                                                 
              MOVE '4'  TO RETURN-CODE                                          
              DISPLAY 'REWRITE ERROR, STATUS : ' BP13KH30-STATUS                
              DISPLAY ' KEY     : ' KH30-KEY-FLD                                
              ADD   1   TO WS-KH30-REWRITE-ERR                                  
              GO        TO 9000-CLOSE-FILES.                                    
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       3800-READ-BP13KQ30.                                                      
      *-----------------------------------------------------------              
           MOVE SPACES                   TO BP13KQ30-REC.                       
           INITIALIZE                       BP13KQ30-REC.                       
           MOVE SPACES                   TO KQ30-ALT-KEY2.                      
           MOVE F310-REGN-NO             TO KQ30-ALT-KEY2.                      
                                                                                
           READ BP13KQ30 KEY IS KQ30-ALT-KEY2.                                  
                                                                                
           EVALUATE BP13KQ30-STATUS                                             
           WHEN 00                                                              
                MOVE  'Y'                TO WS-PAIR-FLAG                        
           WHEN 23                                                              
                MOVE  'N'                TO WS-PAIR-FLAG                        
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13KQ30 : ' BP13KQ30-STATUS               
              MOVE BP13KQ30-STATUS            TO  RETURN-CODE                   
              PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3800-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-----------------------------------------------------------              
       3900-READ-BP13KQ30.                                                      
      *-----------------------------------------------------------              
           MOVE SPACES                   TO BP13KQ30-REC.                       
           INITIALIZE                       BP13KQ30-REC.                       
           MOVE SPACES                   TO KQ30-ALT-KEY3.                      
           MOVE F310-REGN-NO             TO KQ30-ALT-KEY3.                      
                                                                                
           READ BP13KQ30 KEY IS KQ30-ALT-KEY3.                                  
                                                                                
           EVALUATE BP13KQ30-STATUS                                             
           WHEN 00                                                              
                MOVE  'Y'                TO WS-PAIR-FLAG                        
           WHEN 23                                                              
                MOVE  'N'                TO WS-PAIR-FLAG                        
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13KQ30 : ' BP13KQ30-STATUS               
              MOVE BP13KQ30-STATUS            TO  RETURN-CODE                   
              PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3900-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13C21S  *************'.                           
           DISPLAY 'RECS READ    FROM BP13F310  : ' WS-F310-READ-CNT.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS READ    FROM BP13K800  : ' WS-K800-READ-CNT.           
           DISPLAY 'RECS NOTFND    IN BP13K800  : ' WS-K800-READ-ERR.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS READ    FROM BP13K060  : ' WS-K060-READ-CNT.           
           DISPLAY 'RECS NOTFND    IN BP13K060  : ' WS-K060-READ-ERR.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS READ    FROM BP13KH30  : ' WS-KH30-READ-CNT.           
           DISPLAY 'RECS NOTFND    IN BP13KH30  : ' WS-KH30-READ-ERR.           
           DISPLAY 'RECS REWRITTEN IN BP13KH30  : ' WS-KH30-REWRITE-CNT.        
           DISPLAY 'RECS ERROR  WRITE BP13KH30  : ' WS-KH30-REWRITE-ERR.        
                                                                                
           CLOSE BP13F310                                                       
                 BP13K060                                                       
                 BP13K800                                                       
                 BP13KH30                                                       
                 BP13KQ30.                                                      
                                                                                
           IF BP13K060-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13K060-STATUS ' BP13K060-STATUS          
            MOVE BP13K060-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13K800-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13K800-STATUS ' BP13K800-STATUS          
            MOVE BP13K800-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13KH30-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13KH30-STATUS ' BP13KH30-STATUS          
            MOVE BP13KH30-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13KQ30-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13KQ30-STATUS ' BP13KQ30-STATUS          
            MOVE BP13KQ30-STATUS TO RETURN-CODE.                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
