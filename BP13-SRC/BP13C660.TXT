       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C660.                                                 
      *AUTHOR.        LJL.                                                      
      *DATE-WRITTEN.  11/08/1992.                                               
      * ====================================================== *                
      * SYSTEM OF COMMITMENT (SOC)                             *                
      * ====================================================== *                
      *   OBJECTIVE    :                                       *                
      *   A) TO PRINT SUMMARY LIST FOR REFUND VOUCHER FOR      *                
      *   SOCA CASES AFTER VOUCHER WAS PRINTED.                *                
      *   B) TO PRINT SUMMARY LIST FOR REFUND VOUCHER FOR      *                
      *   SA-TP CASES AFTER VOUCHER WAS PRINTED.               *                
      *                                                        *                
      *    INPUT FILES :                                       *                
      *        1. BP13.F650.PREPVRCH  (BP13F650)               *                
      *    REPORT :                                            *                
      *        1. BP13-L660A - 'SUMMARY LIST FOR REFUND VOUCHER*                
      *           FOR SOCA CASES'                              *                
      *        2. BP13-L660B - 'SUMMARY LIST FOR REFUND VOUCHER*                
      *           FOR SA-TP CASES'                             *                
      *                                                        *                
      * ====================================================== *                
      * CHG-NO   DATE   BY  DESCRIPTION                        *                
      * -------- ------ --- -----------                        *                
      * C9207A9  920811 LJL TO PRINT SUMMARY LISTING FOR REFUND*                
      *                     VOUCHER FOR SOCA AND SA-TP CASES.  *                
      * C931147  940304 LJL TO CATER FOR PAYMENT SYSTEM.       *                
      * BP130020 951018 SSS REPOSITION RPT-ID TO ROW 1 COL 1   *                
      * BP130418 980325 SIA CATER FOR Y2000                    *                
      * BP135616 090315 JGO1CHANGE LENGTH OF F650/K650 TO 500  *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION  SECTION.                                                  
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
      *-----------------------*                                                 
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F650  ASSIGN       TO BP13F650.                           
           SELECT BP13L660  ASSIGN       TO BP13L660.                           
                                                                                
       DATA DIVISION.                                                           
      *=============*                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F650            BLOCK  CONTAINS    0 RECORDS                     
                               RECORD CONTAINS  500 CHARACTERS                  
                               LABEL  RECORDS STANDARD.                         
       COPY BP13F650.                                                           
                                                                        00064000
                                                                                
       FD  BP13L660            BLOCK  CONTAINS    0 RECORDS                     
                               RECORD CONTAINS  132 CHARACTERS                  
                               LABEL RECORDS  OMITTED.                          
       01  BP13L660-REC        PIC X(132).                                      
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FLG.                                                              
           05  WS-EOF-F650          PIC X(3)     VALUE 'NO '.                   
               88  END-OF-F650                   VALUE 'YES'.                   
                                                                                
       01  WS-CNT.                                                              
           05  WS-COUNT            PIC 9(5)      VALUE ZERO.                    
           05  WS-CNT-F650         PIC 9(5)      VALUE ZERO.                    
           05  WS-CNT-PROC         PIC 9(5)      VALUE ZERO.                    
           05  WS-CNT-LNE          PIC 9(2)      VALUE ZERO.                    
           05  WS-CNT-PGE          PIC 9(4)      VALUE ZERO.                    
                                                                                
       01  WS-OTHER.                                                            
           05  WS-NUM-GRP-VR-PREV      PIC X(11)     VALUE SPACES.              
           05  WS-BATCH-AMOUNT         PIC 9(6)V99   VALUE ZERO.                
           05  WS-BATCH-AMOUNT-TOTAL   PIC 9(6)V99   VALUE ZERO.                
           05  REGN-NO-TABLE           PIC X(3)      VALUE SPACES.              
           05  WS-REGN-CNT             PIC 9(2)      VALUE ZERO.                
           05  WS-NUM-CASES            PIC 9(2)      VALUE ZERO.                
           05  WS-NUM-CASES-TOTAL      PIC 9(4)      VALUE ZERO.                
                                                                                
       01  WS-DATE                     PIC X(8)      VALUE SPACES.              
                                                                                
       01  WS-L660-AREA.                                                        
           05  WS-HDG1A.                                                        
               10  FILLER          PIC X(8)      VALUE 'P13L660A'.              
               10  FILLER          PIC X(5)      VALUE SPACE.                   
               10  FILLER          PIC X(5)      VALUE 'HCAT3'.                 
               10  FILLER          PIC X(38)     VALUE SPACE.                   
               10  FILLER          PIC X(24)     VALUE                          
                   'HDB SYSTEM OF COMMITMENT'.                                  
               10  FILLER          PIC X(30)     VALUE SPACE.                   
               10  FILLER          PIC X(7)      VALUE 'PAGE : '.               
               10  PRT-PGE         PIC ZZZ9      VALUE ZERO.                    
               10  FILLER          PIC X(6)      VALUE SPACE.                   
           05  WS-HDG2A.                                                        
               10  FILLER          PIC X(48)     VALUE SPACE.                   
               10  FILLER          PIC X(50)     VALUE                          
                   'SUMMARY LISTING FOR REFUND VOUCHER FOR SOCA CASES '.        
               10  FILLER          PIC X(13)     VALUE SPACE.                   
               10  FILLER          PIC X(7)      VALUE 'DATE : '.               
               10  PRT-DTE-CURA    PIC X(10)     VALUE SPACE.                   
           05  WS-HDG3A.                                                        
               10  FILLER          PIC X(10)     VALUE SPACE.                   
               10  FILLER          PIC X(08)     VALUE                          
                   'DATE OF '.                                                  
               10  FILLER          PIC X(36)     VALUE SPACE.                   
               10  FILLER          PIC X(64)     VALUE SPACE.                   
           05  WS-HDG4A.                                                        
               10  FILLER          PIC X(7)      VALUE SPACE.                   
               10  FILLER          PIC X(15)     VALUE                          
                   'AGREEMENT ORDER'.                                           
               10  FILLER          PIC X(6)     VALUE SPACE.                    
               10  FILLER          PIC X(08)     VALUE                          
                   'BATCH NO'.                                                  
               10  FILLER          PIC X(6)     VALUE SPACE.                    
               10  FILLER          PIC X(12)     VALUE                          
                   'NO. OF CASES'.                                              
               10  FILLER          PIC X(6)     VALUE SPACE.                    
               10  FILLER          PIC X(12)     VALUE                          
                   'TOTAL AMOUNT'.                                              
           05  WS-HDG1B.                                                        
               10  FILLER          PIC X(8)      VALUE 'P13L660B'.              
               10  FILLER          PIC X(5)      VALUE SPACE.                   
               10  FILLER          PIC X(5)      VALUE 'HCAT3'.                 
               10  FILLER          PIC X(38)     VALUE SPACE.                   
               10  FILLER          PIC X(24)     VALUE                          
                   'HDB SYSTEM OF COMMITMENT'.                                  
               10  FILLER          PIC X(30)     VALUE SPACE.                   
               10  FILLER          PIC X(7)      VALUE 'PAGE : '.               
               10  PRT-PGEB        PIC ZZZ9      VALUE ZERO.                    
               10  FILLER          PIC X(6)      VALUE SPACE.                   
           05  WS-HDG2B.                                                        
               10  FILLER          PIC X(48)     VALUE SPACE.                   
               10  FILLER          PIC X(50)     VALUE                          
                   'SUMMARY LISTING FOR REFUND VOUCHER FOR SA-TP CASES'.        
               10  FILLER          PIC X(13)     VALUE SPACE.                   
               10  FILLER          PIC X(7)      VALUE 'DATE : '.               
               10  PRT-DTE-CURB    PIC X(10)     VALUE SPACE.                   
           05  WS-HDG3B.                                                        
               10  FILLER          PIC X(10)     VALUE SPACE.                   
               10  FILLER          PIC X(08)     VALUE                          
                   'DATE OF '.                                                  
               10  FILLER          PIC X(36)     VALUE SPACE.                   
               10  FILLER          PIC X(64)     VALUE SPACE.                   
           05  WS-HDG4B.                                                        
               10  FILLER          PIC X(7)      VALUE SPACE.                   
               10  FILLER          PIC X(15)     VALUE                          
                   'SALES ORDER    '.                                           
               10  FILLER          PIC X(6)     VALUE SPACE.                    
               10  FILLER          PIC X(08)     VALUE                          
                   'BATCH NO'.                                                  
               10  FILLER          PIC X(6)     VALUE SPACE.                    
               10  FILLER          PIC X(12)     VALUE                          
                   'NO. OF CASES'.                                              
               10  FILLER          PIC X(6)     VALUE SPACE.                    
               10  FILLER          PIC X(12)     VALUE                          
                   'TOTAL AMOUNT'.                                              
                                                                                
           05  WS-DETAIL-LINE.                                                  
               10  FILLER          PIC X(10)     VALUE SPACE.                   
               10  PRT-DTE-SA      PIC X(8)      VALUE SPACES.                  
               10  FILLER          PIC X(12)     VALUE SPACE.                   
               10  PRT-NUM-GRP-VR  PIC X(11)     VALUE SPACE.                   
               10  FILLER          PIC X(3)     VALUE SPACE.                    
               10  PRT-NUM-CASES   PIC ZZ9       VALUE ZEROS.                   
               10  FILLER          PIC X(6)     VALUE SPACE.                    
               10  FILLER          PIC X(7)      VALUE SPACE.                   
               10  PRT-AMT-BATCH   PIC $Z,ZZZ,ZZ9.99 VALUE ZERO.                
                                                                                
           05  WS-TOTAL-LINE.                                                   
               10  FILLER          PIC X(10)     VALUE SPACE.                   
               10  FILLER          PIC X(8)      VALUE SPACES.                  
               10  FILLER          PIC X(12)     VALUE SPACE.                   
               10  FILLER          PIC X(8)      VALUE 'TOTAL : '.              
               10  FILLER          PIC X(3)      VALUE SPACE.                   
               10  PRT-TOT-CASES   PIC ZZZZZ9    VALUE ZEROS.                   
               10  FILLER          PIC X(6)      VALUE SPACE.                   
               10  FILLER          PIC X(7)      VALUE SPACE.                   
               10  PRT-AMT-TOT     PIC $Z,ZZZ,ZZ9.99 VALUE ZERO.                
                                                                                
       PROCEDURE DIVISION.                                                      
      *==================*                                                      
                                                                                
       000-CONTROL.                                                             
                                                                                
           PERFORM 100-OPEN-FILES      THRU 100-EXIT.                           
           MOVE 50                     TO WS-CNT-LNE.                           
           PERFORM 200-READ-FILES      THRU 200-EXIT.                           
           PERFORM 300-PRINT-SUMMARY   THRU 300-EXIT UNTIL END-OF-F650.         
           PERFORM 350-PRINT-LAST      THRU 350-EXIT.                           
           PERFORM 500-DISPLAY-TOTAL   THRU 500-EXIT.                           
           PERFORM 400-CLOSE-FILES     THRU 400-EXIT.                           
                                                                                
       000-EXIT.                                                                
           STOP RUN.                                                            
                                                                                
       100-OPEN-FILES.                                                          
           OPEN INPUT  BP13F650                                                 
                OUTPUT BP13L660.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           STRING WS-DATE(7:2)  '/'  WS-DATE(5:2)  '/'  WS-DATE(1:4)            
                  DELIMITED BY SIZE INTO PRT-DTE-CURA.                          
           STRING WS-DATE(7:2)  '/'  WS-DATE(5:2)  '/'  WS-DATE(1:4)            
                  DELIMITED BY SIZE INTO PRT-DTE-CURB.                          
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-FILES.                                                          
           READ BP13F650  AT END MOVE 'YES' TO WS-EOF-F650                      
                                 GO         TO 200-EXIT.                        
           ADD 1                            TO WS-CNT-F650.                     
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PRINT-SUMMARY.                                                       
           IF F650-NUM-REGN = SPACES OR LOW-VALUES                              
           PERFORM 200-READ-FILES         THRU 200-EXIT                         
           GO TO 300-EXIT.                                                      
           PERFORM 310-PRINT-LINE         THRU 310-EXIT.                        
           PERFORM 200-READ-FILES         THRU 200-EXIT.                        
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       310-PRINT-LINE.                                                          
           IF  WS-CNT-LNE > 20                                                  
               MOVE 1 TO WS-CNT-LNE                                             
               PERFORM 600-PRINT-HEADING         THRU 600-EXIT                  
           ELSE                                                                 
              NEXT SENTENCE.                                                    
                                                                                
           IF  WS-NUM-GRP-VR-PREV  = SPACES                                     
               ADD 1                   TO WS-NUM-CASES                          
                                          WS-NUM-CASES-TOTAL                    
               ADD F650-AMT-REFUND     TO WS-BATCH-AMOUNT                       
                                          WS-BATCH-AMOUNT-TOTAL                 
               MOVE F650-NUM-GRP-VR              TO WS-NUM-GRP-VR-PREV          
           ELSE                                                                 
           IF  F650-NUM-GRP-VR NOT =  WS-NUM-GRP-VR-PREV                        
               PERFORM 620-PRINT-BATCH-DETAIL    THRU 620-EXIT                  
               ADD 1                   TO WS-NUM-CASES                          
                                          WS-NUM-CASES-TOTAL                    
               ADD F650-AMT-REFUND     TO WS-BATCH-AMOUNT                       
                                          WS-BATCH-AMOUNT-TOTAL                 
               MOVE F650-NUM-GRP-VR              TO WS-NUM-GRP-VR-PREV          
           ELSE                                                                 
               ADD 1                   TO WS-NUM-CASES                          
                                          WS-NUM-CASES-TOTAL                    
               ADD F650-AMT-REFUND     TO WS-BATCH-AMOUNT                       
                                          WS-BATCH-AMOUNT-TOTAL                 
               MOVE F650-NUM-GRP-VR              TO WS-NUM-GRP-VR-PREV.         
                                                                                
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
       350-PRINT-LAST.                                                          
           IF  WS-CNT-LNE > 20                                                  
               MOVE 1                            TO WS-CNT-LNE                  
               PERFORM 600-PRINT-HEADING         THRU 600-EXIT                  
           ELSE                                                                 
              NEXT SENTENCE.                                                    
                                                                                
           PERFORM 700-PRINT-FINAL-TOTAL         THRU 700-EXIT.                 
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-CLOSE-FILES.                                                         
           CLOSE BP13F650                                                       
                 BP13L660.                                                      
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-DISPLAY-TOTAL.                                                       
                                                                                
           DISPLAY  'PROGRAM ID  :  BP13C660'.                                  
           DISPLAY  ' '.                                                        
           DISPLAY                                                              
           'NO OF RECORDS READ FROM BP13F650   = ' WS-CNT-F650.                 
           DISPLAY                                                              
           'NO OF RECORDS PROCESSED & PRINTED  = ' WS-CNT-PROC.                 
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       600-PRINT-HEADING.                                                       
           MOVE    0                 TO   WS-CNT-LNE.                           
           ADD     1                 TO   WS-CNT-PGE.                           
           MOVE    WS-CNT-PGE        TO   PRT-PGE PRT-PGEB.                     
           IF F650-CDE-SALES-PROCESS = 'SA'                                     
           WRITE   BP13L660-REC      FROM WS-HDG1A AFTER PAGE                   
           WRITE   BP13L660-REC      FROM WS-HDG2A AFTER 1                      
           WRITE   BP13L660-REC      FROM WS-HDG3A AFTER 2                      
           WRITE   BP13L660-REC      FROM WS-HDG4A AFTER 2                      
           ELSE                                                                 
           WRITE   BP13L660-REC      FROM WS-HDG1B AFTER PAGE                   
           WRITE   BP13L660-REC      FROM WS-HDG2B AFTER 1                      
           WRITE   BP13L660-REC      FROM WS-HDG3B AFTER 2                      
           WRITE   BP13L660-REC      FROM WS-HDG4B AFTER 2.                     
                                                                                
           MOVE    SPACES            TO   BP13L660-REC.                         
           WRITE   BP13L660-REC      AFTER 1.                                   
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       620-PRINT-BATCH-DETAIL.                                                  
           MOVE  F650-DTE-SALES-AGREEMENT   TO PRT-DTE-SA.                      
           MOVE  WS-BATCH-AMOUNT            TO PRT-AMT-BATCH.                   
           MOVE  WS-NUM-GRP-VR-PREV         TO PRT-NUM-GRP-VR.                  
           MOVE  WS-NUM-CASES               TO PRT-NUM-CASES.                   
           WRITE BP13L660-REC               FROM WS-DETAIL-LINE AFTER 1.        
           ADD 1                            TO WS-CNT-LNE WS-CNT-PROC.          
           MOVE   ZEROS                     TO WS-BATCH-AMOUNT                  
                                               WS-NUM-CASES.                    
                                                                                
       620-EXIT.                                                                
           EXIT.                                                                
                                                                                
       700-PRINT-FINAL-TOTAL.                                                   
           PERFORM 620-PRINT-BATCH-DETAIL   THRU 620-EXIT.                      
           MOVE  WS-BATCH-AMOUNT-TOTAL      TO PRT-AMT-TOT.                     
           MOVE  WS-NUM-CASES-TOTAL         TO PRT-TOT-CASES.                   
           WRITE BP13L660-REC               FROM WS-TOTAL-LINE AFTER 1.         
           ADD 1                            TO WS-CNT-LNE WS-CNT-PROC.          
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
