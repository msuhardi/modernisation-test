      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C40R.                                                 
       AUTHOR.        KOH TECK MING.                                            
       DATE-WRITTEN.  AUG  29,2019.                                             
      *===============================================================*         
      * OBJECTIVE  :  GENERATE A REPORT FOR FTP WITH K800-DTE-SO-APPT *         
      *               ONLY FOR THE PREVIOUS MONTH                     *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :  1. BM06F110                                     *         
      *               2. BM06K100                                     *         
      *               3. BP13K400                                     *         
      *               4. BP13K410                                     *         
      *               5. BP13K800                                     *         
      *               6. BP13K820                                     *         
      *               7. BP13K830                                     *         
      *               8. BP13K857                                     *         
      *               9. BP13K893                                     *         
      *              10. BP13K895                                     *         
      * OUTPUT FILE:  1. BP13REPO                                     *         
      *               2. BP13NOTP                                     *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP137955 29/07/2019 DN8  NEW PROGRAM.                         *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BM06F110  ASSIGN        TO BM06F110.                         
                                                                                
           SELECT  BP13K400  ASSIGN        TO BP13K400                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K400-KEY-FLD                      
                             FILE STATUS   IS WS-K400-STATUS.                   
                                                                                
           SELECT  BP13K410  ASSIGN        TO BP13K410                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K410-KEY-FLD                      
                             FILE STATUS   IS WS-K410-STATUS.                   
                                                                                
           SELECT  BP13K800  ASSIGN        TO BP13K800                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K800-NUM-REGN                     
                             FILE STATUS   IS WS-K800-STATUS.                   
                                                                                
           SELECT  BP13K820  ASSIGN        TO BP13K820                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K820-KEY-FLD                      
                             FILE STATUS   IS WS-K820-STATUS.                   
                                                                                
           SELECT  BP13K830  ASSIGN        TO BP13K830                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K830-KEY-FLD                      
                             ALTERNATE KEY IS K830-NUM-REGN                     
                             FILE STATUS   IS WS-K830-STATUS.                   
                                                                                
           SELECT  BP13K857  ASSIGN        TO BP13K857                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K857-KEY-FLD                      
                             FILE STATUS   IS WS-K857-STATUS.                   
                                                                                
           SELECT  BM06K100  ASSIGN        TO BM06K100                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K100-KEY-FLD                      
                             FILE STATUS   IS WS-K100-STATUS.                   
                                                                                
           SELECT  BP13K893  ASSIGN        TO BP13K893                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K893-KEY-FLD                      
                             FILE STATUS   IS WS-K893-STATUS.                   
                                                                                
           SELECT  BP13K895  ASSIGN        TO BP13K895                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K895-KEY-FLD                      
                             FILE STATUS   IS WS-K895-STATUS.                   
                                                                                
           SELECT  BP13REPO  ASSIGN        TO BP13REPO.                         
           SELECT  BP13NOTP  ASSIGN        TO BP13NOTP.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13K400                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K400.                                                           
                                                                                
       FD   BP13K410                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K410.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400  CHARACTERS.                                    
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250  CHARACTERS.                                    
       COPY BP13K830.                                                           
                                                                                
       FD   BP13K857                                                            
            RECORD CONTAINS 200  CHARACTERS.                                    
       COPY BP13K857.                                                           
                                                                                
       FD   BP13K893                                                            
            RECORD CONTAINS 2050 CHARACTERS.                                    
       COPY BP13K893.                                                           
                                                                                
       FD   BP13K895                                                            
            RECORD CONTAINS 450  CHARACTERS.                                    
       COPY BP13K895.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS 500  CHARACTERS.                                    
       COPY BM06K100.                                                           
                                                                                
       FD   BM06F110                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500  CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BM06F110.                                                           
                                                                                
       FD   BP13REPO                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 700  CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   BP13REPO-REC.                                                       
            05  RPT-SERIAL-NO              PIC ZZZZZZ9.                         
            05  FILLER-1                   PIC X(01).                           
      *     05  RPT-REGN-NUM               PIC X(08).                           
      *     05  FILLER-1A                  PIC X(01).                           
            05  RPT-BTO-PRJ-NAME           PIC X(40).                           
            05  FILLER-2                   PIC X(01).                           
            05  RPT-NUM-BLK                PIC X(05).                           
            05  FILLER-3                   PIC X(01).                           
            05  RPT-HOUSE-NO               PIC X(12).                           
            05  FILLER-4                   PIC X(01).                           
            05  RPT-STREET-NAME            PIC X(32).                           
            05  FILLER-5                   PIC X(01).                           
            05  RPT-FLAT-BUYER-NME1        PIC X(66).                           
            05  FILLER-6                   PIC X(01).                           
            05  RPT-FLAT-BUYER-NME2        PIC X(66).                           
            05  FILLER-7                   PIC X(01).                           
            05  RPT-FLAT-BUYER-NME3        PIC X(66).                           
            05  FILLER-8                   PIC X(01).                           
            05  RPT-FLAT-BUYER-NME4        PIC X(66).                           
            05  FILLER-9                   PIC X(01).                           
            05  RPT-TP-APPT-DATE           PIC X(08).                           
            05  FILLER-10                  PIC X(01).                           
            05  RPT-KEY-COLL-DTE           PIC X(08).                           
            05  FILLER-11                  PIC X(01).                           
            05  RPT-CORR-ADDRESS           PIC X(99).                           
            05  FILLER-12                  PIC X(01).                           
            05  RPT-CORR-EMAIL-ADD         PIC X(50).                           
            05  FILLER-13                  PIC X(01).                           
            05  FILLER                     PIC X(162).                          
                                                                                
       FD   BP13NOTP                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500  CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   BP13NOTP-REC                   PIC X(500).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-K400-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K410-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K800-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K820-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K830-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K857-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K893-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K895-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K100-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-EOF-F110             PIC X(01) VALUE 'N'.                     
           05  WS-EOF-K893             PIC X(01) VALUE 'N'.                     
           05  WS-EOF-K895             PIC X(01) VALUE 'N'.                     
           05  WS-FIRST-REC            PIC X(01) VALUE 'Y'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F110-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-REPO-WRITE       PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-NOTP-WRITE       PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT                  PIC 9(2)  VALUE ZEROES.                  
                                                                                
       01 WS-CURR-DATE.                                                         
          05  WS-CURR-CCYY                 PIC 9(04).                           
          05  WS-CURR-MM                   PIC 9(02).                           
          05  WS-CURR-DD                   PIC 9(02).                           
                                                                                
       01 WS-LAST-MONTH.                                                        
          05  WS-LAST-MM                   PIC 9(02).                           
                                                                                
       01 WS-HEADER.                                                            
            05  HDR-SERIAL-NO       PIC X(07) VALUE 'S.NO '.                    
            05  FILLER              PIC X(01) VALUE ';'.                        
      *     05  HDR-REGN-NUM        PIC X(08) VALUE 'REGN-NUM'.                 
      *     05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-BTO-PRJ-NAME    PIC X(40) VALUE 'PROJECT NAME '.            
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-NUM-BLK         PIC X(05) VALUE 'BLOCK'.                    
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-HOUSE-NO        PIC X(12) VALUE 'UNT-LEVEL '.               
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-STREET-NAME     PIC X(32) VALUE 'STREET NAME'.              
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-FLAT-BUYER-NME1 PIC X(66) VALUE 'FLAT BUYER NAME1'.         
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-FLAT-BUYER-NME2 PIC X(66) VALUE 'FLAT BUYER NAME2'.         
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-FLAT-BUYER-NME3 PIC X(66) VALUE 'FLAT BUYER NAME3'.         
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-FLAT-BUYER-NME4 PIC X(66) VALUE 'FLAT BUYER NAME4'.         
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-TP-APPT-DATE    PIC X(08) VALUE 'APPT DTE'.                 
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-KEY-COLL-DTE    PIC X(08) VALUE 'COLL DTE'.                 
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-CORR-ADDRESS    PIC X(99) VALUE 'CORR ADDRESS'.             
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  HDR-CORR-EMAIL-ADD  PIC X(50) VALUE 'EMAIL ADDRESS'.            
            05  FILLER              PIC X(01) VALUE ';'.                        
            05  FILLER              PIC X(153) VALUE SPACES.                    
                                                                                
       01 WS-NUM-HSEMAIN.                                                       
          05  WS-HASH                      PIC X(01) VALUE '#'.                 
          05  WS-NUM-LEVEL                 PIC X(02) VALUE SPACES.              
          05  WS-HIPHEN                    PIC X(01) VALUE '-'.                 
          05  WS-UNIT-MAIN                 PIC X(04) VALUE SPACES.              
          05  WS-UNIT-SUB                  PIC X(03) VALUE SPACES.              
                                                                                
       01  WS-K820-DTLS.                                                        
           05  WS-FIELD  OCCURS 4 TIMES.                                        
               10 WS-NRIC-K820          PIC X(9)     VALUE SPACES.              
               10 WS-NAME-K820          PIC X(66)    VALUE SPACES.              
                                                                                
       01   WS-DATA-ELEMENTS.                                                   
            05  WS-SERIAL-NUM              PIC 9(07) VALUE ZEROES.              
            05  WS-REGN-NUM                PIC X(08) VALUE ZEROES.              
            05  WS-BTO-PRJ-NAME            PIC X(40) VALUE SPACES.              
            05  WS-NUM-BLK                 PIC X(05) VALUE SPACES.              
            05  WS-HOUSE-NO                PIC X(11) VALUE SPACES.              
            05  WS-STREET-NAME             PIC X(32) VALUE SPACES.              
            05  WS-FLAT-BUYER-NME1         PIC X(66) VALUE SPACES.              
            05  WS-FLAT-BUYER-NME2         PIC X(66) VALUE SPACES.              
            05  WS-FLAT-BUYER-NME3         PIC X(66) VALUE SPACES.              
            05  WS-FLAT-BUYER-NME4         PIC X(66) VALUE SPACES.              
            05  WS-TP-APPT-DATE            PIC X(08) VALUE SPACES.              
            05  WS-KEY-COLL-DTE            PIC X(08) VALUE SPACES.              
            05  WS-CORR-ADDRESS            PIC X(99) VALUE SPACES.              
            05  WS-CORR-EMAIL-ADD          PIC X(50) VALUE SPACES.              
            05  WS-CDE-APPT-TYPE           PIC X(03) VALUE SPACES.              
                                                                                
       01  SQL-CODE                        PIC 9(4).                            
                                                                                
      *---------------------------------------------------------------*         
      *        LINKAGE FOR BP13C916                                   *         
      *---------------------------------------------------------------*         
       01  WS-LINK-REC-C916.                                                    
           05  WS-LINK-BLDNG-GL             PIC X(5).                           
                                                                                
       01  WS-BP13C916-REC.                                                     
           05  C916-NME-LOCAL-PRCNCT        PIC X(40).                          
           05  C916-CDE-SYSERR              PIC S9(3).                          
           05  C916-CDE-ERROR               PIC X(5).                           
                                                                                
      *----------------------------------------------------------------*        
      *        LINKAGE FOR SUB-PROGRAM BP13C913                        *        
      *----------------------------------------------------------------*        
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH         PIC X(4).                            
               10  WS-LINK-NUM-ACC         PIC X(5).                            
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BM06F110      THRU 2000-EXIT                       
             UNTIL WS-EOF-F110 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BM06F110                                                 
                       BM06K100                                                 
                       BP13K400                                                 
                       BP13K410                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BP13K830                                                 
                       BP13K857                                                 
                       BP13K893                                                 
                       BP13K895                                                 
                OUTPUT BP13REPO                                                 
                       BP13NOTP.                                                
                                                                                
                                                                                
           IF WS-K400-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K400 - ERROR OPENING : ' WS-K400-STATUS              
              MOVE WS-K400-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K410-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K410 - ERROR OPENING : ' WS-K410-STATUS              
              MOVE WS-K410-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR OPENING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 - ERROR OPENING : ' WS-K820-STATUS              
              MOVE WS-K820-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K830 - ERROR OPENING : ' WS-K830-STATUS              
              MOVE WS-K830-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K857-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K857 - ERROR OPENING : ' WS-K857-STATUS              
              MOVE WS-K857-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K893-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K893 - ERROR OPENING : ' WS-K893-STATUS              
              MOVE WS-K893-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K895-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K895 - ERROR OPENING : ' WS-K895-STATUS              
              MOVE WS-K895-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K100 - ERROR OPENING : ' WS-K100-STATUS              
              MOVE WS-K100-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-FIRST-REC = 'Y'                                                
              WRITE BP13REPO-REC FROM  WS-HEADER                                
              MOVE 'N' TO WS-FIRST-REC                                          
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE.                          
           IF WS-CURR-MM = 1                                                    
              MOVE 12 TO WS-LAST-MM                                             
           ELSE                                                                 
              COMPUTE WS-LAST-MM = WS-CURR-MM - 1                               
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BM06F110.                                                      
      ******************************************************************        
                                                                                
           READ BM06F110           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F110                             
                GO                 TO   2000-EXIT.                              
           ADD  1                  TO WS-F110-READ.                             
                                                                                
           IF F110-REC       NOT = SPACES AND LOW-VALUES                        
              IF (F110-REGN-NO   NOT = SPACES AND LOW-VALUES ) AND              
                 (F110-REGN-NO(1:2) NOT = '##')                                 
                  PERFORM 3000-READ-INPUT-FILES      THRU 3000-EXIT             
             ELSE                                                               
                 WRITE BP13NOTP-REC        FROM F110-REC                        
                 ADD 1                       TO WS-CNT-NOTP-WRITE               
             END-IF                                                             
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       3000-READ-INPUT-FILES.                                                   
      *************************************************************             
                                                                                
           PERFORM 3100-READ-BP13K800  THRU 3100-EXIT.                          
           PERFORM 3300-READ-BP13K400  THRU 3300-EXIT.                          
                                                                                
           IF F110-REC NOT = SPACES AND LOW-VALUES                              
              PERFORM 3400-READ-BM06K100  THRU 3400-EXIT                        
           END-IF.                                                              
                                                                                
           PERFORM 3500-READ-BP13K830  THRU 3500-EXIT                           
           PERFORM 3600-READ-BP13K857  THRU 3600-EXIT                           
                                                                                
           IF F110-SCH-ACC-NO NOT = SPACES AND LOW-VALUES                       
               MOVE  SPACES                  TO  BP13COMM8-REC                  
               INITIALIZE                        BP13COMM8-REC                  
               MOVE  F110-SCH-ACC-NO(1:9)    TO  WS-LINK-REC                    
               PERFORM 3800-CALL-BP13C913  THRU 3800-EXIT                       
               IF COMM8-CDE-SYSERR = 0                                          
                  PERFORM 3850-MOVE-PIDB-DETLS THRU 3850-EXIT                   
               ELSE                                                             
                  CONTINUE                                                      
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 4100-MOVE-FIELDS    THRU 4100-EXIT.                          
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3100-READ-BP13K800.                                                      
      *************************************************************             
           MOVE SPACES                   TO BP13K800-MASTER.                    
           INITIALIZE                       BP13K800-MASTER.                    
                                                                                
           MOVE F110-REGN-NO             TO K800-NUM-REGN.                      
           MOVE F110-REGN-NO             TO WS-REGN-NUM.                        
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    MOVE K800-DTE-SO-APPT      TO WS-TP-APPT-DATE               
                    MOVE K800-CDE-APPT-TYPE    TO WS-CDE-APPT-TYPE              
                    MOVE K800-NUM-NRIC1        TO WS-NRIC-K820(1)               
                    MOVE K800-NUM-NRIC2        TO WS-NRIC-K820(2)               
                    MOVE K800-NUM-NRIC3        TO WS-NRIC-K820(3)               
                    MOVE K800-NUM-NRIC4        TO WS-NRIC-K820(4)               
                    PERFORM 3200-READ-BP13K820  THRU 3200-EXIT                  
                          VARYING WS-CNT FROM 1 BY 1 UNTIL WS-CNT > 4           
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES          TO BP13K800-MASTER                     
                    INITIALIZE              BP13K800-MASTER                     
                    DISPLAY 'REC NOT FOUND IN K800' F110-REGN-NO                
                    PERFORM 3150-READ-BP13K893 THRU 3150-EXIT                   
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 READ ERROR ' WS-K800-STATUS               
                    MOVE WS-K800-STATUS          TO RETURN-CODE                 
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3150-READ-BP13K893.                                                      
      *************************************************************             
           MOVE SPACES                   TO BP13K893-MASTER.                    
           INITIALIZE                       BP13K893-MASTER.                    
                                                                                
           MOVE F110-REGN-NO             TO K893-KEY-FLD.                       
                                                                                
           START  BP13K893 KEY  >= K893-KEY-FLD                                 
                                                                                
           EVALUATE WS-K893-STATUS                                              
               WHEN 00                                                          
                 READ BP13K893 NEXT RECORD                                      
                  AT END MOVE 'Y'    TO WS-EOF-K893                             
                  NOT AT END                                                    
                                                                                
                  IF WS-K893-STATUS NOT = '00'                                  
                     MOVE SPACES               TO WS-TP-APPT-DATE               
                     MOVE WS-K893-STATUS       TO RETURN-CODE                   
                     DISPLAY 'READ ERROR IN K893 ' F110-REGN-NO                 
                     PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                  
                  END-IF                                                        
                                                                                
                  IF K893-NUM-REGN-HIST = F110-REGN-NO                          
                     MOVE K893-DTE-SO-APPT      TO WS-TP-APPT-DATE              
                     MOVE K893-CDE-APPT-TYPE    TO WS-CDE-APPT-TYPE             
                     MOVE K893-NUM-NRIC1        TO WS-NRIC-K820(1)              
                     MOVE K893-NUM-NRIC2        TO WS-NRIC-K820(2)              
                     MOVE K893-NUM-NRIC3        TO WS-NRIC-K820(3)              
                     MOVE K893-NUM-NRIC4        TO WS-NRIC-K820(4)              
                     PERFORM 3200-READ-BP13K820  THRU 3200-EXIT                 
                          VARYING WS-CNT FROM 1 BY 1 UNTIL WS-CNT > 4           
                     MOVE 'Y' TO WS-EOF-K893                                    
                  ELSE                                                          
                     MOVE SPACES                TO WS-TP-APPT-DATE              
                     DISPLAY 'REGN NOT FND IN K893 ' F110-REGN-NO               
                  END-IF                                                        
                 END-READ                                                       
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES          TO BP13K893-MASTER                     
                    INITIALIZE              BP13K893-MASTER                     
                    MOVE SPACES          TO WS-TP-APPT-DATE                     
                    DISPLAY 'REC NOT FOUND IN K893' F110-REGN-NO                
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K893 READ ERROR ' WS-K893-STATUS               
                    MOVE WS-K893-STATUS          TO RETURN-CODE                 
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3150-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3200-READ-BP13K820.                                                      
      *************************************************************             
           IF  WS-NRIC-K820(WS-CNT) NOT = SPACES AND LOW-VALUES                 
               MOVE SPACES                   TO BP13K820-REC                    
               INITIALIZE                       BP13K820-REC                    
               MOVE F110-REGN-NO             TO K820-NUM-REGN                   
               MOVE WS-NRIC-K820(WS-CNT)     TO K820-NUM-NRIC                   
            ELSE                                                                
               MOVE  SPACES                  TO WS-NAME-K820(WS-CNT)            
               GO TO 3200-EXIT                                                  
           END-IF.                                                              
                                                                                
           READ BP13K820 KEY IS K820-KEY-FLD.                                   
           EVALUATE WS-K820-STATUS                                              
             WHEN '00'                                                          
               IF K820-NME-OCCP   NOT = SPACES AND LOW-VALUES                   
                  MOVE  K820-NME-OCCP   TO WS-NAME-K820(WS-CNT)                 
               ELSE                                                             
                  MOVE  SPACES          TO WS-NAME-K820(WS-CNT)                 
               END-IF                                                           
                                                                                
             WHEN 10                                                            
             WHEN 23                                                            
                  MOVE SPACES           TO BP13K820-REC                         
                  INITIALIZE               BP13K820-REC                         
                  DISPLAY 'REC NOT FOUND IN K820' K820-KEY-FLD                  
                  PERFORM 3250-READ-BP13K895 THRU 3250-EXIT                     
                                                                                
             WHEN OTHER                                                         
                  DISPLAY 'BP13K820 READ ERROR ' WS-K820-STATUS                 
                  MOVE WS-K820-STATUS          TO RETURN-CODE                   
                  PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3250-READ-BP13K895.                                                      
      *************************************************************             
           MOVE SPACES                   TO BP13K895-REC.                       
           INITIALIZE                       BP13K895-REC.                       
                                                                                
           MOVE F110-REGN-NO             TO K895-NUM-REGN-HIST.                 
           MOVE WS-NRIC-K820(WS-CNT)     TO K895-NUM-NRIC-HIST.                 
                                                                                
           START  BP13K895 KEY  >= K895-KEY-FLD                                 
                                                                                
           EVALUATE WS-K895-STATUS                                              
               WHEN 00                                                          
                 READ BP13K895 NEXT RECORD                                      
                  AT END MOVE 'Y'    TO WS-EOF-K895                             
                  NOT AT END                                                    
                                                                                
                  IF WS-K895-STATUS NOT = '00'                                  
                     MOVE SPACES               TO WS-NAME-K820(WS-CNT)          
                     MOVE WS-K895-STATUS       TO RETURN-CODE                   
                     DISPLAY 'READ ERROR IN K895' F110-REGN-NO                  
                     PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                  
                  END-IF                                                        
                                                                                
                  IF K895-NUM-REGN-HIST = F110-REGN-NO AND                      
                     K895-NUM-NRIC-HIST = WS-NRIC-K820(WS-CNT)                  
                      MOVE  K895-NME-OCCP   TO WS-NAME-K820(WS-CNT)             
                      MOVE 'Y' TO WS-EOF-K895                                   
                  ELSE                                                          
                      MOVE  SPACES          TO WS-NAME-K820(WS-CNT)             
                      DISPLAY 'REC NOT FOUND IN K895 ' F110-REGN-NO ';'         
                                                  WS-NRIC-K820(WS-CNT)          
                  END-IF                                                        
                 END-READ                                                       
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES          TO BP13K895-REC                        
                    INITIALIZE              BP13K895-REC                        
                    MOVE SPACES          TO WS-NAME-K820(WS-CNT)                
                    DISPLAY 'REC NOT FOUND IN K895' F110-REGN-NO ' ; '          
                                                    WS-NRIC-K820(WS-CNT)        
               WHEN OTHER                                                       
                    DISPLAY 'BP13K895 READ ERROR ' WS-K895-STATUS               
                    MOVE WS-K895-STATUS          TO RETURN-CODE                 
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3250-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3300-READ-BP13K400.                                                      
      *************************************************************             
           MOVE SPACES                   TO BP13K400-REC.                       
           INITIALIZE                       BP13K400-REC.                       
                                                                                
           MOVE F110-REGN-NO             TO K400-KEY-FLD.                       
                                                                                
           READ BP13K400.                                                       
                                                                                
           EVALUATE WS-K400-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    MOVE K410-DTE-KEY-ISSUE   TO WS-KEY-COLL-DTE                
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES          TO BP13K410-REC                        
                    INITIALIZE              BP13K410-REC                        
                    PERFORM 3350-READ-BP13K410 THRU 3350-EXIT                   
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K400 READ ERROR ' WS-K400-STATUS               
                    MOVE WS-K400-STATUS          TO RETURN-CODE                 
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3350-READ-BP13K410.                                                      
      *************************************************************             
           MOVE SPACES                   TO BP13K410-REC.                       
           INITIALIZE                       BP13K410-REC.                       
                                                                                
           MOVE F110-REGN-NO             TO K410-KEY-FLD.                       
                                                                                
           READ BP13K410.                                                       
                                                                                
           EVALUATE WS-K410-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    MOVE K410-DTE-KEY-ISSUE   TO WS-KEY-COLL-DTE                
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES               TO BP13K410-REC                   
                    INITIALIZE                   BP13K410-REC                   
                    MOVE SPACES               TO WS-KEY-COLL-DTE                
                    DISPLAY 'REG NOT PRESENT IN K410 ' K410-NUM-REGN            
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K410 READ ERROR ' WS-K410-STATUS               
                    MOVE WS-K410-STATUS          TO RETURN-CODE                 
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3350-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3400-READ-BM06K100.                                                      
      *************************************************************             
           MOVE SPACES                   TO K100-REC.                           
           INITIALIZE                       K100-REC.                           
                                                                                
           MOVE F110-AIX1                TO K100-KEY-FLD.                       
                                                                                
           READ BM06K100.                                                       
                                                                                
           EVALUATE WS-K100-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    MOVE K100-CDE-BLK TO WS-LINK-BLDNG-GL                       
                    CALL 'BP13C916' USING WS-LINK-REC-C916                      
                                          WS-BP13C916-REC                       
                    MOVE C916-NME-LOCAL-PRCNCT TO WS-BTO-PRJ-NAME               
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES          TO K100-REC                            
                    INITIALIZE              K100-REC                            
                    MOVE SPACES          TO WS-BTO-PRJ-NAME                     
                    DISPLAY 'REC NOT FOUND IN K100' K100-KEY-FLD                
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BM06K100 READ ERROR ' WS-K100-STATUS               
                    MOVE WS-K100-STATUS          TO RETURN-CODE                 
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3500-READ-BP13K830.                                                      
      *************************************************************             
           MOVE SPACES                   TO BP13K830-REC.                       
           INITIALIZE                       BP13K830-REC.                       
                                                                                
           MOVE F110-REGN-NO             TO K830-NUM-REGN.                      
                                                                                
           READ BP13K830 KEY IS K830-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K830-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                                                                                
                IF K830-NUM-MAIL-ADDR NOT = SPACES AND LOW-VALUES               
                   MOVE K830-NUM-MAIL-ADDR(2:98) TO WS-CORR-ADDRESS             
                ELSE                                                            
                 IF K830-NUM-RESIDENT-ADDR NOT = SPACES AND LOW-VALUES          
                   MOVE K830-NUM-RESIDENT-ADDR(2:98) TO WS-CORR-ADDRESS         
                   ELSE                                                         
                      MOVE SPACES                 TO WS-CORR-ADDRESS            
                  END-IF                                                        
                 END-IF                                                         
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES          TO BP13K830-REC                        
                    INITIALIZE              BP13K830-REC                        
                    MOVE SPACES          TO WS-CORR-ADDRESS                     
                    DISPLAY 'REC NOT FOUND IN K830' K830-KEY-FLD ' ; '          
                                                    K830-NUM-REGN               
               WHEN OTHER                                                       
                    DISPLAY 'BP13K830 READ ERROR ' WS-K830-STATUS               
                    MOVE WS-K830-STATUS          TO RETURN-CODE                 
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3600-READ-BP13K857.                                                      
      *************************************************************             
           MOVE SPACES                   TO BP13K857-REC.                       
           INITIALIZE                       BP13K857-REC.                       
                                                                                
           MOVE F110-REGN-NO             TO K857-KEY-FLD.                       
                                                                                
           READ BP13K857.                                                       
                                                                                
           EVALUATE WS-K857-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                                                                                
                 IF K857-NUM-EMAIL NOT = SPACES AND LOW-VALUES                  
                    MOVE K857-NUM-EMAIL      TO WS-CORR-EMAIL-ADD               
                 ELSE                                                           
                    MOVE SPACES              TO WS-CORR-EMAIL-ADD               
                 END-IF                                                         
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES          TO BP13K857-REC                        
                    INITIALIZE              BP13K857-REC                        
                    MOVE SPACES          TO WS-CORR-EMAIL-ADD                   
                    DISPLAY 'REC NOT FOUND IN K857 ' K857-KEY-FLD               
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K857 READ ERROR ' WS-K857-STATUS               
                    MOVE WS-K857-STATUS          TO RETURN-CODE                 
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3600-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3800-CALL-BP13C913.                                                      
      ******************************************************************        
            CALL 'BP13C913' USING WS-LINK-REC,                                  
                                  BP13COMM8-REC.                                
       3800-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3850-MOVE-PIDB-DETLS.                                                    
      ******************************************************************        
                                                                                
           MOVE COMM8-NUM-LEVEL       TO  WS-NUM-LEVEL.                         
           MOVE COMM8-NUM-UNIT-MAIN   TO  WS-UNIT-MAIN.                         
           MOVE COMM8-NUM-UNIT-SUB    TO  WS-UNIT-SUB.                          
           MOVE COMM8-NUM-BLK         TO  WS-NUM-BLK.                           
           MOVE COMM8-NME-STREET      TO  WS-STREET-NAME.                       
           MOVE WS-NUM-HSEMAIN        TO  WS-HOUSE-NO.                          
                                                                                
       3850-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       4100-MOVE-FIELDS.                                                        
      ******************************************************************        
           MOVE SPACES                  TO BP13REPO-REC.                        
           INITIALIZE                      BP13REPO-REC.                        
                                                                                
           MOVE SPACES                  TO BP13NOTP-REC.                        
           INITIALIZE                      BP13NOTP-REC.                        
                                                                                
           MOVE  ';' TO FILLER-1  FILLER-2  FILLER-3 FILLER-4  FILLER-5         
                        FILLER-6  FILLER-7  FILLER-8 FILLER-9  FILLER-10        
                        FILLER-11 FILLER-12 FILLER-13.                          
                                                                                
      *    MOVE  WS-REGN-NUM           TO RPT-REGN-NUM                          
           MOVE  WS-BTO-PRJ-NAME       TO RPT-BTO-PRJ-NAME                      
           MOVE  WS-NUM-BLK            TO RPT-NUM-BLK                           
           MOVE  WS-HOUSE-NO           TO RPT-HOUSE-NO                          
           MOVE  WS-STREET-NAME        TO RPT-STREET-NAME                       
           MOVE  WS-NAME-K820(1)       TO RPT-FLAT-BUYER-NME1                   
           MOVE  WS-NAME-K820(2)       TO RPT-FLAT-BUYER-NME2                   
           MOVE  WS-NAME-K820(3)       TO RPT-FLAT-BUYER-NME3                   
           MOVE  WS-NAME-K820(4)       TO RPT-FLAT-BUYER-NME4                   
           MOVE  WS-TP-APPT-DATE       TO RPT-TP-APPT-DATE                      
           MOVE  WS-KEY-COLL-DTE       TO RPT-KEY-COLL-DTE                      
           MOVE  WS-CORR-ADDRESS       TO RPT-CORR-ADDRESS                      
           MOVE  WS-CORR-EMAIL-ADD     TO RPT-CORR-EMAIL-ADD                    
                                                                                
           IF (WS-TP-APPT-DATE NOT = SPACES AND LOW-VALUES) AND                 
              (WS-CDE-APPT-TYPE  = 'TP')                                        
            IF WS-TP-APPT-DATE(5:2) = WS-LAST-MONTH                             
              ADD 1                       TO WS-CNT-REPO-WRITE                  
              MOVE WS-CNT-REPO-WRITE      TO RPT-SERIAL-NO                      
              WRITE BP13REPO-REC                                                
            END-IF                                                              
           ELSE                                                                 
              WRITE BP13NOTP-REC        FROM F110-REC                           
              ADD 1                       TO WS-CNT-NOTP-WRITE                  
           END-IF.                                                              
                                                                                
           INITIALIZE                        WS-DATA-ELEMENTS.                  
           INITIALIZE                        WS-K820-DTLS.                      
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C40R'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BM06F110 RECORDS READ............. : '            
                    WS-F110-READ.                                               
           DISPLAY '(2) NO OF BP13REPO(WITH TP APPT DATE) WRITTEN: '            
                    WS-CNT-REPO-WRITE.                                          
           DISPLAY '(3) NO OF BP13NOTP(NO TP APPT DATE) WRITTEN  : '            
                    WS-CNT-NOTP-WRITE.                                          
                                                                                
           CLOSE    BM06F110                                                    
                    BM06K100                                                    
                    BP13K400                                                    
                    BP13K410                                                    
                    BP13K800                                                    
                    BP13K820                                                    
                    BP13K830                                                    
                    BP13K857                                                    
                    BP13K893                                                    
                    BP13K895                                                    
                    BP13REPO                                                    
                    BP13NOTP.                                                   
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K100 - ERROR CLOSING : ' WS-K100-STATUS              
              MOVE WS-K100-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K400-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K400 - ERROR CLOSING : ' WS-K400-STATUS              
              MOVE WS-K400-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K410-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K410 - ERROR CLOSING : ' WS-K410-STATUS              
              MOVE WS-K410-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR CLOSING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 - ERROR CLOSING : ' WS-K820-STATUS              
              MOVE WS-K820-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K830 - ERROR CLOSING : ' WS-K830-STATUS              
              MOVE WS-K830-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K857-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K857 - ERROR CLOSING : ' WS-K857-STATUS              
              MOVE WS-K857-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K893-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K893 - ERROR CLOSING : ' WS-K893-STATUS              
              MOVE WS-K893-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K895-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K895 - ERROR CLOSING : ' WS-K895-STATUS              
              MOVE WS-K895-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
