       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C41K.                                                 
      *AUTHOR.        RJB1.                                                     
      *DATE-WRITTEN.  DEC 26 2017.                                              
      * ============================================================            
      * SYSTEM OF COMMITMENT (SOC)                                              
      * ============================================================            
      * OBJECTIVE  - TO MATCH BP13F800 WITH BP13K410.                           
      *            - RECORDS NOT FOUND IN BP13K410 ARE LISTED INTO AN           
      *              OUPUT.                                                     
      *            - THE RESULT WILL BE USED AS INPUT IN JCL BP13M130           
      *              STEP13.                                                    
      *                                                                         
      * INPUT FILES : BP13F800                                                  
      *               BP13K410                                                  
      *                                                                         
      * OUTPUT FILES: P13F800A                                                  
      * ------------------------------------------------------------            
      * ------------------------------------------------------------            
      * REF NO    DATE  BY   DESCRIPTIONS                                       
      * -------- ------ ---  ------------                                       
      * BP136254 261217 RJB1 NEW PGM                                            
      * ============================================================            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K410 ASSIGN        TO BP13K410                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS DYNAMIC                             
                           RECORD KEY    IS K410-KEY-FLD                        
                           FILE STATUS   IS K410-STATUS.                        
                                                                                
           SELECT BP13F800 ASSIGN        TO BP13F800.                           
           SELECT P13F800A ASSIGN        TO P13F800A.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F800           RECORDING MODE IS F                              
                               RECORD CONTAINS 2000 CHARACTERS                  
                               BLOCK  CONTAINS 0   RECORDS.                     
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K410           RECORD CONTAINS 2000 CHARACTERS.                 
       COPY BP13K410.                                                           
                                                                                
       FD   P13F800A           RECORDING MODE IS F                              
                               RECORD CONTAINS 2000 CHARACTERS                  
                               BLOCK  CONTAINS 0   RECORDS.                     
       01   F800-RECORD        PIC X(2000).                                     
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  K410-STATUS                   PIC 99    VALUE 0.                     
                                                                                
       01  WS-F800-EOF                   PIC X     VALUE 'N'.                   
       01  WS-K410-FND                   PIC X     VALUE 'N'.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F800-READ               PIC 9(7)  VALUE 0.                     
           05 WS-FOUND                   PIC 9(7)  VALUE 0.                     
           05 WS-NOT-FND                 PIC 9(7)  VALUE 0.                     
           05 WS-F800-WRITE              PIC 9(7)  VALUE 0.                     
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-CONTROL.                                                            
      *-------------------------------------------------------------            
           PERFORM 1000-START-UP         THRU 1000-EXIT.                        
           PERFORM 2000-MAIN-ROUTINE     THRU 2000-EXIT                         
                   UNTIL WS-F800-EOF = 'Y'.                                     
           PERFORM 9999-WRAP-UP.                                                
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       1000-START-UP.                                                           
      *-------------------------------------------------------------            
            OPEN INPUT  BP13F800                                                
                        BP13K410                                                
                 OUTPUT P13F800A.                                               
                                                                                
            IF K410-STATUS NOT = 00 AND 97                                      
               DISPLAY 'OPEN FAILED FOR BP13K410,STATUS ' K410-STATUS           
               MOVE K410-STATUS TO RETURN-CODE                                  
               PERFORM 9999-WRAP-UP THRU 9999-EXIT                              
            END-IF.                                                             
                                                                                
            READ BP13F800                                                       
                 AT END  DISPLAY 'NO RECORDS FOUND!'                            
                         MOVE 'Y'          TO WS-F800-EOF                       
                         GO TO 1000-EXIT                                        
            END-READ.                                                           
                                                                                
            ADD  1                         TO WS-F800-READ.                     
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       2000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
      *-------------------------------------------------------------*           
      * READS BP13F800 AND PROCESS EACH RECORD UNTIL END OF FILE    *           
      *-------------------------------------------------------------*           
                                                                                
           PERFORM 3000-READ-K410        THRU 3000-EXIT.                        
      *-------------------------------------------------------------*           
      * TO OUPUT CASES THAT ARE NOT FOUND IN BP13K410               *           
      *-------------------------------------------------------------*           
           IF WS-K410-FND = 'N'                                                 
              MOVE SPACES                  TO F800-RECORD                       
              INITIALIZE                      F800-RECORD                       
                                                                                
              WRITE F800-RECORD          FROM BP13F800-MASTER                   
              ADD 1                        TO WS-F800-WRITE                     
           END-IF.                                                              
                                                                                
           READ BP13F800 AT END                                                 
                MOVE 'Y' TO WS-F800-EOF                                         
                GO TO 2000-EXIT                                                 
                END-READ.                                                       
                                                                                
           ADD  1                          TO WS-F800-READ.                     
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3000-READ-K410.                                                          
      *-------------------------------------------------------------            
           MOVE 'N'                       TO WS-K410-FND.                       
           MOVE SPACES                    TO K410-KEY-FLD.                      
           MOVE F800-NUM-REGN             TO K410-NUM-REGN.                     
                                                                                
           READ BP13K410.                                                       
                                                                                
           EVALUATE K410-STATUS                                                 
           WHEN 00                                                              
              MOVE 'Y'                    TO WS-K410-FND                        
              ADD 1                       TO WS-FOUND                           
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                    TO WS-K410-FND                        
              ADD 1                       TO WS-NOT-FND                         
           WHEN OTHER                                                           
              DISPLAY 'READ ERROR BP13K410 - ' K410-STATUS                      
                      ', KEY IS ' K410-KEY-FLD                                  
              PERFORM 9999-WRAP-UP      THRU 9999-EXIT                          
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       9999-WRAP-UP.                                                            
      *-------------------------------------------------------------            
           DISPLAY ' '.                                                         
           DISPLAY '- BP13C41K CONTROL TOTAL -------------'.                    
           DISPLAY ' TOTAL BP13F800 REC READ   : ' WS-F800-READ.                
           DISPLAY ' TOTAL BP13K410 REC FOUND  : ' WS-FOUND.                    
           DISPLAY ' TOTAL BP13K410 REC NOT FND: ' WS-NOT-FND.                  
           DISPLAY ' TOTAL BP13F800 OUTPUT     : ' WS-F800-WRITE.               
                                                                                
           CLOSE BP13K410                                                       
                 BP13F800                                                       
                 P13F800A.                                                      
                                                                                
           IF K410-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE FAILED FOR BP13K410 ' K410-STATUS                  
              MOVE K410-STATUS TO RETURN-CODE.                                  
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
