       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C292.                                                 
      *AUTHOR.        JIJO FRANCIS.                                             
      *DATE-WRITTEN.  18/11/2002.                                               
      * ========================================================== *            
      * SYSTEM NAME (BP13)                                         *            
      * ========================================================== *            
      * OBJECTIVE   :                                              *            
      *      TO MATCH BP13F205 WITH BM06F800 BY ZONE,FT OR NT,FT   *            
      *         AND DTE-ALLOCN AND WRITE TO BM06K950.              *            
      *                                                            *            
      * INPUT FILES  :  BP13F205  -  CONTROL RECORDS FILE          *            
      *                 BM06F800  -  FLATLIST FILE                 *            
      * OUTPUT FILE  :  BM06K950  -  FIS HISTORY FILE              *            
      *                                                            *            
      * CHG-NO   BY  DATE     - DESCRIPTION                        *            
      * -----------------------------------                        *            
      * BP132221 JF4 20021118 - NEW PGM                            *            
      * BP132278 JF4 20030416 - CHG BM06F800 LRECL FROM 600 TO 800 *            
      * BP134731 PCL 20121109 - TO CATER FOR MODL-DESIGN 89        *            
      * BP135838 ESA120150701 - TO CATER FOR MODL-DESIGN 92        *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205 ASSIGN TO BP13F205                                   
                           ORGANIZATION IS SEQUENTIAL.                          
                                                                                
           SELECT BM06F800 ASSIGN TO BM06F800                                   
                           ORGANIZATION IS SEQUENTIAL.                          
                                                                                
           SELECT BM06K950 ASSIGN TO BM06K950                                   
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE IS RANDOM                                
                           RECORD KEY IS K950-KEY-FLD                           
                           FILE STATUS IS K950-STATUS.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *---------------------------------------------------------------*         
       FD  BP13F205                                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           RECORDING MODE IS F.                                                 
       COPY BP13F205.                                                           
                                                                                
       FD  BM06F800                                                             
           RECORD CONTAINS 800 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       COPY BM06F800.                                                           
                                                                                
       FD  BM06K950                                                             
           RECORD CONTAINS 600 CHARACTERS.                                      
       COPY BM06K950.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *---------------------------------------------------------------*         
       01 WS-CURR-DATE              PIC X(18).                                  
       01 WS-COUNTERS.                                                          
          05 WS-F205-READ-CNT       PIC 9(6)    VALUE 0.                        
          05 WS-K950-WRITE-CNT      PIC 9(6)    VALUE 0.                        
          05 WS-F205-BYPASS-CNT     PIC 9(6)    VALUE 0.                        
          05 WS-F205-MATCH-CNT      PIC 9(6)    VALUE 0.                        
          05 WS-MATCH-CNT           PIC 9(6)    VALUE 0.                        
       01 WS-F205-EOF               PIC X(1)    VALUE 'N'.                      
       01 WS-F800-EOF               PIC X(1)    VALUE 'N'.                      
       01 K950-STATUS               PIC 9(2)    VALUE 0.                        
       01 WS-F800-NUM-FLAT-TYPE     PIC X(2)    VALUE SPACES.                   
       01 WS-TIME-VAR               PIC 9(1)    VALUE 0.                        
       01 WS-CHAR-TIME-VAR          PIC X(1)    VALUE SPACES.                   
                                                                                
       PROCEDURE DIVISION.                                                      
      *---------------------------------------------------------------*         
       0000-CONTROL.                                                            
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES        THRU  1000-EXIT.                      
           PERFORM 2000-F205-READ         THRU  2000-EXIT.                      
           PERFORM 3000-MATCH-ZONE        THRU  3000-EXIT                       
                                          UNTIL WS-F205-EOF = 'Y'.              
           PERFORM 9000-CLOSE-FILES       THRU  9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN    INPUT     BP13F205                                           
                   I-O       BM06K950.                                          
                                                                                
           IF K950-STATUS NOT = 00 AND 97                                       
             DISPLAY 'BM06K950 - OPEN ERROR, K950-STATUS ' K950-STATUS          
             MOVE     K950-STATUS      TO   RETURN-CODE                         
             PERFORM  9000-CLOSE-FILES                                          
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-F205-READ.                                                          
      *---------------------------------------------------------------*         
           READ BP13F205 AT END MOVE 'Y' TO  WS-F205-EOF                        
                                         GO TO 2000-EXIT.                       
           ADD  1    TO WS-F205-READ-CNT.                                       
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-MATCH-ZONE.                                                         
      *---------------------------------------------------------------*         
           OPEN INPUT BM06F800.                                                 
           PERFORM 3100-F800-READ               THRU 3100-EXIT.                 
           MOVE    0     TO WS-MATCH-CNT.                                       
           PERFORM 3200-CHECK-NT-ZONE-ALLOCDTE  THRU 3200-EXIT                  
                                             UNTIL WS-F800-EOF = 'Y'.           
           CLOSE  BM06F800.                                                     
           MOVE   'N'    TO WS-F800-EOF.                                        
           IF WS-MATCH-CNT > 0                                                  
              ADD  1     TO WS-F205-MATCH-CNT                                   
           ELSE                                                                 
              ADD  1     TO WS-F205-BYPASS-CNT                                  
           END-IF                                                               
           PERFORM 2000-F205-READ    THRU 2000-EXIT.                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3100-F800-READ.                                                          
      *---------------------------------------------------------------*         
           READ BM06F800  AT END  MOVE 'Y'  TO    WS-F800-EOF                   
                                            GO TO  3100-EXIT.                   
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3200-CHECK-NT-ZONE-ALLOCDTE.                                             
      *---------------------------------------------------------------*         
           PERFORM 3250-DECODE-FLAT-TYPE      THRU 3250-EXIT.                   
           IF ((F205-NUM-NT-ZONE        = F800-CDE-NT)           OR             
               (F205-NUM-NT-ZONE (1:2)  = F800-CDE-ZONE))        AND            
               (F205-DTE-ALLOCN         = F800-DTE-ALLOCN)       AND            
              ((F205-NUM-FLAT-TYPE      = WS-F800-NUM-FLAT-TYPE) OR             
              ((F205-NUM-FLAT-TYPE      = 'SE')                  AND            
               (WS-F800-NUM-FLAT-TYPE   = '3 ' OR '4 ' OR '5 ')))               
                   ADD 1    TO WS-MATCH-CNT                                     
                   PERFORM 4000-K950-WRITE    THRU   4000-EXIT                  
           ELSE                                                                 
              IF (F205-NUM-NT-ZONE   = F800-NUM-BTO-OPTION)   AND               
                 (F205-DTE-ALLOCN    = F800-DTE-ALLOCN)                         
                   ADD 1    TO WS-MATCH-CNT                                     
                   PERFORM 4000-K950-WRITE    THRU   4000-EXIT                  
              END-IF                                                            
           END-IF.                                                              
           PERFORM 3100-F800-READ   THRU 3100-EXIT.                             
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3250-DECODE-FLAT-TYPE.                                                   
      *---------------------------------------------------------------*         
           EVALUATE F800-CDE-TYP-ACTUSE                                         
           WHEN '01'                                                            
                 IF F800-CDE-MODL-DESG  = '80' OR '89' OR '92'                  
                    MOVE '1A'    TO WS-F800-NUM-FLAT-TYPE                       
                 ELSE                                                           
                    MOVE '1 '    TO WS-F800-NUM-FLAT-TYPE                       
                 END-IF                                                         
           WHEN '02'                                                            
                 IF F800-CDE-MODL-DESG  = '80' OR '89' OR '92'                  
                    MOVE '2A'    TO WS-F800-NUM-FLAT-TYPE                       
                 ELSE                                                           
                    MOVE '2 '    TO WS-F800-NUM-FLAT-TYPE                       
                 END-IF                                                         
           WHEN '03'                                                            
                 MOVE '3 '       TO WS-F800-NUM-FLAT-TYPE                       
           WHEN '04'                                                            
                 MOVE '4 '       TO WS-F800-NUM-FLAT-TYPE                       
           WHEN '05'                                                            
                 MOVE '5 '       TO WS-F800-NUM-FLAT-TYPE                       
           WHEN '06'                                                            
                 MOVE 'E '       TO WS-F800-NUM-FLAT-TYPE                       
           WHEN OTHER                                                           
                 MOVE '  '       TO WS-F800-NUM-FLAT-TYPE                       
           END-EVALUATE.                                                        
                                                                                
       3250-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-K950-WRITE.                                                         
      *---------------------------------------------------------------*         
           PERFORM  4100-K950-FIELDS      THRU 4100-EXIT.                       
           WRITE   BM06K950-REC.                                                
           EVALUATE K950-STATUS                                                 
             WHEN 00                                                            
               ADD   1   TO WS-K950-WRITE-CNT                                   
             WHEN 22                                                            
               CONTINUE                                                         
             WHEN OTHER                                                         
               DISPLAY 'BM06K950 WRITE ERROR, K950-STATUS ' K950-STATUS         
               DISPLAY 'K950-KEY-FLD = ' K950-KEY-FLD                           
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4100-K950-FIELDS.                                                        
      *---------------------------------------------------------------*         
           ADD  1   TO WS-TIME-VAR.                                             
           MOVE WS-TIME-VAR    TO WS-CHAR-TIME-VAR.                             
                                                                                
           INITIALIZE BM06K950-REC.                                             
           MOVE     SPACES                 TO BM06K950-REC.                     
           MOVE     F800-SCH-ACC-NO        TO K950-NUM-SCH-ACC.                 
           MOVE     'OF'                   TO K950-NUM-TRANS.                   
           MOVE     'BP13C292'             TO K950-NUM-USERID.                  
           MOVE     SPACES                 TO K950-NUM-HOUSEHOLD.               
           MOVE     F800-REC(1:500)        TO K950-FIS-DETAILS.                 
           MOVE FUNCTION CURRENT-DATE      TO WS-CURR-DATE.                     
           MOVE WS-CURR-DATE(1:8)          TO K950-DTE-UPDATE.                  
           STRING WS-CURR-DATE(9:8)  WS-CHAR-TIME-VAR                           
                     DELIMITED BY SIZE INTO K950-TME-UPDATE.                    
                                                                                
           IF WS-TIME-VAR = 9                                                   
              MOVE 0   TO WS-TIME-VAR                                           
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-FILES.                                                        
      *---------------------------------------------------------------*         
           DISPLAY '        BP13C292 CONTROL TOTALS'.                           
           DISPLAY '        -----------------------'.                           
           DISPLAY '1. BP13F205 RECORDS READ       = '                          
                                                    WS-F205-READ-CNT.           
           DISPLAY '2. RECORDS WRITTEN TO BM06K950 = '                          
                                                    WS-K950-WRITE-CNT.          
           DISPLAY '3. BP13F205 RECORDS MATCHED    = '                          
                                                    WS-F205-MATCH-CNT.          
           DISPLAY '4. BP13F205 RECORDS BYPASSED   = '                          
                                                    WS-F205-BYPASS-CNT.         
           DISPLAY ' '.                                                         
           DISPLAY '   CHECK  1. =   3. +  4.'.                                 
                                                                                
           CLOSE   BP13F205                                                     
                   BM06K950.                                                    
                                                                                
           IF K950-STATUS NOT = 00 AND 97                                       
             DISPLAY 'BM06K950 - CLOSE ERROR, K950-STATUS ' K950-STATUS         
             MOVE     K950-STATUS      TO   RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
