      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13CB04.                                                 
      *AUTHOR.        SHARON DUMDUM.                                            
      *DATE-WRITTEN.  30 JUNE 2006.                                             
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : THIS PROGRAM VALIDATES TRANSACTIONS PASSED  *           
      *                 FROM PAYMENT SYSTEM TO PROCESS ADDITIONAL   *           
      *                 CPF HOUSING GRANT PAYMENTS.                 *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  AG07F630 -- VOUCHER TRANSACTION FILE                  *           
      *   2.  P13FD10A -- SORTED SOC AHG VOUCHER OIC FILE           *           
      *                                                             *           
      *   I-O FILES :                                               *           
      *   1.  BP13KD10 -- SOC AHG VOUCHER OIC FILE                  *           
      *   2.  BP13K800 -- SOC MASTER FILE                           *           
      *   3.  BP13KD00 -- DBSS MASTER FILE                          *           
      *   4.  BP13K022 -- OIC FILE                                  *           
      *                                                             *           
      *   OUTPUT FILES:                                             *           
      *   1.  BP13F125 -- EDITED FIRST TRANSACTION FILE             *           
      *   2.  P13FD10B -- EDITED SOC AHG VOUCHER OIC FILE           *           
      *   3.  BP13LB04 -- CONTROL LISTING                           *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP132891 30/06/2006 SD10 NEW PROGRAM                        *           
      * BP133061 ED1  06072007   CHANGE KD10-NUM-UIN-PAYEE INTO     *           
      *                          KD10-NUM-CPF-PAYEE & WS-AMT-RECEIPT*           
      *                          TO 9(7)V99                         *           
      * BP133091 RB12 17102007   EXPAND KD00 LENGTH FR 1200 TO 1600 *           
      * BP133398 JB8  18052009   EXPAND KD10 LENGTH FR 450  TO 1400 *           
      * BP134041 JB8  18102010   TO OUTPUT OIC                      *           
      * BP134041 PCL3 19102010   TO ADD CHECKING ON BP13KD07        *           
      * BP134341 MDT2 03082012   CHANGE LRECL OF BP13KD07 FROM 500  *           
      *                          TO 700 IN FD                       *           
      * BP135265 PCL4 12032014   TO UPDATE AHG/HSGNT DTE-PAYMENT    *           
      * BP138015 PP11 20191017   TO UPDATE EHG DTE-PAYMENT          *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13KD00 ASSIGN TO BP13KD00                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KD00-KEY-FLD                               
                  FILE STATUS     IS BP13KD00-STATUS.                           
                                                                                
           SELECT BP13KD07 ASSIGN TO BP13KD07                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KD07-KEY-FLD                               
                  FILE STATUS     IS BP13KD07-STATUS.                           
                                                                                
           SELECT BP13KD10 ASSIGN TO BP13KD10                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KD10-KEY-FLD                               
                  FILE STATUS     IS BP13KD10-STATUS.                           
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS BP13K800-STATUS.                           
                                                                                
           SELECT BP13K022 ASSIGN TO BP13K022                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K022-KEY-FLD                               
                  ALTERNATE KEY   IS K022-NUM-REGN                              
                  FILE STATUS     IS BP13K800-STATUS.                           
                                                                                
           SELECT AG07F630   ASSIGN       TO AG07F630.                          
                                                                                
           SELECT P13FD10A   ASSIGN       TO P13FD10A.                          
                                                                                
           SELECT P13FD10B   ASSIGN       TO P13FD10B.                          
                                                                                
           SELECT BP13F125   ASSIGN       TO BP13F125.                          
                                                                                
           SELECT BP13LB04   ASSIGN       TO BP13LB04.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13KD00                                                            
            RECORD CONTAINS 1600 CHARACTERS.                                    
       COPY BP13KD00.                                                           
                                                                                
       FD   BP13KD07                                                            
            RECORD CONTAINS 700 CHARACTERS.                                     
       COPY BP13KD07.                                                           
                                                                                
       FD   BP13KD10                                                            
            RECORD CONTAINS  1400 CHARACTERS.                                   
       COPY BP13KD10.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100  CHARACTERS.                                    
       COPY BP13K022.                                                           
                                                                                
       FD   AG07F630                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  130 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY AG07F630.                                                           
                                                                                
       FD   P13FD10A                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  1400 CHARACTERS                                    
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13FD10.                                                           
                                                                                
       FD   P13FD10B                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  1400 CHARACTERS                                    
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       01   WS-BP13FD10-REC       PIC X(1400).                          00055007
                                                                                
       FD   BP13F125                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  150 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F125.                                                           
                                                                                
       FD   BP13LB04                                                            
            RECORD CONTAINS   132 CHARACTERS                                    
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
       01   PRINT-REC             PIC X(132).                                   
                                                                                
                                                                                
      ************************                                                  
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  WS-AMT-RECEIPT         PIC S9(7)V99 VALUE ZEROES.                    
       01  WS-NRIC                PIC X(09) VALUE SPACES.                       
       01  WS-NUM-GRANT           PIC X(03) VALUE SPACES.                       
       01  WS-CTR                 PIC 9(01) VALUE ZEROES.                       
       01  WS-K800-FND-SW         PIC X(01) VALUE ZEROES.                       
       01  WS-KD00-FND-SW         PIC X(01) VALUE ZEROES.                       
       01  WS-KD07-FND-SW         PIC X(01) VALUE ZEROES.                       
       01  WS-AHG-CTR             PIC 9(01) VALUE ZEROES.                       
       01  WS-HGT-CTR             PIC 9(01) VALUE ZEROES.                       
       01  WS-EHG-CTR             PIC 9(01) VALUE ZEROES.                       
       01  WS-NUM-CPF-PAYEE       PIC X(09) VALUE SPACES.                       
       01  CNT-LINE-ON-PAGE       PIC 9(02) VALUE 66.                           
       01  CNT-F125-WRITE         PIC 9(05) VALUE ZEROES.                       
       01  CNT-F630-READ          PIC 9(05) VALUE ZEROES.                       
       01  CNT-FD10-WRITE         PIC 9(05) VALUE ZEROES.                       
       01  CNT-KD10-READ          PIC 9(05) VALUE ZEROES.                       
       01  CNT-KD10-DELETE        PIC 9(05) VALUE ZEROES.                       
       01  CNT-KD00-READ          PIC 9(05) VALUE ZEROES.                       
       01  CNT-KD00-NOTFND        PIC 9(05) VALUE ZEROES.                       
       01  CNT-KD00-UPDATE        PIC 9(05) VALUE ZEROES.                       
       01  CNT-KD07-READ          PIC 9(05) VALUE ZEROES.                       
       01  CNT-KD07-NOTFND        PIC 9(05) VALUE ZEROES.                       
       01  CNT-KD07-UPDATE        PIC 9(05) VALUE ZEROES.                       
       01  CNT-K800-READ          PIC 9(05) VALUE ZEROES.                       
       01  CNT-K800-NOTFND        PIC 9(05) VALUE ZEROES.                       
       01  CNT-K800-UPDATE        PIC 9(05) VALUE ZEROES.                       
       01  CNT-PRT-CASES          PIC 9(05) VALUE ZEROES.                       
       01  CNT-FD10-READ          PIC 9(05) VALUE ZEROES.                       
       01  WS-MATCH-REC           PIC 9(05) VALUE ZEROES.                       
       01  CNT-PAGE               PIC 9(03) VALUE ZEROES.                       
       01  WS-F630-EOF            PIC X     VALUE SPACE.                        
       01  WS-FD10-EOF            PIC X     VALUE SPACE.                        
       01  BP13KD10-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13KD00-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13K800-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13K022-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13KD07-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  WS-KD10-REGN-FND       PIC X     VALUE SPACE.                        
       01  WS-VOUCHER-NOTFND      PIC X     VALUE SPACE.                        
       01  WS-HGT-FOUND           PIC X     VALUE SPACE.                        
       01  WS-AHG-FOUND           PIC X     VALUE SPACE.                        
       01  WS-EHG-FOUND           PIC X     VALUE SPACE.                        
       01  WS-DATE                PIC X(08) VALUE SPACES.                       
       01  WS-TME-SYSTEM          PIC X(08).                                    
                                                                                
       01  WS-SCHEDULE-NO.                                                      
           05  WS-SCHEDULE-NUM    PIC X(08).                                    
           05  WS-DOC-NO          PIC X(06).                                    
                                                                                
       01  LB04-PR-HEAD-01.                                                     
           05  FILLER             PIC X(08) VALUE 'BP13LB04'.                   
           05  FILLER             PIC X(05) VALUE SPACES.                       
           05  FILLER             PIC X(08) VALUE 'HDBCAT 3'.                   
           05  FILLER             PIC X(24) VALUE SPACES.                       
           05  FILLER             PIC X(39) VALUE                               
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER             PIC X(17) VALUE SPACES.                       
           05  FILLER             PIC X(06) VALUE 'DATE: '.                     
           05  LB04-DATE          PIC X(10).                                    
           05  FILLER             PIC X(03) VALUE SPACES.                       
           05  FILLER             PIC X(06) VALUE 'PAGE :'.                     
           05  LB04-PAGE          PIC ZZ9 .                                     
                                                                                
       01  LB04-PR-HEAD-02.                                                     
           05  FILLER             PIC X(40) VALUE SPACES.                       
           05  FILLER             PIC X(47) VALUE                               
               '  LIST OF TRANSACTIONS PASSED FROM VOUCHER FILE'.               
           05  FILLER             PIC X(33) VALUE SPACES.                       
                                                                                
       01  LB04-PR-HEAD-03.                                                     
           05  FILLER             PIC X(24)                                     
                  VALUE '    S/NO  VOUCHER NO'.                                 
           05  FILLER             PIC X(43)                                     
                  VALUE 'REGN NO        AMOUNT       RECEIPT NO'.               
           05  FILLER             PIC X(42)                                     
                  VALUE 'PAYMT RELEASE DTE     PAYMT TYPE'.                     
           05  FILLER             PIC X(06)                                     
                  VALUE 'REMARK'.                                               
           05  FILLER             PIC X(17) VALUE SPACES.                       
                                                                                
       01  LB04-PR-DETAILS.                                                     
           05  FILLER             PIC X(01) VALUE SPACES.                       
           05  LB04-SNO           PIC ZZZZZ9 .                                  
           05  FILLER             PIC X(03) VALUE SPACES.                       
           05  LB04-VOUCHER-NO    PIC X(11).                                    
           05  FILLER             PIC X(03) VALUE SPACES.                       
           05  LB04-REG-NO        PIC X(08).                                    
           05  FILLER             PIC X(03) VALUE SPACES.                       
           05  LB04-AMT           PIC $$$$$$$9.99DB.                            
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  LB04-NUM-RECEIPT   PIC X(14).                                    
           05  FILLER             PIC X(08) VALUE SPACES.                       
           05  LB04-DTE-POST      PIC X(08).                                    
           05  FILLER             PIC X(09) VALUE SPACES.                       
           05  LB04-PAYMENT-TYPE  PIC X(09).                                    
           05  FILLER             PIC X(08) VALUE SPACES.                       
           05  LB04-REMARK        PIC X(20).                                    
                                                                                
       01  LB04-CONTROL-LINE.                                                   
           05  FILLER             PIC X(20) VALUE  SPACES.                      
           05  CNTL-ITEM          PIC X(30) VALUE  SPACES.                      
           05  CNTL-CNT           PIC ZZZZ9 .                                   
           05  FILLER             PIC X(58) VALUE  SPACES.                      
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-F630    THRU 2000-EXIT.                            
           PERFORM 2100-READ-FD10    THRU 2100-EXIT.                            
           PERFORM 3000-PROCESS-REC  THRU 3000-EXIT                             
                   UNTIL WS-F630-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  AG07F630                                                 
                       P13FD10A                                                 
                       BP13K022                                                 
                I-O    BP13KD00                                                 
                       BP13KD07                                                 
                       BP13KD10                                                 
                       BP13K800                                                 
                OUTPUT BP13F125                                                 
                       P13FD10B                                                 
                       BP13LB04.                                                
                                                                                
           IF BP13KD10-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KD10 : ' BP13KD10-STATUS             
              MOVE BP13KD10-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13KD00-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KD00 : ' BP13KD00-STATUS             
              MOVE BP13KD00-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13KD07-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KD07 : ' BP13KD07-STATUS             
              MOVE BP13KD07-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K800-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K800 : ' BP13K800-STATUS             
              MOVE BP13K800-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K022-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K022 : ' BP13K022-STATUS             
              MOVE BP13K022-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
              DELIMITED BY SIZE INTO LB04-DATE.                                 
                                                                                
           ACCEPT WS-TME-SYSTEM  FROM TIME.                                     
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-F630.                                                          
      *---------------*                                                         
                                                                                
           READ AG07F630 AT END                                                 
                MOVE HIGH-VALUES TO AG07F630-REC                                
                MOVE 'Y' TO WS-F630-EOF                                         
                GO TO 2000-EXIT.                                                
           ADD 1 TO CNT-F630-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2100-READ-FD10.                                                          
      *---------------*                                                         
                                                                                
           READ P13FD10A AT END                                                 
                MOVE HIGH-VALUES TO BP13FD10-REC                                
                MOVE 'Y' TO WS-FD10-EOF                                         
                GO TO 2100-EXIT.                                                
           ADD 1 TO CNT-FD10-READ.                                              
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           MOVE SPACES                  TO WS-VOUCHER-NOTFND.                   
           IF F630-NUM-VR = FD10-NUM-VR                                         
              ADD  1                    TO WS-MATCH-REC                         
              PERFORM 3100-PROCESS-REC  THRU 3100-EXIT                          
              MOVE SPACES               TO LB04-PR-DETAILS                      
                                           LB04-REMARK                          
              PERFORM 4000-PRINT-REPORT THRU 4000-EXIT                          
              PERFORM 2000-READ-F630    THRU 2000-EXIT                          
              PERFORM 2100-READ-FD10    THRU 2100-EXIT                          
           ELSE                                                                 
              IF F630-NUM-VR > FD10-NUM-VR                                      
                 PERFORM 2100-READ-FD10    THRU 2100-EXIT                       
              ELSE                                                              
                 MOVE SPACES   TO LB04-PR-DETAILS                               
                                  LB04-REMARK                                   
                                  BP13KD10-REC                                  
                 INITIALIZE       BP13KD10-REC                                  
                 MOVE 'VOUCHER NO NOT FOUND'    TO LB04-REMARK                  
                 MOVE 'Y'      TO WS-VOUCHER-NOTFND                             
                 PERFORM 4000-PRINT-REPORT THRU 4000-EXIT                       
                 PERFORM 2000-READ-F630    THRU 2000-EXIT                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3100-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           MOVE SPACES                TO KD10-KEY-FLD.                          
           MOVE 'HGT'                 TO KD10-NUM-TRANS.                        
           MOVE FD10-NUM-REGN         TO KD10-NUM-REGN.                         
           PERFORM 3200-READ-BP13KD10 THRU 3200-EXIT.                           
           PERFORM 8001-READ-BP13K022 THRU 8001-EXIT.                           
           MOVE K022-NUM-OIC          TO KD10-NUM-OIC                           
           MOVE SPACES                TO WS-BP13FD10-REC.                       
           MOVE BP13KD10-REC          TO WS-BP13FD10-REC.                       
           WRITE WS-BP13FD10-REC.                                               
           ADD 1                      TO CNT-FD10-WRITE.                        
                                                                                
           MOVE 'N'                   TO WS-AHG-FOUND                           
                                         WS-HGT-FOUND                           
                                         WS-EHG-FOUND.                          
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 4                  
              IF KD10-NUM-GRANT(WS-CTR) = 'HGT'                                 
                 MOVE 'Y'                   TO WS-HGT-FOUND                     
              END-IF                                                            
                                                                                
              IF KD10-NUM-GRANT(WS-CTR) = 'AHG'                                 
                 MOVE 'Y'                   TO WS-AHG-FOUND                     
              END-IF                                                            
                                                                                
      *       IF KD10-NUM-GRANT(WS-CTR) = 'EHG'                                 
      *          MOVE 'Y'                   TO WS-EHG-FOUND                     
      *       END-IF                                                            
                                                                                
                                                                                
              IF KD10-NUM-CPF-PAYEE(WS-CTR) NOT = SPACES AND LOW-VALUES         
                                              AND ZEROES                        
                 MOVE KD10-NUM-GRANT(WS-CTR)     TO WS-NUM-GRANT                
                 MOVE KD10-NUM-CPF-PAYEE(WS-CTR) TO WS-NRIC                     
                 MOVE KD10-NUM-CPF-PAYEE(WS-CTR) TO WS-NUM-CPF-PAYEE            
                 MOVE ZEROES                     TO WS-AMT-RECEIPT              
                 IF KD10-AMT-CPF-GRANT-PAYEE(WS-CTR) IS NUMERIC                 
                    MOVE KD10-AMT-CPF-GRANT-PAYEE(WS-CTR)                       
                                                 TO WS-AMT-RECEIPT              
                 ELSE                                                           
                    MOVE ZEROES                  TO WS-AMT-RECEIPT              
                 END-IF                                                         
                 PERFORM 3300-MOVE-DATA-BP13F125  THRU 3300-EXIT                
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           PERFORM 3500-UPDATE-BP13K800     THRU 3500-EXIT.                     
           IF WS-K800-FND-SW = 'Y'                                              
              PERFORM 3600-REWRITE-BP13K800 THRU 3600-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 3700-UPDATE-BP13KD00     THRU 3700-EXIT.                     
           IF WS-KD00-FND-SW = 'Y'                                              
              PERFORM 3800-REWRITE-BP13KD00 THRU 3800-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K800-FND-SW = 'Y' AND                                          
              WS-KD00-FND-SW = 'Y'                                              
              PERFORM 3900-DELETE-BP13KD10  THRU 3900-EXIT                      
           END-IF.                                                              
                                                                                
                                                                                
           IF FD10-KEY-FLD(4:1) = '6'                                           
              PERFORM 4200-UPDATE-BP13KD07     THRU 4200-EXIT                   
                                                                                
              IF WS-KD07-FND-SW = 'Y'                                           
                 PERFORM 4300-REWRITE-BP13KD07 THRU 4300-EXIT                   
                 PERFORM 3900-DELETE-BP13KD10  THRU 3900-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3200-READ-BP13KD10.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13KD10.                                                       
           IF BP13KD10-STATUS = 0                                               
              MOVE 'Y'    TO WS-KD10-REGN-FND                                   
           ELSE                                                                 
              IF BP13KD10-STATUS = 23                                           
                 MOVE 'N' TO WS-KD10-REGN-FND                                   
                 DISPLAY 'REGNO NOT FOUND IN BP13KD10 : ' KD10-NUM-REGN         
                 MOVE BP13KD10-STATUS     TO RETURN-CODE                        
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13KD10 : ' BP13KD10-STATUS          
                 MOVE BP13KD10-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       3300-MOVE-DATA-BP13F125.                                                 
      *------------------------*                                                
                                                                                
           MOVE SPACES              TO BP13F125-CASHTRAN.                       
                                                                                
           MOVE '60'                TO F125-CDE-TRANS-TYPE.                     
           MOVE WS-AMT-RECEIPT      TO F125-AMT-RECEIPT.                        
           MOVE FD10-NUM-REGN       TO F125-NUM-REGN.                           
           MOVE WS-NUM-GRANT        TO F125-CDE-PAYMENT-TYPE.                   
           MOVE WS-NRIC             TO F125-NUM-NRIC.                           
           MOVE SPACES              TO F125-NME-NAME.                           
           MOVE WS-NUM-CPF-PAYEE    TO F125-CDE-CPF-APPL.                       
           MOVE F630-NUM-BATCH      TO WS-SCHEDULE-NUM.                         
           MOVE F630-NUM-VR-SERIAL  TO WS-DOC-NO.                               
           MOVE WS-SCHEDULE-NO      TO F125-NUM-RECPT-JRNO.                     
           MOVE F630-NUM-VR-DDD     TO F125-NUM-VR-DDD.                         
           MOVE F630-NUM-VR-YY      TO F125-NUM-VR-YY.                          
           MOVE F630-DTE-TRANS      TO F125-DTE-TRANS                           
                                       F125-DTE-POST.                           
           MOVE SPACES              TO F125-CDE-APPLT-CPF.                      
           MOVE ZERO                TO F125-COUNT-HISTORY.                      
                                                                                
           PERFORM 3400-WRITE-BP13F125 THRU 3400-EXIT.                          
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3400-WRITE-BP13F125.                                                     
      *---------------------*                                                   
                                                                                
           WRITE BP13F125-CASHTRAN.                                             
           ADD 1       TO CNT-F125-WRITE.                                       
                                                                                
           MOVE SPACES TO WS-NRIC.                                              
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3500-UPDATE-BP13K800.                                                    
      *---------------------*                                                   
                                                                                
           MOVE SPACES           TO BP13K800-MASTER                             
                                    WS-K800-FND-SW.                             
           MOVE FD10-NUM-REGN    TO K800-NUM-REGN.                              
                                                                                
           READ BP13K800.                                                       
           IF BP13K800-STATUS = 0                                               
              ADD 1    TO CNT-K800-READ                                         
              MOVE 'Y' TO WS-K800-FND-SW                                        
           ELSE                                                                 
           IF BP13K800-STATUS = 23                                              
              ADD 1    TO CNT-K800-NOTFND                                       
              DISPLAY 'RECORD NOTFND IN BP13K800 : ' KD10-NUM-REGN              
           ELSE                                                                 
              DISPLAY 'ERROR READING - BP13K800 : ' BP13K800-STATUS             
              MOVE BP13K800-STATUS     TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       3600-REWRITE-BP13K800.                                                   
      *----------------------*                                                  
                                                                                
           MOVE F630-DTE-TRANS      TO K800-DTE-UPDATE                          
                                       K800-DTE-AHG-PAYMENT.                    
           MOVE 'BP13CB04'          TO K800-NUM-USERID.                         
           REWRITE BP13K800-MASTER.                                             
                                                                                
           IF BP13K800-STATUS = 0                                               
              ADD 1    TO CNT-K800-UPDATE                                       
           ELSE                                                                 
              DISPLAY 'ERROR READING - BP13K800 : ' BP13K800-STATUS             
              MOVE BP13K800-STATUS     TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3700-UPDATE-BP13KD00.                                                    
      *---------------------*                                                   
                                                                                
           MOVE SPACES           TO BP13KD00-REC                                
                                    WS-KD00-FND-SW.                             
           MOVE KD10-NUM-REGN    TO KD00-NUM-REGN.                              
                                                                                
           READ BP13KD00.                                                       
           IF BP13KD00-STATUS = 0                                               
              ADD 1    TO CNT-KD00-READ                                         
              MOVE 'Y' TO WS-KD00-FND-SW                                        
           ELSE                                                                 
           IF BP13KD00-STATUS = 23                                              
              ADD 1    TO CNT-KD00-NOTFND                                       
              DISPLAY 'RECORD NOTFND IN BP13KD00 : ' KD10-NUM-REGN              
           ELSE                                                                 
              DISPLAY 'ERROR READING - BP13KD00 : ' BP13KD00-STATUS             
              MOVE BP13KD00-STATUS     TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       3800-REWRITE-BP13KD00.                                                   
      *----------------------*                                                  
                                                                                
           MOVE F630-DTE-TRANS      TO KD00-DTE-UPDATE.                         
           MOVE WS-TME-SYSTEM       TO KD00-TME-UPDATE.                         
           MOVE 'BP13CB04'          TO KD00-NUM-USERID.                         
           REWRITE BP13KD00-REC.                                                
                                                                                
           IF BP13KD00-STATUS = 0                                               
              ADD 1    TO CNT-KD00-UPDATE                                       
           ELSE                                                                 
              DISPLAY 'ERROR READING - BP13KD00 : ' BP13KD00-STATUS             
              MOVE BP13KD00-STATUS     TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       3800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3900-DELETE-BP13KD10.                                                    
      *---------------------*                                                   
                                                                                
           DELETE BP13KD10.                                                     
                                                                                
           EVALUATE BP13KD10-STATUS                                             
               WHEN 00                                                          
                    ADD 1 TO CNT-KD10-DELETE                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR DELETING BP13KD10-REC ...'                   
                    MOVE BP13KD10-STATUS TO RETURN-CODE                         
                    PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3900-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       4000-PRINT-REPORT.                                                       
      *------------------*                                                      
                                                                                
           IF CNT-LINE-ON-PAGE > 46                                             
              MOVE 1 TO CNT-LINE-ON-PAGE                                        
              PERFORM 4100-REPORT-HEADING THRU 4100-EXIT                        
           ELSE                                                                 
              ADD 1 TO CNT-LINE-ON-PAGE                                         
           END-IF.                                                              
                                                                                
           ADD 1 TO CNT-PRT-CASES.                                              
                                                                                
           MOVE CNT-PRT-CASES         TO LB04-SNO.                              
                                                                                
           IF WS-VOUCHER-NOTFND = 'Y'                                           
              MOVE F630-NUM-REGN         TO LB04-REG-NO                         
              MOVE F630-NUM-VR           TO LB04-VOUCHER-NO                     
           ELSE                                                                 
              IF FD10-NUM-REGN NOT = SPACES AND LOW-VALUES AND ZEROES           
                 MOVE FD10-NUM-REGN      TO LB04-REG-NO                         
              ELSE                                                              
                 MOVE F630-NUM-REGN      TO LB04-REG-NO                         
              END-IF                                                            
                                                                                
              IF FD10-NUM-VR NOT = SPACES AND LOW-VALUES AND ZEROES             
                 MOVE FD10-NUM-VR        TO LB04-VOUCHER-NO                     
              ELSE                                                              
                 MOVE F630-NUM-VR        TO LB04-VOUCHER-NO                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE F630-DTE-TRANS        TO LB04-DTE-POST.                         
           MOVE F630-NUM-BATCH        TO WS-SCHEDULE-NUM.                       
           MOVE F630-NUM-VR-SERIAL    TO WS-DOC-NO.                             
           MOVE WS-SCHEDULE-NO        TO LB04-NUM-RECEIPT.                      
           MOVE ZEROES                TO WS-AMT-RECEIPT                         
                                         WS-HGT-CTR                             
                                         WS-AHG-CTR.                            
           PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 4                  
              IF KD10-AMT-CPF-GRANT-PAYEE(WS-CTR) NOT NUMERIC                   
                 MOVE ZEROES     TO KD10-AMT-CPF-GRANT-PAYEE(WS-CTR)            
              END-IF                                                            
              IF KD10-NUM-GRANT(WS-CTR) = 'HGT'                                 
                 ADD 1   TO WS-HGT-CTR                                          
              ELSE                                                              
                 IF KD10-NUM-GRANT(WS-CTR) = 'AHG'                              
                    ADD 1   TO WS-AHG-CTR                                       
               ELSE                                                             
                 IF KD10-NUM-GRANT(WS-CTR) = 'EHG'                              
                    ADD 1   TO WS-EHG-CTR                                       
                 END-IF                                                         
                END-IF                                                          
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           IF WS-HGT-CTR > ZEROES AND WS-AHG-CTR > ZEROES                       
              MOVE 'CHG + AHG'      TO LB04-PAYMENT-TYPE                        
           ELSE                                                                 
              IF WS-HGT-CTR > ZEROES AND WS-AHG-CTR = ZEROES                    
                 MOVE 'CHG'         TO LB04-PAYMENT-TYPE                        
              ELSE                                                              
                 IF WS-HGT-CTR = ZEROES AND WS-AHG-CTR > ZEROES                 
                    MOVE 'AHG'      TO LB04-PAYMENT-TYPE                        
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-HGT-CTR > ZEROES AND WS-EHG-CTR > ZEROES                       
              MOVE 'CHG + EHG'      TO LB04-PAYMENT-TYPE                        
           ELSE                                                                 
              IF WS-HGT-CTR > ZEROES AND WS-EHG-CTR = ZEROES                    
                 MOVE 'CHG'         TO LB04-PAYMENT-TYPE                        
              ELSE                                                              
                 IF WS-HGT-CTR = ZEROES AND WS-EHG-CTR > ZEROES                 
                    MOVE 'EHG'      TO LB04-PAYMENT-TYPE                        
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           COMPUTE WS-AMT-RECEIPT = KD10-AMT-CPF-GRANT-PAYEE(1) +               
                                    KD10-AMT-CPF-GRANT-PAYEE(2) +               
                                    KD10-AMT-CPF-GRANT-PAYEE(3) +               
                                    KD10-AMT-CPF-GRANT-PAYEE(4).                
           MOVE WS-AMT-RECEIPT        TO LB04-AMT.                              
           WRITE PRINT-REC          FROM LB04-PR-DETAILS.                       
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       4100-REPORT-HEADING.                                                     
      *--------------------*                                                    
                                                                                
            ADD 1           TO CNT-PAGE.                                        
            MOVE CNT-PAGE   TO LB04-PAGE.                                       
            MOVE SPACES     TO PRINT-REC.                                       
            WRITE PRINT-REC AFTER PAGE.                                         
            WRITE PRINT-REC FROM LB04-PR-HEAD-01 AFTER 2.                       
            WRITE PRINT-REC FROM LB04-PR-HEAD-02 AFTER 3.                       
            WRITE PRINT-REC FROM LB04-PR-HEAD-03 AFTER 3.                       
            MOVE ALL '-'    TO PRINT-REC.                                       
            WRITE PRINT-REC.                                                    
            MOVE SPACES     TO PRINT-REC.                                       
            WRITE PRINT-REC AFTER 1.                                            
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       4200-UPDATE-BP13KD07.                                                    
      *---------------------*                                                   
                                                                                
           MOVE SPACES           TO BP13KD07-REC                                
                                    WS-KD07-FND-SW.                             
           MOVE KD10-NUM-REGN    TO KD07-NUM-REGN.                              
                                                                                
           READ BP13KD07.                                                       
           IF BP13KD07-STATUS = 0                                               
              ADD 1    TO CNT-KD07-READ                                         
              MOVE 'Y' TO WS-KD07-FND-SW                                        
           ELSE                                                                 
           IF BP13KD07-STATUS = 23                                              
              ADD 1    TO CNT-KD07-NOTFND                                       
              DISPLAY 'RECORD NOTFND IN BP13KD07 : ' KD10-NUM-REGN              
           ELSE                                                                 
              DISPLAY 'ERROR READING - BP13KD07 : ' BP13KD07-STATUS             
              MOVE BP13KD07-STATUS     TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       4300-REWRITE-BP13KD07.                                                   
      *----------------------*                                                  
                                                                                
           IF WS-AHG-FOUND = 'Y'                                                
              MOVE F630-DTE-TRANS   TO KD07-DTE-AHG-PAYMENT                     
           END-IF.                                                              
                                                                                
           IF WS-HGT-FOUND = 'Y'                                                
              MOVE F630-DTE-TRANS   TO KD07-DTE-HSGNT-PAYMENT                   
           END-IF.                                                              
                                                                                
      *    IF WS-EHG-FOUND = 'Y'                                                
      *       MOVE F630-DTE-TRANS   TO KD07-DTE-HSGNT-PAYMENT                   
      *    END-IF.                                                              
                                                                                
           MOVE F630-DTE-TRANS      TO KD07-DTE-BKIMAGE.                        
           MOVE WS-TME-SYSTEM       TO KD07-TME-BKIMAGE.                        
           MOVE 'BP13CB04'          TO KD07-NUM-USERID.                         
                                                                                
           REWRITE BP13KD07-REC.                                                
                                                                                
           IF BP13KD07-STATUS = 0                                               
              ADD 1    TO CNT-KD07-UPDATE                                       
           ELSE                                                                 
              DISPLAY 'ERROR READING - BP13KD07 : ' BP13KD07-STATUS             
              MOVE BP13KD07-STATUS     TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       8001-READ-BP13K022.                                                      
      *-----------------*                                                       
                                                                                
           MOVE SPACES                         TO BP13K022-REC                  
           INITIALIZE                             BP13K022-REC                  
                                                                                
           MOVE KD10-NUM-REGN                  TO K022-NUM-REGN                 
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           IF BP13K022-STATUS = 00 OR 02                                        
              CONTINUE                                                          
           ELSE                                                                 
              IF BP13K022-STATUS = 23                                           
                 MOVE SPACES                   TO K022-NUM-OIC                  
              ELSE                                                              
                 DISPLAY 'BP13K022 - READ ERROR (' BP13K022-STATUS ')'          
                 PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8001-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13CB04 *************'.                            
           DISPLAY 'NO OF RECS MATCHED F630/FD10    : ' WS-MATCH-REC.           
           DISPLAY 'NO OF RECS WRITTEN TO BP13F125  : ' CNT-F125-WRITE.         
           DISPLAY 'NO OF FD10 WRITTEN TO BP13FD10  : ' CNT-FD10-WRITE.         
           DISPLAY 'NO OF RECS READ FROM  AG07F630  : ' CNT-F630-READ.          
           DISPLAY 'NO OF RECS READ FROM  BP13KD10  : ' CNT-KD10-READ.          
           DISPLAY 'NO OF RECS READ FROM  BP13KD00  : ' CNT-KD00-READ.          
           DISPLAY 'NO OF RECS READ FROM  BP13KD07  : ' CNT-KD07-READ.          
           DISPLAY 'NO OF RECS READ FROM  BP13K800  : ' CNT-K800-READ.          
           DISPLAY 'NO OF RECS NOTFND IN  BP13K800  : ' CNT-K800-NOTFND.        
           DISPLAY 'NO OF RECS READ FROM  BP13FD10  : ' CNT-FD10-READ.          
           DISPLAY 'NO OF RECS UPDATED IN BP13KD00  : ' CNT-KD00-UPDATE.        
           DISPLAY 'NO OF RECS UPDATED IN BP13KD07  : ' CNT-KD07-UPDATE.        
           DISPLAY 'NO OF RECS UPDATED IN BP13K800  : ' CNT-K800-UPDATE.        
           DISPLAY 'NO OF RECS WRITTEN TO BP13LB04  : ' CNT-PRT-CASES.          
           DISPLAY 'NO OF RECS DELETED FROM BP13KD10: ' CNT-KD10-DELETE.        
                                                                                
           CLOSE BP13KD10                                                       
                 BP13KD00                                                       
                 BP13KD07                                                       
                 BP13K800                                                       
                 BP13K022                                                       
                 AG07F630                                                       
                 P13FD10A                                                       
                 P13FD10B                                                       
                 BP13F125                                                       
                 BP13LB04.                                                      
                                                                                
           IF BP13KD10-STATUS NOT = 0 AND 97                                    
              DISPLAY ' CLOSING ERROR, BP13KD10-STATUS ' BP13KD10-STATUS        
              MOVE BP13KD10-STATUS TO RETURN-CODE.                              
                                                                                
           IF BP13KD00-STATUS NOT = 0 AND 97                                    
              DISPLAY ' CLOSING ERROR, BP13KD00-STATUS ' BP13KD00-STATUS        
              MOVE BP13KD00-STATUS TO RETURN-CODE.                              
                                                                                
           IF BP13KD07-STATUS NOT = 0 AND 97                                    
              DISPLAY ' CLOSING ERROR, BP13KD07-STATUS ' BP13KD07-STATUS        
              MOVE BP13KD07-STATUS TO RETURN-CODE.                              
                                                                                
           IF BP13K800-STATUS NOT = 0 AND 97                                    
              DISPLAY ' CLOSING ERROR, BP13K800-STATUS ' BP13K800-STATUS        
              MOVE BP13K800-STATUS TO RETURN-CODE.                              
                                                                                
           IF BP13K022-STATUS NOT = 0 AND 97                                    
              DISPLAY ' CLOSING ERROR, BP13K022-STATUS ' BP13K022-STATUS        
              MOVE BP13K022-STATUS TO RETURN-CODE.                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
