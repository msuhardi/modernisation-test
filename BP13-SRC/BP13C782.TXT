       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C782.                                                 
       AUTHOR.        SHARON DUMDUM.                                            
      *DATE-WRITTEN.  08/09/2004.                                               
      * =========================================================== *           
      *              SYSTEM OF COMMITMENT (BP13)                    *           
      * =========================================================== *           
      *   OBJECTIVE  :  TO UPDATE NUM-MAX-PA IN BP13K010.           *           
      *                                                             *           
      *   INPUT      :  BP13F012                                    *           
      *   I-O        :  BP13K010                                    *           
      *                                                             *           
      *   AMENDMENTS:                                               *           
      *    REF      DATE     BY          DESCRIPTIONS               *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP132596 08/09/2004 SD10 NEW PROGRAM                        *           
      * BP134861 22/04/2013 EAA1 EXPAND BP13K010 TO 600             *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F012 ASSIGN TO BP13F012.                                  
                                                                                
           SELECT BP13K010 ASSIGN TO        BP13K010                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K010-KEY-FLD                        
                           FILE   STATUS IS WS-K010-STATUS.                     
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   BP13F012                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 400 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F012.                                                           
                                                                                
       FD   BP13K010                                                            
            RECORD CONTAINS 600 CHARACTERS.                                     
       COPY BP13K010.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-F012-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-K010-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-K010-UPDATE          PIC 9(8)    VALUE ZERO.                      
       01  WS-K010-NOTFND          PIC 9(8)    VALUE ZERO.                      
                                                                                
       01  WS-EOF-F012             PIC X       VALUE SPACE.                     
       01  WS-K010-STATUS          PIC 9(2)    VALUE ZEROES.                    
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-REG-PA1          PIC 9(5)    VALUE ZEROES.                    
           05  WS-REG-PA2          PIC 9(5)    VALUE ZEROES.                    
           05  WS-REG-PA3          PIC 9(5)    VALUE ZEROES.                    
                                                                                
       01  WS-TIME-DATES.                                                       
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY.                                                 
                   15  WS-SYS-CC   PIC 9(2).                                    
                   15  WS-SYS-YY   PIC 9(2).                                    
               10  WS-SYS-MM       PIC 9(2).                                    
               10  WS-SYS-DD       PIC 9(2).                                    
           05  WS-EDITED-DATE.                                                  
               10  WS-EDITED-DD    PIC X(2)    VALUE SPACES.                    
               10  FILLER          PIC X       VALUE '/'.                       
               10  WS-EDITED-MM    PIC X(2)    VALUE SPACES.                    
               10  FILLER          PIC X       VALUE '/'.                       
               10  WS-EDITED-YY    PIC X(4)    VALUE SPACES.                    
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-ROUTINE  THRU 1000-EXIT.                           
           PERFORM 2000-PROCESS       THRU 2000-EXIT                            
              UNTIL WS-EOF-F012 = 'Y'.                                          
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F012                                                 
                I-O    BP13K010.                                                
           MOVE 'N' TO WS-EOF-F012.                                             
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-SYSTEM-DATE.                 
                                                                                
           MOVE   WS-SYS-DD           TO WS-EDITED-DD.                          
           MOVE   WS-SYS-MM           TO WS-EDITED-MM.                          
           MOVE   WS-SYS-CCYY         TO WS-EDITED-YY.                          
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K010 OPEN ERROR, STATUS IS ' WS-K010-STATUS          
              GO TO   9000-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
           INITIALIZE    WS-F012-READ                                           
                         WS-K010-READ                                           
                         WS-K010-UPDATE                                         
                         WS-K010-NOTFND.                                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-PROCESS.                                                            
      *****************************************************************         
                                                                                
           PERFORM 3000-READ-F012     THRU 3000-EXIT.                           
           IF WS-EOF-F012 = 'Y'                                                 
              GO TO 2000-EXIT.                                                  
           PERFORM 4000-READ-K010     THRU 4000-EXIT.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-READ-F012.                                                          
      *****************************************************************         
                                                                                
           READ BP13F012 AT END MOVE 'Y' TO WS-EOF-F012                         
                GO TO 3000-EXIT.                                                
            ADD 1 TO WS-F012-READ.                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4000-READ-K010.                                                          
      *****************************************************************         
                                                                                
           MOVE F012-KEY-FLD           TO K010-KEY-FLD.                         
                                                                                
           READ BP13K010.                                                       
           IF WS-K010-STATUS = 00                                               
                                                                                
              ADD 1 TO WS-K010-READ                                             
                                                                                
              COMPUTE  WS-REG-PA1 =                                             
                       (F012-REG-MCP1 + F012-REG-NOMCP1)                        
                                                                                
              COMPUTE  WS-REG-PA2 =                                             
                       (F012-REG-MCP2 + F012-REG-NOMCP2)                        
                                                                                
              COMPUTE  WS-REG-PA3 =                                             
                       (F012-REG-MCP3 + F012-REG-NOMCP3)                        
                                                                                
              MOVE WS-REG-PA1           TO K010-REG-PA1                         
              MOVE WS-REG-PA2           TO K010-REG-PA2                         
              MOVE WS-REG-PA3           TO K010-REG-PA3                         
                                                                                
              PERFORM 5000-UPDATE-K010 THRU 5000-EXIT                           
           ELSE                                                                 
              IF WS-K010-STATUS = 23                                            
                 DISPLAY 'RECORD NOT FOUND IN BP13K010. '                       
                         'K010-CDE-NT-ZONE : ' K010-CDE-NT-ZONE '. '            
                         'K010-CDE-FLAT-TY : ' K010-CDE-FLAT-TY                 
                 ADD 1 TO WS-K010-NOTFND                                        
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K010. STATUS IS '                   
                          WS-K010-STATUS                                        
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       5000-UPDATE-K010.                                                        
      *****************************************************************         
                                                                                
           REWRITE BP13K010-TENDER.                                             
                                                                                
           EVALUATE WS-K010-STATUS                                              
               WHEN ZEROES                                                      
                    ADD 1         TO WS-K010-UPDATE                             
               WHEN 23                                                          
                    DISPLAY 'RECORD NOT FOUND IN BP13K010. '                    
                            'K010-CDE-NT-ZONE : ' K010-CDE-NT-ZONE '. '         
                            'K010-CDE-FLAT-TY : ' K010-CDE-FLAT-TY              
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K010. STATUS IS '                
                             WS-K010-STATUS                                     
                    GO TO 9000-CLOSE-ROUTINE                                    
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-ROUTINE.                                                      
      *****************************************************************         
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'RUN DATE : ' WS-EDITED-DATE.                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13C782 CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY ' F012-READ    :  ' WS-F012-READ.                            
           DISPLAY ' K010-READ    :  ' WS-K010-READ.                            
           DISPLAY ' K010-NOTFND  :  ' WS-K010-NOTFND.                          
           DISPLAY ' K010-UPDATED :  ' WS-K010-UPDATE.                          
           DISPLAY SPACES.                                                      
           DISPLAY '*==============================================*'.          
           CLOSE      BP13F012                                                  
                      BP13K010.                                                 
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
