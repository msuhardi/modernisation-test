       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C579.                                                 
      *AUTHOR.        GILBERT S. DURAN.                                         
      *DATE WRITTEN.  28/10/94.                                                 
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                   *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  1.  TO EXTRACT SUCCESSFUL APPLICANTS FOR      *        
      *                      BOOKING.                                  *        
      *                                                                *        
      *   INPUT  FILE :  1.  BP13F520  -  BALLOT FILE                  *        
      *                                                                *        
      *   OUTPUT FILE :  1.  BP13F200  -  BOOKING APPOINTMENT FILE     *        
      *                                                                *        
      *   LISTINGS    :  1.  CONTROL LIST                              *        
      *                                                                *        
      *   CHGE-NO   BY     DATE    DESCRIPTION                         *        
      *   -------   ---   ------   ---------------------------------   *        
      * BP130250    LSB  140297   TAG 'Y' TO SUCC-4A2 FOR 3-ROOM APPLN *        
      *                           SUCC UNDER 4A2 FLAT SUPPLY           *        
      * BP130250    LMS  180297   TO MOVE NUM-JB-PARENT-REGN INSTEAD   *        
      *                           OF NUM-SEQUENCE.                     *        
      * BP130297    LMS  240597   TO MOVE NUM-SEQUENCE AND JB-PARENT-  *        
      *                           REGN.                                *        
      * BP130418    LSB  180998   CHG Y2000                            *        
      * BP130652    NOR  200299   EXPAND F520 RECL FROM 300 TO 500     *        
      * BP130778    MCC  141299   EVALUATE CDE-COHORT IF '03' OR 'PA'  *        
      * BP130778    YCH  181299   MOVE ACTUAL FLAT TYPE                *        
      * BP130801    SRN  190200   FOR CDE-COHORT = 04 EVALUATE F520-FT *        
      *                           FOR JCL BP13A803, A806, A809         *        
      * BP130830    SRN  270400   FOR F520-FTYPE = '2' MOVE F520-FTYPE *        
      *                           TO F200-NUM-FLAT-TYPE                *        
      * BP130902    ANC  311000   CHANGE NUM-JB-REGN TO NUM-JB-QUEUE-  *        
      *                           SEQUENCE.                            *        
      * BP132095    MTD  250402   EXPAND LENGTH OF BP13F200            *        
      *                           (FROM 100 TO 300)                    *        
      * BP132317    LSB  210303   BTO CHG FT=00, ZONE=00               *        
      * BP132500    YCH2 310304   INCLUDE SEPARATE QUEUE               *        
      * BP132500    YCH2 130404   FLAT TYPE 9 IN SEPARATE QUEUE        *        
      * BP132496    CKK7 220404   CHG ZN = CDE-SUCC-NT-ZONE FOR BTO    *        
      * BP132907    CKK7 220906   ADD CDE-PROJECT AND NUM-REF          *        
      * BP132907    LSB1 020407   ADD QUEUE-FT                         *        
      * BP133349    YCH2 290508   ADD 2-RM FOR QUEUE-FT 03             *        
      * BP134483    SMR2 110412   MOVE F520-NUM-MGPS TO F200-NUM-MGPS  *        
      * BP135458    SMR2 120814   CATER FOR NUM-GROPA-REGN *                    
      * BP135458    IL5  130814   TO OUTPUT RECORDS WITH RESULT STATUS *        
      *                           'K' INTO BP13F200                    *        
      * BP135886    IL5  310715   MOVE CDE-NT1 TO F200-CDE-NT-ZONE FOR *        
      *                           SBF WHEN STATUS = 'K'                *        
      * BP137724    AM25 231019   MOVE SE TO F200 FT FOR ALLO-CAT='SER'*        
      ******************************************************************        
                                                                                
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F520  ASSIGN       TO BP13F520.                           
           SELECT BP13F713  ASSIGN       TO BP13F713.                           
           SELECT BP13F200  ASSIGN       TO BP13F200.                           
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F520                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F520.                                                           
                                                                                
       FD   BP13F200                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13F200.                                                           
                                                                                
       FD   BP13F713                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS  20 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F713.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILE-STATUS.                                                         
           05  WS-F520-STATUS           PIC 9(2)   VALUE 00.                    
           05  WS-F200-STATUS           PIC 9(2)   VALUE 00.                    
                                                                                
       01  WS-ACCUMULATORS.                                                     
           05  WS-F520-READ             PIC 9(6)   VALUE ZEROES.                
           05  WS-F520-REJECTED         PIC 9(6)   VALUE ZEROES.                
           05  WS-F200-WRITE            PIC 9(6)   VALUE ZEROES.                
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F520-EOF-SW           PIC X     VALUE SPACES.                 
               88  C-EOF-F520                     VALUE 'Y'.                    
                                                                                
       01  WS-RUN-DATE.                                                         
           05  WS-RUN-YYMMDD            PIC X(8)  VALUE SPACES.                 
                                                                                
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT BP13F520                                                  
                      BP13F713                                                  
               OUTPUT BP13F200.                                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       2000-PROCESS-ROUTINE.                                                    
      ******************************************************************        
                                                                                
           PERFORM 2100-READ-F520      THRU  2100-EXIT.                         
           PERFORM 2400-READ-F713      THRU 2400-EXIT.                          
           PERFORM 2200-PROCESS-F520   THRU  2200-EXIT                          
              UNTIL C-EOF-F520.                                                 
           GO TO 9000-CLOSE-ROUTINE.                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       2100-READ-F520.                                                          
      ******************************************************************        
                                                                                
           READ BP13F520                                                        
              AT END MOVE 'Y'  TO  WS-F520-EOF-SW                               
              GO TO 2100-EXIT.                                                  
                                                                                
           ADD +1              TO WS-F520-READ.                                 
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2200-PROCESS-F520.                                                       
      ******************************************************************        
                                                                                
           IF F520-CDE-SUCC-NT-ZONE NOT = SPACES OR                             
              F520-CDE-REQUEST-STATUS = 'K'                                     
              PERFORM 2300-WRITE-F200     THRU 2300-EXIT                        
              ADD +1                      TO WS-F200-WRITE                      
           ELSE                                                                 
              ADD +1                      TO WS-F520-REJECTED                   
           END-IF.                                                              
                                                                                
           PERFORM 2100-READ-F520         THRU 2100-EXIT.                       
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2300-WRITE-F200.                                                         
      ******************************************************************        
                                                                                
           MOVE SPACES TO BP13F200-REC.                                         
           INITIALIZE     BP13F200-REC.                                         
                                                                                
           MOVE F520-CDE-SUCC-NT-ZONE     TO F200-NUM-NT-ZONE.                  
           MOVE F520-CDE-FLAT-TYPE        TO F200-NUM-FLAT-TYPE.                
                                                                                
                                                                                
           EVALUATE F713-CDE-COHORT                                             
              WHEN '04'                                                         
              WHEN '03'                                                         
                 IF F520-NUM-ALLO-CAT = 'SER'                                   
                    MOVE 'SE'                  TO F200-NUM-FLAT-TYPE            
                    MOVE F520-CDE-FLAT-TYPE    TO F200-NUM-ACTUAL-FT            
                 END-IF                                                         
              WHEN 'PA'                                                         
                 MOVE F520-CDE-FLAT-TYPE     TO F200-NUM-FLAT-TYPE              
           END-EVALUATE.                                                        
                                                                                
           MOVE F520-NUM-REGN             TO F200-NUM-REGN.                     
           MOVE F520-NUM-REF              TO F200-NUM-REF.                      
           MOVE F520-NUM-JB-PARENT-REGN   TO F200-NUM-JB-PARENT-REGN.           
           MOVE F520-NUM-SEQUENCE         TO F200-NUM-SEQUENCE.                 
           MOVE F520-NUM-CUR-REGN         TO F200-NUM-CUR-REGN.                 
           MOVE F520-DTE-BALLOT           TO F200-DTE-BALLOT.                   
           MOVE F520-NUM-NT-FT-QUEUE      TO F200-NUM-HHTY-QUEUE-SERIAL.        
           MOVE F520-NUM-HHTY-QUEUE-SERIAL TO                                   
                                          F200-NUM-QUEUE-FT.                    
           MOVE F520-NUM-HHTY-QUEUE-ALPHA TO F200-NUM-HHTY-QUEUE-ALPHA.         
           MOVE F520-CDE-HOUSEHOLD        TO F200-NUM-HOUSEHOLD.                
           MOVE F520-NUM-ALLO-CAT         TO F200-CDE-PROJECT.                  
           MOVE F520-NUM-ALLO-CAT         TO F200-NUM-SELTYPE.                  
           MOVE F520-NUM-JB-CNT           TO F200-NUM-JB-CNT.                   
           MOVE F520-NUM-JB-QUEUE-SEQUENCE                                      
                                          TO F200-NUM-JB-QUEUE-SEQUENCE.        
           MOVE F520-NUM-NRIC1            TO F200-NUM-NRIC1.                    
           MOVE F520-NUM-NRIC2            TO F200-NUM-NRIC2.                    
           MOVE F520-NUM-NRIC3            TO F200-NUM-NRIC3.                    
           MOVE F520-NUM-NRIC4            TO F200-NUM-NRIC4.                    
           MOVE 'P13C579'                 TO F200-NUM-USERID.                   
           MOVE FUNCTION CURRENT-DATE(1:8)   TO WS-RUN-YYMMDD.                  
           MOVE WS-RUN-DATE               TO F200-DTE-UPDATE.                   
           MOVE F520-CDE-REQUEST-STATUS   TO F200-NUM-SUCC-4A2.                 
                                                                                
           IF F520-CDE-ALLOC-SCH = 'GRO'                                        
              MOVE F520-NUM-EXIST-REGN    TO F200-NUM-GROPA-REGN                
           END-IF.                                                              
                                                                                
      *    IF F520-CDE-REQUEST-STATUS = '4'                                     
      *        MOVE 'Y' TO F200-NUM-SUCC-4A2.                                   
                                                                                
           IF F520-CDE-REQUEST-STATUS = 'K'                                     
              MOVE F520-CDE-NT1              TO F200-NUM-NT-ZONE                
           END-IF.                                                              
                                                                                
           IF F520-NUM-ALLO-CAT = 'BTO'                                         
              MOVE '00' TO F200-NUM-FLAT-TYPE                                   
              IF F520-NUM-MGPS = 'P' OR 'C'                                     
                 MOVE F520-NUM-MGPS         TO F200-NUM-MGPS-TAG                
                 MOVE F520-NUM-JB-REGN      TO F200-NUM-JB-PARENT-REGN          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           WRITE BP13F200-REC.                                                  
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2400-READ-F713.                                                          
      ******************************************************************        
                                                                                
            READ BP13F713 AT END                                                
                GO TO 2400-EXIT.                                                
                                                                                
       2400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9000-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           CLOSE BP13F520                                                       
                 BP13F713                                                       
                 BP13F200.                                                      
                                                                                
                                                                                
           DISPLAY '                                       '.                   
           DISPLAY ' *****  BP13C579 CONTROL TOTALS ****** '.                   
           DISPLAY '                                       '.                   
           DISPLAY ' NO. OF RECORDS READ  FROM F520  = ' WS-F520-READ.          
           DISPLAY ' NO. OF RECORDS WRITTEN TO F200  = ' WS-F200-WRITE.         
           DISPLAY ' NO. OF RECORDS REJECTED         = '                        
                                                 WS-F520-REJECTED.              
                                                                                
           STOP RUN.                                                            
                                                                                
                                                                                
