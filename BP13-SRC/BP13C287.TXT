      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13C287.                                                 
      *AUTHOR.        JULITO A. MACAPAGAL.                                      
      *DATE-WRITTEN.  10/08/2002.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT  (BP13)                               *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE:  1) SPLIT BP13F230 SEQUENTIAL FILE INTO A      *            
      *                 SMALL CHUNK OF FILES W/ 200+ RECORDS.      *            
      *                 SMALL CHUNK OF FILES W/ 200+ RECORDS.      *            
      *              2) WRITE THE SPLITTED FILE TO A CUMULATIVE    *            
      *                 FILE                                       *            
      *              3) WRITE THE REMAINING RECORDS IN A SEPARATE  *            
      *                 FILE                                       *            
      *  CALLING PROGRAM :                                         *            
      *                                                            *            
      *  INPUT FILES:  BP13F230 - BP13T.F230.FLATLIST.ADHOC        *            
      *                                                            *            
      *  OUTPUT FILES: BP13F23A - TEMPORARY FILES W/ 200+ RECORDS. *            
      *                BP13F23B - CUMMULATIVE FILE.                *            
      *                BP13F23C - FILE CONTAINING REMAINING RECORDS*            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF  BY    DATE     DESCRIPTION                        *            
      * -------- ---  --------  -----------                        *            
      * BP132189 JAM  20021208  NEW PROGRAM.                       *            
      * ========================================================== *            
                                                                                
      ***********************                                                   
       ENVIRONMENT DIVISION.                                                    
      ***********************                                                   
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F230  ASSIGN       TO BP13F230.                           
                                                                                
           SELECT BP13F23A  ASSIGN       TO BP13F23A.                           
                                                                                
           SELECT BP13F23B  ASSIGN       TO BP13F23B.                           
                                                                                
           SELECT BP13F23C  ASSIGN       TO BP13F23C.                           
                                                                                
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
       FILE SECTION.                                                            
                                                                                
       FD  BP13F230                                                             
           RECORDING MODE  IS  F                                                
           LABEL RECORDS STANDARD                                               
           RECORD CONTAINS 130 CHARACTERS.                                      
      *COPY BP13F230.                                                           
       01  F230-HEADER-REC.                                                     
           05  F230-CDE-INTV            PIC X(04).                              
           05  F230-CDE-HEADER          PIC X(02).                              
           05  FILLER                   PIC X(124).                             
                                                                                
       FD  BP13F23A                                                             
           RECORDING MODE  IS  F                                                
           LABEL RECORDS STANDARD                                               
           RECORD CONTAINS 130 CHARACTERS.                                      
       01  BP13F23A-REC    PIC X(130).                                          
                                                                                
       FD  BP13F23B                                                             
           RECORDING MODE  IS  F                                                
           LABEL RECORDS STANDARD                                               
           RECORD CONTAINS 130 CHARACTERS.                                      
       01  BP13F23B-REC    PIC X(130).                                          
                                                                                
       FD  BP13F23C                                                             
           RECORDING MODE  IS  F                                                
           LABEL RECORDS STANDARD                                               
           RECORD CONTAINS 130 CHARACTERS.                                      
       01  BP13F23C-REC    PIC X(130).                                          
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
       01  WS-VARIABLES.                                                        
           05  WS-F230-EOF                PIC X(01) VALUE 'N'.                  
           05  WS-MAX-REC-REACHED         PIC X(01) VALUE 'N'.                  
           05  WS-F230-READ               PIC 9(5) VALUE ZEROES.                
           05  WS-F23A-WRITE              PIC 9(5) VALUE ZEROES.                
           05  WS-F23B-WRITE              PIC 9(5) VALUE ZEROES.                
           05  WS-F23C-WRITE              PIC 9(5) VALUE ZEROES.                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ********************                                                      
       0000-MAIN-ROUTINE.                                                       
      ********************                                                      
                                                                                
           PERFORM 1000-OPEN-FILES         THRU    1000-EXIT.                   
           PERFORM 1100-READ-F230          THRU    1100-EXIT.                   
           PERFORM 1200-PROCESS-F230       THRU    1200-EXIT                    
                   UNTIL WS-MAX-REC-REACHED = 'Y' OR                            
                         WS-F230-EOF = 'Y'.                                     
                                                                                
           IF WS-F230-EOF NOT = 'Y'                                             
              PERFORM 2000-PROCESS-F230-REMAIN THRU    2000-EXIT                
                   UNTIL WS-F230-EOF = 'Y'                                      
           ELSE                                                                 
      *       RETURN CODE 99 WILL BE THE SIGNAL THAT ALL RECORDS                
      *       HAVE BEEN PROCESSED TO STOP ITERATIVE CALL TO THIS                
      *       PROGRAM.                                                          
      *       RETURN CODE = 99, EOF REACHED W/O RECORDS PROCESSED               
              IF WS-F230-READ < 50 OR                                           
                 WS-F230-EOF = 'Y'                                              
                 MOVE 99 TO RETURN-CODE                                         
              END-IF                                                            
           END-IF.                                                              
           PERFORM 9000-CLOSE-FILES        THRU    9000-EXIT.                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       1000-OPEN-FILES.                                                         
      ******************                                                        
                                                                                
           OPEN INPUT  BP13F230,                                                
                OUTPUT BP13F23A,                                                
                       BP13F23B,                                                
                       BP13F23C.                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       1100-READ-F230.                                                          
      ******************                                                        
                                                                                
           READ BP13F230 AT END                                                 
                MOVE 'Y' TO WS-F230-EOF                                         
                GO TO 1100-EXIT.                                                
                                                                                
           ADD 1 TO WS-F230-READ.                                               
                                                                                
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************                                                        
       1200-PROCESS-F230.                                                       
      ******************                                                        
                                                                                
      *    IF F230-CDE-HEADER = '01'                                            
           IF WS-F23A-WRITE = 50                                                
              MOVE 'Y' TO WS-MAX-REC-REACHED                                    
              GO TO 1200-EXIT                                                   
           END-IF                                                               
      *    END-IF.                                                              
                                                                                
           PERFORM 1500-WRITE-REC THRU 1500-EXIT.                               
           PERFORM 1100-READ-F230  THRU    1100-EXIT.                           
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1500-WRITE-REC.                                                          
           WRITE BP13F23A-REC FROM F230-HEADER-REC.                             
           ADD 1 TO WS-F23A-WRITE.                                              
           WRITE BP13F23B-REC FROM F230-HEADER-REC.                             
           ADD 1 TO WS-F23B-WRITE.                                              
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       2000-PROCESS-F230-REMAIN.                                                
           WRITE BP13F23C-REC FROM F230-HEADER-REC.                             
           ADD 1 TO WS-F23C-WRITE.                                              
           PERFORM 1100-READ-F230  THRU    1100-EXIT.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-FILES.                                                        
                                                                                
           DISPLAY '>>>>>> BP13C287 RUN STATISTICS <<<<<<<'.                    
           DISPLAY 'NO. OF RECORDS READ (BP13F230) : ' WS-F230-READ.            
           DISPLAY 'NO. OF RECORDS WRITTEN (SPLIT) : ' WS-F23A-WRITE.           
           DISPLAY 'NO. OF RECORDS WRITTEN (CUMMUL): ' WS-F23B-WRITE.           
           DISPLAY 'NO. OF RECORDS WRITTEN (REMAIN): ' WS-F23C-WRITE.           
           DISPLAY '>>>>>> END-OF JOB BP13C287 <<<<<<<<<<<'.                    
                                                                                
           CLOSE BP13F230, BP13F23A, BP13F23B, BP13F23C.                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
