       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C41C.                                                 
       AUTHOR.        ZARITA SARMIENTO.                                         
       DATE-WRITTEN.  25/02/2019.                                               
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SOC                                   *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : TO UPDATE BP13K800                    *                
      *                  K800-CDE-SPOUSE-OCC-2NDCON            *                
      *                                                        *                
      *    INPUT FILES : BP13F800                              *                
      *                  BP13K820                              *                
      *    I-O FILES   : BP13K800                              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO    BY     DATE      DESCRIPTION                 *                
      * ------    --     ----      -----------                 *                
      * BP136476  ZS13  20190225   NEW PGM                     *                
      * BP137701  ZS13  20190301   REVISED READING OF BP13K820 *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800 ASSIGN       TO BP13F800.                            
           SELECT BP13F80A ASSIGN       TO BP13F80A.                            
                                                                                
           SELECT BP13K800 ASSIGN       TO BP13K800                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K800-NUM-REGN                        
                           FILE STATUS  IS K800-STATUS.                         
                                                                                
           SELECT BP13K820 ASSIGN       TO BP13K820                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K820-KEY-FLD                         
                           FILE STATUS  IS K820-STATUS.                         
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BP13F800                                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       COPY BP13F800.                                                           
                                                                                
       FD  BP13F80A                                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       01  BP13F80A-REC PIC X(2000).                                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-CURR-DATE                   PIC X(8)  VALUE SPACES.               
                                                                                
       01  K800-STATUS                    PIC 9(2)  VALUE ZERO.                 
       01  K820-STATUS                    PIC 9(2)  VALUE ZERO.                 
                                                                                
       01  WS-F800-READ                   PIC 9(5)  VALUE ZERO.                 
       01  WS-K800-READ                   PIC 9(5)  VALUE ZERO.                 
       01  WS-K820-READ                   PIC 9(5)  VALUE ZERO.                 
       01  WS-F820-WRITE                  PIC 9(5)  VALUE ZERO.                 
       01  WS-K800-REWRITE                PIC 9(5)  VALUE ZERO.                 
       01  WS-SPOUSE-NOT-OCCUP            PIC 9(5)  VALUE ZERO.                 
       01  WS-SPOUSE-OCCUP-CTR            PIC 9(5)  VALUE ZERO.                 
       01  WS-K800-SOC                    PIC S9(7)V99  VALUE ZERO.             
       01  WS-F800-SOC                    PIC S9(7)V99  VALUE ZERO.             
       01  WS-DIFF                        PIC S9(7)V99  VALUE ZERO.             
       01  WS-CAT                         PIC X(1)     VALUE SPACES.            
       01  WS-CAT-NRIC1                   PIC X(1)     VALUE SPACES.            
       01  WS-CNT                         PIC 9(2)     VALUE ZEROES.            
       01  WS-F800-REGN                   PIC X(8)     VALUE SPACES.            
       01  WS-SPOUSE-OCCUP-TAG            PIC X(1)     VALUE SPACES.            
       01  WS-SPOUSE-NRIC                 PIC X(9)     VALUE SPACES.            
       01  WS-K800-FND                    PIC X(1)     VALUE SPACES.            
                                                                                
                                                                                
       01  WS-EOF-F800                    PIC X     VALUE SPACE.                
           88 EOF-F800                    VALUE 'Y'.                            
                                                                                
       01  WS-EOF-K820                    PIC X     VALUE SPACE.                
           88 EOF-K820                    VALUE 'Y'.                            
                                                                                
       PROCEDURE DIVISION.                                                      
      *---------------------------------------------------------------*         
       0000-CONTROL.                                                            
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           MOVE FUNCTION CURRENT-DATE    TO  WS-CURR-DATE.                      
           PERFORM 2000-READ-BP13F800    THRU 2000-EXIT.                        
           PERFORM 3000-PROCESS-BP13K800 THRU 3000-EXIT                         
             UNTIL EOF-F800.                                                    
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN INPUT  BP13F800                                                 
                       BP13K820                                                 
               I-O     BP13K800                                                 
               OUTPUT  BP13F80A.                                                
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, K800-STATUS ' K800-STATUS                    
              MOVE K800-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-FILES                                          
           END-IF.                                                              
                                                                                
           IF K820-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, K820-STATUS ' K820-STATUS                    
              MOVE K820-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-FILES                                          
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13F800.                                                      
      *---------------------------------------------------------------*         
           READ BP13F800 AT END                                                 
                MOVE 'Y' TO WS-EOF-F800                                         
                GO TO 2000-EXIT.                                                
           ADD 1 TO WS-F800-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-PROCESS-BP13K800.                                                   
      *---------------------------------------------------------------*         
                                                                                
           MOVE SPACES                TO BP13K800-MASTER                        
                                         WS-EOF-K820                            
                                         WS-SPOUSE-OCCUP-TAG                    
                                         WS-SPOUSE-NRIC.                        
                                                                                
           MOVE F800-NUM-REGN         TO K800-NUM-REGN.                         
           PERFORM 3050-READ-BP13K800 THRU 3050-EXIT                            
      *** FOR NRIC1                                                             
           MOVE SPACES          TO BP13K820-REC                                 
           MOVE K800-NUM-REGN   TO K820-NUM-REGN.                               
           MOVE K800-NUM-NRIC1  TO K820-NUM-NRIC.                               
                                                                                
           IF WS-K800-FND = 'Y'                                                 
             PERFORM 3130-READ-BP13K820 THRU 3130-EXIT                          
                                                                                
             IF WS-SPOUSE-OCCUP-TAG = 'Y'                                       
                ADD 1 TO WS-SPOUSE-NOT-OCCUP                                    
                PERFORM 4200-UPDATE-BP13K800  THRU 4200-EXIT                    
             ELSE                                                               
                ADD 1 TO WS-SPOUSE-OCCUP-CTR                                    
             END-IF                                                             
           END-IF.                                                              
                                                                                
      *** FOR NRIC2                                                             
           IF WS-SPOUSE-OCCUP-TAG NOT = 'Y'                                     
             IF K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES                      
               MOVE SPACES          TO BP13K820-REC                             
               MOVE K800-NUM-REGN   TO K820-NUM-REGN                            
               MOVE K800-NUM-NRIC2  TO K820-NUM-NRIC                            
                                                                                
               IF WS-K800-FND = 'Y'                                             
                 PERFORM 3130-READ-BP13K820 THRU 3130-EXIT                      
                                                                                
                 IF WS-SPOUSE-OCCUP-TAG = 'Y'                                   
                    ADD 1 TO WS-SPOUSE-NOT-OCCUP                                
                    PERFORM 4200-UPDATE-BP13K800  THRU 4200-EXIT                
                 ELSE                                                           
                    ADD 1 TO WS-SPOUSE-OCCUP-CTR                                
                 END-IF                                                         
               END-IF                                                           
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *** FOR NRIC3                                                             
           IF WS-SPOUSE-OCCUP-TAG NOT = 'Y'                                     
             IF K800-NUM-NRIC3 NOT = SPACES AND LOW-VALUES                      
               MOVE SPACES          TO BP13K820-REC                             
               MOVE K800-NUM-REGN   TO K820-NUM-REGN                            
               MOVE K800-NUM-NRIC3  TO K820-NUM-NRIC                            
                                                                                
               IF WS-K800-FND = 'Y'                                             
                 PERFORM 3130-READ-BP13K820 THRU 3130-EXIT                      
                                                                                
                 IF WS-SPOUSE-OCCUP-TAG = 'Y'                                   
                    ADD 1 TO WS-SPOUSE-NOT-OCCUP                                
                    PERFORM 4200-UPDATE-BP13K800  THRU 4200-EXIT                
                 ELSE                                                           
                    ADD 1 TO WS-SPOUSE-OCCUP-CTR                                
                 END-IF                                                         
               END-IF                                                           
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *** FOR NRIC4                                                             
           IF WS-SPOUSE-OCCUP-TAG NOT = 'Y'                                     
             IF K800-NUM-NRIC4 NOT = SPACES AND LOW-VALUES                      
               MOVE SPACES          TO BP13K820-REC                             
               MOVE K800-NUM-REGN   TO K820-NUM-REGN                            
               MOVE K800-NUM-NRIC4  TO K820-NUM-NRIC                            
                                                                                
               IF WS-K800-FND = 'Y'                                             
                 PERFORM 3130-READ-BP13K820 THRU 3130-EXIT                      
                                                                                
                 IF WS-SPOUSE-OCCUP-TAG = 'Y'                                   
                    ADD 1 TO WS-SPOUSE-NOT-OCCUP                                
                    PERFORM 4200-UPDATE-BP13K800  THRU 4200-EXIT                
                 ELSE                                                           
                    ADD 1 TO WS-SPOUSE-OCCUP-CTR                                
                 END-IF                                                         
               END-IF                                                           
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F800    THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3050-READ-BP13K800.                                                      
                                                                                
            READ BP13K800                                                       
                                                                                
            EVALUATE K800-STATUS                                                
               WHEN ZERO                                                        
                 ADD 1   TO WS-K800-READ                                        
                 MOVE 'Y'   TO WS-K800-FND                                      
               WHEN 23                                                          
                 MOVE 'N'   TO WS-K800-FND                                      
                 DISPLAY 'NOT FD IN BP13K800 ' F800-NUM-REGN                    
               WHEN OTHER                                                       
                 DISPLAY 'READ ERROR, K800-STATUS ' K800-STATUS                 
                 DISPLAY 'K800-NUM-REGN = ' K800-NUM-REGN                       
            END-EVALUATE.                                                       
                                                                                
       3050-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3130-READ-BP13K820.                                                      
                                                                                
           READ BP13K820                                                        
                                                                                
           EVALUATE K820-STATUS                                                 
           WHEN 00                                                              
           WHEN 02                                                              
                PERFORM 4100-CHECK-SPOUSE  THRU 4100-EXIT                       
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FOUND IN BP13K820, KEY IS '                 
                         K820-KEY-FLD                                           
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING IN BP13K820, STATUS IS '                 
                         K820-STATUS                                            
                PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
                                                                                
       3130-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4100-CHECK-SPOUSE.                                                       
                                                                                
           IF K820-NUM-NRIC-SPOUSE NOT =  SPACES AND LOW-VALUES                 
             IF K820-NUM-NRIC-SPOUSE = K800-NUM-NRIC1 OR                        
                                       K800-NUM-NRIC2 OR                        
                                       K800-NUM-NRIC3 OR                        
                                       K800-NUM-NRIC4                           
                CONTINUE                                                        
             ELSE                                                               
                MOVE K820-NUM-NRIC-SPOUSE TO WS-SPOUSE-NRIC                     
                MOVE 'Y'    TO   WS-SPOUSE-OCCUP-TAG                            
             END-IF                                                             
           END-IF.                                                              
                                                                                
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4200-UPDATE-BP13K800.                                                    
                                                                                
            DISPLAY 'REGN - ' K800-NUM-REGN                                     
                    ' SPOUSE-NRIC - ' WS-SPOUSE-NRIC                            
                    ' NRIC1 - ' K800-NUM-NRIC1                                  
                    ' NRIC2 - ' K800-NUM-NRIC2                                  
                    ' NRIC3 - ' K800-NUM-NRIC3                                  
                    ' NRIC4 - ' K800-NUM-NRIC4                                  
                                                                                
            MOVE 'Y'   TO K800-CDE-SPOUSE-OCC-2NDCON                            
            WRITE BP13F80A-REC  FROM BP13K800-MASTER                            
                                                                                
            REWRITE BP13K800-MASTER                                             
                                                                                
            EVALUATE K800-STATUS                                                
               WHEN ZERO                                                        
                 ADD 1   TO WS-K800-REWRITE                                     
               WHEN OTHER                                                       
                 DISPLAY 'REWRITE ERROR, K800-STATUS ' K800-STATUS              
                 DISPLAY 'K800-NUM-REGN = ' K800-NUM-REGN                       
            END-EVALUATE.                                                       
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-FILES.                                                        
      *---------------------------------------------------------------*         
           CLOSE BP13F800                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13F80A.                                                      
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, K800-STATUS ' K800-STATUS                   
              MOVE K800-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           IF K820-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, K820-STATUS ' K820-STATUS                   
              MOVE K820-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13C41C ================*'.              
           DISPLAY '   NO. OF F800 RECORDS READ    = ' WS-F800-READ.            
           DISPLAY '   NO. OF K800 RECORDS READ    = ' WS-K800-READ.            
           DISPLAY '   NO. OF K820 RECORDS READ    = ' WS-K820-READ.            
           DISPLAY '   NO. OF K800 RECORDS UPDATED = ' WS-K800-REWRITE.         
           DISPLAY '   NO. OF F800 RECORDS WRITTEN = ' WS-K800-REWRITE.         
           DISPLAY '   NO. OF SPOUSE NOT OCCUP     = '                          
                       WS-SPOUSE-NOT-OCCUP                                      
           DISPLAY '*==========================================*'.              
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
