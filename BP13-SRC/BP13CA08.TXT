      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13CA08.                                                 
      *AUTHOR.        RACHELLE SAN BUENAVENTURA.                                
      *DATE-WRITTEN.  07 MARCH 2008.                                            
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : TO UPDATE SOC AND FIS AFTER REJECTION OF    *           
      *                 DBSS FLAT                                   *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BP13FA40 -- SORTED TRANSACTION FILE                   *           
      *                                                             *           
      *   I-O FILES :                                               *           
      *   1.  BP13K800 -- SOC MASTER FILE                           *           
      *   2.  BP13K840 -- SOC APPLICATION HISTORY FILE              *           
      *   3.  BM06K110 -- FLAT INVENTORY FILE                       *           
      *   4.  BM06K100 -- BLOCK SUMMARY FILE                        *           
      *   5.  BM06KD00 -- HOUSING GRANT, SCHACC-NO                  *           
      *   6.  BP13KD15 -- DBSS TP FILE                              *           
      *                                                             *           
      *   OUTPUT FILES :                                            *           
      *   1.  P13FA40  -- TRANSACTION FILE CONTAING HLA INFO        *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP133290 07/03/2008 RB12 NEW PROGRAM                        *           
      * BP133315 22/04/2008 RB12 ADD UPDATE OF BP27K800/K820        *           
      * BP133315 28/04/2008 RB12 REMOVE UPDATE OF BP27 FILES        *           
      *                          WRITE HLA INFO TO NEW P13FA40 FILE *           
      * BP133403 03/12/2008 ESA1 UPDATE KD15 FROM THE VALUE OF KA30 *           
      * BP133572 29/05/2009 JB8  TO CATER FOR COMPUDIATION CASE     *           
      * BP133642 31/07/2009 LMS1 DO NOT PASS RECORD TO LEAS IF      *           
      *                          CASES HAVE ACCEPTED ANOTHER FLAT   *           
      * BP134215 27/06/2011 JB8  TO CATER FOR EC CASES              *           
      * BP137436 29/10/2018 FNP1 TAG REJECTED FLAT WITH ALO-TAG PPJ *           
      *                          IF FS = ST AND RP-CODE = 00 OR 01  *           
      * BP138047 25/11/2019 KAC1 RECOMPILE FOR BP27 COPYBOOK UPDATE *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13KD00 ASSIGN TO BP13KD00                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KD00-KEY-FLD                               
                  FILE STATUS     IS BP13KD00-STATUS.                           
                                                                                
           SELECT BP13FA40 ASSIGN TO BP13FA40.                                  
                                                                                
           SELECT P13FA40  ASSIGN TO P13FA40.                                   
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS BP13K800-STATUS.                           
                                                                                
           SELECT BP13K840 ASSIGN TO BP13K840                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K840-NUM-REGN                              
                  FILE STATUS     IS BP13K840-STATUS.                           
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K110-KEY-FLD                               
                  FILE STATUS     IS BM06K110-STATUS.                           
                                                                                
           SELECT BM06K100 ASSIGN TO BM06K100                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K100-KEY-FLD                               
                  FILE STATUS     IS BM06K100-STATUS.                           
                                                                                
           SELECT BP13KD15 ASSIGN TO BP13KD15                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KD15-KEY-FLD                               
                  ALTERNATE RECORD KEY IS KD15-ALT-KEY1                         
                  FILE STATUS     IS BP13KD15-STATUS.                           
                                                                                
           SELECT BP13KB20 ASSIGN TO BP13KB20                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KB20-KEY-FLD                               
                  FILE STATUS     IS BP13KB20-STATUS.                           
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13KD00                                                            
            RECORD CONTAINS 1600 CHARACTERS.                                    
       COPY BP13KD00.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K840                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BP13K840.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BM06K110.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BM06K100.                                                           
                                                                                
       FD   BP13KB20                                                            
            RECORD CONTAINS 3300 CHARACTERS.                                    
       COPY BP13KB20.                                                           
                                                                                
       FD   BP13FA40                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  150 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13FA40.                                                           
                                                                                
       FD   P13FA40                                                             
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  150 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       01   P13FA40-REC           PIC X(150).                                   
                                                                                
       FD   BP13KD15                                                            
            RECORD CONTAINS  300 CHARACTERS.                                    
       COPY BP13KD15.                                                           
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  CNT-FA40-READ          PIC 9(08) VALUE ZEROES.                       
       01  CNT-FA40-WRITE         PIC 9(08) VALUE ZEROES.                       
       01  CNT-K800-FND           PIC 9(08) VALUE ZEROES.                       
       01  CNT-K800-NOTFND        PIC 9(08) VALUE ZEROES.                       
       01  CNT-K800-REWRITE       PIC 9(08) VALUE ZEROES.                       
       01  CNT-K840-FND           PIC 9(08) VALUE ZEROES.                       
       01  CNT-K840-WRITE         PIC 9(08) VALUE ZEROES.                       
       01  CNT-K840-REWRITE       PIC 9(08) VALUE ZEROES.                       
       01  CNT-K110-FND           PIC 9(08) VALUE ZEROES.                       
       01  CNT-K110-NOTFND        PIC 9(08) VALUE ZEROES.                       
       01  CNT-K110-REWRITE       PIC 9(08) VALUE ZEROES.                       
       01  CNT-K100-FND           PIC 9(08) VALUE ZEROES.                       
       01  CNT-K100-NOTFND        PIC 9(08) VALUE ZEROES.                       
       01  CNT-K100-REWRITE       PIC 9(08) VALUE ZEROES.                       
       01  CNT-KD00-FND           PIC 9(08) VALUE ZEROES.                       
       01  CNT-KD00-NOTFND        PIC 9(08) VALUE ZEROES.                       
       01  CNT-KD00-DELETE        PIC 9(08) VALUE ZEROES.                       
       01  CNT-KD00-DELETE-ERROR  PIC 9(08) VALUE ZEROES.                       
       01  WS-FA40-EOF            PIC X     VALUE SPACE.                        
       01  BP13KD00-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13K800-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13K840-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BM06K110-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BM06K100-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13KD15-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13KB20-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  CNT-KD15-FND           PIC 9(08) VALUE ZEROES.                       
       01  CNT-KD15-NOTFND        PIC 9(08) VALUE ZEROES.                       
       01  CNT-KD15-REWRITE       PIC 9(08) VALUE ZEROES.                       
       01  RM-PTR                 PIC 9(02) VALUE ZEROES.                       
       01  WS-DATE                PIC X(08) VALUE SPACES.                       
       01  WS-NUM-HLA             PIC X(09) VALUE SPACES.                       
       01  WS-NUM-HLA-STATUS      PIC X(03) VALUE SPACES.                       
       01  WS-SCH-ACC             PIC X(11) VALUE SPACES.                       
       01  WS-CURRENT-TIME        PIC 9(08) VALUE ZEROES.                       
       01  WS-NUM-REGN            PIC X(08) VALUE SPACES.                       
       01  WS-NUM-SCH-ACC         PIC X(11) VALUE SPACES.                       
                                                                                
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-FA40    THRU 2000-EXIT.                            
           PERFORM 3000-PROCESS-REC  THRU 3000-EXIT                             
                   UNTIL WS-FA40-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13FA40                                                 
                       BP13KB20                                                 
                I-O    BP13K800                                                 
                       BP13K840                                                 
                       BP13KD00                                                 
                       BM06K110                                                 
                       BM06K100                                                 
                       BP13KD15                                                 
                OUTPUT P13FA40.                                                 
                                                                                
           IF BP13K800-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING - BP13K800 : ' BP13K800-STATUS             
              MOVE BP13K800-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K840-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING - BP13K840 : ' BP13K840-STATUS             
              MOVE BP13K840-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BM06K110-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING - BM06K110 : ' BM06K110-STATUS             
              MOVE BM06K110-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BM06K100-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING - BM06K100 : ' BM06K100-STATUS             
              MOVE BM06K100-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13KD00-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING - BP13KD00 : ' BP13KD00-STATUS             
              MOVE BP13KD00-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13KD15-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING - BP13KD15 : ' BP13KD15-STATUS             
              MOVE BP13KD15-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13KB20-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING - BP13KB20 : ' BP13KB20-STATUS             
              MOVE BP13KB20-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-FA40.                                                          
      *---------------*                                                         
                                                                                
           READ BP13FA40 AT END                                                 
                MOVE HIGH-VALUES TO BP13FA40-TRANS-REC                          
                MOVE 'Y' TO WS-FA40-EOF                                         
                GO TO 2000-EXIT.                                                
           ADD 1 TO CNT-FA40-READ.                                              
           MOVE FA40-SCH-ACC TO WS-NUM-SCH-ACC.                                 
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           IF FA40-REGN-NO NOT = SPACES AND LOW-VALUES AND ZEROES               
              PERFORM 3200-PROCESS-REC THRU 3200-EXIT                           
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-FA40    THRU 2000-EXIT.                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3200-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           IF FA40-REGN-NO(1:1) = '5'                                           
              MOVE SPACES                     TO WS-SCH-ACC                     
              PERFORM 3400-READ-BP13K800    THRU 3400-EXIT                      
              IF FA40-SCH-ACC NOT = WS-SCH-ACC                                  
                 GO TO 3200-EXIT                                                
              END-IF                                                            
              IF K800-NUM-SCH-ACC = SPACES OR LOW-VALUES                        
                 PERFORM 3430-WRITE-P13FA40 THRU 3430-EXIT                      
              END-IF                                                            
              PERFORM 3500-READ-BP13K840    THRU 3500-EXIT                      
              PERFORM 3600-READ-BM06K110    THRU 3600-EXIT                      
              PERFORM 3800-READ-BP13KD00    THRU 3800-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 3900-READ-BP13KD15       THRU 3900-EXIT.                     
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3400-READ-BP13K800.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES            TO K800-NUM-REGN                              
                                     WS-NUM-HLA                                 
                                     WS-NUM-HLA-STATUS.                         
           MOVE FA40-REGN-NO      TO K800-NUM-REGN.                             
           READ BP13K800.                                                       
                                                                                
           IF BP13K800-STATUS = 00                                              
              ADD 1    TO CNT-K800-FND                                          
              IF FA40-SCH-ACC = K800-NUM-SCH-ACC                                
                 PERFORM 3410-MOVE-DATA-BP13K800 THRU 3410-EXIT                 
                 PERFORM 3420-UPDATE-BP13K800    THRU 3420-EXIT                 
              END-IF                                                            
           ELSE                                                                 
              IF BP13K800-STATUS = 23                                           
                 ADD 1        TO CNT-K800-NOTFND                                
                 MOVE SPACES  TO BP13K800-MASTER                                
                 INITIALIZE      BP13K800-MASTER                                
                 DISPLAY 'RECORD NOT FOUND IN BP13K800 : ' FA40-REGN-NO         
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13K800 : ' BP13K800-STATUS          
                 MOVE BP13K800-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       3410-MOVE-DATA-BP13K800.                                                 
      *------------------------*                                                
                                                                                
           MOVE K800-NUM-SCH-ACC         TO WS-SCH-ACC.                         
           MOVE K800-NUM-HLA OF BP13K800-MASTER                                 
                                         TO WS-NUM-HLA.                         
           MOVE K800-NUM-HLA-STATUS      TO WS-NUM-HLA-STATUS.                  
                                                                                
           MOVE SPACES                   TO K800-NUM-SCH-ACC                    
                                            K800-NUM-HLA                        
                                            OF BP13K800-MASTER                  
                                            K800-NUM-HLA-STATUS.                
                                                                                
           MOVE WS-DATE                  TO K800-NUM-ALLOC-DATE                 
                                            K800-DTE-UPDATE.                    
           MOVE 'RI'                     TO K800-NUM-ALLOC-TAG.                 
                                                                                
           MOVE WS-DATE                  TO K800-DTE-UPDATE.                    
                                                                                
       3410-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3420-UPDATE-BP13K800.                                                    
      *---------------------*                                                   
                                                                                
           REWRITE BP13K800-MASTER.                                             
                                                                                
           IF BP13K800-STATUS = 00 OR 02                                        
              ADD 1    TO CNT-K800-REWRITE                                      
           ELSE                                                                 
              DISPLAY 'ERROR UPDATING - BP13K800 : ' BP13K800-STATUS            
              MOVE BP13K800-STATUS     TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       3420-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3430-WRITE-P13FA40.                                                      
      *-------------------*                                                     
           MOVE SPACES            TO FA40-SCH-ACC.                              
           MOVE WS-NUM-HLA        TO FA40-SCH-ACC.                              
           MOVE SPACES            TO FA40-CDE-NT.                               
           MOVE WS-NUM-HLA-STATUS TO FA40-CDE-NT.                               
           WRITE P13FA40-REC FROM BP13FA40-TRANS-REC.                           
           DISPLAY 'BP13FA40-TRANS-REC: ' BP13FA40-TRANS-REC.                   
                                                                                
       3430-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3500-READ-BP13K840.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES            TO K840-KEY-FLD.                              
           MOVE FA40-REGN-NO      TO K840-NUM-REGN.                             
           READ BP13K840.                                                       
                                                                                
           IF BP13K840-STATUS = 00                                              
              ADD 1    TO CNT-K840-FND                                          
              PERFORM 3510-MOVE-DATA-BP13K840 THRU 3510-EXIT                    
              PERFORM 3520-UPDATE-BP13K840    THRU 3520-EXIT                    
           ELSE                                                                 
              IF BP13K840-STATUS = 23                                           
                 PERFORM 3530-CREATE-BP13K840 THRU 3530-EXIT                    
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13K840 : ' BP13K840-STATUS          
                 MOVE BP13K840-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       3510-MOVE-DATA-BP13K840.                                                 
      *------------------------*                                                
                                                                                
           MOVE K800-NUM-NEW-TOWN        TO K840-NUM-NEW-TOWN.                  
           MOVE K800-NUM-FLAT-TYPE       TO K840-NUM-FLAT-TYPE.                 
           MOVE K800-NUM-IDP-SCHEME      TO K840-NUM-IDP-SCH.                   
           MOVE K800-NUM-ALLO-SCHEME     TO K840-NUM-ALLOC-SCH.                 
           MOVE SPACES                   TO K840-NUM-SCH-ACC.                   
           MOVE K800-DTE-REGN            TO K840-DTE-REGN.                      
           MOVE 'CA08'                   TO K840-NUM-USERID.                    
                                                                                
           MOVE WS-DATE                  TO K840-DTE-UPDATE.                    
                                                                                
           PERFORM 3511-CHECK-HISTORY THRU 3511-EXIT.                           
                                                                                
       3510-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       3511-CHECK-HISTORY.                                                      
      *--------------------*                                                    
                                                                                
           IF K840-DTE-REJECT1 = SPACES OR LOW-VALUES                           
              MOVE WS-DATE                TO K840-DTE-REJECT1                   
              MOVE WS-SCH-ACC             TO K840-NUM-REJECT-FLAT1              
           ELSE                                                                 
              IF K840-DTE-REJECT2 = SPACES OR LOW-VALUES                        
                 MOVE WS-DATE             TO K840-DTE-REJECT2                   
                 MOVE WS-SCH-ACC          TO K840-NUM-REJECT-FLAT2              
              ELSE                                                              
                 IF K840-DTE-REJECT3 = SPACES OR LOW-VALUES                     
                    MOVE WS-DATE          TO K840-DTE-REJECT3                   
                    MOVE WS-SCH-ACC   TO K840-NUM-REJECT-FLAT3                  
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3511-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3520-UPDATE-BP13K840.                                                    
      *---------------------*                                                   
                                                                                
           REWRITE BP13K840-REC.                                                
                                                                                
           IF BP13K840-STATUS = 00 OR 02                                        
              ADD 1    TO CNT-K840-REWRITE                                      
           ELSE                                                                 
              DISPLAY 'ERROR UPDATING - BP13K840 : ' BP13K840-STATUS            
              MOVE BP13K840-STATUS     TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       3520-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3530-CREATE-BP13K840.                                                    
      *---------------------*                                                   
                                                                                
           MOVE SPACES               TO BP13K840-REC.                           
           MOVE K800-NUM-REGN        TO K840-NUM-REGN.                          
           MOVE K800-NUM-NRIC1       TO K840-NUM-NRIC1.                         
                                                                                
           IF K800-NUM-NRIC2 = SPACES OR LOW-VALUES                             
              STRING '#' K800-NUM-REGN                                          
                     DELIMITED BY SIZE INTO K840-NUM-NRIC2                      
              END-STRING                                                        
           ELSE                                                                 
              MOVE K800-NUM-NRIC2        TO K840-NUM-NRIC2                      
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC3 = SPACES OR LOW-VALUES                             
              STRING '#' K800-NUM-REGN                                          
                     DELIMITED BY SIZE INTO K840-NUM-NRIC3                      
              END-STRING                                                        
           ELSE                                                                 
              MOVE K800-NUM-NRIC3        TO K840-NUM-NRIC3                      
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC4 = SPACES OR LOW-VALUES                             
              STRING '#' K800-NUM-REGN                                          
                     DELIMITED BY SIZE INTO K840-NUM-NRIC4                      
              END-STRING                                                        
           ELSE                                                                 
              MOVE K800-NUM-NRIC4        TO K840-NUM-NRIC4                      
           END-IF.                                                              
                                                                                
           MOVE K800-NUM-NEW-TOWN    TO K840-NUM-NEW-TOWN.                      
           MOVE K800-NUM-FLAT-TYPE   TO K840-NUM-FLAT-TYPE.                     
           MOVE K800-NUM-IDP-SCHEME  TO K840-NUM-IDP-SCH.                       
           MOVE K800-NUM-ALLO-SCHEME TO K840-NUM-ALLOC-SCH.                     
           MOVE SPACES               TO K840-NUM-SCH-ACC.                       
           MOVE WS-DATE              TO K840-DTE-REJECT1.                       
           MOVE WS-SCH-ACC           TO K840-NUM-REJECT-FLAT1.                  
           MOVE WS-DATE              TO K840-DTE-UPDATE.                        
                                                                                
           WRITE BP13K840-REC.                                                  
                                                                                
           IF BP13K840-STATUS = 00 OR 02                                        
              ADD 1    TO CNT-K840-WRITE                                        
           ELSE                                                                 
              DISPLAY 'ERROR WRITING - BP13K840 : ' BP13K840-STATUS             
              MOVE BP13K840-STATUS     TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       3530-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3600-READ-BM06K110.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES            TO K110-KEY-FLD.                              
           MOVE WS-SCH-ACC        TO K110-KEY-FLD.                              
           READ BM06K110.                                                       
                                                                                
           IF BM06K110-STATUS = 00                                              
              ADD 1    TO CNT-K110-FND                                          
              PERFORM 3700-READ-BM06K100      THRU 3700-EXIT                    
              PERFORM 3610-MOVE-DATA-BM06K110 THRU 3610-EXIT                    
              PERFORM 3620-UPDATE-BM06K110    THRU 3620-EXIT                    
           ELSE                                                                 
              IF BM06K110-STATUS = 23                                           
                 ADD 1        TO CNT-K110-NOTFND                                
                 MOVE SPACES  TO K110-REC                                       
                 INITIALIZE      K110-REC                                       
                 DISPLAY 'RECORD NOT FOUND IN BM06K110 : '                      
                          WS-SCH-ACC                                            
              ELSE                                                              
                 DISPLAY 'ERROR READING - BM06K110 : ' BM06K110-STATUS          
                 MOVE BM06K110-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       3610-MOVE-DATA-BM06K110.                                                 
      *------------------------*                                                
                                                                                
           MOVE ZEROES                TO   K110-AMT-POD                         
                                           K110-AMT-POF                         
                                           K110-DTE-ACCEPTANCE                  
                                           K110-PREV-SELLING-PRICE              
                                           K110-CURR-SELLING-PRICE              
                                           K110-1ST-OFFER-SELLING-PRICE         
                                           K110-AMT-OCS-DISCOUNT                
                                           K110-AMT-FLAT-DISCOUNT.              
                                                                                
           MOVE SPACES                TO   K110-CDE-POD-TAG                     
                                           K110-CDE-POF-TAG                     
                                           K110-ALLOC-SCH                       
                                           K110-CATEGORY                        
                                           K110-CDE-OCS-TAG.                    
                                                                                
           MOVE 'R'                   TO   K110-ALLOCN-STATUS.                  
                                                                                
           IF K110-FLAT-STATUS = 'ST' AND                                       
              (K110-REPOSSESSED-CODE = '00' OR '01')                            
              MOVE 'PPJ'              TO   K110-ALLOCN-TAG                      
           ELSE                                                                 
              MOVE 'REJ'              TO   K110-ALLOCN-TAG                      
           END-IF.                                                              
                                                                                
           STRING '##' K110-KEY-FLD (6:6)                                       
                  DELIMITED BY SIZE INTO   K110-REGN-NO.                        
                                                                                
           MOVE WS-DATE               TO   K110-DTE-LAST-UPD.                   
                                                                                
                                                                                
       3610-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3620-UPDATE-BM06K110.                                                    
      *---------------------*                                                   
                                                                                
           REWRITE K110-REC.                                                    
                                                                                
           IF BM06K110-STATUS = 00 OR 02                                        
              ADD 1    TO CNT-K110-REWRITE                                      
           ELSE                                                                 
              DISPLAY 'ERROR UPDATING - BM06K110 : ' BM06K110-STATUS            
              MOVE BM06K110-STATUS     TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       3620-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3700-READ-BM06K100.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES             TO K100-KEY-FLD.                             
           MOVE K110-ESTATE        TO K100-ESTATE.                              
           MOVE K110-NEIGHBOURHOOD TO K100-NEIGHBOURHOOD.                       
           MOVE K110-CONTRACT-NO   TO K100-CONTRACT-NO.                         
           MOVE K110-BLK-NO        TO K100-BLK-NO.                              
           READ BM06K100.                                                       
                                                                                
           IF BM06K100-STATUS = 00                                              
              ADD 1    TO CNT-K100-FND                                          
              PERFORM 3710-MOVE-DATA-BM06K100 THRU 3710-EXIT                    
              PERFORM 3720-UPDATE-BM06K100    THRU 3720-EXIT                    
           ELSE                                                                 
              IF BM06K100-STATUS = 23                                           
                 ADD 1        TO CNT-K100-NOTFND                                
                 MOVE SPACES  TO K100-REC                                       
                 INITIALIZE      K100-REC                                       
                 DISPLAY 'RECORD NOT FOUND IN BM06K100 : ' K110-AIX1            
              ELSE                                                              
                 DISPLAY 'ERROR READING - BM06K100 : ' BM06K100-STATUS          
                 MOVE BM06K100-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       3710-MOVE-DATA-BM06K100.                                                 
      *------------------------*                                                
                                                                                
           EVALUATE FA40-CDE-FLAT-TYPE                                          
               WHEN '1 '                                                        
                     MOVE  1 TO RM-PTR                                          
               WHEN '2 '                                                        
                     MOVE  2 TO RM-PTR                                          
               WHEN '3 '                                                        
                     MOVE  3 TO RM-PTR                                          
               WHEN '4 '                                                        
                     MOVE  4 TO RM-PTR                                          
               WHEN '5 '                                                        
                     MOVE  6 TO RM-PTR                                          
               WHEN '6 '                                                        
                     MOVE  7 TO RM-PTR                                          
               WHEN '7 '                                                        
                     MOVE  8 TO RM-PTR                                          
               WHEN '8 '                                                        
                     MOVE  9 TO RM-PTR                                          
           END-EVALUATE.                                                        
                                                                                
           ADD 1 TO K100-BAL-ALLOCN (RM-PTR).                                   
                                                                                
           IF K800-NUM-CAT = 1                                                  
              SUBTRACT 1 FROM K100-CAT1-ACCEPT (RM-PTR)                         
           END-IF.                                                              
                                                                                
           IF K800-NUM-CAT = 2                                                  
              SUBTRACT 1 FROM K100-CAT2-ACCEPT (RM-PTR)                         
           END-IF.                                                              
                                                                                
           IF K800-NUM-CAT = 3                                                  
              SUBTRACT 1 FROM K100-CAT3-ACCEPT (RM-PTR)                         
           END-IF.                                                              
                                                                                
       3710-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3720-UPDATE-BM06K100.                                                    
      *---------------------*                                                   
                                                                                
           REWRITE K100-REC.                                                    
                                                                                
           IF BM06K100-STATUS = 00 OR 02                                        
              ADD 1    TO CNT-K100-REWRITE                                      
           ELSE                                                                 
              DISPLAY 'ERROR UPDATING - BM06K100 : ' BM06K100-STATUS            
              MOVE BM06K100-STATUS     TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       3720-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3800-READ-BP13KD00.                                                      
      *-------------------*                                                     
                                                                                
           DISPLAY '3800-READ-BP13KD00 '                                        
           MOVE SPACES             TO KD00-KEY-FLD.                             
           MOVE K800-NUM-REGN      TO KD00-NUM-REGN.                            
           READ BP13KD00.                                                       
                                                                                
           IF BP13KD00-STATUS = 00                                              
              ADD 1    TO CNT-KD00-FND                                          
              IF KD00-DTE-REPUDIATE IS NOT NUMERIC OR                           
                 KD00-DTE-REPUDIATE = ZEROES                                    
                 PERFORM 3810-DELETE-BP13KD00 THRU 3810-EXIT                    
              END-IF                                                            
           ELSE                                                                 
              IF BP13KD00-STATUS = 23                                           
                 ADD 1    TO CNT-KD00-NOTFND                                    
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13KD00 : ' BP13KD00-STATUS          
                 MOVE BP13KD00-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3810-DELETE-BP13KD00.                                                    
      *---------------------*                                                   
                                                                                
           DELETE BP13KD00.                                                     
                                                                                
           EVALUATE BP13KD00-STATUS                                             
               WHEN 00                                                          
                    ADD 1 TO CNT-KD00-DELETE                                    
               WHEN OTHER                                                       
                    ADD 1 TO CNT-KD00-DELETE-ERROR                              
                    DISPLAY 'ERROR DELETING BP13KD00-REC ...'                   
                    MOVE BP13KD00-STATUS TO RETURN-CODE                         
                    PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3810-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3900-READ-BP13KD15.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES                      TO KD15-ALT-KEY1.                   
           IF FA40-REGN-NO(1:1) = '5'                                           
              MOVE K800-NUM-DBSS-PRJCT      TO KD15-NUM-PRJCT-KEY1              
           ELSE                                                                 
              MOVE FA40-REGN-NO             TO KB20-NUM-REGN                    
              PERFORM 3930-READ-BP13KB20  THRU 3930-EXIT                        
              MOVE KB20-NUM-PROJECT         TO KD15-NUM-PRJCT-KEY1              
           END-IF.                                                              
                                                                                
           MOVE FA40-REGN-NO                TO KD15-NUM-REGN.                   
           READ BP13KD15 KEY IS KD15-ALT-KEY1.                                  
                                                                                
           IF BP13KD15-STATUS = 00                                              
              ADD 1    TO CNT-K800-FND                                          
              PERFORM 3910-MOVE-DATA-BP13KD15 THRU 3910-EXIT                    
              PERFORM 3920-UPDATE-BP13KD15    THRU 3920-EXIT                    
           ELSE                                                                 
              IF BP13KD15-STATUS = 23                                           
                 ADD 1        TO CNT-KD15-NOTFND                                
                 DISPLAY 'RECORD NOT FOUND IN BP13KD15 : ' FA40-REGN-NO         
                 MOVE SPACES  TO BP13KD15-REC                                   
                 INITIALIZE      BP13KD15-REC                                   
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13KD15 : ' BP13KD15-STATUS          
                 MOVE BP13KD15-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3900-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       3910-MOVE-DATA-BP13KD15.                                                 
      *------------------------*                                                
                                                                                
           STRING '##' WS-NUM-SCH-ACC (6:6)                                     
                  DELIMITED BY SIZE INTO WS-NUM-REGN.                           
           MOVE WS-NUM-REGN           TO KD15-NUM-REGN                          
                                         KD15-NUM-REGN3                         
                                         KD15-DTE-DBSS-CONFIRM.                 
           MOVE SPACES                TO KD15-DTE-OPTION.                       
           MOVE WS-DATE               TO KD15-DTE-UPDATE.                       
           MOVE WS-CURRENT-TIME       TO KD15-TME-UPDATE.                       
           MOVE 'BP13CA08'            TO KD15-NUM-USERID.                       
                                                                                
       3910-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3920-UPDATE-BP13KD15.                                                    
      *---------------------*                                                   
                                                                                
           REWRITE BP13KD15-REC.                                                
                                                                                
           IF BP13KD15-STATUS = 00 OR 02                                        
              ADD 1    TO CNT-KD15-REWRITE                                      
           ELSE                                                                 
              DISPLAY 'ERROR UPDATING - BP13KD15 : ' BP13KD15-STATUS            
              MOVE BP13KD15-STATUS     TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       3920-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3930-READ-BP13KB20.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13KB20.                                                       
                                                                                
           IF BP13KB20-STATUS = 00                                              
              CONTINUE                                                          
           ELSE                                                                 
              IF BP13KB20-STATUS = 23                                           
                 MOVE SPACES              TO KB20-NUM-PROJECT                   
                 DISPLAY 'BP13KB20 - REC NOT FND, KEY ' KB20-NUM-REGN           
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13KB20 : ' BP13KB20-STATUS          
                 MOVE BP13KB20-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3930-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13CA08 *************'.                            
           DISPLAY 'NO OF RECS READ FROM  BP13FA40 : ' CNT-FA40-READ.           
           DISPLAY 'NO OF RECS FOUND   IN BP13K800 : ' CNT-K800-FND.            
           DISPLAY 'NO OF RECS NOTFND  IN BP13K800 : ' CNT-K800-NOTFND.         
           DISPLAY 'NO OF RECS UPDATED IN BP13K800 : ' CNT-K800-REWRITE.        
           DISPLAY 'NO OF RECS FOUND   IN BP13K840 : ' CNT-K840-FND.            
           DISPLAY 'NO OF RECS WRITE   IN BP13K840 : ' CNT-K840-WRITE.          
           DISPLAY 'NO OF RECS UPDATED IN BP13K840 : ' CNT-K840-REWRITE.        
           DISPLAY 'NO OF RECS FOUND   IN BM06K110 : ' CNT-K110-FND.            
           DISPLAY 'NO OF RECS NOTFND  IN BM06K110 : ' CNT-K110-NOTFND.         
           DISPLAY 'NO OF RECS UPDATED IN BM06K110 : ' CNT-K110-REWRITE.        
           DISPLAY 'NO OF RECS FOUND   IN BM06K100 : ' CNT-K100-FND.            
           DISPLAY 'NO OF RECS NOTFND  IN BM06K100 : ' CNT-K100-NOTFND.         
           DISPLAY 'NO OF RECS UPDATED IN BM06K100 : ' CNT-K100-REWRITE.        
           DISPLAY 'NO OF RECS FOUND   IN BP13KD00 : ' CNT-KD00-FND.            
           DISPLAY 'NO OF RECS NOTFND  IN BP13KD00 : ' CNT-KD00-NOTFND.         
           DISPLAY 'NO OF RECS DELETED IN BP13KD00 : ' CNT-KD00-DELETE.         
           DISPLAY 'NO OF DELETE ERROR IN BP13KD00 : '                          
                    CNT-KD00-DELETE-ERROR.                                      
           DISPLAY 'NO OF RECS FOUND   IN BP13KD15 : ' CNT-KD15-FND.            
           DISPLAY 'NO OF RECS NOTFND  IN BP13KD15 : ' CNT-KD15-NOTFND.         
           DISPLAY 'NO OF RECS UPDATED IN BP13KD15 : ' CNT-KD15-REWRITE.        
                                                                                
           CLOSE      BP13FA40                                                  
                      BP13KB20                                                  
                      BP13K800                                                  
                      BP13K840                                                  
                      BP13KD00                                                  
                      BM06K110                                                  
                      BM06K100                                                  
                      BP13KD15.                                                 
                                                                                
           IF BP13K800-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSING ERROR - BP13K800 : ' BP13K800-STATUS             
              MOVE BP13K800-STATUS TO RETURN-CODE.                              
                                                                                
           IF BP13K840-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSING ERROR - BP13K840 : ' BP13K840-STATUS             
              MOVE BP13K840-STATUS TO RETURN-CODE.                              
                                                                                
           IF BP13KD00-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSING ERROR - BP13KD00 : ' BP13KD00-STATUS             
              MOVE BP13KD00-STATUS TO RETURN-CODE.                              
                                                                                
           IF BM06K110-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSING ERROR - BM06K110 : ' BM06K110-STATUS             
              MOVE BM06K110-STATUS TO RETURN-CODE.                              
                                                                                
           IF BM06K100-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSING ERROR - BM06K100 : ' BM06K100-STATUS             
              MOVE BM06K100-STATUS TO RETURN-CODE.                              
                                                                                
           IF BP13KD15-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSING ERROR - BP13KD15 : ' BP13KD15-STATUS             
              MOVE BP13KD15-STATUS TO RETURN-CODE.                              
                                                                                
           IF BP13KB20-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSING ERROR - BP13KB20 : ' BP13KB20-STATUS             
              MOVE BP13KB20-STATUS TO RETURN-CODE.                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
