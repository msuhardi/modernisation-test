       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C21Y.                                                 
      *AUTHOR.        ELAINE S. ARGA.                                           
      *DATE-WRITTEN.  20/10/2010.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE   :  PROGRAM TO FILTER CASES WITH DTE-APPOINTMENT*            
      *                ON OR BEFORE THE CURRENT DATE               *            
      *                                                            *            
      *  INPUT FILES :  1)  BP13F800                               *            
      *                 2)  SY02F001  -  CURRENT DATE FILE         *            
      *                                                            *            
      *  OUTPUT FILE :  1)  P13F800C                               *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHGE NO  OIC   DATE     DESCRIPTION                        *            
      * -------- ----  ------   -----------                        *            
      * BP134006 ESA1 20101020  NEW PROGRAM                        *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800  ASSIGN        TO  BP13F800.                         
                                                                                
           SELECT SY02F001  ASSIGN        TO  SY02F001.                         
                                                                                
           SELECT P13F800C  ASSIGN        TO  P13F800C.                         
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F800                                                            
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS  ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 2000  CHARACTERS.                                   
       COPY BP13F800.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   P13F800C                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   P13F800C-MASTER      PIC X(2000).                                   
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-INDICATORS.                                                       
           05  WS-EOF-F800                PIC X       VALUE 'N'.                
           05  WS-EOF-F001                PIC X       VALUE 'N'.                
                                                                                
       01  WS-COUNTERS-POINTERS.                                                
           05  WS-READ-CNT-F800           PIC 9(7)    VALUE ZEROS.              
           05  WS-WRITE-F800C             PIC 9(7)    VALUE ZEROS.              
           05  WS-DTE-BK-APPT             PIC X(8)    VALUE SPACES.             
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           PERFORM  100-OPEN-FILES        THRU   100-EXIT.                      
           PERFORM  150-READ-SY02F001     THRU   150-EXIT                       
           PERFORM  200-READ-BP13F800     THRU   200-EXIT.                      
           PERFORM  300-PROCESS-F800      THRU   300-EXIT                       
                        UNTIL WS-EOF-F800 = 'Y'.                                
           PERFORM  9999-CLOSE-ROUTINE     THRU   9999-EXIT.                    
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  SY02F001                                                 
                       BP13F800                                                 
                OUTPUT P13F800C.                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       150-READ-SY02F001.                                                       
      *-------------------------------------------------------------            
           READ SY02F001 AT END                                                 
                MOVE 'Y'         TO     WS-EOF-F001                             
                GO TO 150-EXIT.                                                 
                                                                                
                                                                                
       150-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-BP13F800.                                                       
      *-------------------------------------------------------------            
           READ BP13F800 AT END                                                 
                MOVE 'Y'         TO     WS-EOF-F800                             
                GO TO 200-EXIT.                                                 
                                                                                
           ADD     1             TO     WS-READ-CNT-F800.                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS-F800.                                                        
      *-------------------------------------------------------------            
           IF F800-DTE-BK-APPT-MGPS NOT = SPACES AND LOW-VALUES                 
                                  AND ZEROES                                    
              MOVE F800-DTE-BK-APPT-MGPS TO WS-DTE-BK-APPT                      
           ELSE                                                                 
              MOVE F800-DTE-BK-APPT      TO WS-DTE-BK-APPT                      
           END-IF.                                                              
                                                                                
           IF WS-DTE-BK-APPT <= F001-DTE-CURRENT                                
              WRITE P13F800C-MASTER FROM BP13F800-MASTER                        
              ADD 1                  TO WS-WRITE-F800C                          
           END-IF.                                                              
                                                                                
           PERFORM  200-READ-BP13F800     THRU   200-EXIT.                      
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       9999-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY '  '.                                                        
           DISPLAY '*----------- BP13C21Y CONTROL LIST ----------*'.            
           DISPLAY '  '.                                                        
           DISPLAY 'NO OF BP13F800 READ        = ' WS-READ-CNT-F800.            
           DISPLAY 'NO OF P13F800C WRITTEN     = ' WS-WRITE-F800C.              
                                                                                
           CLOSE BP13F800                                                       
                 SY02F001                                                       
                 P13F800C.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
