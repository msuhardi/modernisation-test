       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13CFB1.                                                  
      *AUTHOR.       ZDD1.                                                      
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *  OBJECTIVE:                                                *            
      *  TO UPDATE STATUS TO 'C' WHEN RESPONSE IS 'N'              *            
      *                                                            *            
      *  I    :  BC03F853                                          *            
      *  I/O  :  BP13K800                                          *            
      * ========================================================== *            
      * ---------------------------------------------------------- *            
      * BP136912 ZDD 20170817   NEW PROGRAM                        *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BC03F853 ASSIGN TO BC03F853.                                  
           SELECT BC3F853A ASSIGN TO BC3F853A.                                  
           SELECT BP13RPT1 ASSIGN TO BP13RPT1.                                  
           SELECT BP13F213 ASSIGN TO BP13F213.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS WS-K800-STATUS.                            
                                                                                
           SELECT BP13K213 ASSIGN TO BP13K213                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K213-KEY-FLD                               
                  ALTERNATE KEY   IS K213-NUM-HP                                
                  FILE STATUS     IS WS-K213-STATUS.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BC03F853                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 100 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  BC03F853-REC.                                                        
           05  F853-KEY-FLD.                                                    
               10  F853-NUM-MOBILE               PIC X(8).                      
           05  F853-CDE-ACTION                   PIC X(5).                      
           05  F853-NUM-TAG                      PIC X.                         
           05  F853-DTE-CREATE                   PIC X(8).                      
           05  F853-TME-CREATE                   PIC X(8).                      
           05  F853-NME-CREATE                   PIC X(6).                      
           05  FILLER                            PIC X(64).                     
                                                                                
       FD  BC3F853A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 100 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  BC3F853A-REC PIC X(100).                                             
                                                                                
       FD  BP13RPT1                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 100 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  BP13RPT1-REC PIC X(100).                                             
                                                                                
       FD  BP13F213                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  BP13F213-REC PIC X(200).                                             
                                                                                
                                                                                
       FD  BP13K800                                                             
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K213                                                             
                      RECORD CONTAINS 200 CHARACTERS.                           
       COPY BP13K213.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-SWITCHES.                                                         
           05  WS-K800-STATUS            PIC 99    VALUE ZEROS.                 
           05  WS-K213-STATUS            PIC 99    VALUE ZEROS.                 
           05  WS-F853-EOF               PIC X     VALUE 'N'.                   
           05  WS-K213-EOF               PIC X     VALUE 'N'.                   
           05  WS-K213-FND-TAG           PIC X     VALUE 'N'.                   
           05  WS-DUP                    PIC X     VALUE 'N'.                   
           05  WS-BALLOT-MON             PIC X(3)  VALUE SPACES.                
           05  WS-F853-READ              PIC 9(6)  VALUE ZEROS.                 
           05  WS-F853-WRT               PIC 9(6)  VALUE ZEROS.                 
           05  WS-F213-WRT               PIC 9(6)  VALUE ZEROS.                 
           05  WS-K213-READ              PIC 9(6)  VALUE ZEROS.                 
           05  WS-K213-NFND              PIC 9(6)  VALUE ZEROS.                 
           05  WS-K800-READ              PIC 9(6)  VALUE ZEROS.                 
           05  WS-K800-REWRITE           PIC 9(6)  VALUE ZEROS.                 
           05  WS-REPORT-WRT             PIC 9(6)  VALUE ZEROS.                 
           05  WS-ROFY                   PIC 9(6)  VALUE ZEROS.                 
           05  WS-SNO                    PIC 9(5)  VALUE ZEROS.                 
                                                                                
       01  WS-REPORT.                                                           
           05 WS-RPT1-SNO                 PIC X(5).                             
           05 WS-FILLER1                  PIC X(1).                             
           05 WS-RPT1-BAL-QTR             PIC X(12).                            
           05 WS-FILLER2                  PIC X(1).                             
           05 WS-RPT1-QUEUE               PIC X(05).                            
           05 WS-FILLER3                  PIC X(1).                             
           05 WS-RPT1-REGN                PIC X(08).                            
           05 WS-FILLER4                  PIC X(1).                             
           05 WS-RPT1-SMS-RESP            PIC X(01).                            
           05 WS-FILLER5                  PIC X(1).                             
           05 FILLER                      PIC X(64).                            
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-LOGIC.                                                          
            PERFORM 100-OPEN-FILES      THRU 100-EXIT.                          
            PERFORM 200-READ-BC03F853   THRU 200-EXIT.                          
            PERFORM 300-PROCESS-CASE    THRU 300-EXIT                           
                UNTIL WS-F853-EOF = 'Y'                                         
            PERFORM 999-CLOSE-FILES     THRU 999-EXIT.                          
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BC03F853                                                 
                       BP13K213                                                 
                I-O    BP13K800                                                 
                OUTPUT BC3F853A                                                 
                       BP13RPT1                                                 
                       BP13F213.                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, K800-STATUS ' WS-K800-STATUS              
              MOVE WS-K800-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           IF WS-K213-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, K213-STATUS ' WS-K213-STATUS              
              MOVE WS-K213-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
              STRING 'S/NO;BALLOT QTR;QUEUE NUM;REG NO; SMS RESPONSE;'          
              DELIMITED BY SIZE INTO WS-REPORT.                                 
                                                                                
              WRITE BP13RPT1-REC  FROM WS-REPORT.                               
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-BC03F853.                                                       
      *-------------------------------------------------------------            
           READ BC03F853 AT END                                                 
           MOVE 'Y'                      TO WS-F853-EOF                         
           GO TO 200-EXIT.                                                      
                                                                                
           ADD 1                         TO WS-F853-READ.                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS-CASE.                                                        
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACE                  TO WS-K213-FND-TAG.                      
           MOVE 'N'                    TO WS-DUP                                
           MOVE SPACES                 TO BP13K213-REC                          
                                          WS-BALLOT-MON.                        
           MOVE F853-NUM-MOBILE        TO K213-NUM-HP                           
                                                                                
           DISPLAY 'NUM HP - ' F853-NUM-MOBILE.                                 
           PERFORM 400-READ-BP13K213   THRU 400-EXIT.                           
           DISPLAY 'REGN NUM - ' K213-NUM-REGN.                                 
                                                                                
           IF WS-DUP = 'Y'                                                      
              PERFORM 200-READ-BC03F853 THRU 200-EXIT                           
              GO TO 300-EXIT                                                    
           END-IF.                                                              
                                                                                
           IF WS-K213-FND-TAG = 'N'                                             
              WRITE BC3F853A-REC  FROM BC03F853-REC                             
              ADD 1                 TO WS-F853-WRT                              
              PERFORM 200-READ-BC03F853 THRU 200-EXIT                           
              GO TO 300-EXIT                                                    
           ELSE                                                                 
             IF F853-CDE-ACTION = 'ROFY '                                       
                ADD 1 TO WS-ROFY                                                
             ELSE                                                               
                MOVE SPACES           TO BP13K800-MASTER                        
                MOVE K213-NUM-REGN    TO K800-NUM-REGN                          
                PERFORM 500-PROCESS-K800  THRU 500-EXIT                         
             END-IF                                                             
           END-IF.                                                              
                                                                                
           IF F853-CDE-ACTION = 'ROFN '                                         
              MOVE SPACES    TO  WS-REPORT                                      
              MOVE ';'       TO  WS-FILLER1 WS-FILLER2                          
                                 WS-FILLER3 WS-FILLER4                          
                                 WS-FILLER5                                     
              ADD 1 TO WS-SNO                                                   
              MOVE WS-SNO                     TO WS-RPT1-SNO                    
              PERFORM 350-CHECK-DTE-BALLOT  THRU 350-EXIT                       
              STRING WS-BALLOT-MON ' ' K800-DTE-BALLOT(1:4) ' '                 
                     K800-NUM-ALLO-CAT                                          
                     DELIMITED BY SIZE      INTO WS-RPT1-BAL-QTR                
              MOVE K213-NUM-QUEUE             TO WS-RPT1-QUEUE                  
              MOVE K800-NUM-REGN              TO WS-RPT1-REGN                   
              MOVE F853-CDE-ACTION(4:1)       TO WS-RPT1-SMS-RESP               
              WRITE BP13RPT1-REC            FROM WS-REPORT                      
              ADD 1                           TO WS-REPORT-WRT                  
           END-IF                                                               
                                                                                
           PERFORM 200-READ-BC03F853 THRU 200-EXIT.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       350-CHECK-DTE-BALLOT.                                                    
      *-------------------------------------------------------------            
           EVALUATE K800-DTE-BALLOT (5:2)                                       
              WHEN 01                                                           
                 MOVE 'JAN'  TO WS-BALLOT-MON                                   
              WHEN 02                                                           
                 MOVE 'FEB'  TO WS-BALLOT-MON                                   
              WHEN 03                                                           
                 MOVE 'MAR'  TO WS-BALLOT-MON                                   
              WHEN 04                                                           
                 MOVE 'APR'  TO WS-BALLOT-MON                                   
              WHEN 05                                                           
                 MOVE 'MAY'  TO WS-BALLOT-MON                                   
              WHEN 06                                                           
                 MOVE 'JUN'  TO WS-BALLOT-MON                                   
              WHEN 07                                                           
                 MOVE 'JUL'  TO WS-BALLOT-MON                                   
              WHEN 08                                                           
                 MOVE 'AUG'  TO WS-BALLOT-MON                                   
              WHEN 09                                                           
                 MOVE 'SEP'  TO WS-BALLOT-MON                                   
              WHEN 10                                                           
                 MOVE 'OCT'  TO WS-BALLOT-MON                                   
              WHEN 11                                                           
                 MOVE 'NOV'  TO WS-BALLOT-MON                                   
              WHEN 12                                                           
                 MOVE 'DEC'  TO WS-BALLOT-MON                                   
           END-EVALUATE.                                                        
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-READ-BP13K213.                                                       
      *-------------------------------------------------------------            
                                                                                
             READ BP13K213 KEY IS K213-NUM-HP.                                  
                                                                                
             IF WS-K213-STATUS = 00                                             
                ADD 1 TO  WS-K213-READ                                          
                MOVE 'Y' TO WS-K213-FND-TAG                                     
             ELSE                                                               
              IF WS-K213-STATUS = 02                                            
                 MOVE SPACES  TO BP13K213-REC                                   
                 MOVE 'N'     TO WS-K213-EOF                                    
                 MOVE F853-NUM-MOBILE        TO K213-NUM-HP                     
                 PERFORM 410-START-K213  THRU 410-EXIT                          
                 MOVE 'Y'  TO WS-DUP                                            
              ELSE                                                              
                IF WS-K213-STATUS = 23                                          
                   DISPLAY 'RECORD NOT FND IN BP13K213 ' WS-K213-STATUS         
                   MOVE 'N' TO WS-K213-FND-TAG                                  
                   ADD 1 TO  WS-K213-NFND                                       
                ELSE                                                            
                   DISPLAY 'ERROR READ BP13K213,STATUS ' WS-K213-STATUS         
                   MOVE WS-K213-STATUS     TO RETURN-CODE                       
                   GO TO 999-CLOSE-FILES.                                       
                                                                                
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       410-START-K213.                                                          
      *-------------------------------------------------------------            
            START BP13K213 KEY >= K213-NUM-HP                                   
                                                                                
             IF WS-K213-STATUS = 00 OR 02                                       
                PERFORM 420-READ-NEXT   THRU 420-EXIT                           
                    UNTIL  WS-K213-EOF = 'Y' OR                                 
                           K213-NUM-HP  NOT = F853-NUM-MOBILE                   
             ELSE                                                               
                IF WS-K213-STATUS = 23                                          
                   DISPLAY 'RECORD NOT FND IN BP13K213 ' WS-K213-STATUS         
                   MOVE 'N' TO WS-K213-FND-TAG                                  
                   ADD 1 TO  WS-K213-NFND                                       
                ELSE                                                            
                   DISPLAY 'ERROR READ BP13K213,STATUS ' WS-K213-STATUS         
                   MOVE WS-K213-STATUS     TO RETURN-CODE                       
                   GO TO 999-CLOSE-FILES.                                       
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
       420-READ-NEXT.                                                           
      *-------------------------------------------------------------            
           READ BP13K213 NEXT RECORD                                            
                                                                                
             IF WS-K213-STATUS = 00 OR 02                                       
                IF K213-NUM-HP = F853-NUM-MOBILE                                
                   WRITE BP13F213-REC  FROM BP13K213-REC                        
                   ADD 1 TO WS-F213-WRT                                         
                END-IF                                                          
             ELSE                                                               
                IF WS-K213-STATUS = 23 OR 10                                    
                   MOVE 'Y' TO WS-K213-EOF                                      
                ELSE                                                            
                   DISPLAY 'ERROR READNEXT K213,STATUS ' WS-K213-STATUS         
                   MOVE WS-K213-STATUS     TO RETURN-CODE                       
                   GO TO 999-CLOSE-FILES                                        
                END-IF                                                          
             END-IF.                                                            
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-PROCESS-K800.                                                        
      *-------------------------------------------------------------            
                                                                                
             READ BP13K800                                                      
                                                                                
             IF WS-K800-STATUS = 00                                             
                ADD 1 TO  WS-K800-READ                                          
                PERFORM 600-REWRITE-BP13K800 THRU 600-EXIT                      
             ELSE                                                               
                IF WS-K800-STATUS = 23                                          
                   DISPLAY 'RECORD NOT FND IN BP13K800 ' WS-K800-STATUS         
                ELSE                                                            
                   DISPLAY 'ERROR READ BP13K800,STATUS ' WS-K800-STATUS         
                   MOVE WS-K800-STATUS     TO RETURN-CODE                       
                   GO TO 999-CLOSE-FILES.                                       
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       600-REWRITE-BP13K800.                                                    
      *-------------------------------------------------------------            
             DISPLAY 'REGN NO - ' K800-NUM-REGN                                 
                     ' HP NUM - ' F853-NUM-MOBILE                               
                     ' CDE-ACT - ' F853-CDE-ACTION                              
                                                                                
             MOVE 'C'                    TO K800-NUM-STATUS                     
             MOVE '22'                   TO K800-NUM-CANCEL-REASON              
             MOVE 'RQ'                   TO K800-NUM-ALLOC-TAG                  
             MOVE FUNCTION CURRENT-DATE(1:8) TO K800-DTE-CANCEL                 
                                                                                
             REWRITE BP13K800-MASTER                                            
                                                                                
              IF WS-K800-STATUS = 00                                            
                 ADD 1                 TO WS-K800-REWRITE                       
              ELSE                                                              
                 DISPLAY 'ERROR REWRITING BP13K800 STATUS '                     
                                                 WS-K800-STATUS                 
                 MOVE WS-K800-STATUS      TO RETURN-CODE                        
                 PERFORM 999-CLOSE-FILES  THRU 999-EXIT                         
              END-IF.                                                           
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
       999-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
           DISPLAY '* ------------ BP13CFB1 ------------- * '.                  
           DISPLAY 'NO OF REC READ    BC03F853 = ' WS-F853-READ.                
           DISPLAY 'NO OF REC ROFY    BC03F853 = ' WS-ROFY.                     
           DISPLAY 'NO OF REC READ    BP13K213 = ' WS-K213-READ.                
           DISPLAY 'NO OF REC NOT FND BP13K213 = ' WS-K213-NFND.                
           DISPLAY 'NO OF REC WRITTEN BC3F853A = ' WS-F853-WRT.                 
           DISPLAY 'NO OF REC READ    BP13K800 = ' WS-K800-READ.                
           DISPLAY 'NO OF REC REWRITE BP13K800 = ' WS-K800-REWRITE.             
           DISPLAY 'NO OF REC WRITTEN ON REPORT= ' WS-REPORT-WRT.               
           DISPLAY 'NO OF DUPLICATE REG FOR HP = ' WS-F213-WRT.                 
                                                                                
           CLOSE BC03F853                                                       
                 BP13K213                                                       
                 BC3F853A                                                       
                 BP13RPT1                                                       
                 BP13F213                                                       
                 BP13K800.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K800, STATUS ' WS-K800-STATUS          
              MOVE WS-K800-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K213-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K213, STATUS ' WS-K213-STATUS          
              MOVE WS-K213-STATUS        TO RETURN-CODE.                        
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
