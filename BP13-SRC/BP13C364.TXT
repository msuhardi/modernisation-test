      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C364.                                                 
       AUTHOR.        MCC.                                                      
       DATE-WRITTEN.  07/06/03.                                                 
                                                                                
      ************************************************************              
      *                                                          *              
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                    *              
      *                                                          *              
      *    SYSTEM ID   : BP13                                    *              
      *                                                          *              
      *    OBJECTIVE   :                                         *              
      *    TO PROCESS COLLECTION SYSTEM CASH INTERFACE AND       *              
      *    UPDATE TO BP13K130.                                   *              
      *                                                          *              
      *     INPUT FILE :                                         *              
      *                  1. AC28F544 (COLL SYS INTERFACE FILE)   *              
      *                  2. BM06K110 (FIS FILE)                  *              
      *                  3. BP13K671 (BANK ORIG FILE)            *              
      *                                                          *              
      *     OUTPUT FILE:                                         *              
      *                  1. BP13K130 (SOCHIST   FILE)            *              
      *                                                          *              
      *                                                          *              
      *  CHG-NO   BY   ON        DESCRIPTION                     *              
      *  -------- ---  --------  ------------------------------  *              
      *  BP132362 MCC  07/06/03  NEW PROGRAM                     *              
      *  BP132526 SK   18/06/04  INCREASE BP13K671 TO 3000       *              
      *                                                          *              
      ************************************************************              
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
      *-----------------------*                                                 
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *-----------------------*                                                 
       INPUT-OUTPUT SECTION.                                                    
      *-----------------------*                                                 
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K130 ASSIGN         TO BP13K130                           
                           ACCESS MODE    IS DYNAMIC                            
                           ORGANIZATION   IS INDEXED                            
                           RECORD KEY     IS K130-KEY-FLD                       
                           FILE STATUS    IS WS-K130-STATUS.                    
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K110-KEY-FLD                       
                            FILE STATUS   IS WS-K110-STATUS.                    
                                                                                
           SELECT BP13K671 ASSIGN         TO BP13K671                           
                           ACCESS MODE    IS RANDOM                             
                           ORGANIZATION   IS INDEXED                            
                           RECORD KEY     IS K671-NUM-REGN                      
                           FILE STATUS    IS WS-K671-STATUS.                    
                                                                                
           SELECT AC28F544 ASSIGN       TO AC28F544.                            
                                                                                
                                                                                
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
       FILE SECTION.                                                            
      *-----------------------*                                                 
                                                                                
       FD  AC28F544                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 130 CHARACTERS.                                      
       COPY AC28F544.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BP13K671                                                            
            RECORD CONTAINS 3000 CHARACTERS.                                    
       COPY BP13K671.                                                           
                                                                                
       FD  BP13K130                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13K130.                                                           
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------*          
       01  WS-VARIABLES.                                                        
           05  WS-COUNT-HISTORY           PIC 9(3)       VALUE ZEROS.           
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K130-STATUS             PIC 9(2)       VALUE ZEROES.          
           05  WS-K671-STATUS             PIC 9(2)       VALUE ZEROES.          
           05  WS-K110-STATUS             PIC 9(2)       VALUE ZEROES.          
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-K130-WRITE              PIC 9(5)       VALUE ZEROES.          
           05  WS-K110-READ               PIC 9(5)       VALUE ZEROES.          
           05  WS-K110-NOTFD              PIC 9(5)       VALUE ZEROES.          
           05  WS-K671-READ               PIC 9(5)       VALUE ZEROES.          
           05  WS-K671-NOTFD              PIC 9(5)       VALUE ZEROES.          
           05  WS-F544-READ               PIC 9(5)       VALUE ZEROES.          
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F544-EOF                PIC X          VALUE 'N'.             
           05  WS-K130-WRITTEN            PIC X          VALUE SPACE.           
                                                                                
                                                                                
      *-----------------------*                                                 
       PROCEDURE DIVISION.                                                      
      *-----------------------*                                                 
                                                                                
      ******************************************************************        
       000-MAIN.                                                                
      ******************************************************************        
           PERFORM 100-OPEN-FILES       THRU 100-EXIT.                          
           PERFORM 200-READ-AC28F544    THRU 200-EXIT.                          
           PERFORM 300-PROCESS-AC28F544 THRU 300-EXIT                           
                   UNTIL WS-F544-EOF = 'Y'.                                     
           PERFORM 900-CLOSE-FILES      THRU 900-EXIT.                          
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       100-OPEN-FILES.                                                          
      ******************************************************************        
           OPEN INPUT  AC28F544                                                 
                       BM06K110                                                 
                       BP13K671                                                 
                I-O    BP13K130.                                                
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR BM06K110. STATUS : ' WS-K110-STATUS        
              MOVE WS-K110-STATUS       TO   RETURN-CODE                        
              PERFORM 900-CLOSE-FILES THRU 900-EXIT.                            
                                                                                
           IF WS-K671-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR BP13K671. STATUS : ' WS-K671-STATUS        
              MOVE WS-K671-STATUS       TO   RETURN-CODE                        
              PERFORM 900-CLOSE-FILES THRU 900-EXIT.                            
                                                                                
           IF WS-K130-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR BP13K130. STATUS : ' WS-K130-STATUS        
              MOVE WS-K130-STATUS       TO   RETURN-CODE                        
              PERFORM 900-CLOSE-FILES THRU 900-EXIT.                            
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       200-READ-AC28F544.                                                       
      ******************************************************************        
           READ AC28F544    AT END                                              
                MOVE 'Y' TO WS-F544-EOF                                         
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1 TO WS-F544-READ.                                               
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       300-PROCESS-AC28F544.                                                    
      ******************************************************************        
           PERFORM 400-READ-BM06K110 THRU 400-EXIT.                             
                                                                                
           IF WS-K110-STATUS = 00                                               
              PERFORM 500-READ-BP13K671 THRU 500-EXIT.                          
                                                                                
           PERFORM 200-READ-AC28F544 THRU 200-EXIT.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       400-READ-BM06K110.                                                       
      ******************************************************************        
           MOVE SPACES           TO  K110-REC.                                  
           MOVE F544-NUM-HDBREF  TO  K110-KEY-FLD.                              
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-STATUS                                              
              WHEN 00                                                           
                   ADD  1             TO WS-K110-READ                           
                   MOVE K110-REGN-NO  TO F544-NUM-REF                           
              WHEN 23                                                           
                   ADD  1             TO WS-K110-NOTFD                          
                   DISPLAY 'REC NOTFD IN BM06K110. KEY ' K110-KEY-FLD           
              WHEN OTHER                                                        
                   MOVE WS-K110-STATUS     TO RETURN-CODE                       
                   DISPLAY 'ERROR READING BM06K110, STATUS = '                  
                                          WS-K110-STATUS                        
                   PERFORM 900-CLOSE-FILES THRU 900-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       500-READ-BP13K671.                                                       
      ******************************************************************        
           MOVE SPACES           TO BP13K671-REC.                               
           MOVE F544-NUM-REF     TO K671-NUM-REGN.                              
                                                                                
           READ BP13K671.                                                       
                                                                                
           EVALUATE WS-K671-STATUS                                              
              WHEN 00                                                           
                   ADD   1  TO WS-K671-READ                                     
                   MOVE  1  TO WS-COUNT-HISTORY                                 
                   PERFORM 600-WRITE-BP13K130 THRU 600-EXIT                     
              WHEN 23                                                           
                   ADD   1  TO WS-K671-NOTFD                                    
                   DISPLAY 'REC NOTFD IN BP13K671, KEY ' F544-NUM-REF           
              WHEN OTHER                                                        
                   MOVE WS-K671-STATUS    TO RETURN-CODE                        
                   DISPLAY 'ERROR READING BP13K671, STATUS '                    
                                                    WS-K671-STATUS              
                   PERFORM 900-CLOSE-FILES THRU 900-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       600-WRITE-BP13K130.                                                      
      ******************************************************************        
           PERFORM 700-MOVE-VALUES-TO-K130 THRU 700-EXIT.                       
                                                                                
           WRITE BP13K130-CDHIST.                                               
                                                                                
           IF WS-K130-STATUS = '00'                                             
              ADD 1 TO WS-K130-WRITE                                            
           ELSE                                                                 
              IF WS-K130-STATUS = '22'                                          
                 MOVE 'N' TO WS-K130-WRITTEN                                    
                 PERFORM 800-READ-BP13K130 THRU 800-EXIT UNTIL                  
                             WS-K130-WRITTEN = 'Y'                              
              ELSE                                                              
                 MOVE WS-K130-STATUS    TO RETURN-CODE                          
                 DISPLAY 'ERROR READING BP13K130. STATUS : '                    
                                                  WS-K130-STATUS                
                 PERFORM 900-CLOSE-FILES THRU 900-EXIT.                         
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       700-MOVE-VALUES-TO-K130.                                                 
      ******************************************************************        
           MOVE  SPACES                TO    BP13K130-CDHIST.                   
           INITIALIZE                        BP13K130-CDHIST.                   
                                                                                
           MOVE F544-NUM-REF           TO    K130-NUM-ORIG-REGN.                
           MOVE F544-DTE-TXN           TO    K130-DTE-POST                      
                                             K130-DTE-TRANS.                    
           MOVE WS-COUNT-HISTORY       TO    K130-COUNT-HISTORY.                
           MOVE F544-AMT-TXN           TO    K130-AMT-CASH.                     
           MOVE F544-NUM-CRDR          TO    K130-AMT-TYP.                      
           MOVE F544-NUM-HDBREF        TO    K130-NUM-SCH-ACCT.                 
           MOVE F544-NUM-RCPT          TO    K130-NUM-RECPT-JRNO.               
           MOVE SPACES                 TO    K130-NUM-NRIC                      
                                             K130-NME-NAME                      
                                             K130-CDE-ERROR                     
                                             K130-CDE-VALID                     
                                             K130-CDE-CANCEL                    
                                             K130-CDE-BO                        
                                             K130-NUM-FLAT-TYPE-SA              
                                             K130-NUM-FLAT-TYPE-RSL             
                                             K130-NUM-CPF-APPL.                 
                                                                                
           EVALUATE F544-NUM-RCPTCD                                             
              WHEN '6250G'                                                      
                   MOVE '23'            TO K130-TRANS-TYPE                      
                   MOVE 'LSE'           TO K130-PAYMENT-TYPE                    
              WHEN '0933D'                                                      
                   MOVE '23'            TO K130-TRANS-TYPE                      
                   MOVE 'CON'           TO K130-PAYMENT-TYPE                    
              WHEN '6041I'                                                      
                   MOVE '98'            TO K130-TRANS-TYPE                      
                   MOVE 'HB1'           TO K130-PAYMENT-TYPE                    
              WHEN '6034Z'                                                      
                   MOVE '99'            TO K130-TRANS-TYPE                      
                   MOVE 'HB2'           TO K130-PAYMENT-TYPE                    
           END-EVALUATE.                                                        
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       800-READ-BP13K130.                                                       
      ******************************************************************        
           MOVE F544-NUM-REF           TO    K130-NUM-ORIG-REGN.                
           MOVE F544-DTE-TXN           TO    K130-DTE-POST.                     
           ADD 1                       TO    WS-COUNT-HISTORY.                  
           MOVE WS-COUNT-HISTORY       TO    K130-COUNT-HISTORY.                
                                                                                
           READ BP13K130.                                                       
                                                                                
           IF WS-K130-STATUS = '00'                                             
              NEXT SENTENCE                                                     
           ELSE                                                                 
              IF  WS-K130-STATUS = '23'                                         
                  MOVE 'Y'  TO WS-K130-WRITTEN                                  
                  PERFORM 600-WRITE-BP13K130       THRU  600-EXIT.              
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ***************************************************************           
       900-CLOSE-FILES.                                                         
      ***************************************************************           
           DISPLAY '********* BP13C364 *********'.                              
           DISPLAY 'NO OF RECS PROCESSED (AC28F544) = ' WS-F544-READ.           
           DISPLAY 'NO OF RECS WRITTEN   (BP13K130) = ' WS-K130-WRITE.          
           DISPLAY 'NO OF RECS READ      (BM06K110) = ' WS-K110-READ.           
           DISPLAY 'NO OF RECS NOTFD     (BM06K110) = ' WS-K110-NOTFD.          
           DISPLAY 'NO OF RECS READ      (BP13K671) = ' WS-K671-READ.           
           DISPLAY 'NO OF RECS NOTFD     (BP13K671) = ' WS-K671-NOTFD.          
                                                                                
           CLOSE AC28F544                                                       
                 BM06K110                                                       
                 BP13K671                                                       
                 BP13K130.                                                      
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING ERROR BM06K110. STATUS : ' WS-K110-STATUS        
              MOVE WS-K110-STATUS       TO   RETURN-CODE.                       
                                                                                
           IF WS-K671-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING ERROR BP13K671. STATUS : ' WS-K671-STATUS        
              MOVE WS-K671-STATUS       TO   RETURN-CODE.                       
                                                                                
           IF WS-K130-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING ERROR BP13K130. STATUS : ' WS-K130-STATUS        
              MOVE WS-K130-STATUS       TO   RETURN-CODE.                       
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
