       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CH19.                                                 
      *AUTHOR.        PAULO CAMIA LEGASPI.                                      
      *DATE-WRITTEN.  20/04/2009.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVES: TO DETERMINE THE START AND END QUEUE NO. OF    *            
      *             BP13FH15 AND OUTPUT TO BP13FH17.               *            
      * ---------------------------------------------------------- *            
      * CHG REF   BY    DATE      DETAILS                          *            
      * -------   ====  ========= ================================ *            
      * BP133601  PCL3  20042009  NEW PROGRAM                      *            
      * BP134303  PCL3  18082011  TO CHECK QUEUE-NO IF ALPHANUMERIC*            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
      *-------------------------------------------------------------            
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT BP13FH15  ASSIGN        TO BP13FH15.                          
           SELECT BP13FH17  ASSIGN        TO BP13FH17.                          
                                                                                
      *-------------------------------------------------------------            
       DATA DIVISION.                                                           
      *-------------------------------------------------------------            
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13FH15                                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13FH15.                                                           
                                                                                
       FD   BP13FH17                                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13FH17.                                                           
                                                                                
      *-------------------------------------------------------------            
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01 WS-VARIABLES.                                                         
          05 WS-QUEUE-START       PIC 9(05) VALUE ZEROES.                       
          05 WS-QUEUE-END         PIC 9(05) VALUE ZEROES.                       
          05 WS-MAX-QUEUE         PIC 9(05) VALUE ZEROES.                       
          05 WS-MIN-QUEUE-5       PIC 9(05) VALUE ZEROES.                       
          05 WS-MAX-QUEUE-5       PIC 9(05) VALUE ZEROES.                       
          05 WS-FH15-REGN         PIC X(08) VALUE SPACES.                       
                                                                                
       01 WS-COUNTERS.                                                          
          05 WS-FH15-READ         PIC 9(08) VALUE ZEROES.                       
          05 WS-FH17-WRITE        PIC 9(08) VALUE ZEROES.                       
                                                                                
       01 WS-SWITCHES.                                                          
          05 WS-FH15-EOF                 PIC X(1) VALUE 'N'.                    
                                                                                
       01 WS-FH15-KEY.                                                          
          10 WS-FH15-MAIN-KEY.                                                  
             15 WS-FH15-NUM-SELECTION         PIC X(3).                         
             15 WS-FH15-DTE-BALLOT            PIC X(6).                         
             15 WS-FH15-NUM-NT                PIC X(3).                         
             15 WS-FH15-NUM-FT                PIC X(2).                         
             15 WS-FH15-NUM-ETHNIC            PIC X(1).                         
                                                                                
       01 WS-PREV-KEY.                                                          
          10 WS-PREV-MAIN-KEY.                                                  
             15 WS-PREV-NUM-SELECTION         PIC X(3).                         
             15 WS-PREV-DTE-BALLOT            PIC X(6).                         
             15 WS-PREV-NUM-NT                PIC X(3).                         
             15 WS-PREV-NUM-FT                PIC X(2).                         
             15 WS-PREV-NUM-ETHNIC            PIC X(1).                         
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13FH15  THRU 2000-EXIT.                          
           PERFORM 3000-PROCESS-DATA   THRU 3000-EXIT                           
              UNTIL WS-FH15-EOF = 'Y'.                                          
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13FH15                                                 
               OUTPUT  BP13FH17.                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13FH15.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13FH15 AT END                                                 
             MOVE 'Y'         TO WS-FH15-EOF                                    
             MOVE HIGH-VALUES TO WS-FH15-KEY                                    
             GO TO 2000-EXIT.                                                   
                                                                                
           ADD 1                   TO WS-FH15-READ.                             
                                                                                
           MOVE FH15-NUM-SELECTION TO WS-FH15-NUM-SELECTION.                    
           MOVE FH15-DTE-BALLOT    TO WS-FH15-DTE-BALLOT.                       
           MOVE FH15-NUM-NT        TO WS-FH15-NUM-NT.                           
                                                                                
           IF FH15-NUM-FT = '1A'                                                
              MOVE '1 '            TO WS-FH15-NUM-FT                            
           ELSE                                                                 
              MOVE FH15-NUM-FT     TO WS-FH15-NUM-FT                            
           END-IF.                                                              
                                                                                
           MOVE FH15-NUM-ETHNIC    TO WS-FH15-NUM-ETHNIC.                       
                                                                                
           IF FH15-NUM-QUEUE(2:1) NOT NUMERIC                                   
              MOVE ZEROS           TO FH15-NUM-QUEUE(2:1)                       
           END-IF.                                                              
                                                                                
           MOVE ZEROS              TO FH15-NUM-QUEUE(1:1).                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
            MOVE ZEROES                               TO WS-QUEUE-START         
                                                         WS-QUEUE-END           
                                                         WS-MAX-QUEUE           
                                                         WS-MIN-QUEUE-5         
                                                         WS-MAX-QUEUE-5.        
                                                                                
            MOVE WS-FH15-KEY                          TO WS-PREV-KEY.           
            PERFORM 4000-GET-QUEUE-START-END        THRU 4000-EXIT              
              UNTIL WS-FH15-EOF = 'Y' OR                                        
                    WS-FH15-KEY NOT = WS-PREV-KEY.                              
                                                                                
            IF WS-QUEUE-START > 0 AND WS-QUEUE-END  > 0                         
               PERFORM 5000-WRITE-BP13FH17          THRU 5000-EXIT              
            END-IF.                                                             
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-GET-QUEUE-START-END.                                                
      *-------------------------------------------------------------            
                                                                                
            IF FH15-NUM-QUEUE NOT = SPACES AND LOW-VALUES AND ZEROES            
               IF WS-QUEUE-START      = ZEROES OR                               
                  WS-QUEUE-END        = ZEROES                                  
                  MOVE FH15-NUM-QUEUE              TO WS-QUEUE-START            
                                                      WS-QUEUE-END              
               END-IF                                                           
                                                                                
               IF FH15-NUM-QUEUE < WS-QUEUE-START                               
                  MOVE FH15-NUM-QUEUE              TO WS-QUEUE-START            
               ELSE                                                             
                  MOVE FH15-NUM-QUEUE              TO WS-MAX-QUEUE              
               END-IF                                                           
                                                                                
               IF WS-MAX-QUEUE > WS-QUEUE-END                                   
                  MOVE WS-MAX-QUEUE                TO WS-QUEUE-END              
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13FH15            THRU 2000-EXIT.                
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-WRITE-BP13FH17.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                         TO BP13FH17-REC.                 
           INITIALIZE                             BP13FH17-REC.                 
                                                                                
           MOVE WS-PREV-KEY                    TO FH17-KEY-FLD.                 
                                                                                
           IF WS-QUEUE-START >= 6                                               
              MOVE WS-QUEUE-START              TO WS-MIN-QUEUE-5                
              SUBTRACT 5                     FROM WS-MIN-QUEUE-5                
           ELSE                                                                 
              MOVE 1                           TO WS-MIN-QUEUE-5                
           END-IF.                                                              
                                                                                
           MOVE WS-QUEUE-END                   TO WS-MAX-QUEUE-5.               
           ADD  5                              TO WS-MAX-QUEUE-5.               
                                                                                
           MOVE WS-MIN-QUEUE-5                 TO FH17-NUM-QUEUE-START.         
           MOVE WS-MAX-QUEUE-5                 TO FH17-NUM-QUEUE-END.           
                                                                                
           MOVE FH15-DTE-UPDATE                TO FH17-DTE-UPDATE.              
           MOVE FH15-TME-UPDATE                TO FH17-TME-UPDATE.              
           MOVE FH15-NUM-USERID                TO FH17-NUM-USERID.              
                                                                                
           WRITE BP13FH17-REC.                                                  
                                                                                
           ADD 1                               TO WS-FH17-WRITE.                
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' BP13CH19 CONTROL TOTAL '.                                  
           DISPLAY '------------------------------------------'.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECS READ FROM BP13FH15: ' WS-FH15-READ.              
           DISPLAY 'NO OF RECS WRITTEN   BP13FH17: ' WS-FH17-WRITE.             
                                                                                
           CLOSE BP13FH15                                                       
                 BP13FH17.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
