  /*=============================================================*/             
  /* EXCEPTIONAL REPORT FOR EXERCISE BEFORE BALLOTING            */             
  /* (ENDCHK1)                                                   */             
  /*-------------------------------------------------------------*/             
  /* REQ-NO   DATE      BY     DESCRIPTION                       */             
  /*-------------------------------------------------------------*/             
  /* BP133484 17112008 YCH2  NEW PROGRAM                         */             
  /* BP133648 20072008 IL5   TO OUTPUT CASES WITH CAT BLANK ONLY */             
  /* BP13XXXX 15112009 IL5   TO CORRECT TAGGING OF MCPS EXCP     */             
  /* BP136756 20042017 SMR2  REMOVE TCP 1A COLUMN / REPLACE TPS  */             
  /*                         HH COLUMN WITH TPS NTIMER = Y       */             
  /* BP137310 04052018 JF7   TO REPORT MCP-B(Y) OR C(Y) CASES IF */             
  /*                         IF MCPS CODE 1,T OR A, EXCLUDE      */             
  /*                         ELDERLY TAG AND NON-MATURE TOWN ONLY*/             
  /*=============================================================*/             
     DATA BP13TAG;                                                              
     INFILE BP13F730;                                                           
     INPUT  @68   MTH_RESIDE $CHAR3.                                            
            @116  HH         $CHAR1.                                            
            @118  MCPS       $CHAR1.                                            
            @123  REGN       $CHAR8.                                            
            @175  YR05       $CHAR1.                                            
            @187  NT1        $CHAR3.                                            
            @209  NTIMER     $CHAR1.                                            
            @210  ELDERLY    $CHAR1.                                            
            @211  FT         $CHAR2.                                            
            @213  ALLO_SCH   $CHAR3.                                            
            @225  CAT        $CHAR1.                                            
            @248  ELIG_SCH   $CHAR3.                                            
            @253  CDE_RES1   $CHAR1.                                            
            @258  ELIG_TAG   $CHAR2.                                            
            @325  RES_NRIC1  $CHAR9.                                            
            @334  DTE_BALLOT $CHAR6.                                            
            @347  PPO        $CHAR6.                                            
            @360  CDE_DEBAR  $CHAR2.                                            
            @376  ALLO_MODE  $CHAR3.                                            
            @379  RES_NRIC2  $CHAR9.                                            
            @446  LOAN_REQ   $CHAR1. ;                                          
                                                                                
     IF HH=' '   THEN                                                           
        TAG_HH='Y';                                                             
     IF CAT=' '  THEN                                                           
        TAG_CAT='Y';                                                            
     IF YR05='N' THEN                                                           
        TAG_5YRN='Y';                                                           
     IF (YR05='Y' AND (MTH_RESIDE^='000') AND (MTH_RESIDE<'060')) THEN          
        TAG_5YRY='Y';                                                           
     IF ALLO_SCH=' ' OR                                                         
        ELIG_SCH=' ' THEN                                                       
        TAG_ALLO_ELIG='Y';                                                      
     IF DTE_BALLOT=' ' THEN                                                     
        TAG_DTE='Y';                                                            
     IF ELIG_TAG='RR' THEN                                                      
        TAG_DUAL_RR='Y';                                                        
     IF ELIG_TAG='SL' THEN                                                      
        TAG_DUAL_SL='Y';                                                        
     IF ELIG_TAG='RS' THEN                                                      
        TAG_DUAL_RS='Y';                                                        
     IF ALLO_SCH='TPS' AND FT>='4 ' THEN                                        
        TAG_TPS_N3='Y';                                                         
     IF ALLO_SCH='TPS' AND NTIMER='Y' THEN                                      
        TAG_TPS_NTMER='Y';                                                      
     IF (MCPS='C' OR MCPS='P') THEN                                             
        DO;                                                                     
           IF (CDE_RES1=' ' AND (RES_NRIC1^=' ' AND RES_NRIC2^=' '))            
               THEN TAG_MCP_BLNK='Y';                                           
           IF (CDE_RES1^=' ' AND (RES_NRIC1=' ')) THEN                          
               TAG_MCP_Y='Y';                                                   
        END;                                                                    
     IF NT1='   '  THEN                                                         
        TAG_NT1='Y';                                                            
     IF FT='  ' THEN                                                            
        TAG_FT='Y';                                                             
     IF ALLO_MODE='BE ' AND LOAN_REQ=' ' THEN                                   
        TAG_LOAN='Y';                                                           
     IF CDE_DEBAR^=' ' THEN                                                     
        TAG_DEBAR='Y';                                                          
     IF PPO^=' ' THEN                                                           
        DECL_PPO='Y';  /* <== PPO DECLARE BY APPLICANT */                       
                                                                                
     /*---------------------------------------------*/                          
     /* CHECK ON NON-MATURE TOWN                    */                          
     /*---------------------------------------------*/                          
                                                                                
      LENGTH K813_KEY $11.;                                                     
      K813_KEY = NT1||FT||DTE_BALLOT;                                           
                                                                                
      INFILE BP13K813 VSAM KEY=K813_KEY;                                        
      INPUT @;                                                                  
      RC=_IORC_; FBC=_FDBK_;                                                    
      IF RC=0 THEN                                                              
         DO;                                                                    
           INPUT  @249 MATURE_EST_TAG $CHAR1.;                                  
           IF (MATURE_EST_TAG ^= 'Y')              AND                          
              ((ELDERLY^='S') AND (ELDERLY^='F'))  AND                          
              (MCPS='B' OR MCPS='C')               AND                          
              (CDE_RES1='1' OR CDE_RES1='A' OR CDE_RES1='T') THEN               
              MCPS_CDE_BC='Y';                                                  
         END;                                                                   
      ELSE                                                                      
          _ERROR_=0;                                                            
                                                                                
     /*---------------------------------------------*/                          
     /* CHECK ON MCPS ELIGIBILITY IN REQ VS PORTAL  */                          
     /*---------------------------------------------*/                          
                                                                                
      LENGTH KB10KEY $8.;                                                       
      KB10KEY=REGN;                                                             
                                                                                
      INFILE BP13KB10 VSAM KEY=KB10KEY GENKEY;                                  
      INPUT @;                                                                  
      RC=_IORC_; FBC=_FDBK_;                                                    
      IF RC=0 THEN                                                              
         DO;                                                                    
           INPUT  @37 MCP_PORTAL $CHAR1. @594 MCP_ELIG $CHAR1.;                 
           IF MCP_ELIG = 'Y' THEN                                               
              DO;                                                               
                 IF CDE_RES1^='1' AND                                           
                    CDE_RES1^='A' AND                                           
                    CDE_RES1^='T' THEN                                          
                    TAG_MCPS='Y';                                               
              END;                                                              
           ELSE                                                                 
              DO;                                                               
                 IF CDE_RES1='1' OR                                             
                    CDE_RES1='A' OR                                             
                    CDE_RES1='T' THEN                                           
                    TAG_MCPS='Y';                                               
              END;                                                              
         END;                                                                   
      ELSE                                                                      
          _ERROR_=0;                                                            
                                                                                
     /*-------------------------------------------*/                            
     /* OUTPUT CASES WITH TAG='Y'                 */                            
     /*-------------------------------------------*/                            
                                                                                
     IF TAG_HH='Y' OR                                                           
        TAG_CAT='Y' OR                                                          
        TAG_5YRN='Y' OR                                                         
        TAG_5YRY='Y' OR                                                         
        TAG_ALLO_ELIG='Y' OR                                                    
        TAG_DTE='Y' OR                                                          
        TAG_DUAL_RR='Y' OR                                                      
        TAG_DUAL_SL='Y' OR                                                      
        TAG_DUAL_RS='Y' OR                                                      
        TAG_TPS_N3='Y' OR                                                       
        TAG_TPS_NTMER='Y' OR                                                    
        TAG_MCP_BLNK='Y' OR                                                     
        TAG_MCP_Y='Y' OR                                                        
        TAG_MCPS='Y' OR                                                         
        TAG_NT1='Y' OR                                                          
        TAG_FT='Y' OR                                                           
        TAG_LOAN='Y' OR                                                         
        TAG_DEBAR='Y' OR                                                        
        DECL_PPO='Y'  OR                                                        
        MCPS_CDE_BC='Y'                                                         
     THEN  OUTPUT BP13TAG;                                                      
                                                                                
 PROC SORT; BY REGN;                                                            
                                                                                
 DATA _NULL_;                                                                   
 SET BP13TAG; BY REGN;                                                          
                                                                                
    IF TAG_DUAL_RS='Y' THEN                                                     
       DO; TAG_DUAL_RR='Y'; TAG_DUAL_SL='Y'; END;                               
                                                                                
    FILE BP13L555 HEADER=H LINESLEFT=LL NOTITLES;                               
    SNO+1;                                                                      
                                                                                
    PUT @1  SNO                                                                 
        @6   REGN          $CHAR8.                                              
        @17  TAG_HH        $CHAR1.                                              
        @22  TAG_CAT       $CHAR1.                                              
        @26  TAG_ALLO_ELIG $CHAR1.                                              
        @31  TAG_DTE       $CHAR1.                                              
        @37  TAG_FT        $CHAR1.                                              
        @41  TAG_NT1       $CHAR1.                                              
        @45  TAG_LOAN      $CHAR1.                                              
        @52  TAG_5YRN      $CHAR1.                                              
        @65  TAG_DUAL_RR   $CHAR1.                                              
        @67  '/'                                                                
        @70  TAG_DUAL_SL   $CHAR1.                                              
        @78  TAG_TPS_N3    $CHAR1.                                              
        @84  TAG_TPS_NTMER $CHAR1.                                              
        @94  TAG_MCP_BLNK  $CHAR1.                                              
        @105 TAG_MCP_Y     $CHAR1.                                              
        @113 TAG_MCPS      $CHAR1.                                              
        @120 MCPS_CDE_BC   $CHAR1.                                              
        @126 CDE_DEBAR     $CHAR2.                                              
        @130 DECL_PPO      $CHAR1. @;                                           
                                                                                
        IF TAG_5YRY='Y' THEN                                                    
           PUT @61 MTH_RESIDE    $CHAR3.;                                       
        ELSE                                                                    
           PUT @61 '   ';                                                       
                                                                                
   PUT   @1  '-------------------------------------------------------'          
          @55  '------------------------------------------------------'         
          @109 '-----------------------';                                       
                                                                                
 RETURN;                                                                        
 H:                                                                             
   PAGE+1;                                                                      
   CUR_DATE=TODAY();                                                            
                                                                                
   PUT @1 'BP13L555      HDB3'                                                  
       @40 '             SOC  SYSTEM'                                           
       @104 'DATE : ' CUR_DATE DDMMYY10.                                        
       @123 'PAGE : ' PAGE;                                                     
   PUT @1  'BP13S555'                                                           
       @28 'EXCEPTIONAL REPORT BEFORE BALLOT COMMENCE FOR'                      
       @74 ALLO_MODE $CHAR3./;                                                  
   PUT @1  'DTE BALLOT : ' DTE_BALLOT /;                                        
   PUT @17  '<----------- BLANK ----------->'                                   
       @51  '<-- 5YR -->'                                                       
       @64  '<- DUAL ->'                                                        
       @77  '<--- TPS --->'                                                     
       @92  '<-------- MCPS --------------->'                                   
       @129 'DECL';                                                             
   PUT @44  'IND-'                                                              
       @17  '        ALL/ DTE'                                                  
       @51  '"N"   "Y"'                                                         
       @64  'REQ/APPLN'                                                         
       @77  'N3R/NTIMER=Y'                                                      
       @92  'TAG_Y     TAG_BLNK  SOC/'                                          
       @129 'PPO';                                                              
   PUT @1  'SNO'                                                                
       @6   'REGNO'                                                             
       @17  'HH  CAT ELG BALLOT FT NT1 LOAN'                                    
       @55  'MTH-MET'                                                           
       @92  'IC_BLNK IC_NOT_BLNK PORTAL B(Y)'                                   
       @125 'DEB'                                                               
       @129 'TAG';                                                              
   PUT /;                                                                       
  RETURN;                                                                       
                                                                                
                                                                                
