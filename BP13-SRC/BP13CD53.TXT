      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13CD53.                                                 
       AUTHOR.        JIANG BO.                                                 
       DATE-WRITTEN.  17/11/08.                                                 
                                                                                
      *========================================================*                
      *    SYSTEM NAME   :   SYSTEM OF COMMITMENT              *                
      *    SYSTEM ID     :   BP13                              *                
      *                                                        *                
      *    OBJECTIVE     : FOR DBSS FLAT                       *                
      *                  : WHEN DEVELOPER KET IN DTE-IND-TOP   *                
      *                  : UNIT TOP DATE :                     *                
      *                  : 1. UPDATE KEY-ISSUE-DATE IN FIS     *                
      *                  :    BM06K110                         *                
      *                  : 2. OUTPUT FILE TO SINGAPOER POWER   *                
      *                  :    DO NOT OUTPUT IN BP13C409        *                
      *                  :    JCL (BP13D400)                   *                
      *     INPUT  FILE  :                                     *                
      *        1.  BP13FD15                                    *                
      *        2.  BP13K800                                    *                
      *        3.  BP13K820                                    *                
      *     I-O    FILE  :                                     *                
      *        1.  BM06K110   -   UPDATE K110-DTE-KEY-ISSUE    *                
      *     OUTPUT FILE  :                                     *                
      *        1.  BP13F418   -   PUB UPDATE  (BP13F418)       *                
      *                                                        *                
      *  CHG REQ  DATE    BY   DESCRIPTION                     *                
      *  =======  ======  ==   ============================    *                
      * BP133298 201108 JB8 - NEW PROGRAM FOR DBSS             *                
      * BP133505 200109 EA1 - ADDED BF02F070 CUSTOMER INFO     *                
      * BP133858 230710 PCL3- REMOVE BF02F070 CUSTOMER INFO    *                
      * BP137888 280819 DN8 - TO MOVE SPACES TO NRIC & NAME    *                
      *========================================================*                
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FD15 ASSIGN       TO BP13FD15.                            
           SELECT BP13F418 ASSIGN       TO BP13F418.                            
           SELECT BM06K110 ASSIGN       TO BM06K110                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K110-SCH-ACC-NO                      
                           FILE STATUS  IS WS-K110-STATUS.                      
           SELECT BP13K800 ASSIGN       TO BP13K800                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K800-NUM-REGN                        
                           FILE STATUS  IS WS-K800-STATUS.                      
           SELECT BP13K820 ASSIGN       TO BP13K820                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K820-KEY-FLD                         
                           FILE STATUS  IS WS-K820-STATUS.                      
                                                                                
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13FD15                                                            
            RECORDING MODE  IS   F                                              
            BLOCK  CONTAINS 0    RECORDS                                        
            RECORD CONTAINS 300 CHARACTERS                                      
            LABEL  RECORDS  ARE  STANDARD.                                      
       COPY BP13FD15.                                                           
                                                                                
       FD   BP13F418                                                            
            RECORDING MODE  IS   F                                              
            BLOCK  CONTAINS 0    RECORDS                                        
            RECORD CONTAINS 240  CHARACTERS                                     
            LABEL  RECORDS  ARE  STANDARD.                                      
       COPY BP13F418.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500  CHARACTERS.                                    
       COPY BM06K110.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000  CHARACTERS.                                   
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400  CHARACTERS.                                    
       COPY BP13K820.                                                           
                                                                                
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  WS-PROGRAM-SWITCHES.                                                 
           05  WS-EOF                PIC X(2)   VALUE 'N'.                      
                                                                                
       01  WS-SQL-CODE               PIC 9(4)   VALUE ZEROS.                    
       01  WS-K110-STATUS            PIC 9(2)   VALUE ZEROS.                    
       01  WS-K800-STATUS            PIC 9(2)   VALUE ZEROS.                    
       01  WS-K820-STATUS            PIC 9(2)   VALUE ZEROS.                    
       01  WS-K800-FD                PIC X(1)   VALUE SPACES.                   
       01  WS-OCCP-NAME              PIC X(66)  VALUE SPACES.                   
       01  WS-COUNTERS.                                                         
           05  WS-FD15-READ          PIC 9(06)  VALUE ZEROES.                   
           05  WS-K110-UPDATE        PIC 9(06)  VALUE ZEROES.                   
           05  WS-K110-NOTFND        PIC 9(06)  VALUE ZEROES.                   
           05  WS-K800-NOTFND        PIC 9(06)  VALUE ZEROES.                   
           05  WS-K820-NOTFND        PIC 9(06)  VALUE ZEROES.                   
           05  WS-F418-WRITE         PIC 9(06)  VALUE ZEROES.                   
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CC                 PIC 99     VALUE ZEROS.                    
           05  WS-CURRENT-DTE.                                                  
               10  WS-YY             PIC 99     VALUE ZEROS.                    
               10  WS-MM             PIC 99     VALUE ZEROS.                    
               10  WS-DD             PIC 99     VALUE ZEROS.                    
                                                                                
       01  WS-CURR-DATE.                                                        
           05  WS-SYS-DATE.                                                     
               10  WS-CCYY           PIC 9(4)   VALUE 0000.                     
               10  WS-MM             PIC 99     VALUE 00.                       
               10  WS-DD             PIC 99     VALUE 00.                       
           05  WS-SYS-DATE-X         PIC X(08).                                 
           05  WS-TIME.                                                         
               10  WS-HR             PIC 9(2)   VALUE 00.                       
               10  WS-MIN            PIC 9(2)   VALUE 00.                       
               10  WS-SEC            PIC 9(2)   VALUE 00.                       
                                                                                
       01  WS-EDITED-DATE.                                                      
           05  WS-EDITED-DD          PIC 99     VALUE ZEROS.                    
           05  FILLER                PIC X      VALUE '/'.                      
           05  WS-EDITED-MM          PIC 99     VALUE ZEROS.                    
           05  FILLER                PIC X      VALUE '/'.                      
           05  WS-EDITED-CC          PIC 99     VALUE ZEROS.                    
           05  WS-EDITED-YY          PIC 99     VALUE ZEROS.                    
                                                                                
       01  WS-K820-KEY-FLD.                                                     
           05 WS-K820-NUM-REGN       PIC X(8).                                  
           05 WS-K820-NUM-NRIC       PIC X(9).                                  
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NUM-NRIC           PIC X(09).                                 
           05  WS-NME-OCCP           PIC X(66).                                 
           05  WS-K820-NUM-NRIC1     PIC X(09).                                 
           05  WS-K820-NME-OCCP1     PIC X(66).                                 
                                                                                
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH   PIC X(4).                                  
               10  WS-LINK-NUM-ACC   PIC X(5).                                  
       COPY P13COMM8.                                                           
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
                                                                                
      **********************                                                    
       100-MAIN.                                                                
      **********************                                                    
           PERFORM  200-OPENING-ROUTINE     THRU  200-EXIT.                     
           PERFORM  300-READ-FD15-REC       THRU  300-EXIT.                     
           PERFORM  400-PROCESS-FD15-REC    THRU  400-EXIT                      
                        UNTIL WS-EOF = 'Y'.                                     
           PERFORM  600-CLOSING-ROUTINE     THRU  600-EXIT.                     
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************                                                    
       200-OPENING-ROUTINE.                                                     
      **********************                                                    
           OPEN INPUT  BP13FD15                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                I-O    BM06K110                                                 
                OUTPUT BP13F418.                                                
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BM06K110 , '                               
                      'STATUS = ' WS-K110-STATUS                                
              GO TO 600-CLOSING-ROUTINE                                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K800 , '                               
                      'STATUS = ' WS-K800-STATUS                                
              GO TO 600-CLOSING-ROUTINE                                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K820 , '                               
                      'STATUS = ' WS-K820-STATUS                                
              GO TO 600-CLOSING-ROUTINE                                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE.                       
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-SYS-DATE WS-SYS-DATE-X.        
           MOVE FUNCTION CURRENT-DATE(9:6) TO WS-TIME.                          
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *         READ INPUT FILE - AT END SET INDICATOR                 *        
      ******************************************************************        
      **********************                                                    
       300-READ-FD15-REC.                                                       
      **********************                                                    
           READ BP13FD15 AT END                                                 
                         MOVE 'Y' TO WS-EOF                                     
                         GO TO 300-EXIT.                                        
           ADD 1 TO WS-FD15-READ.                                               
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************                                                    
       400-PROCESS-FD15-REC.                                                    
      **********************                                                    
                                                                                
           PERFORM 411-READ-BM06K110          THRU  411-EXIT.                   
           IF WS-K110-STATUS = 00                                               
              IF K110-DTE-KEY-ISSUED = SPACES OR LOW-VALUES OR ZEROES           
                 MOVE ZEROES                    TO  K110-DTE-KEY-ISSUED         
              END-IF                                                            
              IF K110-DTE-KEY-ISSUED = ZEROES                                   
                 MOVE SPACES                    TO  BP13K820-REC                
                 PERFORM 420-CALL-BP13C913    THRU  420-EXIT                    
                 PERFORM 440-READ-BP13K800    THRU  440-EXIT                    
                 IF WS-K800-FD = 'Y'                                            
                                                                                
                    MOVE SPACES                 TO  WS-NUM-NRIC                 
                                                    WS-NME-OCCP                 
                                                    WS-K820-NUM-NRIC1           
                                                    WS-K820-NME-OCCP1           
                                                                                
                    IF K800-NUM-NRIC1(1:1) NOT = SPACES AND                     
                                                 LOW-VALUES AND '#'             
                       MOVE K800-NUM-NRIC1      TO WS-NUM-NRIC                  
                                                   WS-K820-NUM-NRIC             
                                                   WS-K820-NUM-NRIC1            
                       MOVE K800-NUM-REGN       TO WS-K820-NUM-REGN             
                       PERFORM 450-READ-BP13K820   THRU 450-EXIT                
                       MOVE K820-NME-OCCP       TO WS-NME-OCCP                  
                                                   WS-K820-NME-OCCP1            
                    END-IF                                                      
                 END-IF                                                         
                 PERFORM 430-OUTPUT-BP13F418     THRU  430-EXIT                 
              END-IF                                                            
              IF K110-DTE-KEY-ISSUED NOT = FD15-DTE-IND-TP                      
                 PERFORM 412-REWRITE-BM06K110 THRU  412-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM  300-READ-FD15-REC         THRU  300-EXIT.                   
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **********************                                                    
       411-READ-BM06K110.                                                       
      **********************                                                    
                                                                                
           MOVE SPACES                     TO K110-REC.                         
           INITIALIZE                         K110-REC.                         
                                                                                
           MOVE FD15-NUM-SCH-ACC           TO K110-SCH-ACC-NO.                  
                                                                                
           READ BM06K110.                                                       
                                                                                
           IF WS-K110-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-K110-STATUS = 23                                            
                 DISPLAY 'RECORD NOT FOUND , STATUS = '                         
                         WS-K110-STATUS                                         
                 DISPLAY ' SCH-ACC = ' FD15-NUM-SCH-ACC                         
                 ADD 1 TO WS-K110-NOTFND                                        
              ELSE                                                              
                 DISPLAY 'ERROR READ BM06K110 , STATUS = '                      
                         WS-K110-STATUS                                         
                 DISPLAY ' SCH-ACC = ' FD15-NUM-SCH-ACC                         
                 GO TO 600-CLOSING-ROUTINE                                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       411-EXIT.                                                                
           EXIT.                                                                
                                                                                
      **********************                                                    
       412-REWRITE-BM06K110.                                                    
      **********************                                                    
                                                                                
           MOVE FD15-DTE-IND-TP        TO K110-DTE-KEY-ISSUED.                  
                                                                                
           REWRITE K110-REC.                                                    
                                                                                
           IF WS-K110-STATUS = 00                                               
              ADD 1                    TO WS-K110-UPDATE                        
           ELSE                                                                 
              DISPLAY 'ERROR REWRITING BM06K110 ,'                              
                      'STATUS = ' WS-K110-STATUS                                
           END-IF.                                                              
                                                                                
       412-EXIT.                                                                
           EXIT.                                                                
                                                                                
      **********************                                                    
       420-CALL-BP13C913.                                                       
      **********************                                                    
                                                                                
           MOVE SPACES                TO BP13COMM8-REC.                         
           INITIALIZE                    BP13COMM8-REC.                         
                                                                                
           MOVE FD15-NUM-SCH-ACC      TO WS-LINK-REC.                           
           CALL 'BP13C913' USING WS-LINK-REC,                                   
                                 BP13COMM8-REC.                                 
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              MOVE COMM8-CDE-SYSERR TO WS-SQL-CODE                              
              DISPLAY 'FD15-NUM-SCH-ACC =  ' FD15-NUM-SCH-ACC                   
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PIDB TABLE  ' WS-SQL-CODE        
              ELSE                                                              
                 DISPLAY 'ERROR READING PIDB TABLE  ' WS-SQL-CODE.              
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
      **********************                                                    
       430-OUTPUT-BP13F418.                                                     
      **********************                                                    
           MOVE    SPACES                 TO  BP13F418-REC.                     
           MOVE    FD15-DTE-IND-TP        TO  F418-DATE-SOLD.                   
           MOVE    FD15-NUM-SCH-ACC       TO  F418-SCHEME-ACCOUNT.              
           MOVE    COMM8-NUM-BLK          TO  F418-BLOCK.                       
           MOVE    COMM8-NME-STREET       TO  F418-STREETNAME.                  
           MOVE    COMM8-NUM-LEVEL        TO  F418-LEVEL.                       
           MOVE    COMM8-NUM-UNIT-MAIN    TO  F418-MAIN-UNIT.                   
           MOVE    COMM8-NUM-UNIT-SUB     TO  F418-SUB-UNIT.                    
           MOVE    COMM8-NUM-POSTAL-CODE  TO  F418-POSTCODE.                    
           MOVE    COMM8-CDE-ST           TO  F418-STCODE.                      
      *    MOVE    WS-K820-NME-OCCP1      TO  F418-NAME.                        
      *    INSPECT F418-NAME REPLACING   ALL  LOW-VALUES BY ' '.                
      *    MOVE    WS-K820-NUM-NRIC1      TO  F418-NRIC.                        
      *    INSPECT F418-NRIC REPLACING   ALL  LOW-VALUES BY ' '.                
           MOVE    SPACES                 TO  F418-NAME.                        
           MOVE    SPACES                 TO  F418-NRIC.                        
           MOVE    SPACES                 TO  F418-REMARKS.                     
           MOVE    WS-CURRENT-DATE        TO  F418-PROC-DATE.                   
           WRITE   BP13F418-REC.                                                
           ADD     1                      TO  WS-F418-WRITE.                    
                                                                                
       430-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ************************                                                  
       440-READ-BP13K800.                                                       
      ************************                                                  
                                                                                
           MOVE SPACES                      TO WS-K800-FD.                      
           MOVE SPACES                      TO BP13K800-MASTER.                 
           INITIALIZE                          BP13K800-MASTER.                 
                                                                                
           MOVE FD15-NUM-REGN               TO K800-NUM-REGN                    
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = 00                                               
              MOVE 'Y'                      TO WS-K800-FD                       
           ELSE                                                                 
              MOVE SPACES                   TO K820-NUM-NRIC                    
                                               K820-NME-OCCP                    
              DISPLAY 'ERROR READING BP13K800 , '                               
                      'STATUS = ' WS-K800-STATUS                                
              ADD 1 TO WS-K800-NOTFND                                           
           END-IF.                                                              
                                                                                
       440-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ************************                                                  
       450-READ-BP13K820.                                                       
      ************************                                                  
                                                                                
           MOVE SPACES                      TO BP13K820-REC.                    
           INITIALIZE                          BP13K820-REC.                    
                                                                                
           MOVE WS-K820-KEY-FLD             TO K820-KEY-FLD                     
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
              MOVE SPACES                   TO K820-NUM-NRIC                    
                                               K820-NME-OCCP                    
              DISPLAY 'ERROR READING BP13K820 , '                               
                      'STATUS = ' WS-K820-STATUS                                
              ADD 1 TO WS-K820-NOTFND                                           
           END-IF.                                                              
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ************************                                                  
       600-CLOSING-ROUTINE.                                                     
      ************************                                                  
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13CD53'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY 'NO OF BP13FD15 RECORDS READ............. : '                
                    WS-FD15-READ.                                               
           DISPLAY 'NO OF BM06K110 RECORDS UPDATED.......... : '                
                    WS-K110-UPDATE.                                             
           DISPLAY 'NO OF BM06K110 RECORDS NOT FOUND........ : '                
                    WS-K110-NOTFND.                                             
           DISPLAY 'NO OF BP13K800 RECORDS NOT FOUND........ : '                
                    WS-K800-NOTFND.                                             
           DISPLAY 'NO OF BP13K820 RECORDS NOT FOUND........ : '                
                    WS-K820-NOTFND.                                             
           DISPLAY 'NO OF BP13F418 RECORDS WRITTEN.......... : '                
                    WS-F418-WRITE.                                              
                                                                                
           CLOSE BP13FD15                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13F418                                                       
                 BM06K110.                                                      
                                                                                
           IF WS-K110-STATUS NOT = 00                                           
              DISPLAY 'ERROR CLOSING BM06K110 ,'                                
                      'STATUS = ' WS-K110-STATUS                                
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00                                           
              DISPLAY 'ERROR CLOSING BP13K800 ,'                                
                      'STATUS = ' WS-K800-STATUS                                
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00                                           
              DISPLAY 'ERROR CLOSING BP13K820 ,'                                
                      'STATUS = ' WS-K820-STATUS                                
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
