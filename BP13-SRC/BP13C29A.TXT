       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C29A.                                                 
      *AUTHOR.        IMC.                                                      
      *DATE-WRITTEN.  14/11/2014.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT  (SOC)                                 *           
      * =========================================================== *           
      *                                                             *           
      *  OBJECTIVE  : PROGRAM TO GENERATE BP13FD38 TO PASS TO SMS   *           
      *               RECORDS WITH LAST DUE DATE = CURRENT DATE     *           
      *                                                             *           
      *  INPUT  FILE :  1. BP13FD30                                 *           
      *                 2. BP13K800                                 *           
      *                 3. BP13K893                                 *           
      *  OUTPUT FILE :  1. BP13FD38                                 *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT BP13FD30 ASSIGN        TO BP13FD30.                           
                                                                                
           SELECT SY02F001 ASSIGN        TO SY02F001.                           
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS WS-BP13K800-STATUS.                        
                                                                                
           SELECT BP13K893 ASSIGN TO BP13K893                                   
                     ORGANIZATION IS INDEXED                                    
                      ACCESS MODE IS DYNAMIC                                    
                       RECORD KEY IS K893-KEY-FLD                               
                      FILE STATUS IS WS-BP13K893-STATUS.                        
                                                                                
           SELECT BP13FD38 ASSIGN        TO BP13FD38.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD  BP13FD30                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13FD30.                                                           
                                                                                
       FD   BP13K800.                                                           
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K893                                                            
            RECORD CONTAINS 2050 CHARACTERS.                                    
       COPY BP13K893.                                                           
                                                                                
       FD  BP13FD38                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13FD38.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-VARIABLES.                                                   
           05  WS-KD30-STATUS        PIC 9(02)  VALUE ZEROS.                    
           05  WS-BP13K800-STATUS    PIC 9(02)  VALUE ZEROES.                   
           05  WS-BP13K893-STATUS    PIC 9(02)  VALUE ZEROES.                   
           05  CNT-FD38-WRITE        PIC 9(05)  VALUE ZEROS.                    
           05  WS-CURRENT-DATE       PIC X(08)  VALUE ZEROES.                   
           05  WS-CNT-READ           PIC 9(05)  VALUE ZEROS.                    
           05  WS-FD30-EOF           PIC X(01)  VALUE SPACE.                    
           05  WS-UPDATE-PP          PIC X(01)  VALUE SPACE.                    
           05  WS-CDE-PP             PIC 9(02)  VALUE ZEROES.                   
           05  WS-AMT-LOAN           PIC S9(6)V99 VALUE ZEROES.                 
           05  WS-KD30-FND           PIC X(01)  VALUE SPACE.                    
               88  KD30-FND                     VALUE 'Y'.                      
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 1000-OPENING-ROUTINE THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13FD30   THRU 2000-EXIT.                         
           PERFORM 3000-MAIN-PROCESS    THRU 3000-EXIT                          
                   UNTIL WS-FD30-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPENING-ROUTINE.                                                    
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13FD30                                                 
                       BP13K800                                                 
                       BP13K893                                                 
                       SY02F001.                                                
           OPEN OUTPUT BP13FD38.                                                
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-CURRENT TO WS-CURRENT-DATE.                            
                                                                                
           IF WS-BP13K800-STATUS NOT = ZEROS AND 97                             
              DISPLAY 'FILE BP13K800 OPEN ERROR' WS-BP13K800-STATUS             
              MOVE WS-BP13K800-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-BP13K893-STATUS NOT = ZEROS AND 97                             
              DISPLAY 'FILE BP13K893 OPEN ERROR' WS-BP13K893-STATUS             
              MOVE WS-BP13K893-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13FD30.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13FD30                                                        
                AT END                                                          
                  MOVE 'Y'  TO WS-FD30-EOF                                      
                  GO TO 2000-EXIT                                               
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-CNT-READ.                                                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-MAIN-PROCESS.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES     TO BP13FD38-TRANS-REC                                
           INITIALIZE         BP13FD38-TRANS-REC                                
           MOVE 'N'        TO WS-UPDATE-PP                                      
           MOVE ZEROES     TO WS-CDE-PP                                         
                                                                                
           PERFORM 3200-READ-BP13K800         THRU 3200-EXIT.                   
                                                                                
           IF WS-UPDATE-PP = 'Y'                                                
              MOVE FD30-NUM-REGN                TO FD38-NUM-REGN                
              MOVE FD30-NUM-PP-SCHDL(WS-CDE-PP) TO FD38-NUM-SCHEDULE            
              MOVE FD30-NUM-SCH-ACC             TO FD38-NUM-SCH-ACC             
              MOVE FD30-AMT-PP-LOAN(WS-CDE-PP)                                  
                TO FD38-AMT-LOAN-NEW                                            
              MOVE FD30-DTE-PP-DUE(WS-CDE-PP)   TO FD38-DTE-DUE                 
              MOVE 'Y'                          TO FD38-NUM-LAST-LOAN           
                                                                                
              WRITE BP13FD38-TRANS-REC                                          
                                                                                
              ADD 1 TO CNT-FD38-WRITE                                           
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13FD30   THRU 2000-EXIT.                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       3200-READ-BP13K800.                                                      
      *------------------------------------------------------------             
                                                                                
           MOVE FD30-NUM-REGN    TO K800-NUM-REGN                               
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-BP13K800-STATUS                                          
              WHEN 00                                                           
              WHEN 10                                                           
                   PERFORM 3300-PROCESS-RECORD  THRU 3300-EXIT                  
              WHEN 23                                                           
                   PERFORM 5000-START-BP13K893  THRU 5000-EXIT                  
              WHEN OTHER                                                        
                   DISPLAY 'READ BP13K800 ERROR ' WS-BP13K800-STATUS            
                   MOVE WS-BP13K800-STATUS        TO RETURN-CODE                
                   PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3300-PROCESS-RECORD.                                                     
      ******************************************************************        
                                                                                
            IF K800-NUM-PAYMENT-SCHEME = 'P'                                    
               MOVE '10'       TO WS-CDE-PP                                     
               PERFORM 3400-CHECK-PP THRU 3400-EXIT                             
            ELSE                                                                
               IF K800-NUM-PAYMENT-SCHEME = 'D'                                 
                  MOVE '04'       TO WS-CDE-PP                                  
                  PERFORM 3400-CHECK-PP THRU 3400-EXIT                          
               END-IF                                                           
            END-IF.                                                             
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3400-CHECK-PP.                                                           
      ******************************************************************        
                                                                                
           IF FD30-NUM-PP-SCHDL(WS-CDE-PP)     = WS-CDE-PP AND                  
             (FD30-DTE-PP-DUE  (WS-CDE-PP)     = WS-CURRENT-DATE)               
              MOVE 'Y'                         TO WS-UPDATE-PP                  
           END-IF.                                                              
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-START-BP13K893.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                           TO BP13K893-MASTER.            
           INITIALIZE                               BP13K893-MASTER.            
                                                                                
           MOVE SPACES                           TO K893-KEY-FLD.               
           MOVE K800-NUM-REGN                    TO K893-NUM-REGN-HIST.         
                                                                                
           START BP13K893 KEY >= K893-KEY-FLD.                                  
                                                                                
           EVALUATE WS-BP13K893-STATUS                                          
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 5100-READ-BP13K893  THRU 5100-EXIT                  
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR STARTING BP13K893: '                         
                             WS-BP13K893-STATUS                                 
                            ' REGN = ' K893-NUM-REGN-HIST                       
                    PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5100-READ-BP13K893.                                                      
      ******************************************************************        
                                                                                
            READ  BP13K893 NEXT AT END                                          
                  MOVE SPACES               TO K893-KEY-FLD.                    
                                                                                
            EVALUATE WS-BP13K893-STATUS                                         
                WHEN 00                                                         
                WHEN 02                                                         
                     IF K800-NUM-REGN  = K893-NUM-REGN-HIST                     
                        MOVE BP13K893-MASTER TO BP13K800-MASTER                 
                        PERFORM 3300-PROCESS-RECORD THRU 3300-EXIT              
                     ELSE                                                       
                        DISPLAY K800-NUM-REGN ' NOT FOUND IN K893'              
                     END-IF                                                     
                WHEN 23                                                         
                     DISPLAY K800-NUM-REGN ' NOT FOUND IN K893'                 
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K893 : '                        
                              WS-BP13K893-STATUS                                
                             ' REGN = ' K893-NUM-REGN-HIST                      
                     PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                 
            END-EVALUATE.                                                       
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY '---------- BP13C29A ----------'.                            
           DISPLAY ' NO OF FD30 READ   : ' WS-CNT-READ.                         
           DISPLAY ' NO OF FD38 REWRITE: ' CNT-FD38-WRITE.                      
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13FD38                                                       
                 BP13K800                                                       
                 BP13K893                                                       
                 BP13FD30.                                                      
                                                                                
           IF  WS-BP13K800-STATUS NOT = ZEROS AND 97                            
               DISPLAY 'CLOSING ERROR '                                         
               DISPLAY 'WS-BP13K800-STATUS ' WS-BP13K800-STATUS.                
                                                                                
           IF  WS-BP13K893-STATUS NOT = ZEROS AND 97                            
               DISPLAY 'CLOSING ERROR '                                         
               DISPLAY 'WS-BP13K893-STATUS ' WS-BP13K893-STATUS.                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
