      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C185.                                                 
       AUTHOR.        TEREZA SALONGA.                                           
      *-----------------------------------------------------------*             
                                                                                
      *===========================================================*             
      *        SOC - S Y S T E M  O F  C O M M I T M E N T        *             
      *===========================================================*             
      *  OBJECTIVES :                                             *             
      *                                                           *             
      *    1.  TO WRITE ALL RECORDS OF BP13F730 WITH TOTAL        *             
      *        HOUSEHOLD INCOME > 10000 IN THE OUTPUT FILE AND    *             
      *        SET THEIR INCOME TAG TO Y.                         *             
      *                                                           *             
      *    INPUT FILES : BP13F730                                 *             
      *                  BP13K825                                 *             
      *                                                           *             
      *    OUTPUT FILE : P13F730F - LIST OF APPLICANTS WITH INCOME*             
      *                             TAG = Y                       *             
      *                                                           *             
      *    SUBROUTINES : NONE                                     *             
      *                                                           *             
      *---------------------------------------------------------- *             
      *===========================================================*             
      *  REVISION HISTORY :                                       *             
      *  CHG-REQ#  BY    DATE        DESCRIPTION                  *             
      *  --------  ----  ----------  ---------------------------- *             
      *  BP132886  MTSR1 13/07/06    NEW PROGRAM                  *             
      *  BP134356  ZDD11 08/09/11    INCREASE INCOME FROM 8K TO 10K             
      *                              FOR NON BTO AND SBF ELSE     *             
      *                              READ K816 FOR INCOME CEILING *             
      *  BP135190  SMR2  04/12/13    EXPAND BP13K813 TO 1000      *             
      *  BP136901  FNP1  04/12/13    INCLUDE ROF ALLO-CAT         *             
      *  BP137786   KV6  18/04/19    INCLUDE OBF ALLO-CAT         *             
      ************************************************************              
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
      *-----------------------*                                                 
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *-----------------------*                                                 
       INPUT-OUTPUT SECTION.                                                    
      *-----------------------*                                                 
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN    TO BP13F730.                               
                                                                                
           SELECT BP13K825 ASSIGN    TO BP13K825                                
                  ACCESS MODE        IS DYNAMIC                                 
                  ORGANIZATION       IS INDEXED                                 
                  RECORD KEY         IS K825-KEY-FLD                            
                  FILE STATUS        IS WS-K825-STATUS.                         
                                                                                
           SELECT BP13K813 ASSIGN    TO BP13K813                                
                  ACCESS MODE        IS DYNAMIC                                 
                  ORGANIZATION       IS INDEXED                                 
                  RECORD KEY         IS K813-KEY-FLD                            
                  FILE STATUS        IS WS-K813-STATUS.                         
                                                                                
           SELECT P13F730F ASSIGN    TO P13F730F.                               
                                                                                
                                                                                
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
       FILE SECTION.                                                            
      *-----------------------*                                                 
                                                                                
       FD   BP13F730                                                            
            BLOCK      CONTAINS 0    RECORDS                                    
            RECORD     CONTAINS 500  CHARACTERS                                 
            LABEL      RECORDS  ARE  STANDARD                                   
            RECORDING  MODE     IS  F.                                          
       COPY BP13F730.                                                           
                                                                                
       FD   BP13K825                                                            
            RECORD     CONTAINS 200 CHARACTERS.                                 
       COPY BP13K825.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD     CONTAINS 1000 CHARACTERS.                                
       COPY BP13K813.                                                           
                                                                                
       FD   P13F730F                                                            
            BLOCK      CONTAINS 0   RECORDS                                     
            RECORD     CONTAINS 500 CHARACTERS                                  
            LABEL      RECORDS  ARE OMITTED                                     
            RECORDING  MODE     IS  F.                                          
       01   P13F730F-REC        PIC X(500).                                     
                                                                                
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-K825-STATUS             PIC 9(2)    VALUE  0.                 
           05  WS-K813-STATUS             PIC 9(2)    VALUE  0.                 
           05  WS-K825-EOF-SW             PIC X(1)    VALUE 'N'.                
           05  WS-F730-EOF-SW             PIC X(1)    VALUE 'N'.                
                                                                                
       01  WS-RECORD-COUNTERS.                                                  
           05  WS-F730-READ-CTR           PIC 9(5)    VALUE 0.                  
           05  WS-F730-SKIP-CTR           PIC 9(5)    VALUE 0.                  
           05  WS-F730F-ADD-CTR           PIC 9(5)    VALUE 0.                  
                                                                                
       01  WS-F730F-REC.                                                        
           05 FILLER                      PIC X(17)   VALUE SPACES.             
           05 WS-F730F-INC-TAG            PIC X(01)   VALUE SPACES.             
           05 FILLER                      PIC X(482)  VALUE SPACES.             
                                                                                
       01  WS-HH-INCOME                   PIC 9(6)    VALUE ZEROES.             
       01  WS-INCOME-LEVEL                PIC 9(5)    VALUE 10000.              
       01  WS-SYSTEM-DATE                 PIC X(8)    VALUE SPACES.             
                                                                                
      *-----------------------*                                                 
       PROCEDURE DIVISION.                                                      
      *-----------------------*                                                 
                                                                                
      *-------------*                                                           
       0000-CONTROL.                                                            
      *-------------*                                                           
           PERFORM  1000-OPEN-FILES         THRU 1000-EXIT.                     
           PERFORM  2000-MAIN-ROUTINE       THRU 2000-EXIT                      
              UNTIL WS-F730-EOF-SW = 'Y'.                                       
           PERFORM  9000-CLOSE-FILES        THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
           OPEN INPUT  BP13F730                                                 
                       BP13K825                                                 
                       BP13K813                                                 
                OUTPUT P13F730F.                                                
                                                                                
           IF WS-K825-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K825 OPEN ERROR, STATUS IS ' WS-K825-STATUS          
              MOVE     WS-K825-STATUS       TO   RETURN-CODE                    
              PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K813 OPEN ERROR, STATUS IS ' WS-K813-STATUS          
              MOVE     WS-K813-STATUS       TO   RETURN-CODE                    
              PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO   WS-SYSTEM-DATE.                
           PERFORM 3000-READ-F730           THRU 3000-EXIT.                     
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       2000-MAIN-ROUTINE.                                                       
      *------------------*                                                      
           PERFORM  3500-READ-CTRL-K825    THRU 3500-EXIT.                      
                                                                                
           IF F730-NUM-ALLO-CAT = 'BTO' OR 'SBF' OR 'ROF' OR 'OBF'              
              PERFORM 4100-READ-BP13K816   THRU 4100-EXIT                       
              IF WS-HH-INCOME > K813-AMT-INCOME-CEILING                         
                PERFORM 4000-WRITE-F730F     THRU 4000-EXIT                     
              ELSE                                                              
                ADD 1 TO WS-F730-SKIP-CTR                                       
              END-IF                                                            
           ELSE                                                                 
             IF WS-HH-INCOME > WS-INCOME-LEVEL                                  
                PERFORM 4000-WRITE-F730F     THRU 4000-EXIT                     
             ELSE                                                               
                ADD 1 TO WS-F730-SKIP-CTR                                       
             END-IF                                                             
           END-IF.                                                              
                                                                                
           PERFORM  3000-READ-F730         THRU 3000-EXIT.                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------*                                                         
       3000-READ-F730.                                                          
      *---------------*                                                         
           READ BP13F730                                                        
              AT END MOVE 'Y'     TO WS-F730-EOF-SW                             
              GO TO  3000-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD 1                  TO WS-F730-READ-CTR.                          
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------*                                                    
       3500-READ-CTRL-K825.                                                     
      *--------------------*                                                    
           MOVE  F730-NUM-REGN TO K825-NUM-REGN                                 
           MOVE  SPACES        TO K825-NUM-NRIC.                                
                                                                                
           START BP13K825 KEY  IS >= K825-KEY-FLD END-START.                    
                                                                                
           INITIALIZE WS-HH-INCOME.                                             
           MOVE 'N' TO WS-K825-EOF-SW.                                          
                                                                                
           PERFORM 3600-READ-K825 THRU 3600-EXIT                                
              UNTIL WS-K825-EOF-SW = 'Y'.                                       
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------*                                                         
       3600-READ-K825.                                                          
      *---------------*                                                         
           READ BP13K825 NEXT                                                   
              AT END MOVE 'Y'     TO  WS-K825-EOF-SW                            
              GO TO 3600-EXIT.                                                  
                                                                                
           IF F730-NUM-REGN = K825-NUM-REGN                                     
              IF K825-AMT-INCOME  IS  NOT NUMERIC                               
                 MOVE ZEROES      TO  K825-AMT-INCOME                           
              END-IF                                                            
              ADD K825-AMT-INCOME TO  WS-HH-INCOME                              
           ELSE                                                                 
              MOVE 'Y'            TO  WS-K825-EOF-SW                            
           END-IF.                                                              
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       4000-WRITE-F730F.                                                        
      *-----------------*                                                       
           MOVE SPACES TO WS-F730F-REC                                          
           INITIALIZE     WS-F730F-REC.                                         
                                                                                
           MOVE BP13F730-REC TO WS-F730F-REC                                    
           MOVE 'Y'          TO WS-F730F-INC-TAG.                               
                                                                                
           WRITE P13F730F-REC FROM WS-F730F-REC.                                
           ADD 1 TO WS-F730F-ADD-CTR.                                           
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       4100-READ-BP13K816.                                                      
      *-----------------*                                                       
           MOVE SPACES               TO BP13K813-REC.                           
           MOVE F730-CDE-NT1         TO K813-NUM-ZONE                           
           MOVE F730-CDE-FLAT-TYPE   TO K813-NUM-FLAT-TYPE                      
           MOVE F730-DTE-BALLOT      TO K813-DTE-BALLOT                         
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
             WHEN 00                                                            
                IF K813-AMT-INCOME-CEILING IS NOT NUMERIC                       
                  MOVE ZEROES TO K813-AMT-INCOME-CEILING                        
                END-IF                                                          
             WHEN 23                                                            
                MOVE ZEROES TO K813-AMT-INCOME-CEILING                          
                DISPLAY 'RECORD NOT FOUND K813 KEY -'  K813-KEY-FLD             
             WHEN OTHER                                                         
                DISPLAY 'ERROR READING BP13K813 STAT -' WS-K813-STATUS          
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
           CLOSE BP13F730                                                       
                 BP13K825                                                       
                 BP13K813                                                       
                 P13F730F.                                                      
                                                                                
           IF WS-K825-STATUS NOT = 00                                           
              DISPLAY 'BP13K825 CLOSE ERROR, STATUS IS ' WS-K825-STATUS         
              MOVE WS-K825-STATUS TO RETURN-CODE                                
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = 00                                           
              DISPLAY 'BP13K813 CLOSE ERROR, STATUS IS ' WS-K813-STATUS         
              MOVE WS-K813-STATUS TO RETURN-CODE                                
           END-IF.                                                              
                                                                                
           DISPLAY '                           '                                
           DISPLAY 'PROGRAM         : BP13C185 '                                
           DISPLAY 'PROCESSING DATE : ' WS-SYSTEM-DATE                          
           DISPLAY 'NO. OF RECS READ IN F730     :' WS-F730-READ-CTR            
           DISPLAY 'NO. OF RECS W/ INCOME <10000 :' WS-F730-SKIP-CTR            
           DISPLAY 'OR LESS THAN THE INC-CEILING'                               
           DISPLAY 'NO. OF RECS WRITTEN IN F730F :' WS-F730F-ADD-CTR            
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
