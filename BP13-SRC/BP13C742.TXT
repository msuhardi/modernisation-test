      *========================*                                                
       IDENTIFICATION DIVISION.                                                 
      *========================*                                                
       PROGRAM-ID.    BP13C742.                                                 
       AUTHOR.        RAYATHATHA ASHOK.                                         
                                                                                
      **********************************************************                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE  : BASE ON THE SCH-ACC-NO IN F558 FILE    *                
      *                 TO EXTRACT FLAT TYPE FROM PBF          *                
      *                                                        *                
      * CHG-NO   BY   ON    - DESCRIPTION                      *                
      * ---------------------------------                      *                
      * BP130445 LSB 980206 - F558 EXPAND TO X(400)            *                
      * BP130445 MPA 980206 - REMOVE SQL & CALL BP13C904       *                
      * BP130897 AKT 000804 - EXPAND F558 RECL FROM 400 TO 600 *                
      * BP130883 RAS 000808 - TO CHANGE BP13C904 TO BP13C913   *                
      * BP130925 ANC 010704 - INCLUDE TRANSFER RECORDS         *                
      * BP135340 SMR 140422 - INCLUDE MODL-DESG-SMS            *                
      **********************************************************                
                                                                                
      *========================*                                                
       ENVIRONMENT DIVISION.                                                    
      *========================*                                                
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
           SELECT BP13OUT  ASSIGN TO BP13OUT.                                   
                                                                                
                                                                                
      *========================*                                                
       DATA DIVISION.                                                           
      *========================*                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F558                                                             
           BLOCK CONTAINS   0  RECORDS                                          
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORD IS STANDARD                                             
           RECORDING MODE IS F.                                                 
       COPY BP13F558.                                                           
                                                                                
       FD  BP13OUT                                                              
           BLOCK CONTAINS   0  RECORDS                                          
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORD IS STANDARD                                             
           RECORDING MODE IS F.                                                 
       01  BP13OUT-REC                     PIC X(600).                          
                                                                                
                                                                                
      *========================*                                                
       WORKING-STORAGE SECTION.                                                 
      *========================*                                                
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *=====================*                                                   
      *   FILE STATUS &     *                                                   
      *   CONTROL COUNT     *                                                   
      *=====================*                                                   
                                                                                
       77  WS-F558-EOF                      PIC X         VALUE 'N'.            
                                                                                
       01  WS-CONTROL-COUNT.                                                    
           05  WS-CNT-F558-READ             PIC 9(8)      VALUE ZERO.           
           05  WS-CNT-OUT-WRT               PIC 9(8)      VALUE ZERO.           
                                                                                
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH    PIC X(4).                                 
               10  WS-LINK-NUM-SCH    PIC X(5).                                 
           05  WS-LINK-NUM-LSECDE     PIC X(2).                                 
                                                                                
       01  WS-SQL-CODE                      PIC 9(4)   VALUE 0.                 
                                                                                
                                                                                
      *================*                                                        
       PROCEDURE DIVISION.                                                      
      *================*                                                        
                                                                                
      *================*                                                        
       0000-MAIN-LOGIC.                                                         
      *================*                                                        
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13F558   THRU 2000-EXIT.                         
           PERFORM 3000-MAIN-ROUTINE    THRU 3000-EXIT                          
                                            UNTIL  WS-F558-EOF = 'Y'.           
           PERFORM 9999-CLOSE-FILES     THRU 9999-EXIT.                         
                                                                                
                                                                                
      *================*                                                        
       1000-OPEN-FILES.                                                         
      *================*                                                        
           OPEN INPUT  BP13F558                                                 
                OUTPUT BP13OUT.                                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *===================*                                                     
       2000-READ-BP13F558.                                                      
      *===================*                                                     
           READ BP13F558 AT END                                                 
                        MOVE 'Y' TO WS-F558-EOF                                 
                        GO TO 2000-EXIT.                                        
                                                                                
           ADD 1 TO WS-CNT-F558-READ.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
      * RETRIEVE UNIT INFORMATION USING THE SCHEME AND ACCOUNT NUMBER *         
      * FROM THE BP13F558                                             *         
      *===============================================================*         
                                                                                
      *==================*                                                      
       3000-MAIN-ROUTINE.                                                       
      *==================*                                                      
           PERFORM 3100-CALL-BP13C913   THRU 3100-EXIT.                         
           PERFORM 3200-CALL-C913-TRF   THRU 3200-EXIT.                         
           PERFORM 4000-BP13OUT-WRITE   THRU 4000-EXIT.                         
           PERFORM 2000-READ-BP13F558   THRU 2000-EXIT.                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    CALL BP13C913 USING F410-NUM-SCH-ACC VALUES AS A KEY TO     *        
      *                  GET FLAT INFO FROM PBF TABLE                  *        
      ******************************************************************        
       3100-CALL-BP13C913.                                                      
           IF F558-NUM-HDB-REF-SMS = SPACES OR LOW-VALUE                        
              GO TO 3100-EXIT.                                                  
                                                                                
           MOVE  SPACES                    TO BP13COMM8-REC.                    
           INITIALIZE                         BP13COMM8-REC.                    
                                                                                
           MOVE  F558-NUM-HDB-REF-SMS      TO WS-LINK-REC.                      
           CALL 'BP13C913'   USING   WS-LINK-REC,                               
                                     BP13COMM8-REC.                             
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              MOVE COMM8-CDE-SYSERR      TO WS-SQL-CODE                         
              DISPLAY SPACES                                                    
              DISPLAY 'F558-NUM-HDB-REF-SMS = ' F558-NUM-HDB-REF-SMS            
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE   ' WS-SQL-CODE        
              ELSE                                                              
                 DISPLAY 'ERROR READING PBF TABLE   ' WS-SQL-CODE               
           ELSE                                                                 
              MOVE COMM8-CDE-TYP-ACTLUSE  TO F558-CDE-FLAT-TYPE-SMS             
              MOVE COMM8-CDE-MODL-DESG    TO F558-NUM-MODL-DESG                 
                                             F558-CDE-MODL-DESG-SMS             
              MOVE COMM8-CDE-CONFIG-DESG  TO F558-NUM-CONFIG-DESG               
              MOVE COMM8-NUM-MGMT         TO F558-CDE-BO                        
              MOVE COMM8-NUM-ED-GL        TO F558-NUM-ED                        
              MOVE COMM8-NUM-MNTNC        TO F558-NUM-MO                        
              MOVE COMM8-NME-STREET       TO F558-NME-STREET                    
              MOVE COMM8-NUM-POSTAL-CODE  TO F558-NUM-POSTAL-DSTRCT             
              MOVE COMM8-NUM-BLK          TO F558-NUM-BLK                       
              MOVE COMM8-NUM-LEVEL        TO F558-NUM-LVL                       
                                                                                
               STRING COMM8-NUM-UNIT-MAIN(1:4) COMM8-NUM-UNIT-SUB(1:1)          
               DELIMITED BY SIZE INTO F558-NUM-UNIT                             
                                                                                
              MOVE COMM8-NUM-ESTATE-CIM   TO F558-NUM-EST.                      
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3200-CALL-C913-TRF.                                                      
           IF F558-NUM-HDB-REF-TR = SPACES OR LOW-VALUE                         
              GO TO 3200-EXIT.                                                  
                                                                                
           MOVE  SPACES                    TO BP13COMM8-REC.                    
           INITIALIZE                         BP13COMM8-REC.                    
                                                                                
           MOVE  F558-NUM-HDB-REF-TR       TO WS-LINK-REC.                      
           CALL 'BP13C913'   USING   WS-LINK-REC,                               
                                     BP13COMM8-REC.                             
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              MOVE COMM8-CDE-SYSERR      TO WS-SQL-CODE                         
              DISPLAY SPACES                                                    
              DISPLAY 'F558-NUM-HDB-REF-TR = ' F558-NUM-HDB-REF-TR              
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE   ' WS-SQL-CODE        
              ELSE                                                              
                 DISPLAY 'ERROR READING PBF TABLE   ' WS-SQL-CODE               
           ELSE                                                                 
              MOVE COMM8-CDE-TYP-ACTLUSE(2:1) TO F558-CDE-FLAT-TYPE-TR.         
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===================*                                                     
       4000-BP13OUT-WRITE.                                                      
      *===================*                                                     
           WRITE BP13OUT-REC FROM BP13F558-REC.                                 
           ADD 1 TO WS-CNT-OUT-WRT.                                             
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=================*                                                       
       9999-CLOSE-FILES.                                                        
      *=================*                                                       
           DISPLAY ' '.                                                         
           DISPLAY ' **************** BP13C742 ******************** '.          
           DISPLAY '          GET FLAT TYPE FROM PBF                '.          
           DISPLAY ' '.                                                         
           DISPLAY 'TOTAL F558 REC READ     = ' WS-CNT-F558-READ.               
           DISPLAY 'TOTAL RECORDS WRITTEN   = ' WS-CNT-OUT-WRT.                 
                                                                                
           CLOSE      BP13F558                                                  
                      BP13OUT.                                                  
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
