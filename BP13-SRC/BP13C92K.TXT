      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13C92K.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  14 MAY 2015.                                              
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : TO CREATE BP13FW3D BASED ON                 *           
      *                 BP13F200 AND BP13K268/BP13KH15              *           
      *   INPUT FILES :                                             *           
      *   1.  BP13F200                                              *           
      *   2.  BP13K268                                              *           
      *   3.  BP13KH15 - READ IF NOT FOUND IN BP13K268              *           
      *   4.  BP13K800 - GET THE SCHEME ACCOUNT NUMBER              *           
      *   5.  SY02F001 - READ K268/KH15 ONLY IF APPT DATE <= CUR DTE*           
      *                                                             *           
      *   OUTPUT FILES:                                             *           
      *   1.  BP13FW3D                                              *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP136299 14/05/2015 FNP1 NEW PROGRAM                        *           
      * BP136299 06/05/2017 LSB1 ADD MGPS-FT                        *           
      * BP136832 31/05/2017 SMR2 CATER FOR MGPS-FT '3P'             *           
      *                          WHEN NT-ZONE IS NUMERIC, CASE IS   *           
      *                          CONSIDERED AS BTO                  *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F200 ASSIGN TO BP13F200.                                  
                                                                                
           SELECT BP13K268 ASSIGN TO BP13K268                                   
                  ACCESS MODE   IS DYNAMIC                                      
                  ORGANIZATION  IS INDEXED                                      
                  RECORD KEY    IS K268-KEY-FLD                                 
                  ALTERNATE KEY IS K268-NUM-REGN                                
                  FILE STATUS   IS BP13K268-STATUS.                             
                                                                                
           SELECT BP13KH15 ASSIGN TO BP13KH15                                   
                  ACCESS MODE   IS DYNAMIC                                      
                  ORGANIZATION  IS INDEXED                                      
                  RECORD KEY    IS KH15-KEY-FLD                                 
                  ALTERNATE KEY IS KH15-NUM-REGN                                
                  FILE STATUS   IS BP13KH15-STATUS.                             
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE   IS RANDOM                                       
                  ORGANIZATION  IS INDEXED                                      
                  RECORD KEY    IS K800-NUM-REGN                                
                  FILE STATUS   IS BP13K800-STATUS.                             
                                                                                
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
                                                                                
           SELECT BP13FW3D ASSIGN TO BP13FW3D.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13F200                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  300 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F200.                                                           
                                                                                
       FD   BP13K268                                                            
            RECORD CONTAINS 200  CHARACTERS.                                    
       COPY BP13K268.                                                           
                                                                                
       FD   BP13KH15                                                            
            RECORD CONTAINS 200  CHARACTERS.                                    
       COPY BP13KH15.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13FW3D                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  300 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13FW3D.                                                           
                                                                                
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  WS-CURR-DATE           PIC X(08) VALUE ZEROES.                       
       01  WS-F200-READ           PIC 9(08) VALUE ZEROES.                       
       01  WS-K268-FOUND          PIC 9(08) VALUE ZEROES.                       
       01  WS-K800-FOUND          PIC 9(08) VALUE ZEROES.                       
       01  WS-KH15-FOUND          PIC 9(08) VALUE ZEROES.                       
       01  WS-K800-MGPS           PIC 9(08) VALUE ZEROES.                       
       01  WS-K268-NFND           PIC 9(08) VALUE ZEROES.                       
       01  WS-KH15-NFND           PIC 9(08) VALUE ZEROES.                       
       01  WS-FW3D-WRITE          PIC 9(08) VALUE ZEROES.                       
       01  WS-F200-EOF            PIC X     VALUE SPACES.                       
       01  WS-K268-FND-SW         PIC X     VALUE SPACES.                       
       01  WS-KH15-FND-SW         PIC X     VALUE SPACES.                       
       01  BP13F200-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13FW3D-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13K268-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13KH15-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13K800-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  WS-NUM-SCH-ACC         PIC X(11) VALUE SPACES.                       
       01  WS-DTE-BKAPPMT-DATE    PIC X(08) VALUE ZEROES.                       
       01  WS-MGPS                PIC X(01) VALUE SPACES.                       
       01  WS-MGPS-FT-PAIR.                                                     
           05  WS-MGPS-PARENT-FT  PIC X(02) VALUE SPACES.                       
           05  WS-MGPS-CHILD-FT   PIC X(02) VALUE SPACES.                       
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-F200    THRU 2000-EXIT.                            
           PERFORM 3000-PROCESS-REC  THRU 3000-EXIT                             
                   UNTIL WS-F200-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13F200                                                 
                       BP13K268                                                 
                       BP13KH15                                                 
                       BP13K800                                                 
                       SY02F001                                                 
                OUTPUT BP13FW3D.                                                
                                                                                
           IF BP13K268-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K268 : ' BP13K268-STATUS             
              MOVE BP13K268-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13KH15-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KH15 : ' BP13KH15-STATUS             
              MOVE BP13KH15-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K800-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K800 : ' BP13K800-STATUS             
              MOVE BP13K800-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           ACCEPT WS-CURR-DATE FROM DATE YYYYMMDD.                              
                                                                                
           READ SY02F001.                                                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-F200.                                                          
      *---------------*                                                         
                                                                                
           READ BP13F200 AT END                                                 
                MOVE HIGH-VALUES TO BP13F200-REC                                
                MOVE 'Y' TO WS-F200-EOF                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F200-READ.                                               
           IF (F200-DTE-BKAPPMT-DATE-MGPS NOT = SPACES AND LOW-VALUES)          
              AND (F200-DTE-BKAPPMT-DATE-MGPS <= F001-DTE-CURRENT) AND          
              ((F200-DTE-BKAPPMT-DATE-MGPS <= F200-DTE-BKAPPMT-DATE) OR         
               (F200-DTE-BKAPPMT-DATE = SPACES OR LOW-VALUES))                  
               MOVE F200-DTE-BKAPPMT-DATE-MGPS TO WS-DTE-BKAPPMT-DATE           
           ELSE                                                                 
               MOVE F200-DTE-BKAPPMT-DATE      TO WS-DTE-BKAPPMT-DATE           
           END-IF.                                                              
                                                                                
           IF F200-NUM-NT-ZONE IS NUMERIC                                       
              MOVE 'BTO'                       TO F200-NUM-SELTYPE              
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           MOVE SPACES TO WS-MGPS.                                              
           MOVE SPACES TO WS-MGPS-PARENT-FT.                                    
           MOVE SPACES TO WS-MGPS-CHILD-FT.                                     
           IF WS-DTE-BKAPPMT-DATE NOT = SPACES                                  
              IF F001-DTE-CURRENT >= WS-DTE-BKAPPMT-DATE                        
                 PERFORM 3500-READ-BP13K268      THRU 3500-EXIT                 
                 IF WS-K268-FND-SW = 'Y'                                        
                    MOVE SPACES TO WS-NUM-SCH-ACC                               
                    MOVE K268-NUM-REGN TO K800-NUM-REGN                         
                    PERFORM 3600-READ-BP13K800   THRU 3600-EXIT                 
                    IF BP13K800-STATUS = ZEROS                                  
                       MOVE K800-NUM-SCH-ACC TO WS-NUM-SCH-ACC                  
                       MOVE K800-NUM-MGPS TO WS-MGPS                            
                       IF K800-NUM-MGPS = 'P'                                   
                          ADD 1 TO WS-K800-MGPS                                 
                          IF K800-NUM-FLAT-TYPE = '3P'                          
                             MOVE K800-NUM-FLAT-TYPE                            
                                             TO WS-MGPS-PARENT-FT               
                          ELSE                                                  
                             MOVE K800-NUM-ROOM                                 
                                             TO WS-MGPS-PARENT-FT(1:1)          
                          END-IF                                                
                       END-IF                                                   
                    END-IF                                                      
                 ELSE                                                           
                    PERFORM 3700-READ-BP13KH15   THRU 3700-EXIT                 
                 END-IF                                                         
              ELSE                                                              
                 MOVE 'N'       TO WS-K268-FND-SW                               
                                   WS-KH15-FND-SW                               
              END-IF                                                            
           END-IF.                                                              
           PERFORM 4000-MOVE-DETAILS       THRU 4000-EXIT.                      
           PERFORM 2000-READ-F200          THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3500-READ-BP13K268.                                                      
      *-----------------*                                                       
                                                                                
           MOVE 'N'                  TO WS-K268-FND-SW.                         
           MOVE F200-NUM-REGN        TO K268-NUM-REGN.                          
           READ BP13K268         KEY IS K268-NUM-REGN.                          
                                                                                
           EVALUATE BP13K268-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
                ADD 1 TO WS-K268-FOUND                                          
                MOVE 'Y' TO WS-K268-FND-SW                                      
             WHEN 23                                                            
                DISPLAY 'REC NOT FOUND IN BP13K268'                             
                      ', K268-NUM-REGN: ' K268-NUM-REGN                         
                ADD 1 TO WS-K268-NFND                                           
             WHEN OTHER                                                         
                DISPLAY 'BP13K268 READ ERROR,STATUS : ' BP13K268-STATUS         
                      ', K268 KEY FLD : ' K268-KEY-FLD                          
                      ', K268 ALT KEY : ' K268-ALT-KEY                          
                MOVE BP13K268-STATUS       TO RETURN-CODE                       
                PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       3600-READ-BP13K800.                                                      
      *------------------*                                                      
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE BP13K800-STATUS                                             
               WHEN 00                                                          
                    ADD 1 TO WS-K800-FOUND                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 READ ERROR ' BP13K800-STATUS              
                    DISPLAY 'K800 KEY: ' K800-NUM-REGN                          
                    MOVE BP13K800-STATUS       TO RETURN-CODE                   
                    PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3600-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------*                                                       
       3700-READ-BP13KH15.                                                      
      *-----------------*                                                       
                                                                                
           MOVE 'N'                  TO WS-KH15-FND-SW.                         
           MOVE F200-NUM-REGN        TO KH15-NUM-REGN.                          
           READ BP13KH15         KEY IS KH15-NUM-REGN.                          
                                                                                
           EVALUATE BP13KH15-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
                ADD 1 TO WS-KH15-FOUND                                          
                MOVE 'Y' TO WS-KH15-FND-SW                                      
             WHEN 23                                                            
                DISPLAY 'REC NOT FOUND IN BP13KH15'                             
                      ', KH15-NUM-REGN: ' KH15-NUM-REGN                         
                ADD 1 TO WS-KH15-NFND                                           
             WHEN OTHER                                                         
                DISPLAY 'BP13KH15 READ ERROR,STATUS : ' BP13KH15-STATUS         
                      ', KH15 KEY FLD : ' KH15-KEY-FLD                          
                      ', KH15 ALT KEY : ' KH15-ALT-KEY                          
                MOVE BP13KH15-STATUS       TO RETURN-CODE                       
                PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       4000-MOVE-DETAILS.                                                       
      *---------------*                                                         
           MOVE SPACES   TO  BP13FW3D-REC.                                      
           INITIALIZE        BP13FW3D-REC.                                      
                                                                                
      *MOVE DETAILS FROM BP13F200                                               
           MOVE F200-NUM-NT-ZONE            TO FW3D-NUM-NT-ZONE.                
           MOVE F200-NUM-FLAT-TYPE          TO FW3D-NUM-FLAT-TYPE.              
           MOVE F200-DTE-BALLOT             TO FW3D-DTE-BALLOT.                 
           MOVE F200-DTE-ALLOC              TO FW3D-DTE-ALLOC.                  
           MOVE F200-NUM-REGN               TO FW3D-NUM-REGN.                   
           MOVE F200-NUM-JB-PARENT-REGN     TO FW3D-NUM-JB-PARENT-REGN.         
           MOVE F200-DTE-BKAPPMT-DATE       TO FW3D-DTE-BKAPPMT-DATE.           
           MOVE F200-TME-BKAPPMT            TO FW3D-TME-BKAPPMT.                
           MOVE F200-NUM-HHTY-QUEUE         TO FW3D-NUM-HHTY-QUEUE.             
           MOVE F200-NUM-JB-CNT             TO FW3D-NUM-JB-CNT.                 
           MOVE F200-NUM-JB-QUEUE-SEQUENCE                                      
                                     TO FW3D-NUM-JB-QUEUE-SEQUENCE.             
           MOVE F200-NUM-HOUSEHOLD          TO FW3D-NUM-HOUSEHOLD.              
           MOVE F200-NUM-SUCC-4A2           TO FW3D-NUM-SUCC-4A2.               
           MOVE F200-NUM-ACTUAL-FT          TO FW3D-NUM-ACTUAL-FT.              
           MOVE F200-NUM-REOPT              TO FW3D-NUM-REOPT.                  
           MOVE F200-NUM-STATUS             TO FW3D-NUM-STATUS.                 
           MOVE F200-DTE-RESELECTION        TO FW3D-DTE-RESELECTION.            
           MOVE F200-NUM-SELTYPE            TO FW3D-NUM-SELTYPE                 
                                               FW3D-NUM-ALLO-CAT.               
           MOVE F200-CDE-PROJECT            TO FW3D-CDE-PROJECT.                
           MOVE F200-NUM-QUEUE-FT           TO FW3D-NUM-QUEUE-FT.               
           MOVE F200-NUM-NRIC1              TO FW3D-NUM-NRIC1.                  
           MOVE F200-NUM-RESERVE            TO FW3D-NUM-RESERVE.                
           MOVE F200-DTE-SER-REQUEST        TO FW3D-DTE-SER-REQUEST.            
           MOVE F200-NUM-MGPS-QUEUE         TO FW3D-NUM-MGPS-QUEUE.             
           MOVE F200-NUM-GROPA-REGN         TO FW3D-NUM-GROPA-REGN.             
                                                                                
      *MOVE DETAILS FROM BP13K268/BP13KH15                                      
           IF WS-K268-FND-SW = 'Y'                                              
              MOVE K268-DTE-ACCEPT             TO FW3D-DTE-ACCEPT               
              MOVE K268-TME-ACCEPT             TO FW3D-TME-ACCEPT               
              MOVE K268-NUM-BALLOT-HH          TO FW3D-NUM-BALLOT-HH            
              MOVE K268-NUM-ETHNIC             TO FW3D-NUM-ETHNIC               
              MOVE K268-NUM-FT2T               TO FW3D-NUM-FT2T                 
              MOVE K268-NUM-BOOK-STATUS        TO FW3D-NUM-BOOK-STATUS          
              MOVE K268-NUM-WITHIN-LIMIT       TO FW3D-NUM-WITHIN-LIMIT         
              MOVE K268-NUM-REJECT             TO FW3D-NUM-REJECT               
              MOVE K268-NUM-BOOK               TO FW3D-NUM-BOOK                 
              MOVE K268-NUM-UNIT-AVAIL1        TO FW3D-NUM-UNIT-AVAIL1          
              MOVE K268-NUM-QUOTA-AVAIL1       TO FW3D-NUM-QUOTA-AVAIL1         
              MOVE K268-NUM-UNIT-AVAIL2        TO FW3D-NUM-UNIT-AVAIL2          
              MOVE K268-NUM-QUOTA-AVAIL2       TO FW3D-NUM-QUOTA-AVAIL2         
              MOVE K268-NUM-UNIT-AVAIL3        TO FW3D-NUM-UNIT-AVAIL3          
              MOVE K268-NUM-QUOTA-AVAIL3       TO FW3D-NUM-QUOTA-AVAIL3         
              MOVE K268-NUM-UNIT-SPR           TO FW3D-NUM-UNIT-SPR             
              MOVE K268-NUM-QUOTA-SPR          TO FW3D-NUM-QUOTA-SPR            
              MOVE WS-NUM-SCH-ACC              TO FW3D-NUM-SCH-ACC              
           ELSE                                                                 
           IF WS-KH15-FND-SW = 'Y'                                              
              MOVE KH15-NUM-BALLOT-HH          TO FW3D-NUM-BALLOT-HH            
              MOVE KH15-NUM-ETHNIC             TO FW3D-NUM-ETHNIC               
              MOVE KH15-NUM-FT2T               TO FW3D-NUM-FT2T                 
              MOVE KH15-NUM-BOOK-STATUS        TO FW3D-NUM-BOOK-STATUS          
              MOVE KH15-NUM-WITHIN-LIMIT       TO FW3D-NUM-WITHIN-LIMIT         
              MOVE KH15-NUM-REJECT             TO FW3D-NUM-REJECT               
              MOVE KH15-NUM-BOOK               TO FW3D-NUM-BOOK                 
              MOVE KH15-NUM-UNIT-AVAIL1        TO FW3D-NUM-UNIT-AVAIL1          
              MOVE KH15-NUM-QUOTA-AVAIL1       TO FW3D-NUM-QUOTA-AVAIL1         
              MOVE KH15-NUM-UNIT-AVAIL2        TO FW3D-NUM-UNIT-AVAIL2          
              MOVE KH15-NUM-QUOTA-AVAIL2       TO FW3D-NUM-QUOTA-AVAIL2         
              MOVE KH15-NUM-UNIT-AVAIL3        TO FW3D-NUM-UNIT-AVAIL3          
              MOVE KH15-NUM-QUOTA-AVAIL3       TO FW3D-NUM-QUOTA-AVAIL3         
            END-IF                                                              
           END-IF.                                                              
                                                                                
           MOVE F200-DTE-BKAPPMT-DATE-MGPS                                      
                                     TO FW3D-DTE-BKAPPMT-DATE-MGPS.             
           MOVE F200-TME-BKAPPMT-MGPS                                           
                                     TO FW3D-TME-BKAPPMT-MGPS.                  
           MOVE WS-MGPS                     TO FW3D-NUM-MGPS-TAG.               
           IF WS-MGPS = 'P'                                                     
               MOVE K800-NUM-JT-BALLOT-REGN TO K800-NUM-REGN                    
               PERFORM 3600-READ-BP13K800   THRU 3600-EXIT                      
               IF BP13K800-STATUS = ZEROS                                       
                  IF K800-NUM-FLAT-TYPE = '3P'                                  
                     MOVE K800-NUM-FLAT-TYPE                                    
                                     TO WS-MGPS-CHILD-FT                        
                  ELSE                                                          
                     MOVE K800-NUM-ROOM                                         
                                     TO WS-MGPS-CHILD-FT(1:1)                   
                  END-IF                                                        
               END-IF                                                           
               MOVE WS-MGPS-FT-PAIR TO FW3D-NUM-MGPS-FT-PAIR                    
           END-IF.                                                              
                                                                                
           PERFORM 4500-WRITE-BP13FW3D  THRU 4500-EXIT.                         
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       4500-WRITE-BP13FW3D.                                                     
      *---------------*                                                         
                                                                                
                                                                                
           WRITE BP13FW3D-REC.                                                  
                                                                                
           ADD   1  TO WS-FW3D-WRITE.                                           
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13C92K  *************'.                           
           DISPLAY 'RECS READ FROM  BP13F200    : ' WS-F200-READ.               
           DISPLAY 'RECS FOUND IN   BP13K800    : ' WS-K800-FOUND.              
           DISPLAY 'RECS FOUND IN K800 MGPS     : ' WS-K800-MGPS.               
           DISPLAY 'RECS FOUND IN   BP13K268    : ' WS-K268-FOUND.              
           DISPLAY 'RECS NOT FOUND  BP13K268    : ' WS-K268-NFND.               
           DISPLAY 'RECS FOUND IN   BP13KH15    : ' WS-KH15-FOUND.              
           DISPLAY 'RECS NOT FOUND  BP13KH15    : ' WS-KH15-NFND.               
           DISPLAY '                              '.                            
           DISPLAY 'RECS WRITTEN IN  BP13FW3D   : ' WS-FW3D-WRITE.              
                                                                                
           CLOSE BP13F200                                                       
                 BP13K268                                                       
                 BP13KH15                                                       
                 BP13K800                                                       
                 BP13FW3D.                                                      
                                                                                
           IF BP13K268-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K268 : ' BP13K268-STATUS             
              MOVE BP13K268-STATUS TO RETURN-CODE                               
           END-IF.                                                              
                                                                                
           IF BP13KH15-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KH15 : ' BP13KH15-STATUS             
              MOVE BP13KH15-STATUS TO RETURN-CODE                               
           END-IF.                                                              
                                                                                
           IF BP13K800-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K800 : ' BP13K800-STATUS             
              MOVE BP13K800-STATUS TO RETURN-CODE                               
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
