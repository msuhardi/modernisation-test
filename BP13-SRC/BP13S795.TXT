  /*---------------------------------------------------------------*/           
  /*  OBJECTIVES : TO TAB THE RESULT OF BALLOTING ON THE UPGRADER  */           
  /*               AND NEW HH FOR BOOKING SYSTEM                   */           
  /*               SIMILAR BP13S755                                */           
  /*---------------------------------------------------------------*/           
  /*  CHG-REQNO   BY   DATE   DESCRIPTION                          */           
  /*  BP130206   EVM  251096  TO TABULATE REPORT ACCORDING TO      */           
  /*                          PERIOD OF DTE-REGN                   */           
  /*  BP130261   YCH  110497  BREAKDOWN OF RFS APPLN               */           
  /*  BP130630   YCH  300699  DELETE DTE-BAL                       */           
  /*---------------------------------------------------------------*/           
   PROC FORMAT;                                                                 
    VALUE $PD_FMT  "01" = " 1ST QTR "                                           
                   "02" = " 2ND QTR "                                           
                   "03" = " 3RD QTR "                                           
                   "04" = " 4TH QTR "                                           
                   "00" = " OCT 94 ";                                           
                                                                                
    VALUE $CT_FMT  "1"     = "1"                                                
                   "2"     = "2"                                                
                   "3","4" = "3";                                               
                                                                                
    VALUE $HH_FMT  "G1"="UPG EXISTING LESSEES             "                     
                   "G2"="UPG EXISTING RESALE FLAT LESSEES "                     
                   "G3"="UPG NON-LESSEES                  "                     
                   "H1"="NHH EXISTING RESALE FLAT LESSEES "                     
                   "H2"="NHH EX-RESALE FLAT LESSEES       "                     
                   "NT"="TENANTS   "                                            
                   "O "="OCCUPIERS "                                            
                   "OT"="OTHERS    ";                                           
                                                                                
    VALUE $RM_FMT  "1 ","01"="1-RM"                                             
                   "2 ","02"="2-RM"                                             
                   "3 ","03"="3-RM"                                             
                   "4 ","04"="4-RM"                                             
                   "5 ","05"="5-RM"                                             
                   "06"="EXEC"                                                  
                   "07"="HUDC"                                                  
                   "08"="MG";                                                   
                                                                                
    VALUE $FT_FMT  "01"="1-RM"                                                  
                   "02"="2-RM"                                                  
                   "03"="3-RM"                                                  
                   "04"="4-RM"                                                  
                   "05"="5-RM"                                                  
                   "0E"="EXEC"                                                  
                   ;                                                            
                                                                                
   %GLOBAL DATE;                                                                
   OPTIONS NODATE;                                                              
   DATA EXIST_GP EX_GP;                                                         
   INFILE BP13UPG END=EOF;                                                      
   INPUT @1   REG_NO   $CHAR8.                                                  
         @18  DP_OM    $CHAR1.  @28  REG_YEAR $CHAR4.                           
         @32  REG_MM   $CHAR2.  @34  REG_DD   $CHAR2.                           
         @40  NT_REQ   $CHAR3.                                                  
         @46  RM_REQ   $CHAR2.  @50  CAT      $CHAR1.                           
         @51  SCHEME   $CHAR4.  @78  ZONE     $CHAR2.                           
         @86  NT_RES   $CHAR3.  @82  RM_RES   $CHAR2.                           
         @89  CDE_OCC  $CHAR1.                                                  
         @91  CDE_FILE $CHAR1.  @92  CDE_HH   $CHAR1.                           
         ;                                                                      
                                                                                
   IF _N_ = 1 THEN                                                              
      DO;                                                                       
        CUR_DTE=PUT(TODAY(),DDMMYY8.);                                          
        CALL SYMPUT('DATE',CUR_DTE);                                            
      END;                                                                      
                                                                                
   IF REG_MM>='10' AND REG_MM<='12' THEN                                        
      DTE_PD="04";                                                              
   ELSE IF REG_MM>='07' AND REG_MM<='09' THEN                                   
      DTE_PD="03";                                                              
   ELSE IF REG_MM>='04' AND REG_MM<='06' THEN                                   
      DTE_PD="02";                                                              
   ELSE                                                                         
      DTE_PD="01";                                                              
                                                                                
   IF REG_YEAR = '1994' AND REG_MM = '10' THEN                                  
      DO;                                                                       
      IF REG_NO LE '1167829G' THEN DTE_PD = "00";                               
      ELSE DTE_PD = "04";                                                       
      END;                                                                      
                                                                                
   IF RM_RES="0E"  OR                                                           
      RM_RES="E " THEN                                                          
      RM_RES="06";                                                              
   IF RM_RES="0H" OR                                                            
      RM_RES="H " THEN                                                          
      RM_RES="07";                                                              
   IF RM_RES="0G" THEN                                                          
      RM_RES="08";                                                              
                                                                                
   IF DP_OM="D" THEN CDE_TYPE="G1";                                             
   IF DP_OM="O" THEN CDE_TYPE="G2";                                             
   IF DP_OM="R" THEN CDE_TYPE="G3";                                             
   IF DP_OM="S" THEN CDE_TYPE="H1";                                             
   IF DP_OM="X" THEN CDE_TYPE="H2";                                             
   IF DP_OM="Z" THEN                                                            
      DO;                                                                       
         IF (CDE_OCC="0" OR CDE_OCC="1") AND                                    
             SCHEME<="6999" THEN                                                
             CDE_TYPE="NT";                                                     
         ELSE                                                                   
            IF CDE_OCC="2" THEN                                                 
               CDE_TYPE="O ";                                                   
            ELSE                                                                
               CDE_TYPE="OT";                                                   
       END;                                                                     
                                                                                
   IF DP_OM="D" OR DP_OM="S" OR DP_OM="Z" THEN OUTPUT EXIST_GP;                 
   IF DP_OM="O" OR DP_OM="R" OR DP_OM="X" THEN OUTPUT EX_GP;                    
   RUN;                                                                         
                                                                                
   /*---------------------------------------------*/                            
   /*    TABULATE ON EXISTING FLAT INFORMATION    */                            
   /*---------------------------------------------*/                            
   OPTIONS MISSING=0;                                                           
   PROC PRINTTO UNIT=18 NEW;                                                    
   PROC TABULATE DATA=EXIST_GP MISSING FORMAT=7.;                               
                                                                                
   TITLE1 "BP13L795 HDB3                                        SYSTEM OF COMMIT
MENT                                         PAGE :     ";                      
   TITLE2 "                                       BREAKDOWN OF UPGRADERS AND NEW
 HOUSEHOLD FOR BOOKING SYSTEM                   DATE : &DATE";                  
                                                                                
   CLASS REG_YEAR DTE_PD CDE_TYPE CAT                                           
         RM_REQ RM_RES ;                                                        
                                                                                
   FORMAT DTE_PD   $PD_FMT.                                                     
          CAT      $CT_FMT.                                                     
          RM_REQ   $FT_FMT.                                                     
          RM_RES   $RM_FMT.                                                     
          CDE_TYPE $HH_FMT.;                                                    
                                                                                
   KEYLABEL N  =" "                                                             
            ALL="TOTAL";                                                        
                                                                                
   TABLE ((CDE_TYPE=" " ALL="OVERALL-HHTYPE")*                                  
         (RM_RES="EXISTING FLAT TYPE" ALL="SUBTOTAL")                           
         ALL="OVERALL"),                                                        
         (REG_YEAR=" "*                                                         
         (DTE_PD=" "*RM_REQ=" "*CAT="CAT")) ALL                                 
         /RTS=25 BOX="PERIOD OF REGISTRATION";                                  
                                                                                
   /*---------------------------------------------*/                            
   /*    TABULATE ON EX-RESALE FLAT INFORMATION   */                            
   /*---------------------------------------------*/                            
   PROC TABULATE DATA=EX_GP MISSING FORMAT=7.;                                  
                                                                                
   TITLE1 "BP13L795 HDB3                                        SYSTEM OF COMMIT
MENT                                         PAGE :     ";                      
   TITLE2 "                                       BREAKDOWN OF UPGRADERS AND NEW
 HOUSEHOLD FOR BOOKING SYSTEM                   DATE : &DATE";                  
                                                                                
   CLASS REG_YEAR DTE_PD CDE_TYPE CAT                                           
         RM_REQ RM_RES ;                                                        
                                                                                
   FORMAT DTE_PD   $PD_FMT.                                                     
          CAT      $CT_FMT.                                                     
          RM_REQ   $FT_FMT.                                                     
          RM_RES   $RM_FMT.                                                     
          CDE_TYPE $HH_FMT.;                                                    
                                                                                
   KEYLABEL N  =" "                                                             
            ALL="TOTAL";                                                        
                                                                                
   TABLE ((CDE_TYPE=" " ALL="OVERALL-HHTYPE")*                                  
         (RM_RES="EX-DP FLAT TYPE" ALL="SUBTOTAL")                              
         ALL="OVERALL"),                                                        
         (REG_YEAR=" "*                                                         
         (DTE_PD=" "*RM_REQ=" "*CAT="CAT")) ALL                                 
         /RTS=25 BOX="PERIOD OF REGISTRATION";                                  
                                                                                
