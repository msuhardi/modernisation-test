      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C28A.                                                 
      *AUTHOR.        ELGER DE GUZMAN                                           
      *DATE-WRITTEN.  22/01/2014.                                               
                                                                                
      * ====================================================== *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE  :                                        *                
      *        1.  TO GENERATE GL TRANSACTION RECORDS FOR      *                
      *            STUDIO APARTMENT CASES.                     *                
      *                                                        *                
      *     INPUT/OUTPUT FILES:                                *                
      *              BP13F400 (SALES ORDER FILE)               *                
      *              BP13K800 (APPLICANT MASTER)               *                
      *              BP13K350 (STUD APT. FILE)                 *                
      *              BP13F120 (SOC HISTORY MASTER)             *                
      *                                                        *                
      * CHG-NO   BY    ON      DESCRIPTION                     *                
      * ------   ---  -------- -----------                     *                
      * BP135052 ESD1 20010427 NEW PGM                         *                
      * ====================================================== *                
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
       CONFIGURATION SECTION.                                                   
      *------------------------*                                                
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *------------------------*                                                
       INPUT-OUTPUT SECTION.                                                    
      *------------------------*                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS DYNAMIC                                         
                RECORD KEY   IS K800-NUM-REGN                                   
                FILE STATUS  IS K800-STATUS.                                    
                                                                                
           SELECT BP13K350 ASSIGN TO BP13K350                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS DYNAMIC                                         
                RECORD KEY   IS K350-NUM-REGN                                   
                FILE STATUS  IS K350-STATUS.                                    
                                                                                
           SELECT BP13F120 ASSIGN TO BP13F120.                                  
                                                                                
           SELECT BP13F400 ASSIGN TO BP13F400.                                  
                                                                                
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
      *------------------------*                                                
                                                                                
       FD  BP13F400           BLOCK CONTAINS 0 RECORDS                          
                              RECORD CONTAINS 2000 CHARACTERS                   
                              LABEL RECORDS ARE STANDARD                        
                              RECORDING MODE IS F.                              
       COPY BP13F400.                                                           
                                                                                
       FD  BP13K800           RECORD CONTAINS 2000 CHARACTERS.                  
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K350           RECORD CONTAINS 600 CHARACTERS.                   
       COPY BP13K350.                                                           
                                                                                
       FD  BP13F120           BLOCK CONTAINS 0 RECORDS                          
                              RECORD CONTAINS 150 CHARACTERS                    
                              LABEL RECORDS ARE STANDARD                        
                              RECORDING MODE IS F.                              
       COPY BP13F120.                                                           
                                                                                
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  K800-STATUS           PIC 9(2)    VALUE ZEROS.                   
           05  K350-STATUS           PIC 9(2)    VALUE ZEROS.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ-CNT           PIC 9(6)    VALUE ZEROS.                   
           05  WS-WRITE-CNT          PIC 9(6)    VALUE ZEROS.                   
                                                                                
       01  WS-PROGRAM-SWITCHES.                                                 
           05  WS-EOF-F400           PIC X       VALUE SPACES.                  
           05  WS-K800-REC-FND       PIC X       VALUE SPACES.                  
           05  WS-CHECK-CD-AMT       PIC X       VALUE SPACES.                  
           05  WS-K350-FOUND         PIC X       VALUE SPACES.                  
           05  WS-REFUND-SWITCH      PIC X       VALUE SPACES.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-CC                 PIC X(2).                                  
           05  WS-YY                 PIC X(2).                                  
           05  WS-MM                 PIC X(2).                                  
           05  WS-DD                 PIC X(2).                                  
                                                                                
       01  WS-AMT-CD                 PIC S9(7)V99 VALUE ZEROS.                  
       01  WS-BK                     PIC S9(7)V99 VALUE +0.                     
                                                                                
       01  WS-AMT-SOC-PAID           PIC S9(7)V99 VALUE ZEROS.                  
                                                                                
       01  WS-ADV-COL                PIC S9(8)V99 VALUE ZEROS.                  
       01  WS-LEVY-AMT               PIC S9(7)V99 VALUE ZEROS.                  
       01  WS-REFUND-AMT             PIC S9(7)V99 VALUE ZEROS.                  
       01  WS-BK-TRANSFER            PIC S9(7)V99 VALUE ZEROS.                  
       01  WS-AMT-ICP-TOTAL          PIC 9(6)V99 VALUE ZEROS.                   
       01  WS-CONTRA-CONVY-FEES      PIC 9(5)V99 VALUE ZEROS.                   
                                                                                
       01  WS-EDIT-CD-DEBIT          PIC Z,ZZZ,ZZ9.99- VALUE ZEROS.             
                                                                                
       01  WS-NUM-RECEIPT-JRNO.                                                 
           05  WS-RECEIPT-CD         PIC X(2).                                  
           05  WS-RECEIPT-DATE.                                                 
               10  WS-RECEIPT-DD     PIC 9(2).                                  
               10  WS-RECEIPT-MM     PIC 9(2).                                  
               10  WS-SLASH          PIC X.                                     
               10  WS-RECEIPT-YY     PIC 9(2).                                  
           05  WS-NUM-DOC            PIC 9(5)   VALUE ZEROES.                   
                                                                                
       01   WS-NUM-BO                     PIC X(02).                            
       01   WS-LINK-REC.                                                        
            05 WS-LINK-NUM-OFFICE         PIC X(8).                             
            05 WS-LINK-NUM-TYPE-OFFICE    PIC X(15).                            
            05 WS-LINK-NUM-SQNC           PIC X(2).                             
       01   WS-SQL-CODE                   PIC 9(4).                             
                                                                                
       COPY P13COMM4.                                                           
                                                                                
                                                                                
      *------------------------*                                                
       PROCEDURE DIVISION.                                                      
      *------------------------*                                                
                                                                                
      *------------------------*                                                
       001-MAIN.                                                                
      *------------------------*                                                
           PERFORM 010-START-PROCESS  THRU 010-EXIT.                            
           PERFORM 020-READ-F400      THRU 020-EXIT.                            
           PERFORM 030-PROCESS-UPDATE THRU 030-EXIT                             
                   UNTIL WS-EOF-F400 = 'Y'.                                     
           PERFORM 040-END-PROCESS    THRU 040-EXIT.                            
                                                                                
       001-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       010-START-PROCESS.                                                       
      *------------------------*                                                
           OPEN INPUT BP13F400                                                  
                      BP13K350                                                  
                I-O   BP13K800                                                  
               OUTPUT BP13F120.                                                 
                                                                                
           IF K800-STATUS NOT = 0 AND 97                                        
              DISPLAY 'OPENING ERROR K800-STATUS IS ' K800-STATUS               
              PERFORM 040-END-PROCESS THRU 040-EXIT.                            
                                                                                
           IF K350-STATUS NOT = 0 AND 97                                        
              DISPLAY 'OPENING ERROR K350-STATUS IS ' K350-STATUS               
              PERFORM 040-END-PROCESS THRU 040-EXIT.                            
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           MOVE SPACES                TO WS-PROGRAM-SWITCHES.                   
           MOVE ZEROS                 TO WS-COUNTERS.                           
           MOVE 'N'                   TO WS-EOF-F400.                           
                                                                                
       010-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *             INITIAL READING OF BP13F400 FILE                   *        
      ******************************************************************        
      *------------------------*                                                
       020-READ-F400.                                                           
      *------------------------*                                                
           READ BP13F400 AT END                                                 
                         MOVE 'Y' TO WS-EOF-F400                                
                         GO TO 020-EXIT.                                        
                                                                                
           ADD 1 TO WS-READ-CNT.                                                
                                                                                
       020-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       030-PROCESS-UPDATE.                                                      
      *------------------------*                                                
           PERFORM 100-READ-BP13K800 THRU 100-EXIT.                             
                                                                                
           IF WS-K800-REC-FND = 'Y'                                             
              PERFORM 105-CHECK-BK-PAID       THRU 105-EXIT                     
           ELSE                                                                 
              DISPLAY ' REGN NOT-FND' F400-NUM-REGN                             
           END-IF.                                                              
                                                                                
           PERFORM 020-READ-F400    THRU 020-EXIT.                              
                                                                                
       030-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       040-END-PROCESS.                                                         
      *------------------------*                                                
           CLOSE   BP13F400                                                     
                   BP13K800                                                     
                   BP13K350                                                     
                   BP13F120.                                                    
                                                                                
           IF K800-STATUS NOT = 0 AND 97                                        
              DISPLAY 'CLOSING ERROR, K800-STATUS IS ' K800-STATUS.             
                                                                                
           IF K350-STATUS NOT = 0 AND 97                                        
              DISPLAY 'CLOSING ERROR, K350-STATUS IS ' K350-STATUS.             
                                                                                
           DISPLAY ' =============  BP13C286 ===================='.             
           DISPLAY 'NO OF RECORDS READ FROM BP13F400  = ' WS-READ-CNT.          
           DISPLAY 'NO OF RECORDS WRITTEN TO BP13F120 = ' WS-WRITE-CNT.         
           DISPLAY ' ============================================'.             
                                                                                
           STOP RUN.                                                            
                                                                                
       040-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    USE THE REG  NUMBER IN BP13F400 FILE TO ACCESS THE RECORDS  *        
      *    IN BP13K800 FILE.                                           *        
      ******************************************************************        
      *------------------------*                                                
       100-READ-BP13K800.                                                       
      *------------------------*                                                
           MOVE 'Y'           TO WS-K800-REC-FND.                               
           MOVE F400-NUM-REGN TO K800-NUM-REGN.                                 
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF K800-STATUS = ZEROES                                              
              NEXT SENTENCE                                                     
           ELSE                                                                 
              IF K800-STATUS = 23                                               
                 DISPLAY 'REC NOT FOUND IN BP13K800 '                           
                      F400-NUM-REGN                                             
                 MOVE 'N' TO WS-K800-REC-FND                                    
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K800, STATUS : '                    
                      K800-STATUS                                               
                 PERFORM 040-END-PROCESS    THRU 040-EXIT.                      
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ***************************************************                       
      *  ZEROLISE BOOKING FEE FOR BOOKING FEE USED AS   *                       
      *  BOOKING FEE IS TRANSFER TO CD TO BE USED AS ICP*                       
      ***************************************************                       
      *------------------------*                                                
       105-CHECK-BK-PAID.                                                       
      *------------------------*                                                
           DISPLAY '105-CHECK-BK-PAID'.                                         
           IF K800-NUM-DROP-TAG = 'Y'                                           
              CONTINUE                                                          
           ELSE                                                                 
              PERFORM 175-PROCESS-SOC-CASH THRU 175-EXIT                        
           END-IF.                                                              
                                                                                
       105-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       175-PROCESS-SOC-CASH.                                                    
      *------------------------*                                                
           DISPLAY '175-PROCESS-SOC-CASH'.                                      
           DISPLAY 'K800-AMT-SOC-CASH = ' K800-AMT-SOC-CASH.                    
                                                                                
      *    COMPUTE WS-AMT-SOC-PAID = K800-AMT-SOC-CASH.                         
           IF K800-AMT-SOC-CASH IS NOT NUMERIC                                  
              MOVE ZEROES TO K800-AMT-SOC-CASH                                  
           END-IF.                                                              
           IF K800-AMT-CPF1-PAID IS NOT NUMERIC                                 
              MOVE ZEROES TO K800-AMT-CPF1-PAID                                 
           END-IF.                                                              
           IF K800-AMT-CPF2-PAID IS NOT NUMERIC                                 
              MOVE ZEROES TO K800-AMT-CPF2-PAID                                 
           END-IF.                                                              
           IF K800-AMT-CPF3-PAID IS NOT NUMERIC                                 
              MOVE ZEROES TO K800-AMT-CPF3-PAID                                 
           END-IF.                                                              
           IF K800-AMT-CPF4-PAID IS NOT NUMERIC                                 
              MOVE ZEROES TO K800-AMT-CPF4-PAID                                 
           END-IF.                                                              
                                                                                
           COMPUTE WS-AMT-SOC-PAID = K800-AMT-SOC-CASH  +                       
                                     K800-AMT-CPF1-PAID +                       
                                     K800-AMT-CPF2-PAID +                       
                                     K800-AMT-CPF3-PAID +                       
                                     K800-AMT-CPF4-PAID.                        
                                                                                
           IF WS-AMT-SOC-PAID NOT = ZEROES                                      
              PERFORM 305-PROCESS-SOC-PAID THRU 305-EXIT                        
           END-IF.                                                              
                                                                                
           COMPUTE K800-AMT-CD        = ZEROES.                                 
           COMPUTE K800-AMT-SOC-CASH  = ZEROES.                                 
           COMPUTE K800-AMT-CPF1-PAID = ZEROES.                                 
           COMPUTE K800-AMT-CPF2-PAID = ZEROES.                                 
           COMPUTE K800-AMT-CPF3-PAID = ZEROES.                                 
           COMPUTE K800-AMT-CPF4-PAID = ZEROES.                                 
           MOVE    'Y'     TO  K800-NUM-DROP-TAG                                
           PERFORM 400-REWRITE-BP13K800 THRU 400-EXIT.                          
                                                                                
       175-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       305-PROCESS-SOC-PAID.                                                    
      *------------------------*                                                
           MOVE SPACES                TO BP13F120-CASHTRAN.                     
           MOVE '20'                  TO F120-CDE-TRANS-TYPE.                   
           MOVE K800-NUM-REGN         TO F120-NUM-REGN.                         
           MOVE K800-NUM-NRIC1        TO F120-NUM-NRIC.                         
           MOVE WS-DATE               TO F120-DTE-POST.                         
           MOVE ZEROES                TO F120-COUNT-HISTORY                     
           MOVE SPACES                TO F120-NUM-CPF-APPL.                     
           MOVE SPACES                TO F120-NME-NAME.                         
           MOVE 'SOC'                 TO F120-CDE-PAYMENT-TYPE.                 
           MOVE WS-DD                 TO WS-RECEIPT-DD.                         
           MOVE WS-MM                 TO WS-RECEIPT-MM.                         
           MOVE WS-YY                 TO WS-RECEIPT-YY.                         
           MOVE 'JR'                  TO WS-RECEIPT-CD.                         
           MOVE '/'                   TO WS-SLASH.                              
           MOVE WS-NUM-RECEIPT-JRNO   TO F120-NUM-RECPT-JRNO.                   
           COMPUTE F120-AMT-RECEIPT = WS-AMT-SOC-PAID * -1                      
           MOVE 'D'                   TO F120-CDE-CRDR.                         
           MOVE WS-DATE               TO F120-DTE-TRANS.                        
           MOVE F400-NUM-SCH-ACC      TO F120-NUM-SCH-ACCT.                     
           MOVE  K800-NUM-FLAT-TYPE   TO F120-NUM-FLAT-TYPE-SA                  
                                                                                
           WRITE BP13F120-CASHTRAN.                                             
           ADD  1                     TO WS-WRITE-CNT.                          
                                                                                
       305-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       400-REWRITE-BP13K800.                                                    
      *------------------------*                                                
           REWRITE BP13K800-MASTER.                                             
                                                                                
           IF K800-STATUS = ZEROES                                              
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'ERROR REWRITING BP13K800, STATUS : '                     
                       K800-STATUS                                              
              PERFORM 040-END-PROCESS    THRU 040-EXIT.                         
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------------------*                                          
      *         END OF PROGRAM       *                                          
      *------------------------------*                                          
