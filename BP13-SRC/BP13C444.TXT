       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C444.                                                 
      *AUTHOR.        NINO ANGELO GARILLO .                                     
      *DATE-WRITTEN.  09/11/04.                                                 
      *DATE-COMPILED.                                                           
      *    SKIP3                                                                
      *REMARKS.                                                                 
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE  :                                        *                
      *        1.  TO DELETE RECORDS IN BP13K130/K893/K895     *                
      *            FOR CASES FOUND IN BP13K410 WITH            *                
      *            (CANCELLATION DATE - CURRENT DATE > 5 YRS)  *                
      *                                                        *                
      *     I-O FILE:                                          *                
      *           1. BP13F893 (SEQL OF K893 WITH STATUS = C)   *                
      *           2. BP13K130 (SOC TRANS FILE)                 *                
      *           3. BP13K893 (SOC APPLICATION HISTORY)        *                
      *           4. BP13K895 (SOC OCCUPIER HISTORY)           *                
      * =========================================================== *           
      * CHGE REQ   DATE    AUTHOR  REMARKS                          *           
      * --------  -------- ------  -------                          *           
      * BP130418  19980828 CLT1    Y2K COMPLIANCE CHANGES           *           
      * BP130712  19990511 NOR     REVISE PRGM                      *           
      * BP132199  20020816 ANC1    MOVE CURRENT DATE TO K820-DTE-UPD*           
      *                            BEFORE WRITING. RETAIN K820 RECS *           
      *                            IF MATCH W/ K800.                *           
      * BP132627  09112004 GAN1    CHG F800/F820 TO K893/K895       *           
      * =========================================================== *           
             EJECT                                                              
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13K130 ASSIGN TO BP13K130                                   
                ACCESS MODE    IS SEQUENTIAL                                    
                ORGANIZATION   IS INDEXED                                       
                RECORD KEY     IS K130-KEY-FLD                                  
                FILE STATUS    IS K130-STATUS.                                  
           SELECT BP13K893 ASSIGN TO BP13K893                                   
                ACCESS MODE    IS SEQUENTIAL                                    
                ORGANIZATION   IS INDEXED                                       
                RECORD KEY     IS K893-KEY-FLD                                  
                FILE STATUS    IS K893-STATUS.                                  
           SELECT BP13K895 ASSIGN TO BP13K895                                   
                ACCESS MODE    IS SEQUENTIAL                                    
                ORGANIZATION   IS INDEXED                                       
                RECORD KEY     IS K895-KEY-FLD                                  
                FILE STATUS    IS K895-STATUS.                                  
                                                                                
           SELECT BP13F893 ASSIGN TO BP13F893.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K130                 BLOCK  CONTAINS   0 RECORDS                 
                                    RECORD CONTAINS 150 CHARACTERS              
                                    LABEL RECORDS ARE STANDARD.                 
       COPY BP13K130.                                                           
                                                                                
       FD  BP13K893                 BLOCK  CONTAINS   0 RECORDS                 
                                    RECORD CONTAINS 2050 CHARACTERS             
                                    LABEL RECORDS ARE STANDARD.                 
       COPY BP13K893.                                                           
                                                                                
       FD  BP13K895                 BLOCK  CONTAINS   0 RECORDS                 
                                    RECORD CONTAINS 450 CHARACTERS              
                                    LABEL RECORDS ARE STANDARD.                 
       COPY BP13K895.                                                           
                                                                                
       FD  BP13F893                 BLOCK  CONTAINS   0 RECORDS                 
                                    RECORD CONTAINS 2050 CHARACTERS             
                                    LABEL RECORDS ARE STANDARD.                 
       COPY BP13F893.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILE-STATUS.                                                         
           03  K130-STATUS     PIC 9(2) VALUE 0.                                
               88  K130-REC-FOUND   VALUE 0.                                    
               88  K130-REC-NOTFND  VALUE 23.                                   
           03  K893-STATUS     PIC 9(2) VALUE 0.                                
               88  K893-REC-FOUND   VALUE 0.                                    
               88  K893-REC-NOTFND  VALUE 23.                                   
           03  K895-STATUS     PIC 9(2) VALUE 0.                                
               88  K895-REC-FOUND   VALUE 0.                                    
               88  K895-REC-NOTFND  VALUE 23.                                   
                                                                                
       01  WS-FILE-SWITCHES.                                                    
           03  WS-EOFILE-IND   PIC X(1) VALUE SPACE.                            
               88  WS-EOFILE   VALUE 'Y'.                                       
                                                                                
       01  WS-K130-EOF         PIC X   VALUE 'N'.                               
       01  WS-K893-EOF         PIC X   VALUE 'N'.                               
       01  WS-K895-EOF         PIC X   VALUE 'N'.                               
                                                                                
       01  WS-CURR-DATE.                                                        
           03  WS-CURR-CCYY    PIC 9(4).                                        
           03  WS-CURR-MM      PIC 99.                                          
           03  WS-CURR-DD      PIC 99.                                          
                                                                                
       01  WS-CURR-CCYYMMDD.                                                    
           03  WS-CURR-YEAR    PIC 9(4).                                        
           03  WS-CURR-MTH     PIC 99.                                          
           03  WS-CURR-DAY     PIC 99.                                          
                                                                                
       01  WS-DTE-CAN.                                                          
           03  WS-DTE-CAN-CCYY  PIC 9(4).                                       
           03  WS-DTE-CAN-MM    PIC 99.                                         
           03  WS-DTE-CAN-DD    PIC 99.                                         
                                                                                
       01  WS-ACCUMULATORS.                                                     
           03  WS-NUM-READ-F893     PIC 9(5)    VALUE ZEROS.                    
           03  WS-NUM-DELETED-K130  PIC 9(5)    VALUE ZEROS.                    
           03  WS-NUM-DELETED-K893  PIC 9(5)    VALUE ZEROS.                    
           03  WS-NUM-DELETED-K895  PIC 9(5)    VALUE ZEROS.                    
           03  WS-DIFF-MTHS         PIC S9(4)   VALUE ZEROS.                    
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-----------------*                                                       
       010-START-ROUTINE.                                                       
      *-----------------*                                                       
            OPEN INPUT   BP13F893                                               
                 I-O     BP13K130                                               
                         BP13K893                                               
                         BP13K895.                                              
                                                                                
            IF K130-STATUS NOT = 0 AND 97                                       
               DISPLAY ' ERROR OPENING BP13K130, STATUS ' K130-STATUS           
               MOVE K130-STATUS TO RETURN-CODE                                  
               PERFORM 030-CLOSE-ROUTINE.                                       
                                                                                
            IF K893-STATUS NOT = 0 AND 97                                       
               DISPLAY ' ERROR OPENING BP13K893, STATUS ' K893-STATUS           
               MOVE K893-STATUS TO RETURN-CODE                                  
               PERFORM 030-CLOSE-ROUTINE.                                       
                                                                                
            IF K895-STATUS NOT = 0 AND 97                                       
               DISPLAY ' ERROR OPENING BP13K895, STATUS ' K895-STATUS           
               MOVE K895-STATUS TO RETURN-CODE                                  
               PERFORM 030-CLOSE-ROUTINE.                                       
                                                                                
                                                                                
      *-------------------*                                                     
       020-PROCESS-ROUTINE.                                                     
      *-------------------*                                                     
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-CCYYMMDD.                      
           MOVE WS-CURR-YEAR          TO WS-CURR-CCYY.                          
           MOVE WS-CURR-MTH           TO WS-CURR-MM.                            
           MOVE WS-CURR-DAY           TO WS-CURR-DD.                            
                                                                                
           PERFORM 100-READ-F893 THRU 100-EXIT UNTIL WS-EOFILE.                 
                                                                                
      *-----------------*                                                       
       030-CLOSE-ROUTINE.                                                       
      *-----------------*                                                       
           CLOSE BP13F893                                                       
                 BP13K130                                                       
                 BP13K893                                                       
                 BP13K895.                                                      
                                                                                
           IF K130-STATUS NOT = 0 AND 97                                        
              DISPLAY ' ERROR CLOSING BP13K130, STATUS ' K130-STATUS.           
                                                                                
           IF K893-STATUS NOT = 0 AND 97                                        
              DISPLAY ' ERROR CLOSING BP13K893, STATUS ' K893-STATUS.           
                                                                                
           IF K895-STATUS NOT = 0 AND 97                                        
              DISPLAY ' ERROR CLOSING BP13K895, STATUS ' K895-STATUS.           
                                                                                
                                                                                
           DISPLAY ' **** BP13C444 ****'.                                       
           DISPLAY ' NO. OF RECORDS  READ    FROM F893 '                        
                    WS-NUM-READ-F893.                                           
           DISPLAY ' NO. OF RECORDS DELETED  FROM K130 '                        
                    WS-NUM-DELETED-K130.                                        
           DISPLAY ' NO. OF RECORDS DELETED  FROM K895 '                        
                    WS-NUM-DELETED-K895.                                        
           DISPLAY ' NO. OF RECORDS DELETED  FROM K893 '                        
                    WS-NUM-DELETED-K893.                                        
           STOP RUN.                                                            
                                                                                
      ******************************************************************        
      *                                                                *        
      *         READ INPUT FILE - AT END SET INDICATOR                 *        
      *                                                                *        
      ******************************************************************        
                                                                                
       100-READ-F893.                                                           
                                                                                
           READ BP13F893 AT END                                                 
               MOVE 'Y' TO WS-EOFILE-IND                                        
               GO TO 100-EXIT.                                                  
                                                                                
           ADD 1 TO WS-NUM-READ-F893.                                           
                                                                                
           MOVE F893-DTE-CANCEL TO WS-DTE-CAN.                                  
                                                                                
           IF F893-DTE-CANCEL NOT NUMERIC                                       
              DISPLAY 'INVALID F893 CANCEL-DATE ' WS-DTE-CAN                    
              GO TO 100-EXIT.                                                   
                                                                                
           COMPUTE                                                              
               WS-DIFF-MTHS =   (WS-CURR-CCYY    * 12 + WS-CURR-MM)             
                             -  (WS-DTE-CAN-CCYY * 12 + WS-DTE-CAN-MM).         
                                                                                
           IF (WS-DIFF-MTHS < 60)                                               
              GO TO 100-EXIT                                                    
           ELSE                                                                 
              PERFORM 200-DELETE-K130 THRU 200-EXIT.                            
              PERFORM 230-DELETE-K895 THRU 230-EXIT.                            
              PERFORM 260-DELETE-K893 THRU 260-EXIT.                            
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-DELETE-K130.                                                         
                                                                                
           MOVE SPACES             TO WS-K130-EOF.                              
           MOVE SPACES             TO K130-KEY-FLD.                             
           MOVE F893-NUM-REGN-HIST TO K130-NUM-ORIG-REGN.                       
           START BP13K130 KEY >= K130-NUM-ORIG-REGN.                            
                                                                                
           IF K130-STATUS = 00                                                  
              PERFORM 210-READNXT-K130  THRU 210-EXIT                           
              PERFORM 220-DELETE-REC    THRU 220-EXIT                           
                 UNTIL K130-NUM-ORIG-REGN NOT = F893-NUM-REGN-HIST              
                 OR WS-K130-EOF = 'Y'                                           
           ELSE                                                                 
               DISPLAY 'ERROR START BP13K130'  K130-STATUS.                     
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       210-READNXT-K130.                                                        
           READ BP13K130 NEXT AT END                                            
                MOVE 'Y' TO WS-K130-EOF.                                        
                                                                                
       210-EXIT.                                                                
           EXIT.                                                                
                                                                                
       220-DELETE-REC.                                                          
           DELETE BP13K130.                                                     
                                                                                
           IF K130-STATUS = 0                                                   
              ADD 1 TO WS-NUM-DELETED-K130                                      
           ELSE                                                                 
              DISPLAY ' ERROR DELETING RECORD, KEY ' K130-KEY-FLD               
              MOVE K130-STATUS TO RETURN-CODE                                   
              PERFORM 030-CLOSE-ROUTINE.                                        
                                                                                
           PERFORM 210-READNXT-K130 THRU 210-EXIT.                              
                                                                                
       220-EXIT.                                                                
           EXIT.                                                                
                                                                                
       230-DELETE-K895.                                                         
                                                                                
           MOVE SPACES             TO WS-K895-EOF.                              
           MOVE SPACES             TO K895-KEY-FLD.                             
           MOVE F893-NUM-REGN-HIST TO K895-NUM-REGN-HIST.                       
           START BP13K895 KEY >= K895-NUM-REGN-HIST.                            
                                                                                
           IF K895-STATUS = 00                                                  
              PERFORM 240-READNXT-K895  THRU 240-EXIT                           
              PERFORM 250-DELETE-REC    THRU 250-EXIT                           
                 UNTIL K895-NUM-REGN-HIST NOT = F893-NUM-REGN-HIST              
                 OR WS-K895-EOF = 'Y'                                           
           ELSE                                                                 
               DISPLAY 'ERROR START BP13K895'  K895-STATUS.                     
                                                                                
       230-EXIT.                                                                
           EXIT.                                                                
                                                                                
       240-READNXT-K895.                                                        
           READ BP13K895 NEXT AT END                                            
                MOVE 'Y' TO WS-K895-EOF.                                        
                                                                                
       240-EXIT.                                                                
           EXIT.                                                                
                                                                                
       250-DELETE-REC.                                                          
           DELETE BP13K895.                                                     
                                                                                
           IF K895-STATUS = 00                                                  
              ADD 1 TO WS-NUM-DELETED-K895                                      
           ELSE                                                                 
              DISPLAY ' ERROR DELETING RECORD, KEY ' K895-KEY-FLD               
              MOVE K895-STATUS TO RETURN-CODE                                   
              PERFORM 030-CLOSE-ROUTINE.                                        
                                                                                
           PERFORM 240-READNXT-K895  THRU 240-EXIT.                             
                                                                                
       250-EXIT.                                                                
           EXIT.                                                                
                                                                                
       260-DELETE-K893.                                                         
                                                                                
           MOVE SPACES             TO WS-K893-EOF.                              
           MOVE SPACES             TO K893-KEY-FLD.                             
           MOVE F893-NUM-REGN-HIST TO K893-NUM-REGN-HIST.                       
           START BP13K893 KEY >= K893-NUM-REGN-HIST.                            
                                                                                
           IF K893-STATUS = 00                                                  
              PERFORM 270-READNXT-K893 THRU 270-EXIT                            
              PERFORM 280-DELETE-REC   THRU 280-EXIT                            
                 UNTIL K893-NUM-REGN-HIST NOT = F893-NUM-REGN-HIST              
                 OR WS-K893-EOF = 'Y'                                           
           ELSE                                                                 
               DISPLAY 'ERROR START BP13K893'  K893-STATUS.                     
                                                                                
       260-EXIT.                                                                
           EXIT.                                                                
                                                                                
       270-READNXT-K893.                                                        
           READ BP13K893 NEXT AT END                                            
                MOVE 'Y' TO WS-K893-EOF.                                        
                                                                                
       270-EXIT.                                                                
           EXIT.                                                                
                                                                                
       280-DELETE-REC.                                                          
           DELETE BP13K893.                                                     
                                                                                
           IF K893-STATUS = 00                                                  
              ADD 1 TO WS-NUM-DELETED-K893                                      
           ELSE                                                                 
              DISPLAY ' ERROR DELETING RECORD, KEY ' K893-KEY-FLD               
              MOVE K893-STATUS TO RETURN-CODE                                   
              PERFORM 030-CLOSE-ROUTINE.                                        
                                                                                
           PERFORM 270-READNXT-K893 THRU 270-EXIT.                              
                                                                                
       280-EXIT.                                                                
           EXIT.                                                                
                                                                                
