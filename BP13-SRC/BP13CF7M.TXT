       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13CF7M.                                                  
       AUTHOR.       AM25.                                                      
       DATE-WRITTEN. 28/08/2019.                                                
      *****************************************************************         
      *                SOC - SYSTEM OF COMMITMENT (BP13)              *         
      *===============================================================*         
      *  OBJECTIVES : TO UPDATE NUM-DEFER-INCOME-ASSESS IN            *         
      *               BP13K800 FOR SERS CASES                         *         
      *===============================================================*         
      * FILES DESCRIPTION :-                                          *         
      *                                                               *         
      *                ACCESS     REC                                 *         
      * DD NAME  I/O/E MODE       LEN  KEY FIELDS    REMARKS          *         
      * -------- ----- ---------- ---- ------------- ---------------- *         
      * BP13F740 I     SEQUENTIAL 500                                 *         
      * BP17F000 I     SEQUENTIAL 200                                 *         
      * BP13K800 I/O   RANDOM     2000 K800-NUM-REGN                  *         
      *===============================================================*         
      * MODIFICATIONS :                                               *         
      *                                                               *         
      * CHG-REQ#  DD/MM/YY   BY   REMARKS                             *         
      * --------  --------  ----  ----------------------------------- *         
      * BP137724  28/08/19  AM25  NEW PROGRAM                         *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN      TO BP13F740.                             
           SELECT BP17F000 ASSIGN      TO BP17F000.                             
                                                                                
           SELECT BP13K800 ASSIGN      TO BP13K800                              
              ORGANIZATION  IS INDEXED                                          
              ACCESS MODE   IS RANDOM                                           
              RECORD KEY    IS K800-NUM-REGN                                    
              FILE STATUS   IS WS-K800-STATUS.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F740                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F740.                                                           
                                                                                
       FD  BP17F000                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP17F000.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-K800-STATUS          PIC 9(2)        VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F740-READ            PIC 9(5)        VALUE ZEROES.            
           05  WS-F000-READ            PIC 9(5)        VALUE ZEROES.            
           05  WS-K800-READ            PIC 9(5)        VALUE ZEROES.            
           05  WS-K800-UPDT            PIC 9(5)        VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES-FLAGS.                                                   
           05  WS-F740-EOF             PIC X           VALUE 'N'.               
           05  WS-F000-EOF             PIC X           VALUE 'N'.               
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 2000-READ-BP13F740    THRU 2000-EXIT.                        
           PERFORM 2500-READ-BP17F000    THRU 2500-EXIT.                        
           PERFORM 3000-MAIN-PROCESSING  THRU 3000-EXIT                         
              UNTIL WS-F740-EOF = 'Y' AND WS-F000-EOF = 'Y'.                    
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN INPUT  BP13F740                                                 
                       BP17F000                                                 
                I-O    BP13K800.                                                
                                                                                
           IF (WS-K800-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K800 FAIL STATUS=' WS-K800-STATUS               
              MOVE WS-K800-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-READ-BP13F740.                                                      
      *****************************************************************         
                                                                                
           READ BP13F740                                                        
              AT END                                                            
                 MOVE HIGH-VALUES      TO F740-NUM-REGN                         
                 MOVE 'Y'              TO WS-F740-EOF                           
              NOT AT END                                                        
                 ADD 1                 TO WS-F740-READ                          
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2500-READ-BP17F000.                                                      
      *****************************************************************         
                                                                                
           READ BP17F000                                                        
              AT END                                                            
                 MOVE HIGH-VALUES      TO F000-NUM-REGNO                        
                 MOVE 'Y'              TO WS-F000-EOF                           
              NOT AT END                                                        
                 ADD 1                 TO WS-F000-READ                          
           END-READ.                                                            
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-MAIN-PROCESSING.                                                    
      *****************************************************************         
                                                                                
           EVALUATE TRUE                                                        
              WHEN F740-NUM-REGN = F000-NUM-REGNO                               
                 PERFORM 3100-READ-BP13K800 THRU 3100-EXIT                      
                 PERFORM 2000-READ-BP13F740 THRU 2000-EXIT                      
                 PERFORM 2500-READ-BP17F000 THRU 2500-EXIT                      
              WHEN F740-NUM-REGN < F000-NUM-REGNO                               
                 PERFORM 2000-READ-BP13F740 THRU 2000-EXIT                      
              WHEN F740-NUM-REGN > F000-NUM-REGNO                               
                 PERFORM 2500-READ-BP17F000 THRU 2500-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3100-READ-BP13K800.                                                      
      *****************************************************************         
                                                                                
           MOVE F740-NUM-REGN          TO K800-NUM-REGN.                        
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K800-READ                          
                 PERFORM 3200-UPDATE-BP13K800 THRU 3200-EXIT                    
              WHEN 23                                                           
                 DISPLAY 'REC NOT FOUND IN BP13K800: ' F740-NUM-REGN            
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K800 FAIL STATUS=' WS-K800-STATUS            
                 MOVE WS-K800-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3200-UPDATE-BP13K800.                                                    
      *****************************************************************         
                                                                                
           MOVE 'Y'                    TO K800-NUM-DEFER-INCOME-ASSESS.         
                                                                                
           REWRITE BP13K800-MASTER.                                             
                                                                                
           IF WS-K800-STATUS = ZEROES                                           
              ADD 1                    TO WS-K800-UPDT                          
           ELSE                                                                 
              DISPLAY 'REWRITE BP13K800 FAIL STATUS=' WS-K800-STATUS            
              MOVE WS-K800-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           CLOSE BP13F740                                                       
                 BP17F000                                                       
                 BP13K800.                                                      
                                                                                
           IF WS-K800-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K800 FAIL STATUS=' WS-K800-STATUS              
              MOVE WS-K800-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           DISPLAY '*****************************'.                             
           DISPLAY '* PROGRAM BP13CF7M          *'.                             
           DISPLAY '* CONTROL COUNTS            *'.                             
           DISPLAY '*---------------------------*'.                             
           DISPLAY '* REC READ F740     : ' WS-F740-READ ' *'.                  
           DISPLAY '* REC READ F000     : ' WS-F000-READ ' *'.                  
           DISPLAY '* REC READ K800     : ' WS-K800-READ ' *'.                  
           DISPLAY '* REC UPDT K800     : ' WS-K800-UPDT ' *'.                  
           DISPLAY '*****************************'.                             
                                                                                
       9000-EXIT.                                                               
           STOP RUN.                                                            
           EXIT.                                                                
                                                                                
      *======================  END OF PROGRAM  ======================*          
