      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13CD24.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  FEBRUARY 25, 2009.                                        
      *===============================================================*         
      *   OBJECTIVE:  PRINT DBSS CASES WITH HOUSING GRANT REQUEST DATE*         
      *               SORTED BY PROJECT, HSGNT-ELIG & REGN NO.        *         
      *===============================================================*         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *                  1. BP13FD00                                  *         
      *                  2. SY02F001                                  *         
      *                  3. BP13K800                                  *         
      *                  4. BP13K022                                  *         
      *                  5. BP13K024                                  *         
      *                  6. BM06K110                                  *         
      *                                                               *         
      *     OUTPUT FILE: 1. BP13LD24                                  *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------- ---- -----------------------              *         
      * BP133522 25/02/2009 PCL3 NEW PROGRAM.                         *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13FD00  ASSIGN        TO BP13FD00.                         
           SELECT  SY02F001  ASSIGN        TO SY02F001.                         
           SELECT  BP13K800  ASSIGN        TO BP13K800                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K800-NUM-REGN                     
                             FILE STATUS   IS WS-K800-STATUS.                   
           SELECT  BP13K022  ASSIGN       TO BP13K022                           
                             ACCESS MODE  IS RANDOM                             
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS K022-KEY-FLD                       
                             ALTERNATE RECORD KEY IS K022-NUM-REGN              
                             FILE STATUS  IS WS-K022-STATUS.                    
           SELECT  BP13K024  ASSIGN        TO BP13K024                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K024-KEY-FLD                      
                             FILE STATUS   IS WS-K024-STATUS.                   
           SELECT  BM06K110  ASSIGN        TO BM06K110                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K110-KEY-FLD                      
                             FILE STATUS   IS WS-K110-STATUS.                   
           SELECT  BP13LD24  ASSIGN        TO BP13LD24.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13FD00                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 1600 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13FD00.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K024                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K024.                                                           
                                                                                
       FD   BP13LD24                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORD IS OMITTED                                             
            RECORDING MODE IS F.                                                
       01   BP13LD24-REC               PIC X(132).                              
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-F001-EOF             PIC X(1)  VALUE 'N'.                     
               88 EOF-F001                       VALUE 'Y'.                     
           05  WS-EOF-FD00             PIC X(1)  VALUE 'N'.                     
           05  WS-K800-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K110-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K022-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K024-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-FD00-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-K800-NOTFND          PIC 9(7)  VALUE ZEROES.                  
           05  WS-K800-ELIG-TAG-N      PIC 9(7)  VALUE ZEROES.                  
           05  WS-HSGNT-AHG-SPACE      PIC 9(7)  VALUE ZEROES.                  
           05  WS-SNO                  PIC 9(7)  VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(2)  VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(4)  VALUE ZEROES.                  
           05  WS-CNT-LD24-WRITE       PIC 9(7)  VALUE ZEROES.                  
           05  WS-MAX-LINE             PIC 9(2)  VALUE 55.                      
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-SYSTEM-DATE           PIC 9(8)  VALUE ZEROES.                  
           05 WS-FIRST-WRITE           PIC X(1)  VALUE 'Y'.                     
           05 WS-NUM-PROJECT           PIC X(3)  VALUE SPACES.                  
                                                                                
       01  LD24-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13LD24'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  LD24-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LD24-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
       01  LD24-HEADER02.                                                       
           05  FILLER              PIC X(38)     VALUE SPACES.                  
           05  FILLER              PIC X(52)     VALUE                          
               'REPORT ON DBSS CASES WITH HOUSING GRANT REQUEST DATE'.          
                                                                                
       01  LD24-HEADER03.                                                       
           05  LD24-NME-PRJCT      PIC X(37)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'HG  '.                  
           05  FILLER              PIC X(09)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'AHG '.                  
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'HG  '.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'AHG '.                  
                                                                                
       01  LD24-HEADER04.                                                       
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE '  S/N'.                 
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'REGN NO '.              
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(12)     VALUE 'BOOKING DATE'.          
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(12)     VALUE 'REQUEST DATE'.          
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(12)     VALUE 'REQUEST DATE'.          
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'CODE'.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'CODE'.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'ELIG CHECK'.            
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'CSM'.                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
                                                                                
       01  LD24-DETAIL01.                                                       
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  LD24-SN             PIC ZZZZ9        VALUE ZEROES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LD24-REGN           PIC X(08)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LD24-BOOKING        PIC X(12)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LD24-HG-REQ-DATE    PIC X(12)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LD24-AHG-REQ-DATE   PIC X(12)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LD24-HG-CODE        PIC X(01)        VALUE SPACE.                
           05  FILLER              PIC X(05)        VALUE SPACES.               
           05  LD24-AHG-CODE       PIC X(03)        VALUE SPACE.                
           05  FILLER              PIC X(03)        VALUE SPACES.               
           05  LD24-ELIG-CHK       PIC X(01)        VALUE SPACE.                
           05  FILLER              PIC X(11)        VALUE SPACES.               
           05  LD24-CSM            PIC X(30)        VALUE SPACES.               
                                                                                
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13FD00      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-FD00 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13FD00                                                 
                       SY02F001                                                 
                       BP13K800                                                 
                       BP13K022                                                 
                       BP13K024                                                 
                       BM06K110                                                 
              OUTPUT   BP13LD24.                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR OPENING : ' WS-K800-STATUS              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 - ERROR OPENING : ' WS-K110-STATUS              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K022 - ERROR OPENING : ' WS-K022-STATUS              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K024-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K024 - ERROR OPENING : ' WS-K024-STATUS              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           READ SY02F001 AT END MOVE 'Y'  TO WS-F001-EOF.                       
           IF EOF-F001                                                          
              DISPLAY 'RECORD NOT FOUND IN SY01F001 '                           
              DISPLAY 'PROCESSING TERMINATED '                                  
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           ELSE                                                                 
              MOVE F001-DTE-CURRENT      TO   WS-SYSTEM-DATE                    
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13FD00.                                                      
      ******************************************************************        
                                                                                
           READ BP13FD00           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-FD00                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO WS-FD00-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           IF FD00-NUM-REGN > SPACES                                            
               PERFORM 4000-READ-BP13K800    THRU 4000-EXIT                     
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13FD00        THRU 2000-EXIT.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-READ-BP13K800.                                                      
      ******************************************************************        
                                                                                
           MOVE FD00-NUM-REGN TO K800-NUM-REGN.                                 
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    PERFORM 5000-CHECK-DBSS-CASES   THRU 5000-EXIT              
               WHEN 23                                                          
                    ADD 1 TO WS-K800-NOTFND                                     
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K800 : ' WS-K800-STATUS          
                            ' REGN = ' K800-NUM-REGN                            
                    PERFORM  9999-CLOSE-ROUTINE     THRU 9999-EXIT              
           END-EVALUATE.                                                        
                                                                                
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-CHECK-DBSS-CASES.                                                   
      ******************************************************************        
                                                                                
           IF ((FD00-DTE-HSGNT-CPFREQ NOT = SPACES AND LOW-VALUES AND           
                                          ZEROES) AND                           
              (FD00-DTE-HSGNT-SUBMIT = SPACES OR LOW-VALUES OR ZEROES))         
                                          OR                                    
              ((K800-DTE-AHG-REQ NOT = SPACES AND LOW-VALUES AND                
                                          ZEROES) AND                           
              (K800-DTE-AHG-PROCESS  = SPACES OR LOW-VALUES OR ZEROES))         
              IF K800-NUM-ELIG-TAG = 'Y'                                        
                 PERFORM 5100-GET-FIELDS       THRU 5100-EXIT                   
                 PERFORM 6000-PRINT-REPORT     THRU 6000-EXIT                   
              ELSE                                                              
                 ADD   1                         TO WS-K800-ELIG-TAG-N          
              END-IF                                                            
           ELSE                                                                 
              ADD 1                              TO WS-HSGNT-AHG-SPACE          
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5100-GET-FIELDS.                                                         
      ******************************************************************        
                                                                                
           MOVE SPACES                               TO LD24-DETAIL01.          
           INITIALIZE                                   LD24-DETAIL01.          
                                                                                
           STRING WS-SYSTEM-DATE(7:2) '/'                                       
                  WS-SYSTEM-DATE(5:2) '/'                                       
                  WS-SYSTEM-DATE(1:4)                                           
              DELIMITED BY SIZE INTO LD24-DATE-HDR.                             
                                                                                
           MOVE FD00-NUM-PROJECT                     TO LD24-NME-PRJCT.         
           MOVE FD00-NUM-REGN                        TO LD24-REGN.              
           PERFORM 5200-READ-BM06K110              THRU 5200-EXIT.              
                                                                                
           IF FD00-DTE-HSGNT-CPFREQ NOT = SPACES AND LOW-VALUES AND             
                                                     ZEROES                     
              STRING FD00-DTE-HSGNT-CPFREQ(7:2) '/'                             
                     FD00-DTE-HSGNT-CPFREQ(5:2) '/'                             
                     FD00-DTE-HSGNT-CPFREQ(1:4)                                 
                     DELIMITED BY SIZE INTO LD24-HG-REQ-DATE                    
           END-IF.                                                              
                                                                                
           IF K800-DTE-AHG-REQ NOT = SPACES AND LOW-VALUES AND                  
                                                     ZEROES                     
              STRING K800-DTE-AHG-REQ(7:2) '/'                                  
                     K800-DTE-AHG-REQ(5:2) '/'                                  
                     K800-DTE-AHG-REQ(1:4)                                      
                     DELIMITED BY SIZE INTO LD24-AHG-REQ-DATE                   
           END-IF.                                                              
                                                                                
           MOVE FD00-NUM-HSGNT-ELIG            TO LD24-HG-CODE.                 
           MOVE K800-NUM-AHG                   TO LD24-AHG-CODE.                
           MOVE K800-NUM-ELIG-TAG              TO LD24-ELIG-CHK.                
                                                                                
           PERFORM 5300-READ-BP13K022        THRU 5300-EXIT.                    
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5200-READ-BM06K110.                                                      
      ******************************************************************        
                                                                                
           MOVE K800-NUM-SCH-ACC TO K110-KEY-FLD.                               
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-STATUS                                              
               WHEN 00                                                          
                    IF K110-DTE-ACCEPTANCE NOT = SPACES AND LOW-VALUES          
                                                        AND ZEROES              
                       STRING K110-DTE-ACCEPTANCE(7:2) '/'                      
                              K110-DTE-ACCEPTANCE(5:2) '/'                      
                              K110-DTE-ACCEPTANCE(1:4)                          
                             DELIMITED BY SIZE INTO LD24-BOOKING                
                    END-IF                                                      
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BM06K110 : ' WS-K110-STATUS          
                            ' SCHEME ACCT = ' K110-KEY-FLD                      
                    PERFORM  9999-CLOSE-ROUTINE         THRU 9999-EXIT          
           END-EVALUATE.                                                        
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5300-READ-BP13K022.                                                      
      ******************************************************************        
                                                                                
            MOVE SPACES                              TO BP13K022-REC.           
            INITIALIZE                                  BP13K022-REC.           
                                                                                
            MOVE FD00-NUM-REGN                       TO K022-NUM-REGN.          
                                                                                
            READ BP13K022 KEY IS K022-NUM-REGN.                                 
                                                                                
            EVALUATE WS-K022-STATUS                                             
                WHEN 00                                                         
                     PERFORM 5400-READ-BP13K024    THRU 5400-EXIT               
                WHEN 23                                                         
                     MOVE SPACES TO LD24-CSM                                    
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K022 : ' WS-K022-STATUS         
                             ' REGN NO = ' K022-NUM-REGN                        
                     PERFORM  9999-CLOSE-ROUTINE   THRU 9999-EXIT               
            END-EVALUATE.                                                       
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5400-READ-BP13K024.                                                      
      ******************************************************************        
                                                                                
            MOVE SPACES                                TO BP13K024-REC.         
            INITIALIZE                                    BP13K024-REC.         
                                                                                
            MOVE K022-NUM-OIC                          TO K024-NUM-OIC.         
                                                                                
            READ BP13K024.                                                      
                                                                                
            EVALUATE WS-K024-STATUS                                             
                WHEN 00                                                         
                     MOVE K024-NME-OIC                 TO LD24-CSM              
                WHEN 23                                                         
                     MOVE SPACES                       TO LD24-CSM              
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K024 : ' WS-K024-STATUS         
                             ' OIC  = ' K024-NUM-OIC                            
                     PERFORM  9999-CLOSE-ROUTINE     THRU 9999-EXIT             
            END-EVALUATE.                                                       
                                                                                
       5400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6000-PRINT-REPORT.                                                       
      ******************************************************************        
                                                                                
           PERFORM 6100-CHECK-WRITE-HEADER      THRU 6100-EXIT.                 
                                                                                
           IF WS-FIRST-WRITE = 'Y'                                              
              MOVE FD00-NUM-PROJECT               TO WS-NUM-PROJECT             
              MOVE 'N'                            TO WS-FIRST-WRITE             
           END-IF.                                                              
                                                                                
                                                                                
           IF FD00-NUM-PROJECT NOT = WS-NUM-PROJECT                             
              PERFORM 7000-PRINT-HEADER           THRU 7000-EXIT                
              MOVE 1                              TO   WS-SNO                   
              MOVE WS-SNO                         TO   LD24-SN                  
              MOVE FD00-NUM-PROJECT               TO   WS-NUM-PROJECT           
              WRITE BP13LD24-REC                  FROM LD24-DETAIL01            
           ELSE                                                                 
              PERFORM 6100-CHECK-WRITE-HEADER     THRU 6100-EXIT                
              ADD  1                              TO   WS-SNO                   
              MOVE WS-SNO                         TO   LD24-SN                  
              WRITE BP13LD24-REC                  FROM LD24-DETAIL01            
           END-IF.                                                              
                                                                                
           ADD 1                                  TO WS-LINE-CTR.               
           ADD 1                                  TO WS-CNT-LD24-WRITE.         
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6100-CHECK-WRITE-HEADER.                                                 
      ******************************************************************        
                                                                                
           IF (WS-LINE-CTR  >  WS-MAX-LINE) OR                                  
                                            (WS-FIRST-WRITE = 'Y')              
              PERFORM 7000-PRINT-HEADER      THRU 7000-EXIT                     
           END-IF.                                                              
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7000-PRINT-HEADER.                                                       
      ******************************************************************        
                                                                                
           ADD 1                               TO WS-PAGE-CTR.                  
           MOVE ZEROES                         TO LD24-PAGE-HDR.                
           MOVE WS-PAGE-CTR                    TO LD24-PAGE-HDR.                
                                                                                
           WRITE BP13LD24-REC    FROM LD24-HEADER01 AFTER PAGE.                 
           WRITE BP13LD24-REC    FROM LD24-HEADER02.                            
           WRITE BP13LD24-REC    FROM LD24-HEADER03 AFTER 2.                    
           WRITE BP13LD24-REC    FROM LD24-HEADER04.                            
                                                                                
           INITIALIZE WS-LINE-CTR.                                              
           MOVE 5  TO WS-LINE-CTR.                                              
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CD24'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13FD00 RECORDS READ............. : '            
                    WS-FD00-READ.                                               
           DISPLAY '(2) NO OF BP13LD24 RECORDS WRITTEN.......... : '            
                    WS-CNT-LD24-WRITE.                                          
           DISPLAY '(3) NO OF BP13K800 RECORDS NOT FOUND ....... : '            
                    WS-K800-NOTFND.                                             
           DISPLAY '(4) NO OF BP13K800 RECORDS ELIG-TAG NOT = Y. : '            
                    WS-K800-ELIG-TAG-N.                                         
           DISPLAY '(5) NO OF DATE-HSGNT-SUBMIT AND AHG-PROCESS '               
           DISPLAY '    RECORDS = SPACES........................ : '            
                    WS-HSGNT-AHG-SPACE.                                         
                                                                                
           CLOSE    BP13FD00                                                    
                    SY02F001                                                    
                    BP13K800                                                    
                    BP13K022                                                    
                    BP13K024                                                    
                    BM06K110                                                    
                    BP13LD24.                                                   
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR CLOSING : ' WS-K800-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 - ERROR CLOSING : ' WS-K110-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K022 - ERROR CLOSING : ' WS-K022-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K024-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K024 - ERROR CLOSING : ' WS-K024-STATUS              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
