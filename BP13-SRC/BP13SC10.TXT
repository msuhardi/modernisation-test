/*-----------------------------------------------------------------*/           
/* SYSTEM OF COMMITMENT - DBSS  (BP13)                             */           
/*-----------------------------------------------------------------*/           
/*-----------------------------------------------------------------*/           
/* PROGRAM TO PRINT STATUS OF DBSS LOAN ACCOUNT ON EVERY MONTH     */           
/*-----------------------------------------------------------------*/           
/* CHG-NO   BY  ON           DESCRIPTION                           */           
/* -------- --  --           ------------                          */           
/* BP132934 BA2 07/11/2006   NEW PRG.                              */           
/* BP133209 ED1 04/02/2008   CHGE TYP_ACTUSE POSN INTO 1663        */           
/*-----------------------------------------------------------------*/           
                                                                                
PROC FORMAT;                                                                    
                                                                                
     VALUE RMFMT  1 = '1 RM  '                                                  
                  2 = '2 RM  '                                                  
                  3 = '3 RM  '                                                  
                  4 = '4 RM  '                                                  
                  5 = '5 RM  '                                                  
                  6 = 'EXEC  '                                                  
                  7 = 'HUDC  '                                                  
                  8 = '  MG  ';                                                 
                                                                                
     VALUE ASFMT  1 = 'ABORTIVE (A)'                                            
                  2 = 'CURRENT  (C)'                                            
                  3 = 'FUTURE   (F)'                                            
                  4 = 'VACANT   (V)'                                            
                  5 = 'EX-LESSESS WITH O/S  BAL(X)'                             
                  6 = 'EX-LESSESS WITH ZERO BAL(Z)';                            
                                                                                
DATA BP13FD04;                                                                  
     INFILE BP13FD04;                                                           
     INPUT @33   FD04_ACCT_STAT           $CHAR1.                               
           @298  FD04_AMT_LOAN_BAL        ZD9.2                                 
           @1663 FD04_CDE_TYP_ACTUSE      $CHAR2.;                              
                                                                                
IF FD04_CDE_TYP_ACTUSE ^= '  ' AND FD04_ACCT_STAT ^= ' ';                       
                                                                                
PROC SORT;                                                                      
     BY FD04_CDE_TYP_ACTUSE FD04_ACCT_STAT;                                     
                                                                                
DATA SY02F001;                                                                  
     INFILE SY02F001 END = EOF1;                                                
     INPUT  @01  DATE_CYM_CURR        YYMMDD8.                                  
            @21  DATE_CYM_PREV        $CHAR6.                                   
            @21  DATE_CY_PREV         $CHAR4.                                   
            @58  PREV_MTH_TXT         $CHAR3.;                                  
                                                                                
     CALL SYMPUT ('PREV_MTH',PREV_MTH_TXT);                                     
     CALL SYMPUT ('PREV_CY',DATE_CY_PREV);                                      
     CALL SYMPUT ('CURR_DTE',(PUT(DATE_CYM_CURR,DDMMYY10.)));                   
     CALL SYMPUT ('PREVDTE', DATE_CYM_PREV);                                    
     CALL SYMPUT ('PREMTXT', PREV_MTH_TXT);                                     
                                                                                
DATA _NULL_;                                                                    
                                                                                
     OPTIONS PAGESIZE = 55 LS = 132 NODATE;                                     
     FILE BP13LC08 NOTITLES HEADER = HH;                                        
                                                                                
     IF TOTAL = 0 THEN                                                          
        DO;                                                                     
          PUT _PAGE_;                                                           
          PUT // @1 '*** NO RECORDS PRINTED IN THIS RUN';                       
        END;                                                                    
                                                                                
     SET BP13FD04 END = EOF NOBS=TOTAL;                                         
         BY FD04_CDE_TYP_ACTUSE FD04_ACCT_STAT;                                 
                                                                                
    /* ============================================================= */         
    /* ARRAY TABA {AA,BB,CC}                                         */         
    /* ============================================================= */         
    /* DIMENSION:                                                    */         
    /*       9 X 7 X 2                                               */         
    /*       |   |    |                                              */         
    /*       |   |    COL: 1. OUT STANDING AMT                       */         
    /*       |   |         2. NO.OF A/CS                             */         
    /*       |   |                                                   */         
    /*       |   |                                                   */         
    /*       |   ROW : A/C STATUS                                    */         
    /*       |         1. A                                          */         
    /*       |         2. C                                          */         
    /*       |         3. F                                          */         
    /*       |         4. V                                          */         
    /*       |         5. X                                          */         
    /*       |         6. Z                                          */         
    /*       |         7. SUB-TOTAL                                  */         
    /*       |                                                       */         
    /*       ROW : FLAT TYPE                                         */         
    /*             1. 1 (1 ROOM)                                     */         
    /*             2. 2 (2 ROOM)                                     */         
    /*             3. 3 (3 ROOM)                                     */         
    /*             4. 4 (4 ROOM)                                     */         
    /*             5. 5 (5 ROOM)                                     */         
    /*             6. 6 (EXEC  )                                     */         
    /*             7. 7 (HUDC  )                                     */         
    /*             8. 8 (MG    )                                     */         
    /*             9. GRAND TOTAL                                    */         
    /*                                                               */         
    /* ============================================================= */         
                                                                                
    ARRAY TABA {9,7,2};                                                         
                                                                                
    /* ============================================================= */         
    /* INITIALIZE ARRAYS TABA                                        */         
    /* ============================================================= */         
                                                                                
    IF _N_ = 1 THEN                                                             
       DO;                                                                      
          DO AA = 1 TO 9;                                                       
             DO BB = 1 TO 7;                                                    
                DO CC = 1 TO 2;                                                 
                   TABA {AA,BB,CC} = 0;                                         
                END;                                                            
             END;                                                               
          END;                                                                  
          LENGTH WS_PREV_DATE      $8;                                          
          RETAIN TABA WS_PREV_DATE;                                             
       END;                                                                     
                                                                                
    /* ============================================================= */         
    /* FORMATING PREVIOUSE MONTH                                     */         
    /* ============================================================= */         
    WS_PREV_DATE = SUBSTR("&PREMTXT",1,3) || ' ' ||                             
                   SUBSTR("&PREV_CY",1,4);                                      
                                                                                
    /* ============================================================= */         
    /* CLASSIFY RECORDS BY FLAT TYPE                                 */         
    /* ============================================================= */         
                                                                                
     IF FD04_CDE_TYP_ACTUSE IN ('01','1 ','1R','R1') THEN                       
         AA = 1;                                                                
     ELSE                                                                       
     IF FD04_CDE_TYP_ACTUSE IN ('02','2 ','2R','R2') THEN                       
         AA = 2;                                                                
     ELSE                                                                       
     IF FD04_CDE_TYP_ACTUSE IN ('03','3 ','3R','R3') THEN                       
         AA = 3;                                                                
     ELSE                                                                       
     IF FD04_CDE_TYP_ACTUSE IN ('04','4 ','4R','R4') THEN                       
         AA = 4;                                                                
     ELSE                                                                       
     IF FD04_CDE_TYP_ACTUSE IN ('05','5 ','5R','R5') THEN                       
         AA = 5;                                                                
     ELSE                                                                       
     IF FD04_CDE_TYP_ACTUSE IN ('06','6 ','6R','R6') THEN                       
         AA = 6;                                                                
     ELSE                                                                       
     IF FD04_CDE_TYP_ACTUSE IN ('07','7 ','7R','R7') THEN                       
         AA = 7;                                                                
     ELSE                                                                       
     IF FD04_CDE_TYP_ACTUSE IN ('08','8 ','8R','R8') THEN                       
         AA = 8;                                                                
     ELSE                                                                       
         AA = 0;                                                                
                                                                                
    /* ============================================================= */         
    /* CLASSIFY RECORDS BY ACCOUNT STATUS                            */         
    /* ============================================================= */         
                                                                                
     IF FD04_ACCT_STAT = 'A' THEN                                               
         BB = 1;                                                                
     ELSE                                                                       
     IF FD04_ACCT_STAT = 'C' THEN                                               
         BB = 2;                                                                
     ELSE                                                                       
     IF FD04_ACCT_STAT = 'F' THEN                                               
         BB = 3;                                                                
     ELSE                                                                       
     IF FD04_ACCT_STAT = 'V' THEN                                               
         BB = 4;                                                                
     ELSE                                                                       
     IF FD04_ACCT_STAT = 'X' THEN                                               
         BB = 5;                                                                
     ELSE                                                                       
     IF FD04_ACCT_STAT = 'Z' THEN                                               
         BB = 6;                                                                
     ELSE                                                                       
         BB = 0;                                                                
                                                                                
    /* ============================================================= */         
    /* COMPUTATION                                                   */         
    /* ============================================================= */         
     IF AA ^= 0 AND BB ^= 0 THEN                                                
        DO;                                                                     
          IF FD04_AMT_LOAN_BAL ^= '         ' THEN                              
             DO ;                                                               
                TABA {AA,BB,1} = TABA {AA,BB,1} + FD04_AMT_LOAN_BAL;            
                TABA {AA,7,1} = TABA {AA,7,1} + FD04_AMT_LOAN_BAL;              
                TABA {9,7,1} = TABA {9,7,1} + FD04_AMT_LOAN_BAL;                
             END;                                                               
                                                                                
          TABA {AA,BB,2} + 1;                                                   
          TABA {AA,7,2} + 1;                                                    
          TABA {9,7,2} + 1;                                                     
        END;                                                                    
                                                                                
    /*============================================================== */         
    /* PRINT REPORT                                                  */         
    /*============================================================== */         
    IF EOF THEN DO;                                                             
       DO AA = 1 TO 8;                                                          
          IF TABA {AA,7,2} ^= 0 THEN DO;                                        
             DO BB = 1 TO 7;                                                    
                IF BB = 1 THEN                                                  
                   DO;                                                          
                     PUT @01 AA                RMFMT.                           
                         @13 BB                ASFMT.                           
                         @45 TABA {AA,BB,1} DOLLAR10.2                          
                         @67 TABA {AA,BB,2};                                    
                   END;                                                         
                ELSE IF BB = 7 THEN                                             
                   DO;                                                          
                     PUT /@45 '------------'                                    
                          @67 '-----';                                          
                     PUT  @23 'SUBTOTAL '                                       
                          @45 TABA {AA,BB,1} DOLLAR10.2                         
                          @67 TABA {AA,BB,2};                                   
                     PUT  @45 '------------'                                    
                          @67 '-----';                                          
                   END;                                                         
                ELSE                                                            
                   DO;                                                          
                     PUT  @13 BB               ASFMT.                           
                          @45 TABA {AA,BB,1} DOLLAR10.2                         
                          @67 TABA {AA,BB,2};                                   
                   END;                                                         
             END;                                                               
          END;                                                                  
       END;                                                                     
             PUT /@45 '------------'                                            
                  @67 '-----';                                                  
             PUT  @23 'GRAND TOTAL'                                             
                  @45 TABA {9,7,1} DOLLAR10.2                                   
                  @67 TABA {9,7,2};                                             
             PUT  @45 '------------'                                            
                  @67 '-----';                                                  
    END;                                                                        
                                                                                
RETURN;                                                                         
                                                                                
  HH:                                                                           
    PAGE+1;                                                                     
    CURRENT=DATE();                                                             
    PUT /@01       'BP13LC08'                                                   
        @12        'HDBCAT3'                                                    
        @46   'S Y S T E M   O F   C O M M I T M E N T'                         
        @105 'PAGE : ' @113 PAGE 4./                                            
        @27  'MONTHLY REPORT ON STATUS OF DBSS LOAN ACCOUNT '                   
        @73   WS_PREV_DATE                                                      
        @105 'DATE : ' @113 CURRENT DDMMYY10./                                  
    PUT @01 ' '/;                                                               
    PUT @01    'ROOM TYPE'                                                      
        @13    'A/C STATUS'                                                     
        @45    'OUTSTANDING-AMOUNT'                                             
        @67    'NO.OF A/CS';                                                    
    PUT @01 '-------------------------------------------------------------------
---------';                                                                     
    PUT @01 ' '/;                                                               
    RETURN;                                                                     
                                                                                
RETURN;                                                                         
                                                                                
