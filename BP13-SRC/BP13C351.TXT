       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C351.                                                    
       AUTHOR. REY ORBITA GASTADOR.                                             
      *================================================================*        
      *                                                                *        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT   (SOC)                  *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  1.  UPON SUB-TRANSFER, PROGRAM TO MATCH THE   *        
      *                      OCCUPIER RECORDS OF SUB-TRANSFER WITH THE *        
      *                      OCCUPIER RECORDS OF SOC.                  *        
      *                                                                *        
      *                  2.  OUTPUT THE MATCHED RECORDS BY REGN-NO     *        
      *                      FOR THE NEXT BATCH PROGRAM BP13C358.      *        
      *                                                                *        
      *                  3.  DELETE SUB-TRANSFER OCCUPIER RECORDS      *        
      *                      AFTER WRITE TO BP13F890.                  *        
      *                                                                *        
      *   INPUT FILES :      BP13F880                                  *        
      *                      BP13K820                                  *        
      *                                                                *        
      *   I-O FILES   :      BP13K890                                  *        
      *                                                                *        
      *   OUTPUT FILES:      BP13F890                                  *        
      *                      BP13F820                                  *        
      *                                                                *        
      *                                                                *        
      *   CHGE-NO   DATE   BY   DESCRIPTION                            *        
      *   -------  ------  ---  -------------------------------------  *        
      * BP130094   080296  EG1  REVISED FORMATTING.                    *        
      * BP130418   060498  RHB  Y2K                                    *        
      *================================================================*        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F880 ASSIGN TO BP13F880.                                  
           SELECT BP13K890 ASSIGN TO BP13K890                                   
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K890-KEY-FLD                         
                           FILE STATUS  IS K890-FS.                             
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K820-KEY-FLD                         
                           FILE STATUS  IS K820-FS.                             
           SELECT BP13F890 ASSIGN TO BP13F890.                                  
           SELECT BP13F820 ASSIGN TO BP13F820.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F880                                                             
           BLOCK CONTAINS    0 RECORDS                                          
           RECORD CONTAINS 2000 CHARACTERS                                      
           RECORDING MODE   IS F.                                               
       COPY BP13F880.                                                           
                                                                                
       FD  BP13K890                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K890.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BP13F890                                                             
           BLOCK CONTAINS    0  RECORDS                                         
           RECORD CONTAINS 400 CHARACTERS                                       
           RECORDING MODE   IS F.                                               
       COPY BP13F890.                                                           
                                                                                
       FD  BP13F820                                                             
           BLOCK CONTAINS    0  RECORDS                                         
           RECORD CONTAINS 400 CHARACTERS                                       
           RECORDING MODE   IS F.                                               
       COPY BP13F820.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-WORKING-AREA.                                                     
           05  WS-FLAGS.                                                        
               10 F880-EOF             PIC X.                                   
                  88 F880-IS-EOF       VALUE 'Y'.                               
               10 K890-EOF             PIC X.                                   
                  88 K890-IS-EOF       VALUE 'Y'.                               
               10 K820-EOF             PIC X.                                   
                  88 K820-IS-EOF       VALUE 'Y'.                               
           05  WS-FILE-STATUS.                                                  
               10 K890-FS              PIC 99.                                  
               10 K820-FS              PIC 99.                                  
           05  WS-COUNTERS.                                                     
               10 F880-READ-CTR        PIC 9(8)     VALUE ZEROES.               
               10 K890-READ-CTR        PIC 9(8)     VALUE ZEROES.               
               10 K820-READ-CTR        PIC 9(8)     VALUE ZEROES.               
               10 F890-WRTE-CTR        PIC 9(8)     VALUE ZEROES.               
               10 F820-WRTE-CTR        PIC 9(8)     VALUE ZEROES.               
               10 K890-DELETE-CTR      PIC 9(8)     VALUE ZEROES.               
                                                                                
       PROCEDURE DIVISION.                                                      
      *====================                                                     
       0000-MAIN-LOGIC.                                                         
      *====================                                                     
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-BP13F880 THRU 2000-EXIT.                           
           PERFORM 3000-PROCESS-RTN   THRU 3000-EXIT                            
                   UNTIL F880-IS-EOF.                                           
           PERFORM 9999-CLOSE-FILES   THRU 9999-EXIT.                           
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=======================                                                  
       1000-OPEN-FILES.                                                         
      *=======================                                                  
           OPEN INPUT  BP13F880                                                 
                       BP13K820                                                 
                I-O    BP13K890                                                 
                OUTPUT BP13F890                                                 
                       BP13F820.                                                
                                                                                
           IF K890-FS NOT = ZEROES AND 97                                       
              DISPLAY 'BP13K890 - OPEN ERROR, FS=' K890-FS                      
              GO TO    9999-CLOSE-FILES                                         
           END-IF.                                                              
                                                                                
           IF K820-FS NOT = ZEROES AND 97                                       
              DISPLAY 'BP13K820 - OPEN ERROR, FS=' K820-FS                      
              GO TO    9999-CLOSE-FILES                                         
           END-IF.                                                              
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=========================                                                
       2000-READ-BP13F880.                                                      
      *=========================                                                
                                                                                
           READ BP13F880 AT END                                                 
                MOVE 'Y' TO F880-EOF.                                           
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *============================                                             
       3000-PROCESS-RTN.                                                        
      *============================                                             
                                                                                
           ADD 1 TO F880-READ-CTR.                                              
           PERFORM 4000-PROCESS-K890 THRU 4000-EXIT.                            
           PERFORM 5000-PROCESS-K820 THRU 5000-EXIT.                            
                                                                                
           PERFORM 2000-READ-BP13F880        THRU 2000-EXIT.                    
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *============================                                             
       4000-PROCESS-K890.                                                       
      *============================                                             
                                                                                
           MOVE F880-KEY-FLD TO K890-NUM-REGN.                                  
           MOVE SPACES       TO K890-NUM-NRIC.                                  
           START BP13K890 KEY NOT < K890-KEY-FLD.                               
           IF K890-FS = 00                                                      
               PERFORM 4100-READ-BP13K890   THRU 4100-EXIT                      
               PERFORM 4500-OUTPUT-BP13F890 THRU 4500-EXIT                      
                   UNTIL (K890-NUM-REGN NOT = F880-KEY-FLD)                     
                      OR (K890-IS-EOF)                                          
           ELSE                                                                 
              MOVE 99 TO RETURN-CODE                                            
              DISPLAY 'BP13K890 START ERROR STATUS = ' K890-FS                  
              DISPLAY 'K890-KEY-FLD = ' K890-KEY-FLD                            
           END-IF.                                                              
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *============================                                             
       4100-READ-BP13K890.                                                      
      *============================                                             
           READ BP13K890 NEXT RECORD                                            
             AT END MOVE 'Y' TO K890-EOF.                                       
           ADD 1  TO K890-READ-CTR.                                             
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *============================                                             
       4500-OUTPUT-BP13F890.                                                    
      *============================                                             
                                                                                
           IF K890-NUM-REGN = F880-KEY-FLD                                      
              WRITE  BP13F890-REC FROM BP13K890-REC                             
              ADD 1  TO F890-WRTE-CTR                                           
              DELETE BP13K890                                                   
              IF K890-FS = 00                                                   
                  ADD 1 TO K890-DELETE-CTR                                      
              ELSE                                                              
                  DISPLAY 'BP13K890-DELETE ERROR STATUS = ' K890-FS             
                  DISPLAY 'K890-KEY-FLD = ' K890-KEY-FLD                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 4100-READ-BP13K890   THRU 4100-EXIT.                         
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *============================                                             
       5000-PROCESS-K820.                                                       
      *============================                                             
           MOVE F880-KEY-FLD TO K820-NUM-REGN.                                  
           MOVE SPACES       TO K820-NUM-NRIC.                                  
           START BP13K820 KEY NOT < K820-KEY-FLD.                               
           IF K820-FS = 00                                                      
               PERFORM 5100-READ-BP13K820   THRU 5100-EXIT                      
               PERFORM 5500-OUTPUT-BP13F820 THRU 5500-EXIT                      
                   UNTIL (K820-NUM-REGN NOT = F880-KEY-FLD)                     
                      OR (K820-IS-EOF)                                          
           ELSE                                                                 
               MOVE 99 TO RETURN-CODE                                           
               DISPLAY 'BP13K820 START ERROR STATUS = ' K820-FS                 
               DISPLAY 'K820-KEY-FLD = ' K820-KEY-FLD                           
           END-IF.                                                              
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *============================                                             
       5100-READ-BP13K820.                                                      
      *============================                                             
                                                                                
           READ BP13K820 NEXT RECORD                                            
             AT END MOVE 'Y' TO K820-EOF.                                       
                                                                                
           ADD 1   TO K820-READ-CTR.                                            
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *============================                                             
       5500-OUTPUT-BP13F820.                                                    
      *============================                                             
                                                                                
           IF K820-NUM-REGN = F880-KEY-FLD                                      
               WRITE   BP13F820-REC FROM BP13K820-REC                           
               ADD 1   TO F820-WRTE-CTR.                                        
                                                                                
           PERFORM 5100-READ-BP13K820   THRU 5100-EXIT.                         
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=======================                                                  
       9999-CLOSE-FILES.                                                        
      *=======================                                                  
           DISPLAY '*****************'.                                         
           DISPLAY 'PROGRAM: BP13C351'.                                         
           DISPLAY '*****************'.                                         
           DISPLAY 'FILE STATISTICS'.                                           
           DISPLAY '@BP13F880'.                                                 
           DISPLAY '  =>RECORDS READ    = ' F880-READ-CTR.                      
           DISPLAY '@BP13K890'.                                                 
           DISPLAY '  =>RECORDS READ    = ' K890-READ-CTR.                      
           DISPLAY '@BP13K820'.                                                 
           DISPLAY '  =>RECORDS READ    = ' K820-READ-CTR.                      
           DISPLAY '@BP13F890'.                                                 
           DISPLAY '  =>RECORDS WRITE   = ' F890-WRTE-CTR.                      
           DISPLAY '@BP13F820'.                                                 
           DISPLAY '  =>RECORDS WRITE   = ' F820-WRTE-CTR.                      
           DISPLAY '@BP13K890'.                                                 
           DISPLAY '  =>RECORDS DELETED = ' K890-DELETE-CTR.                    
           CLOSE BP13F880  BP13K890  BP13K820                                   
                           BP13F890  BP13F820.                                  
           STOP RUN.                                                            
       9999-EXIT.                                                               
           EXIT.                                                                
