      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C21T.                                                 
       AUTHOR.        RALSTON JAMES ELIEZER B MEDRANO.                          
      *DATE-WRITTEN.  01/08/2011.                                               
      *-----------------------------------------------------------*             
                                                                                
      *===========================================================*             
      *        SOC - S Y S T E M  O F  C O M M I T M E N T        *             
      *===========================================================*             
      *  OBJECTIVES : READ BP13KQ10 AND WRITE RECORDS INTO        *             
      *               BP13FQ10 AND BP13FQ1A WITH THE FOLLOWING    *             
      *               CONDITIONS.                                 *             
      *                                                           *             
      *    1. WRITE RECORDS TO BP13FQ10                           *             
      *       - IF RECORDS ARE NOT FOUND IN BP13K800              *             
      *       - IF RECORDS ARE FOUND IN BP13K800 WITH STATUS = 'C'*             
      *                                                           *             
      *    2. WRITE RECORDS TO BP13FQ1A                           *             
      *       - IF RECORDS ARE FOUND IN BP13K800 WITH STATUS NOT  *             
      *         EQUAL TO 'C'                                      *             
      *                                                           *             
      *    INPUT FILES:  BP13K800                                 *             
      *                                                           *             
      *    OUTPUT FILES: BP13FQ10                                 *             
      *                  BP13FQ1A                                 *             
      *                                                           *             
      *---------------------------------------------------------- *             
      *===========================================================*             
      *  REVISION HISTORY :                                       *             
      *  CHG-REQ#  BY    DATE        DESCRIPTION                  *             
      *  --------  ----  ----------  ---------------------------- *             
      *  BP134135  RJE1  08/01/11    NEW PROGRAM                  *             
      ************************************************************              
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
      *-----------------------*                                                 
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *-----------------------*                                                 
       INPUT-OUTPUT SECTION.                                                    
      *-----------------------*                                                 
       FILE-CONTROL.                                                            
                                                                                
                                                                                
           SELECT BP13KQ10 ASSIGN       TO BP13KQ10                             
                           ACCESS       IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS KQ10-KEY-FLD                         
                           FILE STATUS  IS WS-BP13KQ10-STATUS.                  
                                                                                
           SELECT BP13K800 ASSIGN       TO BP13K800                             
                           ACCESS       IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K800-NUM-REGN                        
                           FILE STATUS  IS WS-BP13K800-STATUS.                  
                                                                                
           SELECT BP13FQ10 ASSIGN       TO BP13FQ10.                            
           SELECT BP13FQ1A ASSIGN       TO BP13FQ1A.                            
                                                                                
      *--------------*                                                          
       DATA DIVISION.                                                           
      *--------------*                                                          
       FILE SECTION.                                                            
      *--------------*                                                          
                                                                                
       FD  BP13KQ10                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13KQ10.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13FQ10                                                             
           BLOCK CONTAINS  0   RECORDS                                          
           LABEL RECORDS   ARE STANDARD                                         
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 400 CHARACTERS.                                      
       01  P13FQ10-REC     PIC X(400).                                          
                                                                                
       FD  BP13FQ1A                                                             
           BLOCK CONTAINS  0   RECORDS                                          
           LABEL RECORDS   ARE STANDARD                                         
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 400 CHARACTERS.                                      
       01  P13FQ1A-REC     PIC X(400).                                          
                                                                                
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-BP13K800-STATUS         PIC 9(2)    VALUE  0.                 
           05  WS-BP13KQ10-STATUS         PIC 9(2)    VALUE  0.                 
           05  WS-KQ10-EOF                PIC X(1)    VALUE 'N'.                
           05  WS-FOUND                   PIC X(1)    VALUE 'N'.                
           05  WS-K800-FOUND              PIC X(1)    VALUE SPACES.             
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-KQ10-READ               PIC 9(5)    VALUE  0.                 
           05  WS-K800-READ               PIC 9(5)    VALUE  0.                 
           05  WS-FQ10-WRITE              PIC 9(5)    VALUE  0.                 
           05  WS-FQ1A-WRITE              PIC 9(5)    VALUE  0.                 
           05  WS-FOUND-CNT               PIC 9(5)    VALUE  0.                 
           05  WS-FOUND2-CNT              PIC 9(5)    VALUE  0.                 
           05  WS-NTFOUND-CNT             PIC 9(5)    VALUE  0.                 
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      *-------------*                                                           
       0000-CONTROL.                                                            
      *-------------*                                                           
            PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                       
            PERFORM 2000-READ-BP13KQ10    THRU 2000-EXIT.                       
            PERFORM 4000-PROCESS-REC      THRU 4000-EXIT                        
               UNTIL WS-KQ10-EOF = 'Y'.                                         
            PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
           OPEN INPUT  BP13K800                                                 
                       BP13KQ10                                                 
                OUTPUT BP13FQ10                                                 
                       BP13FQ1A.                                                
                                                                                
           IF WS-BP13K800-STATUS NOT = 00 AND 97                                
              DISPLAY 'OPENING BP13K800 ERROR ' WS-BP13K800-STATUS              
              MOVE     WS-BP13K800-STATUS  TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-FILES    THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-BP13KQ10-STATUS NOT = 00 AND 97                                
              DISPLAY 'OPENING BP13KQ10 ERROR ' WS-BP13KQ10-STATUS              
              MOVE     WS-BP13KQ10-STATUS  TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-FILES    THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------*                                                     
       2000-READ-BP13KQ10.                                                      
      *-------------------*                                                     
                                                                                
           MOVE LOW-VALUES TO KQ10-KEY-FLD.                                     
                                                                                
           READ BP13KQ10 NEXT                                                   
              AT END MOVE 'Y' TO WS-KQ10-EOF                                    
              GO TO 2000-EXIT.                                                  
                                                                                
           EVALUATE WS-BP13KQ10-STATUS                                          
                                                                                
              WHEN 00                                                           
                 ADD 1                TO WS-KQ10-READ                           
              WHEN 23                                                           
                 DISPLAY 'BP13KQ10 RECORD NOT FOUND'                            
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READ BP13KQ10,STATUS '                  S       
                         WS-BP13KQ10-STATUS                                     
                 MOVE WS-BP13KQ10-STATUS TO RETURN-CODE                         
                 GO TO 9000-CLOSE-FILES                                         
                                                                                
           END-EVALUATE.                                                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3000-READ-BP13K800.                                                      
      *-------------------*                                                     
                                                                                
           MOVE KQ10-NUM-REGN  TO   K800-NUM-REGN.                              
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF WS-BP13K800-STATUS = 00                                           
              MOVE 'Y' TO WS-K800-FOUND                                         
           ELSE                                                                 
           IF WS-BP13K800-STATUS = 23                                           
              MOVE 'N' TO WS-K800-FOUND                                         
              DISPLAY 'RECORD NOT FOUND IN BP13K800 ' WS-BP13K800-STATUS        
              DISPLAY '       K800-NUM-REGN = ' K800-NUM-REGN                   
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K800 = ' WS-BP13K800-STATUS            
              DISPLAY '      K800-NUM-REGN    = ' K800-NUM-REGN                 
              MOVE    WS-BP13K800-STATUS TO RETURN-CODE                         
              PERFORM 9000-CLOSE-FILES.                                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       4000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           INITIALIZE                     WS-K800-FOUND.                        
                                                                                
           PERFORM 3000-READ-BP13K800     THRU 3000-EXIT                        
                                                                                
           IF WS-K800-FOUND = 'Y'                                               
              IF K800-NUM-STATUS = 'C'                                          
                 PERFORM 5000-WRITE-BP13FQ10  THRU 5000-EXIT                    
                 ADD 1 TO WS-FOUND-CNT                                          
              ELSE                                                              
                 PERFORM 6000-WRITE-BP13FQ1A  THRU 6000-EXIT                    
                 ADD 1 TO WS-FOUND2-CNT                                         
              END-IF                                                            
           ELSE                                                                 
              PERFORM 5000-WRITE-BP13FQ10  THRU 5000-EXIT                       
              ADD 1    TO WS-NTFOUND-CNT                                        
           END-IF                                                               
                                                                                
           PERFORM 2000-READ-BP13KQ10      THRU 2000-EXIT.                      
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------------*                                                   
       5000-WRITE-BP13FQ10.                                                     
      *---------------------*                                                   
                                                                                
           MOVE SPACES         TO P13FQ10-REC.                                  
                                                                                
           MOVE BP13KQ10-REC   TO P13FQ10-REC.                                  
                                                                                
           WRITE P13FQ10-REC.                                                   
           ADD 1 TO WS-FQ10-WRITE.                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       6000-WRITE-BP13FQ1A.                                                     
      *----------------------*                                                  
                                                                                
           MOVE SPACES         TO P13FQ1A-REC.                                  
                                                                                
           MOVE BP13KQ10-REC   TO P13FQ1A-REC.                                  
                                                                                
           WRITE P13FQ1A-REC.                                                   
           ADD 1 TO WS-FQ1A-WRITE.                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
           CLOSE BP13KQ10                                                       
                 BP13K800                                                       
                 BP13FQ10                                                       
                 BP13FQ1A.                                                      
                                                                                
           IF WS-BP13KQ10-STATUS NOT = 00                                       
              DISPLAY 'BP13KQ10 CLOSE ERROR,STATUS IS '                 US      
                      WS-BP13KQ10-STATUS                                        
              MOVE WS-BP13KQ10-STATUS TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           IF WS-BP13K800-STATUS NOT = 00                                       
              DISPLAY 'BP13K800 CLOSE ERROR,STATUS IS '                 US      
                      WS-BP13K800-STATUS                                        
              MOVE WS-BP13K800-STATUS TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13C21T CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13KQ10 RECORDS READ        = ' WS-KQ10-READ.              
           DISPLAY 'BP13FQ10 RECORDS WRITTEN     = ' WS-FQ10-WRITE.             
           DISPLAY 'BP13FQ1A RECORDS WRITTEN     = ' WS-FQ1A-WRITE.             
           DISPLAY 'NOT FOUND IN K800            = ' WS-NTFOUND-CNT.            
           DISPLAY 'FOUND WITH STATUS  = C       = ' WS-FOUND-CNT.              
           DISPLAY 'FOUND WITH STATUS ^= C       = ' WS-FOUND2-CNT.             
           DISPLAY SPACES.                                                      
           DISPLAY '*==============================================*'.          
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
