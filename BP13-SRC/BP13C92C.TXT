      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13C92C.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  28 JUL 2016.                                              
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : TO CREATE BP13FW3C BASED ON BP13FA20,       *           
      *                 BP13FA30, BP13FA10 AND BP13FB20             *           
      *   INPUT FILES :                                             *           
      *   1.  BP13FA20                                              *           
      *                                                             *           
      *   OUTPUT FILES:                                             *           
      *   1.  BP13FW3C                                              *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP136299 28/07/2016 FNP1 NEW PROGRAM                        *           
      * BP137084 14/11/2017 ZDD1 ADD TOTAL-BOOK BEFORE READING KB20 *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FA20 ASSIGN TO BP13FA20.                                  
           SELECT BP13FA30 ASSIGN TO BP13FA30.                                  
                                                                                
           SELECT BP13KA10 ASSIGN TO BP13KA10                                   
                  ACCESS MODE IS RANDOM                                         
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS KA10-KEY-FLD                                    
                  FILE STATUS IS KA10-STATUS.                                   
                                                                                
           SELECT BP13KB20 ASSIGN TO BP13KB20                                   
                  ACCESS MODE IS RANDOM                                         
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS KB20-KEY-FLD                                    
                  FILE STATUS IS KB20-STATUS.                                   
                                                                                
           SELECT BP13FW3C ASSIGN TO BP13FW3C.                                  
           SELECT BP13FW3D ASSIGN TO BP13FW3D.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13FA20                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  200 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13FA20.                                                           
                                                                                
       FD   BP13FA30                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  200 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13FA30.                                                           
                                                                                
       FD   BP13KB20                                                            
            RECORD CONTAINS 3300 CHARACTERS.                                    
       COPY BP13KB20.                                                           
                                                                                
       FD   BP13KA10                                                            
            RECORD CONTAINS  800 CHARACTERS.                                    
       COPY BP13KA10.                                                           
                                                                                
       FD   BP13FW3C                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  500 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13FW3C.                                                           
                                                                                
       FD   BP13FW3D                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  300 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13FW3D.                                                           
                                                                                
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  WS-CURR-DATE           PIC X(08) VALUE ZEROES.                       
       01  WS-FA20-READ           PIC 9(08) VALUE ZEROES.                       
       01  WS-FA30-READ           PIC 9(08) VALUE ZEROES.                       
       01  WS-FW3C-WRITE          PIC 9(08) VALUE ZEROES.                       
       01  WS-FW3D-WRITE          PIC 9(08) VALUE ZEROES.                       
       01  WS-FA20-EOF            PIC X     VALUE SPACES.                       
       01  WS-FA30-EOF            PIC X     VALUE SPACES.                       
       01  WS-KB20-FOUND          PIC X     VALUE SPACES.                       
       01  KB20-STATUS            PIC 9(02) VALUE ZEROES.                       
       01  KA10-STATUS            PIC 9(02) VALUE ZEROES.                       
       01  WS-TOTAL-BOOK          PIC 9(04) VALUE ZEROES.                       
       01  WS-TOTAL-UNIT          PIC 9(04) VALUE ZEROES.                       
       01  WS-DTE-BALLOT          PIC X(06) VALUE SPACES.                       
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NUM-FT            PIC X(02) VALUE SPACES.                     
           05  WS-FA30-MATCHED      PIC 9(8)    VALUE ZEROES.                   
           05  WS-FA30-NOMATCH      PIC 9(8)    VALUE ZEROES.                   
           05  WS-FA20-NOMATCH      PIC 9(8)    VALUE ZEROES.                   
           05  WS-NUM-NT            PIC X(03)   VALUE SPACES.                   
                                                                                
       01  WS-FA20-KEY.                                                         
           05  WS-FA20-NUM-PRJCT        PIC X(03).                              
                                                                                
       01  WS-FA30-KEY.                                                         
           05  WS-FA30-NUM-PRJCT        PIC X(03).                              
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 1100-READ-FA20    THRU 1100-EXIT.                            
           PERFORM 1200-READ-FA30    THRU 1200-EXIT.                            
           PERFORM 2000-PROCESS-REC  THRU 2000-EXIT                             
                   UNTIL WS-FA20-KEY = HIGH-VALUES  AND                         
                         WS-FA30-KEY = HIGH-VALUES.                             
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13FA20                                                 
                       BP13FA30                                                 
                       BP13KA10                                                 
                       BP13KB20                                                 
                OUTPUT BP13FW3C                                                 
                       BP13FW3D.                                                
                                                                                
           IF KA10-STATUS NOT = ZEROES AND 97                                   
              DISPLAY 'BP13KA10 OPEN ERROR ' KA10-STATUS                        
              MOVE KA10-STATUS        TO RETURN-CODE                            
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF KB20-STATUS NOT = ZEROES AND 97                                   
              DISPLAY 'BP13KB20 OPEN ERROR ' KB20-STATUS                        
              MOVE KB20-STATUS        TO RETURN-CODE                            
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       1100-READ-FA20.                                                          
      *---------------*                                                         
                                                                                
           READ BP13FA20 AT END                                                 
                MOVE HIGH-VALUES TO WS-FA20-KEY                                 
                GO TO 1100-EXIT.                                                
                                                                                
           MOVE FA20-NUM-PRJCT    TO WS-FA20-NUM-PRJCT.                         
                                                                                
           ADD 1 TO WS-FA20-READ.                                               
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       1200-READ-FA30.                                                          
      *---------------*                                                         
                                                                                
           READ BP13FA30 AT END                                                 
                MOVE HIGH-VALUES TO WS-FA30-KEY                                 
                GO TO 1200-EXIT.                                                
                                                                                
           MOVE FA30-NUM-PRJCT    TO WS-FA30-NUM-PRJCT.                         
                                                                                
           ADD 1 TO WS-FA30-READ.                                               
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       2000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           MOVE ZEROS    TO WS-TOTAL-BOOK.                                      
           MOVE ZEROS    TO WS-TOTAL-UNIT.                                      
           MOVE SPACES   TO WS-NUM-FT.                                          
           MOVE SPACES   TO WS-NUM-NT.                                          
           MOVE SPACES   TO WS-DTE-BALLOT.                                      
                                                                                
           IF WS-FA30-KEY = WS-FA20-KEY                                         
              ADD 1 TO WS-FA30-MATCHED                                          
              PERFORM 3000-READ-BP13KA10   THRU 3000-EXIT                       
              PERFORM 2100-PROCESS-MATCHED   THRU 2100-EXIT                     
                 UNTIL WS-FA30-KEY > WS-FA20-KEY                                
                 OR WS-FA30-KEY = HIGH-VALUES                                   
                                                                                
              PERFORM 4000-WRITE-BP13FW3C  THRU 4000-EXIT                       
                                                                                
              PERFORM 1100-READ-FA20       THRU 1100-EXIT                       
           ELSE                                                                 
              IF WS-FA30-KEY < WS-FA20-KEY                                      
                 ADD 1 TO WS-FA30-NOMATCH                                       
                 PERFORM 1200-READ-FA30     THRU 1200-EXIT                      
              ELSE                                                              
                 ADD 1 TO WS-FA20-NOMATCH                                       
                 PERFORM 1100-READ-FA20     THRU 1100-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2100-PROCESS-MATCHED.                                                    
      *---------------*                                                         
                                                                                
           IF FA30-NUM-REGN(1:1) = SPACES OR '#'                                
                                                                                
              MOVE SPACES   TO  BP13FW3C-REC                                    
              INITIALIZE        BP13FW3C-REC                                    
           ELSE                                                                 
              MOVE SPACES   TO  BP13FW3C-REC                                    
              INITIALIZE        BP13FW3C-REC                                    
              MOVE SPACES   TO  BP13FW3D-REC                                    
              INITIALIZE        BP13FW3D-REC                                    
                                                                                
              IF FA30-DTE-ACCEPT > ZEROS                                        
                 ADD 1 TO WS-TOTAL-BOOK                                         
              END-IF                                                            
                                                                                
              PERFORM 3500-READ-BP13KB20     THRU 3500-EXIT                     
              IF WS-KB20-FOUND = 'Y'                                            
                 MOVE KB20-NUM-HH            TO FW3D-NUM-HOUSEHOLD              
                 MOVE KB20-NUM-HH            TO FW3D-NUM-BALLOT-HH              
                 MOVE KB20-NUM-APPLT1-UIN    TO FW3D-NUM-NRIC1                  
                 MOVE KB20-NUM-NEW-TOWN      TO FW3D-NUM-NT-ZONE                
                 MOVE KB20-NUM-FLAT-TYPE     TO FW3D-NUM-FLAT-TYPE              
                                                                                
                 IF WS-NUM-NT = SPACES                                          
                   IF KB20-NUM-NEW-TOWN NOT = SPACES                            
                      MOVE KB20-NUM-NEW-TOWN  TO WS-NUM-NT                      
                    END-IF                                                      
                 END-IF                                                         
                                                                                
                 IF KB20-DTE-BALLOT NOT = SPACES                                
                    MOVE KB20-DTE-BALLOT         TO FW3D-DTE-BALLOT             
                 ELSE                                                           
                    MOVE KB20-DTE-REQUESTED(1:6) TO FW3D-DTE-BALLOT             
                 END-IF                                                         
                                                                                
                 IF WS-DTE-BALLOT  = SPACES                                     
                    IF KB20-DTE-BALLOT NOT = SPACES                             
                       MOVE KB20-DTE-BALLOT      TO WS-DTE-BALLOT               
                    END-IF                                                      
                 END-IF                                                         
                                                                                
                 PERFORM 4100-WRITE-BP13FW3D   THRU 4100-EXIT                   
                                                                                
               END-IF                                                           
           END-IF.                                                              
                                                                                
           ADD 1         TO WS-TOTAL-UNIT.                                      
                                                                                
           PERFORM 1200-READ-FA30         THRU 1200-EXIT.                       
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       3000-READ-BP13KA10.                                                      
      *---------------*                                                         
                                                                                
           MOVE SPACES         TO KA10-KEY-FLD.                                 
           MOVE FA20-NUM-PRJCT TO KA10-NUM-PRJCT.                               
                                                                                
           READ BP13KA10.                                                       
                                                                                
           EVALUATE KA10-STATUS                                                 
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN 10                                                           
              WHEN 23                                                           
                 MOVE SPACES              TO KA10-NME-PRJCT                     
                                             KA10-NUM-NEW-TOWN                  
                                             KA10-DTE-ESTMD-TOP                 
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BP13KA10, STATUS IS '                   
                      KA10-STATUS                                               
                 MOVE KA10-STATUS TO RETURN-CODE                                
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       3500-READ-BP13KB20.                                                      
      *---------------*                                                         
                                                                                
           MOVE SPACES         TO WS-KB20-FOUND.                                
           MOVE SPACES         TO KB20-KEY-FLD.                                 
           MOVE FA30-NUM-REGN  TO KB20-NUM-REGN.                                
                                                                                
           READ BP13KB20.                                                       
                                                                                
           EVALUATE KB20-STATUS                                                 
              WHEN 00                                                           
                 MOVE 'Y'   TO WS-KB20-FOUND                                    
              WHEN 10                                                           
              WHEN 23                                                           
                 MOVE SPACES              TO KB20-DTE-BALLOT                    
                                             KB20-NUM-STATUS                    
                                             KB20-NUM-NEW-TOWN                  
                                             KB20-NUM-HH                        
                                             KB20-NUM-APPLT1-UIN                
                                             KB20-NUM-FLAT-TYPE                 
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BP13KB20, STATUS IS '                   
                      KB20-STATUS                                               
                 MOVE KB20-STATUS TO RETURN-CODE                                
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------*                                                         
       4000-WRITE-BP13FW3C.                                                     
      *---------------*                                                         
                                                                                
           MOVE FA20-NUM-PRJCT         TO FW3C-NUM-ZONE.                        
           MOVE FA20-NUM-PRJCT(2:2)    TO FW3C-NUM-FT.                          
           IF WS-NUM-NT = SPACES                                                
               MOVE FA20-NUM-PRJCT         TO FW3C-NUM-NT                       
           ELSE                                                                 
               MOVE WS-NUM-NT              TO FW3C-NUM-NT                       
           END-IF.                                                              
           MOVE WS-NUM-NT              TO FW3C-NUM-NT-CHAR3.                    
           IF   WS-DTE-BALLOT = SPACES                                          
                MOVE FA30-DTE-UPDATE(1:6) TO FW3C-DTE-BALLOT                    
           ELSE                                                                 
                MOVE WS-DTE-BALLOT        TO FW3C-DTE-BALLOT                    
           END-IF.                                                              
           MOVE 'N'                    TO FW3C-NUM-BONUS.                       
                                                                                
           IF FA20-NUM-PRJCT(1:1) = 'D'                                         
              MOVE 'DBS'                  TO FW3C-NUM-SELECTION                 
           ELSE                                                                 
              MOVE 'EC '                  TO FW3C-NUM-SELECTION                 
           END-IF.                                                              
           MOVE FA20-DTE-ACTUAL-TOP    TO FW3C-DTE-ACTUAL-TOP.                  
           MOVE KA10-NME-PRJCT         TO FW3C-NME-PROJECT.                     
           MOVE KA10-DTE-ESTMD-TOP     TO FW3C-DTE-EST-TOP.                     
           MOVE WS-TOTAL-UNIT          TO FW3C-NUM-TOTAL-FLAT.                  
           MOVE WS-TOTAL-BOOK          TO FW3C-NUM-TOTAL-BOOK.                  
           COMPUTE FW3C-NUM-AVAIL-TOTAL = FW3C-NUM-TOTAL-FLAT -                 
                                          FW3C-NUM-TOTAL-BOOK.                  
                                                                                
           WRITE BP13FW3C-REC.                                                  
           ADD   1  TO WS-FW3C-WRITE.                                           
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       4100-WRITE-BP13FW3D.                                                     
      *---------------*                                                         
                                                                                
           IF FA30-NUM-PRJCT(1:1) = 'D'                                         
              MOVE 'DBS'                  TO FW3D-NUM-SELTYPE                   
                                             FW3D-NUM-ALLO-CAT                  
           ELSE                                                                 
              MOVE 'EC '                  TO FW3D-NUM-SELTYPE                   
                                             FW3D-NUM-ALLO-CAT                  
           END-IF.                                                              
                                                                                
           MOVE FA30-NUM-PRJCT            TO FW3D-CDE-PROJECT.                  
           MOVE FA30-NUM-REGN             TO FW3D-NUM-REGN.                     
           MOVE FA30-DTE-ACCEPT           TO FW3D-DTE-ACCEPT.                   
           MOVE FA30-DTE-ACCEPT           TO FW3D-DTE-BKAPPMT-DATE.             
           MOVE FA30-TME-ACCEPT           TO FW3D-TME-ACCEPT.                   
           MOVE FA30-TME-ACCEPT           TO FW3D-TME-BKAPPMT.                  
           MOVE FA30-NUM-ETHNIC           TO FW3D-NUM-ETHNIC.                   
           MOVE FA30-NUM-ACCEPT           TO FW3D-NUM-BOOK.                     
           IF  FA30-NUM-SCH-ACC(1:1) = '#' OR SPACES                            
               MOVE SPACES                TO FW3D-NUM-SCH-ACC                   
           ELSE                                                                 
               MOVE FA30-NUM-SCH-ACC      TO FW3D-NUM-SCH-ACC                   
           END-IF.                                                              
                                                                                
                                                                                
           WRITE BP13FW3D-REC.                                                  
           ADD   1  TO WS-FW3D-WRITE.                                           
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-ROUTINE.                                                      
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13C92C  *************'.                           
           DISPLAY 'RECS READ FROM  BP13FA20    : ' WS-FA20-READ.               
           DISPLAY 'RECS READ FROM  BP13FA30    : ' WS-FA30-READ.               
           DISPLAY '                              '.                            
           DISPLAY 'NO OF RECORDS MATCHED W/ FA20   ' WS-FA30-MATCHED.          
           DISPLAY 'FOUND FA30    NOT MTCH W/ FA20  ' WS-FA30-NOMATCH.          
           DISPLAY 'RECS WRITTEN IN  BP13FW3C   : ' WS-FW3C-WRITE.              
           DISPLAY 'RECS WRITTEN IN  BP13FW3D   : ' WS-FW3D-WRITE.              
                                                                                
           CLOSE BP13FA20                                                       
                 BP13FA30                                                       
                 BP13KA10                                                       
                 BP13KB20                                                       
                 BP13FW3C                                                       
                 BP13FW3D.                                                      
                                                                                
           IF KA10-STATUS NOT = ZEROES AND 97                                   
              DISPLAY 'BP13KA10 CLOSE ERROR ' KA10-STATUS                       
              MOVE KA10-STATUS        TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           IF KB20-STATUS NOT = ZEROES AND 97                                   
              DISPLAY 'BP13KB20 CLOSE ERROR ' KB20-STATUS                       
              MOVE KB20-STATUS        TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
