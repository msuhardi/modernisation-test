      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13CB0F.                                                 
      *AUTHOR.        MARGE LAM KO.                                             
      *DATE-WRITTEN.  21 OCT 2015.                                              
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      * OBJECTIVES :                                                *           
      *   BREAK DOWN REPORT 2 INTO 2A AND 2B                        *           
      *   - BY MATCHING REPT 2 WITH PREV MONTH REPT 5               *           
      *   BREAK DOWN REPORT 5 INTO 5A AND 5B                        *           
      *   - 5A WITH CANCELLATION DATE, 5B WITHOUT CANCELLATION DATE *           
      *   CREATE REPORT 6                                           *           
      *   - BY MATCHING REPORT 5 WITH REP1 AND PREV MONTH REP4      *           
      *   CREATE REPORT 8                                           *           
      *   - VERSION OF REPORT 5 BUT INCLUDING MTH BAL = 0           *           
      *                                                             *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BP13RPT1                                              *           
      *   2.  BP13RPT2                                              *           
      *   3.  BP13RPT5                                              *           
      *   4.  PREVRPT4 - PREVIOUS MONTH'S REPORT 4                  *           
      *   5.  PREVRPT5 - PREVIOUS MONTH'S REPORT 5                  *           
      *                                                             *           
      *   OUTPUT FILES:                                             *           
      *   1.  BP13REPT                                              *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP135980 19/10/2015 MRN1 REPORTS 2A, 2B, 5A, 5B, 6, 8       *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
                                                                                
           SELECT BP13RPT1   ASSIGN       TO BP13RPT1.                          
                                                                                
           SELECT BP13RPT2   ASSIGN       TO BP13RPT2.                          
                                                                                
           SELECT BP13RPT5   ASSIGN       TO BP13RPT5.                          
                                                                                
           SELECT PREVRPT4   ASSIGN       TO PREVRPT4.                          
                                                                                
           SELECT PREVRPT5   ASSIGN       TO PREVRPT5.                          
                                                                                
           SELECT BP13REPT   ASSIGN       TO BP13REPT.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13RPT1                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01  WS-BP13RPT1-REC.                                                     
           05  WS-RPT1-GL-ACCT      PIC X(13).                                  
           05  FILLER               PIC X(01).                                  
           05  WS-RPT1-TYPE         PIC X(02).                                  
           05  FILLER               PIC X(01).                                  
           05  WS-RPT1-REGN-NO      PIC X(08).                                  
           05  WS-RPT1-NRIC         PIC X(09).                                  
           05  WS-RPT1-RECEIPT      PIC X(14).                                  
           05  WS-RPT1-SCHACCT      PIC X(11).                                  
           05  WS-RPT1-DTE-KEYISSUE PIC X(08).                                  
           05  WS-RPT1-GRANT-AMT    PIC S9(7)V99.                               
           05  WS-RPT1-RCVRY-AMT    PIC S9(7)V99.                               
           05  WS-RPT1-DTE-POST     PIC X(08).                                  
           05  WS-RPT1-MTH-BAL      PIC S9(8)V99.                               
           05  WS-RPT1-DTE-CANCEL   PIC X(08).                                  
           05  FILLER               PIC X(89).                                  
                                                                                
       FD   BP13RPT2                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01  WS-BP13RPT2-REC.                                                     
           05  WS-RPT2-GL-ACCT      PIC X(13).                                  
           05  FILLER               PIC X(01).                                  
           05  WS-RPT2-TYPE         PIC X(02).                                  
           05  FILLER               PIC X(01).                                  
           05  WS-RPT2-REGN-NO      PIC X(08).                                  
           05  WS-RPT2-NRIC         PIC X(09).                                  
           05  WS-RPT2-RECEIPT      PIC X(14).                                  
           05  WS-RPT2-SCHACCT      PIC X(11).                                  
           05  WS-RPT2-DTE-KEYISSUE PIC X(08).                                  
           05  WS-RPT2-GRANT-AMT    PIC S9(7)V99.                               
           05  WS-RPT2-RCVRY-AMT    PIC S9(7)V99.                               
           05  WS-RPT2-DTE-POST     PIC X(08).                                  
           05  WS-RPT2-MTH-BAL      PIC S9(8)V99.                               
           05  WS-RPT2-DTE-CANCEL   PIC X(08).                                  
           05  FILLER               PIC X(89).                                  
                                                                                
       FD   BP13RPT5                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01  WS-BP13RPT5-REC.                                                     
           05  WS-RPT5-GL-ACCT      PIC X(13).                                  
           05  FILLER               PIC X(01).                                  
           05  WS-RPT5-TYPE         PIC X(02).                                  
           05  FILLER               PIC X(01).                                  
           05  WS-RPT5-REGN-NO      PIC X(08).                                  
           05  WS-RPT5-NRIC         PIC X(09).                                  
           05  WS-RPT5-RECEIPT      PIC X(14).                                  
           05  WS-RPT5-SCHACCT      PIC X(11).                                  
           05  WS-RPT5-DTE-KEYISSUE PIC X(08).                                  
           05  WS-RPT5-GRANT-AMT    PIC S9(7)V99.                               
           05  WS-RPT5-RCVRY-AMT    PIC S9(7)V99.                               
           05  WS-RPT5-DTE-POST     PIC X(08).                                  
           05  WS-RPT5-MTH-BAL      PIC S9(8)V99.                               
           05  WS-RPT5-DTE-CANCEL   PIC X(08).                                  
           05  FILLER               PIC X(89).                                  
                                                                                
       FD   PREVRPT4                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01  WS-PREVRPT4-REC.                                                     
           05  WS-PRV4-GL-ACCT      PIC X(13).                                  
           05  FILLER               PIC X(01).                                  
           05  WS-PRV4-TYPE         PIC X(02).                                  
           05  FILLER               PIC X(01).                                  
           05  WS-PRV4-REGN-NO      PIC X(08).                                  
           05  WS-PRV4-NRIC         PIC X(09).                                  
           05  WS-PRV4-RECEIPT      PIC X(14).                                  
           05  WS-PRV4-SCHACCT      PIC X(11).                                  
           05  WS-PRV4-DTE-KEYISSUE PIC X(08).                                  
           05  WS-PRV4-GRANT-AMT    PIC S9(7)V99.                               
           05  WS-PRV4-RCVRY-AMT    PIC S9(7)V99.                               
           05  WS-PRV4-DTE-POST     PIC X(08).                                  
           05  WS-PRV4-MTH-BAL      PIC S9(8)V99.                               
           05  WS-PRV4-DTE-CANCEL   PIC X(08).                                  
           05  FILLER               PIC X(89).                                  
                                                                                
       FD   PREVRPT5                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01  WS-PREVRPT5-REC.                                                     
           05  WS-PRV5-GL-ACCT      PIC X(13).                                  
           05  FILLER               PIC X(01).                                  
           05  WS-PRV5-TYPE         PIC X(02).                                  
           05  FILLER               PIC X(01).                                  
           05  WS-PRV5-REGN-NO      PIC X(08).                                  
           05  WS-PRV5-NRIC         PIC X(09).                                  
           05  WS-PRV5-RECEIPT      PIC X(14).                                  
           05  WS-PRV5-SCHACCT      PIC X(11).                                  
           05  WS-PRV5-DTE-KEYISSUE PIC X(08).                                  
           05  WS-PRV5-GRANT-AMT    PIC S9(7)V99.                               
           05  WS-PRV5-RCVRY-AMT    PIC S9(7)V99.                               
           05  WS-PRV5-DTE-POST     PIC X(08).                                  
           05  WS-PRV5-MTH-BAL      PIC S9(8)V99.                               
           05  WS-PRV5-DTE-CANCEL   PIC X(08).                                  
           05  FILLER               PIC X(89).                                  
                                                                                
       FD   BP13REPT                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01  WS-BP13REPT-REC.                                                     
           05  FILLER               PIC X(14).                                  
           05  WS-REPT-TYPE         PIC X(02).                                  
           05  FILLER               PIC X(184).                                 
                                                                                
      ************************                                                  
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  CNT-REPT1-READ           PIC 9(05) VALUE ZEROES.                     
       01  CNT-REPT2-READ           PIC 9(05) VALUE ZEROES.                     
       01  CNT-REPT5-READ           PIC 9(05) VALUE ZEROES.                     
       01  CNT-PREV4-READ           PIC 9(05) VALUE ZEROES.                     
       01  CNT-PREV5-READ           PIC 9(05) VALUE ZEROES.                     
       01  WS-REPT1-EOF             PIC X(01) VALUE 'N'.                        
       01  WS-REPT2-EOF             PIC X(01) VALUE 'N'.                        
       01  WS-REPT5-EOF             PIC X(01) VALUE 'N'.                        
       01  WS-PREV4-EOF             PIC X(01) VALUE 'N'.                        
       01  WS-PREV5-EOF             PIC X(01) VALUE 'N'.                        
       01  WS-RPT2A-CNT             PIC 9(05) VALUE ZEROES.                     
       01  WS-RPT2B-CNT             PIC 9(05) VALUE ZEROES.                     
       01  WS-RPT5A-CNT             PIC 9(05) VALUE ZEROES.                     
       01  WS-RPT5B-CNT             PIC 9(05) VALUE ZEROES.                     
       01  WS-RPT6-CNT              PIC 9(05) VALUE ZEROES.                     
       01  WS-RPT8-CNT              PIC 9(05) VALUE ZEROES.                     
       01  WS-PREV-REGNO4           PIC X(08) VALUE SPACES.                     
       01  WS-PREV-REGNO5           PIC X(08) VALUE SPACES.                     
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-REPT2   THRU 2000-EXIT.                            
           PERFORM 2100-READ-PREV5   THRU 2100-EXIT.                            
           PERFORM 3000-PROCESS-REPT2 THRU 3000-EXIT                            
                   UNTIL WS-REPT2-EOF = 'Y'.                                    
           PERFORM 4000-READ-REPT1   THRU 4000-EXIT.                            
           PERFORM 4100-READ-REPT5   THRU 4100-EXIT.                            
           PERFORM 4200-READ-PREV4   THRU 4200-EXIT.                            
           PERFORM 5000-PROCESS-REPT6 THRU 5000-EXIT                            
                   UNTIL WS-REPT5-EOF = 'Y'.                                    
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13RPT1                                                 
                       BP13RPT2                                                 
                       BP13RPT5                                                 
                       PREVRPT4                                                 
                       PREVRPT5                                                 
                OUTPUT BP13REPT.                                                
                                                                                
           MOVE SPACES TO WS-BP13REPT-REC.                                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-REPT2.                                                         
      *---------------*                                                         
                                                                                
           READ BP13RPT2 AT END                                                 
              MOVE HIGH-VALUES TO WS-BP13RPT2-REC                               
              MOVE 'Y' TO WS-REPT2-EOF                                          
              GO TO 2000-EXIT.                                                  
                                                                                
           ADD 1 TO CNT-REPT2-READ.                                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2100-READ-PREV5.                                                         
      *---------------*                                                         
                                                                                
           READ PREVRPT5  AT END                                                
              MOVE HIGH-VALUES TO WS-PREVRPT5-REC                               
              MOVE 'Y' TO WS-PREV5-EOF                                          
              GO TO 2100-EXIT.                                                  
                                                                                
           ADD 1 TO CNT-PREV5-READ.                                             
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REPT2.                                                      
      *-----------------*                                                       
           EVALUATE TRUE                                                        
                                                                                
               WHEN WS-RPT2-REGN-NO = WS-PRV5-REGN-NO                           
                    MOVE SPACES               TO WS-BP13REPT-REC                
                    MOVE WS-BP13RPT2-REC      TO WS-BP13REPT-REC                
                    MOVE '2B'                 TO WS-REPT-TYPE                   
                    WRITE WS-BP13REPT-REC                                       
                    ADD 1 TO WS-RPT2B-CNT                                       
                    PERFORM 2000-READ-REPT2   THRU 2000-EXIT                    
                    MOVE WS-PRV5-REGN-NO TO WS-PREV-REGNO5                      
                    PERFORM 2100-READ-PREV5   THRU 2100-EXIT                    
                                                                                
               WHEN WS-RPT2-REGN-NO > WS-PRV5-REGN-NO                           
                    MOVE WS-PRV5-REGN-NO TO WS-PREV-REGNO5                      
                    PERFORM 2100-READ-PREV5   THRU 2100-EXIT                    
                                                                                
               WHEN WS-RPT2-REGN-NO < WS-PRV5-REGN-NO                           
                    IF WS-RPT2-REGN-NO = WS-PREV-REGNO5                         
                       MOVE SPACES               TO WS-BP13REPT-REC             
                       MOVE WS-BP13RPT2-REC      TO WS-BP13REPT-REC             
                       MOVE '2B'                 TO WS-REPT-TYPE                
                       WRITE WS-BP13REPT-REC                                    
                       ADD 1 TO WS-RPT2B-CNT                                    
                    ELSE                                                        
                       MOVE WS-BP13RPT2-REC      TO WS-BP13REPT-REC             
                       MOVE '2A'                 TO WS-REPT-TYPE                
                       WRITE WS-BP13REPT-REC                                    
                       ADD 1 TO WS-RPT2A-CNT                                    
                    END-IF                                                      
                    PERFORM 2000-READ-REPT2   THRU 2000-EXIT                    
                                                                                
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       4000-READ-REPT1.                                                         
      *---------------*                                                         
                                                                                
           READ BP13RPT1 AT END                                                 
              MOVE HIGH-VALUES TO WS-BP13RPT1-REC                               
              MOVE 'Y' TO WS-REPT1-EOF                                          
              GO TO 4000-EXIT.                                                  
                                                                                
           ADD 1 TO CNT-REPT1-READ.                                             
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       4100-READ-REPT5.                                                         
      *---------------*                                                         
                                                                                
           READ BP13RPT5  AT END                                                
              MOVE HIGH-VALUES TO WS-BP13RPT5-REC                               
              MOVE 'Y' TO WS-REPT5-EOF                                          
              GO TO 4100-EXIT.                                                  
                                                                                
           ADD 1 TO CNT-REPT5-READ.                                             
                                                                                
           IF WS-RPT5-MTH-BAL NOT = ZEROES                                      
             IF WS-RPT5-DTE-CANCEL = SPACES OR LOW-VALUES OR '00000000'         
                MOVE SPACES               TO WS-BP13REPT-REC                    
                MOVE WS-BP13RPT5-REC      TO WS-BP13REPT-REC                    
                MOVE '5B'                 TO WS-REPT-TYPE                       
                WRITE WS-BP13REPT-REC                                           
                ADD 1 TO WS-RPT5B-CNT                                           
             ELSE                                                               
                MOVE SPACES               TO WS-BP13REPT-REC                    
                MOVE WS-BP13RPT5-REC      TO WS-BP13REPT-REC                    
                MOVE '5A'                 TO WS-REPT-TYPE                       
                WRITE WS-BP13REPT-REC                                           
                ADD 1 TO WS-RPT5A-CNT                                           
             END-IF                                                             
           END-IF.                                                              
                                                                                
      * REPORT 8 (INCLUDING WS-RPT5-MTH-BAL = ZEROES)                           
           MOVE SPACES               TO WS-BP13REPT-REC.                        
           MOVE WS-BP13RPT5-REC      TO WS-BP13REPT-REC.                        
           MOVE '8 '                 TO WS-REPT-TYPE.                           
           WRITE WS-BP13REPT-REC.                                               
           ADD 1 TO WS-RPT8-CNT.                                                
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       4200-READ-PREV4.                                                         
      *---------------*                                                         
                                                                                
           READ PREVRPT4  AT END                                                
              MOVE HIGH-VALUES TO WS-PREVRPT4-REC                               
              MOVE 'Y' TO WS-PREV4-EOF                                          
              GO TO 4200-EXIT.                                                  
                                                                                
           ADD 1 TO CNT-PREV4-READ.                                             
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       5000-PROCESS-REPT6.                                                      
      *-----------------*                                                       
           IF (WS-RPT5-REGN-NO = WS-RPT1-REGN-NO) AND                           
              (WS-RPT5-REGN-NO = WS-PRV4-REGN-NO)                               
               IF WS-RPT5-MTH-BAL NOT = ZEROES                                  
                  MOVE SPACES            TO WS-BP13REPT-REC                     
                  MOVE WS-BP13RPT5-REC   TO WS-BP13REPT-REC                     
                  MOVE '6 '              TO WS-REPT-TYPE                        
                  WRITE WS-BP13REPT-REC                                         
                  ADD 1 TO WS-RPT6-CNT                                          
               END-IF                                                           
               PERFORM 4100-READ-REPT5   THRU 4100-EXIT                         
               PERFORM 4200-READ-PREV4   THRU 4200-EXIT                         
               PERFORM 4000-READ-REPT1   THRU 4000-EXIT                         
           ELSE                                                                 
              IF (WS-RPT5-REGN-NO > WS-RPT1-REGN-NO) OR                         
                 (WS-RPT5-REGN-NO > WS-PRV4-REGN-NO)                            
                 IF WS-RPT5-REGN-NO > WS-RPT1-REGN-NO                           
                    PERFORM 4000-READ-REPT1   THRU 4000-EXIT                    
                 END-IF                                                         
                 IF WS-RPT5-REGN-NO > WS-PRV4-REGN-NO                           
                    PERFORM 4200-READ-PREV4   THRU 4200-EXIT                    
                 END-IF                                                         
              ELSE                                                              
                 IF (WS-RPT5-REGN-NO < WS-RPT1-REGN-NO) OR                      
                    (WS-RPT5-REGN-NO < WS-PRV4-REGN-NO)                         
                    PERFORM 4100-READ-REPT5   THRU 4100-EXIT                    
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13CB0F *************'.                            
           DISPLAY 'NO OF RECS READ FROM  BP13RPT1  : ' CNT-REPT1-READ.         
           DISPLAY 'NO OF RECS READ FROM  BP13RPT2  : ' CNT-REPT2-READ.         
           DISPLAY 'NO OF RECS READ FROM  BP13RPT5  : ' CNT-REPT5-READ.         
           DISPLAY 'NO OF RECS READ FROM  PREVRPT5  : ' CNT-PREV5-READ.         
           DISPLAY 'NO OF RECS WRITTEN FOR REPORT 2A: ' WS-RPT2A-CNT.           
           DISPLAY 'NO OF RECS WRITTEN FOR REPORT 2B: ' WS-RPT2B-CNT.           
           DISPLAY 'NO OF RECS WRITTEN FOR REPORT 5A: ' WS-RPT5A-CNT.           
           DISPLAY 'NO OF RECS WRITTEN FOR REPORT 5B: ' WS-RPT5B-CNT.           
           DISPLAY 'NO OF RECS WRITTEN FOR REPORT 6 : ' WS-RPT6-CNT.            
           DISPLAY 'NO OF RECS WRITTEN FOR REPORT 8 : ' WS-RPT8-CNT.            
                                                                                
           CLOSE BP13RPT1                                                       
                 BP13RPT2                                                       
                 BP13RPT5                                                       
                 PREVRPT4                                                       
                 PREVRPT5                                                       
                 BP13REPT.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
