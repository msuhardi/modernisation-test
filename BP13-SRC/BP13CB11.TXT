       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB11.                                                 
       AUTHOR.        BALA AMUTHAN.                                             
      *DATE-WRITTEN.  25/09/2007.                                               
      *========================================================*                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      *========================================================*                
      *  UPDATE SOCA FILELESS MODULE (BP13K20A)                *                
      *                                                        *                
      *    INPUT   :  BP13F310                                 *                
      *            :  BM06K110                                 *                
      *            :  BP13K800                                 *                
      *    I-O     :  BP13K20A.                                *                
      * ------------------------------------------------------ *                
      * CHG-NO    CHG-DATE    BY    DESCRIPTION                *                
      * ------    --------    --    -----------                *                
      * BP132988  25/09/2007  BA2   NEW PROGRAM                *                
      **********************************************************                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F310 ASSIGN TO BP13F310.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS BP13K800-STATUS.                           
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K110-KEY-FLD                               
                  ALTERNATE KEY   IS K110-REGN-NO                               
                  FILE STATUS     IS BM06K110-STATUS.                           
                                                                                
           SELECT BP13K20A ASSIGN TO BP13K20A                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K20A-KEY-FLD                               
                  FILE STATUS     IS BP13K20A-STATUS.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F310                                                             
           RECORD    CONTAINS 150 CHARACTERS                                    
           BLOCK     CONTAINS   0 RECORDS                                       
           LABEL     RECORDS  ARE STANDARD                                      
           RECORDING MODE     IS  F.                                            
       COPY BP13F310.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD    CONTAINS 2000 CHARACTERS.                                  
       COPY BP13K800.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD    CONTAINS 500  CHARACTERS.                                  
       COPY BM06K110.                                                           
                                                                                
       FD  BP13K20A                                                             
           RECORD    CONTAINS 200  CHARACTERS.                                  
       COPY BP13K20A.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  FILE-STATUS.                                                         
           05  BP13K800-STATUS         PIC 9(02)  VALUE 0.                      
           05  BM06K110-STATUS         PIC 9(02)  VALUE 0.                      
           05  BP13K20A-STATUS         PIC 9(02)  VALUE 0.                      
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F310-EOF             PIC X       VALUE SPACES.                
               88 F310-EOF             VALUE 'Y'.                               
                                                                                
           05  WS-K800-FND             PIC X       VALUE SPACES.                
               88 K800-FND             VALUE 'Y'.                               
                                                                                
           05  WS-K110-FND             PIC X       VALUE SPACES.                
               88 K110-FND             VALUE 'Y'.                               
                                                                                
       01  WS-ACCUMULATORS.                                                     
           05  WS-NO-READ-F310         PIC 9(6)    VALUE ZERO.                  
           05  WS-NO-READ-K800         PIC 9(6)    VALUE ZERO.                  
           05  WS-NO-READ-K110         PIC 9(6)    VALUE ZERO.                  
           05  WS-NO-NOT-FND-K800      PIC 9(6)    VALUE ZERO.                  
           05  WS-NO-NOT-FND-K110      PIC 9(6)    VALUE ZERO.                  
           05  WS-NO-SPACE-SCH-ACC     PIC 9(6)    VALUE ZERO.                  
           05  WS-K20A-WRITE           PIC 9(6)    VALUE ZERO.                  
           05  WS-K20A-REWRITE         PIC 9(6)    VALUE ZERO.                  
           05  WS-K20A-DEL             PIC 9(6)    VALUE ZERO.                  
           05  WS-K20A-DEL-N           PIC 9(6)    VALUE ZERO.                  
                                                                                
       01  WS-CUR-DATE                 PIC X(8).                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-PROCESS.                                                       
      *-------------------------------------------------------------            
             PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                   
             PERFORM 2000-READ-BP13F310       THRU 2000-EXIT.                   
             PERFORM 3000-WRITE-PROCESS       THRU 3000-EXIT                    
               UNTIL F310-EOF.                                                  
             PERFORM 9999-CLOSE-ROUTINE       THRU 9999-EXIT.                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT BP13F310                                                  
                      BP13K800                                                  
                      BM06K110                                                  
                I-O   BP13K20A.                                                 
                                                                                
           IF BP13K800-STATUS NOT = ZEROS AND 97                                
              DISPLAY ' ERROR OPENING BP13K800  FILE, STATUS '                  
                  BP13K800-STATUS                                               
              MOVE BP13K800-STATUS TO RETURN-CODE                               
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                        
                                                                                
           IF BM06K110-STATUS NOT = ZEROS AND 97                                
              DISPLAY ' ERROR OPENING BM06K110  FILE, STATUS '                  
                  BM06K110-STATUS                                               
              MOVE BM06K110-STATUS TO RETURN-CODE                               
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                        
                                                                                
           IF BP13K20A-STATUS NOT = ZEROS AND 97                                
              DISPLAY ' ERROR OPENING BP13K20A  FILE, STATUS '                  
                  BP13K20A-STATUS                                               
              MOVE BP13K20A-STATUS TO RETURN-CODE                               
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                        
                                                                                
           MOVE FUNCTION CURRENT-DATE    TO WS-CUR-DATE.                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       2000-READ-BP13F310.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F310                                                        
                AT END  MOVE 'Y' TO WS-F310-EOF                                 
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-NO-READ-F310.                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************************************************                 
      *                                                       *                 
      *    READ BP13F310 UNTIL END OF FILE.                   *                 
      *    1. READ BM06K110 FILE USING F310-REGN-NO.          *                 
      *    2. READ BP13K800 FILE USING F310-REGN-NO, IF REC   *                 
      *       FOUND AT BM06K110.                              *                 
      *    3. WRITE INTO BP13K20A FILE FROM BP13K800, IF      *                 
      *       K800-NUM-SCH-ACC NOT = SPACES.                  *                 
      *                                                       *                 
      *********************************************************                 
       3000-WRITE-PROCESS.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES           TO K110-REC                                    
                                    BP13K800-MASTER                             
                                    BP13K20A-REC.                               
           INITIALIZE               K110-REC                                    
                                    BP13K800-MASTER                             
                                    BP13K20A-REC.                               
           MOVE F310-REGN-NO     TO K110-REGN-NO                                
                                    K800-NUM-REGN.                              
           IF F310-CDE-TRANS = 'DE'                                             
              PERFORM 3050-DELETE-K20A     THRU 3050-EXIT                       
           ELSE                                                                 
              PERFORM 3100-READ-BM06K110      THRU  3100-EXIT                   
                                                                                
              IF K110-FND AND (K110-DTE-ACCEPTANCE NOT = SPACES AND             
                               LOW-VALUES AND ZEROES AND ALL 'X')               
                 PERFORM 3200-READ-BP13K800   THRU  3200-EXIT                   
                 IF K800-FND                                                    
                   IF (K800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES AND         
                                  ZEROES AND ALL 'X' )                          
                      PERFORM 3300-READ-UPD-BP13K20A THRU 3300-EXIT             
                   ELSE                                                         
                      ADD +1       TO  WS-NO-SPACE-SCH-ACC                      
                   END-IF                                                       
                 END-IF                                                         
              END-IF                                                            
           END-IF                                                               
                                                                                
           PERFORM 2000-READ-BP13F310      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3050-DELETE-K20A.                                                        
      *-------------------------------------------------------------            
                                                                                
           MOVE F310-REGN-NO          TO K20A-NUM-REGN.                         
                                                                                
           READ BP13K20A.                                                       
                                                                                
           EVALUATE BP13K20A-STATUS                                             
              WHEN 00                                                           
              WHEN 02                                                           
                   DELETE BP13K20A                                              
                   IF BP13K20A-STATUS = 00                                      
                      ADD 1                   TO WS-K20A-DEL                    
                   ELSE                                                         
                      DISPLAY 'INVALID DELETE BP13K20A KEY: '                   
                                              K20A-NUM-REGN                     
                      ADD 1                 TO WS-K20A-DEL-N                    
                   END-IF                                                       
              WHEN OTHER                                                        
                   DISPLAY 'RECORD NOT FOUND, BP13K20A, KEY: '                  
                                               K20A-NUM-REGN                    
                   ADD 1                    TO WS-K20A-DEL-N                    
           END-EVALUATE.                                                        
                                                                                
       3050-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3100-READ-BM06K110.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BM06K110 KEY IS K110-REGN-NO.                                   
                                                                                
           EVALUATE BM06K110-STATUS                                             
              WHEN 00                                                           
              WHEN 02                                                           
                 MOVE 'Y'     TO WS-K110-FND                                    
                 ADD +1       TO WS-NO-READ-K110                                
              WHEN 23                                                           
                 ADD +1       TO WS-NO-NOT-FND-K110                             
                 MOVE 'N'     TO WS-K110-FND                                    
                 DISPLAY 'RECORD NOT FOUND IN BM06K110 : '                      
                                                  K110-REGN-NO                  
              WHEN OTHER                                                        
                 DISPLAY ' ERROR READING BM06K110 REC '                         
                         ' STATUS ' BM06K110-STATUS                             
                 DISPLAY ' KEY IS ' K110-REGN-NO                                
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3200-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE BP13K800-STATUS                                             
              WHEN 00                                                           
              WHEN 02                                                           
                 MOVE 'Y'     TO WS-K800-FND                                    
                 ADD +1       TO WS-NO-READ-K800                                
              WHEN 23                                                           
                 ADD +1       TO WS-NO-NOT-FND-K800                             
                 MOVE 'N'     TO WS-K800-FND                                    
                 DISPLAY 'RECORD NOT FOUND IN BP13K800 : '                      
                                                  K800-NUM-REGN                 
              WHEN OTHER                                                        
                 DISPLAY ' ERROR READING BP13K800 MASTER REC '                  
                         ' STATUS ' BP13K800-STATUS                             
                 DISPLAY ' KEY IS ' K800-NUM-REGN                               
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3300-READ-UPD-BP13K20A.                                                  
      *-------------------------------------------------------------            
           MOVE K800-NUM-ALLO-CAT     TO K20A-NUM-ALLO-MODE.                    
           MOVE K800-DTE-BALLOT       TO K20A-DTE-BALLOT.                       
           MOVE K110-DTE-ACCEPTANCE   TO K20A-DTE-ACCEPTANCE.                   
           MOVE F310-REGN-NO          TO K20A-NUM-REGN.                         
           MOVE 'NEW'                 TO K20A-NUM-PREE-STATUS.                  
                                                                                
           READ BP13K20A.                                                       
                                                                                
           EVALUATE BP13K20A-STATUS                                             
              WHEN 00                                                           
              WHEN 02                                                           
                  PERFORM 3600-REWRITE-BP13K20A  THRU 3600-EXIT                 
              WHEN 23                                                           
                  PERFORM 3500-WRITE-BP13K20A    THRU 3500-EXIT                 
              WHEN OTHER                                                        
                 DISPLAY ' ERROR READING BP13K20A REC '                         
                         ' STATUS ' BP13K20A-STATUS                             
                 DISPLAY ' KEY IS ' K20A-NUM-REGN                               
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3400-MOVE-BP13K20A.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE K800-NUM-NEW-TOWN     TO K20A-NUM-NEW-TOWN.                     
           MOVE K800-NUM-FLAT-TYPE    TO K20A-NUM-FLAT-TYPE.                    
           MOVE K800-NUM-ELIG-SCHEME  TO K20A-NUM-ELIG-SCH.                     
           MOVE K800-NUM-IDP-SCHEME   TO K20A-NUM-IDP-SCH.                      
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3500-WRITE-BP13K20A.                                                     
      *-------------------------------------------------------------            
                                                                                
           PERFORM 3400-MOVE-BP13K20A     THRU 3400-EXIT.                       
                                                                                
           WRITE BP13K20A-REC.                                                  
                                                                                
           EVALUATE BP13K800-STATUS                                             
              WHEN 00                                                           
              WHEN 02                                                           
                 ADD 1 TO  WS-K20A-WRITE                                        
              WHEN OTHER                                                        
                 DISPLAY ' ERROR WRITING INTO BP13K20A REC '                    
                         ' STATUS ' BP13K20A-STATUS                             
                 DISPLAY ' KEY IS ' K20A-NUM-REGN                               
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3600-REWRITE-BP13K20A.                                                   
      *-------------------------------------------------------------            
                                                                                
           PERFORM 3400-MOVE-BP13K20A     THRU 3400-EXIT.                       
                                                                                
           WRITE BP13K20A-REC.                                                  
                                                                                
           EVALUATE BP13K800-STATUS                                             
              WHEN 00                                                           
              WHEN 02                                                           
                 ADD 1 TO  WS-K20A-REWRITE                                      
              WHEN OTHER                                                        
                 DISPLAY ' ERROR REWRITING INTO BP13K20A REC '                  
                         ' STATUS ' BP13K20A-STATUS                             
                 DISPLAY ' KEY IS ' K20A-NUM-REGN                               
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9999-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           CLOSE BP13F310                                                       
                 BP13K800                                                       
                 BM06K110                                                       
                 BP13K20A.                                                      
                                                                                
           IF BP13K800-STATUS NOT = 00 AND 97                                   
              DISPLAY ' ERROR CLOSING BP13K800  FILE, STATUS '                  
              BP13K800-STATUS.                                                  
                                                                                
           IF BM06K110-STATUS NOT = 00 AND 97                                   
              DISPLAY ' ERROR CLOSING BM06K110  FILE, STATUS '                  
              BM06K110-STATUS.                                                  
                                                                                
           IF BP13K20A-STATUS NOT = 00 AND 97                                   
              DISPLAY ' ERROR CLOSING BP13K20A  FILE, STATUS '                  
              BP13K800-STATUS.                                                  
                                                                                
           DISPLAY '    BP13CB11 - UPDATE SOCA FILELESS MODULE       '.         
           DISPLAY ' ***********************************************'.          
           DISPLAY ' NO. OF REC READ FROM BP13F310            = '               
                                                       WS-NO-READ-F310.         
           DISPLAY ' NO. OF REC READ FROM BM06K110            = '               
                                                       WS-NO-READ-K110.         
           DISPLAY ' NO. OF REC NOT FOUND BM06K110            = '               
                                                    WS-NO-NOT-FND-K110.         
           DISPLAY ' NO. OF REC READ FROM BP13K800            = '               
                                                       WS-NO-READ-K800.         
           DISPLAY ' NO. OF REC NOT FOUND BP13K800            = '               
                                                    WS-NO-NOT-FND-K800.         
           DISPLAY ' NO. OF REC SCH ACC SPACES IN BP13K800    = '               
                                                   WS-NO-SPACE-SCH-ACC.         
           DISPLAY ' NO. OF REC WRITE INTO BP13K20A           = '               
                                                       WS-K20A-WRITE.           
           DISPLAY ' NO. OF REC UPDATE INTO BP13K20A          = '               
                                                     WS-K20A-REWRITE.           
           DISPLAY ' NO. OF REC DELETED FROM BP13K20A         = '               
                                                     WS-K20A-DEL.               
           DISPLAY ' NO. OF REC NOT FOUND IN BP13K20A         = '               
                                                     WS-K20A-DEL-N.             
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
