                                                                                
       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C591.                                                    
      *AUTHOR. JAI.                                                             
      *DATE-WRITTEN. 06/DEC/94.                                                 
                                                                                
      **********************************************************                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *========================================================*                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *                                                        *                
      *      1.  UPDATE THE LAST 10 UNITS OF PUBLIC APPLICANT  *                
      *          TO BE UNSUCCESSFUL, BYPASS THE  PRIORITY CASES*                
      *                                                        *                
      *    INPUT FILE : BP13F520                               *                
      *    I-O        : BP13K520                               *                
      *                                                        *                
      *CHG-NO  BY  ON   DESCRIPTION                            *                
      *----------------------------                            *                
      * BP130652 NOR 20021999 EXPAND F520 & K520 RECL 300 - 500*                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F520 ASSIGN       TO BP13F520.                            
           SELECT BP13K520 ASSIGN       TO BP13K520                             
                           ACCESS       IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K520-KEY-NEW                         
                           FILE STATUS  IS WS-K520-STATUS.                      
           SELECT BP13L591 ASSIGN       TO BP13L591.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F520                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F520.                                                           
                                                                                
       FD   BP13K520.                                                           
       COPY BP13K520.                                                           
                                                                                
                                                                                
       FD   BP13L591                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       01   BP13L591-REC           PIC X(132).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-K520-STATUS          PIC 9(02) VALUE ZEROES.                      
       01  WS-F520-UNSUCC          PIC 9(08) VALUE 0.                           
       01  WS-TOT-FLAT             PIC 9(02) VALUE 10.                          
       01  WS-SER-NO               PIC 9(08) VALUE 0.                           
       01  WS-F520-READ            PIC 9(08) VALUE 0.                           
       01  WS-L591-WRITE           PIC 9(08) VALUE 0.                           
       01  WS-K520-REWRITE         PIC 9(08) VALUE 0.                           
       01  WS-PREV-ZONE            PIC X(02) VALUE SPACES.                      
       01  WS-PREV-FLAT-TYPE       PIC X(02) VALUE SPACES.                      
       01  WS-PAGE-NO              PIC 9(05) VALUE ZEROES.                      
       01  WS-LINE-COUNT-L591      PIC 9(02) VALUE 65.                          
       01  WS-MAX-LINES            PIC 9(02) VALUE 55.                          
       01  WS-ALLO-SCHEME          PIC X(03) VALUE SPACES.                      
                                                                                
       01  WS-CUR-DATE.                                                         
           05 WS-YY                PIC 9(04).                                   
           05 WS-MM                PIC 9(02).                                   
           05 WS-DD                PIC 9(02).                                   
                                                                                
       01  WS-EDIT-DATE.                                                        
           05 WS-EDIT-DD           PIC 9(02).                                   
           05 FILLER               PIC X(01) VALUE '/'.                         
           05 WS-EDIT-MM           PIC 9(02).                                   
           05 FILLER               PIC X(01) VALUE '/'.                         
           05 WS-EDIT-YY           PIC 9(04).                                   
                                                                                
           COPY PRIOSCH.                                                        
                                                                                
       01  L591-HDG1.                                                           
           05 FILLER               PIC X(14) VALUE 'BP13L591'.                  
           05 FILLER               PIC X(08) VALUE 'HDB3'.                      
           05 FILLER               PIC X(18) VALUE SPACES.                      
           05 FILLER               PIC X(39) VALUE                              
              'S Y S T E M   O F   C O M M I T M E N T'.                        
           05 FILLER               PIC X(16) VALUE SPACES.                      
           05 FILLER               PIC X(07) VALUE 'DATE: '.                    
           05 HDG1-DATE            PIC X(10) VALUE SPACES.                      
           05 FILLER               PIC X(03) VALUE SPACES.                      
           05 FILLER               PIC X(07) VALUE 'PAGE: '.                    
           05 HDG1-PAGE-NO         PIC ZZZZ9.                                   
                                                                                
       01  L591-HDG2.                                                           
           05 FILLER               PIC X(30) VALUE SPACES.                      
           05 FILLER               PIC X(57) VALUE                              
              'UPDATE THE LAST 10 UNITS OF PUBLIC APPLICANTS TO BE UNS'.        
           05 FILLER               PIC X(44) VALUE                              
              'CCESSFUL'.                                                       
                                                                                
       01  L591-HDG3.                                                           
           05 FILLER               PIC X(05) VALUE SPACES.                      
           05 FILLER               PIC X(08) VALUE ' SER NO '.                  
           05 FILLER               PIC X(05) VALUE SPACES.                      
           05 FILLER               PIC X(08) VALUE ' SEQ  # '.                  
           05 FILLER               PIC X(05) VALUE SPACES.                      
           05 FILLER               PIC X(08) VALUE ' REGN # '.                  
           05 FILLER               PIC X(05) VALUE SPACES.                      
           05 FILLER               PIC X(04) VALUE 'ZONE'.                      
           05 FILLER               PIC X(05) VALUE SPACES.                      
           05 FILLER               PIC X(09) VALUE 'FLAT TYPE'.                 
           05 FILLER               PIC X(05) VALUE SPACES.                      
           05 FILLER               PIC X(07) VALUE 'ALC SCH'.                   
           05 FILLER               PIC X(05) VALUE SPACES.                      
           05 FILLER               PIC X(03) VALUE 'CAT'.                       
           05 FILLER               PIC X(27) VALUE SPACES.                      
                                                                                
       01  L591-HDG4.                                                           
           05 FILLER               PIC X(05) VALUE SPACES.                      
           05 FILLER               PIC X(08) VALUE '========'.                  
           05 FILLER               PIC X(05) VALUE SPACES.                      
           05 FILLER               PIC X(08) VALUE '========'.                  
           05 FILLER               PIC X(05) VALUE SPACES.                      
           05 FILLER               PIC X(08) VALUE '========'.                  
           05 FILLER               PIC X(05) VALUE SPACES.                      
           05 FILLER               PIC X(04) VALUE '===='.                      
           05 FILLER               PIC X(05) VALUE SPACES.                      
           05 FILLER               PIC X(09) VALUE '========='.                 
           05 FILLER               PIC X(05) VALUE SPACES.                      
           05 FILLER               PIC X(07) VALUE '======='.                   
           05 FILLER               PIC X(05) VALUE SPACES.                      
           05 FILLER               PIC X(03) VALUE '==='.                       
           05 FILLER               PIC X(27) VALUE SPACES.                      
                                                                                
       01  L591-DETAIL-REC.                                                     
           05 FILLER               PIC X(05) VALUE SPACES.                      
           05 L591-SER-NO          PIC ZZZZZZZ9.                                
           05 FILLER               PIC X(05) VALUE SPACES.                      
           05 L591-NUM-SEQ         PIC X(08) VALUE SPACES.                      
           05 FILLER               PIC X(05) VALUE SPACES.                      
           05 L591-NUM-REGN        PIC X(08) VALUE SPACES.                      
           05 FILLER               PIC X(06) VALUE SPACES.                      
           05 L591-ZONE            PIC X(03) VALUE SPACES.                      
           05 FILLER               PIC X(09) VALUE SPACES.                      
           05 L591-FT-TYPE         PIC X(02) VALUE SPACES.                      
           05 FILLER               PIC X(11) VALUE SPACES.                      
           05 L591-ALC-SCH         PIC X(03) VALUE SPACES.                      
           05 FILLER               PIC X(08) VALUE SPACES.                      
           05 L591-CAT             PIC X(01) VALUE SPACES.                      
           05 FILLER               PIC X(28) VALUE SPACES.                      
                                                                                
       01  L591-SUB-TOTAL.                                                      
           05 FILLER               PIC X(17) VALUE                              
              ' **  SUB-TOTAL = '.                                              
           05 L591-SUB-TOT         PIC ZZZZZ9.                                  
           05 FILLER               PIC X(05) VALUE SPACES.                      
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      **********                                                                
       000-MAIN.                                                                
      **********                                                                
                                                                                
           PERFORM 100-OPEN-FILES    THRU 100-EXIT.                             
                                                                                
           PERFORM 200-READ-F520-REC THRU 200-EXIT.                             
                                                                                
           MOVE F520-CDE-FLAT-TYPE  TO WS-PREV-FLAT-TYPE.                       
           MOVE F520-CDE-ZONE       TO WS-PREV-ZONE.                            
                                                                                
           PERFORM 300-PROCESS-REC   THRU 300-EXIT                              
                   UNTIL F520-NUM-REGN = HIGH-VALUE.                            
                                                                                
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ****************                                                          
       100-OPEN-FILES.                                                          
      ****************                                                          
                                                                                
           OPEN INPUT  BP13F520                                                 
                I-O    BP13K520.                                                
           OPEN OUTPUT BP13L591.                                                
                                                                                
           ACCEPT WS-CUR-DATE  FROM DATE YYYYMMDD.                              
           MOVE   WS-YY        TO   WS-EDIT-YY.                                 
           MOVE   WS-MM        TO   WS-EDIT-MM.                                 
           MOVE   WS-DD        TO   WS-EDIT-DD.                                 
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *******************                                                       
       200-READ-F520-REC.                                                       
      *******************                                                       
                                                                                
           READ BP13F520 AT END                                                 
                MOVE HIGH-VALUE TO F520-NUM-REGN                                
                GO TO 200-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F520-READ.                                               
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *==============================================*                          
      *       DOCUMENTATION                          *                          
      *==============================================*                          
                                                                                
      *****************                                                         
       300-PROCESS-REC.                                                         
      *****************                                                         
                                                                                
           PERFORM 320-DETERMINE-SCHEME  THRU 320-EXIT.                         
                                                                                
           IF (F520-CDE-FLAT-TYPE EQUAL  WS-PREV-FLAT-TYPE) AND                 
              (F520-CDE-ZONE      EQUAL  WS-PREV-ZONE     )                     
               IF WS-ALLO-SCHEME = 'PA '                                        
                  IF (WS-F520-UNSUCC < WS-TOT-FLAT)                             
                     PERFORM 360-UPDATE-K520  THRU 360-EXIT                     
                  END-IF                                                        
               END-IF                                                           
           ELSE                                                                 
               MOVE WS-F520-UNSUCC TO L591-SUB-TOT                              
               WRITE BP13L591-REC  FROM L591-SUB-TOTAL AFTER 2                  
               MOVE    ZEROES           TO WS-F520-UNSUCC                       
               MOVE F520-CDE-FLAT-TYPE  TO WS-PREV-FLAT-TYPE                    
               MOVE F520-CDE-ZONE       TO WS-PREV-ZONE                         
               IF WS-ALLO-SCHEME = 'PA '                                        
                  IF (WS-F520-UNSUCC < WS-TOT-FLAT)                             
                     PERFORM 360-UPDATE-K520  THRU 360-EXIT                     
                  END-IF                                                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-F520-REC THRU 200-EXIT.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      **********************                                                    
       320-DETERMINE-SCHEME.                                                    
      **********************                                                    
                                                                                
           SET WS-ALLOC-PTR TO 1.                                               
                                                                                
           SEARCH WS-ALLOC-SCH                                                  
              AT END MOVE 'XXX' TO WS-ALLO-SCHEME                               
              WHEN F520-CDE-ALLOC-SCH =                                         
                   WS-ALLOC-SCH-DESP (WS-ALLOC-PTR)                             
              MOVE WS-MAIN-SCH-CD    (WS-ALLOC-PTR) TO                          
                   WS-ALLO-SCHEME                                               
           END-SEARCH.                                                          
                                                                                
       320-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *****************                                                         
       360-UPDATE-K520.                                                         
      *****************                                                         
                                                                                
           INITIALIZE BP13K520-REC.                                             
           MOVE F520-KEY-NEW        TO K520-KEY-NEW.                            
                                                                                
           READ BP13K520.                                                       
                                                                                
           EVALUATE WS-K520-STATUS                                              
           WHEN 00                                                              
                PERFORM 370-REWRITE-BP13K520 THRU 370-EXIT                      
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FOUND IN K520 ' K520-KEY-FLD                
           WHEN OTHER                                                           
                DISPLAY 'ERROR IN READING K520 ' WS-K520-STATUS                 
                DISPLAY 'ABORTING PROGRAM '                                     
                PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                         
           END-EVALUATE.                                                        
                                                                                
           PERFORM  400-WRITE-L591  THRU 400-EXIT.                              
                                                                                
       360-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *********************                                                     
       370-REWRITE-BP13K520.                                                    
      *********************                                                     
                                                                                
           MOVE SPACES        TO K520-CDE-SUCC-NT-ZONE.                         
           MOVE 'F'           TO K520-CDE-UNSUCC.                               
                                                                                
           REWRITE BP13K520-REC.                                                
                                                                                
           EVALUATE WS-K520-STATUS                                              
           WHEN 00                                                              
              ADD  1    TO WS-F520-UNSUCC                                       
              ADD  1    TO WS-K520-REWRITE                                      
           WHEN OTHER                                                           
               DISPLAY 'ERROR IN REWRITING BP13K520'                            
               DISPLAY 'ABORTING PROGRAM '                                      
               PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                          
           END-EVALUATE.                                                        
                                                                                
       370-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ***************                                                           
       400-WRITE-L591.                                                          
      ***************                                                           
                                                                                
           IF WS-LINE-COUNT-L591 > WS-MAX-LINES                                 
              PERFORM 450-WRITE-HEADING  THRU 450-EXIT                          
           END-IF.                                                              
                                                                                
           MOVE F520-NUM-SEQUENCE   TO L591-NUM-SEQ                             
           MOVE F520-NUM-REGN       TO L591-NUM-REGN                            
           MOVE F520-CDE-ZONE       TO L591-ZONE.                               
           MOVE F520-CDE-FLAT-TYPE  TO L591-FT-TYPE.                            
           MOVE WS-ALLO-SCHEME      TO L591-ALC-SCH.                            
           MOVE F520-CDE-CATEGORY   TO L591-CAT.                                
           ADD  1                   TO WS-SER-NO.                               
           MOVE  WS-SER-NO          TO L591-SER-NO.                             
                                                                                
           WRITE BP13L591-REC  FROM L591-DETAIL-REC  AFTER 1.                   
           ADD   2                  TO WS-LINE-COUNT-L591.                      
           ADD   1                  TO WS-L591-WRITE.                           
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************                                                        
       450-WRITE-HEADING.                                                       
      ******************                                                        
                                                                                
           ADD     1              TO WS-PAGE-NO.                                
           MOVE    WS-EDIT-DATE   TO HDG1-DATE.                                 
           MOVE    WS-PAGE-NO     TO HDG1-PAGE-NO.                              
                                                                                
           WRITE   BP13L591-REC FROM L591-HDG1 AFTER PAGE.                      
           WRITE   BP13L591-REC FROM L591-HDG2 AFTER 2.                         
           WRITE   BP13L591-REC FROM L591-HDG3 AFTER 4.                         
           WRITE   BP13L591-REC FROM L591-HDG4 AFTER 1.                         
                                                                                
           MOVE SPACES  TO BP13L591-REC.                                        
           WRITE BP13L591-REC.                                                  
                                                                                
           MOVE  11  TO WS-LINE-COUNT-L591.                                     
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************                                                        
       900-CLOSE-ROUTINE.                                                       
      ******************                                                        
                                                                                
           ACCEPT WS-CUR-DATE FROM DATE.                                        
                                                                                
           DISPLAY '                                      '.                    
           DISPLAY 'PROGRAM-ID : BP13C591 '.                                    
           DISPLAY 'UPDATE LAST 10 PA APPLICANTS TO BE UNSUCCESSFUL'.           
           DISPLAY 'CURR-DATE  : ' WS-CUR-DATE.                                 
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS READ FROM F520   ' WS-F520-READ.              
           DISPLAY 'NO OF RECORDS RE-WRITE   K520  ' WS-K520-REWRITE.           
           DISPLAY 'NO OF RECORDS WRITTEN TO L591  ' WS-L591-WRITE.             
                                                                                
           CLOSE BP13F520                                                       
                 BP13L591.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
