      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C24N.                                                 
       AUTHOR.        ELAINE S ARGA.                                            
       DATE-WRITTEN.  28 NOVEMBER 2017.                                         
                                                                                
      * ====================================================== *                
      *    ONE-TIME PROGRAM.                                   *                
      *--------------------------------------------------------*                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : TO FILTER THE APPOINTMENT DATE <= 30  *                
      *                  DAYS FROM THE CURRENT DATE            *                
      *--------------------------------------------------------*                
      * CHG-NO   BY   DATE     DETAILS                         *                
      * -------  ---  ------   -----------------------------   *                
      * BP137145 ESA1 130118   NEW PROGRAM                     *                
      * BP137208 EL27 150318   BYPASS CHECKING FOR SERS        *                
      * BP137021 ESA1 220518   TO CHANGE DTE COMPUTATION TO +31*                
      * BP137021 CC37 080320   TO CHANGE DTE COMPUTATION TO +15*                
      * ====================================================== *                
                                                                                
      *---------------------*                                                   
       ENVIRONMENT DIVISION.                                                    
      *---------------------*                                                   
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
      *---------------------*                                                   
       INPUT-OUTPUT SECTION.                                                    
      *---------------------*                                                   
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F203 ASSIGN TO BP13F203.                                  
                                                                                
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS K800-STATUS.                               
                                                                                
           SELECT BP13K76D ASSIGN TO BP13K76D                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K76D-KEY-FLD                               
                  FILE STATUS     IS K76D-STATUS.                               
                                                                                
           SELECT P13F203A ASSIGN TO P13F203A.                                  
                                                                                
      *-------------*                                                           
       DATA DIVISION.                                                           
      *-------------*                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F203                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       01  BP13F203-REC.                                                        
           05  F203-KEY-FLD.                                                    
               10  F203-NUM-ALLO-MODE         PIC X(3).                 00161400
               10  F203-NUM-NEW-TOWN          PIC X(3).                 00161000
               10  F203-NUM-FLAT-TYPE         PIC X(2).                 00161105
               10  F203-TYPE-APPMT            PIC X(2).                 00161000
               10  F203-NUM-ETHNIC            PIC X(1).                 00161000
               10  F203-NUM-REGN              PIC X(8).                 00161000
           05  F203-ALT-KEY.                                                    
               10  F203-DTE-BALLOT            PIC X(6).                 00161400
               10  F203-DTE-APPMT             PIC X(8).                 00161200
               10  F203-NUM-QUEUE             PIC X(5).                 00161400
           05  F203-TME-APPMT                 PIC X(4).                 00161200
           05  F203-NUM-NRIC                  PIC X(9).                 00161400
           05  F203-NME-NRIC                  PIC X(66).                00161400
           05  F203-NUM-HP                    PIC X(9).                 00161400
           05  F203-NUM-APLCNT                PIC X(3).                 00161400
           05  F203-NUM-ZONE                  PIC X(2).                 00161000
           05  F203-NUM-REF                   PIC X(8).                 00161000
           05  F203-NUM-BLOCK                 PIC X(1).                 00161000
           05  F203-NUM-STATUS                PIC X(1).                 00161400
           05  F203-DTE-BOOK-PD-END           PIC X(8).                         
           05  F203-NUM-PGM-ID                PIC X(4).                 00161400
           05  F203-DTE-BKAPPMT-DATE          PIC X(8).                 00270000
           05  F203-TME-BKAPPMT               PIC X(4).                 00271000
           05  F203-NUM-MGPS-TAG              PIC X(1).                 00480006
           05  F203-DTE-BKAPPMT-DATE-MGPS     PIC X(8).                 00270000
           05  F203-TME-BKAPPMT-MGPS          PIC X(4).                 00271000
           05  FILLER                         PIC X(22).                00161400
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K76D                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K76D.                                                           
                                                                                
       FD  P13F203A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       01 P13F203A-REC          PIC X(200).                                     
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
       01  WS-FILE-STATUS.                                                      
           05 K800-STATUS                 PIC 99    VALUE ZEROS.                
           05 K76D-STATUS                 PIC 99    VALUE ZEROS.                
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F203-READ                PIC 9(5)  VALUE 0.                    
           05 WS-F203-WRITE               PIC 9(5)  VALUE 0.                    
           05 WS-K76D-NFND-CNT            PIC 9(5)  VALUE 0.                    
           05 WS-K76D-FND-CNT             PIC 9(5)  VALUE 0.                    
           05 WS-K800-FND                 PIC 9(5)  VALUE 0.                    
           05 WS-K800-NFND                PIC 9(5)  VALUE 0.                    
           05 WS-K800-CANCEL              PIC 9(5)  VALUE 0.                    
           05 WS-K800-NCANCEL             PIC 9(5)  VALUE 0.                    
           05 WS-K800-ACCEPTED            PIC 9(5)  VALUE 0.                    
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-K76D-FND                 PIC X     VALUE 'N'.                  
                                                                                
       01  WS-EOF-F203                    PIC 9     VALUE 0.                    
           88 EOF-F203                              VALUE 1.                    
                                                                                
       01  WS-CONTROL-DATE.                                                     
           05 WS-CTRL-CC                  PIC XX.                               
           05 WS-CTRL-YY                  PIC XX.                               
           05 WS-CTRL-MM                  PIC XX.                               
           05 WS-CTRL-DD                  PIC XX.                               
                                                                                
       01  WS-DATE.                                                             
           05 WS-CCYY.                                                          
              10 WS-CC                    PIC 99.                               
              10 WS-YY                    PIC 99.                               
           05 WS-CCYY1 REDEFINES WS-CCYY  PIC 9(4).                             
           05 WS-MM                       PIC 99.                               
           05 WS-DD                       PIC 99.                               
       01  WS-DATE9 REDEFINES WS-DATE     PIC 9(8).                             
       01  WS-INTEGER-CUR-DATE2           PIC S9(9) VALUE ZEROES.               
       01  WS-DATE2-N                     PIC  9(8) VALUE ZEROES.               
       01  WS-DATE2 REDEFINES WS-DATE2-N  PIC  X(8).                            
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *------------------*                                                      
       000-MAIN-ROUTINE.                                                        
      *------------------*                                                      
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 150-READ-SY02F001  THRU 150-EXIT.                            
           PERFORM 200-READ-BP13F203  THRU 200-EXIT.                            
           PERFORM 300-PROCESS-RTN    THRU 300-EXIT                             
             UNTIL EOF-F203.                                                    
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       100-INITIALIZATION.                                                      
      *------------------*                                                      
                                                                                
           OPEN INPUT  BP13F203                                                 
                       BP13K800                                                 
                       BP13K76D                                                 
                       SY02F001                                                 
                OUTPUT P13F203A.                                                
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K800 OPEN ERROR, STATUS ' K800-STATUS                
              MOVE K800-STATUS        TO RETURN-CODE                            
              PERFORM 999-CLOSE-FILES    THRU 999-EXIT                          
           END-IF.                                                              
                                                                                
           IF K76D-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K76D OPEN ERROR, STATUS ' K76D-STATUS                
              MOVE K76D-STATUS        TO RETURN-CODE                            
              PERFORM 999-CLOSE-FILES    THRU 999-EXIT                          
           END-IF.                                                              
                                                                                
           MOVE ZEROS  TO WS-COUNTERS.                                          
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       150-READ-SY02F001.                                                       
      *-------------------------------------------------------------            
           READ SY02F001                                                        
                AT END                                                          
                   GO TO 150-EXIT.                                              
                                                                                
           MOVE F001-DTE-CURRENT TO WS-DATE.                                    
                                                                                
           COMPUTE WS-INTEGER-CUR-DATE2 =                                       
              FUNCTION INTEGER-OF-DATE(WS-DATE9) + 15.                          
                                                                                
           COMPUTE WS-DATE2-N =                                                 
              FUNCTION DATE-OF-INTEGER(WS-INTEGER-CUR-DATE2).                   
                                                                                
       150-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       200-READ-BP13F203.                                                       
      *------------------*                                                      
           READ BP13F203 AT END                                                 
                MOVE 1   TO WS-EOF-F203                                         
                GO TO 200-EXIT.                                                 
                                                                                
           ADD  1  TO WS-F203-READ.                                             
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       300-PROCESS-RTN.                                                         
      *------------------*                                                      
           PERFORM 400-READ-BP13K800 THRU 400-EXIT.                             
           PERFORM 200-READ-BP13F203 THRU 200-EXIT.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       400-READ-BP13K800.                                                       
      *------------------*                                                      
           MOVE F203-NUM-REGN           TO K800-NUM-REGN.                       
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF K800-STATUS = 00                                                  
              ADD  1    TO WS-K800-FND                                          
              IF K800-NUM-STATUS = 'C'                                          
                 ADD  1    TO WS-K800-CANCEL                                    
                 GO TO 400-EXIT                                                 
              ELSE                                                              
                 IF K800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES                
                    ADD  1    TO WS-K800-ACCEPTED                               
                    GO TO 400-EXIT                                              
                 ELSE                                                           
                    ADD  1    TO WS-K800-NCANCEL                                
                    PERFORM 500-READ-BP13K76D  THRU 500-EXIT                    
                    IF WS-K76D-FND = 'N'                                        
                       CONTINUE                                                 
                    ELSE                                                        
                       GO TO 400-EXIT                                           
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              ADD  1    TO WS-K800-NFND                                         
              IF K800-STATUS = 23                                               
                 DISPLAY 'RECORD NOT FOUND IN BP13K800, ' K800-NUM-REGN         
                 GO TO 400-EXIT                                                 
              ELSE                                                              
                 MOVE K800-STATUS       TO RETURN-CODE                          
                 DISPLAY 'ERROR READING BP13K800, ' K800-NUM-REGN               
                 GO TO 999-CLOSE-FILES                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F203-NUM-PGM-ID = 'D243'                                          
              MOVE F203-DTE-BKAPPMT-DATE      TO K800-DTE-BK-APPT               
              MOVE F203-DTE-BKAPPMT-DATE-MGPS TO K800-DTE-BK-APPT-MGPS          
      *       MOVE F203-NUM-MGPS-TAG          TO K800-NUM-MGPS                  
           END-IF.                                                              
                                                                                
           IF ((K800-DTE-BK-APPT-MGPS NOT = SPACES AND LOW-VALUES AND           
                                            ZEROES) OR                          
              (K800-DTE-BK-APPT NOT = SPACES AND LOW-VALUES AND ZEROES))        
              IF K800-NUM-MGPS = 'P' OR 'C'                                     
                 IF K800-DTE-BK-APPT-MGPS <= WS-DATE2                           
                    MOVE SPACES          TO P13F203A-REC                        
                    MOVE BP13F203-REC    TO P13F203A-REC                        
                    WRITE P13F203A-REC                                          
                    ADD 1     TO  WS-F203-WRITE                                 
                 ELSE                                                           
                    GO TO 400-EXIT                                              
                 END-IF                                                         
              ELSE                                                              
                 IF K800-DTE-BK-APPT <= WS-DATE2                                
                    MOVE SPACES          TO P13F203A-REC                        
                    MOVE BP13F203-REC    TO P13F203A-REC                        
                    WRITE P13F203A-REC                                          
                    ADD 1     TO  WS-F203-WRITE                                 
                 ELSE                                                           
                    IF K800-NUM-ALLO-CAT = 'SER'                                
                       MOVE SPACES          TO P13F203A-REC                     
                       MOVE BP13F203-REC    TO P13F203A-REC                     
                       WRITE P13F203A-REC                                       
                       ADD 1     TO  WS-F203-WRITE                              
                    ELSE                                                        
                       GO TO 400-EXIT                                           
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       500-READ-BP13K76D.                                                       
      *------------------*                                                      
           MOVE K800-NUM-REGN          TO K76D-NUM-REGN.                        
                                                                                
           READ BP13K76D.                                                       
                                                                                
           IF K76D-STATUS = 00                                                  
              MOVE 'Y'      TO WS-K76D-FND                                      
              ADD 1   TO WS-K76D-FND-CNT                                        
           ELSE                                                                 
             IF K76D-STATUS = 23                                                
                MOVE 'N'      TO WS-K76D-FND                                    
                ADD 1 TO WS-K76D-NFND-CNT                                       
             ELSE                                                               
                DISPLAY 'READ BP13K76D ERROR ' K76D-NUM-REGN                    
                DISPLAY 'STATUS ' K76D-STATUS                                   
                MOVE K76D-STATUS TO RETURN-CODE                                 
                PERFORM 999-CLOSE-FILES   THRU 999-EXIT                         
             END-IF                                                             
           END-IF.                                                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------*                                                          
       999-CLOSE-FILES.                                                         
      *--------------*                                                          
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13C24N ================*'.              
           DISPLAY '    NO. OF F203 RECORDS READ     = ' WS-F203-READ.          
           DISPLAY '    NO. OF F203 RECORDS WRITTEN  = ' WS-F203-WRITE.         
           DISPLAY '    NO. OF RECORDS IN BP13K800   = '                        
                        WS-K800-FND                                             
           DISPLAY '    NO. OF REC NT FND BP13K800   = '                        
                        WS-K800-NFND                                            
           DISPLAY '    NO. OF RECORDS CANCEL(K800)  = '                        
                        WS-K800-CANCEL                                          
           DISPLAY '    NO. OF RECORDS ACCEPT(K800)  = '                        
                        WS-K800-ACCEPTED                                        
           DISPLAY '    NO. OF REC NT CANCEL (K800)  = '                        
                        WS-K800-NCANCEL                                         
           DISPLAY '    NO. OF REC NT FND BP13K800   = '                        
                        WS-K800-NFND                                            
           DISPLAY '    NO. OF RECORDS IN BLOCKLIST  = '                        
                        WS-K76D-FND-CNT                                         
           DISPLAY '    NO. OF REC NT FND BLOCKLIST  = '                        
                        WS-K76D-NFND-CNT                                        
           DISPLAY '*==========================================*'.              
                                                                                
           CLOSE BP13F203                                                       
                 BP13K800                                                       
                 BP13K76D                                                       
                 P13F203A.                                                      
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR BP13K800, STATUS ' K800-STATUS               
              MOVE K800-STATUS        TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           IF K76D-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR BP13K76D, STATUS ' K76D-STATUS               
              MOVE K76D-STATUS        TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
        999-EXIT.                                                               
            EXIT.                                                               
