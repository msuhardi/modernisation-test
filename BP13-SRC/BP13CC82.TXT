      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13CC82.                                                 
      *AUTHOR.        EDEN ROSE DINGAL.                                         
      *DATE-WRITTEN.  23 MAY 2007.                                              
      * =========================================================== *           
      * SYSTEM OF COMMITMENT  (BP13)                                *           
      * =========================================================== *           
      *  OBJECTIVES : 1) TO READ BP13FD92 AND PRINT LETTER          *           
      *                  FOR CHANGE OF INSTALMENT RATE              *           
      *                                                             *           
      * INPUT FILES  :  1.  BP13FD92                                *           
      *                 2.  BP13K800                                *           
      *                 3.  BP13K820                                *           
      *                 4.  BP13K830                                *           
      *                 5.  BP13KD00                                *           
      *                 6.  SY02F001                                *           
      *                                                             *           
      * OUTPUT       :  1.  BP13LC82                                *           
      *                 2.  P13PC82A (RATE)                         *           
      *                 2.  P13PC82B (RPAY)                         *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG REF  DATE     BY   DESCRIPTION                          *           
      * -------- -------- ---  -----------                          *           
      * BP133052 20070523 ED1  NEW PGM                              *           
      * BP133314 20080623 JB8  CHANGE BP13PC82 TO P13PC82A , NEW    *           
      *                        OUTPUT P13PC82B FOR REVISING RPAY PRD*           
      * =========================================================== *           
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
      *-----------------------*                                                 
                                                                                
         SOURCE-COMPUTER.   IBM-3090.                                           
         OBJECT-COMPUTER.   IBM-3090.                                           
                                                                                
      *-----------------------*                                                 
       INPUT-OUTPUT SECTION.                                                    
      *-----------------------*                                                 
         FILE-CONTROL.                                                          
           SELECT BP13FD92 ASSIGN TO BP13FD92.                                  
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
                                                                                
           SELECT BP13KD00 ASSIGN TO BP13KD00                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS KD00-NUM-REGN                                   
                FILE STATUS  IS WS-KD00-STATUS.                                 
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K800-NUM-REGN                                   
                FILE STATUS  IS WS-K800-STATUS.                                 
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K820-KEY-FLD                                    
                FILE STATUS  IS WS-K820-STATUS.                                 
                                                                                
           SELECT BP13K830 ASSIGN TO BP13K830                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K830-KEY-FLD                                    
                ALTERNATE RECORD KEY IS K830-NUM-REGN                           
                FILE STATUS  IS WS-K830-STATUS.                                 
                                                                                
           SELECT BP13LC82  ASSIGN TO BP13LC82.                                 
           SELECT P13PC82A  ASSIGN TO P13PC82A.                                 
           SELECT P13PC82B  ASSIGN TO P13PC82B.                                 
                                                                                
                                                                                
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
       FILE SECTION.                                                            
      *-----------------------*                                                 
                                                                                
       FD   BP13FD92    BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 1000 CHARACTERS                         
                        LABEL RECORDS ARE STANDARD                              
                        RECORDING MODE IS F.                                    
       COPY BP13FD92.                                                           
                                                                                
       FD   BP13KD00    RECORD CONTAINS 1600 CHARACTERS.                        
       COPY BP13KD00.                                                           
                                                                                
       FD   BP13K800    RECORD CONTAINS 2000 CHARACTERS.                        
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820    RECORD CONTAINS 400 CHARACTERS.                         
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830    RECORD CONTAINS 250 CHARACTERS.                         
       COPY BP13K830.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13LC82    BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 132 CHARACTERS                          
                        LABEL RECORD IS OMITTED                                 
                        RECORDING MODE IS F.                                    
       01  LC82-PRTREC  PIC X(132).                                             
                                                                                
       FD  P13PC82A     RECORD CONTAINS 90 CHARACTERS                           
                        RECORDING MODE IS F.                                    
       01  PC82A-PRTREC  PIC X(90).                                             
                                                                                
       FD  P13PC82B     RECORD CONTAINS 90 CHARACTERS                           
                        RECORDING MODE IS F.                                    
       01  PC82B-PRTREC  PIC X(90).                                             
                                                                                
      *--------------------------------------------------------------           
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------           
       01  WS-COUNTERS.                                                         
           05 WS-CNT-FD92-READ       PIC 9(7) VALUE ZEROES.                     
           05 WS-CNT-LC82-WRITE      PIC 9(7) VALUE ZEROES.                     
           05 WS-PC82A-WRT-CTR       PIC 9(7) VALUE ZEROES.                     
           05 WS-PC82B-WRT-CTR       PIC 9(7) VALUE ZEROES.                     
           05 WS-SNO                 PIC 9(5) VALUE ZEROES.                     
           05 WS-PAGE-CTR            PIC 9(4) VALUE ZEROES.                     
           05 WS-LINE-CTR            PIC 9(2) VALUE ZEROES.                     
                                                                                
       01  WS-FILE-STATUS.                                                      
           05 WS-KD00-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K800-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K820-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K830-STATUS         PIC 9(2) VALUE ZEROES.                     
                                                                                
       01  WS-DATE-HDR               PIC X(10) VALUE SPACES.                    
       01  WS-MAX-LINE               PIC 99    VALUE 53.                        
                                                                                
       01  WS-DATE.                                                             
           05  WS-CCYY               PIC X(4).                                  
           05  WS-MM                 PIC X(2).                                  
           05  WS-MM-R REDEFINES WS-MM.                                         
               10 WS-MTH             PIC 9(2).                                  
           05  WS-DD                 PIC X(2).                                  
                                                                                
       01  WS-TOTAL                  PIC 9(7)V99 VALUE ZEROES.                  
       01  WS-TMP-AMT                PIC 9(8)    VALUE ZEROES.                  
                                                                                
       01 WS-MONTH-NAME.                                                        
          05 WS-STRING PIC X(36) VALUE                                          
                'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'.                         
          05 WS-MONTH  REDEFINES WS-STRING OCCURS 12 TIMES PIC XXX.             
                                                                        01010000
       01  WS-BLANK-LINE  PIC X(132)             VALUE SPACES.                  
                                                                                
       01  WS-DASH-LINE   PIC X(132)             VALUE ALL '-'.                 
                                                                                
       01  LC82-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13LC82'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  LC82-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LC82-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
                                                                                
       01  LC82-HEADER02.                                                       
           05  FILLER              PIC X(34)     VALUE SPACES.                  
           05  FILLER              PIC X(43)     VALUE                          
               'REPORT ON CHANGE OF MONTHLY INSTALMENT RATE'.                   
           05  FILLER              PIC X(17)     VALUE                          
               ' AND REPAY PERIOD'.                                             
                                                                                
       01  LC82-HEADER03.                                                       
           05  FILLER              PIC X(71)     VALUE SPACES.                  
           05  FILLER              PIC X(45)     VALUE                          
               '---- INSTALMENT RATE(%)/REPAY PERIOD(YR) ----'.                 
                                                                                
       01  LC82-HEADER04.                                                       
           05  FILLER              PIC X(03)     VALUE 'S/N'.                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'REGN NO'.               
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(12)     VALUE 'NAME/ADDRESS'.          
           05  FILLER              PIC X(58)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'CPF1'.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'CPF2'.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'CPF3'.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'CPF4'.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'CASH'.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'REPY'.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'EFF-DTE'.               
                                                                                
       01 LC82-DETAILS.                                                         
          05 LC82-DTL-SERIAL      PIC ZZZZ9     VALUE ZEROES.                   
          05 FILLER               PIC X(01)     VALUE SPACES.                   
          05 LC82-REGNO           PIC X(08)     VALUE SPACES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 LC82-NAME1           PIC X(66)     VALUE SPACES.                   
          05 FILLER               PIC X(04)     VALUE SPACES.                   
          05 LC82-RATE1           PIC ZZ9       VALUE ZEROES.                   
          05 FILLER               PIC X(03)     VALUE SPACES.                   
          05 LC82-RATE2           PIC ZZ9       VALUE ZEROES.                   
          05 FILLER               PIC X(03)     VALUE SPACES.                   
          05 LC82-RATE3           PIC ZZ9       VALUE ZEROES.                   
          05 FILLER               PIC X(03)     VALUE SPACES.                   
          05 LC82-RATE4           PIC ZZ9       VALUE ZEROES.                   
          05 FILLER               PIC X(03)     VALUE SPACES.                   
          05 LC82-CASH            PIC ZZ9       VALUE ZEROES.                   
          05 FILLER               PIC X(03)     VALUE SPACES.                   
          05 LC82-RPAY-PRD        PIC Z9        VALUE ZEROES.                   
          05 FILLER               PIC X(04)     VALUE SPACES.                   
          05 LC82-EFF-DTE         PIC X(10)     VALUE SPACES.                   
                                                                                
       01 LC82-DETAIL2.                                                         
          05 FILLER               PIC X(16)     VALUE SPACES.                   
          05 LC82-NAME2           PIC X(60)     VALUE SPACES.                   
                                                                                
       01 LC82-DETAIL3.                                                         
          05 FILLER               PIC X(16)     VALUE SPACES.                   
          05 LC82-ADDRESS         PIC X(90)     VALUE SPACES.                   
                                                                                
                                                                                
      *--> DETAIL LINES FOR LETTER                                              
                                                                                
       01  PC82-PR-LINE-1.                                                      
           05  FILLER                   PIC X(72)  VALUE SPACES.                
           05  PC82-HDBREF              PIC X(14)  VALUE SPACES.                
                                                                                
       01  PC82-PR-LINE-2.                                                      
           05  FILLER                   PIC X(72)  VALUE SPACES.                
           05  PC82-REGNO               PIC X(08)  VALUE SPACES.                
                                                                                
       01  PC82-PR-LINE-3.                                                      
           05  FILLER                   PIC X(72)  VALUE SPACES.                
           05  PC82-DATE-HDR            PIC X(10)  VALUE SPACES.                
                                                                                
       01  PC82-PR-LINE-4.                                                      
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  PC82-NAME1B              PIC X(66)  VALUE SPACES.                
                                                                                
       01  PC82-PR-LINE-5.                                                      
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  PC82-NAME2B              PIC X(66)  VALUE SPACES.                
                                                                                
       01  PC82-PR-LINE-6.                                                      
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  PC82-BLK                 PIC X(16)  VALUE SPACES.                
           05  FILLER                   PIC X      VALUE SPACE.                 
           05  PC82-UNIT                PIC X(11)  VALUE SPACES.                
                                                                                
       01  PC82-PR-LINE-7.                                                      
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  PC82-ADDR1               PIC X(65)  VALUE SPACES.                
           05  FILLER                   PIC X      VALUE SPACE.                 
                                                                                
       01  PC82-PR-LINE-8.                                                      
           05  FILLER                   PIC X(12)  VALUE SPACES.                
           05  PC82-ADDR2               PIC X(65)  VALUE SPACES.                
                                                                                
       01  PC82-PR-LINE-9.                                                      
           05  FILLER                   PIC X(10)  VALUE SPACES.                
           05  PC82-BAR-CODE            PIC X(09)  VALUE SPACES.                
                                                                                
       01  PC82-PR-LINE-10.                                                     
           05  FILLER                   PIC X(11)  VALUE SPACES.                
           05  PC82-FLAT-ADDR           PIC X(70)  VALUE SPACES.                
                                                                                
       01  PC82-PR-LINE-11.                                                     
           05  FILLER                   PIC X(10)  VALUE SPACES.                
           05  PC82-EFF-DTE             PIC X(10)  VALUE SPACES.                
                                                                                
       01  PC82-PR-FILLER.                                                      
           05  FILLER                   PIC X(10)  VALUE SPACES.                
                                                                                
       01  PC82-PR-LINE-12.                                                     
           05  FILLER                   PIC X(10)  VALUE SPACES.                
           05  FILLER                   PIC X(04)  VALUE 'MODE'.                
           05  FILLER                   PIC X(01)  VALUE SPACES.                
           05  FILLER                   PIC X(04)  VALUE 'NAME'.                
           05  FILLER                   PIC X(51)  VALUE SPACES.                
           05  FILLER                   PIC X(03)  VALUE 'UIN'.                 
           05  FILLER                   PIC X(04)  VALUE SPACES.                
           05  FILLER                   PIC X(07)  VALUE 'RATE(%)'.             
                                                                                
       01  PC82-PR-LINE-12A.                                                    
           05  FILLER                   PIC X(10)  VALUE SPACES.                
           05  FILLER                   PIC X(03)  VALUE 'CPF'.                 
           05  FILLER                   PIC X(02)  VALUE SPACES.                
           05  PC82-NAME1               PIC X(50)  VALUE SPACES.                
           05  FILLER                   PIC X(02)  VALUE SPACES.                
           05  PC82-CPF1                PIC X(09)  VALUE SPACES.                
           05  FILLER                   PIC X(02)  VALUE SPACES.                
           05  PC82-RATE1               PIC ZZ9    VALUE ZEROES.                
                                                                                
       01  PC82-PR-LINE-12B.                                                    
           05  FILLER                   PIC X(10)  VALUE SPACES.                
           05  FILLER                   PIC X(03)  VALUE 'CPF'.                 
           05  FILLER                   PIC X(02)  VALUE SPACES.                
           05  PC82-NAME2               PIC X(50)  VALUE SPACES.                
           05  FILLER                   PIC X(02)  VALUE SPACES.                
           05  PC82-CPF2                PIC X(09)  VALUE SPACES.                
           05  FILLER                   PIC X(02)  VALUE SPACES.                
           05  PC82-RATE2               PIC ZZ9    VALUE ZEROES.                
                                                                                
       01  PC82-PR-LINE-12C.                                                    
           05  FILLER                   PIC X(10)  VALUE SPACES.                
           05  FILLER                   PIC X(03)  VALUE 'CPF'.                 
           05  FILLER                   PIC X(02)  VALUE SPACES.                
           05  PC82-NAME3               PIC X(50)  VALUE SPACES.                
           05  FILLER                   PIC X(02)  VALUE SPACES.                
           05  PC82-CPF3                PIC X(09)  VALUE SPACES.                
           05  FILLER                   PIC X(02)  VALUE SPACES.                
           05  PC82-RATE3               PIC ZZ9    VALUE ZEROES.                
                                                                                
       01  PC82-PR-LINE-12D.                                                    
           05  FILLER                   PIC X(10)  VALUE SPACES.                
           05  FILLER                   PIC X(03)  VALUE 'CPF'.                 
           05  FILLER                   PIC X(02)  VALUE SPACES.                
           05  PC82-NAME4               PIC X(50)  VALUE SPACES.                
           05  FILLER                   PIC X(02)  VALUE SPACES.                
           05  PC82-CPF4                PIC X(09)  VALUE SPACES.                
           05  FILLER                   PIC X(02)  VALUE SPACES.                
           05  PC82-RATE4               PIC ZZ9    VALUE ZEROES.                
                                                                                
       01  PC82-PR-LINE-13.                                                     
           05  FILLER                   PIC X(10)  VALUE SPACES.                
           05  FILLER                   PIC X(09)  VALUE 'CASH/GIRO'.           
           05  FILLER                   PIC X(59)  VALUE SPACES.                
           05  PC82-CASH                PIC ZZ9    VALUE ZEROES.                
                                                                                
       01  PC82A-PR-LINE-11.                                                    
           05  FILLER                   PIC X(51)  VALUE SPACES.                
           05  PC82A-EFF-DTE            PIC X(10)  VALUE SPACES.                
                                                                                
       01  PC82A-PR-LINE-12.                                                    
           05  FILLER                   PIC X(51)  VALUE SPACES.                
           05  PC82A-RPAY-PRD           PIC X(02)  VALUE SPACES.                
           05  FILLER                   PIC X(04)  VALUE ' yrs'.                
                                                                                
       01  PC82A-PR-LINE-13.                                                    
           05  FILLER                   PIC X(51)  VALUE SPACES.                
           05  PC82A-AMT-LOAN           PIC $Z,ZZZ,ZZ9.99 VALUE ZEROES.         
           05  FILLER                   PIC X(07)  VALUE ' as at '.             
           05  PC82A-DTE-AS-AT          PIC X(10)  VALUE SPACES.                
                                                                                
       01  PC82A-PR-LINE-14.                                                    
           05  FILLER                   PIC X(51)  VALUE SPACES.                
           05  PC82A-REMAIN-YR          PIC Z9     VALUE ZEROES.                
           05  FILLER                   PIC X(05)  VALUE ' yrs '.               
           05  PC82A-REMAIN-MTH         PIC Z9     VALUE ZEROES.                
           05  FILLER                   PIC X(05)  VALUE ' mths'.               
                                                                                
       01  PC82A-PR-LINE-15.                                                    
           05  FILLER                   PIC X(51)  VALUE SPACES.                
           05  PC82A-CPF-INSTAL         PIC $Z,ZZ9.99   VALUE ZEROES.           
           05  FILLER                   PIC X(23)  VALUE                        
               ' at prevailing interest'.                                       
                                                                                
       01  PC82A-PR-LINE-15A.                                                   
           05  FILLER                   PIC X(51)  VALUE SPACES.                
           05  FILLER                   PIC X(03)  VALUE 'of '.                 
           05  PC82A-INT-RATE           PIC Z9.99  VALUE ZEROES.                
           05  FILLER                   PIC X(11)  VALUE '% per annum'.         
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR AV02C015                                                   
      *-------------------------------------------------------------            
        01  WS-LINK-AV02C015.                                                   
            05  AV02C015-IN-POSTCODE          PIC X(6).                         
            05  AV02C015-OUT-POSTCODE-CHKSUM  PIC X(9).                         
                                                                                
       COPY AV02COMM.                                                           
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR BP13C913                                                   
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH            PIC X(04).                             
           05 WS-LINK-NUM-ACC            PIC X(04).                             
           05 WS-LINK-NUM-CHK            PIC X(01).                             
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           PERFORM 1000-START-UP          THRU 1000-EXIT.                       
           PERFORM 2000-READ-BP13FD92     THRU 2000-EXIT.                       
           PERFORM 3000-PROCESS-RECORDS   THRU 3000-EXIT                        
                   UNTIL FD92-NUM-REGN = HIGH-VALUES.                           
           PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       1000-START-UP.                                                           
      *--------------------------------------------------------------           
           OPEN INPUT  BP13FD92                                                 
                       BP13KD00                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BP13K830                                                 
                       SY02F001                                                 
               OUTPUT  BP13LC82                                                 
                       P13PC82A                                                 
                       P13PC82B.                                                
                                                                                
           IF WS-KD00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KD00 FILE ERROR ' WS-KD00-STATUS             
              MOVE     WS-KD00-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K800 FILE ERROR ' WS-K800-STATUS             
              MOVE     WS-K800-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K820 FILE ERROR ' WS-K820-STATUS             
              MOVE     WS-K820-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K830 FILE ERROR ' WS-K830-STATUS             
              MOVE     WS-K830-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-CURRENT TO WS-DATE.                                    
                                                                                
           STRING WS-DD '/' WS-MM '/' WS-CCYY                                   
           DELIMITED BY SIZE                   INTO WS-DATE-HDR.                
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       2000-READ-BP13FD92.                                                      
      *--------------------------------------------------------------           
           READ BP13FD92                                                        
              AT END MOVE HIGH-VALUES TO FD92-NUM-REGN                          
              GO TO 2000-EXIT.                                                  
                                                                                
           ADD 1 TO WS-CNT-FD92-READ.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       3000-PROCESS-RECORDS.                                                    
      *--------------------------------------------------------------           
           PERFORM 3100-READ-BP13K800         THRU 3100-EXIT                    
                                                                                
           IF WS-K800-STATUS = 00                                               
              PERFORM 3200-INIT-FIELDS        THRU 3200-EXIT                    
              PERFORM 4000-PRINT-LETTER       THRU 4000-EXIT                    
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13FD92         THRU 2000-EXIT.                   
                                                                                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       3100-READ-BP13K800.                                                      
      *--------------------------------------------------------------           
      *  RETRIEVE OCCUPIERS  *                                                  
      *----------------------*                                                  
           MOVE FD92-NUM-REGN TO K800-NUM-REGN.                                 
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = 00                                               
              CONTINUE                                                          
                                                                                
           ELSE                                                                 
             IF WS-K800-STATUS = 23                                             
                DISPLAY 'REC NOT FND IN K800, STATUS ' WS-K800-STATUS           
                        ', REGN IS ' FD92-NUM-REGN                              
             ELSE                                                               
                DISPLAY 'ERROR READING K800, STATUS ' WS-K800-STATUS            
                        ', REGN IS ' FD92-NUM-REGN                              
                MOVE WS-K800-STATUS        TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
             END-IF                                                             
           END-IF.                                                              
                                                                                
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       3200-INIT-FIELDS.                                                        
      *--------------------------------------------------------------           
           INITIALIZE        PC82-PR-LINE-1                                     
                             PC82-PR-LINE-2                                     
                             PC82-PR-LINE-3                                     
                             PC82-PR-LINE-4                                     
                             PC82-PR-LINE-5                                     
                             PC82-PR-LINE-6                                     
                             PC82-PR-LINE-7                                     
                             PC82-PR-LINE-8                                     
                             PC82-PR-LINE-9                                     
                             PC82-PR-LINE-10                                    
                             PC82-PR-LINE-11                                    
                             PC82-PR-LINE-12                                    
                             PC82-PR-LINE-12A                                   
                             PC82-PR-LINE-12B                                   
                             PC82-PR-LINE-12C                                   
                             PC82-PR-LINE-12D                                   
                             PC82-PR-LINE-13                                    
                             LC82-DETAILS                                       
                             LC82-DETAIL2                                       
                             LC82-DETAIL3                                       
                             PC82A-PR-LINE-11                                   
                             PC82A-PR-LINE-12                                   
                             PC82A-PR-LINE-13                                   
                             PC82A-PR-LINE-14                                   
                             PC82A-PR-LINE-15                                   
                             PC82A-PR-LINE-15A.                                 
                                                                                
           IF FD92-AMT-RATE-CPF1-NEW NOT NUMERIC                                
              MOVE 0 TO FD92-AMT-RATE-CPF1-NEW                                  
           END-IF.                                                              
           IF FD92-AMT-RATE-CPF2-NEW NOT NUMERIC                                
              MOVE 0 TO FD92-AMT-RATE-CPF2-NEW                                  
           END-IF.                                                              
           IF FD92-AMT-RATE-CPF3-NEW NOT NUMERIC                                
              MOVE 0 TO FD92-AMT-RATE-CPF3-NEW                                  
           END-IF.                                                              
           IF FD92-AMT-RATE-CPF4-NEW NOT NUMERIC                                
              MOVE 0 TO FD92-AMT-RATE-CPF4-NEW                                  
           END-IF.                                                              
           IF FD92-AMT-CASH-NEW      NOT NUMERIC                                
              MOVE 0 TO FD92-AMT-CASH-NEW                                       
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       4000-PRINT-LETTER.                                                       
      *--------------------------------------------------------------           
           IF K800-NUM-NRIC1 NOT = SPACES AND LOW-VALUES AND ZEROES             
              AND K800-NUM-NRIC1(1:1) NOT = '#'                                 
              MOVE SPACES                TO   BP13K820-REC                      
              MOVE K800-NUM-NRIC1        TO   K820-NUM-NRIC                     
              MOVE FD92-NUM-REGN         TO   K820-NUM-REGN                     
              PERFORM 4100-READ-BP13K820 THRU 4100-EXIT                         
              MOVE K820-NUM-NRIC         TO   PC82-CPF1                         
              MOVE K820-NME-OCCP         TO   LC82-NAME1                        
                                              PC82-NAME1                        
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES AND ZEROES             
              AND K800-NUM-NRIC2(1:1) NOT = '#'                                 
              MOVE SPACES                TO   BP13K820-REC                      
              MOVE K800-NUM-NRIC2        TO   K820-NUM-NRIC                     
              MOVE FD92-NUM-REGN         TO   K820-NUM-REGN                     
              PERFORM 4100-READ-BP13K820 THRU 4100-EXIT                         
              MOVE K820-NUM-NRIC         TO   PC82-CPF2                         
              MOVE K820-NME-OCCP         TO   LC82-NAME2                        
                                              PC82-NAME2                        
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC3 NOT = SPACES AND LOW-VALUES AND ZEROES             
              AND K800-NUM-NRIC3(1:1) NOT = '#'                                 
              MOVE SPACES                TO   BP13K820-REC                      
              MOVE K800-NUM-NRIC3        TO   K820-NUM-NRIC                     
              MOVE FD92-NUM-REGN         TO   K820-NUM-REGN                     
              PERFORM 4100-READ-BP13K820 THRU 4100-EXIT                         
              MOVE K820-NUM-NRIC         TO   PC82-CPF3                         
              MOVE K820-NME-OCCP         TO   PC82-NAME3                        
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC4 NOT = SPACES AND LOW-VALUES AND ZEROES             
              AND K800-NUM-NRIC4(1:1) NOT = '#'                                 
              MOVE SPACES                TO   BP13K820-REC                      
              MOVE K800-NUM-NRIC4        TO   K820-NUM-NRIC                     
              MOVE FD92-NUM-REGN         TO   K820-NUM-REGN                     
              PERFORM 4100-READ-BP13K820 THRU 4100-EXIT                         
              MOVE K820-NUM-NRIC         TO   PC82-CPF4                         
              MOVE K820-NME-OCCP         TO   PC82-NAME4                        
           END-IF.                                                              
                                                                                
           PERFORM 4200-GET-CORR-ADDR         THRU 4200-EXIT.                   
           PERFORM 4300-GET-FLAT-ADDR         THRU 4300-EXIT.                   
           PERFORM 5000-MOVE-REPORT-DTLS      THRU 5000-EXIT.                   
           PERFORM 5100-MOVE-LETTER-DTLS      THRU 5100-EXIT.                   
           PERFORM 6000-PRINT-LETTER          THRU 6000-EXIT.                   
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       4100-READ-BP13K820.                                                      
      *--------------------------------------------------------------           
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
                                                                                
             MOVE SPACES TO K820-NME-OCCP                                       
                                                                                
             IF WS-K820-STATUS = 23                                             
                DISPLAY 'REC NOT FND IN K820, STATUS ' WS-K820-STATUS           
                        ', KEY IS ' K820-NUM-REGN ' ' K820-NUM-NRIC             
             ELSE                                                               
                DISPLAY 'ERROR READING K820, STATUS ' WS-K820-STATUS            
                        ', KEY IS ' K820-NUM-REGN ' ' K820-NUM-NRIC             
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
             END-IF                                                             
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       4200-GET-CORR-ADDR.                                                      
      *-----------------------------------------------------------*             
           MOVE FD92-NUM-REGN          TO K830-NUM-REGN.                        
                                                                                
           READ BP13K830 KEY IS K830-NUM-REGN.                                  
                                                                                
           IF WS-K830-STATUS  = 00 OR 02                                        
              IF (K830-NUM-MAIL-ADDR =  SPACES OR LOW-VALUES) OR                
                 ((K830-NUM-MAIL-LVL-CHAR =  '#') AND                           
                 (K830-NUM-MAIL-LVL-NUM =  SPACES OR LOW-VALUES))               
                 MOVE K830-NUM-BLK         TO PC82-BLK                          
                 MOVE K830-NUM-FLAT        TO PC82-UNIT                         
                 MOVE K830-NME-STREET-BLDG TO PC82-ADDR1                        
                 IF K830-NUM-POSTAL  = SPACES OR LOW-VALUES OR ZEROES           
                    MOVE SPACES            TO PC82-ADDR2                        
                 ELSE                                                           
                    STRING 'SINGAPORE '  K830-NUM-POSTAL                        
                       DELIMITED BY SIZE    INTO PC82-ADDR2                     
                                                                                
                    MOVE SPACES          TO WS-LINK-AV02C015                    
                    MOVE K830-NUM-POSTAL TO AV02C015-IN-POSTCODE                
                    PERFORM 4400-CALL-AV02C015     THRU 4400-EXIT               
                    MOVE AV02C015-OUT-POSTCODE-CHKSUM TO PC82-BAR-CODE          
                 END-IF                                                         
              ELSE                                                              
                 MOVE K830-NUM-MAIL-BLK        TO PC82-BLK                      
                 MOVE K830-NUM-MAIL-FLAT       TO PC82-UNIT                     
                 MOVE K830-NME-MAIL-STR-BLDG   TO PC82-ADDR1                    
                 IF K830-NUM-MAIL-POSTAL = SPACES OR LOW-VALUES OR ZEROS        
                    MOVE SPACES                TO PC82-ADDR2                    
                 ELSE                                                           
                    STRING 'SINGAPORE '      K830-NUM-MAIL-POSTAL               
                           DELIMITED BY SIZE INTO PC82-ADDR2                    
                                                                                
                    MOVE SPACES          TO WS-LINK-AV02C015                    
                    MOVE K830-NUM-MAIL-POSTAL TO AV02C015-IN-POSTCODE           
                    PERFORM 4400-CALL-AV02C015     THRU 4400-EXIT               
                    MOVE AV02C015-OUT-POSTCODE-CHKSUM TO PC82-BAR-CODE          
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
             IF WS-K830-STATUS = 23                                             
                DISPLAY 'REC NOT FND IN K830, STATUS ' WS-K830-STATUS           
                        ', REGN IS  '    FD92-NUM-REGN                          
             ELSE                                                               
                DISPLAY 'ERROR READING K830, STATUS ' WS-K830-STATUS            
                        ', REGN IS  '    FD92-NUM-REGN                          
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
             END-IF                                                             
                                                                                
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------*           
       4300-GET-FLAT-ADDR.                                                      
      *-------------------------------------------------------------*           
           MOVE SPACES                TO BP13COMM8-REC.                         
           MOVE K800-NUM-SCH-ACC(1:9) TO WS-LINK-REC.                           
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC, BP13COMM8-REC.                    
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
             WHEN 000                                                           
                STRING 'BLK '  COMM8-NUM-BLK  ' '                               
                       '#'  COMM8-NUM-LEVEL   '-'                               
                       COMM8-NUM-UNIT-MAIN    ' ' DELIMITED BY SIZE             
                       COMM8-NME-STREET       DELIMITED BY '   '                
                       INTO PC82-FLAT-ADDR                                      
             WHEN 100                                                           
                DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE - '                     
                         K800-NUM-SCH-ACC ', REGN : ' FD92-NUM-REGN             
             WHEN OTHER                                                         
                DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR              
                        ', AT KEY ' K800-NUM-SCH-ACC                            
                        ', REGN : ' FD92-NUM-REGN                               
           END-EVALUATE.                                                        
                                                                                
       4300-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *----------------------*                                                  
       4400-CALL-AV02C015.                                                      
      *----------------------*                                                  
           CALL 'AV02C015'          USING                                       
                                    AV02C015-IN-POSTCODE                        
                                    AV02C015-OUT-POSTCODE-CHKSUM.               
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       4500-GET-LOAN-BAL.                                                       
      *--------------------------------------------------------------           
                                                                                
           MOVE SPACES                           TO BP13KD00-REC                
           INITIALIZE                               BP13KD00-REC.               
                                                                                
           MOVE FD92-NUM-REGN                    TO KD00-NUM-REGN.              
                                                                                
           READ BP13KD00.                                                       
                                                                                
           IF WS-KD00-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
             IF WS-KD00-STATUS = 23                                             
                MOVE ZEROES                   TO KD00-AMT-LOAN-BAL              
                DISPLAY 'REC NOT FND IN KD00, STATUS ' WS-KD00-STATUS           
                        ', REGN IS ' FD92-NUM-REGN                              
             ELSE                                                               
                DISPLAY 'ERROR READING KD00, STATUS ' WS-KD00-STATUS            
                        ', REGN IS ' FD92-NUM-REGN                              
                MOVE WS-KD00-STATUS        TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
             END-IF                                                             
           END-IF.                                                              
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------           
       5000-MOVE-REPORT-DTLS.                                                   
      *--------------------------------------------------------------           
           MOVE FD92-NUM-REGN           TO  LC82-REGNO.                         
           MOVE WS-DATE-HDR             TO  LC82-DATE-HDR.                      
           IF FD92-NUM-TRANS = 'RPAY'                                           
              MOVE FD92-NUM-REPAY-NEW   TO  LC82-RPAY-PRD                       
           END-IF.                                                              
           IF FD92-AMT-RATE-CPF1-NEW = 0 AND                                    
              FD92-AMT-RATE-CPF2-NEW = 0 AND                                    
              FD92-AMT-RATE-CPF3-NEW = 0 AND                                    
              FD92-AMT-RATE-CPF4-NEW = 0 AND                                    
              FD92-AMT-CASH-NEW      = 0                                        
              CONTINUE                                                          
           ELSE                                                                 
              MOVE FD92-AMT-RATE-CPF1-NEW  TO  LC82-RATE1                       
              MOVE FD92-AMT-RATE-CPF2-NEW  TO  LC82-RATE2                       
              MOVE FD92-AMT-RATE-CPF3-NEW  TO  LC82-RATE3                       
              MOVE FD92-AMT-RATE-CPF4-NEW  TO  LC82-RATE4                       
              MOVE FD92-AMT-CASH-NEW       TO  LC82-CASH                        
           END-IF.                                                              
                                                                                
           STRING FD92-DTE-REVSD-INSTAL-EFF(7:2) '/'                            
                  FD92-DTE-REVSD-INSTAL-EFF(5:2) '/'                            
                  FD92-DTE-REVSD-INSTAL-EFF(1:4)                                
                  DELIMITED BY SIZE INTO LC82-EFF-DTE.                          
                                                                                
           IF PC82-FLAT-ADDR NOT = SPACES                                       
              STRING 'BLK '  COMM8-NUM-BLK   ' '                                
                  '#'     COMM8-NUM-LEVEL '-'                                   
                          COMM8-NUM-UNIT-MAIN ' '   DELIMITED BY SIZE           
                          COMM8-NUM-UNIT-SUB  ' '   DELIMITED BY SIZE           
                          COMM8-NME-STREET          DELIMITED BY '   '          
                   ' S('  COMM8-NUM-POSTAL-CODE ')' DELIMITED BY SIZE           
                   INTO   LC82-ADDRESS                                          
              END-STRING                                                        
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       5100-MOVE-LETTER-DTLS.                                                   
      *--------------------------------------------------------------           
           MOVE LC82-REGNO        TO  PC82-REGNO.                               
           MOVE WS-DATE-HDR       TO  PC82-DATE-HDR                             
           STRING F001-LST-PREV-DD '/'                                          
                  F001-LST-PREV-MM '/'                                          
                  F001-DTE-LAST-DAY-PREV(1:4)                                   
              DELIMITED BY SIZE INTO PC82A-DTE-AS-AT.                           
           MOVE LC82-NAME1        TO  PC82-NAME1B.                              
           MOVE LC82-NAME2        TO  PC82-NAME2B.                              
           MOVE LC82-EFF-DTE      TO  PC82-EFF-DTE                              
                                      PC82A-EFF-DTE.                            
           STRING FD92-NUM-SCH-ACC(1:4) '-'                                     
                  FD92-NUM-SCH-ACC(5:4) '-'                                     
                  FD92-NUM-SCH-ACC(9:1) '-'                                     
                  FD92-NUM-SCH-ACC(10:2)                                        
                  DELIMITED BY SIZE INTO PC82-HDBREF.                           
                                                                                
           IF FD92-NUM-TRANS = 'RPAY'                                           
              MOVE FD92-NUM-REPAY-NEW         TO PC82A-RPAY-PRD                 
              PERFORM 4500-GET-LOAN-BAL     THRU 4500-EXIT                      
              MOVE KD00-AMT-LOAN-BAL          TO PC82A-AMT-LOAN                 
              COMPUTE PC82A-CPF-INSTAL = FD92-AMT-INSTAL-CASH +                 
                                         FD92-CPF-INSTAL-TOT-NEW                
              MOVE FD92-CDE-RATE              TO PC82A-INT-RATE                 
              IF FD92-DTE-INT-START(1:2) NOT NUMERIC                            
                 MOVE ZEROES                  TO PC82A-REMAIN-YR                
              ELSE                                                              
                 MOVE FD92-DTE-INT-START(1:2) TO PC82A-REMAIN-YR                
              END-IF                                                            
              IF FD92-DTE-INT-END(1:2)   NOT NUMERIC                            
                 MOVE ZEROES                  TO PC82A-REMAIN-MTH               
              ELSE                                                              
                 MOVE FD92-DTE-INT-END(1:2)   TO PC82A-REMAIN-MTH               
              END-IF                                                            
           ELSE                                                                 
              MOVE LC82-CASH                  TO  PC82-CASH                     
              IF PC82-NAME1 NOT = SPACES AND LOW-VALUES AND ZEROS               
                 MOVE FD92-AMT-RATE-CPF1-NEW  TO  PC82-RATE1                    
              END-IF                                                            
              IF PC82-NAME2 NOT = SPACES AND LOW-VALUES AND ZEROS               
                 MOVE FD92-AMT-RATE-CPF2-NEW  TO  PC82-RATE2                    
              END-IF                                                            
              IF PC82-NAME3 NOT = SPACES AND LOW-VALUES AND ZEROS               
                 MOVE FD92-AMT-RATE-CPF3-NEW  TO  PC82-RATE3                    
              END-IF                                                            
              IF PC82-NAME4 NOT = SPACES AND LOW-VALUES AND ZEROS               
                 MOVE FD92-AMT-RATE-CPF4-NEW  TO  PC82-RATE4                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------           
       6000-PRINT-LETTER.                                                       
      *--------------------------------------------------------------           
      *-->  PRINT REPORT                                                        
           ADD   1                      TO  WS-SNO.                             
           MOVE  WS-SNO                 TO  LC82-DTL-SERIAL.                    
                                                                                
           IF WS-LINE-CTR > WS-MAX-LINE OR                                      
              WS-SNO       = 1                                                  
              ADD            1   TO WS-PAGE-CTR                                 
              MOVE ZEROES        TO LC82-PAGE-HDR                               
              MOVE WS-PAGE-CTR   TO LC82-PAGE-HDR                               
              PERFORM 7000-PRINT-HEADING THRU 7000-EXIT                         
           END-IF.                                                              
                                                                                
           WRITE LC82-PRTREC FROM LC82-DETAILS.                                 
           WRITE LC82-PRTREC FROM LC82-DETAIL2.                                 
           WRITE LC82-PRTREC FROM LC82-DETAIL3.                                 
           WRITE LC82-PRTREC FROM WS-BLANK-LINE.                                
                                                                                
           ADD 4 TO WS-LINE-CTR.                                                
           ADD   1              TO    WS-CNT-LC82-WRITE.                        
                                                                                
      *--> PRINT LETTER                                                         
           IF FD92-NUM-TRANS = 'RATE'                                           
              PERFORM 6100-PRINT-RATE          THRU 6100-EXIT                   
           ELSE                                                                 
              PERFORM 6300-PRINT-RPAY          THRU 6300-EXIT                   
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       6100-PRINT-RATE.                                                         
      *--------------------------------------------------------------*          
                                                                                
           WRITE PC82A-PRTREC    FROM  PC82-PR-LINE-1 AFTER PAGE.               
           WRITE PC82A-PRTREC    FROM  PC82-PR-LINE-2.                          
           WRITE PC82A-PRTREC    FROM  PC82-PR-LINE-3.                          
           WRITE PC82A-PRTREC    FROM  PC82-PR-LINE-4 AFTER 4.                  
           WRITE PC82A-PRTREC    FROM  PC82-PR-LINE-5.                          
           WRITE PC82A-PRTREC    FROM  PC82-PR-LINE-6.                          
           WRITE PC82A-PRTREC    FROM  PC82-PR-LINE-7.                          
           WRITE PC82A-PRTREC    FROM  PC82-PR-LINE-8.                          
           WRITE PC82A-PRTREC    FROM  PC82-PR-LINE-9  AFTER 4.                 
           WRITE PC82A-PRTREC    FROM  PC82-PR-LINE-10 AFTER 6.                 
                                                                                
           WRITE PC82A-PRTREC    FROM  PC82-PR-LINE-11 AFTER 4                  
           WRITE PC82A-PRTREC    FROM  PC82-PR-FILLER  AFTER 1                  
           WRITE PC82A-PRTREC    FROM  PC82-PR-LINE-12 AFTER 1                  
           WRITE PC82A-PRTREC    FROM  PC82-PR-FILLER  AFTER 1                  
                                                                                
           PERFORM 6200-PRINT-CPF-RATES  THRU 6200-EXIT                         
                                                                                
           ADD 1   TO  WS-PC82A-WRT-CTR.                                        
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------           
       6200-PRINT-CPF-RATES.                                                    
      *--------------------------------------------------------------           
           IF PC82-NAME1 NOT = SPACES AND LOW-VALUES AND ZEROS                  
              WRITE PC82A-PRTREC    FROM  PC82-PR-LINE-12A AFTER 1              
           END-IF.                                                              
           IF PC82-NAME2 NOT = SPACES AND LOW-VALUES AND ZEROS                  
              WRITE PC82A-PRTREC    FROM  PC82-PR-LINE-12B AFTER 1              
           END-IF.                                                              
           IF PC82-NAME3 NOT = SPACES AND LOW-VALUES AND ZEROS                  
              WRITE PC82A-PRTREC    FROM  PC82-PR-LINE-12C AFTER 1              
           END-IF.                                                              
           IF PC82-NAME4 NOT = SPACES AND LOW-VALUES AND ZEROS                  
              WRITE PC82A-PRTREC    FROM  PC82-PR-LINE-12D AFTER 1              
           END-IF.                                                              
                                                                                
           IF FD92-AMT-CASH-NEW > 0                                             
              WRITE PC82A-PRTREC    FROM  PC82-PR-FILLER  AFTER 1               
              WRITE PC82A-PRTREC    FROM  PC82-PR-LINE-13 AFTER 1               
           END-IF.                                                              
                                                                                
                                                                                
       6200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------           
       6300-PRINT-RPAY.                                                         
      *--------------------------------------------------------------           
                                                                                
           WRITE PC82B-PRTREC    FROM  PC82-PR-LINE-1 AFTER PAGE.               
           WRITE PC82B-PRTREC    FROM  PC82-PR-LINE-2.                          
           WRITE PC82B-PRTREC    FROM  PC82-PR-LINE-3.                          
           WRITE PC82B-PRTREC    FROM  PC82-PR-LINE-4 AFTER 4.                  
           WRITE PC82B-PRTREC    FROM  PC82-PR-LINE-5.                          
           WRITE PC82B-PRTREC    FROM  PC82-PR-LINE-6.                          
           WRITE PC82B-PRTREC    FROM  PC82-PR-LINE-7.                          
           WRITE PC82B-PRTREC    FROM  PC82-PR-LINE-8.                          
           WRITE PC82B-PRTREC    FROM  PC82-PR-LINE-9  AFTER 4.                 
           WRITE PC82B-PRTREC    FROM  PC82-PR-LINE-10 AFTER 6.                 
                                                                                
           WRITE PC82B-PRTREC    FROM  PC82A-PR-LINE-11 AFTER 8                 
           WRITE PC82B-PRTREC    FROM  PC82A-PR-LINE-12 AFTER 1                 
           WRITE PC82B-PRTREC    FROM  PC82A-PR-LINE-13 AFTER 1                 
           WRITE PC82B-PRTREC    FROM  PC82A-PR-LINE-14 AFTER 1                 
           WRITE PC82B-PRTREC    FROM  PC82A-PR-LINE-15 AFTER 1                 
           WRITE PC82B-PRTREC    FROM  PC82A-PR-LINE-15A AFTER 1                
                                                                                
           ADD 1   TO  WS-PC82B-WRT-CTR.                                        
                                                                                
       6300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------           
       7000-PRINT-HEADING.                                                      
      *--------------------------------------------------------------           
            WRITE LC82-PRTREC FROM WS-BLANK-LINE AFTER PAGE.                    
            WRITE LC82-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LC82-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LC82-PRTREC FROM LC82-HEADER01.                               
            WRITE LC82-PRTREC FROM LC82-HEADER02.                               
            WRITE LC82-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LC82-PRTREC FROM LC82-HEADER03.                               
            WRITE LC82-PRTREC FROM LC82-HEADER04.                               
            WRITE LC82-PRTREC FROM WS-DASH-LINE.                                
                                                                                
            INITIALIZE WS-LINE-CTR.                                             
            MOVE  9 TO WS-LINE-CTR.                                             
                                                                                
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *--------------------------------------------------------------           
       9000-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------------           
           CLOSE  BP13FD92                                                      
                  BP13KD00                                                      
                  BP13K800                                                      
                  BP13K820                                                      
                  BP13K830                                                      
                  SY02F001                                                      
                  BP13LC82                                                      
                  P13PC82A                                                      
                  P13PC82B.                                                     
                                                                                
                                                                                
           DISPLAY ' RUN DATE: ' WS-DATE-HDR.                                   
           DISPLAY ' *------------ BP13CC82 AUDIT TRAIL ------------*'.         
           DISPLAY '    '.                                                      
           DISPLAY 'NO. OF RECORDS READ FROM DAILY TRANS (BP13FD92) = '         
                   WS-CNT-FD92-READ.                                            
           DISPLAY 'NO. OF LETTERS PRINTED-RATE          (P13PC82A) = '         
                   WS-PC82A-WRT-CTR.                                            
           DISPLAY 'NO. OF LETTERS PRINTED-RPAY          (P13PC82B) = '         
                   WS-PC82B-WRT-CTR.                                            
                                                                                
           IF WS-KD00-STATUS NOT = 00                                           
              DISPLAY 'CLOSING BP13KD00 FILE ERROR ' WS-KD00-STATUS             
              MOVE     WS-KD00-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00                                           
              DISPLAY 'CLOSING BP13K800 FILE ERROR ' WS-K800-STATUS             
              MOVE     WS-K800-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00                                           
              DISPLAY 'CLOSING BP13K820 FILE ERROR ' WS-K820-STATUS             
              MOVE     WS-K820-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS NOT = 00                                           
              DISPLAY 'CLOSING BP13K830 FILE ERROR ' WS-K830-STATUS             
              MOVE     WS-K830-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
