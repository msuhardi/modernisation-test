       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD12.                                                 
      *AUTHOR.        MSD1.                                                     
      *DATE-WRITTEN.  31/01/2008.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVES: UPLOAD DBSS RESULT TO SOC MASTER (BP13K800)    *            
      *                                                                         
      *                                                            *            
      *  INPUT FILE :   BP13F800 -                                 *            
      *  OUTPUT FILE:   BP13K800 -                                 *            
      * ---------------------------------------------------------- *            
      * CHG REF   BY    DATE      DETAILS                          *            
      * -------   ====  ========= ================================ *            
      * BP133244  MSD1  20080131  NEW PGM                          *            
      * BP134371  ZDD1  20111101  ADD BP13KB78 FOR DBSS-PROJECT    *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS K800-STATUS.                               
           SELECT BP13KB78 ASSIGN TO BP13KB78                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KB78-KEY-FLD                               
                  FILE STATUS     IS KB78-STATUS.                               
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F800                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD    CONTAINS 2000 CHARACTERS.                                  
       COPY BP13K800.                                                           
                                                                                
       FD  BP13KB78                                                             
           RECORD    CONTAINS 3300 CHARACTERS.                                  
       COPY BP13KB78.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01 WS-COUNTERS.                                                          
          05 WS-F800-READ         PIC 9(08) VALUE ZEROES.                       
          05 WS-K800-WRITE        PIC 9(08) VALUE ZEROES.                       
          05 WS-KB78-FND          PIC 9(08) VALUE ZEROES.                       
          05 WS-KB78-NFND         PIC 9(08) VALUE ZEROES.                       
       01 KB78-STATUS             PIC 9(02) VALUE ZEROES.                       
       01 K800-STATUS             PIC 9(02) VALUE ZEROES.                       
      *01 WS-CUR-DATE             PIC X(08) VALUE SPACES.                       
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13F800  THRU 2000-EXIT.                          
           PERFORM 3000-PROCESS-DATA   THRU 3000-EXIT                           
              UNTIL F800-NUM-REGN = HIGH-VALUES.                                
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP13KB78                                                 
                I-O    BP13K800.                                                
                                                                                
           IF K800-STATUS NOT = ZEROS AND 97                                    
              DISPLAY ' ERROR OPENING BP13K800  FILE, STATUS '                  
                  K800-STATUS                                                   
              MOVE K800-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                        
           IF KB78-STATUS NOT = ZEROS AND 97                                    
              DISPLAY ' ERROR OPENING BP13KB78  FILE, STATUS '                  
                  KB78-STATUS                                                   
              MOVE KB78-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                        
                                                                                
      *    MOVE FUNCTION CURRENT-DATE      TO WS-CUR-DATE.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F800.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F800                                                        
                AT END                                                          
                   MOVE HIGH-VALUES TO F800-NUM-REGN                            
                NOT AT END                                                      
                   ADD 1    TO WS-F800-READ                                     
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF F800-NUM-REGN NOT = SPACES AND LOW-VALUES                         
                                                                                
           PERFORM 4000-READ-BP13KB78   THRU 4000-EXIT                          
           MOVE KB78-NUM-PROJECT        TO F800-NUM-DBSS-PRJCT                  
           MOVE  BP13F800-MASTER TO BP13K800-MASTER                             
           WRITE BP13K800-MASTER                                                
                                                                                
           EVALUATE  K800-STATUS                                                
           WHEN 00                                                              
                ADD 1     TO WS-K800-WRITE                                      
           WHEN 22                                                              
                DISPLAY ' ERROR WRITING TO BP13K800, FOUND DUPLICATE'           
                        ' REC - ' K800-NUM-REGN                                 
           WHEN OTHER                                                           
              DISPLAY ' ERROR WRITING BP13K800, STATUS ' K800-STATUS            
              DISPLAY 'K800-NUM-REGN - ' K800-NUM-REGN                          
              MOVE K800-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-EVALUATE                                                         
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F800  THRU 2000-EXIT.                          
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-READ-BP13KB78.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE F800-NUM-REGN TO KB78-NUM-REGN.                                 
                                                                                
           READ  BP13KB78.                                                      
                                                                                
           EVALUATE  KB78-STATUS                                                
           WHEN 00                                                              
                ADD 1     TO WS-KB78-FND                                        
           WHEN 23                                                              
                MOVE SPACES   TO KB78-NUM-PROJECT                               
                ADD 1     TO WS-KB78-NFND                                       
                DISPLAY ' NOT FOUND IN BP13KB78, '                              
                        ' REC - ' KB78-NUM-REGN                                 
           WHEN OTHER                                                           
              DISPLAY ' ERROR READING BP13KB78, STATUS ' KB78-STATUS            
              DISPLAY 'KB78-NUM-REGN - ' KB78-NUM-REGN                          
              MOVE KB78-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' BP13CD12 CONTROL TOTAL '.                                  
           DISPLAY '------------------------------------------'.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECS READ  FROM BP13F800 : ' WS-F800-READ.            
           DISPLAY 'NO OF RECS WRITTEN TO BP13K800 : ' WS-K800-WRITE.           
           DISPLAY 'NO OF RECS FOUND   IN BP13KB78 : ' WS-KB78-FND.             
           DISPLAY 'NO OF RECS N-FOUND IN BP13KB78 : ' WS-KB78-NFND.            
                                                                                
           CLOSE BP13F800                                                       
                 BP13K800                                                       
                 BP13KB78.                                                      
                                                                                
           IF K800-STATUS NOT = ZEROES                                          
              DISPLAY 'ERROR CLOSING BP13K800 : ' K800-STATUS                   
              MOVE K800-STATUS                 TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           IF KB78-STATUS NOT = ZEROES                                          
              DISPLAY 'ERROR CLOSING BP13KB78 : ' KB78-STATUS                   
              MOVE KB78-STATUS                 TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
