       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C541.                                                    
      *AUTHOR. M.PRINCE.                                                        
      *DATE-WRITTEN.   15/10/1997.                                              
      * ====================================================== *                
      *              SYSTEM OF COMMITMENT  (BP13)              *                
      * ====================================================== *                
      *                                                        *                
      *    OBJECTIVE  :                                        *                
      *        1.  PROGRAM TO VALIDATE MARITAL STATUS WITH     *                
      *            AGE OF THE OCCUPIER /APPLICANT              *                
      *                                                        *                
      *     INPUT FILES:                                       *                
      *           1. BP13F825  - BP13.F825.REQOCCP             *                
      *           2. BP13F500  - BP13.F500.PREREGN             *                
      *                                                        *                
      *     OUTPUT:  P13L541A                                  *                
      *                                                        *                
      * CHG NO    DATE         BY   DESCRIPTION                *                
      * -------   --------     ---  -----------                *                
      * BP130386  15/10/1997   MPA                             *                
      * BP130652  19/02/1999   NOR  EXPAND F500 RECL FR 300-500*                
      **********************************************************                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F500   ASSIGN TO BP13F500.                                
           SELECT BP13F825   ASSIGN TO BP13F825.                                
           SELECT P13L541A   ASSIGN TO P13L541A.                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F500                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13F500.                                                           
                                                                                
       FD   BP13F825                                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13F825.                                                           
                                                                                
       FD   P13L541A                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       01   P13L541A-REC               PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WORK-AREAS.                                                          
           05  WS-REGN-MTCH            PIC 9(8)    VALUE 0.                     
           05  WS-REGN-UNM             PIC 9(8)    VALUE 0.                     
           05  WS-READ-F500            PIC 9(8)    VALUE 0.                     
           05  WS-READ-F825            PIC 9(8)    VALUE 0.                     
                                                                                
       01 L451-COUNTERS.                                                        
           05 WS-LINE-CTR              PIC 9(2)     VALUE 60.                   
           05 WS-CASE-CTR              PIC 9(8)     VALUE 0.                    
           05 WS-PAGENO                PIC 9(4)     VALUE 0.                    
                                                                                
       01 WS-CUR-DATE.                                                          
             05 WS-DTE-YY              PIC 9(4) VALUE 0.                        
             05 WS-DTE-MM              PIC 9(2) VALUE 0.                        
             05 WS-DTE-DD              PIC 9(2) VALUE 0.                        
                                                                                
       01 WS-DTE-BIRTH.                                                         
             05 WS-DOB-CCYY            PIC 9(4) VALUE 0.                        
             05 WS-DOB-MM              PIC 9(2) VALUE 0.                        
             05 WS-DOB-DD              PIC 9(2) VALUE 0.                        
                                                                                
       01  WS-PRINT-VARIABLES.                                                  
           05  L541-HDG1-LINE.                                                  
               10  FILLER              PIC X(8)   VALUE 'BP13L541'.             
               10  FILLER              PIC X(25)  VALUE SPACES.                 
               10  FILLER              PIC X(43)  VALUE                         
                   'S Y S T E M     O F     C O M M I T M E N T'.               
               10  FILLER              PIC X(20)  VALUE SPACES.                 
               10  FILLER              PIC X(8)   VALUE 'DATE :'.               
               10  L541-DATE           PIC X(10).                               
               10  FILLER              PIC X(6)   VALUE SPACES.                 
               10  FILLER              PIC X(7)   VALUE 'PAGE : '.              
               10  L541-PAGE-CTR       PIC ZZZ9.                                
                                                                                
           05  L541-HDG2-LINE.                                                  
               10  FILLER              PIC X(31)  VALUE SPACES.                 
               10  FILLER              PIC X(47)  VALUE                         
                   'ERROR REPORT ON MARITAL STATUS                '.            
               10  FILLER              PIC X(18)  VALUE SPACES.                 
               10  FILLER              PIC X(30)  VALUE                         
                   'USER : APPLICATION TEAM'.                                   
                                                                                
           05  L541-HDG3-LINE.                                                  
               10  FILLER              PIC X(8)   VALUE '    S/NO'.             
               10  FILLER              PIC X(4)   VALUE  SPACES.                
               10  FILLER              PIC X(7)   VALUE 'REGN NO'.              
               10  FILLER              PIC X(4)   VALUE  SPACES.                
               10  FILLER              PIC X(5)   VALUE 'ZONE '.                
               10  FILLER              PIC X(3)   VALUE  SPACES.                
               10  FILLER              PIC X(3)   VALUE 'FT '.                  
               10  FILLER              PIC X(3)   VALUE  SPACES.                
               10  FILLER              PIC X(2)   VALUE 'NT'.                   
               10  FILLER              PIC X(3)   VALUE  SPACES.                
               10  FILLER              PIC X(5)   VALUE 'NRIC '.                
               10  FILLER              PIC X(8)   VALUE  SPACES.                
               10  FILLER              PIC X(20)  VALUE 'NAME'.                 
               10  FILLER              PIC X(4)   VALUE  SPACES.                
               10  FILLER              PIC X(8)   VALUE 'MAR STAT'.             
               10  FILLER              PIC X(3)   VALUE  SPACES.                
               10  FILLER              PIC X(3)   VALUE 'HA '.                  
               10  FILLER              PIC X(3)   VALUE  SPACES.                
               10  FILLER              PIC X(4)   VALUE  'DOB '.                
                                                                                
           05  L541-HDG4-LINE.                                                  
               10  FILLER              PIC X(8)   VALUE '    ----'.             
               10  FILLER              PIC X(4)   VALUE  SPACES.                
               10  FILLER              PIC X(8)   VALUE '--------'.             
               10  FILLER              PIC X(3)   VALUE  SPACES.                
               10  FILLER              PIC X(5)   VALUE '---- '.                
               10  FILLER              PIC X(3)   VALUE  SPACES.                
               10  FILLER              PIC X(3)   VALUE '-- '.                  
               10  FILLER              PIC X(3)   VALUE  SPACES.                
               10  FILLER              PIC X(2)   VALUE '--'.                   
               10  FILLER              PIC X(3)   VALUE  SPACES.                
               10  FILLER              PIC X(9)   VALUE '---------'.            
               10  FILLER              PIC X(4)   VALUE  SPACES.                
               10  FILLER              PIC X(20)  VALUE ALL '-'.                
               10  FILLER              PIC X(4)   VALUE  SPACES.                
               10  FILLER              PIC X(8)   VALUE '--------'.             
               10  FILLER              PIC X(3)   VALUE  SPACES.                
               10  FILLER              PIC X(3)   VALUE '-- '.                  
               10  FILLER              PIC X(3)   VALUE  SPACES.                
               10  FILLER              PIC X(10)   VALUE  '----------'.         
                                                                                
       01  L541-PR-DETAILS.                                                     
           05  FILLER                  PIC X(01)     VALUE SPACES .             
           05  L541-SNO                PIC ZZZZZZ9.                             
           05  FILLER                  PIC X(04)     VALUE SPACES .             
           05  L541-REGNO              PIC X(08).                               
           05  FILLER                  PIC X(04)     VALUE SPACES .             
           05  L541-ZONE               PIC X(02).                               
           05  FILLER                  PIC X(05)     VALUE SPACES .             
           05  L541-FLAT-TYPE          PIC X(02).                               
           05  FILLER                  PIC X(04)     VALUE SPACES .             
           05  L541-NT1                PIC X(03) .                              
           05  FILLER                  PIC X(02)     VALUE SPACES .             
           05  L541-NRIC               PIC X(09) .                              
           05  FILLER                  PIC X(04)     VALUE SPACES .             
           05  L541-NME-OCCP           PIC X(20) .                              
           05  FILLER                  PIC X(07)     VALUE SPACES .             
           05  L541-MAR-STAT           PIC X(01) .                              
           05  FILLER                  PIC X(07)     VALUE SPACES .             
           05  L541-HA                 PIC X(01) .                              
           05  FILLER                  PIC X(05)     VALUE SPACES .             
           05  L541-DOB                PIC X(10) .                              
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       000-MAIN.                                                                
      *---------*                                                               
           OPEN INPUT  BP13F500                                                 
                       BP13F825                                                 
                OUTPUT P13L541A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-CUR-DATE.                    
                                                                                
           STRING    WS-CUR-DATE(7:2) '/'                                       
                     WS-CUR-DATE(5:2) '/'                                       
                     WS-CUR-DATE(1:4) '/'                                       
           DELIMITED BY SIZE INTO     L541-DATE.                                
                                                                                
           PERFORM 100-READ-F500      THRU 100-EXIT.                            
           PERFORM 200-READ-F825      THRU 200-EXIT.                            
           PERFORM 300-COMPARE        THRU 300-EXIT                             
                   UNTIL F500-NUM-REGN = HIGH-VALUES                            
                    AND  F825-NUM-REGN = HIGH-VALUES.                           
           PERFORM 900-CLOSE-ROUTINE.                                           
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-READ-F500.                                                           
      *--------------*                                                          
           READ BP13F500                                                        
           AT END MOVE HIGH-VALUES    TO F500-NUM-REGN                          
                       GO TO 100-EXIT.                                          
                                                                                
           ADD 1 TO WS-READ-F500.                                               
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-F825.                                                           
      *--------------*                                                          
           READ BP13F825                                                        
           AT END MOVE HIGH-VALUES    TO F825-NUM-REGN                          
                       GO TO 200-EXIT.                                          
                                                                                
           ADD 1 TO WS-READ-F825.                                               
           IF F825-DTE-BIRTH IS NOT NUMERIC                                     
              MOVE ZEROS  TO WS-DTE-BIRTH                                       
           END-IF.                                                              
                                                                                
           MOVE F825-DTE-BIRTH        TO WS-DTE-BIRTH.                          
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-COMPARE.                                                             
      *------------*                                                            
           IF F500-NUM-REGN = F825-NUM-REGN                                     
              ADD    1                TO WS-REGN-MTCH                           
              PERFORM 400-MATCH-RECORDS       THRU 400-EXIT                     
                 UNTIL F825-NUM-REGN NOT = F500-NUM-REGN                        
                 OR    F825-NUM-REGN = HIGH-VALUES                              
              PERFORM 100-READ-F500   THRU 100-EXIT                             
           ELSE                                                                 
              IF F500-NUM-REGN > F825-NUM-REGN                                  
                 ADD 1                TO WS-REGN-UNM                            
                 PERFORM 200-READ-F825  THRU 200-EXIT                           
              ELSE                                                              
                 ADD 1                TO WS-REGN-UNM                            
                 PERFORM 100-READ-F500  THRU 100-EXIT.                          
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-MATCH-RECORDS.                                                       
      *------------------*                                                      
           IF F825-NUM-MARITAL-STATUS = '2'                                     
           IF (F825-NUM-NRIC = F500-NUM-NRIC1) OR                               
              (F825-NUM-NRIC = F500-NUM-NRIC2) OR                               
              (F825-NUM-NRIC = F500-NUM-NRIC3) OR                               
              (F825-NUM-NRIC = F500-NUM-NRIC4)                                  
               PERFORM 410-COMPUTE-APPLT-AGE THRU   410-EXIT                    
           ELSE                                                                 
               PERFORM 420-COMPUTE-OCCP-AGE  THRU   420-EXIT.                   
                                                                                
           PERFORM 200-READ-F825      THRU   200-EXIT.                          
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       410-COMPUTE-APPLT-AGE.                                                   
      *----------------------*                                                  
                                                                                
           IF WS-DTE-BIRTH > ZEROS                                              
              ADD 0021                   TO WS-DOB-CCYY                         
              IF WS-DTE-BIRTH > WS-CUR-DATE                                     
                 MOVE 'Y'                TO  L541-HA                            
                 PERFORM 440-PRINT-PARA  THRU   440-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
      *                                                                         
       420-COMPUTE-OCCP-AGE.                                                    
      *----------------------*                                                  
                                                                                
           IF WS-DTE-BIRTH > ZEROS                                              
              ADD 0014                   TO WS-DOB-CCYY                         
              IF WS-DTE-BIRTH > WS-CUR-DATE                                     
                 MOVE SPACES             TO  L541-HA                            
                 PERFORM 440-PRINT-PARA     THRU   440-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
      *                                                                         
       430-MOVE-VBLES.                                                          
      *---------------*                                                         
           MOVE F500-NUM-REGN         TO  L541-REGNO.                           
           MOVE F500-CDE-ZONE         TO  L541-ZONE.                            
           MOVE F500-CDE-FLAT-TYPE    TO  L541-FLAT-TYPE.                       
           MOVE F500-CDE-NT1          TO  L541-NT1.                             
           MOVE F825-NUM-NRIC         TO  L541-NRIC.                            
           MOVE F825-NME-OCCP         TO  L541-NME-OCCP.                        
           MOVE F825-NUM-MARITAL-STATUS TO  L541-MAR-STAT.                      
                                                                                
           STRING    F825-DTE-BIRTH(7:2) '/'                                    
                     F825-DTE-BIRTH(5:2) '/'                                    
                     F825-DTE-BIRTH(1:4) '/'                                    
           DELIMITED BY SIZE INTO L541-DOB.                                     
                                                                                
       430-EXIT.                                                                
           EXIT.                                                                
      *                                                                         
       440-PRINT-PARA.                                                          
      *---------------*                                                         
           PERFORM 430-MOVE-VBLES     THRU 430-EXIT.                            
           IF WS-LINE-CTR >= 60                                                 
              PERFORM 450-PRT-HDR     THRU 450-EXIT                             
           END-IF.                                                              
           PERFORM 460-PRT-DTL        THRU 460-EXIT.                            
                                                                                
       440-EXIT.                                                                
           EXIT.                                                                
      *                                                                         
       450-PRT-HDR.                                                             
      *------------*                                                            
           ADD   1                    TO  WS-PAGENO.                            
           MOVE  WS-PAGENO            TO  L541-PAGE-CTR.                        
           WRITE P13L541A-REC FROM L541-HDG1-LINE AFTER PAGE.                   
           WRITE P13L541A-REC FROM L541-HDG2-LINE.                              
           WRITE P13L541A-REC FROM L541-HDG3-LINE.                              
           WRITE P13L541A-REC FROM L541-HDG4-LINE.                              
           MOVE  4                    TO  WS-LINE-CTR.                          
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
      *                                                                         
       460-PRT-DTL.                                                             
      *---------------*                                                         
           ADD 1                      TO  WS-LINE-CTR                           
                                          WS-CASE-CTR.                          
           MOVE WS-CASE-CTR           TO  L541-SNO.                             
           WRITE P13L541A-REC   FROM  L541-PR-DETAILS.                          
                                                                                
       460-EXIT.                                                                
           EXIT.                                                                
      *                                                                         
       900-CLOSE-ROUTINE.                                                       
      *------------------*                                                      
           DISPLAY '********************  '.                                    
           DISPLAY '                      '.                                    
           DISPLAY 'PROGRAM-ID : BP13C541 '.                                    
           DISPLAY 'OUTPUT BP13L541       '.                                    
           DISPLAY 'CURR-DATE  : ' WS-CUR-DATE.                                 
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS READ    : '                                   
           DISPLAY '   BP13F500           : ' WS-READ-F500.                     
           DISPLAY '   BP13F825           : ' WS-READ-F825.                     
           DISPLAY 'NO OF PAGES PRINTED   : '                                   
           DISPLAY '   P13L541A           : ' WS-PAGENO.                        
           DISPLAY 'NO OF ROWS PRINTED    : ' WS-CASE-CTR.                      
           DISPLAY 'NO REGN MATCH         : ' WS-REGN-MTCH.                     
           DISPLAY 'NO REGN UNMATCH       : ' WS-REGN-UNM.                      
           DISPLAY '                      '.                                    
           DISPLAY '********************  '.                                    
                                                                                
           CLOSE BP13F500                                                       
                 BP13F825                                                       
                 P13L541A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
