       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C550                                                  
      *AUTHOR.        GD.                                                       
      *DATE-WRITTEN.  07/03/95.                                                 
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *      1.  TO EXTRACT HA RECORDS FROM F825 USING HA1-HA4 *                
      *          NRIC FROM F740.                               *                
      *                                                        *                
      *    TASK :                                              *                
      *      1.  TO OUTPUT F825 RECORDS WITH MATCHING NRIC     *                
      *          IN BP13INP.                                   *                
      *                                                        *                
      *                                                        *                
      *     INPUT FILES:                                       *                
      *      1  BP13INP   - CONTAINS HA1-HA4 REFNO, NRIC ONLY  *                
      *      2  BP13F825                                       *                
      *                                                        *                
      *     OUTPUT FILE :                                      *                
      *      1  BP13OUT                                        *                
      *                                                        *                
      * CHG-NO  BY   ON       DESCRIPTION                      *                
      * ---------------------------------                      *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT BP13F825 ASSIGN TO BP13F825.                                  
           SELECT BP13INP ASSIGN TO BP13INP.                                    
           SELECT BP13OUT ASSIGN TO BP13OUT.                                    
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F825                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F825.                                                           
                                                                                
       FD   BP13INP                                                             
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 20 CHARACTERS                                       
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
                                                                                
       01   BP13INP-REC.                                                        
            05 FILLER              PIC X(6).                                    
            05 INP-KEY.                                                         
               10 INP-NRIC         PIC X(9).                                    
            05 FILLER              PIC X(5).                                    
                                                                                
       FD   BP13OUT                                                             
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       01   BP13OUT-REC            PIC X(200).                                  
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-F825-CNT             PIC 9(8)    VALUE ZERO.                      
       01  WS-F825-WRITE           PIC 9(8)    VALUE ZERO.                      
       01  WS-INPUT-CNT            PIC 9(8)    VALUE ZERO.                      
       01  WS-MATCH                PIC 9(8)    VALUE ZERO.                      
       01  WS-F825-UNMATCH         PIC 9(8)    VALUE ZERO.                      
       01  WS-EOF-F825             PIC X(3).                                    
           88  EOF-OF-F825                     VALUE 'YES'.                     
       01  WS-EOF-INPUT            PIC X(3).                                    
           88  EOF-OF-INPUT                    VALUE 'YES'.                     
                                                                                
       01  WS-F825-KEY.                                                         
           05  WS-F825-NRIC        PIC X(9).                                    
                                                                                
       PROCEDURE DIVISION.                                                      
       10-MAIN-LOGIC.                                                           
           PERFORM 20-INITIALIZATION  THRU 20-EXIT.                             
           PERFORM 200-READ-F825      THRU 200-EXIT.                            
           PERFORM 300-READ-INPUT     THRU 300-EXIT.                            
           PERFORM 400-MATCHING-ROUTINE THRU 400-EXIT                           
                   UNTIL EOF-OF-INPUT AND EOF-OF-F825.                          
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       20-INITIALIZATION.                                                       
           OPEN INPUT  BP13F825                                                 
                       BP13INP                                                  
               OUTPUT  BP13OUT.                                                 
                                                                                
           MOVE 'NO' TO WS-EOF-F825.                                            
           MOVE 'NO' TO WS-EOF-INPUT.                                           
                                                                                
       20-EXIT.                                                                 
           EXIT.                                                                
                                                                                
       200-READ-F825.                                                           
           READ BP13F825                                                        
                AT END MOVE 'YES' TO WS-EOF-F825                                
                MOVE HIGH-VALUE TO WS-F825-KEY                                  
                GO TO 200-EXIT.                                                 
           ADD 1 TO WS-F825-CNT.                                                
           MOVE F825-NUM-NRIC TO WS-F825-NRIC.                                  
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-READ-INPUT.                                                          
           READ BP13INP                                                         
               AT END MOVE 'YES' TO WS-EOF-INPUT                                
               MOVE HIGH-VALUE TO INP-KEY                                       
               GO TO 300-EXIT.                                                  
           ADD 1 TO WS-INPUT-CNT.                                               
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *  MATCH F825 WITH INPUT FILE (NUM-NRIC)                        *         
      *****************************************************************         
                                                                                
       400-MATCHING-ROUTINE.                                                    
           IF WS-F825-KEY = INP-KEY                                             
                ADD 1 TO WS-MATCH                                       .       
                PERFORM 410-WRITE-F825 THRU 410-EXIT                            
                  UNTIL WS-F825-KEY NOT = INP-KEY                               
                PERFORM 300-READ-INPUT THRU 300-EXIT                            
           ELSE                                                                 
           IF WS-F825-KEY > INP-KEY                                             
                PERFORM 300-READ-INPUT THRU 300-EXIT                            
           ELSE                                                                 
                ADD 1 TO WS-F825-UNMATCH                                .       
                PERFORM 200-READ-F825  THRU 200-EXIT.                           
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       410-WRITE-F825.                                                          
           WRITE BP13OUT-REC FROM BP13F825-REC.                                 
           ADD 1 TO WS-F825-WRITE                                               
           PERFORM 200-READ-F825  THRU 200-EXIT.                                
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
           DISPLAY ' '.                                                         
           DISPLAY '---- BP13C550 --------------------------'.                  
           DISPLAY ' '.                                                         
           DISPLAY '1. RECORDS READ FROM BP13F825   = ' WS-F825-CNT.            
           DISPLAY '2. RECORDS READ FROM BP13INP    = ' WS-INPUT-CNT.           
           DISPLAY '3. MATCHED RECORDS              = ' WS-MATCH.               
           DISPLAY '4. F825 UNMATCH RECORDS         = ' WS-F825-UNMATCH.        
           DISPLAY '5. RECORDS WRITTEN OUT          = ' WS-F825-WRITE.          
           DISPLAY ' '.                                                         
                                                                                
           CLOSE      BP13INP                                                   
                      BP13F825                                                  
                      BP13OUT.                                                  
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
