      *--------------------------------------------------------------*          
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------------*          
       PROGRAM-ID.    BP13C23S.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  DEC 09 2009.                                              
      *--------------------------------------------------------------*          
      *                                                              *          
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)                 *          
      *                                                              *          
      *    SYSTEM ID   : BP13                                        *          
      *                                                              *          
      *    OBJECTIVES :  SORT QUEUE FOR 100% AND 200%                *          
      *                                                              *          
      *    INPUT FILES  : BP13F205                                   *          
      *                   BP13F740                                   *          
      *    OUTPUT       : P13F740A                                   *          
      *                   P13F740B                                   *          
      *--------------------------------------------------------------*          
      * CHG-NO  BY   DATE     DETAILS                                *          
      *-------- ---- -------- ---------------------------------------*          
      *BP133770 ESA1 09122009 NEW PGM                                *          
      *BP133770 ESA1 23122009 CATER FOR NPL                          *          
      *BP134043 ESA1 15112010 ADD READING OF BP13K813                *          
      *BP135190 SMR2 04122013 EXPAND BP13K813 TO 1000                *          
      *-------- ---- -------- ---------------------------------------*          
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205 ASSIGN TO BP13F205.                                  
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
           SELECT P13F740A ASSIGN TO P13F740A.                                  
           SELECT P13F740B ASSIGN TO P13F740B.                                  
                                                                                
           SELECT BP13K813 ASSIGN TO BP13K813                                   
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K813-KEY-FLD                         
                           FILE STATUS  IS WS-K813-STATUS.                      
                                                                                
      *--------------------------------------------------------------*          
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------------*          
                                                                                
       FD   BP13F205                                                            
            RECORD CONTAINS 80 CHARACTERS                                       
            RECORDING MODE  IS F.                                               
       COPY BP13F205.                                                           
                                                                                
       FD   BP13F740                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       COPY BP13F740.                                                           
                                                                                
       FD  BP13K813                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13K813.                                                           
                                                                                
       FD   P13F740A                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE  IS F.                                               
       01   P13F740A-REC         PIC X(500).                                    
                                                                                
       FD   P13F740B                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE  IS F.                                               
       01   P13F740B-REC         PIC X(500).                                    
                                                                                
      *--------------------------------------------------------------*          
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------*          
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F205-EOF                PIC X(1)  VALUE 'N'.                  
           05  WS-F740-EOF                PIC X(1)  VALUE 'N'.                  
           05  WS-K813-FND                PIC X(1)  VALUE 'N'.                  
                                                                                
       01  WS-MISCELLANEOUS-VARS.                                               
           05  WS-EDITED-DATE             PIC X(10) VALUE SPACES.               
           05  WS-FIRST-100               PIC X     VALUE SPACE.                
           05  WS-CNT-F740A-WRITE         PIC 9(8)  VALUE 0.                    
           05  WS-CNT-F740B-WRITE         PIC 9(8)  VALUE 0.                    
           05  WS-CNT-F740-READ           PIC 9(8)  VALUE 0.                    
           05  WS-CNT-F205-READ           PIC 9(8)  VALUE 0.                    
           05  WS-K813-STATUS             PIC 9(02) VALUE 0.                    
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY                PIC 9(4).                             
           05  WS-SYS-MM                  PIC 9(2).                             
           05  WS-SYS-DD                  PIC 9(2).                             
                                                                                
      *--------------------------------------------------------------*          
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------------*          
                                                                                
      *--------------------------------------------------------------*          
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------------------------------------*          
                                                                                
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
           PERFORM 1100-READ-F205         THRU 1100-EXIT.                       
           PERFORM 2000-PROCESS           THRU 2000-EXIT                        
                   UNTIL WS-F205-EOF = 'Y'.                                     
           PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1000-OPEN-ROUTINE.                                                       
      *--------------------------------------------------------------*          
                                                                                
           OPEN INPUT  BP13F205                                                 
                       BP13F740                                                 
                       BP13K813                                                 
                OUTPUT P13F740A                                                 
                       P13F740B.                                                
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K813 OPEN ERROR, STATUS ' WS-K813-STATUS             
              MOVE WS-K813-STATUS        TO RETURN-CODE                         
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-SYSTEM-DATE.                   
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1100-READ-F205.                                                          
      *--------------------------------------------------------------*          
           READ BP13F205                                                        
                AT END  MOVE 'Y' TO WS-F205-EOF                                 
                        MOVE 'Y' TO WS-F740-EOF                                 
                GO TO 1100-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1                 TO WS-CNT-F205-READ.                           
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1200-READ-F740.                                                          
      *--------------------------------------------------------------*          
           READ BP13F740                                                        
                AT END  MOVE 'Y' TO WS-F740-EOF                                 
                GO TO 1200-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-CNT-F740-READ.                                           
                                                                                
       1200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------*          
       1300-CLOSE-OPEN-F740.                                                    
      *--------------------------------------------------------------*          
           CLOSE      BP13F740.                                                 
           OPEN INPUT BP13F740.                                                 
                                                                                
           MOVE 'N'   TO WS-F740-EOF.                                           
                                                                                
       1300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       2000-PROCESS.                                                            
      *--------------------------------------------------------------*          
           PERFORM 4000-PROCESS           THRU 4000-EXIT                        
                   UNTIL WS-F740-EOF = 'Y'.                                     
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------------------------------------------------         
       4000-PROCESS.                                                            
      *----------------------------------------------------------------         
           PERFORM 1200-READ-F740    THRU 1200-EXIT.                            
                                                                                
           IF WS-F740-EOF = 'Y'                                                 
              PERFORM 1100-READ-F205        THRU 1100-EXIT                      
                                                                                
              IF WS-F205-EOF = 'Y'                                              
                 GO TO 4000-EXIT                                                
              END-IF                                                            
                                                                                
              PERFORM 1300-CLOSE-OPEN-F740  THRU 1300-EXIT                      
              GO TO 4000-EXIT                                                   
           ELSE                                                                 
              IF ((F205-NUM-NT-ZONE = F740-CDE-NT1) AND                         
                  (F205-NUM-FLAT-TYPE = F740-CDE-FLAT-TYPE))                    
                  IF F205-NUM-SELECTION = 'NPL'                                 
                    PERFORM 5000-WRITE-P13F740A   THRU 5000-EXIT                
                  ELSE                                                          
                    IF F205-NUM-SELECTION = 'SBF'                               
                       PERFORM 4500-VALIDATE-FT-QUEUE THRU 4500-EXIT            
                       IF WS-FIRST-100 = 'Y'                                    
                          PERFORM 5000-WRITE-P13F740A   THRU 5000-EXIT          
                       ELSE                                                     
                          IF WS-FIRST-100 = 'N'                                 
                             PERFORM 6000-WRITE-P13F740B THRU 6000-EXIT         
                          END-IF                                                
                       END-IF                                                   
                    END-IF                                                      
                  END-IF                                                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       4500-VALIDATE-FT-QUEUE.                                                  
      *-------------------------------------------------------------            
           MOVE SPACES   TO  WS-FIRST-100.                                      
                                                                                
           PERFORM 7000-READ-BP13K813    THRU 7000-EXIT.                        
                                                                                
           IF WS-K813-FND = 'Y'                                                 
              IF F740-NUM-NT-FT-QUEUE <= K813-NUM-QUEUE-MID                     
                 MOVE 'Y'    TO  WS-FIRST-100                                   
              ELSE                                                              
                 MOVE 'N'    TO  WS-FIRST-100                                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       5000-WRITE-P13F740A.                                                     
      *--------------------------------------------------------------*          
            MOVE SPACES        TO   P13F740A-REC.                               
            WRITE P13F740A-REC FROM BP13F740-REC.                               
            ADD  1  TO WS-CNT-F740A-WRITE.                                      
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------*          
       6000-WRITE-P13F740B.                                                     
      *--------------------------------------------------------------*          
            MOVE SPACES        TO   P13F740B-REC.                               
            WRITE P13F740B-REC FROM BP13F740-REC.                               
            ADD  1  TO WS-CNT-F740B-WRITE.                                      
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       7000-READ-BP13K813.                                                      
      *--------------------------------------------------------------*          
           MOVE 'N'                 TO WS-K813-FND.                             
           MOVE F205-NUM-NT-ZONE    TO K813-NUM-ZONE.                           
           MOVE F205-NUM-FLAT-TYPE  TO K813-NUM-FLAT-TYPE.                      
           MOVE F205-DTE-BALLOT     TO K813-DTE-BALLOT.                         
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
           WHEN 00                                                              
                MOVE 'Y'            TO WS-K813-FND                              
           WHEN 23                                                              
                MOVE 'N'            TO WS-K813-FND                              
                DISPLAY 'RECORD NOT FOUND IN BP13K813 : '                       
                         K813-KEY-FLD                                           
           WHEN OTHER                                                           
                DISPLAY 'READ BP13K813 ERROR ' K813-KEY-FLD                     
                DISPLAY 'STATUS ' WS-K813-STATUS                                
                MOVE WS-K813-STATUS TO RETURN-CODE                              
                PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       9999-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------------*          
                                                                                
           CLOSE BP13F205                                                       
                 BP13F740                                                       
                 BP13K813                                                       
                 P13F740A                                                       
                 P13F740B.                                                      
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR BP13K813, STATUS ' WS-K813-STATUS            
              MOVE WS-K813-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '********* BP13C23S CONTROL *********'.                      
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF READ (BP13F205) = ' WS-CNT-F205-READ.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF READ (BP13F740) = ' WS-CNT-F740-READ.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF WRITE(100%)     = ' WS-CNT-F740A-WRITE.              
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF WRITE(200%)     = ' WS-CNT-F740B-WRITE.              
           DISPLAY SPACES.                                                      
           DISPLAY '************************************'.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************** E N D  O F  P R O G R A M *****************             
