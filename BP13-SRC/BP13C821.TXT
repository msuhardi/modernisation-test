       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C821.                                                 
       AUTHOR.        GD.                                                       
      *DATE-WRITTEN.  31/12/94.                                                 
      *REMARKS.                                                                 
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES :                                        *                
      *                                                        *                
      *    1.  TO COMPUTE ESTIMATED QUEUE POSITION FOR BY      *                
      *        ZONE, FT, HHTY, CATEGORY.                       *                
      *                                                        *                
      *    INPUT       : BP13F520                              *                
      *                                                        *                
      *    OUTPUT      : BP13F500                              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO   BY   DATE    DETAILS                          *                
      * ======   ==   ====  ================================== *                
      * BP130652 NOR  19021999 EXPAND F500 & F520 RECL 300-500 *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F520 ASSIGN TO BP13F520.                                  
           SELECT BP13F500 ASSIGN TO BP13F500.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F520                                                            
            RECORDING MODE  F                                                   
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F520.                                                           
                                                                                
       FD   BP13F500                                                            
            RECORDING MODE  F                                                   
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F500.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F520-EOF-SW                 PIC X(1)  VALUE 'N'.              
           05  WS-F500-EOF-SW                 PIC X(1)  VALUE 'N'.              
                                                                                
       01  WS-RECORD-COUNTERS.                                                  
           05  WS-F520-READ-CTR               PIC 9(5)  VALUE ZEROS.            
           05  WS-F500-WRITE-CTR              PIC 9(5)  VALUE ZEROS.            
                                                                                
       01  WS-CONTROL-FIELDS.                                                   
           05  WS-PREV-ZONE                   PIC X(2).                         
           05  WS-PREV-FLAT-TYPE              PIC X(2).                         
           05  WS-PREV-ALLOC-SCH              PIC X(2).                         
           05  WS-PREV-HOUSEHOLD              PIC X(1).                         
           05  WS-PREV-CDE-CATEGORY           PIC X(1).                         
                                                                                
       01  WS-SHORT-LIST-COUNTERS.                                              
           05  WS-SHORTLIST-PRIO-CAT1         PIC 9(5)  VALUE ZEROS.            
           05  WS-SHORTLIST-PRIO-CAT2         PIC 9(5)  VALUE ZEROS.            
           05  WS-SHORTLIST-PRIO-CAT3         PIC 9(5)  VALUE ZEROS.            
                                                                                
       01  WS-SYSTEM-DATE                     PIC 9(8)  VALUE ZEROS.            
                                                                                
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-INITIALIZATION THRU 1000-EXIT.                          
           PERFORM 2000-READ-F520      THRU 2000-EXIT.                          
           PERFORM 3000-PROCESS-F520   THRU 3000-EXIT                           
              UNTIL WS-F520-EOF-SW = 'Y'.                                       
           PERFORM 9999-CLOSE-FILES.                                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-INITIALIZATION.                                                     
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F520                                                 
                OUTPUT BP13F500.                                                
                                                                                
           MOVE SPACES TO WS-PREV-ZONE.                                         
           MOVE SPACES TO WS-PREV-FLAT-TYPE.                                    
           MOVE SPACES TO WS-PREV-ALLOC-SCH.                                    
           MOVE SPACES TO WS-PREV-HOUSEHOLD.                                    
           MOVE SPACES TO WS-PREV-CDE-CATEGORY.                                 
           MOVE ZEROS  TO WS-SHORTLIST-PRIO-CAT1                                
                          WS-SHORTLIST-PRIO-CAT2                                
                          WS-SHORTLIST-PRIO-CAT3.                               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2000-READ-F520.                                                          
      ****************************************************************          
                                                                                
           READ BP13F520                                                        
              AT END                                                            
                 MOVE 'Y' TO WS-F520-EOF-SW                                     
                 GO  TO 2000-EXIT                                               
           END-READ.                                                            
                                                                                
           ADD  1 TO WS-F520-READ-CTR.                                          
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3000-PROCESS-F520.                                                       
      ****************************************************************          
                                                                                
           IF (F520-CDE-FLAT-TYPE  =  WS-PREV-FLAT-TYPE)  AND                   
              (F520-CDE-ZONE       = WS-PREV-ZONE) AND                          
              (F520-CDE-ALLOC-SCH (1:2) =  WS-PREV-ALLOC-SCH) AND               
              (F520-CDE-HOUSEHOLD =  WS-PREV-HOUSEHOLD) AND                     
              (F520-CDE-CATEGORY = WS-PREV-CDE-CATEGORY)                        
              NEXT SENTENCE                                                     
           ELSE                                                                 
              MOVE ZEROS              TO   WS-SHORTLIST-PRIO-CAT1               
                                           WS-SHORTLIST-PRIO-CAT2               
                                           WS-SHORTLIST-PRIO-CAT3               
              MOVE F520-CDE-FLAT-TYPE TO   WS-PREV-FLAT-TYPE                    
              MOVE F520-CDE-ZONE      TO   WS-PREV-ZONE                         
              MOVE F520-CDE-ALLOC-SCH TO   WS-PREV-ALLOC-SCH                    
              MOVE F520-CDE-CATEGORY  TO   WS-PREV-CDE-CATEGORY                 
              MOVE F520-CDE-HOUSEHOLD TO   WS-PREV-HOUSEHOLD                    
           END-IF.                                                              
                                                                                
           EVALUATE F520-CDE-CATEGORY                                           
              WHEN '1'                                                          
                 ADD +1                 TO WS-SHORTLIST-PRIO-CAT1               
                 MOVE WS-SHORTLIST-PRIO-CAT1                                    
                               TO F520-NUM-SHORTLIST-PRIO-CAT                   
              WHEN '2'                                                          
                 ADD +1                 TO WS-SHORTLIST-PRIO-CAT2               
                 MOVE WS-SHORTLIST-PRIO-CAT2                                    
                               TO F520-NUM-SHORTLIST-PRIO-CAT                   
              WHEN '3'                                                          
              WHEN '4'                                                          
                 ADD +1                 TO WS-SHORTLIST-PRIO-CAT3               
                 MOVE WS-SHORTLIST-PRIO-CAT3                                    
                                        TO F520-NUM-SHORTLIST-PRIO-CAT          
           END-EVALUATE.                                                        
                                                                                
           WRITE BP13F500-REC        FROM BP13F520-REC.                         
           ADD +1                    TO   WS-F500-WRITE-CTR.                    
                                                                                
           PERFORM 2000-READ-F520    THRU 2000-EXIT.                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-FILES.                                                        
      ****************************************************************          
                                                                                
           CLOSE BP13F520                                                       
                 BP13F500.                                                      
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-SYSTEM-DATE.                 
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13C821 CONTROL TOTALS============*'.          
           DISPLAY 'RUN DATE : ' WS-SYSTEM-DATE (7:2) '/'                       
                                 WS-SYSTEM-DATE (5:2) '/'                       
                                 WS-SYSTEM-DATE (1:4).                          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13F520 RECORDS READ      = ' WS-F520-READ-CTR.            
           DISPLAY 'BP13F500 RECORDS WRITTEN   = ' WS-F500-WRITE-CTR.           
           DISPLAY SPACES.                                                      
           DISPLAY '*======================================*'.                  
                                                                                
           STOP RUN.                                                            
                                                                                
