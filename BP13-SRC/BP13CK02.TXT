       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CK02.                                                 
      *AUTHOR.        CHYEO.                                                    
      *DATE-WRITTEN.  03/05/2006.                                               
      * ====================================================== *                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      * ====================================================== *                
      *                                                        *                
      *    OBJECTIVE   : IF SEPARATE RESALE 2DP AND 1 DP FLAT  *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY   DATE     DETAILS                          *                
      *--------------------------------------------------------*                
      *BP132833 YCH2 03032006 NEW PROGRAM                      *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
           SELECT P13F558A ASSIGN TO P13F558A.                                  
           SELECT P13F558B ASSIGN TO P13F558B.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F558                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F558.                                                           
                                                                                
       FD  P13F558A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  P13F558A-REC                   PIC X(600).                           
                                                                                
       FD  P13F558B                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  P13F558B-REC                   PIC X(600).                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F558-READ                PIC 9(8)    VALUE ZEROS.              
           05 WS-F558-WRITE               PIC 9(8)    VALUE ZEROS.              
           05 WS-F558A-WRITE              PIC 9(8)    VALUE ZEROS.              
           05 WS-F558B-WRITE              PIC 9(8)    VALUE ZEROS.              
           05 WS-OCCUP                    PIC 9(2)    VALUE 0.                  
           05 WS-CNT                      PIC 9(2)    VALUE 0.                  
                                                                                
       01  WS-STORE-NRIC                  PIC X(9)    VALUE SPACES.             
       01  WS-EOF-F558                    PIC 9.                                
           88 EOF-F558                    VALUE 1.                              
                                                                                
       01  WS-PREV-F558-RECORD.                                                 
           03  WS-PREV-F558-REC OCCURS 5 TIMES.                                 
           05 WS-PREV-REGN                PIC X(8).                             
           05 WS-PREV-MATURE-KEY REDEFINES WS-PREV-REGN.                        
              10 WS-PREV-REFNO            PIC X(6).                             
              10 FILLER                   PIC X(2).                             
           05 WS-PREV-NRIC                PIC X(9).                             
           05 FILLER                      PIC X(35).                            
           05 WS-PREV-DTE-FLAT-SOLD-SMS   PIC X(8).                             
           05 WS-PREV-SALE-TY-SMS         PIC X(2).                             
           05 FILLER                      PIC X(30).                            
           05 WS-PREV-HOUSEHOLD           PIC X(1).                             
           05 FILLER                      PIC X(1).                             
           05 WS-PREV-TWO-ROOMER          PIC X(1).                             
           05 FILLER                      PIC X(4).                             
           05 WS-PREV-CDE-FILE            PIC X(1).                             
           05 WS-PREV-NUM-HDB-REF         PIC X(11).                            
           05 FILLER                      PIC X(3).                             
           05 WS-PREV-FLAT-TYPE           PIC XX.                               
           05 FILLER                      PIC X(30).                            
           05 WS-PREV-ALLOC-SCH           PIC X(3).                             
           05 FILLER                      PIC X.                                
           05 FILLER                      PIC X(450).                           
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       000-MAIN-ROUTINE.                                                        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F558      THRU 200-EXIT.                            
           PERFORM 300-PROCESS-RTN    THRU 300-EXIT                             
             UNTIL EOF-F558.                                                    
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-INITIALIZATION.                                                      
                                                                                
           OPEN INPUT  BP13F558                                                 
                OUTPUT P13F558A                                                 
                       P13F558B.                                                
                                                                                
           MOVE SPACES                  TO WS-PREV-F558-RECORD.                 
           MOVE ZEROS                   TO WS-COUNTERS                          
                                           WS-EOF-F558.                         
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-F558.                                                           
                                                                                
           READ BP13F558 AT END                                                 
                MOVE HIGH-VALUE TO F558-NUM-NRIC                                
                MOVE 1          TO WS-EOF-F558                                  
                GO TO 200-EXIT.                                                 
                                                                                
           ADD  1 TO WS-F558-READ.                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PROCESS-RTN.                                                         
                                                                                
           MOVE ZEROES TO WS-OCCUP.                                             
           MOVE F558-NUM-NRIC TO WS-STORE-NRIC                                  
                                                                                
           PERFORM 305-INITIALIZE THRU 305-EXIT                                 
           VARYING WS-CNT FROM 1 BY 1                                           
             UNTIL WS-CNT > 5.                                                  
                                                                                
           MOVE 0 TO WS-CNT.                                                    
           PERFORM 310-STORE-NRIC THRU 310-EXIT                                 
             UNTIL F558-NUM-NRIC NOT = WS-STORE-NRIC                            
               OR  EOF-F558.                                                    
                                                                                
           IF WS-OCCUP = 1                                                      
              PERFORM 500-WRITE-OUTPUT THRU 500-EXIT                            
           ELSE                                                                 
              PERFORM 510-WRITE-F558B THRU 510-EXIT                             
                VARYING WS-CNT FROM 1 BY 1                                      
                  UNTIL WS-CNT > WS-OCCUP                                       
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       305-INITIALIZE.                                                          
           MOVE SPACES TO WS-PREV-F558-REC(WS-CNT).                             
                                                                                
       305-EXIT.                                                                
           EXIT.                                                                
                                                                                
       310-STORE-NRIC.                                                          
                                                                                
           ADD 1 TO WS-CNT.                                                     
           MOVE BP13F558-REC  TO WS-PREV-F558-REC(WS-CNT).                      
           ADD 1  TO WS-OCCUP.                                                  
                                                                                
           PERFORM 200-READ-F558      THRU 200-EXIT.                            
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-WRITE-OUTPUT.                                                        
           MOVE WS-PREV-F558-REC(WS-CNT) TO P13F558A-REC.                       
           WRITE P13F558A-REC.                                                  
           ADD  1  TO WS-F558A-WRITE.                                           
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       510-WRITE-F558B.                                                         
           MOVE WS-PREV-F558-REC(WS-CNT) TO P13F558B-REC.                       
           WRITE P13F558B-REC                                                   
           ADD  1  TO WS-F558B-WRITE.                                           
                                                                                
       510-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       999-CLOSE-FILES.                                                         
           CLOSE BP13F558                                                       
                 P13F558A                                                       
                 P13F558B.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13CK02 ================*'.              
           DISPLAY '    NO. OF F558 RECORDS READ     = ' WS-F558-READ.          
           DISPLAY '    NO. OF F558 RECORDS WRITTEN  = ' WS-F558-WRITE.         
           DISPLAY '    NO. OF F558A WRITE           = ' WS-F558A-WRITE.        
           DISPLAY '    NO. OF F558B WRITE           = ' WS-F558B-WRITE.        
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
        999-EXIT.                                                               
            EXIT.                                                               
