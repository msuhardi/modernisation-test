       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C31J.                                                 
       AUTHOR.        KSJ3.                                                     
       DATE-WRITTEN.  23/08/2016.                                               
      * ======================================================== *              
      *                SYSTEM OF COMMITMENT  (BP13)              *              
      * ======================================================== *              
      *                                                          *              
      *    OBJECTIVE   :  TO CHECK AMOUNT CONSISTENCY IN BP13K800*              
      *                                                          *              
      *    INPUT FILE  :  BP13F300                               *              
      *                                                          *              
      *    OUTPUT FILE :  BP13L31J                               *              
      *                                                          *              
      * ======================================================== *              
      * CHG-NO   BY  DATE   DESCRIPTION                          *              
      * -------- --- ------ -----------                          *              
      * BP136402 KSJ 230818 NEW PROGRAM                          *              
      * ======================================================== *              
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F300 ASSIGN        TO BP13F300.                           
                                                                                
           SELECT BP13K800 ASSIGN        TO BP13K800                            
                           ACCESS MODE   IS RANDOM                              
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K800-NUM-REGN                       
                           FILE STATUS   IS WS-K800-STATUS.                     
                                                                                
           SELECT BP13K022 ASSIGN        TO BP13K022                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K022-KEY-FLD                        
                           ALTERNATE KEY IS K022-NUM-REGN                       
                           FILE STATUS   IS WS-K022-STATUS.                     
                                                                                
           SELECT SY08K010 ASSIGN        TO SY08K010                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS RANDOM                              
                           RECORD KEY    IS K010-NUM-USERID                     
                           ALTERNATE KEY IS K010-NUM-LOTUS-NCKNM                
                           FILE STATUS   IS WS-K010-STATUS.                     
                                                                                
           SELECT BP13L31J ASSIGN        TO BP13L31J.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F300                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL RECORD IS STANDARD.                                            
           COPY BP13F300.                                                       
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
           COPY BP13K800.                                                       
                                                                                
       FD  BP13K022                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
           COPY BP13K022.                                                       
                                                                                
       FD  SY08K010                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
           COPY SY08K010.                                                       
                                                                                
       FD  BP13L31J                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  BP13L31J-REC               PIC X(132).                               
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K800-STATUS         PIC 9(2)    VALUE ZEROES.                 
           05  WS-K010-STATUS         PIC 9(2)    VALUE ZEROES.                 
           05  WS-K022-STATUS         PIC 9(2)    VALUE ZEROES.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F300-READ       PIC 9(4)    VALUE ZEROS.                  
           05  WS-CNT-L31J-PRINT      PIC 9(4)    VALUE ZEROS.                  
                                                                                
       01  WS-FLAG-VARIABLES.                                                   
           05  WS-K800-FND            PIC X(1)    VALUE 'N'.                    
           05  WS-K010-FND            PIC X(1)    VALUE 'N'.                    
           05  WS-K022-FND            PIC X(1)    VALUE 'N'.                    
           05  WS-F300-EOF            PIC X(1)    VALUE 'N'.                    
           05  WS-HDR-PRINTED         PIC X(1)    VALUE 'N'.                    
           05  WS-TALLY-OK            PIC X(1)    VALUE 'N'.                    
                                                                                
       01  WS-DATES-VARIABLES.                                                  
           05  WS-CURRENT-DATE.                                                 
               10 WS-CUR-CCYY         PIC 9(4).                                 
               10 WS-CUR-MM           PIC 99.                                   
               10 WS-CUR-DD           PIC 99.                                   
           05  WS-EDITED-DATE.                                                  
               10 WS-CUR-DD           PIC 99.                                   
               10 FILLER              PIC X       VALUE '/'.                    
               10 WS-CUR-MM           PIC 99.                                   
               10 FILLER              PIC X       VALUE '/'.                    
               10 WS-CUR-CCYY         PIC 9(4).                                 
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-RPT-STATUS          PIC X(4)    VALUE SPACES.                 
           05  WS-NUM-CRM             PIC X(5)    VALUE SPACES.                 
           05  WS-NME-CRM             PIC X(20)   VALUE SPACES.                 
           05  WS-AMT-CONVEY          PIC 9(7)V99 VALUE ZEROES.                 
           05  WS-AMT-CD              PIC 9(7)V99 VALUE ZEROES.                 
           05  WS-AMT-SOC-PAID        PIC 9(7)V99 VALUE ZEROES.                 
                                                                                
      *-------------------------------------------------------------            
      *    EMAIL REPORT COMMANDS                                                
      *-------------------------------------------------------------            
       01  MAIL-COMMANDS.                                                       
           05  WS-MAIL-HELO           PIC X(132)  VALUE                         
               'HELO SGPHDB1'.                                                  
           05  WS-MAIL-MAIL-FROM      PIC X(132)  VALUE                         
               'MAIL FROM:<OPCP@SGPHDB1>'.                                      
           05  WS-MAIL-RCPT-TO-1      PIC X(132)  VALUE                         
               'RCPT TO:<LOW_SOO_CHIEW@HDB.GOV.SG>'.                            
           05  WS-MAIL-RCPT-TO-2      PIC X(132)  VALUE                         
               'RCPT TO:<TAN_SIEW_LAY@HDB.GOV.SG>'.                             
           05  WS-MAIL-DATA           PIC X(132)  VALUE 'DATA'.                 
           05  WS-MAIL-FROM           PIC X(132)  VALUE                         
               'FROM:Soc System - Email Alert'.                                 
           05  WS-MAIL-TO-1           PIC X(132)  VALUE                         
               'TO:<LOW_SOO_CHIEW@HDB.GOV.SG>'.                                 
           05  WS-MAIL-TO-2           PIC X(132)  VALUE                         
               'TO:<TAN_SIEW_LAY@HDB.GOV.SG>'.                                  
           05  WS-MAIL-SUBJECT.                                                 
               10  FILLER             PIC X(132)  VALUE                         
                   'SUBJECT : Bank Loan cases not in order'.                    
           05  WS-MAIL-DATE.                                                    
               10  FILLER             PIC X(6)    VALUE                         
                   'DATE: '.                                                    
               10  WS-MAIL-DTE-CURR   PIC X(10)   VALUE SPACES.                 
           05  WS-MAIL-BODY.                                                    
               10  FILLER             PIC X(132)  VALUE                         
                   'Bank Loan cases not in order, please do not schedule        
      -            ' for TP. Thanks.'.                                          
                                                                                
      *-------------------------------------------------------------            
      *    REPORT LAYOUT                                                        
      *-------------------------------------------------------------            
       01  L31J-PR-HEAD-01.                                                     
           05  FILLER                 PIC X(14)   VALUE 'BP13L31J'.             
           05  FILLER                 PIC X(8)    VALUE 'HDB3    '.             
           05  FILLER                 PIC X(25)   VALUE SPACES.                 
           05  FILLER                 PIC X(39)   VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                 PIC X(13)   VALUE SPACES.                 
           05  FILLER                 PIC X(7)    VALUE 'DATE: '.               
           05  PRINT-CURRENT-DATE     PIC X(10)   VALUE SPACES.                 
           05  FILLER                 PIC X(3)    VALUE SPACES.                 
           05  FILLER                 PIC X(7)    VALUE 'PAGE: '.               
           05  PRINT-PAGE             PIC ZZZ9.                                 
                                                                                
       01  L31J-PR-HEAD-02.                                                     
           05  FILLER                 PIC X(58)   VALUE SPACES.                 
           05  FILLER                 PIC X(14)   VALUE                         
              'AGREEMENT TEAM'.                                                 
           05  FILLER                 PIC X(60)   VALUE SPACES.                 
                                                                                
       01  L31J-PR-HEAD-03.                                                     
           05  FILLER                 PIC X(34)   VALUE SPACES.                 
           05  FILLER                 PIC X(63)   VALUE                         
              'DETAIL LIST OF RAISED AGREEMENT ORDER WITH INCONSISTENT A        
      -       'MOUNTS'.                                                         
                                                                                
       01  L31J-PR-TOTAL-LINE.                                                  
           05  FILLER                 PIC X(45)   VALUE SPACES.                 
           05  FILLER                 PIC X(31)                                 
           VALUE 'TOTAL NO. OF RECORDS PRINTED = '.                             
           05  L31J-TOTAL-REC         PIC ZZZ9.                                 
           05  FILLER                 PIC X(56)   VALUE SPACES.                 
                                                                                
       01  L31J-PR-ASTERICK-LINE.                                               
           05  FILLER                 PIC X(56)   VALUE SPACES.                 
           05  FILLER                 PIC X(15)   VALUE ALL '*'.                
           05  FILLER                 PIC X(61)   VALUE SPACES.                 
                                                                                
       01  L31J-PR-TITLE-1.                                                     
           05  FILLER                 PIC X(132)  VALUE                         
              'REGN       USERID    CD AMT       CONVEY FEE   SOC PAID          
      -       '   STATUS                                     CRM'.              
                                                                                
       01  L31J-PR-TITLE-2.                                                     
           05  FILLER                 PIC X(132)  VALUE                         
              '--------   -------   ----------   ----------   ----------        
      -       '   ----------------------------------------   -----------        
      -       '---------------'.                                                
                                                                                
       01  L31J-PR-DTL.                                                         
           05  L31J-NUM-REGN          PIC X(8).                                 
           05  FILLER                 PIC X(3)    VALUE SPACES.                 
           05  L31J-NUM-USERID        PIC X(7).                                 
           05  FILLER                 PIC X(3)    VALUE SPACES.                 
           05  L31J-AMT-CD            PIC ZZZ,ZZ9.99.                           
           05  FILLER                 PIC X(3)    VALUE SPACES.                 
           05  L31J-AMT-CONVEY        PIC ZZZ,ZZ9.99.                           
           05  FILLER                 PIC X(3)    VALUE SPACES.                 
           05  L31J-AMT-SOC-PAID      PIC ZZZ,ZZ9.99.                           
           05  FILLER                 PIC X(3)    VALUE SPACES.                 
           05  L31J-NUM-STATUS        PIC X(40).                                
           05  FILLER                 PIC X(3)    VALUE SPACES.                 
           05  L31J-NUM-CRM           PIC X(5).                                 
           05  FILLER                 PIC X(1)    VALUE '-'.                    
           05  L31J-NME-CRM           PIC X(20).                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ****************************************************************          
       MAIN-ROUTINE SECTION.                                                    
      ****************************************************************          
                                                                                
           PERFORM 100-OPEN-ROUTINE        THRU 100-EXIT.                       
           PERFORM 200-MAIN-ROUTINE        THRU 200-EXIT                        
                   UNTIL WS-F300-EOF = 'Y'.                                     
           PERFORM 900-CLOSE-ROUTINE       THRU 900-EXIT.                       
                                                                                
       MAIN-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       100-OPEN-ROUTINE.                                                        
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F300                                                 
                       BP13K800                                                 
                       BP13K022                                                 
                       SY08K010                                                 
                OUTPUT BP13L31J.                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO   RETURN-CODE                     
              PERFORM  900-CLOSE-ROUTINE   THRU 900-EXIT                        
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K022 ERROR ' WS-K022-STATUS                  
              MOVE     WS-K022-STATUS      TO   RETURN-CODE                     
              PERFORM  900-CLOSE-ROUTINE   THRU 900-EXIT                        
           END-IF.                                                              
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING SY08K010 ERROR ' WS-K010-STATUS                  
              MOVE     WS-K010-STATUS      TO   RETURN-CODE                     
              PERFORM  900-CLOSE-ROUTINE   THRU 900-EXIT                        
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       200-MAIN-ROUTINE.                                                        
      ****************************************************************          
                                                                                
           READ BP13F300                                                        
                AT END MOVE 'Y'            TO   WS-F300-EOF                     
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1                           TO   WS-CNT-F300-READ.               
                                                                                
           MOVE SPACES                     TO   WS-RPT-STATUS                   
                                                WS-NUM-CRM                      
                                                WS-NME-CRM.                     
                                                                                
      *---------------------------------------------------------------          
      *    RETRIEVE AMOUNTS FROM SOC MASTER                                     
      *     - COMPARE AMT-CD VS SOC-CASH                                        
      *     - IF EQUAL, SKIP RECORD.                                            
      *     - IF SOC-DEBTOR > 0, SKIP RECORD.                                   
      *---------------------------------------------------------------          
           MOVE SPACES                     TO   BP13K800-MASTER.                
           MOVE F300-CUR-REG               TO   K800-NUM-REGN.                  
           PERFORM 210-READ-BP13K800       THRU 210-EXIT.                       
                                                                                
           IF WS-K800-FND NOT = 'Y'                                             
              MOVE 'NFND'                  TO   WS-RPT-STATUS                   
           ELSE                                                                 
              MOVE 'Y'                     TO   WS-TALLY-OK                     
              PERFORM 220-CHECK-AMTS       THRU 220-EXIT                        
                                                                                
              IF WS-TALLY-OK = 'Y'                                              
                 GO TO 200-EXIT                                                 
              END-IF                                                            
                                                                                
              MOVE 'AMTS'                  TO   WS-RPT-STATUS                   
           END-IF.                                                              
                                                                                
      *---------------------------------------------------------------          
      *    GET CRM ID AND NAME                                                  
      *---------------------------------------------------------------          
           MOVE SPACES                     TO   BP13K022-REC.                   
           MOVE F300-CUR-REG               TO   K022-NUM-REGN.                  
           PERFORM 230-READ-BP13K022       THRU 230-EXIT.                       
                                                                                
           IF (WS-K022-FND = 'Y') AND                                           
              (K022-NUM-OIC-APPL NOT = SPACES AND LOW-VALUES)                   
              MOVE K022-NUM-OIC-APPL       TO   WS-NUM-CRM                      
                                                                                
              MOVE K022-NUM-OIC-APPL       TO   K010-NUM-LOTUS-NCKNM            
              PERFORM 240-READ-SY08K010    THRU 240-EXIT                        
                                                                                
              IF WS-K010-FND = 'Y'                                              
                 MOVE K010-NME-USER        TO   WS-NME-CRM                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *---------------------------------------------------------------          
      *    PRINT REPORT                                                         
      *---------------------------------------------------------------          
           PERFORM 300-PRINT-REPORT        THRU 300-EXIT.                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       210-READ-BP13K800.                                                       
      ****************************************************************          
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN ZEROES                                                          
              MOVE 'Y'                     TO   WS-K800-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K800-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K800, STATUS = ' WS-K800-STATUS        
              MOVE    WS-K800-STATUS       TO   RETURN-CODE                     
              PERFORM 900-CLOSE-ROUTINE    THRU 900-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       210-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       220-CHECK-AMTS.                                                          
      ****************************************************************          
                                                                                
           IF K800-AMT-SOC-DEBTOR NOT NUMERIC                                   
              MOVE ZEROES                  TO   K800-AMT-SOC-DEBTOR.            
                                                                                
           If K800-AMT-SOC-DEBTOR > 0                                           
              GO TO 220-EXIT.                                                   
                                                                                
           IF K800-AMT-CD NOT NUMERIC                                           
              MOVE ZEROES                  TO   K800-AMT-CD.                    
                                                                                
           IF K800-AMT-CONVEY-FEE NOT NUMERIC                                   
              MOVE ZEROES                  TO   K800-AMT-CONVEY-FEE.            
                                                                                
           IF K800-AMT-CONVEY-FEE-GST NOT NUMERIC                               
              MOVE ZEROES                  TO   K800-AMT-CONVEY-FEE-GST.        
                                                                                
           IF K800-AMT-SOC-CASH NOT NUMERIC                                     
              MOVE ZEROES                  TO   K800-AMT-SOC-CASH.              
                                                                                
           IF K800-AMT-CPF1-PAID NOT NUMERIC                                    
              MOVE ZEROES                  TO   K800-AMT-CPF1-PAID.             
                                                                                
           IF K800-AMT-CPF2-PAID NOT NUMERIC                                    
              MOVE ZEROES                  TO   K800-AMT-CPF2-PAID.             
                                                                                
           IF K800-AMT-CPF3-PAID NOT NUMERIC                                    
              MOVE ZEROES                  TO   K800-AMT-CPF3-PAID.             
                                                                                
           IF K800-AMT-CPF4-PAID NOT NUMERIC                                    
              MOVE ZEROES                  TO   K800-AMT-CPF4-PAID.             
                                                                                
           COMPUTE WS-AMT-CONVEY   = K800-AMT-CONVEY-FEE +                      
                                     K800-AMT-CONVEY-FEE-GST.                   
                                                                                
           COMPUTE WS-AMT-CD       = K800-AMT-CD        +                       
                                     WS-AMT-CONVEY.                             
                                                                                
           COMPUTE WS-AMT-SOC-PAID = K800-AMT-SOC-CASH  +                       
                                     K800-AMT-CPF1-PAID +                       
                                     K800-AMT-CPF2-PAID +                       
                                     K800-AMT-CPF3-PAID +                       
                                     K800-AMT-CPF4-PAID.                        
                                                                                
           IF WS-AMT-CD NOT = WS-AMT-SOC-PAID                                   
              MOVE 'N'                     TO   WS-TALLY-OK                     
           END-IF.                                                              
                                                                                
       220-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       230-READ-BP13K022.                                                       
      ****************************************************************          
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K022-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              MOVE 'Y'                     TO   WS-K022-FND                     
                                                                                
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K022-FND                     
                                                                                
           WHEN OTHER                                                           
              MOVE WS-K022-STATUS          TO   RETURN-CODE                     
              DISPLAY 'ERROR READING BP13K022, STATUS = ' WS-K022-STATUS        
              PERFORM 900-CLOSE-ROUTINE    THRU 900-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       230-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       240-READ-SY08K010.                                                       
      ****************************************************************          
                                                                                
           READ SY08K010 KEY IS K010-NUM-LOTUS-NCKNM.                           
                                                                                
           EVALUATE WS-K010-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              MOVE 'Y'                     TO   WS-K010-FND                     
                                                                                
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K010-FND                     
                                                                                
           WHEN OTHER                                                           
              MOVE WS-K010-STATUS          TO   RETURN-CODE                     
              DISPLAY 'ERROR READING SY08K010, STATUS = ' WS-K010-STATUS        
              PERFORM 900-CLOSE-ROUTINE    THRU 900-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       240-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       300-PRINT-REPORT.                                                        
      ****************************************************************          
                                                                                
           IF WS-HDR-PRINTED NOT = 'Y'                                          
              PERFORM 310-PRINT-REPORT-HDR THRU 310-EXIT                        
           END-IF.                                                              
                                                                                
           MOVE F300-CUR-REG               TO   L31J-NUM-REGN                   
           MOVE F300-USERID                TO   L31J-NUM-USERID                 
                                                                                
           IF WS-RPT-STATUS = 'NFND'                                            
              MOVE ZEROES                  TO   L31J-AMT-CD                     
                                                L31J-AMT-CONVEY                 
                                                L31J-AMT-SOC-PAID               
              MOVE 'Regn not found in SOC Master'                               
                                           TO   L31J-NUM-STATUS                 
           END-IF.                                                              
                                                                                
           IF WS-RPT-STATUS = 'AMTS'                                            
              MOVE K800-AMT-CD             TO   L31J-AMT-CD                     
              MOVE WS-AMT-CONVEY           TO   L31J-AMT-CONVEY                 
              MOVE WS-AMT-SOC-PAID         TO   L31J-AMT-SOC-PAID               
              MOVE 'CD + CF not tally with SOC Paid'                            
                                           TO   L31J-NUM-STATUS                 
           END-IF.                                                              
                                                                                
           MOVE WS-NUM-CRM                 TO   L31J-NUM-CRM.                   
           MOVE WS-NME-CRM                 TO   L31J-NME-CRM.                   
                                                                                
           WRITE BP13L31J-REC              FROM L31J-PR-DTL.                    
           ADD 1                           TO   WS-CNT-L31J-PRINT.              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       310-PRINT-REPORT-HDR.                                                    
      ****************************************************************          
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-CURRENT-DATE.                
           MOVE CORR WS-CURRENT-DATE       TO   WS-EDITED-DATE.                 
           MOVE WS-EDITED-DATE             TO   WS-MAIL-DTE-CURR.               
                                                                                
           WRITE BP13L31J-REC              FROM WS-MAIL-HELO.                   
           WRITE BP13L31J-REC              FROM WS-MAIL-MAIL-FROM.              
           WRITE BP13L31J-REC              FROM WS-MAIL-RCPT-TO-1.              
           WRITE BP13L31J-REC              FROM WS-MAIL-RCPT-TO-2.              
           WRITE BP13L31J-REC              FROM WS-MAIL-DATA.                   
           WRITE BP13L31J-REC              FROM WS-MAIL-FROM.                   
           WRITE BP13L31J-REC              FROM WS-MAIL-TO-1.                   
           WRITE BP13L31J-REC              FROM WS-MAIL-TO-2.                   
           WRITE BP13L31J-REC              FROM WS-MAIL-SUBJECT.                
           WRITE BP13L31J-REC              FROM WS-MAIL-DATE.                   
           WRITE BP13L31J-REC              FROM WS-MAIL-BODY.                   
                                                                                
           MOVE SPACES                     TO   BP13L31J-REC.                   
           WRITE BP13L31J-REC.                                                  
                                                                                
           MOVE WS-EDITED-DATE             TO   PRINT-CURRENT-DATE.             
           MOVE 1                          TO   PRINT-PAGE.                     
                                                                                
           WRITE BP13L31J-REC              FROM L31J-PR-HEAD-01.                
           WRITE BP13L31J-REC              FROM L31J-PR-HEAD-02.                
           WRITE BP13L31J-REC              FROM L31J-PR-HEAD-03.                
                                                                                
           MOVE SPACES                     TO   BP13L31J-REC.                   
           WRITE BP13L31J-REC.                                                  
                                                                                
           WRITE BP13L31J-REC              FROM L31J-PR-TITLE-1.                
           WRITE BP13L31J-REC              FROM L31J-PR-TITLE-2.                
                                                                                
           MOVE SPACES                     TO   BP13L31J-REC.                   
           WRITE BP13L31J-REC.                                                  
                                                                                
           MOVE 'Y'                        TO   WS-HDR-PRINTED.                 
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       900-CLOSE-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           IF WS-HDR-PRINTED = 'Y'                                              
              MOVE SPACES                  TO   BP13L31J-REC                    
              WRITE BP13L31J-REC                                                
                                                                                
              WRITE BP13L31J-REC           FROM L31J-PR-ASTERICK-LINE           
                                                                                
              MOVE SPACES                  TO   BP13L31J-REC                    
              WRITE BP13L31J-REC                                                
                                                                                
              MOVE WS-CNT-L31J-PRINT       TO   L31J-TOTAL-REC                  
              WRITE BP13L31J-REC           FROM L31J-PR-TOTAL-LINE              
           END-IF.                                                              
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*------- BP13C31J CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1.  NO OF BP13F300 READ      : ' WS-CNT-F300-READ.         
           DISPLAY ' 2.  NO OF BP13L31J PRINTED   : ' WS-CNT-L31J-PRINT.        
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13F300                                                       
                 BP13K800                                                       
                 BP13K022                                                       
                 SY08K010                                                       
                 BP13L31J.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K022 ERROR ' WS-K022-STATUS                  
              MOVE     WS-K022-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING SY08K010 ERROR ' WS-K010-STATUS                  
              MOVE     WS-K010-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
