       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C012.                                                 
      *AUTHOR.        ED.                                                       
      *DATE-WRITTEN.  09NOV2001.                                                
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)           *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES :                                        *                
      *                                                        *                
      *    1. MATCHING PROGRAM FOR APPLICANTS WITH MULTIPLE    *                
      *       REFNO/REGNO.                                     *                
      *                                                        *                
      *    INPUT  FILES : BP13F820 - OCCUPIER FILE             *                
      *                   BP13K800 - SOC MASTER                *                
      *                   BP13K730 - REQ FILE                  *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY    DATE    DETAILS                          *                
      * ======  ==   ======== ================================ *                
      *BP131064 ED   20011109 - NEW PRGM                       *                
      *BP131064 ED   20020123 - DO NOT PRINT CANCELLED CASE    *                
      *BP132200 CKK  20021112 - DO NOT ALLOW 200201 TO GO THRU *                
      *BP132396 SK   20030830 - OMIT SERS CASES                *                
      *BP132583 SD10 20040826 - MODIFY CHECKING OF SERS CASE   *                
      *BP132818 SD10 20060209 - CHANGE REFNO FROM 6 TO 8 BYTES *                
      *BP133010 CKK7 20070402 - INCLUDE REQUEST TYPE           *                
      *BP133292 YCH2 20081110 - 'RS' FOR DUAL APPLICATION      *                
      *                         FOUND IN REQUEST AND SOC       *                
      *                         MASTER WITH BOOKED CASES       *                
      *BP133484 YCH2 20090109 - RE-ORG THE PROGRAM DROP        *                
      *                         REPORTS                        *                
      *BP137048 SMR2 20171116 - BYPASS NPL AND SERS CASES WHEN *                
      *                         UPDATING ELIG-TAG TO 'RR'      *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F820 ASSIGN TO BP13F820.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE IS DYNAMIC                                        
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K800-NUM-REGN                                   
                  FILE STATUS IS WS-K800-STATUS.                                
                                                                                
           SELECT BP13K730 ASSIGN TO BP13K730                                   
                  ACCESS MODE   IS RANDOM                                       
                  ORGANIZATION  IS INDEXED                                      
                  RECORD KEY    IS K730-KEY-NEW                                 
                  FILE STATUS   IS WS-K730-STATUS.                              
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F820                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13F820.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K730                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K730.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F820-EOF-SW                 PIC X(1)  VALUE 'N'.              
           05  WS-K800-STATUS                 PIC 9(2)  VALUE 0.                
           05  WS-K730-STATUS                 PIC 9(2)  VALUE 0.                
                                                                                
       01  WS-RECORD-COUNTERS.                                                  
           05  WS-CNT-READ-F820               PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-READ-K800               PIC 9(6)  VALUE ZEROS.            
           05  WS-K730-UPDATE                 PIC 9(6)  VALUE ZEROS.            
                                                                                
       01  WS-MISCELLANEOUS-VARS.                                               
           05  WS-PREV-NRIC                   PIC X(9)   VALUE SPACES.          
           05  WS-BLANKS                      PIC X(132) VALUE SPACES.          
           05  WS-CTR                         PIC 9(2)   VALUE 0.               
           05  WS-ARR-CTR                     PIC 9(2)   VALUE 0.               
           05  WS-REFNO-CTR                   PIC 9(2)   VALUE 0.               
           05  WS-REGNO-CTR                   PIC 9(2)   VALUE 0.               
           05  WS-SL-TAG                      PIC X      VALUE 'N'.             
                                                                                
       01  WS-ARRAY.                                                            
           05  WS-F820-ARR                    OCCURS 15 TIMES.                  
               10  WS-REFNO-REGNO             PIC X(08).                        
               10  WS-NRIC                    PIC X(09).                        
               10  WS-REQ                     PIC X(02).                        
                                                                                
       01  WS-TIME-DATES.                                                       
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY.                                                 
                   15  WS-SYS-CC              PIC 9(2).                         
                   15  WS-SYS-YY              PIC 9(2).                         
               10  WS-SYS-MM                  PIC 9(2).                         
               10  WS-SYS-DD                  PIC 9(2).                         
           05  WS-EDITED-DATE.                                                  
               10  WS-EDITED-DD               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-MM               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-YY               PIC 9(4).                         
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
           PERFORM 2000-READ-F820         THRU 2000-EXIT.                       
           PERFORM 3000-PROCESS-F820      THRU 3000-EXIT                        
             UNTIL (WS-F820-EOF-SW = 'Y').                                      
           PERFORM 9999-CLOSE-ROUTINE.                                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F820                                                 
                       BP13K800                                                 
                I-O    BP13K730.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-SYSTEM-DATE.                 
                                                                                
           MOVE   WS-SYS-DD           TO WS-EDITED-DD.                          
           MOVE   WS-SYS-MM           TO WS-EDITED-MM.                          
           MOVE   WS-SYS-CCYY         TO WS-EDITED-YY.                          
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 OPEN ERROR, STATUS IS ' WS-K800-STATUS          
              GO TO   9999-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
           IF WS-K730-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K730 OPEN ERROR, STATUS IS ' WS-K730-STATUS          
              GO TO   9999-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       2000-READ-F820.                                                          
      ****************************************************************          
                                                                                
           READ BP13F820                                                        
              AT END MOVE 'Y'   TO WS-F820-EOF-SW                               
                     GO TO 2000-EXIT.                                           
                                                                                
           ADD  +1                 TO WS-CNT-READ-F820.                         
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3000-PROCESS-F820.                                                       
      ****************************************************************          
                                                                                
           MOVE ZEROES        TO WS-ARR-CTR.                                    
           MOVE ZEROES        TO WS-REFNO-CTR                                   
                                 WS-REGNO-CTR.                                  
           MOVE 'N'           TO WS-SL-TAG.                                     
                                                                                
           MOVE F820-NUM-NRIC TO WS-PREV-NRIC.                                  
                                                                                
           PERFORM 3050-INITIALIZE   THRU 3050-EXIT                             
            VARYING WS-CTR FROM 1 BY 1                                          
              UNTIL WS-CTR > 15.                                                
                                                                                
           PERFORM 3100-SAVE-ARR     THRU 3100-EXIT                             
             UNTIL F820-NUM-NRIC NOT = WS-PREV-NRIC                             
              OR   WS-F820-EOF-SW = 'Y'                                         
                                                                                
           IF WS-REFNO-CTR >= 1                                                 
              PERFORM 3200-NRIC-BRK     THRU 3200-EXIT                          
                      VARYING WS-CTR FROM 1 BY 1                                
                        UNTIL WS-CTR > WS-ARR-CTR                               
                                                                                
              PERFORM 3500-READ-K730    THRU 3500-EXIT                          
                      VARYING WS-CTR FROM 1 BY 1 UNTIL                          
                              WS-CTR > WS-ARR-CTR                               
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3050-INITIALIZE.                                                         
      ****************************************************************          
           MOVE SPACES TO WS-F820-ARR(WS-CTR).                                  
                                                                                
       3050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3100-SAVE-ARR.                                                           
      ****************************************************************          
                                                                                
           ADD  1                 TO WS-ARR-CTR.                                
                                                                                
           MOVE F820-NUM-REGN     TO WS-REFNO-REGNO(WS-ARR-CTR).                
           MOVE F820-NUM-NRIC     TO WS-NRIC(WS-ARR-CTR).                       
                                                                                
           IF F820-NUM-DP-TAG = 'RQ'                                            
              ADD  1                 TO WS-REFNO-CTR                            
              MOVE F820-NUM-DP-TAG   TO WS-REQ(WS-ARR-CTR)                      
           ELSE                                                                 
              ADD  1                 TO WS-REGNO-CTR                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F820         THRU 2000-EXIT.                       
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3200-NRIC-BRK.                                                           
      ****************************************************************          
                                                                                
           IF WS-REQ (WS-CTR) = 'RQ'                                            
              GO TO 3200-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF WS-REGNO-CTR >= 1                                                 
              PERFORM 3300-CHECK-K800 THRU 3300-EXIT                            
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3300-CHECK-K800.                                                         
      ****************************************************************          
                                                                                
           MOVE WS-REFNO-REGNO(WS-CTR)         TO K800-NUM-REGN.                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
            WHEN  00                                                            
                  IF K800-NUM-STATUS NOT = 'C' AND                              
                     (K800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES)             
                      MOVE 'Y'       TO WS-SL-TAG                               
                  END-IF                                                        
            WHEN  23                                                            
                  DISPLAY 'RECORD NOT FOUND IN BP13K800. KEY IS '               
                          WS-REFNO-REGNO(WS-CTR)                                
            WHEN  OTHER                                                         
                  DISPLAY 'ERROR READING BP13K800. STATUS IS '                  
                          WS-K800-STATUS                                        
                  GO TO 9999-CLOSE-ROUTINE                                      
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3500-READ-K730.                                                          
      ****************************************************************          
                                                                                
           IF WS-REQ(WS-CTR) = SPACES OR LOW-VALUES                             
              GO TO 3500-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE WS-REFNO-REGNO(WS-CTR)      TO K730-NUM-REGN.                   
                                                                                
           READ BP13K730.                                                       
                                                                                
           EVALUATE WS-K730-STATUS                                              
             WHEN 00                                                            
                  PERFORM 3510-UPD-K730       THRU 3510-EXIT                    
             WHEN 23                                                            
                  DISPLAY 'RECORD NOT FOUND IN BP13K730. KEY IS '               
                          WS-REFNO-REGNO(WS-CTR)                                
             WHEN OTHER                                                         
                  DISPLAY 'ERROR READING BP13K730. STATUS IS '                  
                          WS-K730-STATUS                                        
                  GO TO 9999-CLOSE-ROUTINE                                      
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3510-UPD-K730.                                                           
      ****************************************************************          
                                                                                
           IF K730-CDE-ELIG-TAG = SPACES OR LOW-VALUES                          
              IF WS-SL-TAG = 'Y'                                                
                 IF WS-REFNO-CTR > 1                                            
                    MOVE 'RS'            TO K730-CDE-ELIG-TAG                   
                 ELSE                                                           
                    MOVE 'SL'            TO K730-CDE-ELIG-TAG                   
                 END-IF                                                         
              ELSE                                                              
                 IF K730-NUM-ALLO-CAT = 'NPL' OR 'SER'                          
                    CONTINUE                                                    
                 ELSE                                                           
                    IF (WS-REFNO-CTR > 1)                                       
                       MOVE 'RR'         TO K730-CDE-ELIG-TAG                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              IF (K730-CDE-ELIG-TAG = 'RS')                                     
                 CONTINUE                                                       
              ELSE                                                              
                 IF (K730-CDE-ELIG-TAG = 'RR' AND WS-SL-TAG = 'Y') OR           
                    (K730-CDE-ELIG-TAG = 'SL' AND WS-REFNO-CTR > 1)             
                     MOVE 'RS'            TO K730-CDE-ELIG-TAG                  
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF K730-CDE-ELIG-TAG NOT = SPACES AND LOW-VALUES                     
              REWRITE BP13K730-REC                                              
                                                                                
              EVALUATE WS-K730-STATUS                                           
              WHEN ZEROES                                                       
                 ADD 1         TO WS-K730-UPDATE                                
              WHEN 23                                                           
                 DISPLAY 'RECORD NOT FOUND IN BP13K730. KEY IS '                
                          WS-REFNO-REGNO(WS-CTR)                                
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BP13K730. STATUS IS '                   
                          WS-K730-STATUS                                        
                 GO TO 9999-CLOSE-ROUTINE                                       
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
       3510-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           CLOSE BP13F820                                                       
                 BP13K800                                                       
                 BP13K730.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'RUN DATE : ' WS-EDITED-DATE.                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13C012 CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13F820 RECORDS READ        = ' WS-CNT-READ-F820.          
           DISPLAY 'BP13K730 RECORDS UPDATED     = ' WS-K730-UPDATE.            
           DISPLAY SPACES.                                                      
           DISPLAY '*==============================================*'.          
                                                                                
           STOP RUN.                                                            
                                                                                
