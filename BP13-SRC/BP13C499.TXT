       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C499.                                                 
       AUTHOR.        JERRY FU.                                                 
      *DATE-WRITTEN.  17/12/93.                                                 
      * ====================================================== *                
      *          SYSTEM OF COMMITMENT  (BP13)                  *                
      * ====================================================== *                
      *                                                        *                
      *    OBJECTIVE   :                                       *                
      *      - EXTRACT HISTORY RECORDS WHICH CLOSING DATE >    *                
      *        1 YEAR(KEY ISSUED CASE) & 3 YEARS(CANCELLED     *                
      *        CASE)                                           *                
      *      - DELETE THE KEY ISSUED RECORDS FROM BP13K830 &   *                
      *        BP13K840 IF K840-DTE-TP > 1 YEAR OR 12 MONTHS.  *                
      *      - DELETE THE CANCELLED RECORDS FROM BP13K830 &    *                
      *        BP13K840 IF K840-DTE-CANCEL > 3 YRS OR 36 MTHS. *                
      *      - PRINT OUT CHECK LIST FOR DELETED RECORDS.       *                
      *                                                        *                
      *    INPUT/OUTPUT FILES:                                 *                
      *      1. BP13K800 (APPLICATION MASTER)                  *                
      *      2. BP13K830 (ADDRESS MASTER FILE)                 *                
      *      3. BP13K840 (HISTORY TRANSACTION LOG)             *                
      *                                                        *                
      * CHG-NO   DATE   BY   DESCRIPTION                       *                
      * -------- ------ ---  -----------                       *                
      *          951101 SSS  CHG POSITION OF LISTING ID        *                
      * BP130418 980829 CLT  Y2K CHANGES.                      *                
      * BP138865 211028 KR13 REMOVED REPORT BP13L499.          *                
      * ====================================================== *                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K800-NUM-REGN                        
                           FILE STATUS  IS K800-STATUS.                         
                                                                                
           SELECT BP13K830 ASSIGN TO BP13K830                                   
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K830-KEY-FLD                         
                           ALTERNATE RECORD KEY IS K830-NUM-REGN                
                           FILE STATUS  IS K830-STATUS.                         
                                                                                
           SELECT BP13K840 ASSIGN TO BP13K840                                   
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS SEQUENTIAL                           
                           RECORD KEY   IS K840-KEY-FLD                         
                           FILE STATUS  IS K840-STATUS.                         
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K800           RECORD CONTAINS 2000 CHARACTERS.                  
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K830           RECORD CONTAINS 250 CHARACTERS.                   
       COPY BP13K830.                                                           
                                                                                
       FD  BP13K840           RECORD CONTAINS 500 CHARACTERS.                   
       COPY BP13K840.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-SYS-TIME.                                                         
           05  WS-SYS-TIME-HH       PIC X(2).                                   
           05  WS-SYS-TIME-MM       PIC X(2).                                   
           05  WS-SYS-TIME-SS       PIC X(2).                                   
                                                                                
       01  WS-EDT-TIME.                                                         
           05  WS-EDT-TIME-HH       PIC X(2).                                   
           05  FILLER               PIC X     VALUE ':'.                        
           05  WS-EDT-TIME-MM       PIC X(2).                                   
           05  FILLER               PIC X     VALUE ':'.                        
           05  WS-EDT-TIME-SS       PIC X(2).                                   
                                                                                
       01  WS-DTE-CURRENT.                                                      
           05  WS-DTE-CUR-CCYY.                                                 
               10  WS-DTE-CUR-CC    PIC 9(2).                                   
               10  WS-DTE-CUR-YY    PIC 9(2).                                   
           05  WS-DTE-CUR-CCYY1 REDEFINES WS-DTE-CUR-CCYY   PIC 9(4).           
           05  WS-DTE-CUR-MM        PIC 9(2).                                   
           05  WS-DTE-CUR-DD        PIC 9(2).                                   
                                                                                
       01  WS-DTE-CANCEL.                                                       
           05  WS-DTE-CANCEL-CCYY.                                              
               10  WS-DTE-CANCEL-CC PIC 9(2).                                   
               10  WS-DTE-CANCEL-YY PIC 9(2).                                   
           05  WS-DTE-CANCEL-CCYY1 REDEFINES                                    
               WS-DTE-CANCEL-CCYY   PIC 9(4).                                   
           05  WS-DTE-CANCEL-MM     PIC 9(2).                                   
           05  WS-DTE-CANCEL-DD     PIC 9(2).                                   
                                                                                
       01  WS-DTE-TP.                                                           
           05  WS-DTE-TP-CCYY.                                                  
               10  WS-DTE-TP-CC PIC 9(2).                                       
               10  WS-DTE-TP-YY PIC 9(2).                                       
           05  WS-DTE-TP-CCYY1 REDEFINES                                        
               WS-DTE-TP-CCYY       PIC 9(4).                                   
           05  WS-DTE-TP-MM         PIC 9(2).                                   
           05  WS-DTE-TP-DD         PIC 9(2).                                   
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  K800-STATUS          PIC 9(2)  VALUE ZEROS.                      
           05  K830-STATUS          PIC 9(2)  VALUE ZEROS.                      
           05  K840-STATUS          PIC 9(2)  VALUE ZEROS.                      
                                                                                
       01  WS-COUNTS.                                                           
           05  WS-EOF-K840          PIC X        VALUE 'N'.                     
           05  WS-IND-K840          PIC X(2)     VALUE SPACES.                  
           05  WS-K830-DELETE-CNT   PIC 9(6)     VALUE ZEROS.                   
           05  WS-K840-DEL-TP-CNT   PIC 9(6)     VALUE ZEROS.                   
           05  WS-K840-DEL-CA-CNT   PIC 9(6)     VALUE ZEROS.                   
           05  WS-K840-DELETE-CNT   PIC 9(6)     VALUE ZEROS.                   
           05  WS-K840-READ-CNT     PIC 9(6)     VALUE ZEROS.                   
           05  WS-K800-FOUND-CNT    PIC 9(6)     VALUE ZEROS.                   
           05  WS-NUM-MONTH         PIC S9(5)    VALUE +0.                      
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *   MAIN PROCESS   *                                                      
      ********************                                                      
       0000-MAIN.                                                               
           PERFORM 1000-START-PROCESS  THRU 1000-EXIT.                          
           PERFORM 2000-DELETE-PROCESS THRU 2000-EXIT                           
                   UNTIL WS-EOF-K840 = 'Y'.                                     
           PERFORM 9000-CLOSE.                                                  
                                                                                
                                                                                
      ********************                                                      
      *   FILE OPENING   *                                                      
      ********************                                                      
       1000-START-PROCESS.                                                      
           OPEN INPUT BP13K800                                                  
                I-O   BP13K830                                                  
                      BP13K840.                                                 
                                                                                
           IF K800-STATUS NOT = 0 AND 97                                        
              DISPLAY 'OPENING ERROR K800-STATUS IS ' K800-STATUS               
              PERFORM 9000-CLOSE.                                               
                                                                                
           IF K830-STATUS NOT = 0 AND 97                                        
              DISPLAY 'OPENING ERROR K830-STATUS IS ' K830-STATUS               
              PERFORM 9000-CLOSE.                                               
                                                                                
           IF K840-STATUS NOT = 0 AND 97                                        
              DISPLAY 'OPENING ERROR K840-STATUS IS ' K840-STATUS               
              PERFORM 9000-CLOSE.                                               
                                                                                
           ACCEPT WS-SYS-TIME         FROM TIME.                                
           MOVE FUNCTION CURRENT-DATE TO   WS-DTE-CURRENT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      **************************************************************            
      *   READ BP13K840 SEQUENTIALLY & DELETE RECORD ACCORDINGLY   *            
      **************************************************************            
       2000-DELETE-PROCESS.                                                     
           READ BP13K840 AT END                                                 
                         MOVE 'Y' TO WS-EOF-K840                                
                         GO TO 2000-EXIT.                                       
           ADD 1 TO WS-K840-READ-CNT.                                           
                                                                                
           IF (K840-DTE-TP = SPACES OR LOW-VALUE) AND                           
              (K840-DTE-CANCEL = SPACES OR LOW-VALUE)                           
              GO TO 2000-EXIT.                                                  
                                                                                
           IF K840-DTE-CANCEL IS NUMERIC AND K840-DTE-CLOSE IS NUMERIC          
              IF K840-DTE-TP IS NUMERIC AND                                     
                 K840-DTE-TP > K840-DTE-CANCEL                                  
                 NEXT SENTENCE                                                  
              ELSE                                                              
                 PERFORM 3100-COMP-DEL-CANCEL THRU 3100-EXIT                    
                 GO TO 2000-EXIT                                                
           ELSE NEXT SENTENCE.                                                  
                                                                                
           IF K840-DTE-TP IS NUMERIC AND K840-DTE-CLOSE IS NUMERIC              
              PERFORM 3200-COMP-DEL-TP THRU 3200-EXIT.                          
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
      *   COMPUTE THE # OF MONTH FROM CURRENT & CANCELLED DATE *                
      **********************************************************                
       3100-COMP-DEL-CANCEL.                                                    
           MOVE K840-DTE-CANCEL TO WS-DTE-CANCEL.                               
                                                                                
           COMPUTE WS-NUM-MONTH =                                               
                   (WS-DTE-CUR-CCYY1 * 12 + WS-DTE-CUR-MM)                      
                 - (WS-DTE-CANCEL-CCYY1 * 12 + WS-DTE-CANCEL-MM).               
                                                                                
      **********************************************************                
      *   DELETE RECORDS IF NUMBER OF MONTH > OR = 36.         *                
      **********************************************************                
           IF (WS-NUM-MONTH > 36) OR                                            
              (WS-NUM-MONTH = 36 AND WS-DTE-CANCEL-DD > WS-DTE-CUR-DD)          
              MOVE 'CA' TO WS-IND-K840                                          
              PERFORM 3300-DEL-BP13K830-K840 THRU 3300-EXIT.                    
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
      *   COMPUTE THE NUMBER OF MONTH FROM CURRENT & TP DATES  *                
      **********************************************************                
       3200-COMP-DEL-TP.                                                        
           MOVE K840-DTE-TP TO WS-DTE-TP.                                       
                                                                                
           COMPUTE WS-NUM-MONTH =                                               
                   (WS-DTE-CUR-CCYY1 * 12 + WS-DTE-CUR-MM)                      
                 - (WS-DTE-TP-CCYY1 * 12 + WS-DTE-TP-MM).                       
                                                                                
           IF (WS-NUM-MONTH > 12) OR (WS-NUM-MONTH = 12 AND                     
                                       WS-DTE-TP-DD > WS-DTE-CUR-DD)            
              MOVE 'TP' TO WS-IND-K840                                          
              PERFORM 3300-DEL-BP13K830-K840 THRU 3300-EXIT.                    
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
      *   DELETE RECORDS FROM BP13K830 & BP13K840              *                
      **********************************************************                
       3300-DEL-BP13K830-K840.                                                  
           MOVE K840-NUM-REGN TO K800-NUM-REGN.                                 
           READ BP13K800.                                                       
                                                                                
           IF K800-STATUS = 0                                                   
              ADD 1 TO WS-K800-FOUND-CNT                                        
              DISPLAY 'CLOSED CASE FOUND IN MASTER & CANNOT DELETE = '          
                       K800-NUM-REGN                                            
              GO TO 3300-EXIT.                                                  
                                                                                
           DELETE BP13K840.                                                     
           IF K840-STATUS = 0                                                   
              IF WS-IND-K840 = 'TP'                                             
                 ADD 1 TO WS-K840-DEL-TP-CNT WS-K840-DELETE-CNT                 
              ELSE                                                              
                 ADD 1 TO WS-K840-DEL-CA-CNT WS-K840-DELETE-CNT                 
           ELSE                                                                 
              DISPLAY 'BP13K840 DELETION FAILED = ' K840-NUM-REGN.              
                                                                                
           MOVE K840-NUM-REGN TO K830-NUM-REGN.                                 
           READ BP13K830 KEY IS K830-NUM-REGN.                                  
           IF K830-STATUS = 0                                                   
              DELETE BP13K830                                                   
              IF K830-STATUS = 0                                                
                 ADD 1 TO WS-K830-DELETE-CNT                                    
              ELSE                                                              
                 DISPLAY 'BP13K830 DELETION FAILED = ' K830-NUM-REGN.           
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       9000-CLOSE.                                                              
      ***************************************************************           
                                                                                
           CLOSE BP13K800                                                       
                 BP13K830                                                       
                 BP13K840.                                                      
                                                                                
           IF K800-STATUS NOT = 0 AND 97                                        
              DISPLAY 'CLOSING ERROR, K800-STATUS IS ' K800-STATUS.             
                                                                                
           IF K830-STATUS NOT = 0 AND 97                                        
              DISPLAY 'CLOSING ERROR, K830-STATUS IS ' K830-STATUS.             
                                                                                
           IF K840-STATUS NOT = 0 AND 97                                        
              DISPLAY 'CLOSING ERROR, K840-STATUS IS ' K840-STATUS.             
                                                                                
           STOP RUN.                                                            
