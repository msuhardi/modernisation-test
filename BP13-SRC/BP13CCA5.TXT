       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CCA5.                                                 
      *AUTHOR.        BO JIANG.                                                 
      *DATE-WRITTEN.  26/12/08.                                                 
      * ========================================================== *            
      * SYSTEM OF COMMITMENT  (BP13)                               *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE   : IF NO SUB-TRANSFER IN PROCESS JV RECORD.    *            
      *              : FOR SUB-TRANSFER CASES , CHECK DTE-EFF-TRF, *            
      *              : PROCESS JV RECORD ONLY IF                   *            
      *              : SYSTEM DATE = DATE SUB-TRANSFER-EFF         *            
      *                                                            *            
      *  INPUT  FILES  1.  AG07F630                                *            
      *                2.  BP13KD00                                *            
      *                                                            *            
      *  OUTPUT FILES  1.  AG7F630A                                *            
      *                2.  AG7F630B (KIV)                          *            
      * ---------------------------------------------------------- *            
      * CHG REF  BY    DATE    DESCRIPTION                         *            
      * -------- ---- -------- -----------                         *            
      * BP133480 JB8  20081226 NEW PROGRAM.                        *            
      * BP133545 JB8  20090304 TO CATER FOR DTE-EFF < DTE-CURR     *            
      * BP133662 JB8  20090803 TO CATER FOR SUB-TRANSFER           *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
      *-------------------------------------------------------------            
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT AG07F630  ASSIGN        TO AG07F630.                          
           SELECT AG7F630A  ASSIGN        TO AG7F630A.                          
           SELECT AG7F630B  ASSIGN        TO AG7F630B.                          
           SELECT SY02F001  ASSIGN        TO SY02F001.                          
                                                                                
           SELECT BP13KD00  ASSIGN        TO BP13KD00                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KD00-KEY-FLD                       
                            FILE STATUS   IS WS-KD00-STATUS.                    
                                                                                
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-REGN                      
                            FILE STATUS   IS WS-K800-STATUS.                    
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       COPY SY02F001.                                                           
                                                                                
       FD   AG07F630                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 130 CHARACTERS.                                     
       COPY AG07F630.                                                           
                                                                                
       FD   AG7F630A                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 130 CHARACTERS.                                     
       01   AG7F630A-REC                     PIC X(130).                        
                                                                                
       FD   AG7F630B                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 130 CHARACTERS.                                     
       01   AG7F630B-REC                     PIC X(130).                        
                                                                                
       FD   BP13KD00                                                            
            RECORD CONTAINS 1600 CHARACTERS.                                    
       COPY BP13KD00.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-STATUS.                                                           
           05  WS-KD00-STATUS               PIC 99    VALUE ZEROS.              
           05  WS-K800-STATUS               PIC 99    VALUE ZEROS.              
           05  WS-F630-EOF                  PIC X(01) VALUE SPACE.              
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F630-READ-CTR             PIC 9(06)  VALUE ZEROS.             
           05  WS-F630A-WRT-CTR             PIC 9(06)  VALUE ZEROS.             
           05  WS-F630B-WRT-CTR             PIC 9(06)  VALUE ZEROS.             
                                                                                
      *=============================================================            
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-FILES             THRU 1000-EXIT.                  
           PERFORM 3000-PROCESS-RECORDS        THRU 3000-EXIT                   
             UNTIL WS-F630-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE          THRU 9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-FILES.                                                         
      *=============================================================            
                                                                                
           OPEN INPUT AG07F630                                                  
                      SY02F001                                                  
                      BP13K800                                                  
                      BP13KD00                                                  
               OUTPUT AG7F630A                                                  
                      AG7F630B.                                                 
                                                                                
           IF WS-KD00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR KD00 STATUS ' WS-KD00-STATUS               
              MOVE     WS-KD00-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K800 STATUS ' WS-K800-STATUS               
              MOVE     WS-K800-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
                                                                                
           PERFORM 2000-READ-AG07F630  THRU 2000-EXIT.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       2000-READ-AG07F630.                                                      
      *=============================================================            
                                                                                
            READ AG07F630 AT END                                                
                 MOVE 'Y'         TO WS-F630-EOF                                
                 MOVE HIGH-VALUES TO F630-NUM-REGN                              
                 GO TO 2000-EXIT.                                               
                                                                                
            ADD  1  TO WS-F630-READ-CTR.                                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       3000-PROCESS-RECORDS.                                                    
      *=============================================================            
                                                                                
           MOVE F630-NUM-REGN                      TO KD00-NUM-REGN.            
           PERFORM 4000-READ-BP13KD00            THRU 4000-EXIT.                
                                                                                
           IF KD00-DTE-EFF-TRANSFER = SPACES OR LOW-VALUES OR ZEROES            
              PERFORM 3500-PROCESS-SUBTRF        THRU 3500-EXIT                 
           ELSE                                                                 
              IF KD00-DTE-EFF-TRANSFER <= F001-DTE-CURRENT                      
                 PERFORM 5000-WRITE-AG7F630A     THRU 5000-EXIT                 
              ELSE                                                              
                 PERFORM 6000-WRITE-AG7F630B     THRU 6000-EXIT                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-AG07F630            THRU 2000-EXIT.                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       3500-PROCESS-SUBTRF.                                                     
      **********************                                                    
                                                                                
           PERFORM 4100-READ-BP13K800            THRU 4100-EXIT                 
                                                                                
           IF K800-NUM-DBSS-REGN-SUBTRF NOT = SPACES AND LOW-VALUES             
                                                     AND ZEROES                 
              MOVE K800-NUM-DBSS-REGN-SUBTRF       TO KD00-NUM-REGN             
              PERFORM 4000-READ-BP13KD00         THRU 4000-EXIT                 
                                                                                
              IF KD00-DTE-EFF-TRANSFER = SPACES OR LOW-VALUES OR ZEROES         
                 PERFORM 5000-WRITE-AG7F630A     THRU 5000-EXIT                 
              ELSE                                                              
                 IF KD00-DTE-EFF-TRANSFER <= F001-DTE-CURRENT                   
                    PERFORM 5000-WRITE-AG7F630A  THRU 5000-EXIT                 
                 ELSE                                                           
                    PERFORM 6000-WRITE-AG7F630B  THRU 6000-EXIT                 
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              PERFORM 5000-WRITE-AG7F630A        THRU 5000-EXIT                 
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       4000-READ-BP13KD00.                                                      
      *=============================================================            
                                                                                
           READ BP13KD00.                                                       
                                                                                
           IF WS-KD00-STATUS = 00 OR 02                                         
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-KD00-STATUS = 23                                            
                 DISPLAY 'RECORD NOT FOUND IN BP13KD00,REGN='                   
                                                 KD00-NUM-REGN                  
                 INITIALIZE                      BP13KD00-REC                   
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13KD00 ,RC= ' WS-KD00-STATUS          
                 MOVE WS-KD00-STATUS           TO RETURN-CODE                   
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       4100-READ-BP13K800.                                                      
      *=============================================================            
                                                                                
           MOVE SPACES                        TO BP13K800-MASTER.               
           INITIALIZE                            BP13K800-MASTER.               
                                                                                
           MOVE F630-NUM-REGN                 TO K800-NUM-REGN.                 
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = 00 OR 02                                         
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-K800-STATUS = 23                                            
                 DISPLAY 'RECORD NOT FOUND IN BP13K800,REGN='                   
                                                 K800-NUM-REGN                  
                 INITIALIZE                      BP13K800-MASTER                
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K800 ,RC= ' WS-K800-STATUS          
                 MOVE WS-K800-STATUS           TO RETURN-CODE                   
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       5000-WRITE-AG7F630A.                                                     
      *=============================================================            
                                                                                
           MOVE SPACES                         TO AG7F630A-REC.                 
           MOVE AG07F630-REC                   TO AG7F630A-REC.                 
           WRITE AG7F630A-REC.                                                  
           ADD 1 TO WS-F630A-WRT-CTR.                                           
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       6000-WRITE-AG7F630B.                                                     
      *=============================================================            
                                                                                
           MOVE SPACES                         TO AG7F630B-REC.                 
           MOVE AG07F630-REC                   TO AG7F630B-REC.                 
           WRITE AG7F630B-REC.                                                  
           ADD 1 TO WS-F630B-WRT-CTR.                                           
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       9000-CLOSE-ROUTINE.                                                      
      **************************************************************            
           DISPLAY ' '.                                                         
           DISPLAY ' ====  BP13CCA5 CONTROL TOTALS  ======'.                    
           DISPLAY ' '.                                                         
           DISPLAY ' AG07F630 RECORDS READ     : ' WS-F630-READ-CTR.            
           DISPLAY ' AG7F630A RECORDS WRITE    : ' WS-F630A-WRT-CTR.            
           DISPLAY ' AG7F630B RECORDS WRITE    : ' WS-F630B-WRT-CTR.            
           DISPLAY ' '.                                                         
           DISPLAY ' ====================================='.                    
                                                                                
           CLOSE AG07F630                                                       
                 SY02F001                                                       
                 BP13KD00                                                       
                 BP13K800                                                       
                 AG7F630A                                                       
                 AG7F630B.                                                      
                                                                                
           IF WS-KD00-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR BP13KD00-STAT ' WS-KD00-STATUS.            
                                                                                
           IF WS-K800-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR BP13K800-STAT ' WS-K800-STATUS.            
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
