       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C101.                                                 
       AUTHOR. YEONG TAT CHEONG.                                                
       DATE-WRITTEN.  05/05/89.                                                 
                                                                                
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE  :                                        *                
      *        1.  THIS PROGRAMME WILL VALIDATE                *                
      *            NRIC-NO IN TRANSACTIONS FROM NCN            *                
      *            SYSTEM (CPF PAYMENT).                       *                
      *            IF THE RECORD DOES NOT MATCH, THE           *                
      *            OUTPUT RECORD WILL MOVE TO THE KIV          *                
      *            FILE (ERROR FILE).                          *                
      *                                                        *                
      *        2.  THE PREVIOUS PROGRAM ALREADY UPDATE THE     *                
      *            NCN FILE BY MATCHING THE CPF-NO IN BP13K820 *                
      *            TO GET THE NRIC.                            *                
      *                                                        *                
      *     INPUT FILES:                                       *                
      *           1. BP13F103 (WEEKLY CPF TRANSACTIONS)        *                
      *           2. BP13F800 (RFF)                            *                
      *           3. BP13K880 (SUB-TRANSFER FILE)              *                
      *                                                        *                
      *     OUTPUT FILE:                                       *                
      *           1. BP13K140 (KIV FILE)                       *                
      *           2. BP13F121 (CPF TRANSACTIONS FILE)          *                
      *                                                        *                
      *     LISTINGS:                                          *                
      *           1. BP13L101 (CONTROL LIST)                   *                
      *                                                        *                
      * SOC-PH8B KPS 930708 - UPD BP13K800 INSTEAD OF RFF AND  *                
      *                       EXPAND REGN AND NRIC NO          *                
      * SOC-PH8F LJL 950722 - TO ACCEPT SUSPENSE ACCOUNT FOR   *                
      *                       SUB-TRANSFER MODULE.             *                
      * BP130103 LJL 960326 - TO CATER FOR SUSPENSE ACCOUNT    *                
      *                       CHANGES.                         *                
      * BP130289 SGK 970422 - TO CATER FOR SUSPENSE ACCOUNT    *                
      *                       REGN-NUM FOUND IN  SUB-TRANSFER  *                
      *                       FILE                             *                
      * BP130338 LSB 970820 - TO DIFF CDE-ERROR FOR CPF-NO/NRIC*                
      *                       "C" - CPF  ERROR                 *                
      * BP130523 RHB 980722 - TO CATER FOR CPFB Y2000.         *                
      * BP130418 NOR 980820 - MAKE PRGM Y2K COMPLIANT.         *                
      **********************************************************                
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F103 ASSIGN TO BP13F103.                                  
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
                                                                                
           SELECT BP13F121 ASSIGN TO BP13F121.                                  
                                                                                
           SELECT BP13K140 ASSIGN TO BP13K140                                   
                ACCESS MODE IS RANDOM                                           
                ORGANIZATION IS INDEXED                                         
                RECORD KEY IS K140-KEY-FLD                                      
                FILE STATUS IS K140-STATUS.                                     
                                                                                
                                                                                
           SELECT BP13K880 ASSIGN TO BP13K880                                   
                ACCESS MODE IS RANDOM                                           
                ORGANIZATION IS INDEXED                                         
                RECORD KEY IS K880-KEY-FLD                                      
                FILE STATUS IS K880-STATUS.                                     
                                                                                
           SELECT BP13L101 ASSIGN TO BP13L101.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F103                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 120 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F103.                                                           
                                                                                
       FD  BP13F800                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F800.                                                           
                                                                                
       FD  BP13F121                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       COPY BP13F121.                                                           
                                                                                
       FD  BP13K140                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13K140.                                                           
                                                                                
                                                                                
       FD  BP13K880                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K880.                                                           
                                                                                
       FD  BP13L101                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  PRINT-CONTROL-LIST             PIC X(132).                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  F800-STATUS                    PIC 99      VALUE ZEROES.             
       01  K140-STATUS                    PIC 99      VALUE ZEROES.             
       01  K880-STATUS                    PIC 99      VALUE ZEROES.             
                                                                                
       01  WS-K140-STATUS.                                                      
           05  WS-K140-STATUS1            PIC 9.                                
           05  WS-K140-STATUS2            PIC 9.                                
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F103-EOF-FLAG           PIC X       VALUE 'N'.                
           05  WS-F800-EOF-FLAG           PIC X       VALUE 'N'.                
                                                                                
       01  WS-CNT-BP13F103-READ           PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-BP13F800-READ           PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-K140-KIV                PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-F121-VALID              PIC 9(5)    VALUE ZEROES.             
       01  WS-K140-COUNT-HISTORY          PIC 9(2)    VALUE ZEROES.             
       01  WS-F121-COUNT-HISTORY          PIC 9(3)    VALUE ZEROES.             
       01  WS-CDE-APPLT                   PIC X       VALUE SPACES.             
       01  WS-NUM-NRIC                    PIC X(9)    VALUE SPACES.             
                                                                                
       01  M-KEY                          PIC X(11).                            
       01  T-KEY                          PIC X(11).                            
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05 WS-CCYY                     PIC 9999.                             
           05 WS-MM                       PIC 99.                               
           05 WS-DD                       PIC 99.                               
                                                                                
      ***************************************************************           
      *    WS  OR CONTROL-LIST                                      *           
      ***************************************************************           
                                                                                
       01  WS-CONTROL-LIST-1.                                                   
           05  FILLER              PIC X(10)         VALUE SPACES.              
           05  FILLER              PIC X(43)         VALUE                      
               'NO. OF RECORDS READ FROM FILE BP13F103 ARE'.                    
           05  FILLER              PIC X(6)          VALUE SPACES.              
           05  CNT-BP13F103-READ    PIC 9(5).                                   
                                                                                
       01  WS-CONTROL-LIST-2.                                                   
           05  FILLER              PIC X(10)         VALUE SPACES.              
           05  FILLER              PIC X(43)         VALUE                      
               'NO. OF RECORDS READ FROM FILE BP13F800 ARE'.                    
           05  FILLER              PIC X(6)          VALUE SPACES.              
           05  CNT-BP13F800-READ   PIC 9(5).                                    
                                                                                
       01  WS-CONTROL-LIST-3.                                                   
           05  FILLER              PIC X(10)         VALUE SPACES.              
           05  FILLER              PIC X(22)         VALUE                      
               'NO. OF ERROR CASES ARE'.                                        
           05  FILLER              PIC X(6)          VALUE SPACES.              
           05  CNT-K140-KIV        PIC 9(5).                                    
                                                                                
       01  WS-CONTROL-LIST-4.                                                   
           05  FILLER              PIC X(10)         VALUE SPACES.              
           05  FILLER              PIC X(22)         VALUE                      
               'NO. OF VALID CASES ARE'.                                        
           05  FILLER              PIC X(6)          VALUE SPACES.              
           05  CNT-F121-VALID      PIC 9(5).                                    
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
                                                                                
      ******************************************************************        
      *        MAIN LINE PROCESS                                       *        
      ******************************************************************        
       MAIN-PROCESS.                                                            
           PERFORM 000-OPEN-FILE                THRU 000-EXIT.                  
           PERFORM 100-READ-F103                THRU 100-EXIT.                  
           PERFORM 200-READ-F800                THRU 200-EXIT.                  
           PERFORM 300-PROCESS-SEQUENTIAL-FILES THRU 300-EXIT                   
               UNTIL WS-F103-EOF-FLAG = 'Y'                                     
               AND   WS-F800-EOF-FLAG = 'Y'.                                    
           PERFORM 1000-CLOSE-ROUTINE           THRU 1000-EXIT.                 
                                                                                
      ***************************************************************           
      *    OPENING FILES SECTION                                    *           
      ***************************************************************           
                                                                                
       000-OPEN-FILE.                                                           
           OPEN INPUT  BP13F103                                                 
                       BP13F800                                                 
                       BP13K880                                                 
                I-O    BP13K140                                                 
                OUTPUT BP13F121                                                 
                       BP13L101.                                                
                                                                                
           IF K880-STATUS NOT = 0  AND                                          
              K880-STATUS NOT = 97                                              
              DISPLAY ' OPENING ERROR, BP13K880 STATUS ' K880-STATUS            
              MOVE K880-STATUS           TO RETURN-CODE                         
              PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT.                        
                                                                                
           IF K140-STATUS NOT = 0  AND                                          
              K140-STATUS NOT = 97                                              
              DISPLAY ' OPENING ERROR, BP13K140 STATUS ' K140-STATUS            
              MOVE K140-STATUS           TO RETURN-CODE                         
              PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT.                        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CURRENT-DATE.                  
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ************************************************************              
      *    READ SEQUENTIAL FILE BP13F103                         *              
      ************************************************************              
                                                                                
       100-READ-F103.                                                           
                                                                                
           READ BP13F103                                                        
                AT END MOVE HIGH-VALUE TO M-KEY                                 
                MOVE 'Y'               TO WS-F103-EOF-FLAG                      
                GO                     TO 100-EXIT.                             
           ADD  1                      TO WS-CNT-BP13F103-READ.                 
      *    MOVE F103-SCHACC(3:11)      TO M-KEY.                                
           MOVE F103-KEY(3:11)         TO M-KEY.                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-F800.                                                           
           READ BP13F800                                                        
                AT END  MOVE HIGH-VALUE TO T-KEY                                
                MOVE 'Y'                TO WS-F800-EOF-FLAG                     
                GO                      TO 200-EXIT.                            
           ADD  1                       TO WS-CNT-BP13F800-READ.                
           MOVE F800-NUM-SCH-ACC        TO T-KEY.                               
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PROCESS-SEQUENTIAL-FILES.                                            
                                                                                
           MOVE SPACES TO WS-CDE-APPLT  WS-NUM-NRIC.                            
                                                                                
           IF M-KEY = T-KEY                                                     
               PERFORM 400-PROCESS-CPF  THRU 400-EXIT                           
               PERFORM 100-READ-F103    THRU 100-EXIT                           
           ELSE                                                                 
               IF M-KEY > T-KEY                                                 
                  PERFORM 200-READ-F800 THRU 200-EXIT                           
               ELSE                                                             
                   DISPLAY ' KEY ' F103-KEY                                     
                   MOVE SPACES                TO BP13K140-KIVTRAN               
                   MOVE 'A'                   TO K140-CDE-ERROR                 
                   PERFORM 320-PROCESS-INVALID-SCH-ACCT THRU 320-EXIT           
                   PERFORM 100-READ-F103                THRU 100-EXIT.          
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      * OUTPUT ERROR RECORD TO KIV FILE                                         
                                                                                
       320-PROCESS-INVALID-SCH-ACCT.                                            
                                                                                
           MOVE '12'                  TO K140-CDE-TRANS-TYPE.                   
           MOVE WS-CURRENT-DATE       TO K140-DTE-POST.                         
           ADD  1                     TO WS-K140-COUNT-HISTORY.                 
           MOVE 'N'                   TO K140-COUNT-HIST-CHAR.                  
           MOVE WS-K140-COUNT-HISTORY TO K140-COUNT-HIST-NUM.                   
           MOVE F103-PROC-DATE        TO K140-DTE-TRANS.                        
           MOVE F103-NUM-NRIC         TO K140-NUM-NRIC.                         
                                                                                
           IF   F103-AMT-DEP  IS NOT NUMERIC                                    
                MOVE 0 TO   F103-AMT-DEP.                                       
                                                                                
           IF   F103-AMT-SUSP IS NOT NUMERIC                                    
                MOVE 0 TO   F103-AMT-SUSP.                                      
                                                                                
           IF   F103-AMT-DEP          >  0                                      
           MOVE 'SOC'                 TO K140-CDE-PAYMENT-TYPE                  
           MOVE F103-AMT-DEP          TO K140-AMT-RECEIPT                       
           ELSE                                                                 
           IF   F103-AMT-SUSP         >  0                                      
           MOVE 'SUS'                 TO K140-CDE-PAYMENT-TYPE                  
           MOVE F103-AMT-SUSP         TO K140-AMT-RECEIPT                       
           END-IF.                                                              
                                                                                
           MOVE F103-JR-VR            TO K140-NUM-RECPT-JRNO.                   
           MOVE F103-SCHACC           TO K140-NUM-SCH-ACCT.                     
           IF K140-CDE-ERROR = 'A'                                              
               DISPLAY ' KEYA' F103-KEY                                         
      *       NEXT SENTENCE                                                     
           ELSE                                                                 
              MOVE F800-NUM-REGN      TO K140-NUM-REGN.                         
                                                                                
           WRITE BP13K140-KIVTRAN.                                              
                                                                                
           MOVE K140-STATUS TO  WS-K140-STATUS.                                 
                                                                                
           IF WS-K140-STATUS1 = 0                                               
              ADD 1 TO WS-CNT-K140-KIV                                          
           ELSE                                                                 
              IF K140-STATUS = 22                                               
                 DISPLAY ' ERROR WRITING DUP REC : ' K140-KEY-FLD               
              ELSE                                                              
                 DISPLAY ' OPENING ERROR, BP13K140 STATUS '                     
                         K140-STATUS                                            
                 MOVE K140-STATUS           TO   RETURN-CODE                    
                 PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT.                     
                                                                                
       320-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ********************************************************                  
      * CHECK CPF-NO.  IF IT MATCHES CPF-NO1, MOVE '1' TO    *                  
      * CDE-APPLT-CPF, IT IT DOES NOT MATCH, PROCEED TO      *                  
      * NEXT PROCESSING.                                     *                  
      ********************************************************                  
                                                                                
       400-PROCESS-CPF.                                                         
           IF  F103-NUM-NRIC = F800-NUM-NRIC1                                   
               MOVE F800-NUM-NRIC1    TO   WS-NUM-NRIC                          
               MOVE '1'               TO   WS-CDE-APPLT                         
               PERFORM 520-SOC-DEBTOR       THRU 520-EXIT                       
           ELSE                                                                 
           IF  F103-NUM-NRIC = SPACES OR LOW-VALUES                             
               MOVE 'C'                   TO K140-CDE-ERROR                     
               PERFORM 320-PROCESS-INVALID-SCH-ACCT THRU 320-EXIT               
           ELSE                                                                 
               PERFORM 420-CONTINUE-PROCESS THRU 420-EXIT.                      
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       420-CONTINUE-PROCESS.                                                    
           IF  F103-NUM-NRIC = F800-NUM-NRIC2                                   
               MOVE F800-NUM-NRIC2    TO   WS-NUM-NRIC                          
               MOVE '2'               TO   WS-CDE-APPLT                         
               PERFORM 520-SOC-DEBTOR       THRU 520-EXIT                       
           ELSE                                                                 
               PERFORM 440-CONTINUE-PROCESS THRU 440-EXIT.                      
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
       440-CONTINUE-PROCESS.                                                    
           IF F103-NUM-NRIC = F800-NUM-NRIC3                                    
               MOVE F800-NUM-NRIC3    TO   WS-NUM-NRIC                          
               MOVE '3'               TO   WS-CDE-APPLT                         
               PERFORM 520-SOC-DEBTOR THRU 520-EXIT                             
           ELSE                                                                 
               PERFORM 460-CONTINUE-PROCESS THRU 460-EXIT.                      
                                                                                
       440-EXIT.                                                                
           EXIT.                                                                
                                                                                
       460-CONTINUE-PROCESS.                                                    
           IF F103-NUM-NRIC = F800-NUM-NRIC4                                    
               MOVE F800-NUM-NRIC4    TO   WS-NUM-NRIC                          
               MOVE '4'               TO   WS-CDE-APPLT                         
               PERFORM 520-SOC-DEBTOR THRU 520-EXIT                             
           ELSE                                                                 
               MOVE SPACES                          TO BP13K140-KIVTRAN         
               MOVE 'C'                             TO K140-CDE-ERROR           
               PERFORM 320-PROCESS-INVALID-SCH-ACCT THRU 320-EXIT.              
                                                                                
       460-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       520-SOC-DEBTOR.                                                          
           MOVE SPACES                TO BP13F121-CPFTRAN.                      
           MOVE F800-NUM-REGN         TO F121-NUM-REGN.                         
           MOVE WS-CURRENT-DATE       TO F121-DTE-POST.                         
           MOVE ZEROS                 TO F121-COUNT-HISTORY.                    
           MOVE ZEROS                 TO F121-AMT-RECIEPT.                      
           MOVE F103-PROC-DATE        TO F121-DTE-TRANS.                        
           MOVE F103-SCHACC           TO F121-NUM-SCH-ACCT.                     
           MOVE F103-JR-VR            TO F121-NUM-RECPT-JRNO.                   
           MOVE '12'                  TO F121-CDE-TRANS-TYPE.                   
                                                                                
           IF   F103-AMT-DEP  IS NOT NUMERIC                                    
                MOVE 0 TO   F103-AMT-DEP.                                       
           IF   F103-AMT-SUSP IS NOT NUMERIC                                    
                MOVE 0 TO   F103-AMT-SUSP.                                      
           IF   F103-AMT-DEP          > 0                                       
           MOVE F103-AMT-DEP          TO F121-AMT-RECIEPT                       
           MOVE 'SOC'                 TO F121-CDE-PAYMENT-TYPE                  
           ELSE                                                                 
           IF   F103-AMT-SUSP         > 0                                       
            PERFORM  700-READ-K880     THRU   700-EXIT                          
             IF K880-STATUS NOT = 0                                             
               MOVE SPACES                TO BP13K140-KIVTRAN                   
               MOVE 'S'                 TO  K140-CDE-ERROR                      
               PERFORM 320-PROCESS-INVALID-SCH-ACCT THRU  320-EXIT              
               GO TO 520-EXIT                                                   
             END-IF                                                             
             MOVE F103-AMT-SUSP         TO F121-AMT-RECIEPT                     
             MOVE 'SUS'                 TO F121-CDE-PAYMENT-TYPE                
           END-IF.                                                              
                                                                                
           MOVE WS-NUM-NRIC           TO F121-NUM-NRIC.                         
           MOVE F103-NUM-CPF          TO F121-NUM-CPF-APPL.                     
           MOVE WS-CDE-APPLT          TO F121-CDE-APPLT-CPF.                    
           WRITE BP13F121-CPFTRAN.                                              
           ADD 1 TO WS-CNT-F121-VALID.                                          
                                                                                
       520-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *    PRINT CONTROL-LIST                                         *         
      *****************************************************************         
                                                                                
       600-PRINT-CONTROL-LIST.                                                  
           MOVE WS-CNT-BP13F103-READ   TO CNT-BP13F103-READ.                    
           MOVE WS-CNT-BP13F800-READ   TO CNT-BP13F800-READ.                    
           MOVE WS-CNT-F121-VALID      TO CNT-F121-VALID.                       
           MOVE WS-CNT-K140-KIV        TO CNT-K140-KIV.                         
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-LIST-1 AFTER 2.             
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-LIST-2 AFTER 2.             
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-LIST-3 AFTER 2.             
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-LIST-4 AFTER 2.             
                                                                                
                                                                                
        600-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
      *    SEARCH F800-NUM-REGN  IN BP13K880                          *         
      *****************************************************************         
                                                                                
       700-READ-K880.                                                           
                                                                                
           MOVE  F800-NUM-REGN       TO     K880-NUM-REGN.                      
           READ BP13K880.                                                       
                                                                                
                                                                                
        700-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       1000-CLOSE-ROUTINE.                                                      
           PERFORM 600-PRINT-CONTROL-LIST    THRU   600-EXIT.                   
                                                                                
           DISPLAY '******  BP13C101 *************'.                            
           DISPLAY 'NO OF F103 READ      (F103) : ' CNT-BP13F103-READ.          
           DISPLAY 'NO OF RFF RECS READ  (F800) : ' CNT-BP13F800-READ.          
           DISPLAY 'NO OF F121 READ      (F121) : ' CNT-F121-VALID.             
           DISPLAY 'NO OF K140 REC       (K140) : ' CNT-K140-KIV.               
                                                                                
           CLOSE BP13F800                                                       
                 BP13F103                                                       
                 BP13F121                                                       
                 BP13K140                                                       
                 BP13K880                                                       
                 BP13L101.                                                      
                                                                                
           IF K140-STATUS NOT = 0                                               
               DISPLAY ' CLOSING ERROR, BP13K140 STATUS ' K140-STATUS           
               MOVE K140-STATUS TO RETURN-CODE.                                 
                                                                                
           IF K880-STATUS NOT = 0                                               
               DISPLAY ' CLOSING ERROR, BP13K880 STATUS ' K880-STATUS           
               MOVE K880-STATUS TO RETURN-CODE.                                 
                                                                                
           STOP RUN.                                                            
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
