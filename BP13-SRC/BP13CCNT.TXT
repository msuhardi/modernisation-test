      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13CCNT.                                                 
      *AUTHOR.        RJB1.                                                     
      *DATE-WRITTEN.  22/05/2015.                                               
                                                                                
      ***************************************************************           
      *            SYSTEM OF COMMITMENT (BP13)                      *           
      *=============================================================*           
      *  OBJECTIVE  : TO RETRIEVE DATA FROM CONTACT TABLE           *           
      *               OFFICE-CNTCT                                  *           
      *                                                             *           
      *  INPUT      : NICKNME AND CONTACT TYPE                      *           
      *  OUTPUT     : CONTACT INFO                                  *           
      *  COMMAREA   : 68 BYTES                                      *           
      ***************************************************************           
      * MODIFICATIONS :                                           *             
      *                                                           *             
      * CHGE REQ #  DATE        AUTHOR  REMARKS                   *             
      * ----------  ----------  ------  ------------------------- *             
      * BP135711    22/05/2015  RJB1    NEW PROGRAM.              *             
      *************************************************************             
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-SQL-CODES.                                                        
           05  WS-SQL-FOUND              PIC S9(4) COMP VALUE +000.             
           05  WS-SQL-NOT-FOUND          PIC S9(4) COMP VALUE +100.             
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-STAFF-NO               PIC X(6)   VALUE SPACES.               
           05  WS-STAFF-NICK             PIC X(6)   VALUE SPACES.               
           05  WS-STAFF-CNTCT            PIC X(50)  VALUE SPACES.               
           05  WS-CNTCT-TYPE             PIC X(3)   VALUE SPACES.               
                                                                                
       01  WS-NCK                        PIC X(3)   VALUE 'NCK'.                
                                                                                
           EXEC SQL INCLUDE P13VOCNT END-EXEC.                                  
           EXEC SQL INCLUDE SQLCA    END-EXEC.                                  
                                                                                
                                                                                
      *-----------------------*                                                 
       LINKAGE SECTION.                                                         
      *-----------------------*                                                 
       01  LINK-REC.                                                            
           05  LINK-CNTCT-TYPE              PIC X(3).                           
           05  LINK-STAFF-NICK              PIC X(6).                           
           05  LINK-STAFF-NUM               PIC X(6).                           
           05  LINK-STAFF-CNTCT             PIC X(50).                          
           05  LINK-SYSERR                  PIC 9(4).                           
                                                                                
      *------------------*                                                      
       PROCEDURE DIVISION USING LINK-REC.                                       
      *------------------*                                                      
                                                                                
           EXEC SQL WHENEVER NOT FOUND                                          
                             CONTINUE END-EXEC.                                 
           EXEC SQL WHENEVER SQLERROR                                           
                             GO TO 9000-ABORT-PROCESS END-EXEC.                 
           EXEC SQL WHENEVER SQLWARNING                                         
                             GO TO 9000-ABORT-PROCESS END-EXEC.                 
                                                                                
                                                                                
      *-----------------------*                                                 
       0000-START-ROUTINE.                                                      
      *-----------------------*                                                 
           MOVE LINK-STAFF-NICK          TO WS-STAFF-NICK.                      
           MOVE LINK-STAFF-NUM           TO WS-STAFF-NO.                        
           MOVE ZEROES                   TO LINK-SYSERR.                        
                                                                                
           IF WS-STAFF-NO NOT = SPACES AND LOW-VALUES                           
              PERFORM 1000-GET-CONTACT                                          
                 THRU 1000-EXIT                                                 
           ELSE                                                                 
              IF WS-STAFF-NICK NOT = SPACES AND LOW-VALUES                      
                 PERFORM 2000-GET-STAFF-NO                                      
                    THRU 2000-EXIT                                              
                 PERFORM 1000-GET-CONTACT                                       
                    THRU 1000-EXIT                                              
              END-IF                                                            
           END-IF.                                                              
                                                                                
           EXEC SQL COMMIT END-EXEC.                                            
                                                                                
           GOBACK.                                                              
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------*                    
      *    RETRIEVES CONTACT INFORMATION USING STAFF NO                         
      *    FROM THE OFFICE CONTACT TABLE                                        
      *----------------------------------------------------*                    
      *-----------------------*                                                 
       1000-GET-CONTACT.                                                        
      *-----------------------*                                                 
                                                                                
           MOVE LINK-CNTCT-TYPE            TO WS-CNTCT-TYPE                     
                                                                                
           EXEC SQL                                                             
                SELECT NUM_CNTCT                                                
                  INTO :WS-STAFF-CNTCT                                          
                  FROM OFFICE_CNTCT                                             
                 WHERE NUM_STAFF = :WS-STAFF-NO                                 
                   AND NUM_CNTCT_TYPE = :WS-CNTCT-TYPE                          
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = WS-SQL-FOUND                                        
              MOVE SPACES                  TO LINK-STAFF-CNTCT                  
              DISPLAY 'REC NOTFND IN OFFICE_CNTCT = ' WS-STAFF-NICK             
                      ' - ' WS-STAFF-NO                                         
              DISPLAY 'SQLCODE                    = ' SQLCODE                   
              MOVE SQLCODE                 TO LINK-SYSERR                       
           ELSE                                                                 
              MOVE WS-STAFF-CNTCT          TO LINK-STAFF-CNTCT                  
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------                                                 
       2000-GET-STAFF-NO.                                                       
      *------------------------                                                 
           MOVE WS-NCK                     TO WS-CNTCT-TYPE.                    
                                                                                
           EXEC SQL                                                             
                SELECT NUM_STAFF                                                
                  INTO :WS-STAFF-NO                                             
                  FROM OFFICE_CNTCT                                             
                 WHERE NUM_CNTCT = :WS-STAFF-NICK                               
                   AND NUM_CNTCT_TYPE = :WS-CNTCT-TYPE                          
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = WS-SQL-FOUND                                        
              MOVE SPACES                  TO LINK-STAFF-CNTCT                  
              MOVE SPACES                  TO WS-STAFF-NO                       
              DISPLAY 'REC NOTFND IN OFFICE_CNTCT = ' WS-STAFF-NICK             
              DISPLAY 'SQLCODE                    = ' SQLCODE                   
              MOVE SQLCODE                 TO LINK-SYSERR                       
           ELSE                                                                 
              MOVE WS-STAFF-NO             TO LINK-STAFF-NUM                    
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------*                                                 
       9000-ABORT-PROCESS.                                                      
      *-----------------------*                                                 
           CALL 'DBATIAR' USING SQLCA.                                          
           MOVE SQLCODE       TO LINK-SYSERR                                    
                                 RETURN-CODE.                                   
           GOBACK.                                                              
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
