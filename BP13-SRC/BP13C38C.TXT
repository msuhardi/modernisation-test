       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C38C.                                                 
       AUTHOR.        ELGER DE GUZMAN                                           
      *DATE-WRITTEN.  16/05/2011.                                               
      * ===============================================================*        
      * SYSTEM NAME (SYSTEM ID)                                        *        
      * ===============================================================*        
      * OBJECTIVE   : CREATE REPORT FOR TP OR SA/TP 2 DAYS PRIOR       *        
      *               TO THE ACTUAL APPOINTMENT.                       *        
      * CR NO.    EDITED  DATE       Description                       *        
      * ========  ======= =====      ================================= *        
      * BP134129  ESD1    13-06-2011 REMOVE BUYER FROM THE REPORT      *        
      * BP134233  JB8     06-10-2011 ADD TGK1 TO MAIL LIST             *        
      * BP134478  AB9     12-04-2012 REPLACE TGK1 WITH OTS1            *        
      * BP134269  IMC1    20-06-2012 REMOVE LKC13 ADD "LSC13"          *        
      * BP134571  imC1    12-07-2012 REPLACE OTS1 WITH TGK1            *        
      * BP134695  PCL4    17-01-2013 UPDATE EMAIL RECIPIENT            *        
      * BP134850  PCL4    19-03-2013 UPDATE WS-NUM-DTE-APPT            *        
      * BP134850  PCL4    25-03-2013 TO ADD FT & DESIGN                *        
      * BP134925  PCL4    09-05-2013 TO INCREASE THE LENGTH OF MAILC38C*        
      * BP134925  PCL4    27-05-2013 TO ADD RESALE CODE VLN            *        
      * BP134952  PCL4    06-08-2013 TO INCLUDE WS-PRIOR-DTE IN        *        
      *                              EMAIL SUBJECT HEADER              *        
      * BP135116  PCL4    01-11-2013 TO CHANGE THE EMAIL ALERT RECIPIENT        
      * BP135384  PCL4    21-06-2014 TO CATER FOR CG DEBAR CODE                 
      * BP136082  KVL1    27-01-2016 TO REPLACE SU1 W/ HWM2 IN THE CC  *        
      *                              AND CHANGED ALL RECIPIENTS TO LONG*        
      *                              NAME                              *        
      * BP136196  RJB1    08-04-2016 TO REPLACE GAY KWEE TAN WITH      *        
      *                              JUARIAH ALI                       *        
      * BP136241  KVL1    11-05-2016 TO include resale status "SD" in  *        
      *                              the list to flag out in the e-alert        
      * BP136477  RJB1    18-10-2016 TO REPLACE KIM HUAT TO YEE MIEN   *        
      * BP137090  PCL4    28-11-2017 TO UPDATE EMAIL RECIPIENTS        *        
      * BP137230  PCL4    01-13-2018 TO UPDATE EMAIL RECIPIENTS        *        
      * BP138352  KV5     29-06-2020 ADDED RECIPIENT AND APPT DATE TIME*        
      * BP139030  ZAR7    07-01-2022 REPLACE CHUA YEE MIEN TO LIM KOK CH        
      * BP139276  KAC1 05-04-2024 REPLACE LIM KOK CHUN BY              *        
      *                                   KATHIJAH_BEE_ALI_MOHAMED     *        
      * BP139160  KAC1 17-04-2024 Include Jialing HE(HDB); Catherine HL         
      *                           Chee Khiang CHIAM (HDB) in the cc list        
      *================================================================*        
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
            SELECT BP13F454 ASSIGN TO BP13F454.                                 
                                                                                
            SELECT BP13K800 ASSIGN TO BP13K800                                  
                   ORGANIZATION IS INDEXED                                      
                   RECORD KEY IS K800-NUM-REGN                                  
                   FILE STATUS IS WS-BP13K800-STAT                              
                   ACCESS MODE IS RANDOM.                                       
                                                                                
            SELECT BP13K820 ASSIGN TO BP13K820                                  
                   ORGANIZATION IS INDEXED                                      
                   RECORD KEY IS K820-KEY-FLD                                   
                   FILE STATUS IS WS-BP13K820-STAT                              
                   ACCESS MODE IS DYNAMIC.                                      
                                                                                
            SELECT P04K010 ASSIGN TO P04K010                                    
                   ORGANIZATION IS INDEXED                                      
                   RECORD KEY IS K010-PRIME-KEY                                 
                   FILE STATUS IS WS-P04K010-STAT                               
                   ACCESS MODE IS DYNAMIC.                                      
                                                                                
            SELECT BB24K510 ASSIGN TO BB24K510                                  
                   ORGANIZATION IS INDEXED                                      
                   RECORD KEY IS K510-KEY-FLD                                   
                   FILE STATUS IS WS-BB24K510-STAT                              
                   ACCESS MODE IS DYNAMIC.                                      
                                                                                
            SELECT BB24K500 ASSIGN TO BB24K500                                  
                   ORGANIZATION IS INDEXED                                      
                   RECORD KEY IS K500-KEY-FLD                                   
                   FILE STATUS IS WS-BB24K500-STAT                              
                   ACCESS MODE IS RANDOM.                                       
                                                                                
            SELECT MAILC38C ASSIGN TO MAILC38C.                                 
                                                                                
                                                                                
      ****************                                                          
       DATA DIVISION.                                                           
      ****************                                                          
                                                                                
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F454                                                            
            RECORDING MODE  IS   F                                              
            BLOCK  CONTAINS 0    RECORDS                                        
            RECORD CONTAINS 100 CHARACTERS                                      
            LABEL  RECORDS  ARE  STANDARD.                                      
       COPY BP13F454.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS  2000 CHARACTERS.                                   
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS  400 CHARACTERS.                                    
       COPY BP13K820.                                                           
                                                                                
       FD   P04K010                                                             
            RECORD CONTAINS  300 CHARACTERS.                                    
       COPY P04K010.                                                            
                                                                                
       FD   BB24K510                                                            
            RECORD CONTAINS  50 CHARACTERS.                                     
       COPY BB24K510.                                                           
                                                                                
       FD   BB24K500                                                            
            RECORD CONTAINS  1700 CHARACTERS.                                   
       COPY BB24K500.                                                           
                                                                                
       FD   MAILC38C                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS  120 CHARACTERS.                                    
       01   MAILC38C-REC               PIC X(120).                              
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       COPY DESIGTAB.                                                           
                                                                                
       01 WS-CUR-DATE.                                                          
          05  WS-CUR-CCYY                  PIC 9(04).                           
          05  WS-CUR-MM                    PIC 9(02).                           
          05  WS-CUR-DD                    PIC 9(02).                           
                                                                                
       01 WS-PRIO-DTE.                                                          
          05  WS-PRIO-CCYY                 PIC 9(04).                           
          05  WS-PRIO-MM                   PIC 9(02).                           
          05  WS-PRIO-DD                   PIC 9(02).                           
                                                                                
       01 WS-NUM-DTE-APPT           PIC 9(07) VALUE ZEROES.                     
       01 WS-PRIOR-DTE              PIC 9(08) VALUE ZEROES.                     
       01 WS-NRIC-KEY               PIC X(9)  VALUE SPACES.                     
       01 WS-NRIC-RPT               PIC X(9)  VALUE SPACES.                     
       01 WS-DCODE                  PIC X(3)  VALUE SPACES.                     
       01 WS-DEBAR-RPT              PIC X(2)  VALUE SPACES.                     
       01 WS-DEBAR-CG               PIC X(2)  VALUE SPACES.                     
       01 WS-SPOUSE-NRIC            PIC X(9)  VALUE SPACES.                     
       01 WS-FIANCE-NRIC            PIC X(9)  VALUE SPACES.                     
                                                                                
       01 WS-FILE-STATUS.                                                       
          05 WS-BB24K510-STAT        PIC X(02) VALUE SPACES.                    
          05 WS-BB24K500-STAT        PIC X(02) VALUE SPACES.                    
          05 WS-BP13K800-STAT        PIC X(02) VALUE SPACES.                    
          05 WS-BP13K820-STAT        PIC X(02) VALUE SPACES.                    
          05 WS-P04K010-STAT         PIC X(02) VALUE SPACES.                    
                                                                                
       01 WS-SWITCHES.                                                          
          05 WS-K800-FOUND           PIC X      VALUE 'N'.                      
          05 WS-F454-EOF             PIC X      VALUE 'N'.                      
          05 WS-EOF-K010             PIC X      VALUE 'N'.                      
          05 WS-EOF-K820             PIC X      VALUE 'N'.                      
          05 WS-BB24K510-EOF         PIC X      VALUE 'N'.                      
          05 WS-WRITE-RECORD         PIC X      VALUE 'N'.                      
          05 WS-WRITE-CG-OK          PIC X      VALUE 'N'.                      
          05 WS-RESALE-OK            PIC X      VALUE 'N'.                      
          05 WS-VALID-DATE           PIC X      VALUE 'N'.                      
                                                                                
       01 WS-COUNTER.                                                           
          05 WS-I                    PIC 9(1)   VALUE ZEROES.                   
          05 WS-WRITTEN-TOTAL        PIC 9(4)   VALUE ZEROES.                   
          05 WS-SKIP-TOTAL           PIC 9(4)   VALUE ZEROES.                   
          05 WS-SKIP-CG              PIC 9(4)   VALUE ZEROES.                   
          05 WS-HA-INFO.                                                        
             10  HA-INFO OCCURS 4 TIMES.                                        
                 20  HA-NRIC               PIC  X(09).                          
                                                                                
          05 WS-DEBAR-INFO.                                                     
             10  DEBAR-INFO OCCURS 6 TIMES.                                     
                 20  WS-DEBAR              PIC  X(02).                          
                                                                                
          05 WS-DEBAR-NRIC.                                                     
             10  DEBAR-NRIC OCCURS 6 TIMES.                                     
                 20  WS-NUM-NRIC           PIC  X(09).                          
                                                                                
       01 WS-VALID-RESALE-STATUS     PIC X(3).                                  
          88 WS-VLD-RSL-STAT         VALUE 'LN ', 'LNS', 'PN ', 'FN ',          
                                           'LNH', 'FNH', 'QN ', 'QNH',          
                                           'BLD', 'BLN', 'EDQ', 'ELF',          
                                           'ELN', 'ELP', 'EQN', 'HNQ',          
                                           'TOM', 'TOT', 'FNQ', 'PNQ',          
                                           'BQN', 'BHN', 'VLN', 'SD '.          
                                                                                
       01  WS-REPORT-DTL.                                                       
                                                                                
           05  WS-EMPTY-RPT.                                                    
               10 FILLER                  PIC X(30) VALUE SPACES.               
               10 FILLER                  PIC X(21)                             
                                          VALUE '<   EMPTY  REPORT   >'.        
           05  WS-TITLE01-RPT.                                                  
               10 FILLER                  PIC X(80) VALUE                       
                  'Counter Supervisor'.                                         
                                                                                
           05  WS-TITLE02-RPT.                                                  
               10 FILLER                  PIC X(80) VALUE                       
                  'Case with keys collection appointment whereby resale         
      -           'levy has not been paid.'.                                    
                                                                                
           05  WS-TITLE03-RPT.                                                  
               10 FILLER                  PIC X(80) VALUE                       
                  'Please check and highlight case to CSO.'.                    
                                                                                
           05  WS-HEADER1-RPT.                                                  
               10 FILLER                  PIC X(03) VALUE SPACES.               
               10 FILLER                  PIC X(08) VALUE 'REG NO'.             
               10 FILLER                  PIC X(01) VALUE SPACES.               
               10 FILLER                  PIC X(09)                             
                                          VALUE 'UIN'.                          
               10 FILLER                  PIC X(01) VALUE SPACES.               
               10 FILLER                  PIC X(11)                             
                                          VALUE 'RESALE SCH'.                   
               10 FILLER                  PIC X(01) VALUE SPACES.               
               10 FILLER                  PIC X(11)                             
                                          VALUE 'SOC SCH'.                      
               10 FILLER                  PIC X(01) VALUE SPACES.               
               10 FILLER                  PIC X(04)                             
                                          VALUE 'MODE'.                         
               10 FILLER                  PIC X(01) VALUE SPACES.               
               10 FILLER                  PIC X(02)                             
                                          VALUE 'FT'.                           
               10 FILLER                  PIC X(01) VALUE SPACES.               
               10 FILLER                  PIC X(20)                             
                                          VALUE 'DESIGN'.                       
               10 FILLER                  PIC X(01) VALUE SPACES.               
               10 FILLER                  PIC X(10)                             
                                          VALUE 'RSL STATUS'.                   
               10 FILLER                  PIC X(01) VALUE SPACES.               
               10 FILLER                  PIC X(03)                             
                                          VALUE 'DEB'.                          
               10 FILLER                  PIC X(01) VALUE SPACES.               
               10 FILLER                  PIC X(10)                             
                                          VALUE 'APPT DATE '.                   
               10 FILLER                  PIC X(01) VALUE SPACES.               
               10 FILLER                  PIC X(09)                             
                                          VALUE 'APPT TIME'.                    
                                                                                
           05  WS-DETAIL-RPT.                                                   
               10 FILLER                    PIC X(3)  VALUE SPACES.             
               10 WS-REGN-DTL               PIC X(8)  VALUE SPACES.             
               10 FILLER                    PIC X(1)  VALUE SPACES.             
               10 WS-APPL-UIN               PIC X(9)  VALUE SPACES.             
               10 FILLER                    PIC X(1)  VALUE SPACES.             
               10 WS-SCHEME-A               PIC X(11) VALUE SPACES.             
               10 FILLER                    PIC X(1) VALUE SPACES.              
               10 WS-SCHEME-B               PIC X(11) VALUE SPACES.             
               10 FILLER                    PIC X(1) VALUE SPACES.              
               10 WS-ALC-MODE               PIC X(03) VALUE SPACES.             
               10 FILLER                    PIC X(2) VALUE SPACES.              
               10 WS-FLAT-TYPE              PIC X(02) VALUE SPACES.             
               10 FILLER                    PIC X(1) VALUE SPACES.              
               10 WS-DESIGN                 PIC X(20) VALUE SPACES.             
               10 FILLER                    PIC X(1) VALUE SPACES.              
               10 WS-RSL-STATUS1            PIC X(03) VALUE SPACES.             
               10 FILLER                    PIC X(3) VALUE SPACES.              
               10 WS-RSL-STATUS2            PIC X(03) VALUE SPACES.             
               10 FILLER                    PIC X(2) VALUE SPACES.              
               10 WS-DEB-CODE               PIC X(03) VALUE SPACES.             
               10 FILLER                    PIC X(1) VALUE SPACES.              
               10 WS-DATE-DTL               PIC X(10) VALUE SPACES.             
               10 FILLER                    PIC X(1) VALUE SPACES.              
               10 WS-TIME-DTL               PIC X(05) VALUE SPACES.             
                                                                                
           05  WS-DATE-RPT-DTL.                                                 
               10 FILLER                PIC X(6)  VALUE 'DATE: '.               
               10 WS-DATE-RPT           PIC X(10) VALUE SPACES.                 
           05  WS-MAIL-RPT1             PIC X(12) VALUE 'HELO SGPHDB1'.         
           05  WS-MAIL-RPT2             PIC X(27)                               
               VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.                                
           05  WS-MAIL-RPT4             PIC X(50)                               
      *        VALUE 'RCPT TO:<SU1@HDB.GOV.SG>'.                                
               VALUE 'RCPT TO:<HEE_WAN_MENG@HDB.GOV.SG>'.                       
           05  WS-MAIL-RPT5             PIC X(50)                               
               VALUE 'RCPT TO:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.           
           05  WS-MAIL-RPT5a            PIC X(50)                               
               VALUE 'RCPT TO:<Catherine_HL_TAN@hdb.gov.sg>'.                   
           05  WS-MAIL-RPT5b            PIC X(50)                               
               VALUE 'RCPT TO:<CHIAM_Chee_Khiang@hdb.gov.sg>'.                  
           05  WS-MAIL-RPT5c            PIC X(50)                               
               VALUE 'RCPT TO:<HE_Jialing@hdb.gov.sg>'.                         
           05  WS-MAIL-RPT6             PIC X(50)                               
               VALUE 'RCPT TO:<LOW_SOO_CHIEW@HDB.GOV.SG>'.                      
           05  WS-MAIL-RPT7             PIC X(50)                               
               VALUE 'RCPT TO:<JAMILAH_BAHROM@HDB.GOV.SG>'.                     
           05  WS-MAIL-RPT7A            PIC X(50)                               
               VALUE 'RCPT TO:<JUARIAH_ALI@HDB.GOV.SG>'.                        
           05  WS-MAIL-RPT7B            PIC X(50)                               
               VALUE 'RCPT TO:<TOH_LAY_YEN@HDB.GOV.SG>'.                        
           05  WS-MAIL-RPT7C            PIC X(50)                               
               VALUE 'RCPT TO:<NORAZEAN_HAMID@HDB.GOV.SG>'.                     
           05  WS-MAIL-RPT8             PIC X(4)                                
               VALUE 'DATA'.                                                    
           05  WS-MAIL-RPT9             PIC X(29)                               
               VALUE 'FROM:Soc System - Email Alert'.                           
           05  WS-MAIL-RPT11            PIC X(50)                               
               VALUE 'TO:<JUARIAH_ALI@HDB.GOV.SG>'.                             
           05  WS-MAIL-RPT11A           PIC X(50)                               
               VALUE 'TO:<TOH_LAY_YEN@HDB.GOV.SG>'.                             
           05  WS-MAIL-RPT12            PIC X(50)                               
               VALUE 'TO:<JAMILAH_BAHROM@HDB.GOV.SG>'.                          
           05  WS-MAIL-RPT12A           PIC X(50)                               
               VALUE 'TO:<NORAZEAN_HAMID@HDB.GOV.SG>'.                          
           05  WS-MAIL-RPT13A           PIC X(50)                               
      *        VALUE 'CC:<SU1@HDB.GOV.SG>'.                                     
               VALUE 'CC:<HEE_WAN_MENG@HDB.GOV.SG>'.                            
           05  WS-MAIL-RPT13B           PIC X(50)                               
               VALUE 'CC:<LOW_SOO_CHIEW@HDB.GOV.SG>'.                           
           05  WS-MAIL-RPT13C           PIC X(50)                               
               VALUE 'CC:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                
           05  WS-MAIL-RPT13d           PIC X(50)                               
               VALUE 'CC:<Catherine_HL_TAN@hdb.gov.sg>'.                        
           05  WS-MAIL-RPT13e           PIC X(50)                               
               VALUE 'CC:<CHIAM_Chee_Khiang@hdb.gov.sg>'.                       
           05  WS-MAIL-RPT13f           PIC X(50)                               
               VALUE 'CC:<HE_Jialing@hdb.gov.sg>'.                              
           05  WS-MAIL-RPT15            PIC X(90)                               
               VALUE 'SUBJECT: RESALE CASES REPORT'.                            
                                                                                
      *----------------------------------------------------------------*        
      *        LINKAGE FOR SUB-PROGRAM BP13C913                        *        
      *----------------------------------------------------------------*        
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH     PIC X(4).                                
               10  WS-LINK-NUM-ACC     PIC X(5).                                
                                                                                
       COPY P13COMM8.                                                           
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================*           
       MAIN.                                                                    
      *=============================================================*           
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13F454   THRU 2000-EXIT.                         
           PERFORM 3000-PROCESS         THRU 3000-EXIT                          
           UNTIL   WS-F454-EOF          = 'Y'.                                  
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
      *=============================================================*           
       1000-OPEN-ROUTINE.                                                       
      *=============================================================*           
                                                                                
           OPEN        INPUT    BP13F454                                        
                                BP13K800                                        
                                BP13K820                                        
                                P04K010                                         
                                BB24K510                                        
                                BB24K500                                        
                       OUTPUT   MAILC38C.                                       
                                                                                
           INITIALIZE  BP13F454-REC                                             
                       BP13K800-MASTER                                          
                       BB24K510-REC                                             
                       BB24K500-REC.                                            
                                                                                
           MOVE        FUNCTION CURRENT-DATE(1:8) TO    WS-CUR-DATE.            
           PERFORM     3100-GET-DATE              THRU  3100-EXIT.              
           PERFORM     5000-HEADER-EMAIL          THRU  5000-EXIT.              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       2000-READ-BP13F454.                                                      
      *=============================================================*           
                                                                                
            READ BP13F454                                                       
                 AT END MOVE 'Y'  TO WS-F454-EOF                                
            END-READ.                                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       3000-PROCESS.                                                            
      *=============================================================*           
                                                                                
            IF F454-DTE-APPT  <=   WS-PRIOR-DTE                                 
               MOVE 'Y'   TO   WS-VALID-DATE                                    
            ELSE                                                                
               MOVE 'N'   TO   WS-VALID-DATE                                    
            END-IF.                                                             
                                                                                
            IF F454-CDE-APPT-TYPE = 'TP' OR 'SP' OR 'SR'                        
               IF WS-VALID-DATE = 'Y'                                           
                  PERFORM 3200-READ-BP13K800 THRU 3200-EXIT                     
                  IF WS-K800-FOUND = 'Y'                                        
                     PERFORM 3300-CHECK-CG-NRIC   THRU 3300-EXIT                
                     PERFORM 4600-PROCESS-P04K010 THRU 4600-EXIT                
                     PERFORM 4000-CHECK-BB24      THRU 4000-EXIT                
                  ELSE                                                          
                     CONTINUE                                                   
                  END-IF                                                        
               ELSE                                                             
                  NEXT SENTENCE                                                 
               END-IF                                                           
               IF WS-WRITE-RECORD  =  'Y'                                       
                  PERFORM 5200-CALL-BP13C913  THRU 5200-EXIT                    
                  PERFORM 3600-WRITE-CG-DEBAR THRU 3600-EXIT                    
               ELSE                                                             
                  IF WS-DEBAR-CG = 'Y'                                          
                     MOVE SPACES                 TO WS-NRIC-KEY                 
                     PERFORM 5200-CALL-BP13C913  THRU 5200-EXIT                 
                     PERFORM 3600-WRITE-CG-DEBAR THRU 3600-EXIT                 
                  ELSE                                                          
                     DISPLAY F454-NUM-REGN " + " F454-DTE-APPT " + "            
                             WS-PRIOR-DTE                                       
                     ADD 1  TO   WS-SKIP-TOTAL                                  
                  END-IF                                                        
               END-IF                                                           
            END-IF.                                                             
                                                                                
            PERFORM 2000-READ-BP13F454   THRU 2000-EXIT.                        
                                                                                
            IF WS-F454-EOF = 'Y'                                                
               IF WS-WRITTEN-TOTAL  >  1                                        
                  CONTINUE                                                      
               ELSE                                                             
                  WRITE MAILC38C-REC FROM WS-EMPTY-RPT                          
               END-IF                                                           
            END-IF.                                                             
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       3100-GET-DATE.                                                           
      ************************                                                  
                                                                                
           MOVE    WS-CUR-DATE  TO  WS-PRIOR-DTE.                               
                                                                                
           COMPUTE WS-NUM-DTE-APPT =                                            
                   FUNCTION INTEGER-OF-DATE(WS-PRIOR-DTE).                      
                                                                                
           ADD 4  TO  WS-NUM-DTE-APPT.                                          
                                                                                
           COMPUTE  WS-PRIOR-DTE =                                              
                    FUNCTION DATE-OF-INTEGER(WS-NUM-DTE-APPT).                  
                                                                                
           MOVE WS-PRIOR-DTE        TO WS-PRIO-DTE.                             
           DISPLAY  'PRIOR DATE USED ' WS-PRIOR-DTE.                            
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       3200-READ-BP13K800.                                                      
      ************************                                                  
                                                                                
           MOVE SPACES                      TO BP13K800-MASTER.                 
           MOVE 'N'                         TO WS-K800-FOUND.                   
           INITIALIZE                       BP13K800-MASTER.                    
                                                                                
           MOVE F454-NUM-REGN               TO K800-NUM-REGN.                   
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF WS-BP13K800-STAT = 00                                             
              MOVE 'Y' TO WS-K800-FOUND                                         
           ELSE                                                                 
              IF WS-BP13K800-STAT = 23                                          
                 MOVE 'N' TO WS-WRITE-RECORD                                    
                 DISPLAY 'REGN NOT FOUND ON BP13K800 = ' K800-NUM-REGN          
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K800 , '                            
                     'STATUS = ' WS-BP13K800-STAT                               
                 MOVE WS-BP13K800-STAT      TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       3300-CHECK-CG-NRIC.                                                      
      ************************                                                  
                                                                                
           MOVE SPACES                        TO WS-FIANCE-NRIC                 
                                                 WS-SPOUSE-NRIC                 
                                                 WS-DEBAR-CG                    
                                                 WS-WRITE-CG-OK.                
                                                                                
           PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > 6                      
               MOVE SPACES                    TO WS-NUM-NRIC(WS-I)              
                                                 WS-DEBAR(WS-I)                 
           END-PERFORM.                                                         
                                                                                
           MOVE K800-NUM-NRIC1                TO HA-NRIC(1).                    
           MOVE K800-NUM-NRIC2                TO HA-NRIC(2).                    
           MOVE K800-NUM-NRIC3                TO HA-NRIC(3).                    
           MOVE K800-NUM-NRIC4                TO HA-NRIC(4).                    
                                                                                
           MOVE SPACES                        TO BP13K820-REC.                  
           INITIALIZE                            BP13K820-REC.                  
                                                                                
           MOVE 'N'                           TO WS-EOF-K820.                   
           MOVE K800-NUM-REGN                 TO K820-NUM-REGN.                 
                                                                                
           START BP13K820 KEY = K820-NUM-REGN.                                  
                                                                                
           EVALUATE WS-BP13K820-STAT                                            
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 3400-READNEXT-BP13K820 THRU 3400-EXIT               
                    PERFORM 3500-PROCESS-BP13K820  THRU 3500-EXIT               
                            UNTIL WS-EOF-K820 = 'Y'                             
               WHEN 20                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'START ERROR BP13K820.' WS-BP13K820-STAT            
                    MOVE WS-BP13K820-STAT          TO RETURN-CODE               
                    PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT               
            END-EVALUATE.                                                       
                                                                                
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3400-READNEXT-BP13K820.                                                  
      *================================================================*        
                                                                                
           READ BP13K820 NEXT RECORD                                            
                         AT END MOVE 'Y' TO WS-EOF-K820.                        
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3500-PROCESS-BP13K820.                                                   
      *================================================================*        
                                                                                
           IF K820-NUM-REGN = K800-NUM-REGN                                     
              IF K820-NUM-RELATIONSHIP = '09'                                   
                 MOVE K820-NUM-NRIC              TO WS-FIANCE-NRIC              
              END-IF                                                            
                                                                                
              IF K820-NUM-NRIC-SPOUSE NOT = SPACES AND LOW-VALUES               
                 IF K820-NUM-NRIC-SPOUSE NOT = HA-NRIC(1) AND                   
                                               HA-NRIC(2) AND                   
                                               HA-NRIC(3) AND                   
                                               HA-NRIC(4)                       
                    MOVE K820-NUM-NRIC-SPOUSE  TO WS-SPOUSE-NRIC                
                                                                                
                 END-IF                                                         
              END-IF                                                            
                                                                                
              PERFORM 3400-READNEXT-BP13K820   THRU 3400-EXIT                   
           ELSE                                                                 
               MOVE 'Y'                          TO WS-EOF-K820                 
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *=============================================================*           
       3600-WRITE-CG-DEBAR.                                                     
      *=============================================================*           
                                                                                
           PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > 6                      
                IF WS-NRIC-KEY = WS-NUM-NRIC(WS-I) AND                          
                   WS-NUM-NRIC(WS-I) NOT = SPACES AND LOW-VALUES                
                   MOVE SPACES               TO WS-DETAIL-RPT                   
                   MOVE 'Y'                  TO WS-WRITE-CG-OK                  
                                                WS-RESALE-OK                    
                   MOVE WS-NRIC-KEY          TO WS-NRIC-RPT                     
                   MOVE WS-DEBAR(WS-I)       TO WS-DEBAR-RPT                    
                   PERFORM 5100-BODY-EMAIL THRU 5100-EXIT                       
                                                                                
                   ADD 1                     TO WS-WRITTEN-TOTAL                
                ELSE                                                            
                   IF (WS-DEBAR(WS-I) NOT = SPACES AND LOW-VALUES)              
                      MOVE SPACES             TO WS-DETAIL-RPT                  
                      MOVE 'Y'                TO WS-WRITE-CG-OK                 
                      MOVE 'N'                TO WS-RESALE-OK                   
                      MOVE WS-NUM-NRIC(WS-I)  TO WS-NRIC-RPT                    
                      MOVE WS-DEBAR(WS-I)     TO WS-DEBAR-RPT                   
                      PERFORM 5100-BODY-EMAIL THRU 5100-EXIT                    
                                                                                
                      ADD 1                   TO WS-WRITTEN-TOTAL               
                                                 WS-SKIP-CG                     
                   END-IF                                                       
                END-IF                                                          
           END-PERFORM.                                                         
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       4000-CHECK-BB24.                                                         
      *================================================================*        
           MOVE   'N'       TO      WS-WRITE-RECORD.                            
                                                                                
           IF K800-NUM-NRIC1  NOT = SPACES AND LOW-VALUES                       
              MOVE    K800-NUM-NRIC1          TO WS-NRIC-KEY                    
              PERFORM 4100-START-BB24K510     THRU 4100-EXIT                    
           END-IF.                                                              
                                                                                
           IF (K800-NUM-NRIC2  NOT = SPACES AND LOW-VALUES)  AND                
              (WS-WRITE-RECORD NOT =  'Y')                                      
              MOVE    K800-NUM-NRIC2          TO WS-NRIC-KEY                    
              PERFORM 4100-START-BB24K510     THRU 4100-EXIT                    
           END-IF.                                                              
                                                                                
           IF (K800-NUM-NRIC3  NOT = SPACES AND LOW-VALUES) AND                 
              (WS-WRITE-RECORD NOT =  'Y')                                      
              MOVE    K800-NUM-NRIC3          TO WS-NRIC-KEY                    
              PERFORM 4100-START-BB24K510     THRU 4100-EXIT                    
           END-IF.                                                              
                                                                                
           IF (K800-NUM-NRIC4 NOT = SPACES AND LOW-VALUES) AND                  
              (WS-WRITE-RECORD NOT =  'Y')                                      
              MOVE    K800-NUM-NRIC4          TO WS-NRIC-KEY                    
              PERFORM 4100-START-BB24K510     THRU 4100-EXIT                    
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4100-START-BB24K510.                                                     
      *================================================================*        
                                                                                
           MOVE SPACES                    TO   K510-KEY-FLD.                    
           MOVE 'N'                       TO   WS-BB24K510-EOF.                 
           MOVE WS-NRIC-KEY               TO   K510-NUM-UIN.                    
                                                                                
           START BB24K510 KEY IS >= K510-KEY-FLD.                               
           EVALUATE WS-BB24K510-STAT                                            
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 4200-READNEXT-BB24K510 THRU 4200-EXIT               
                        UNTIL WS-BB24K510-EOF = 'Y'                             
               WHEN 23                                                          
                    MOVE    'N'  TO    WS-WRITE-RECORD                          
                    DISPLAY 'NOT FOUND ON BB24K510 = ' WS-NRIC-KEY              
               WHEN OTHER                                                       
                    DISPLAY 'ERROR STARTBR BB24K510..' WS-BB24K510-STAT         
                    MOVE WS-BB24K510-STAT       TO   RETURN-CODE                
                    PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4200-READNEXT-BB24K510.                                                  
      *================================================================*        
                                                                                
           READ BB24K510 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-BB24K510-STAT                                            
               WHEN 00                                                          
               WHEN 22                                                          
                  IF WS-NRIC-KEY = K510-NUM-UIN                                 
                      PERFORM 4300-READ-BB24K500  THRU 4300-EXIT                
                  ELSE                                                          
                      MOVE 'Y'      TO     WS-BB24K510-EOF                      
                  END-IF                                                        
               WHEN 23                                                          
               WHEN 10                                                          
                  MOVE    'Y'  TO WS-BB24K510-EOF                               
                  MOVE    'N'  TO WS-WRITE-RECORD                               
                  DISPLAY 'NOT FOUND ON BB24K510 = ' WS-NRIC-KEY                
               WHEN OTHER                                                       
                 DISPLAY 'ERROR OPENING BB24K510..' WS-BB24K510-STAT            
                 MOVE WS-BB24K510-STAT             TO RETURN-CODE               
                 PERFORM 9000-CLOSE-ROUTINE        THRU 9000-EXIT               
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4300-READ-BB24K500.                                                      
      *================================================================*        
                                                                                
           MOVE SPACES              TO   K500-KEY-FLD.                          
                                                                                
           MOVE K510-NUM-HDB-REF    TO   K500-NUM-HDB-REF.                      
           MOVE K510-DTE-APPROVAL   TO   K500-DTE-APPROVAL.                     
                                                                                
           READ BB24K500.                                                       
                                                                                
           EVALUATE WS-BB24K500-STAT                                            
             WHEN 00                                                            
             WHEN 02                                                            
                                                                                
               MOVE K500-CDE-RESALE-STATUS  TO  WS-VALID-RESALE-STATUS          
               IF (WS-VLD-RSL-STAT)  AND                                        
                  (K510-CDE-STATUS  NOT = 'B')                                  
                  IF K510-CDE-STATUS = 'P'                                      
                     PERFORM 4400-CHECK-SPOUSE  THRU  4400-EXIT                 
                     GO TO 4300-EXIT                                            
                  END-IF                                                        
                  MOVE  'Y'     TO   WS-WRITE-RECORD                            
                  MOVE  'Y'     TO   WS-BB24K510-EOF                            
               ELSE                                                             
                  MOVE  'N'     TO   WS-WRITE-RECORD                            
                  DISPLAY 'NRIC / RESALE CODE = '  WS-NRIC-KEY  ' / '           
                                                 K500-CDE-RESALE-STATUS         
                  DISPLAY 'CODE STATUS ' K510-CDE-STATUS                        
               END-IF                                                           
             WHEN 23                                                            
               MOVE    'N'  TO   WS-WRITE-RECORD                                
               DISPLAY 'NOT FOUND ON BB24K500 = ' WS-NRIC-KEY ' / '             
                        K500-NUM-HDB-REF ' / ' K500-DTE-APPROVAL                
             WHEN OTHER                                                         
               DISPLAY 'ERROR READING BB24K500..' WS-BB24K500-STAT              
               MOVE WS-BB24K500-STAT             TO RETURN-CODE                 
               PERFORM 9000-CLOSE-ROUTINE        THRU 9000-EXIT                 
           END-EVALUATE.                                                        
                                                                                
       4300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4400-CHECK-SPOUSE.                                                       
      *================================================================*        
           IF WS-NRIC-KEY  =  K500-NUM-UIN-BUYER1 OR                            
              WS-NRIC-KEY  =  K500-NUM-UIN-BUYER2 OR                            
              WS-NRIC-KEY  =  K500-NUM-UIN-BUYER3 OR                            
              WS-NRIC-KEY  =  K500-NUM-UIN-BUYER4                               
              MOVE  'N'     TO   WS-WRITE-RECORD                                
              DISPLAY 'NRIC IS A BUYER  = '  WS-NRIC-KEY  ' / '                 
                                             K500-CDE-RESALE-STATUS             
           ELSE                                                                 
              MOVE  'Y'     TO   WS-WRITE-RECORD                                
              MOVE  'Y'     TO   WS-BB24K510-EOF                                
           END-IF.                                                              
                                                                                
       4400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4600-PROCESS-P04K010.                                                    
      *================================================================*        
                                                                                
           PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > 6                      
                IF WS-I <= 4                                                    
                   IF HA-NRIC(WS-I) NOT = SPACES AND LOW-VALUES                 
                      MOVE HA-NRIC(WS-I)         TO WS-NUM-NRIC(WS-I)           
                      PERFORM 4700-START-P04K010 THRU 4700-EXIT                 
                   END-IF                                                       
                END-IF                                                          
                                                                                
                IF WS-I = 5                                                     
                  IF (WS-SPOUSE-NRIC NOT = SPACES AND LOW-VALUES) AND           
                     (WS-SPOUSE-NRIC NOT = HA-NRIC(1) AND HA-NRIC(2)            
                                        AND HA-NRIC(3) AND HA-NRIC(4))          
                     MOVE WS-SPOUSE-NRIC        TO WS-NUM-NRIC(WS-I)            
                     PERFORM 4700-START-P04K010 THRU 4700-EXIT                  
                  END-IF                                                        
                END-IF                                                          
                                                                                
                IF WS-I = 6                                                     
                   IF (WS-FIANCE-NRIC NOT = SPACES AND LOW-VALUES) AND          
                      (WS-FIANCE-NRIC NOT = HA-NRIC(1) AND HA-NRIC(2)           
                                        AND HA-NRIC(3) AND HA-NRIC(4))          
                       MOVE WS-FIANCE-NRIC        TO WS-NUM-NRIC(WS-I)          
                       PERFORM 4700-START-P04K010 THRU 4700-EXIT                
                   END-IF                                                       
                END-IF                                                          
           END-PERFORM.                                                         
                                                                                
                                                                                
       4600-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4700-START-P04K010.                                                      
      *================================================================*        
                                                                                
           MOVE SPACES                              TO P04K010-REC.             
           INITIALIZE                                  P04K010-REC.             
                                                                                
           MOVE 'N'                                 TO WS-EOF-K010.             
                                                                                
           MOVE SPACES                              TO K010-PRIME-KEY.          
                                                                                
           MOVE WS-NUM-NRIC(WS-I)                   TO K010-NUM-UINFIN.         
                                                                                
           START P04K010  KEY = K010-NUM-UINFIN.                                
                                                                                
           EVALUATE WS-P04K010-STAT                                             
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 4800-READNEXT-P04K010  THRU 4800-EXIT               
                    PERFORM 4900-PROCESS-P04K010   THRU 4900-EXIT               
                            UNTIL WS-EOF-K010 = 'Y'                             
               WHEN 20                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'START ERROR P04K010..' WS-P04K010-STAT             
                    MOVE WS-P04K010-STAT             TO RETURN-CODE             
                    PERFORM 9000-CLOSE-ROUTINE        THRU 9000-EXIT            
            END-EVALUATE.                                                       
                                                                                
       4700-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4800-READNEXT-P04K010.                                                   
      *================================================================*        
                                                                                
           READ P04K010 NEXT RECORD                                             
                         AT END MOVE 'Y' TO WS-EOF-K010.                        
                                                                                
       4800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       4900-PROCESS-P04K010.                                                    
      *================================================================*        
                                                                                
           IF K010-NUM-UINFIN = WS-NUM-NRIC(WS-I)                               
              IF K010-REASON-CODE  = 'CG'                                       
                 MOVE K010-REASON-CODE              TO WS-DEBAR(WS-I)           
                 MOVE 'Y'                           TO WS-DEBAR-CG              
                 MOVE 'Y'                           TO WS-EOF-K010              
              END-IF                                                            
                                                                                
              PERFORM 4800-READNEXT-P04K010       THRU 4800-EXIT                
           ELSE                                                                 
               MOVE 'Y'                             TO WS-EOF-K010              
           END-IF.                                                              
                                                                                
       4900-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       5000-HEADER-EMAIL.                                                       
      *================================================================*        
                                                                                
               WRITE MAILC38C-REC FROM WS-MAIL-RPT1                             
               WRITE MAILC38C-REC FROM WS-MAIL-RPT2                             
               WRITE MAILC38C-REC FROM WS-MAIL-RPT4                             
               WRITE MAILC38C-REC FROM WS-MAIL-RPT5                             
               WRITE MAILC38C-REC FROM WS-MAIL-RPT5A                            
               WRITE MAILC38C-REC FROM WS-MAIL-RPT5B                            
               WRITE MAILC38C-REC FROM WS-MAIL-RPT5C                            
               WRITE MAILC38C-REC FROM WS-MAIL-RPT6                             
               WRITE MAILC38C-REC FROM WS-MAIL-RPT7                             
               WRITE MAILC38C-REC FROM WS-MAIL-RPT7A                            
               WRITE MAILC38C-REC FROM WS-MAIL-RPT7B                            
               WRITE MAILC38C-REC FROM WS-MAIL-RPT7C                            
               WRITE MAILC38C-REC FROM WS-MAIL-RPT8                             
               WRITE MAILC38C-REC FROM WS-MAIL-RPT9                             
               WRITE MAILC38C-REC FROM WS-MAIL-RPT11                            
               WRITE MAILC38C-REC FROM WS-MAIL-RPT11A                           
               WRITE MAILC38C-REC FROM WS-MAIL-RPT12                            
               WRITE MAILC38C-REC FROM WS-MAIL-RPT12A                           
               WRITE MAILC38C-REC FROM WS-MAIL-RPT13A                           
               WRITE MAILC38C-REC FROM WS-MAIL-RPT13B                           
               WRITE MAILC38C-REC FROM WS-MAIL-RPT13C                           
               WRITE MAILC38C-REC FROM WS-MAIL-RPT13D                           
               WRITE MAILC38C-REC FROM WS-MAIL-RPT13E                           
               WRITE MAILC38C-REC FROM WS-MAIL-RPT13F                           
                                                                                
               STRING 'SUBJECT: RESALE CASES REPORT - '                         
                      'SCHEDULED KEYS COLLECTION FROM '                         
                       WS-CUR-DATE(7:2) '/' WS-CUR-DATE(5:2) '/'                
                       WS-CUR-DATE(1:4) ' TO '                                  
                       WS-PRIO-DD '/' WS-PRIO-MM '/' WS-PRIO-CCYY               
                DELIMITED BY SIZE INTO WS-MAIL-RPT15.                           
                                                                                
               WRITE MAILC38C-REC FROM WS-MAIL-RPT15                            
               STRING WS-CUR-DD '/' WS-CUR-MM '/' WS-CUR-CCYY                   
                      DELIMITED BY SIZE INTO WS-DATE-RPT                        
               WRITE MAILC38C-REC FROM WS-DATE-RPT-DTL                          
               MOVE SPACES TO  MAILC38C-REC                                     
               WRITE   MAILC38C-REC                                             
               WRITE   MAILC38C-REC   FROM  WS-TITLE01-RPT                      
      *        WRITE   MAILC38C-REC   FROM  WS-TITLE02-RPT AFTER 2              
               WRITE   MAILC38C-REC   FROM  WS-TITLE02-RPT                      
               WRITE   MAILC38C-REC   FROM  WS-TITLE03-RPT                      
               WRITE   MAILC38C-REC   FROM  WS-HEADER1-RPT                      
               MOVE SPACES TO  MAILC38C-REC                                     
               WRITE   MAILC38C-REC.                                            
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       5100-BODY-EMAIL.                                                         
      *================================================================*        
                                                                                
            IF WS-WRITE-CG-OK = 'Y' AND WS-RESALE-OK = 'Y'                      
               MOVE K500-NUM-HDB-REF        TO WS-SCHEME-A                      
               MOVE K500-CDE-RESALE-STATUS  TO WS-RSL-STATUS1                   
               MOVE K500-CDE-RESALE-STATUS2 TO WS-RSL-STATUS2                   
            ELSE                                                                
               MOVE SPACES                  TO WS-SCHEME-A                      
                                               WS-RSL-STATUS1                   
                                               WS-RSL-STATUS2                   
            END-IF.                                                             
                                                                                
                                                                                
            MOVE F454-NUM-REGN      TO    WS-REGN-DTL.                          
            MOVE F454-DTE-APPT      TO    WS-DATE-DTL.                          
            MOVE F454-TME-APPT      TO    WS-TIME-DTL.                          
            MOVE WS-NRIC-RPT        TO    WS-APPL-UIN.                          
                                                                                
            MOVE K800-NUM-SCH-ACC   TO    WS-SCHEME-B.                          
            MOVE K800-NUM-ALLO-CAT  TO    WS-ALC-MODE.                          
            MOVE K800-NUM-FLAT-TYPE TO    WS-FLAT-TYPE.                         
            MOVE WS-DEBAR-RPT             TO WS-DEB-CODE.                       
                                                                                
            WRITE MAILC38C-REC      FROM  WS-DETAIL-RPT.                        
                                                                                
       5100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       5200-CALL-BP13C913.                                                      
      *================================================================*        
                                                                                
           MOVE K800-NUM-SCH-ACC     TO WS-LINK-REC.                            
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC, BP13COMM8-REC.                    
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
             WHEN 000                                                           
                  PERFORM 5300-SEARCH-DESIGN-DESC THRU 5300-EXIT                
             WHEN OTHER                                                         
                  MOVE  SPACES                      TO WS-DESIGN                
           END-EVALUATE.                                                        
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       5300-SEARCH-DESIGN-DESC.                                                 
      *================================================================*        
                                                                                
           MOVE COMM8-CDE-MODL-DESG     TO WS-DCODE(1:2).                       
           MOVE COMM8-CDE-CONFIG-DESG   TO WS-DCODE(3:1).                       
                                                                                
           SET WS-DSGN-IND TO 1.                                                
           SEARCH WS-DESIGN-ENTRY                                               
               AT END                                                           
                  MOVE SPACES           TO WS-DESIGN                            
             WHEN WS-DSGN-CDE(WS-DSGN-IND) = WS-DCODE                           
                  MOVE WS-DSGN-TEXT(WS-DSGN-IND)                                
                                        TO WS-DESIGN.                           
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       9000-CLOSE-ROUTINE.                                                      
      *================================================================*        
            DISPLAY 'TOTAL VALID RECORD WRITTEN   : ' WS-WRITTEN-TOTAL          
            DISPLAY 'TOTAL VALID RECORD SKIPPED   : ' WS-SKIP-TOTAL             
            DISPLAY 'TOTAL VALID RECORD CG DEBAR  : ' WS-SKIP-CG.               
                                                                                
            CLOSE BP13F454                                                      
                  BP13K800                                                      
                  BP13K820                                                      
                  P04K010                                                       
                  BB24K510                                                      
                  BB24K500                                                      
                  MAILC38C.                                                     
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
