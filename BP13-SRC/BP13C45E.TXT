       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C45E.                                                 
      *AUTHOR.        KSJ3.                                                     
      *DATE-WRITTEN.  24/10/2016.                                               
      * ========================================================== *            
      * OBJECTIVE    : DAILY PROGRAM TO EXTRACT REGNS SENT FOR     *            
      *                SELF-BOOKING FOR THE DAY, AND CREATE        *            
      *                BP13F203 AND BP13F23B RECORDS TO SEND SMS   *            
      *                AND EMAIL NOTIFICATIONS ON 9AM THE NEXT     *            
      *                WORKING DAY, AND BP13FC2A TO CREATE BP13KC20*            
      *                RECORD AFTER NOTIFICATION                   *            
      * ========================================================== *            
      * INPUT  FILES :  1.) BP13FC25                               *            
      *                 2.) BP13K800                               *            
      *                 3.) BP13K820                               *            
      *                 4.) BP13K857                               *            
      *                 5.) BP13K435                               *            
      *                 6.) BM06K110                               *            
      *                 7.) BP13K022                               *            
      *                 8.) BP13K024                               *            
      * OUTPUT FILES :  1.) BP13F203                               *            
      *                 2.) BP13F23B                               *            
      *                 3.) BP13FC2A                               *            
      * LISTING      :  1.) BP13L45E                               *            
      * ========================================================== *            
      * CHG-NO   BY   DATE       DESCRIPTION                       *            
      * -------- ---  ------     -----------                       *            
      * BP136561 KSJ3 24/10/2016 NEW PROGRAM                       *            
      * BP138334 KAC1 01/06/2020 TO CHANGE THE NO OF SELF BK FROM  *            
      *                          3 DAYS TO 10 DAYS (WS-BOOK-DAYS)  *            
      * BP138334 KAC1 15/06/2020 TO INCREASE FROM 50 TO 100 CASES  *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FC25  ASSIGN        TO BP13FC25.                          
                                                                                
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-REGN                      
                            FILE STATUS   IS WS-K800-STATUS.                    
                                                                                
           SELECT BP13K820  ASSIGN        TO BP13K820                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K820-KEY-FLD                       
                            FILE STATUS   IS WS-K820-STATUS.                    
                                                                                
           SELECT BP13K857  ASSIGN        TO BP13K857                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K857-KEY-FLD                       
                            FILE STATUS   IS WS-K857-STATUS.                    
                                                                                
           SELECT BP13K022  ASSIGN        TO BP13K022                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K022-KEY-FLD                       
                            ALTERNATE KEY IS K022-NUM-REGN                      
                            FILE STATUS   IS WS-K022-STATUS.                    
                                                                                
           SELECT BP13K024  ASSIGN        TO BP13K024                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K024-KEY-FLD                       
                            FILE STATUS   IS WS-K024-STATUS.                    
                                                                                
           SELECT BP13K435  ASSIGN        TO BP13K435                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K435-KEY-FLD                       
                            FILE STATUS   IS WS-K435-STATUS.                    
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K110-KEY-FLD                       
                            FILE STATUS   IS WS-K110-STATUS.                    
                                                                                
           SELECT BP13F203  ASSIGN        TO BP13F203.                          
                                                                                
           SELECT BP13F23B  ASSIGN        TO BP13F23B.                          
                                                                                
           SELECT BP13FC2A  ASSIGN        TO BP13FC2A.                          
                                                                                
           SELECT BP13L45E  ASSIGN        TO BP13L45E.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13FC25                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 150 CHARACTERS.                                      
           COPY BP13FC25.                                                       
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
           COPY BP13K800.                                                       
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
           COPY BP13K820.                                                       
                                                                                
       FD  BP13K857                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
           COPY BP13K857.                                                       
                                                                                
       FD  BP13K022                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
           COPY BP13K022.                                                       
                                                                                
       FD  BP13K024                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
           COPY BP13K024.                                                       
                                                                                
       FD  BP13K435                                                             
           RECORD CONTAINS 250 CHARACTERS.                                      
           COPY BP13K435.                                                       
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
           COPY BM06K110.                                                       
                                                                                
       FD  BP13F203                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 200 CHARACTERS.                                      
           COPY BP13F203.                                                       
                                                                                
       FD  BP13F23B                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 200 CHARACTERS.                                      
           COPY BP13F23B.                                                       
                                                                                
       FD  BP13FC2A                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 150 CHARACTERS.                                      
       01  BP13FC2A-REC                  PIC X(150).                            
                                                                                
       FD  BP13L45E                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 132  CHARACTERS.                                     
       01  BP13L45E-REC                  PIC X(132).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
      *-------------------------------------------------------------            
      *    AV02C002 COMM-AREA                                                   
      *-------------------------------------------------------------            
       COPY AV02COMM.                                                           
                                                                                
      *-------------------------------------------------------------            
      *    BP13C913 COMM-AREA                                                   
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH       PIC X(4)   VALUE SPACES.               
               10  WS-LINK-NUM-SCH       PIC X(5)   VALUE SPACES.               
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *-------------------------------------------------------------            
      *        NO OF DAYS BUYER IS ALLOWED TO BOOK APPOINTMENT                  
      *        NO OF DAYS BUYER IS ALLOWED TO BOOK APPOINTMENT TO 10            
      *-------------------------------------------------------------            
      *77  WS-BOOK-DAYS                  PIC 9(2)   VALUE 03.                   
       77  WS-BOOK-DAYS                  PIC 9(2)   VALUE 10.                   
      *-------------------------------------------------------------            
      *        NO OF APPT DAYS BUYER IS ALLOWED TO CHOOSE FROM                  
      *-------------------------------------------------------------            
       77  WS-APPT-DAYS                  PIC 9(2)   VALUE 10.                   
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K800-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K820-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K857-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K022-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K024-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K435-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K110-STATUS            PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-FC25-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-FC25-SKIP          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F203-WRITE         PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F23B-WRITE         PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-FC2A-WRITE         PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-KC20-DEL           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-L45E-WRITE         PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-L45E-LINE          PIC 9(2)   VALUE 60.                   
           05  WS-CNT-L45E-PAGE          PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-L45E-SNO           PIC 9(5)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-FC25-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K800-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K820-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K857-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K022-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K024-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K435-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K435-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K110-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-PIDB-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-HP-FND                 PIC X(1)   VALUE 'N'.                  
           05  WS-EMAIL-FND              PIC X(1)   VALUE 'N'.                  
           05  WS-DTE-VALID-FL             PIC X(1).                            
               88  WS-DTE-VALID-Y                     VALUE 'Y'.                
               88  WS-DTE-VALID-N                     VALUE 'N'.                
           05  WS-CHECK-HOL-FL             PIC X(1).                            
               88  WS-CHECK-HOL-Y                     VALUE 'Y'.                
               88  WS-CHECK-HOL-N                     VALUE 'N'.                
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE SPACES.               
           05  WS-DTE-ADD                PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-ADD-INT            PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-SMS-SENT           PIC X(8)   VALUE ZEROES.               
           05  WS-DTE-BOOK-PD-END        PIC X(8)   VALUE ZEROES.               
           05  WS-TIME-RUN               PIC X(8)   VALUE ZEROES.               
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-NUM-DESC               PIC X(60)  VALUE SPACES.               
           05  WS-NUM-DAYS               PIC 9(2)   VALUE ZEROES.               
           05  WS-SQL-CODE               PIC 9(4)   VALUE 0.                    
           05  WS-K435-DTE-KEY-BORROW    PIC X(8)   VALUE SPACES.               
           05  WS-K435-NUM-KEY-LOCN      PIC X(4)   VALUE SPACES.               
                                                                                
      *-------------------------------------------------------------            
      *    REPORT LAYOUT FOR EXCEPTIONS                                         
      *-------------------------------------------------------------            
       01  L45E-PR-HEAD-01.                                                     
           05  FILLER                    PIC X(14)   VALUE 'BP13L45E'.          
           05  FILLER                    PIC X(8)    VALUE 'HDB3    '.          
           05  FILLER                    PIC X(25)   VALUE SPACES.              
           05  FILLER                    PIC X(39)   VALUE                      
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                    PIC X(13)   VALUE SPACES.              
           05  FILLER                    PIC X(7)    VALUE 'DATE: '.            
           05  L45E-DTE-CURRENT          PIC X(10)   VALUE SPACES.              
           05  FILLER                    PIC X(3)    VALUE SPACES.              
           05  FILLER                    PIC X(7)    VALUE 'PAGE: '.            
           05  L45E-PAGE                 PIC ZZZ9.                              
                                                                                
       01  L45E-PR-HEAD-02.                                                     
           05  FILLER                    PIC X(41)   VALUE SPACES.              
           05  FILLER                    PIC X(50)   VALUE                      
              'EXCEPTION REPORT ON CREATING SELF BOOKING SMS FILE'.             
                                                                                
       01  L45E-PR-HEAD-03.                                                     
           05  FILLER                    PIC X(132)  VALUE                      
              '    S/NO   REGN      ERROR DESCRIPTION'.                         
                                                                                
       01  L45E-PR-DTL.                                                         
           05  FILLER                    PIC X(4)    VALUE SPACES.              
           05  L45E-SNO                  PIC ZZZZ9.                             
           05  FILLER                    PIC X(2)    VALUE SPACES.              
           05  L45E-NUM-REGN             PIC X(8).                              
           05  FILLER                    PIC X(2)    VALUE SPACES.              
           05  L45E-NUM-DESC             PIC X(60).                             
                                                                                
       01  L45E-PR-DASH-LINE.                                                   
           05  FILLER                    PIC X(4)    VALUE SPACES.              
           05  FILLER                    PIC X(90)   VALUE ALL '-'.             
                                                                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
           PERFORM 2000-BP13FC25-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-FC25-EOF = 'Y'                                      
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13FC25                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BP13K857                                                 
                       BP13K022                                                 
                       BP13K024                                                 
                       BP13K435                                                 
                       BM06K110                                                 
                OUTPUT BP13F203                                                 
                       BP13F23B                                                 
                       BP13FC2A                                                 
                       BP13L45E.                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K820 ERROR ' WS-K820-STATUS                  
              MOVE     WS-K820-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K857-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K857 ERROR ' WS-K857-STATUS                  
              MOVE     WS-K857-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K022 ERROR ' WS-K022-STATUS                  
              MOVE     WS-K022-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K024-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K024 ERROR ' WS-K024-STATUS                  
              MOVE     WS-K024-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K435-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K435 ERROR ' WS-K435-STATUS                  
              MOVE     WS-K435-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    GET CURRENT DATE                                                     
      *-------------------------------------------------------------            
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8           TO   WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
      *-------------------------------------------------------------            
      *    GET NEXT WORKING DATE AS SMS SENT DATE                               
      *-------------------------------------------------------------            
           SET WS-CHECK-HOL-Y              TO   TRUE.                           
           MOVE WS-DTE-CURR-8              TO   WS-DTE-ADD.                     
           MOVE 0                          TO   WS-NUM-DAYS                     
           PERFORM 1110-ADD-DAYS           THRU 1110-EXIT.                      
                                                                                
           MOVE WS-DTE-ADD                 TO   WS-DTE-SMS-SENT.                
                                                                                
      *-------------------------------------------------------------            
      *    COMPUTE BOOKING END DATE                                             
      *-------------------------------------------------------------            
           SET WS-CHECK-HOL-N              TO   TRUE.                           
           MOVE WS-DTE-SMS-SENT            TO   WS-DTE-ADD.                     
           COMPUTE WS-NUM-DAYS = WS-BOOK-DAYS - 1.                              
           PERFORM 1110-ADD-DAYS           THRU 1110-EXIT.                      
                                                                                
           MOVE WS-DTE-ADD                 TO   WS-DTE-BOOK-PD-END.             
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1110-ADD-DAYS.                                                           
      *=============================================================            
                                                                                
           SET WS-DTE-VALID-N              TO   TRUE.                           
                                                                                
           PERFORM UNTIL WS-DTE-VALID-Y                                         
      *-------------------------------------------------------------            
      *    CONVERT DATE TO INTEGER                                              
      *-------------------------------------------------------------            
              COMPUTE WS-DTE-ADD-INT = FUNCTION INTEGER-OF-DATE                 
                                       (WS-DTE-ADD)                             
                                                                                
      *-------------------------------------------------------------            
      *    ADD ACTUAL NUMBER OF DAYS                                            
      *-------------------------------------------------------------            
              ADD WS-NUM-DAYS              TO   WS-DTE-ADD-INT                  
                                                                                
      *-------------------------------------------------------------            
      *    CONVERT INTEGER TO DATE                                              
      *-------------------------------------------------------------            
              COMPUTE WS-DTE-ADD     = FUNCTION DATE-OF-INTEGER                 
                                       (WS-DTE-ADD-INT)                         
                                                                                
      *-------------------------------------------------------------            
      *    CHECK FOR HOLIDAY / EVE OF PUBLIC HOLIDAY                            
      *-------------------------------------------------------------            
              IF WS-CHECK-HOL-Y                                                 
                 MOVE SPACES               TO   LINK-REC                        
                 MOVE 'HD'                 TO   WS-IN-OPTION                    
                 MOVE WS-DTE-ADD           TO   WS-IN-DATE                      
                 PERFORM 8000-CALL-AV02C002     THRU 8000-EXIT                  
                                                                                
                 IF WS-RET-DAY = 'HOL' OR 'EVE'                                 
                    MOVE 1                 TO   WS-NUM-DAYS                     
                 ELSE                                                           
                    SET WS-DTE-VALID-Y     TO   TRUE                            
                 END-IF                                                         
              ELSE                                                              
                 SET WS-DTE-VALID-Y        TO   TRUE                            
              END-IF                                                            
                                                                                
           END-PERFORM.                                                         
                                                                                
       1110-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13FC25-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13FC25                                                        
              AT END MOVE 'Y'              TO   WS-FC25-EOF.                    
                                                                                
      *    IF WS-CNT-FC2A-WRITE >= 50                                           
           IF WS-CNT-FC2A-WRITE >= 100                                          
              MOVE 'Y'                     TO   WS-FC25-EOF                     
           END-IF.                                                              
                                                                                
           IF WS-FC25-EOF NOT = 'Y'                                             
              ADD 1                        TO   WS-CNT-FC25-READ                
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           MOVE 'N'                        TO   WS-HP-FND WS-EMAIL-FND.         
                                                                                
           IF FC25-NUM-STATUS NOT = 'A'                                         
              GO TO 3000-CONTINUE-READ-NEXT.                                    
                                                                                
      *-------------------------------------------------------------            
      *    GET SCHEME ACCOUNT FROM SOC MASTER                                   
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   BP13K800-MASTER.                
           MOVE FC25-NUM-REGN              TO   K800-NUM-REGN.                  
                                                                                
           PERFORM 3100-BP13K800-READ      THRU 3100-EXIT.                      
                                                                                
           IF WS-K800-FND = 'N'                                                 
              MOVE 'REGN NOT FOUND IN SOC MASTER'                               
                                           TO   WS-NUM-DESC                     
              PERFORM 7900-PRINT-ERROR     THRU 7900-EXIT                       
              GO TO 3000-CONTINUE-READ-NEXT                                     
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    CHECK KEY LOCATION                                                   
      *-------------------------------------------------------------            
                                                                                
           MOVE 'N'                        TO   WS-K435-EOF                     
                                                WS-K435-FND.                    
           MOVE SPACES                     TO   BP13K435-REC.                   
           MOVE K800-NUM-SCH-ACC           TO   K435-NUM-HDB-REF.               
                                                                                
           PERFORM 3210-BP13K435-STARTBR   THRU 3210-EXIT.                      
           PERFORM 3220-BP13K435-READNEXT  THRU 3220-EXIT                       
                   UNTIL WS-K435-EOF = 'Y'.                                     
                                                                                
           IF WS-K435-FND = 'Y'                                                 
              IF WS-K435-NUM-KEY-LOCN NOT = 'SOU '                              
                 MOVE 'KEY HAS BEEN BORROWED'                                   
                                           TO   WS-NUM-DESC                     
                 PERFORM 7900-PRINT-ERROR  THRU 7900-EXIT                       
                 GO TO 3000-CONTINUE-READ-NEXT                                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    GET HANDPHONE                                                        
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   BP13K820-REC.                   
           MOVE FC25-NUM-REGN              TO   K820-NUM-REGN.                  
           MOVE K800-NUM-NRIC1             TO   K820-NUM-NRIC.                  
                                                                                
           PERFORM 3300-BP13K820-READ      THRU 3300-EXIT.                      
                                                                                
           IF WS-K820-FND NOT = 'Y'                                             
              MOVE 'REGN NOT FOUND IN SOC OCCUP FILE'                           
                                           TO   WS-NUM-DESC                     
              PERFORM 7900-PRINT-ERROR     THRU 7900-EXIT                       
              GO TO 3000-CONTINUE-READ-NEXT                                     
           END-IF.                                                              
                                                                                
           IF K820-NUM-HP-PGR = SPACES OR LOW-VALUES OR ZEROES                  
              CONTINUE                                                          
           ELSE                                                                 
              IF K820-NUM-HP-PGR(1:1) NOT = '3' AND '8' AND '9'                 
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE 'Y'                  TO   WS-HP-FND                       
                 PERFORM 4000-CREATE-SMS   THRU 4000-EXIT                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    GET EMAIL ADDRESS                                                    
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   BP13K857-REC.                   
           MOVE FC25-NUM-REGN              TO   K857-NUM-REGN.                  
                                                                                
           PERFORM 3400-BP13K857-READ      THRU 3400-EXIT.                      
                                                                                
           IF (WS-K857-FND = 'Y') AND                                           
              (K857-NUM-EMAIL NOT = SPACES AND LOW-VALUES AND ZEROES)           
              MOVE 'Y'                     TO   WS-EMAIL-FND                    
              PERFORM 4100-CREATE-EMAIL    THRU 4100-EXIT                       
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    IF HP OR EMAIL = Y, CREATE BP13FC2A                                  
      *-------------------------------------------------------------            
           IF (WS-HP-FND = 'Y') OR (WS-EMAIL-FND = 'Y')                         
              MOVE WS-DTE-BOOK-PD-END      TO   FC25-DTE-BOOK-PD-END            
              WRITE BP13FC2A-REC           FROM BP13FC25-REC                    
              ADD 1                        TO   WS-CNT-FC2A-WRITE               
           ELSE                                                                 
              MOVE 'NO HANDPHONE AND EMAIL ADDRESS IN FILE'                     
                                           TO   WS-NUM-DESC                     
              PERFORM 7900-PRINT-ERROR     THRU 7900-EXIT                       
              GO TO 3000-CONTINUE-READ-NEXT                                     
           END-IF.                                                              
                                                                                
       3000-CONTINUE-READ-NEXT.                                                 
                                                                                
           PERFORM 2000-BP13FC25-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3100-BP13K800-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K800-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K800-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K800 ERROR '    WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3210-BP13K435-STARTBR.                                                   
      *=============================================================            
                                                                                
           START BP13K435 KEY >= K435-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K435-STATUS                                              
           WHEN 00                                                              
              CONTINUE                                                          
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'Y'                     TO   WS-K435-EOF                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'START BP13K435 ERROR '   WS-K435-STATUS                  
              MOVE    WS-K435-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3210-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3220-BP13K435-READNEXT.                                                  
      *=============================================================            
                                                                                
           READ BP13K435 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K435-STATUS                                              
           WHEN 00                                                              
              IF K435-NUM-HDB-REF NOT = K800-NUM-SCH-ACC                        
                 MOVE 'Y'                  TO   WS-K435-EOF                     
              ELSE                                                              
                 MOVE 'Y'                  TO   WS-K435-FND                     
                 IF K435-DTE-KEY-BORROW > WS-K435-DTE-KEY-BORROW                
                    MOVE K435-DTE-KEY-BORROW                                    
                                           TO   WS-K435-DTE-KEY-BORROW          
                    MOVE K435-NUM-KEY-LOCN TO   WS-K435-NUM-KEY-LOCN            
                 END-IF                                                         
              END-IF                                                            
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'Y'                     TO   WS-K435-EOF                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READNEXT BP13K435 ERROR ' WS-K435-STATUS                 
              MOVE    WS-K435-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3220-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3300-BP13K820-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K820.                                                       
                                                                                
           EVALUATE WS-K820-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K820-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K820-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K820 ERROR '    WS-K820-STATUS                  
              MOVE     WS-K820-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3400-BP13K857-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K857.                                                       
                                                                                
           EVALUATE WS-K857-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K857-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K857-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K857 ERROR '    WS-K857-STATUS                  
              MOVE     WS-K857-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4000-CREATE-SMS.                                                         
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   BP13F203-REC.                   
           MOVE K800-NUM-ALLO-CAT          TO   F203-NUM-ALLO-MODE.             
           MOVE K800-NUM-NEW-TOWN          TO   F203-NUM-NEW-TOWN.              
           MOVE K800-NUM-FLAT-TYPE         TO   F203-NUM-FLAT-TYPE.             
           MOVE 'W1'                       TO   F203-TYPE-APPMT.                
           MOVE K800-NUM-CAT               TO   F203-NUM-ETHNIC.                
           MOVE FC25-NUM-REGN              TO   F203-NUM-REGN.                  
           MOVE K800-DTE-BALLOT            TO   F203-DTE-BALLOT.                
           MOVE K800-DTE-BK-APPT           TO   F203-DTE-APPMT.                 
           MOVE K800-NUM-NT-FT-QUEUE       TO   F203-NUM-QUEUE.                 
           MOVE K800-TME-BK-APPT           TO   F203-TME-APPMT.                 
           MOVE K800-NUM-NRIC1             TO   F203-NUM-NRIC.                  
           MOVE K820-NME-OCCP              TO   F203-NME-NRIC.                  
           MOVE K820-NUM-HP-PGR            TO   F203-NUM-HP.                    
           MOVE 'HA1'                      TO   F203-NUM-APLCNT.                
           MOVE K800-NUM-ZONE              TO   F203-NUM-ZONE.                  
           MOVE 'A'                        TO   F203-NUM-STATUS.                
           MOVE WS-DTE-BOOK-PD-END         TO   F203-DTE-BOOK-PD-END.           
                                                                                
           WRITE BP13F203-REC.                                                  
           ADD 1                           TO   WS-CNT-F203-WRITE.              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4100-CREATE-EMAIL.                                                       
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    GET ACCEPTANCE DATE                                                  
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   K110-REC.                       
           MOVE K800-NUM-SCH-ACC           TO   K110-SCH-ACC-NO.                
                                                                                
           PERFORM 4120-BM06K110-READ      THRU 4120-EXIT.                      
                                                                                
           IF WS-K110-FND NOT = 'Y'                                             
              GO TO 4100-EXIT.                                                  
                                                                                
      *-------------------------------------------------------------            
      *    GET ADDRESS                                                          
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   WS-LINK-REC                     
                                                BP13COMM8-REC.                  
           MOVE K800-NUM-SCH-ACC           TO   WS-LINK-REC.                    
                                                                                
           PERFORM 8100-CALL-BP13C913      THRU 8100-EXIT.                      
                                                                                
           IF WS-PIDB-FND NOT = 'Y'                                             
              GO TO 4100-EXIT.                                                  
                                                                                
      *-------------------------------------------------------------            
      *    GET APPLICATION OIC                                                  
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   BP13K022-REC.                   
           MOVE FC25-NUM-REGN              TO   K022-NUM-REGN.                  
                                                                                
           PERFORM 4130-BP13K022-READ      THRU 4130-EXIT.                      
                                                                                
           IF WS-K022-FND = 'Y'                                                 
              MOVE SPACES                  TO   BP13K024-REC                    
              MOVE K022-NUM-OIC            TO   K024-NUM-OIC                    
                                                                                
              PERFORM 4140-BP13K024-READ   THRU 4140-EXIT                       
                                                                                
              IF WS-K024-FND NOT = 'Y'                                          
                 GO TO 4100-EXIT                                                
              END-IF                                                            
           ELSE                                                                 
              GO TO 4100-EXIT                                                   
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    WRITE ACTUAL RECORD                                                  
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   BP13F23B-REC.                   
           MOVE FC25-NUM-REGN              TO   F23B-NUM-REGN.                  
           MOVE 'W1'                       TO   F23B-NUM-APPT-TYPE.             
           MOVE K857-NUM-EMAIL             TO   F23B-NUM-EMAIL.                 
           MOVE K110-DTE-ACCEPTANCE        TO   F23B-DTE-ACCEPTANCE.            
           MOVE WS-DTE-BOOK-PD-END         TO   F23B-DTE-BOOK-PD-END.           
           MOVE COMM8-NUM-BLK              TO   F23B-NUM-BLK.                   
           MOVE COMM8-NUM-LEVEL            TO   F23B-NUM-LEVEL.                 
           MOVE COMM8-NUM-UNIT-MAIN        TO   F23B-NUM-UNIT-MAIN.             
           MOVE COMM8-NUM-UNIT-SUB         TO   F23B-NUM-UNIT-SUB.              
           MOVE COMM8-NME-STREET           TO   F23B-NME-STREET.                
           MOVE COMM8-NUM-POSTAL-CODE      TO   F23B-NUM-POSTAL-CODE.           
           MOVE K024-NME-OIC               TO   F23B-NME-OIC.                   
                                                                                
           WRITE BP13F23B-REC.                                                  
           ADD 1                           TO   WS-CNT-F23B-WRITE.              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4120-BM06K110-READ.                                                      
      *=============================================================            
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K110-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K110-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BM06K110 ERROR '    WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4120-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4130-BP13K022-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K022-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              MOVE 'Y'                     TO   WS-K022-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K022-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K022 ERROR '    WS-K022-STATUS                  
              MOVE    WS-K022-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4130-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4140-BP13K024-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K024.                                                       
                                                                                
           EVALUATE WS-K024-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              MOVE 'Y'                     TO   WS-K024-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K024-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K024 ERROR '    WS-K024-STATUS                  
              MOVE    WS-K024-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4140-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       7900-PRINT-ERROR.                                                        
      *=============================================================            
                                                                                
           IF WS-CNT-L45E-LINE > 54                                             
              PERFORM 7910-PRINT-ERROR-HDR THRU 7910-EXIT                       
           END-IF.                                                              
                                                                                
           ADD  1                          TO   WS-CNT-L45E-SNO                 
                                                WS-CNT-L45E-LINE                
                                                WS-CNT-FC25-SKIP.               
                                                                                
           MOVE WS-CNT-L45E-SNO            TO   L45E-SNO.                       
           MOVE FC25-NUM-REGN              TO   L45E-NUM-REGN.                  
           MOVE WS-NUM-DESC                TO   L45E-NUM-DESC.                  
                                                                                
           WRITE BP13L45E-REC              FROM L45E-PR-DTL.                    
           ADD 1                           TO   WS-CNT-L45E-WRITE.              
                                                                                
       7900-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       7910-PRINT-ERROR-HDR.                                                    
      *=============================================================            
                                                                                
           ADD 1                           TO   WS-CNT-L45E-PAGE.               
           MOVE WS-CNT-L45E-PAGE           TO   L45E-PAGE.                      
           MOVE WS-DTE-CURR-10             TO   L45E-DTE-CURRENT.               
                                                                                
           WRITE BP13L45E-REC FROM L45E-PR-HEAD-01 AFTER PAGE.                  
           WRITE BP13L45E-REC FROM L45E-PR-HEAD-02.                             
           WRITE BP13L45E-REC FROM L45E-PR-DASH-LINE AFTER 2.                   
           WRITE BP13L45E-REC FROM L45E-PR-HEAD-03.                             
           WRITE BP13L45E-REC FROM L45E-PR-DASH-LINE.                           
                                                                                
           MOVE SPACES                     TO   BP13L45E-REC.                   
           WRITE BP13L45E-REC.                                                  
                                                                                
           MOVE 7                          TO   WS-CNT-L45E-LINE.               
                                                                                
       7910-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8000-CALL-AV02C002.                                                      
      *=============================================================            
                                                                                
           CALL 'AV02C002' USING LINK-REC.                                      
                                                                                
           IF WS-RETURN = 'EID'                                                 
              MOVE 99                      TO   RETURN-CODE                     
              DISPLAY 'ERROR IN DATE : '        WS-IN-DATE                      
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8100-CALL-BP13C913.                                                      
      *=============================================================            
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC, BP13COMM8-REC.                    
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
           WHEN ZEROES                                                          
           WHEN SPACES                                                          
           WHEN LOW-VALUES                                                      
              MOVE 'Y'                     TO   WS-PIDB-FND                     
                                                                                
           WHEN 100                                                             
              MOVE 'N'                     TO   WS-PIDB-FND                     
                                                                                
           WHEN OTHER                                                           
              MOVE 99                      TO   RETURN-CODE                     
              MOVE COMM8-CDE-SYSERR        TO   WS-SQL-CODE                     
              DISPLAY 'ERROR CALLING BP13C913. CODE: ' WS-SQL-CODE              
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       8100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           CLOSE BP13FC25                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K857                                                       
                 BP13K022                                                       
                 BP13K024                                                       
                 BP13K435                                                       
                 BM06K110                                                       
                 BP13F203                                                       
                 BP13F23B                                                       
                 BP13FC2A                                                       
                 BP13L45E.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K820 ERROR ' WS-K820-STATUS                  
              MOVE     WS-K820-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K857-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K857 ERROR ' WS-K857-STATUS                  
              MOVE     WS-K857-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K022 ERROR ' WS-K022-STATUS                  
              MOVE     WS-K022-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K024-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K024 ERROR ' WS-K024-STATUS                  
              MOVE     WS-K024-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K435-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K435 ERROR ' WS-K435-STATUS                  
              MOVE     WS-K435-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13C45E CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13FC25 READ      : ' WS-CNT-FC25-READ.          
           DISPLAY ' 2. NO OF BP13F203 WRITTEN   : ' WS-CNT-F203-WRITE.         
           DISPLAY ' 3. NO OF BP13F23B WRITTEN   : ' WS-CNT-F23B-WRITE.         
           DISPLAY ' 4. NO OF BP13FC2A WRITTEN   : ' WS-CNT-FC2A-WRITE.         
           DISPLAY ' 5. NO OF BP13L45E WRITTEN   : ' WS-CNT-L45E-WRITE.         
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
