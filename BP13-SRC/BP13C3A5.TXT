       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C3A5.                                                 
      *AUTHOR.        KSJ3.                                                     
      *DATE-WRITTEN.  03/05/2016.                                               
      * ========================================================== *            
      * OBJECTIVE    :  TO EXTRACT SERS CASES FROM KIV FILE        *            
      * ========================================================== *            
      * INPUT  FILES :  1.) BP13F300 (KIVAGR)                      *            
      * I-O    FILES :  1.) BP13K300 (AGRORDER)                    *            
      * OUTPUT FILES :  1.) BP13F30A (SUCCESS.SERS)                *            
      * ========================================================== *            
      * CHG-NO   BY   DATE       DESCRIPTION                       *            
      * -------- ---  ------     -----------                       *            
      * BP135833 KSJ3 03/05/2016 NEW PROGRAM                       *            
      * BP135824 KSJ3 26/05/2016 TO BYPASS SA CASES                *            
      * BP138086 KAC1 03/01/2020 TO CATER FOR BANK LOAN SA         *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F300  ASSIGN       TO BP13F300.                           
                                                                                
           SELECT BP13K300  ASSIGN       TO BP13K300                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K300-KEY-FLD                        
                            FILE STATUS  IS WS-K300-STATUS.                     
                                                                                
           SELECT BP13F30A  ASSIGN       TO BP13F30A.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F300                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 2000 CHARACTERS.                                     
           COPY BP13F300.                                                       
                                                                                
       FD  BP13K300                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
           COPY BP13K300.                                                       
                                                                                
       FD  BP13F30A                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 2000 CHARACTERS.                                     
       01  BP13F30A-REC                  PIC X(2000).                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K300-STATUS            PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F300-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F300-SKIP          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F30A-WRT           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K300-UPD           PIC 9(6)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F300-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K300-FND               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-FMT8-CCYY.                                            
                   15  WS-DTE-FMT8-CC    PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-FMT8-YY    PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-FMT8-MM        PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-FMT8-DD        PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-FMT10-DD       PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-FMT10-MM       PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-FMT10-CCYY.                                           
                   15  WS-DTE-FMT10-CC   PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-FMT10-YY   PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE SPACES.               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-BP13F300-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-F300-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13F300                                                 
                I-O    BP13K300                                                 
                OUTPUT BP13F30A.                                                
                                                                                
           IF WS-K300-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K300 ERROR ' WS-K300-STATUS                  
              MOVE     WS-K300-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
                                                                                
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           PERFORM 8100-CONV-DATE-TO-10    THRU 8100-EXIT.                      
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13F300-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13F300                                                        
              AT END MOVE 'Y'              TO   WS-F300-EOF.                    
                                                                                
           IF WS-F300-EOF NOT = 'Y'                                             
              ADD   1                      TO   WS-CNT-F300-READ                
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           IF (F300-IDP-SCH                 = 'HSP') AND                        
      *       (F300-CDE-TRANS           NOT = 'SA')  AND                        
              (F300-DTE-SERS-CONTRA     NOT = SPACES AND LOW-VALUES AND         
                                              ZEROES) AND                       
              (F300-DTE-SERS-CONTRA-TO-BP17 = SPACES OR LOW-VALUES OR           
                                              ZEROES)                           
              MOVE SPACES                  TO   BP13K300-REC                    
              MOVE F300-CUR-REG            TO   K300-CUR-REG                    
              PERFORM 3100-READ-BP13K300   THRU 3100-EXIT                       
                                                                                
              IF WS-K300-FND = 'Y'                                              
                 MOVE WS-DTE-CURR-8   TO   K300-DTE-SERS-CONTRA-TO-BP17         
                                           F300-DTE-SERS-CONTRA-TO-BP17         
                 PERFORM 3200-REWRITE-BP13K300  THRU 3200-EXIT                  
                                                                                
                 PERFORM 3300-WRITE-BP13F30A    THRU 3300-EXIT                  
              ELSE                                                              
                 DISPLAY 'BP13K300 NOT FOUND: ' F300-CUR-REG                    
              END-IF                                                            
           ELSE                                                                 
              ADD 1                        TO   WS-CNT-F300-SKIP                
           END-IF.                                                              
                                                                                
           PERFORM 2000-BP13F300-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3100-READ-BP13K300.                                                      
      *=============================================================            
                                                                                
           READ BP13K300.                                                       
                                                                                
           EVALUATE WS-K300-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K300-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K300-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K300 ERROR '    WS-K300-STATUS                  
              MOVE     WS-K300-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3200-REWRITE-BP13K300.                                                   
      *=============================================================            
                                                                                
           REWRITE BP13K300-REC.                                                
                                                                                
           EVALUATE WS-K300-STATUS                                              
           WHEN 00                                                              
              ADD 1                        TO   WS-CNT-K300-UPD                 
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'REWRITE BP13K300 ERROR ' WS-K300-STATUS                  
              MOVE     WS-K300-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3300-WRITE-BP13F30A.                                                     
      *=============================================================            
                                                                                
           WRITE BP13F30A-REC         FROM BP13F300-REC.                        
           ADD 1                      TO   WS-CNT-F30A-WRT.                     
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8000-CONV-DATE-TO-8.                                                     
      *=============================================================            
      *    INPUT  - WS-DTE-FMT10                                                
      *    OUTPUT - WS-DTE-FMT8                                                 
      *-------------------------------------------------------------            
                                                                                
           MOVE WS-DTE-FMT10-CCYY          TO   WS-DTE-FMT8-CCYY.               
           MOVE WS-DTE-FMT10-MM            TO   WS-DTE-FMT8-MM.                 
           MOVE WS-DTE-FMT10-DD            TO   WS-DTE-FMT8-DD.                 
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8100-CONV-DATE-TO-10.                                                    
      *=============================================================            
      *    INPUT  - WS-DTE-FMT8                                                 
      *    OUTPUT - WS-DTE-FMT10                                                
      *-------------------------------------------------------------            
                                                                                
           MOVE WS-DTE-FMT8-CCYY           TO   WS-DTE-FMT10-CCYY.              
           MOVE WS-DTE-FMT8-MM             TO   WS-DTE-FMT10-MM.                
           MOVE WS-DTE-FMT8-DD             TO   WS-DTE-FMT10-DD.                
                                                                                
       8100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           CLOSE BP13F300                                                       
                 BP13K300                                                       
                 BP13F30A.                                                      
                                                                                
           IF WS-K300-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K300 ERROR ' WS-K300-STATUS                  
              MOVE     WS-K300-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13C3A5 CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13F300 READ      : ' WS-CNT-F300-READ.          
           DISPLAY ' 2. NO OF BP13F300 SKIPPED   : ' WS-CNT-F300-SKIP.          
           DISPLAY ' 3. NO OF BP13F30A WRITTEN   : ' WS-CNT-F30A-WRT.           
           DISPLAY ' 4. NO OF BP13K300 UPDATED   : ' WS-CNT-K300-UPD.           
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
