       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C109.                                                 
      *AUTHOR.        WONG LOK CHENG.                                           
      *DATE-WRITTEN.  09/05/89.                                                 
                                                                                
      *REMARKS.                                                                 
      * ====================================================== *                
      *    OBJECTIVES -                                        *                
      *            (1) TO ADD THE AMOUNT FOR THE VARIOUS FEE   *                
      *                BY GL ACC NUMBER THAT HAVE TO BE POSTED *                
      *                TO GL.                                  *                
      *                                                        *                
      *    FREQUENCY  -                                        *                
      *                                                        *                
      *    INPUT-FILE -                                        *                
      *            BP13F180            DEVICE = DISK;          *                
      *                                RECSIZ = 120;           *                
      *                                ORGANI = SEQ;           *                
      *                                                        *                
      *    OUTPUT-FILE -                                       *                
      *            AG20F900            DEVICE = DISK;          *                
      *                                RECSIZ = 120;           *                
      *                                ACCESS = SEQ;           *                
      * CHG-NO   BY   ON                                       *                
      * ----------------                                       *                
      * SOC-PH8  LSB 230793 - COBOL II                         *                
      * BP130418 RHB 250898 - Y2K COMPLIANCE CHANGES.          *                
      * BP132571 LSB 070704 - BATCH TOTAL FOR CR & DR          *                
      * BP133346 LSB 070209 - BLANK OFF SCH-ACC-NO             *                
      * BP134034 ESD1 100211 CATER FOR INTERFACE FILE TO ERP   *                
      * BP135903 FNP1 300316 CATER FOR LBS/FLEXI CASES         *                
      * ====================================================== *                
           EJECT                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F180   ASSIGN TO BP13F180.                                
           SELECT AG20F900   ASSIGN TO AG20F900.                                
           EJECT                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F180                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 120 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
           COPY BP13F180.                                                       
                                                                                
       FD  AG20F900                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 120 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
           COPY AG20F900.                                                       
                                                                                
           SKIP3                                                                
       WORKING-STORAGE SECTION.                                                 
           SKIP3                                                                
       01  WS-VARIABLES.                                                        
                                                                                
           05  WS-COUNTERS.                                                     
               10  WS-CTR-F180-READ    PIC 9(9)      VALUE 0.                   
               10  WS-CTR-F900-WRITE   PIC 9(9)      VALUE 0.                   
                                                                                
           05 WS-F180-EOF               PIC 9          VALUE 0.                 
                                                                                
           05 WS-OLD-ACC-NO             PIC X(13)      VALUE SPACES.            
           05 WS-OLD-TRANS-DESC         PIC X(25)      VALUE SPACES.            
           05 WS-OLD-TRANS-DESC-FULL    PIC X(40)      VALUE SPACES.            
                                                                                
           05 WS-OLD-GST-RATE           PIC 99V99      VALUE ZEROS.             
           05 WS-GLCODE-CR              PIC S9(11)V99  VALUE ZEROS.             
           05 WS-GLCODE-DR              PIC S9(11)V99  VALUE ZEROS.             
                                                                                
           05 WS-TOTAL-CR               PIC S9(11)V99  VALUE ZEROS.             
                                                                                
       01  WS-F180-GL-REC.                                                      
           05  WS-F180-JR-ENTRY-NO.                                             
               10 WS-F180-JR-SC                 PIC X(2).                       
               10 WS-F180-JR-MM                 PIC X(2).                       
               10 WS-F180-JR-YY                 PIC X(2).                       
           05  WS-F180-PERIOD                   PIC 9(2).                       
           05  WS-F180-TRANS-DATE.                                              
               10 WS-F180-TRANS-DD              PIC 9(2).                       
               10 WS-F180-TRANS-MM              PIC 9(2).                       
               10 WS-F180-TRANS-YY              PIC 9(2).                       
           05  WS-F180-TRANS-DATE-R REDEFINES                                   
               WS-F180-TRANS-DATE               PIC X(6).                       
           05  WS-F180-ACC-NO.                                                  
               10  WS-F180-ACC-NO1              PIC X(13).                      
               10  WS-F180-ACC-NO1-R REDEFINES WS-F180-ACC-NO1.                 
                   15  FILLER                   PIC X(1).                       
                   15  WS-F180-CC-CDE           PIC X(2).                       
                   15  FILLER                   PIC X(10).                      
               10  FILLER                       PIC X(11).                      
           05  WS-F180-DOC-NO                   PIC X(6).                       
           05  WS-F180-CR-AMT                   PIC S9(11)V99.                  
           05  WS-F180-CR-AMT-R REDEFINES                                       
               WS-F180-CR-AMT                   PIC X(13).                      
           05  WS-F180-TRANS-DESC               PIC X(40).                      
           05  FILLER                           PIC X(23).                      
                                                                                
                                                                                
           SKIP1                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       000-MAIN-PROGRAM.                                                        
                                                                                
           PERFORM 100-OPEN-FILES         THRU 100-EXIT.                        
           PERFORM 200-READ-F180          THRU 200-EXIT.                        
           PERFORM 300-PROCESS-GL-REC     THRU 300-EXIT                         
              UNTIL WS-F180-EOF = 1.                                            
           PERFORM 400-WRITE-BATCH        THRU 400-EXIT.                        
           PERFORM 999-CLOSE-FILES        THRU 999-EXIT.                        
                                                                                
       000-EXIT.                                                                
                                                                                
       100-OPEN-FILES.                                                          
           OPEN INPUT  BP13F180                                                 
                OUTPUT AG20F900.                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-F180.                                                           
           READ BP13F180 AT END                                                 
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
           ADD 1 TO WS-CTR-F180-READ.                                           
           MOVE SPACES TO F180-NUM-SCH-ACC.                                     
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      * ADD ALL THE AMTS FOR THE SAME ACCNT-NO                                  
                                                                                
       300-PROCESS-GL-REC.                                                      
                                                                                
           IF WS-OLD-ACC-NO = SPACES                                            
              MOVE F180-ACC-NO        TO WS-OLD-ACC-NO                          
              MOVE F180-AMT-GST-RATE  TO WS-OLD-GST-RATE                        
              MOVE F180-TRANS-DESC(1:25) TO WS-OLD-TRANS-DESC                   
              MOVE F180-TRANS-DESC       TO WS-OLD-TRANS-DESC-FULL              
           END-IF.                                                              
                                                                                
           IF WS-OLD-ACC-NO = F180-ACC-NO AND                                   
              WS-OLD-GST-RATE = F180-AMT-GST-RATE AND                           
              WS-OLD-TRANS-DESC-FULL = F180-TRANS-DESC                          
              PERFORM 340-ACCUMULATE-GL-AMT   THRU  340-EXIT                    
           ELSE                                                                 
              PERFORM 350-WRITE-GL-REC THRU 350-EXIT                            
              MOVE ZEROS TO WS-GLCODE-CR                                        
              MOVE ZEROS TO WS-GLCODE-DR                                        
              PERFORM 340-ACCUMULATE-GL-AMT   THRU  340-EXIT                    
              MOVE F180-ACC-NO TO WS-OLD-ACC-NO                                 
              MOVE F180-AMT-GST-RATE  TO WS-OLD-GST-RATE                        
              MOVE F180-TRANS-DESC(1:25) TO WS-OLD-TRANS-DESC                   
              MOVE F180-TRANS-DESC       TO WS-OLD-TRANS-DESC-FULL.             
                                                                                
           READ BP13F180 AT END                                                 
              MOVE 1 TO WS-F180-EOF                                             
              PERFORM 350-WRITE-GL-REC THRU 350-EXIT                            
              GO TO 300-EXIT.                                                   
           ADD 1 TO WS-CTR-F180-READ.                                           
           MOVE SPACES TO F180-NUM-SCH-ACC.                                     
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       340-ACCUMULATE-GL-AMT.                                                   
                                                                                
           MOVE F180-GL-REC TO WS-F180-GL-REC.                                  
           IF F180-CR-AMT < 0                                                   
              COMPUTE WS-GLCODE-CR = WS-GLCODE-CR + F180-CR-AMT                 
           ELSE                                                                 
              COMPUTE WS-GLCODE-DR = WS-GLCODE-DR + F180-CR-AMT.                
                                                                                
       340-EXIT.                                                                
           EXIT.                                                                
                                                                                
      * WRITE TO GL SUMMARY FILE                                                
       350-WRITE-GL-REC.                                                        
                                                                                
                                                                                
           IF WS-GLCODE-CR < 0                                                  
               MOVE WS-F180-GL-REC     TO AG20F900-REC                          
               MOVE WS-OLD-ACC-NO      TO F900-HDR-ACCT-NO                      
               MOVE WS-GLCODE-CR       TO F900-HDR-DR-CR-AMT                    
               PERFORM  360-WRITE-GST-DET   THRU  360-EXIT                      
               ADD WS-GLCODE-CR        TO WS-TOTAL-CR                           
               WRITE AG20F900-REC                                               
               ADD 1                   TO WS-CTR-F900-WRITE                     
           END-IF.                                                              
                                                                                
           IF WS-GLCODE-DR > 0                                                  
               MOVE WS-F180-GL-REC     TO AG20F900-REC                          
               MOVE SPACES             TO F900-DTL-BANK-POSTG                   
               MOVE WS-OLD-ACC-NO      TO F900-HDR-ACCT-NO                      
               MOVE WS-GLCODE-DR       TO F900-HDR-DR-CR-AMT                    
               PERFORM  360-WRITE-GST-DET   THRU  360-EXIT                      
               WRITE AG20F900-REC                                               
               ADD 1                   TO WS-CTR-F900-WRITE                     
           END-IF.                                                              
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
       360-WRITE-GST-DET.                                                       
                                                                                
           IF WS-OLD-TRANS-DESC     = 'SOC-CR TO SURVEY FEE (SA)' OR            
              WS-OLD-TRANS-DESC     = 'SOC-CR TO SURVEY GST (SA)' OR            
              WS-OLD-TRANS-DESC     = 'SOC-CR TO SURVEY FEE     ' OR            
              WS-OLD-TRANS-DESC     = 'SOC-CR TO SURVEY GST     ' OR            
              WS-OLD-TRANS-DESC     = 'SOC-CREDIT TO CC FEE (SA)' OR            
              WS-OLD-TRANS-DESC     = 'SOC-CREDIT TO GST (SA)   ' OR            
              WS-OLD-TRANS-DESC     = 'SOC-CREDIT TO CC FEE     ' OR            
              WS-OLD-TRANS-DESC     = 'SOC-CREDIT TO GST        '               
              MOVE WS-OLD-GST-RATE   TO  F900-DTL-TAX-RATE                      
           ELSE                                                                 
              MOVE ZEROES            TO  F900-DTL-TAX-RATE                      
           END-IF.                                                              
                                                                                
           IF WS-OLD-TRANS-DESC     = 'SOC-CR TO SURVEY GST (SA)' OR            
              WS-OLD-TRANS-DESC     = 'SOC-CR TO SURVEY GST     ' OR            
              WS-OLD-TRANS-DESC     = 'SOC-CREDIT TO GST (SA)   ' OR            
              WS-OLD-TRANS-DESC     = 'SOC-CREDIT TO GST        '               
              MOVE 'GST'     TO   F900-DTL-GST-INDC                             
           ELSE                                                                 
              MOVE SPACES    TO   F900-DTL-GST-INDC                             
           END-IF.                                                              
           MOVE SPACES       TO   F900-DTL-BANK-POSTG.                          
                                                                                
           IF WS-OLD-ACC-NO(10:4) = '5050' OR '5052' OR '5650' OR '5659'        
                                 OR '5051' OR '5053' OR '5054' OR '5055'        
              MOVE  'EX '            TO   F900-DTL-GST-INDC                     
              MOVE ZEROES            TO   F900-DTL-TAX-RATE                     
           END-IF.                                                              
                                                                                
       360-EXIT.                                                                
           EXIT.                                                                
                                                                                
      * WRITE TO GL SUMMARY FILE                                                
       400-WRITE-BATCH.                                                         
                                                                                
           MOVE SPACES                 TO AG20F900-REC.                         
           MOVE WS-F180-JR-ENTRY-NO    TO F900-HDR-JRNL-NO.                     
           MOVE WS-F180-PERIOD         TO F900-HDR-TRANS-PERIOD.                
           MOVE WS-F180-TRANS-DATE     TO F900-HDR-TRANS-DATE.                  
           MOVE WS-TOTAL-CR            TO F900-HDR-DR-CR-AMT.                   
           MOVE 'T'                    TO F900-HDR-JRNL-TOT-CODE.               
                                                                                
           WRITE AG20F900-REC.                                                  
                                                                                
           ADD 1                       TO WS-CTR-F900-WRITE.                    
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       999-CLOSE-FILES.                                                         
           DISPLAY ' PROGRAM ID : BP13C109'.                                    
           DISPLAY '                      '.                                    
           DISPLAY ' NO OF RECORDS READ F180    = ' WS-CTR-F180-READ.           
           DISPLAY ' NO OF RECORDS WRITTEN F900 = ' WS-CTR-F900-WRITE.          
                                                                                
           CLOSE  BP13F180                                                      
                  AG20F900.                                                     
                                                                                
           STOP RUN.                                                            
       999-EXIT.                                                                
           EXIT.                                                                
                                                                                
