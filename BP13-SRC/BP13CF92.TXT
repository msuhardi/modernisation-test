       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF92.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  10/07/2013.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE    : REPORT FOR SMS, RSL, TRF, MAM & SLS SCHEME  *            
      *                UPDATE K730-NUM-FT2T = Y FOR FT-2T COUPLE   *            
      *                                                            *            
      * INPUT  FILES : BP13F730 - CASES PER REGN                   *            
      *                BP13F205 - NUM-OPTION = '2T'                *            
      *                BP13K558 - BP13.K558.UPGOCCP                *            
      * I-O    FILE  : BP13K730 - UPDATE NUM-FT2T TO 'Y'           *            
      * OUTPUT FILE  : BP13LF92 - GENERATED REPORT                 *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP135002  SMR2  10072013  NEW PROGRAM                      *            
      * BP135148  SMR2  25112013  CHANGE THE REPORT TITLE          *            
      * BP136397  SMR2  15082016  DO NOT PRINT ELIG-SCH = 'FS' BUT *            
      *                           STILL UPDATE TO FT2T = 'Y'       *            
      * BP137179  FNP1  19032018  EXCLUDE SOME PA CASES            *            
      * BP137439  AM25  31082018  CORRECT PAGINATION ISSUE         *            
      * BP137547  FNP1  11102018  ENHANCE EXCLUSION OF PA CASES    *            
      * BP138285  AM25  18052020  CHANGE REPORT ID                 *            
      * BP138374  FNP1  23062020  REMOVE EXTRA CHECK FOR PA CASES  *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN       TO BP13F730.                            
                                                                                
           SELECT BP13F205 ASSIGN       TO BP13F205.                            
                                                                                
           SELECT BP13K558 ASSIGN       TO BP13K558                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K558-KEY-FLD                         
                           FILE STATUS  IS WS-K558-STATUS.                      
                                                                                
           SELECT BP13K730 ASSIGN       TO BP13K730                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K730-NUM-REGN                        
                           FILE STATUS  IS WS-K730-STATUS.                      
                                                                                
           SELECT BP13LF92 ASSIGN       TO BP13LF92.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F730                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  500 CHARACTERS                                     
            RECORDING MODE    IS F                                              
            LABEL RECORD      IS STANDARD.                                      
       COPY BP13F730.                                                           
                                                                                
       FD   BP13F205                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS   80 CHARACTERS                                     
            RECORDING MODE    IS F                                              
            LABEL RECORD      IS STANDARD.                                      
       COPY BP13F205.                                                           
                                                                                
       FD   BP13K558                                                            
            RECORD CONTAINS  600 CHARACTERS.                                    
       COPY BP13K558.                                                           
                                                                                
       FD   BP13K730                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BP13K730.                                                           
                                                                                
       FD   BP13LF92                                                            
            RECORDING MODE    IS F                                              
            RECORD CONTAINS  132 CHARACTERS.                                    
       01   BP13LF92-REC                   PIC X(132).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K558-STATUS              PIC 9(02)   VALUE ZEROES.            
           05  WS-K730-STATUS              PIC 9(02)   VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F730-READ                PIC 9(05)   VALUE ZEROES.            
           05  WS-LF92-SNO                 PIC 9(05)   VALUE ZEROES.            
           05  WS-K730-FOUND               PIC 9(05)   VALUE ZEROES.            
           05  WS-K730-NOTFND              PIC 9(05)   VALUE ZEROES.            
           05  WS-K730-UPDATE              PIC 9(05)   VALUE ZEROES.            
           05  WS-CNT-K558-FND             PIC 9(05)   VALUE ZEROES.            
           05  WS-F730-FS                  PIC 9(05)   VALUE ZEROES.            
           05  WS-F730-PA                  PIC 9(05)   VALUE ZEROES.            
           05  WS-OTH-SCH                  PIC 9(05)   VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F730-EOF                 PIC X(01)   VALUE 'N'.               
           05  WS-K558-EOF                 PIC X(01)   VALUE 'N'.               
           05  WS-K558-FND                 PIC X(01)   VALUE 'N'.               
           05  WS-OCCP-FND                 PIC X(01)   VALUE 'N'.               
           05  WS-LF92-HDR                 PIC X(01)   VALUE 'N'.               
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-CUR-DATE                 PIC X(08)   VALUE SPACES.            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NRIC                     PIC X(09)   VALUE SPACES.            
           05  WS-AP-FLAGS.                                                     
               10  WS-AP-FND           PIC X(01) VALUE SPACES.                  
               10  WS-AP-CDE-HH        PIC X(01) VALUE SPACES.                  
               10  WS-AP-SELLER        PIC X(01) VALUE SPACES.                  
               10  WS-AP-TYPE6         PIC X(01) VALUE SPACES.                  
               10  WS-AP-HG4           PIC X(01) VALUE SPACES.                  
               10  WS-AP-FG            PIC X(01) VALUE SPACES.                  
           05  WS-SP-FLAGS.                                                     
               10  WS-SP-FND           PIC X(01) VALUE SPACES.                  
               10  WS-SP-CDE-HH        PIC X(01) VALUE SPACES.                  
               10  WS-SP-SELLER        PIC X(01) VALUE SPACES.                  
               10  WS-SP-TYPE6         PIC X(01) VALUE SPACES.                  
               10  WS-SP-HG4           PIC X(01) VALUE SPACES.                  
               10  WS-SP-FG            PIC X(01) VALUE SPACES.                  
           05  WS-OCC-DETAILS OCCURS 12 TIMES.                                  
               10  WS-NUM-NRIC         PIC X(09) VALUE SPACES.                  
               10  WS-RELN             PIC X(02) VALUE SPACES.                  
               10  WS-CDE-HH           PIC X(1)  VALUE SPACES.                  
               10  WS-FLAT-SMS         PIC X(11) VALUE SPACES.                  
               10  WS-TYPE-SMS         PIC X(02) VALUE SPACES.                  
               10  WS-DTE-SOLD-SMS     PIC X(08) VALUE SPACES.                  
               10  WS-FLAT-RSL         PIC X(11) VALUE SPACES.                  
               10  WS-STAT-RSL         PIC X(03) VALUE SPACES.                  
               10  WS-DTE-APPR-RSL     PIC X(08) VALUE SPACES.                  
               10  WS-FLAT-TRF         PIC X(11) VALUE SPACES.                  
               10  WS-TYPE-TRF         PIC X(02) VALUE SPACES.                  
               10  WS-DTE-EFF-TRF      PIC X(08) VALUE SPACES.                  
               10  WS-FLAT-MAM         PIC X(11) VALUE SPACES.                  
               10  WS-TYPE-MAM         PIC X(02) VALUE SPACES.                  
               10  WS-FLAT-SLS         PIC X(11) VALUE SPACES.                  
               10  WS-TYPE-SLS         PIC X(02) VALUE SPACES.                  
           05  WS-CHILD-FND            PIC X(01) VALUE SPACES.                  
           05  WS-CHILD-SMS-FND        PIC X(01) VALUE SPACES.                  
           05  WS-CHILD-RSL-FND        PIC X(01) VALUE SPACES.                  
           05  WS-OTHER-OCC-FND        PIC X(01) VALUE SPACES.                  
           05  WS-CNT                  PIC 9(2)  VALUE ZEROES.                  
           05  CNT                     PIC 9(2)  VALUE ZEROES.                  
           05  WS-PAGE-CNT             PIC 9(4)  VALUE ZEROES.                  
           05  WS-LINE-CNT             PIC 9(2)  VALUE ZEROES.                  
           05  WS-PARM1                PIC X(4)  VALUE SPACES.                  
                                                                                
       01 WS-REPORT-VARS.                                                       
          05 LF92-HEADER1.                                                      
             10 FILLER            PIC X(08)  VALUE 'BP13LF92'.                  
             10 LF92-SMODE        PIC X(04)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 FILLER            PIC X(08)  VALUE 'HDBCAT 3'.                  
             10 FILLER            PIC X(39)  VALUE SPACES.                      
             10 FILLER            PIC X(20)  VALUE                              
                'SYSTEM OF COMMITMENT'.                                         
             10 FILLER            PIC X(18)  VALUE SPACES.                      
             10 FILLER            PIC X(7)   VALUE 'PAGE : '.                   
             10 LF92-PAGE         PIC Z,ZZ9  VALUE ZEROES.                      
             10 FILLER            PIC X(5)   VALUE SPACES.                      
             10 FILLER            PIC X(07)  VALUE 'DATE : '.                   
             10 LF92-DATE         PIC X(10)  VALUE SPACES.                      
          05 LF92-HEADER2.                                                      
             10 FILLER            PIC X(27)  VALUE SPACES.                      
             10 FILLER            PIC X(09)  VALUE 'DOUBTFUL '.                 
             10 FILLER            PIC X(47)  VALUE                              
                'REPORT ON FIRST-TIMER/SECOND TIMER APPLICATIONS'.              
          05 LF92-HEADER3.                                                      
             10 FILLER            PIC X(30)  VALUE SPACES.                      
             10 FILLER            PIC X(03)  VALUE 'SMS'.                       
             10 FILLER            PIC X(21)  VALUE SPACES.                      
             10 FILLER            PIC X(03)  VALUE 'RSL'.                       
             10 FILLER            PIC X(22)  VALUE SPACES.                      
             10 FILLER            PIC X(03)  VALUE 'TRF'.                       
             10 FILLER            PIC X(21)  VALUE SPACES.                      
             10 FILLER            PIC X(03)  VALUE 'MAM'.                       
             10 FILLER            PIC X(12)  VALUE SPACES.                      
             10 FILLER            PIC X(03)  VALUE 'SLS'.                       
          05 LF92-HEADER4.                                                      
             10 FILLER            PIC X(52)  VALUE                              
                ' S/NO REG NO   NRIC       REL HDB REF     ST FT SOLD'.         
             10 FILLER            PIC X(50)  VALUE                              
                '  HDB REF     FS  APPROVAL HDB REF     ST EFF SALE'.           
             10 FILLER            PIC X(30)  VALUE                              
                ' HDB REF     ST HDB REF     ST'.                               
          05 LF92-BLANK.                                                        
             10 FILLER            PIC X(132) VALUE SPACES.                      
          05 LF92-RECORD.                                                       
             10 LF92-SNO          PIC X(05)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF92-REGN         PIC X(08)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF92-DETAIL       PIC X(117) VALUE SPACES.                      
          05 LF92-DET-REC.                                                      
             10 LF92-DET-NRIC     PIC X(09)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF92-DET-APPLN    PIC X(01)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF92-DET-REL      PIC X(02)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF92-SMS-HDBREF   PIC X(11)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF92-SMS-ST       PIC X(02)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF92-SMS-DATE     PIC X(08)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF92-RSL-HDBREF   PIC X(11)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF92-RSL-FS       PIC X(03)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF92-RSL-DATE     PIC X(08)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF92-TRF-HDBREF   PIC X(11)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF92-TRF-ST       PIC X(02)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF92-TRF-DATE     PIC X(08)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF92-MAM-HDBREF   PIC X(11)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF92-MAM-ST       PIC X(02)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF92-SLS-HDBREF   PIC X(11)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF92-SLS-ST       PIC X(02)  VALUE SPACES.                      
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ********************                                                      
       0000-MAIN-ROUTINE.                                                       
      ********************                                                      
                                                                                
           ACCEPT WS-PARM1.                                                     
                                                                                
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 1100-READ-BP13F205      THRU 1100-EXIT.                      
           PERFORM 2000-READ-BP13F730      THRU 2000-EXIT                       
             UNTIL WS-F730-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       1000-OPEN-FILES.                                                         
      ******************                                                        
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13F205                                                 
                       BP13K558                                                 
                I-O    BP13K730                                                 
                OUTPUT BP13LF92.                                                
                                                                                
           IF WS-K558-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K558 FILE ERROR ' WS-K558-STATUS             
              MOVE WS-K558-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K730-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K730 ERROR ' WS-K730-STATUS                  
              MOVE WS-K730-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-CUR-DATE.                      
           STRING WS-CUR-DATE(7:2), '/',                                        
                  WS-CUR-DATE(5:2), '/',                                        
                  WS-CUR-DATE(1:4) DELIMITED BY SIZE                            
             INTO LF92-DATE.                                                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       1050-PRINT-HEADING.                                                      
      *********************                                                     
                                                                                
           ADD 1                        TO WS-PAGE-CNT.                         
           MOVE WS-PAGE-CNT             TO LF92-PAGE.                           
           MOVE WS-PARM1                TO LF92-SMODE.                          
                                                                                
           WRITE BP13LF92-REC           FROM LF92-HEADER1 AFTER PAGE.           
           WRITE BP13LF92-REC           FROM LF92-HEADER2.                      
           WRITE BP13LF92-REC           FROM LF92-BLANK.                        
           WRITE BP13LF92-REC           FROM LF92-BLANK.                        
           WRITE BP13LF92-REC           FROM LF92-HEADER3.                      
           WRITE BP13LF92-REC           FROM LF92-HEADER4 AFTER 2.              
                                                                                
           MOVE 6                       TO WS-LINE-CNT.                         
                                                                                
       1050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       1100-READ-BP13F205.                                                      
      *********************                                                     
                                                                                
           READ BP13F205 AT END                                                 
                GO TO 9000-CLOSE-FILES.                                         
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       2000-READ-BP13F730.                                                      
      *********************                                                     
                                                                                
           READ BP13F730 AT END                                                 
                MOVE 'Y'                   TO WS-F730-EOF                       
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                           TO WS-F730-READ.                     
                                                                                
           IF F730-CDE-ELIG-SCH = 'FS '                                         
              ADD 1                        TO WS-F730-FS                        
           ELSE                                                                 
              IF F730-CDE-ELIG-SCH = 'PA '                                      
                 ADD 1                     TO WS-F730-PA                        
              ELSE                                                              
                 ADD 1                     TO WS-OTH-SCH                        
              END-IF                                                            
              PERFORM 3000-PROCESS-RECS THRU 3000-EXIT                          
           END-IF.                                                              
                                                                                
           IF F205-NUM-OPTION = '2T'                                            
              PERFORM 6000-READ-BP13K730   THRU 6000-EXIT                       
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       3000-PROCESS-RECS.                                                       
      ********************                                                      
                                                                                
           MOVE 'N'                        TO WS-K558-FND.                      
                                                                                
           MOVE '1'                        TO LF92-DET-APPLN.                   
           MOVE F730-NUM-NRIC1             TO WS-NRIC.                          
           PERFORM 4000-READ-BP13K558      THRU 4000-EXIT.                      
           IF WS-K558-FND = 'Y'                                                 
      *       ADD 1                        TO WS-LF92-SNO                       
              PERFORM 8000-WRITE-BP13LF92  THRU 8000-EXIT                       
           END-IF.                                                              
           PERFORM 4200-CHECK-OTHER-HA     THRU 4200-EXIT.                      
           PERFORM 5000-STARTBR-K558       THRU 5000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       4000-READ-BP13K558.                                                      
      *********************                                                     
                                                                                
           MOVE SPACES                     TO BP13K558-REC.                     
           INITIALIZE                         BP13K558-REC.                     
                                                                                
           MOVE F730-NUM-REGN              TO K558-NUM-REGN.                    
           MOVE WS-NRIC                    TO K558-NUM-NRIC.                    
                                                                                
           READ BP13K558.                                                       
                                                                                
           EVALUATE WS-K558-STATUS                                              
           WHEN ZEROES                                                          
              MOVE 'Y'                     TO WS-K558-FND                       
              ADD 1                        TO WS-CNT-K558-FND                   
              PERFORM 4100-MOVE-DETAILS    THRU 4100-EXIT                       
                                                                                
           WHEN 23                                                              
              MOVE 'N'                     TO WS-K558-FND                       
      *****   DISPLAY 'RECORD NOT FOUND IN BP13K558 : ' K558-KEY-FLD            
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING ON BP13K558'                               
              MOVE WS-K558-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       4100-MOVE-DETAILS.                                                       
      ********************                                                      
                                                                                
           MOVE K558-NUM-NRIC              TO LF92-DET-NRIC.                    
           MOVE K558-CDE-RELATIONSHIP      TO LF92-DET-REL.                     
           MOVE K558-NUM-HDB-REF-SMS       TO LF92-SMS-HDBREF.                  
           MOVE K558-CDE-SALES-TYPE-SMS    TO LF92-SMS-ST.                      
           MOVE K558-DTE-FLAT-SOLD-SMS     TO LF92-SMS-DATE.                    
           MOVE K558-NUM-HDB-REF-RSL       TO LF92-RSL-HDBREF.                  
           MOVE K558-CDE-FILE-STATUS-RSL   TO LF92-RSL-FS.                      
           MOVE K558-DTE-APPROVAL-RSL      TO LF92-RSL-DATE.                    
           MOVE K558-NUM-HDB-REF-TR        TO LF92-TRF-HDBREF.                  
           MOVE K558-NUM-SALES-TYPE-TR     TO LF92-TRF-ST.                      
           MOVE K558-DTE-EFF-SALE-TR       TO LF92-TRF-DATE.                    
           MOVE K558-NUM-HDB-REF-MAM       TO LF92-MAM-HDBREF.                  
           MOVE K558-NUM-SALES-TYPE-MAM    TO LF92-MAM-ST.                      
           MOVE K558-NUM-HDB-REF-SLS       TO LF92-SLS-HDBREF.                  
           MOVE K558-NUM-SALES-TYPE-SLS    TO LF92-SLS-ST.                      
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       4200-CHECK-OTHER-HA.                                                     
      **********************                                                    
                                                                                
           IF F730-NUM-NRIC2 NOT = SPACES AND LOW-VALUES                        
              IF F730-NUM-NRIC2(1:1) NOT = '#'                                  
                 MOVE '2'                  TO LF92-DET-APPLN                    
                 MOVE F730-NUM-NRIC2       TO WS-NRIC                           
                 PERFORM 4000-READ-BP13K558   THRU 4000-EXIT                    
                 IF WS-K558-FND = 'Y'                                           
                    PERFORM 8000-WRITE-BP13LF92  THRU 8000-EXIT                 
                 END-IF                                                         
              ELSE                                                              
                 GO TO 4200-EXIT                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F730-NUM-NRIC3 NOT = SPACES AND LOW-VALUES                        
              IF F730-NUM-NRIC3(1:1) NOT = '#'                                  
                 MOVE '3'                  TO LF92-DET-APPLN                    
                 MOVE F730-NUM-NRIC3       TO WS-NRIC                           
                 PERFORM 4000-READ-BP13K558   THRU 4000-EXIT                    
                 IF WS-K558-FND = 'Y'                                           
                    PERFORM 8000-WRITE-BP13LF92  THRU 8000-EXIT                 
                 END-IF                                                         
              ELSE                                                              
                 GO TO 4200-EXIT                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F730-NUM-NRIC4 NOT = SPACES AND LOW-VALUES                        
              IF F730-NUM-NRIC4(1:1) NOT = '#'                                  
                 MOVE '4'                  TO LF92-DET-APPLN                    
                 MOVE F730-NUM-NRIC4       TO WS-NRIC                           
                 PERFORM 4000-READ-BP13K558   THRU 4000-EXIT                    
                 IF WS-K558-FND = 'Y'                                           
                    PERFORM 8000-WRITE-BP13LF92  THRU 8000-EXIT                 
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       5000-STARTBR-K558.                                                       
      ********************                                                      
                                                                                
           MOVE SPACES                     TO BP13K558-REC.                     
           INITIALIZE                         BP13K558-REC.                     
                                                                                
           MOVE 'N'                        TO WS-K558-EOF                       
                                              WS-OCCP-FND.                      
                                                                                
           MOVE F730-NUM-REGN              TO K558-NUM-REGN.                    
                                                                                
           START BP13K558 KEY >= K558-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K558-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              PERFORM 5100-READNEXT-K558   THRU 5100-EXIT                       
              PERFORM 5200-PROCESS-K558 THRU 5200-EXIT                          
                    UNTIL WS-K558-EOF = 'Y'                                     
                                                                                
           WHEN 23                                                              
           WHEN 10                                                              
              CONTINUE                                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'START ERROR BP13K558 : ' WS-K558-STATUS                  
                      ' REGN : ' K558-NUM-REGN                                  
              MOVE WS-K558-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       5100-READNEXT-K558.                                                      
      *********************                                                     
                                                                                
           READ BP13K558 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K558-STATUS                                              
           WHEN ZEROES                                                          
              CONTINUE                                                          
                                                                                
           WHEN 23                                                              
           WHEN 10                                                              
      *       DISPLAY 'RECORD NOT FOUND IN BP13K558 : ' K558-KEY-FLD            
              MOVE 'Y'                     TO WS-K558-EOF                       
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING ON BP13K558'                               
              MOVE WS-K558-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       5200-PROCESS-K558.                                                       
      ********************                                                      
                                                                                
           IF F730-NUM-REGN = K558-NUM-REGN                                     
              ADD 1 TO WS-CNT-K558-FND                                          
              IF K558-NUM-NRIC NOT = F730-NUM-NRIC1 AND                         
                                     F730-NUM-NRIC2 AND                         
                                     F730-NUM-NRIC3 AND                         
                                     F730-NUM-NRIC4                             
                 IF WS-OCCP-FND = 'N'                                           
                    MOVE 'O'               TO LF92-DET-APPLN                    
                    MOVE 'Y'               TO WS-OCCP-FND                       
                 ELSE                                                           
                    MOVE SPACES            TO LF92-DET-APPLN                    
                 END-IF                                                         
                 PERFORM 4100-MOVE-DETAILS    THRU 4100-EXIT                    
                 PERFORM 8000-WRITE-BP13LF92  THRU 8000-EXIT                    
              END-IF                                                            
           ELSE                                                                 
              MOVE 'Y'                     TO WS-K558-EOF                       
           END-IF.                                                              
                                                                                
           PERFORM 5100-READNEXT-K558      THRU 5100-EXIT.                      
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *********************                                                     
       6000-READ-BP13K730.                                                      
      *********************                                                     
                                                                                
           MOVE SPACES                      TO BP13K730-REC.                    
           INITIALIZE                          BP13K730-REC.                    
                                                                                
           MOVE F730-NUM-REGN               TO K730-NUM-REGN.                   
                                                                                
           READ BP13K730.                                                       
                                                                                
           EVALUATE WS-K730-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                        TO WS-K730-FOUND                     
              PERFORM 6100-REWRITE-BP13K730 THRU 6100-EXIT                      
                                                                                
           WHEN 23                                                              
              ADD 1                        TO WS-K730-NOTFND                    
      **      DISPLAY 'NUM-REGN NOT FOUND IN K730 : ' F730-NUM-REGN             
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K730   : ' WS-K730-STATUS              
              MOVE WS-K730-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       6100-REWRITE-BP13K730.                                                   
      ************************                                                  
                                                                                
           IF K730-CDE-ELIG-SCH = 'JSE'                                         
              MOVE 'E'                     TO K730-NUM-FT2T                     
           ELSE                                                                 
              MOVE 'Y'                     TO K730-NUM-FT2T                     
           END-IF.                                                              
                                                                                
           REWRITE BP13K730-REC.                                                
                                                                                
           EVALUATE WS-K730-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                        TO WS-K730-UPDATE                    
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR WRITING TO BP13K730. STATUS IS - '                 
                      WS-K730-STATUS                                            
              MOVE WS-K730-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       8000-WRITE-BP13LF92.                                                     
      **********************                                                    
                                                                                
           MOVE SPACES                     TO BP13LF92-REC.                     
                                                                                
           IF WS-LF92-HDR = 'N'                                                 
              MOVE 'Y'                     TO WS-LF92-HDR                       
              PERFORM 1050-PRINT-HEADING THRU 1050-EXIT                         
           ELSE                                                                 
              IF WS-LINE-CNT > 59                                               
                 MOVE ZEROES               TO WS-LINE-CNT                       
                 PERFORM 1050-PRINT-HEADING THRU 1050-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO LF92-RECORD.                      
           IF LF92-DET-APPLN = '1'                                              
              ADD 1                        TO WS-LF92-SNO                       
              MOVE WS-LF92-SNO             TO LF92-SNO                          
              MOVE F730-NUM-REGN           TO LF92-REGN                         
              WRITE BP13LF92-REC           FROM LF92-BLANK                      
              ADD 1                        TO WS-LINE-CNT                       
           ELSE                                                                 
              MOVE SPACES                  TO LF92-SNO                          
                                              LF92-REGN                         
           END-IF.                                                              
           MOVE LF92-DET-REC               TO LF92-DETAIL.                      
           WRITE BP13LF92-REC              FROM LF92-RECORD.                    
           ADD 1                           TO WS-LINE-CNT.                      
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *******************                                                       
       9000-CLOSE-FILES.                                                        
      *******************                                                       
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13CF92 ================*'.              
           DISPLAY '  NO OF F730 RECORDS READ     = ' WS-F730-READ.             
           DISPLAY '  NO OF LF92 RECORDS WRITTEN  = ' WS-LF92-SNO.              
           DISPLAY '  NO OF K730 RECORDS FOUND    = ' WS-K730-FOUND.            
           DISPLAY '  NO OF K730 RECORDS NOT FND  = ' WS-K730-NOTFND.           
           DISPLAY '  NO OF K730 RECORDS UPDATED  = ' WS-K730-UPDATE.           
           DISPLAY '  NO OF K558 RECORDS FOUND    = ' WS-CNT-K558-FND.          
           DISPLAY '  NO OF FS SCHEME NOT PRINTED = ' WS-F730-FS.               
           DISPLAY '  NO OF PA SCHEME             = ' WS-F730-PA.               
           DISPLAY '  NO OF SCHEME OTHER THAN PA/FS=' WS-OTH-SCH.               
           DISPLAY '*==========================================*'.              
                                                                                
           CLOSE BP13F730                                                       
                 BP13F205                                                       
                 BP13K558                                                       
                 BP13K730                                                       
                 BP13LF92.                                                      
                                                                                
           IF WS-K558-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K558 FILE ERROR ' WS-K558-STATUS             
              MOVE WS-K558-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K730-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K730 ERROR ' WS-K730-STATUS                  
              MOVE WS-K730-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
        9000-EXIT.                                                              
            EXIT.                                                               
                                                                                
