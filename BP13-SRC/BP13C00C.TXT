       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C00C.                                                 
      *AUTHOR.        KATHRINA VALENZUELA.                                      
      *DATE-WRITTEN.  25/10/2019.                                               
      **************************************************************            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      **************************************************************            
      * OBJECTIVE : REPORT CONTAINS:                               *            
      *             REGN NO, SCH-ACC, BLK, UNIT NO, KEY ISSUE DATE *            
      *             PROCESS RECORDS IN BP13F410. CALL BP13C913     *            
      *             USING SCH-ACC TO GET BLK/UNIT NO. IN PIDB      *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY   DESCRIPTION                         *            
      * -------- -------- ---- -----------                         *            
      * BP138064 20191025 KV6   NEW PRGM                           *            
      **************************************************************            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F410          ASSIGN BP13F410.                            
                                                                                
           SELECT BP13L00C          ASSIGN BP13L00C.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   BP13F410                RECORD CONTAINS 2000 CHARACTERS             
                                    LABEL RECORDS ARE STANDARD                  
                                    RECORDING MODE IS F.                        
       COPY BP13F410.                                                           
                                                                                
       FD   BP13L00C                RECORD CONTAINS 100 CHARACTERS              
                                    LABEL RECORDS ARE OMITTED                   
                                    RECORDING MODE IS F.                        
       01   BP13L00C-REC            PIC X(100).                                 
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FLAG-N-STATUS.                                                    
           05  WS-F410-EOF-FLAG             PIC X(01)  VALUE 'N'.               
               88  WS-F410-EOF              VALUE 'Y'.                          
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F410-READ                 PIC 9(05)  VALUE ZEROS.             
           05  WS-F410-NOTFND               PIC 9(05)  VALUE ZEROS.             
           05  WS-L00C-WRTN                 PIC 9(05)  VALUE ZEROS.             
                                                                                
      *---------------------------------------------------------------*         
      *                      REPORT DETAILS                           *         
      *---------------------------------------------------------------*         
       01  L00C-HEADER.                                                         
           05  FILLER               PIC X(18)   VALUE SPACES.                   
           05  FILLER               PIC X(63)   VALUE                           
                       'LIST OF CASES WHO COLLECTED KEYS FROM BLK 78 AND        
      -        ' 79 DAWSON ROAD'.                                               
           05  FILLER               PIC X(19)   VALUE SPACES.                   
                                                                                
       01  L00C-LINE                PIC X(100)  VALUE ALL '-'.                  
       01  L00C-SPACES              PIC X(100)  VALUE SPACES.                   
                                                                                
       01  L00C-HEADER1.                                                        
           05  FILLER               PIC X(07)   VALUE 'REGN NO'.                
           05  FILLER               PIC X(08)   VALUE SPACES.                   
           05  FILLER               PIC X(14)   VALUE 'SCHEME ACCOUNT'.         
           05  FILLER               PIC X(06)   VALUE SPACES.                   
           05  FILLER               PIC X(03)   VALUE 'BLK'.                    
           05  FILLER               PIC X(07)   VALUE SPACES.                   
           05  FILLER               PIC X(11)   VALUE 'UNIT NUMBER'.            
           05  FILLER               PIC X(04)   VALUE SPACES.                   
           05  FILLER               PIC X(20)   VALUE                           
                                               'KEYS COLLECTION DATE'.          
           05  FILLER               PIC X(20)   VALUE SPACES.                   
                                                                                
       01  L00C-DETAIL1.                                                        
           05  L00C-REGN            PIC X(08)   VALUE SPACES.                   
           05  FILLER               PIC X(07)   VALUE SPACES.                   
           05  L00C-SCH-ACC         PIC X(11)   VALUE SPACES.                   
           05  FILLER               PIC X(09)   VALUE SPACES.                   
           05  L00C-BLK             PIC X(05)   VALUE SPACES.                   
           05  FILLER               PIC X(05)   VALUE SPACES.                   
           05  L00C-UNIT-NO         PIC X(08)   VALUE SPACES.                   
           05  FILLER               PIC X(07)   VALUE SPACES.                   
           05  L00C-DTE-KEY-ISSUED  PIC X(10)   VALUE SPACES.                   
           05  FILLER               PIC X(30)   VALUE SPACES.                   
                                                                                
      *-----------------------------------------------------------*             
      *        LINKAGE FOR SUB-PROGRAM BP13C913                   *             
      *-----------------------------------------------------------*             
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH      PIC X(04).                              
               10  WS-LINK-NUM-ACC      PIC X(05).                              
                                                                                
       COPY P13COMM8.                                                           
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES           THRU  1000-EXIT.                   
           PERFORM 2000-READ-BP13F410        THRU  2000-EXIT.                   
           PERFORM 3000-PROCESS              THRU  3000-EXIT                    
             UNTIL WS-F410-EOF.                                                 
           PERFORM 9000-CLOSE-FILES          THRU  9000-EXIT.                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT BP13F410                                                  
               OUTPUT BP13L00C.                                                 
                                                                                
           WRITE BP13L00C-REC FROM L00C-HEADER.                                 
           WRITE BP13L00C-REC FROM L00C-SPACES.                                 
                                                                                
           WRITE BP13L00C-REC FROM L00C-LINE.                                   
           WRITE BP13L00C-REC FROM L00C-HEADER1.                                
           WRITE BP13L00C-REC FROM L00C-LINE.                                   
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F410.                                                      
      *-------------------------------------------------------------            
           READ BP13F410                                                        
             AT END                                                             
                MOVE 'Y'                    TO  WS-F410-EOF-FLAG                
            NOT AT END                                                          
                ADD   1                     TO  WS-F410-READ                    
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS.                                                            
      *-------------------------------------------------------------            
           IF F410-NUM-SCH-ACC NOT = SPACES OR LOW-VALUES                       
              PERFORM 3100-CHECK-PIDB            THRU 3100-EXIT                 
              PERFORM 4000-OUTPUT-RECORDS        THRU 4000-EXIT                 
           END-IF                                                               
           PERFORM 2000-READ-BP13F410            THRU 2000-EXIT.                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3100-CHECK-PIDB.                                                         
      *----------------------------------------------------------------*        
                                                                                
           MOVE SPACES                     TO BP13COMM8-REC.                    
           INITIALIZE                         BP13COMM8-REC.                    
                                                                                
           MOVE F410-NUM-SCH-ACC           TO WS-LINK-REC.                      
           CALL 'BP13C913' USING WS-LINK-REC,                                   
                                 BP13COMM8-REC.                                 
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0 AND 100                                  
              DISPLAY 'ERROR IN READING PIDB TABLE  '                           
                      COMM8-CDE-SYSERR                                          
              ADD 1                        TO WS-F410-NOTFND                    
           END-IF.                                                              
           IF COMM8-CDE-SYSERR = 100                                            
              ADD 1                        TO WS-F410-NOTFND                    
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-OUTPUT-RECORDS.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                      TO BP13L00C-REC.                    
           INITIALIZE BP13L00C-REC.                                             
                                                                                
           MOVE F410-NUM-REGN               TO L00C-REGN                        
           MOVE F410-NUM-SCH-ACC            TO L00C-SCH-ACC                     
           MOVE COMM8-NUM-BLK               TO L00C-BLK                         
                                                                                
           STRING '#' COMM8-NUM-LEVEL                                           
                  '-' COMM8-NUM-UNIT-MAIN                                       
           DELIMITED BY SIZE       INTO L00C-UNIT-NO                            
                                                                                
           STRING F410-DTE-KEY-ISSUE(1:4) '/'                                   
                  F410-DTE-KEY-ISSUE(5:2) '/'                                   
                  F410-DTE-KEY-ISSUE(7:2)                                       
           DELIMITED BY SIZE INTO L00C-DTE-KEY-ISSUED                           
                                                                                
           WRITE BP13L00C-REC FROM L00C-DETAIL1.                                
           ADD  1                        TO WS-L00C-WRTN.                       
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           DISPLAY SPACES.                                                      
           DISPLAY '------- BP13C00C -------'.                                  
           DISPLAY ' RECORDS READ    IN BP13F410 : ' WS-F410-READ.              
           DISPLAY ' RECORDS WRITTEN IN P13L00C  : ' WS-L00C-WRTN.              
           DISPLAY SPACES.                                                      
           DISPLAY ' F410 NOT FOUND IN PIDB   : ' WS-F410-NOTFND.               
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE BP13F410                                                       
                 BP13L00C.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
