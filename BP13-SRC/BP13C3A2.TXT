      *-----------------------------------------------------------*             
       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C3A2.                                                 
       AUTHOR.        ESD1.                                                     
      *-----------------------------------------------------------*             
                                                                                
      *===========================================================*             
      *          BP13 - LOAN ELIGIBILITY & ASSESSMENT SYSTEM      *             
      *===========================================================*             
      *                                                           *             
      * OBJECTIVES :  THIS SUBROUTINE WILL UPDATE SOC AFTER       *             
      *               HLE HAS BEEN PROCESSED. CANCEL/REJECTED CASE*             
      *                                                           *             
      *               FILES TO BE UPDATED :                       *             
      *               - BP13K800                                  *             
      *               - BP13K820                                  *             
      *               - BP13K640                                  *             
      *                                                           *             
      *===========================================================*             
      *  CHG-REQ   BY    DATE        DESCRIPTION                  *             
      *  --------  ----  ----------  ---------------------------- *             
      *  BP135420  ESD1  26/09/14    NEW PROGRAM                  *             
      *  BP135683  RJB1  06/04/15    TO ALLOW TO PROCEED FOR CASES*             
      *                              NOT FOUND IN FIS             *             
      *  BP136161  KSJ3  19/04/16    TO ADD NEW EXCEPTION RPT TO  *             
      *                              PRINT MISSING RECS FROM K800 *             
      *                              AND ALLOW NEXT REC PROCESSING*             
      *  BP138047  KAC1  25/11/19 RECOMPILE FOR BP27 COPYBOOK UPDATE            
      *  BP139362  KAC1  11/10/19 CATER FOR REVISED FLD NME IN BP27K800         
      *             K800-NUM-INTEREST -> K800-NUM-APPLICABLE1-INT-RATE          
      *************************************************************             
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP27F340  ASSIGN        TO BP27F340.                          
                                                                                
           SELECT BP27K800  ASSIGN        TO BP27K800                           
                            ACCESS MODE      IS RANDOM                          
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K800-NUM-HLA                    
                                                OF BP27K800-MASTER              
                            FILE STATUS      IS BP27-K800-STAT.                 
                                                                                
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE      IS RANDOM                          
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K800-NUM-REGN                   
                            FILE STATUS      IS BP13-K800-STAT.                 
                                                                                
           SELECT BP13K640  ASSIGN        TO BP13K640                           
                            ACCESS MODE      IS RANDOM                          
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K640-KEY-FLD                    
                            FILE STATUS      IS BP13-K640-STAT.                 
                                                                                
           SELECT BP13K848  ASSIGN        TO BP13K848                           
                            ACCESS MODE      IS RANDOM                          
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K848-KEY-FLD                    
                            FILE STATUS      IS BP13-K848-STAT.                 
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ACCESS MODE      IS RANDOM                          
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K110-KEY-FLD                    
                            FILE STATUS      IS BM06-K110-STAT.                 
                                                                                
           SELECT BP13K820  ASSIGN        TO BP13K820                           
                            ACCESS MODE      IS RANDOM                          
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K820-KEY-FLD                    
                            FILE STATUS      IS BP13-K820-STAT.                 
                                                                                
           SELECT BP13K410  ASSIGN           TO BP13K410                        
                            ACCESS MODE      IS RANDOM                          
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K410-KEY-FLD                    
                            FILE STATUS      IS BP13-K410-STAT.                 
                                                                                
           SELECT BP13L3A2  ASSIGN           TO BP13L3A2.                       
                                                                                
                                                                                
      ****************                                                          
       DATA DIVISION.                                                           
      ****************                                                          
                                                                                
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP27F340                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 50 CHARACTERS.                                       
       COPY BP27F340.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K640                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13K640.                                                           
                                                                                
       FD  BP13K848                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K848.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BP27K800                                                             
           RECORD CONTAINS 1200 CHARACTERS.                                     
       COPY BP27K800.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BP13K410                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K410.                                                           
                                                                                
       FD  BP13L3A2                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  BP13L3A2-REC                  PIC X(132).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-RESP                   PIC 9(04)    VALUE ZEROES.             
           05  WS-RESP2                  PIC 9(04)    VALUE ZEROES.             
           05  WS-NRIC                   PIC X(09)    VALUE SPACES.             
           05  WS-CURR-SELLING-PRICE     PIC S9(7)V99 VALUE ZEROES.             
           05  WS-TEMP-AMT               PIC 9(07)    VALUE ZEROES.             
           05  WS-TEMP-AMT2              PIC 9(07)    VALUE ZEROES.             
           05  WS-FLAT-OK                PIC X(01)    VALUE 'N'.                
           05  WS-K640-FND               PIC X(01)    VALUE 'N'.                
           05  WS-K800-FND               PIC X(01)    VALUE 'N'.                
           05  WS-K410-FND               PIC X(01)    VALUE 'N'.                
                                                                                
           05  WS-COMM-INT-RATE               PIC 9(02)V9(02).                  
           05  WS-COMM-REPAY-PERIOD           PIC 9(02).                        
           05  WS-COMM-MAX-LOAN               PIC 9(06).                        
           05  WS-COMM-MTH-INSTALL            PIC 9(05).                        
           05  WS-INSTAL                      PIC S9(5)V9(8) VALUE ZERO.        
           05  WS-INSTAL-DOL                  PIC S9(07) VALUE ZEROES.          
           05  WS-FAC                         PIC 9(02) VALUE ZEROES.           
           05  WS-INT                         PIC 99V9(8) VALUE ZEROES.         
           05  WS-DIV                         PIC 99V9(8) VALUE ZEROES.         
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY                  PIC 9(04).                          
           05  WS-CUR-MM                    PIC 9(02).                          
           05  WS-CUR-DD                    PIC 9(02).                          
                                                                                
       01  WS-SYSTEM-TIME.                                                      
           05 WS-HH                       PIC 9(2).                             
           05 WS-MM                       PIC 9(2).                             
           05 WS-SS                       PIC 9(2).                             
           05 WS-MI                       PIC 9(2).                             
                                                                                
       01  WS-F340-EOF                    PIC X   VALUE SPACE.                  
       01  WS-FILE-STATUS.                                                      
           05 BP13-K800-STAT              PIC 99    VALUE ZEROS.                
           05 BP27-K800-STAT              PIC 99    VALUE ZEROS.                
           05 BP13-K640-STAT              PIC 99    VALUE ZEROS.                
           05 BP13-K848-STAT              PIC 99    VALUE ZEROS.                
           05 BM06-K110-STAT              PIC 99    VALUE ZEROS.                
           05 BP13-K820-STAT              PIC 99    VALUE ZEROS.                
           05 BP13-K410-STAT              PIC 99    VALUE ZEROS.                
                                                                                
       01  C3A1-COMMAREA.                                                       
           05  C3A1-NUM-REGN             PIC X(08)    VALUE SPACES.             
           05  C3A1-NUM-HLA              PIC X(09)    VALUE SPACES.             
           05  C3A1-NUM-ACTION           PIC X(01)    VALUE SPACES.             
           05  C3A1-NUM-USERID           PIC X(07)    VALUE SPACES.             
                                                                                
       01  WS-RPT-VARS.                                                         
           05  WS-CNT-PAGE               PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-LINE               PIC 9(2)   VALUE 60.                   
           05  WS-CNT-SNO                PIC 9(4)   VALUE ZEROES.               
                                                                                
       01  L3A2-HEADER1.                                                        
           05  FILLER                    PIC X(8)   VALUE 'BP13L3A2'.           
           05  FILLER                    PIC X(4)   VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE 'HDBCAT 3'.           
           05  FILLER                    PIC X(28)  VALUE SPACES.               
           05  FILLER                    PIC X(39)  VALUE                       
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                    PIC X(17)  VALUE SPACES.               
           05  FILLER                    PIC X(6)   VALUE 'DATE: '.             
           05  L3A2-DTE-CURRENT          PIC X(10)  VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE '  PAGE: '.           
           05  L3A2-CNT-PAGE             PIC ZZZ9.                              
                                                                                
       01  L3A2-HEADER2.                                                        
           05  FILLER                    PIC X(39)  VALUE SPACES.               
           05  FILLER                    PIC X(54)  VALUE                       
               'EXCEPTION REPORT ON HLE REJECTION/CANCELLATION IN SOC'.         
                                                                                
       01  L3A2-HEADER3.                                                        
           05  FILLER                    PIC X(132) VALUE                       
               '     S/NO     REGN NO      HLA           TRANS     ERROR        
      -        ' DESCRIPTION'.                                                  
                                                                                
       01  L3A2-HEADER4.                                                        
           05  FILLER                    PIC X(132) VALUE                       
               '     ----     --------     ---------     -----     -----        
      -        '---------------------------------------------'.                 
                                                                                
       01  L3A2-DETAIL.                                                         
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  L3A2-SNO                  PIC ZZZ9.                              
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  L3A2-NUM-REGN             PIC X(8)   VALUE SPACES.               
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  L3A2-NUM-HLA              PIC X(9)   VALUE SPACES.               
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  L3A2-NUM-TRANS            PIC X(2)   VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE SPACES.               
           05  L3A2-NUM-DESC             PIC X(50)  VALUE SPACES.               
                                                                                
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      *------------------------*                                                
       0000-MAIN-LOGIC.                                                         
      *------------------------*                                                
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 2000-READ-INPUT      THRU 2000-EXIT.                         
           PERFORM 3000-PROCESS-DATA    THRU 3000-EXIT                          
           UNTIL WS-F340-EOF = 'Y'.                                             
                                                                                
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       1000-OPEN-ROUTINE.                                                       
      *=============================================================*           
                                                                                
           OPEN INPUT  BP27F340                                                 
                       BP27K800                                                 
                       BM06K110                                                 
                       BP13K410                                                 
                I-O    BP13K800                                                 
                       BP13K640                                                 
                       BP13K820                                                 
                       BP13K848                                                 
                OUTPUT BP13L3A2.                                                
                                                                                
           INITIALIZE  BP13K800-MASTER BP27F340-REC    BP13K848-MASTER          
                       BP27K800-MASTER BP13K820-REC K110-REC                    
                       BP13K640-TRANS-REC                                       
                       BP13K410-REC.                                            
                                                                                
           IF BP13-K410-STAT NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K410 ERROR ' BP13-K410-STAT                  
              MOVE     BP13-K410-STAT      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF BP13-K800-STAT NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K800 ERROR ' BP13-K800-STAT                  
              MOVE     BP13-K800-STAT      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF BP13-K640-STAT NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K640 ERROR ' BP13-K640-STAT                  
              MOVE     BP13-K640-STAT      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF BP13-K848-STAT NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K848 ERROR ' BP13-K848-STAT                  
              MOVE     BP13-K848-STAT      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF BP13-K820-STAT NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K820 ERROR ' BP13-K820-STAT                  
              MOVE     BP13-K820-STAT      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF BP27-K800-STAT NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP27K800 ERROR ' BP27-K800-STAT                  
              MOVE     BP27-K800-STAT      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF BM06-K110-STAT NOT = 00 AND 97                                    
              DISPLAY 'OPENING BM06K110 ERROR ' BM06-K110-STAT                  
              MOVE     BM06-K110-STAT      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CUR-DATE.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       2000-READ-INPUT.                                                         
      *=============================================================*           
                                                                                
            READ BP27F340                                                       
                 AT END MOVE 'Y'  TO WS-F340-EOF                                
                                                                                
            END-READ.                                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       3000-PROCESS-DATA.                                                       
      *=============================================================*           
                                                                                
           MOVE  F340-NUM-REGN       TO  C3A1-NUM-REGN                          
           MOVE  F340-NUM-HLA        TO  C3A1-NUM-HLA                           
           MOVE  'BP13C3A2'          TO  C3A1-NUM-USERID                        
                                                                                
           IF F340-NUM-REGN = SPACES OR LOW-VALUES                              
              CONTINUE                                                          
           ELSE                                                                 
              PERFORM 4000-READ-BP27K800        THRU 4000-EXIT                  
              PERFORM 4100-READ-BP13K800        THRU 4100-EXIT                  
                                                                                
              IF WS-K800-FND = 'Y'                                              
                 PERFORM 4200-READ-BP13K640     THRU 4200-EXIT                  
                 PERFORM 4500-READ-BM06K110     THRU 4500-EXIT                  
                 PERFORM 5000-UPDATE-FILES      THRU 5000-EXIT                  
              ELSE                                                              
                 PERFORM 4600-READ-BP13K410     THRU 4600-EXIT                  
                                                                                
                 MOVE SPACES                    TO   L3A2-NUM-DESC              
                 IF WS-K410-FND = 'Y'                                           
                    MOVE 'KEY HAS ALREADY BEEN ISSUED'                          
                                                TO   L3A2-NUM-DESC              
                 ELSE                                                           
                    MOVE 'SOC RECORD NOT FOUND' TO   L3A2-NUM-DESC              
                 END-IF                                                         
                                                                                
                 PERFORM 8000-PRINT-ERROR       THRU 8000-EXIT                  
              END-IF                                                            
           END-IF.                                                              
           PERFORM 2000-READ-INPUT           THRU 2000-EXIT.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-READ-BP27K800.                                                      
      *---------------------------------------------------------------*         
           MOVE SPACES                   TO BP27K800-MASTER.                    
           INITIALIZE                       BP27K800-MASTER.                    
                                                                                
           MOVE C3A1-NUM-HLA             TO K800-NUM-HLA OF                     
                                            BP27K800-MASTER.                    
           READ BP27K800                                                        
                                                                                
           EVALUATE BP27-K800-STAT                                              
               WHEN 00                                                          
                    CONTINUE                                                    
               WHEN 23                                                          
                    MOVE  BP27-K800-STAT TO RETURN-CODE                         
                    DISPLAY 'RECORD NOT FOUND BP27K800 : ' C3A1-NUM-HLA         
                    PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                  
               WHEN OTHER                                                       
                    MOVE  BP27-K800-STAT TO RETURN-CODE                         
                    DISPLAY 'FILE ERROR  BP27K800 : ' C3A1-NUM-HLA              
                    PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                  
            END-EVALUATE.                                                       
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4100-READ-BP13K800.                                                      
      *---------------------------------------------------------------*         
           MOVE SPACES                   TO BP13K800-MASTER.                    
           INITIALIZE                       BP13K800-MASTER.                    
                                                                                
           MOVE C3A1-NUM-REGN            TO K800-NUM-REGN OF                    
                                            BP13K800-MASTER.                    
           READ BP13K800                                                        
                                                                                
           EVALUATE BP13-K800-STAT                                              
               WHEN 00                                                          
                    MOVE 'Y'             TO WS-K800-FND                         
               WHEN 23                                                          
                    MOVE 'N'             TO WS-K800-FND                         
               WHEN OTHER                                                       
                    MOVE  BP13-K800-STAT TO RETURN-CODE                         
                    DISPLAY 'FILE ERROR  BP13K800 : ' C3A1-NUM-REGN             
                    PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                  
            END-EVALUATE.                                                       
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4200-READ-BP13K640.                                                      
      *---------------------------------------------------------------*         
           MOVE SPACES                   TO BP13K640-TRANS-REC                  
           INITIALIZE                       BP13K640-TRANS-REC.                 
                                                                                
           MOVE C3A1-NUM-REGN            TO K640-REGN-NO                        
                                                                                
           READ BP13K640                                                        
                                                                                
           EVALUATE BP13-K640-STAT                                              
               WHEN 00                                                          
                    MOVE  'Y'   TO  WS-K640-FND                                 
               WHEN 23                                                          
                    MOVE  'N'   TO  WS-K640-FND                                 
               WHEN OTHER                                                       
                    MOVE  BP13-K640-STAT TO RETURN-CODE                         
                    DISPLAY 'FILE ERROR  BP13K640 : ' C3A1-NUM-REGN             
                    PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4300-READ-BP13K820.                                                      
      *---------------------------------------------------------------*         
                                                                                
           INITIALIZE                             BP13K820-REC.                 
           MOVE SPACES                      TO    BP13K820-REC.                 
                                                                                
           MOVE K800-NUM-REGN               TO    K820-NUM-REGN.                
           MOVE WS-NRIC                     TO    K820-NUM-NRIC.                
                                                                                
           READ BP13K820                                                        
                                                                                
           EVALUATE BP13-K820-STAT                                              
               WHEN 00                                                          
                  MOVE 'CA'                TO   K820-NUM-CA-TAG                 
                  MOVE ZEROES              TO   K820-AMT-INCOME-CA              
                  MOVE WS-CUR-DATE         TO K820-DTE-UPDATE OF                
                                                BP13K820-REC                    
                  MOVE C3A1-NUM-USERID  TO K820-NUM-USERID                      
                                             OF BP13K820-REC                    
               WHEN 23                                                          
                    MOVE  BP13-K820-STAT TO RETURN-CODE                         
                    DISPLAY 'RECORD NOT FND BP13K820 : ' C3A1-NUM-REGN          
                    PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                  
               WHEN OTHER                                                       
                    MOVE  BP13-K820-STAT TO RETURN-CODE                         
                    DISPLAY 'FILE ERROR  BP13K820 : ' C3A1-NUM-REGN             
                    PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                  
            END-EVALUATE.                                                       
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4500-READ-BM06K110.                                                      
      *---------------------------------------------------------------*         
           MOVE SPACES                   TO K110-REC                            
           INITIALIZE                       K110-REC.                           
                                                                                
           MOVE K800-NUM-SCH-ACC         TO K110-SCH-ACC-NO                     
           READ BM06K110                                                        
                                                                                
           EVALUATE BM06-K110-STAT                                              
               WHEN 00                                                          
                    IF K800-AMT-PREMIUM IS NOT NUMERIC                          
                       MOVE ZEROES  TO K800-AMT-PREMIUM                         
                    END-IF                                                      
                    IF K110-CURR-SELLING-PRICE IS NOT NUMERIC                   
                       MOVE ZEROES  TO K110-CURR-SELLING-PRICE                  
                    END-IF                                                      
                    COMPUTE WS-CURR-SELLING-PRICE = K800-AMT-PREMIUM +          
                                            K110-CURR-SELLING-PRICE             
               WHEN 23                                                          
                    DISPLAY 'RECORD NOT FND BM06K110: ' K800-NUM-SCH-ACC        
               WHEN OTHER                                                       
                    MOVE  BM06-K110-STAT TO RETURN-CODE                         
                    DISPLAY 'FILE ERROR  BM06K110 : ' K800-NUM-SCH-ACC          
                    PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                  
            END-EVALUATE.                                                       
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4600-READ-BP13K410.                                                      
      *---------------------------------------------------------------*         
                                                                                
           MOVE SPACES                     TO   BP13K410-REC                    
           INITIALIZE                           BP13K410-REC.                   
                                                                                
           MOVE C3A1-NUM-REGN              TO   K410-NUM-REGN                   
                                                                                
           READ BP13K410.                                                       
                                                                                
           EVALUATE BP13-K410-STAT                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K410-FND                     
                                                                                
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K410-FND                     
                                                                                
           WHEN OTHER                                                           
              MOVE    BP13-K410-STAT       TO   RETURN-CODE                     
              DISPLAY 'FILE ERROR  BP13K640 : ' C3A1-NUM-REGN                   
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       5000-UPDATE-FILES.                                                       
      *----------------------*                                                  
                                                                                
           PERFORM 7000-WRITE-K800-IMAGE  THRU 7000-EXIT                        
                                                                                
           PERFORM 5100-UPDATE-K800      THRU  5100-EXIT.                       
           PERFORM 5200-UPDATE-K820      THRU  5200-EXIT.                       
           PERFORM 5300-UPDATE-BP13K640  THRU  5300-EXIT.                       
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       5100-UPDATE-K800.                                                        
      *----------------------*                                                  
                                                                                
           IF F340-NUM-TRANS = 'AP'                                             
              MOVE 'H'   TO  K800-NUM-LOAN-TAG   OF BP13K800-MASTER             
              MOVE 'RSV' TO  K800-NUM-HLA-STATUS OF BP13K800-MASTER             
           END-IF.                                                              
                                                                                
           IF F340-NUM-TRANS = 'RJ'                                             
              MOVE 'B'   TO  K800-NUM-LOAN-TAG   OF BP13K800-MASTER             
              MOVE 'REJ' TO  K800-NUM-HLA-STATUS OF BP13K800-MASTER             
           END-IF.                                                              
                                                                                
           IF F340-NUM-TRANS = 'CA' OR 'NL' OR 'BL'                             
              MOVE 'N'   TO  K800-NUM-LOAN-TAG   OF BP13K800-MASTER             
              MOVE 'CAN' TO  K800-NUM-HLA-STATUS OF BP13K800-MASTER             
              IF K800-NUM-COFN = 'A'                                            
                 MOVE 'T'   TO  K800-NUM-LOAN-TAG   OF BP13K800-MASTER          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE WS-CUR-DATE                TO K800-DTE-UPDATE OF                
                                              BP13K800-MASTER.                  
                                                                                
           MOVE C3A1-NUM-USERID  TO K800-NUM-USERID OF BP13K800-MASTER          
                                                                                
           PERFORM 6000-REWRITE-BP13K800   THRU 6000-EXIT.                      
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------*                                                 
       5200-UPDATE-K820.                                                        
      *-----------------------*                                                 
           IF K800-NUM-NRIC1 OF BP13K800-MASTER NOT = SPACES AND                
                                                      LOW-VALUES                
              MOVE K800-NUM-NRIC1 OF BP13K800-MASTER  TO   WS-NRIC              
              PERFORM 4300-READ-BP13K820        THRU 4300-EXIT                  
              PERFORM 6200-REWRITE-BP13K820     THRU 6200-EXIT                  
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC2 OF BP13K800-MASTER NOT = SPACES AND                
                                                      LOW-VALUES                
              MOVE SPACES                             TO   WS-NRIC              
              MOVE K800-NUM-NRIC2 OF BP13K800-MASTER  TO   WS-NRIC              
              PERFORM 4300-READ-BP13K820        THRU 4300-EXIT                  
              PERFORM 6200-REWRITE-BP13K820     THRU 6200-EXIT                  
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC3 OF BP13K800-MASTER NOT = SPACES AND                
                                                      LOW-VALUES                
              MOVE SPACES                             TO   WS-NRIC              
              MOVE K800-NUM-NRIC3 OF BP13K800-MASTER  TO   WS-NRIC              
              PERFORM 4300-READ-BP13K820        THRU 4300-EXIT                  
              PERFORM 6200-REWRITE-BP13K820     THRU 6200-EXIT                  
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC4 OF BP13K800-MASTER NOT = SPACES AND                
                                                      LOW-VALUES                
              MOVE SPACES                             TO   WS-NRIC              
              MOVE K800-NUM-NRIC4 OF BP13K800-MASTER  TO   WS-NRIC              
              PERFORM 4300-READ-BP13K820        THRU 4300-EXIT                  
              PERFORM 6200-REWRITE-BP13K820     THRU 6200-EXIT                  
           END-IF.                                                              
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       5300-UPDATE-BP13K640.                                                    
      *--------------------*                                                    
           MOVE SPACES                  TO   BP13K640-TRANS-REC.                
           INITIALIZE                        BP13K640-TRANS-REC.                
                                                                                
           MOVE C3A1-NUM-REGN           TO   K640-REGN-NO.                      
           PERFORM 5310-FMT-BP13K640    THRU 5310-EXIT.                         
                                                                                
           IF WS-K640-FND = 'Y'                                                 
              PERFORM 6100-REWRITE-BP13K640 THRU 6100-EXIT                      
           ELSE                                                                 
              PERFORM 7100-WRITE-K640 THRU 7100-EXIT                            
           END-IF.                                                              
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       5310-FMT-BP13K640.                                                       
      *-----------------*                                                       
           IF K800-NUM-SUBSIDISED-LOAN OF BP27K800-MASTER NOT NUMERIC           
              MOVE ZEROES TO K800-NUM-SUBSIDISED-LOAN OF BP27K800-MASTER        
           END-IF.                                                              
                                                                                
           MOVE K800-NUM-SUBSIDISED-LOAN OF BP27K800-MASTER TO                  
                K640-NUM-SUBSIDISED-LOAN.                                       
                                                                                
           IF K800-NUM-LOAN-TAG-GRANT NOT = SPACES AND LOW-VALUES               
              IF K800-NUM-LOAN-TAG-GRANT = 'S'                                  
                 MOVE 'H'  TO  K800-NUM-LOAN-TAG-GRANT                          
              END-IF                                                            
                                                                                
              IF K800-NUM-LOAN-TAG-GRANT = 'U'                                  
                 MOVE 'A'  TO  K800-NUM-LOAN-TAG-GRANT                          
              END-IF                                                            
                                                                                
              MOVE K800-NUM-LOAN-TAG-GRANT TO K640-CDE-LOAN-OPTION              
           ELSE                                                                 
              IF K800-NUM-LOAN-TAG OF BP27K800-MASTER = 'S'                     
                 MOVE 'H'  TO  K800-NUM-LOAN-TAG OF BP27K800-MASTER             
              END-IF                                                            
                                                                                
              IF K800-NUM-LOAN-TAG OF BP27K800-MASTER = 'U'                     
                 MOVE 'A'  TO  K800-NUM-LOAN-TAG OF BP27K800-MASTER             
              END-IF                                                            
                                                                                
              MOVE K800-NUM-LOAN-TAG OF BP27K800-MASTER TO                      
                   K640-CDE-LOAN-OPTION                                         
           END-IF.                                                              
                                                                                
           IF K640-CDE-LOAN-OPTION = 'H'                                        
              MOVE 'P' TO K640-CDE-LOAN-OPTION                                  
           END-IF.                                                              
                                                                                
           MOVE K800-AMT-MAX-LOAN  OF BP27K800-MASTER   TO                      
                K640-AMT-CA-LOAN.                                               
                                                                                
      * COMPUTE MAX LOAN                                                        
           IF K800-NUM-HLA-HDBREF-SALE(1:8) NOT = SPACES AND LOW-VALUES         
              IF K800-NUM-HLA-HDBREF-SALE(9:3)  = SPACES OR  LOW-VALUES         
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE  ZEROES TO WS-CURR-SELLING-PRICE                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           INITIALIZE                  WS-TEMP-AMT.                             
                                                                                
           COMPUTE WS-TEMP-AMT = 0.9 * WS-CURR-SELLING-PRICE.                   
                                                                                
           IF K800-AMT-MAX-LOAN <= WS-TEMP-AMT                                  
              MOVE K800-AMT-MAX-LOAN OF BP27K800-MASTER                         
                TO K640-AMT-MAX-LOAN                                            
           ELSE                                                                 
              INITIALIZE             WS-TEMP-AMT2                               
              COMPUTE WS-TEMP-AMT2 = WS-TEMP-AMT / 100                          
              INITIALIZE             WS-TEMP-AMT                                
              COMPUTE WS-TEMP-AMT  = WS-TEMP-AMT2 * 100                         
              MOVE WS-TEMP-AMT    TO K640-AMT-MAX-LOAN                          
           END-IF.                                                              
                                                                                
      *****IF K800-NUM-INTEREST    OF BP27K800-MASTER NOT NUMERIC               
      *       MOVE ZEROES TO K800-NUM-INTEREST OF BP27K800-MASTER               
      *    END-IF.                                                              
      *    MOVE K800-NUM-INTEREST  OF BP27K800-MASTER TO                        
      **********K640-AMT-INTEREST-RATE.                                         
           IF K800-NUM-APPLICABLE1-INT-RATE NOT NUMERIC                         
              MOVE ZEROES          TO K800-NUM-APPLICABLE1-INT-RATE             
           END-IF.                                                              
                                                                                
           MOVE K800-NUM-APPLICABLE1-INT-RATE TO                                
                K640-AMT-INTEREST-RATE.                                         
                                                                                
           IF K800-AMT-INSTAL      OF BP27K800-MASTER NOT NUMERIC               
              MOVE ZEROES TO K800-AMT-INSTAL OF BP27K800-MASTER                 
           END-IF.                                                              
           MOVE K800-AMT-INSTAL    OF BP27K800-MASTER   TO                      
                K640-AMT-ELIG-INSTAL.                                           
                                                                                
           IF K800-NUM-REPAY       OF BP27K800-MASTER NOT NUMERIC               
              MOVE ZEROES TO K800-NUM-REPAY OF BP27K800-MASTER                  
           END-IF.                                                              
           MOVE K800-NUM-REPAY     OF BP27K800-MASTER TO                        
                K640-AMT-REPAY-PERIOD.                                          
                                                                                
           MOVE K800-NUM-HOUSEHOLD OF BP13K800-MASTER TO                        
                K640-NUM-HOUSEHOLD.                                             
                                                                                
           MOVE K800-NUM-FLAT-UPGRADE  OF BP27K800-MASTER      TO               
                K640-NUM-FLAT-UPGRADE.                                          
                                                                                
           IF K800-NUM-LOAN-TAG-GRANT NOT = SPACES AND LOW-VALUES               
              IF K800-NUM-LOAN-TAG-GRANT = 'S'                                  
                 MOVE 'H'  TO  K800-NUM-LOAN-TAG-GRANT                          
              END-IF                                                            
                                                                                
              IF K800-NUM-LOAN-TAG-GRANT = 'U'                                  
                 MOVE 'A'  TO  K800-NUM-LOAN-TAG-GRANT                          
              END-IF                                                            
                                                                                
              IF K800-NUM-LOAN-TAG-GRANT = 'A'                                  
                 MOVE 'H' TO K640-NUM-LOAN-TAG                                  
              ELSE                                                              
                 MOVE K800-NUM-LOAN-TAG-GRANT TO                                
                      K640-NUM-LOAN-TAG                                         
              END-IF                                                            
           ELSE                                                                 
              IF K800-NUM-LOAN-TAG OF BP27K800-MASTER = 'S'                     
                 MOVE 'H'  TO  K800-NUM-LOAN-TAG OF BP27K800-MASTER             
              END-IF                                                            
                                                                                
              IF K800-NUM-LOAN-TAG OF BP27K800-MASTER = 'U'                     
                 MOVE 'A'  TO  K800-NUM-LOAN-TAG OF BP27K800-MASTER             
              END-IF                                                            
                                                                                
              IF K800-NUM-LOAN-TAG OF BP27K800-MASTER = 'A'                     
                 MOVE 'H' TO K640-NUM-LOAN-TAG                                  
              ELSE                                                              
                 MOVE K800-NUM-LOAN-TAG OF BP27K800-MASTER TO                   
                      K640-NUM-LOAN-TAG                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE WS-CUR-DATE                TO K640-DTE-UPDATE.                  
           MOVE C3A1-NUM-USERID            TO K640-USERID                       
                                                                                
      * GET MONTHLY INSTALLMENT                                                 
           PERFORM 5320-COMPUTE-MTH-INSTAL THRU 5320-EXIT.                      
                                                                                
      * FORMAT OTHER DATA                                                       
           MOVE K800-NUM-SCH-ACC           TO K640-SCH-ACC.                     
           MOVE WS-CUR-DATE                TO K640-DTE-LOAN-APPROVAL            
                                              K640-DTE-LOAN-APPLN-RETURN        
                                              K640-DTE-LETTER-PRINTED           
                                              K640-DTE-MLA-SENT.                
           MOVE '00000000'                 TO K640-DTE-REMINDER-SENT.           
           MOVE '01'                       TO K640-NUM-LETTER-PRINTED.          
           MOVE 'N'                        TO K640-NUM-OUTRGHT-PURC.            
                                                                                
       5310-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       5320-COMPUTE-MTH-INSTAL.                                                 
      *------------------------*                                                
                                                                                
      *****MOVE K800-NUM-INTEREST OF BP27K800-MASTER                            
      *****                                   TO WS-COMM-INT-RATE.              
           MOVE K800-NUM-APPLICABLE1-INT-RATE TO WS-COMM-INT-RATE.              
                                                                                
           MOVE K800-NUM-REPAY OF BP27K800-MASTER                               
                                              TO WS-COMM-REPAY-PERIOD.          
           MOVE K640-AMT-MAX-LOAN             TO WS-COMM-MAX-LOAN.              
                                                                                
                                                                                
      ** TEST                                                                   
           IF WS-COMM-MAX-LOAN  = ZEROES OR LOW-VALUES                          
              DISPLAY K800-NUM-HLA OF BP27K800-MASTER ' ' K800-NUM-REGN         
                      ':WS-COMM-MAX-LOAN:'                                      
              MOVE ZEROES TO K640-AMT-ELIG-INSTAL                               
              GO TO 5320-EXIT                                                   
           END-IF                                                               
      ** TEST                                                                   
                                                                                
           MOVE 12 TO WS-FAC.                                                   
           COMPUTE WS-INT = WS-COMM-INT-RATE / 100.                             
           COMPUTE WS-DIV = WS-INT / WS-FAC.                                    
                                                                                
           COMPUTE WS-INSTAL = (WS-COMM-MAX-LOAN * WS-DIV)/                     
                               (1 - (1 / (1 + WS-DIV) **                        
                               (WS-FAC * WS-COMM-REPAY-PERIOD))).               
                                                                                
           COMPUTE WS-INSTAL-DOL = WS-INSTAL + 0.99.                            
           MOVE WS-INSTAL-DOL         TO WS-COMM-MTH-INSTALL.                   
                                                                                
           MOVE WS-COMM-MTH-INSTALL   TO K640-AMT-ELIG-INSTAL.                  
                                                                                
       5320-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       6000-REWRITE-BP13K800.                                                   
      *================================================================*        
                                                                                
           REWRITE BP13K800-MASTER                                              
                                                                                
           IF BP13-K800-STAT = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
              MOVE  BP13-K800-STAT TO RETURN-CODE                               
              DISPLAY 'ERROR WRITING BP13K800 FILE : ' BP13-K800-STAT           
              DISPLAY 'K800-NUM-REGN = ' K800-NUM-REGN                          
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       6100-REWRITE-BP13K640.                                                   
      *================================================================*        
                                                                                
           REWRITE BP13K640-TRANS-REC                                           
                                                                                
           IF BP13-K640-STAT = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
              MOVE  BP13-K640-STAT TO RETURN-CODE                               
              DISPLAY 'ERROR WRITING BP13K640 FILE : ' BP13-K640-STAT           
              DISPLAY 'K640-REGN-NO = ' K640-REGN-NO                            
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       6100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       6200-REWRITE-BP13K820.                                                   
      *================================================================*        
                                                                                
           REWRITE BP13K820-REC                                                 
                                                                                
           IF BP13-K820-STAT = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
              MOVE  BP13-K820-STAT TO RETURN-CODE                               
              DISPLAY 'ERROR WRITING BP13K820 FILE : ' BP13-K820-STAT           
              DISPLAY 'K820-NUM-REGN = ' K820-NUM-REGN                          
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       6200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------*          
       7000-WRITE-K800-IMAGE.                                                   
      *--------------------------------------------------------------*          
                                                                                
           MOVE BP13K800-MASTER TO BP13K848-MASTER.                             
           MOVE 'C3A1'          TO K848-NUM-TRANS-BKIMAGE.                      
                                                                                
           MOVE C3A1-NUM-USERID TO K848-NUM-USERID.                             
           MOVE WS-CUR-DATE     TO K848-DTE-BKIMAGE.                            
                                                                                
           MOVE FUNCTION CURRENT-DATE(9:8) TO WS-SYSTEM-TIME.                   
                                                                                
           MOVE WS-SYSTEM-TIME  TO K848-TME-BKIMAGE.                            
           MOVE K848-NUM-REGN   TO K848-NUM-REGN-BKIMAGE.                       
                                                                                
           WRITE BP13K848-MASTER.                                               
                                                                                
           EVALUATE BP13-K848-STAT                                              
               WHEN 00                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                 MOVE  BP13-K848-STAT TO RETURN-CODE                            
                 DISPLAY 'ERR WRITING BP13K848 FILE : ' BP13-K848-STAT          
                 DISPLAY 'K848-NUM-REGN = ' K848-NUM-REGN                       
                 PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       7100-WRITE-K640.                                                         
      *--------------------------------------------------------------*          
                                                                                
           WRITE BP13K640-TRANS-REC.                                            
                                                                                
           EVALUATE BP13-K640-STAT                                              
               WHEN 00                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                 MOVE  BP13-K640-STAT TO RETURN-CODE                            
                 DISPLAY 'ERR WRITING BP13K640 FILE : ' BP13-K640-STAT          
                 DISPLAY 'K640-REGN-NO  = ' K640-REGN-NO                        
                 PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       8000-PRINT-ERROR.                                                        
      *--------------------------------------------------------------*          
                                                                                
           IF WS-CNT-LINE > 50                                                  
              PERFORM 8010-PRINT-ERROR-HDR THRU 8010-EXIT                       
           END-IF.                                                              
                                                                                
           ADD  1                          TO   WS-CNT-SNO                      
                                                WS-CNT-LINE.                    
                                                                                
           MOVE WS-CNT-SNO                 TO   L3A2-SNO.                       
           MOVE F340-NUM-REGN              TO   L3A2-NUM-REGN.                  
           MOVE F340-NUM-HLA               TO   L3A2-NUM-HLA.                   
           MOVE F340-NUM-TRANS             TO   L3A2-NUM-TRANS.                 
                                                                                
           WRITE BP13L3A2-REC              FROM L3A2-DETAIL.                    
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       8010-PRINT-ERROR-HDR.                                                    
      *--------------------------------------------------------------*          
                                                                                
           ADD 1                           TO   WS-CNT-PAGE.                    
           MOVE WS-CNT-PAGE                TO   L3A2-CNT-PAGE.                  
           STRING WS-CUR-DD '/' WS-CUR-MM '/' WS-CUR-CCYY                       
                  DELIMITED BY SIZE        INTO L3A2-DTE-CURRENT.               
                                                                                
           WRITE BP13L3A2-REC FROM L3A2-HEADER1 AFTER PAGE.                     
           WRITE BP13L3A2-REC FROM L3A2-HEADER2.                                
           WRITE BP13L3A2-REC FROM L3A2-HEADER3 AFTER 2.                        
           WRITE BP13L3A2-REC FROM L3A2-HEADER4.                                
                                                                                
           MOVE SPACES                     TO   BP13L3A2-REC.                   
           WRITE BP13L3A2-REC.                                                  
                                                                                
           MOVE 6                          TO   WS-CNT-LINE.                    
                                                                                
       8010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       9000-CLOSE-ROUTINE.                                                      
      *================================================================*        
                                                                                
           CLOSE BP13K800                                                       
                 BP27F340                                                       
                 BP13K820                                                       
                 BP13K848                                                       
                 BP27K800                                                       
                 BM06K110                                                       
                 BP13K640                                                       
                 BP13K410                                                       
                 BP13L3A2.                                                      
                                                                                
           IF BP13-K800-STAT NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K800 ERROR ' BP13-K800-STAT                  
              MOVE     BP13-K800-STAT      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF BP13-K820-STAT NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K820 ERROR ' BP13-K820-STAT                  
              MOVE     BP13-K820-STAT      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF BP13-K640-STAT NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K640 ERROR ' BP13-K640-STAT                  
              MOVE     BP13-K640-STAT      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF BP13-K848-STAT NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K848 ERROR ' BP13-K848-STAT                  
              MOVE     BP13-K848-STAT      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF BM06-K110-STAT NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BM06K110 ERROR ' BM06-K110-STAT                  
              MOVE     BM06-K110-STAT      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF BP27-K800-STAT NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP27K800 ERROR ' BP27-K800-STAT                  
              MOVE     BP27-K800-STAT      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF BP13-K410-STAT NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K410 ERROR ' BP13-K410-STAT                  
              MOVE     BP13-K410-STAT      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
