      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C25F.                                                 
       AUTHOR.        BALA AMUTHAN.                                             
       DATE-WRITTEN.  01 SEP 2006.                                              
                                                                                
      *============================================================*            
      * BP13 - LOAN ELIGIBILITY AND ASSESSMENT SYSTEM              *            
      *============================================================*            
      * OBJECTIVES       : RPOGRAM TO CHECK FLAT AVAILABLE FOR     *            
      *                    SELECTION BY NT/N/C/FT, DETERMINE THE   *            
      *                    SALES STATUS. EVERYDAY PUSH THE DATA TO *            
      *                    SERVER FRO E-SALES DISPLAY.             *            
      *                    A= ALL SOLD                             *            
      *                    S= SELLING FAST IF LEFT HALF OF THE TOT *            
      *                    L= IF LEFT < 5 UNITS                    *            
      *                                                            *            
      * FILES            : BP13IPUT - BP13.F240.HEADFLAT           *            
      *============================================================*            
      * REVISION HISTORY :                                         *            
      * CHG-REQ#  BY    DATE        DESCRIPTION                    *            
      * --------  ----  ----------  ------------------------------ *            
      * BP132925  BA2   01/09/2006  NEW PROGRAM.                   *            
      * BP133086  BA2   11/10/2007  FIND REMARK FOR CAT1,CAT2,CAT3 *            
      * BP133777  PCL3  15/04/2010  ADD CHECK FOR BTO, QUEENSTOWN  *            
      *============================================================*            
      *------------------------------------------------------------*            
       ENVIRONMENT DIVISION.                                                    
      *-------------------------------------------------------------            
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13IPUT ASSIGN TO BP13IPUT.                                  
                                                                                
           SELECT BP13F299 ASSIGN TO BP13F299.                                  
                                                                                
      *-------------------------------------------------------------            
       DATA DIVISION.                                                           
      *-------------------------------------------------------------            
       FILE SECTION.                                                            
       FD  BP13IPUT                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 80  CHARACTERS                                       
           DATA RECORDS IS IPUT-REC                                             
           LABEL RECORDS ARE STANDARD.                                          
       01 IPUT-REC PIC X(80).                                                   
                                                                                
       FD  BP13F299                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 80 CHARACTERS                                        
           LABEL RECORDS ARE OMITTED.                                           
       COPY BP13F299.                                                           
                                                                                
      *------------------------------------------------------------*            
       WORKING-STORAGE SECTION.                                                 
      *------------------------------------------------------------*            
      *-------------------------*                                               
      *    DATE - VARIABLES     *                                               
      *-------------------------*                                               
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURR-DATE              PIC X(08)  VALUE SPACES.               
           05  WS-CURRENT-TIME           PIC X(09)  VALUE SPACES.               
               05  WS-TIME REDEFINES WS-CURRENT-TIME.                           
                   10  WS-CURR-TIME      PIC X(08).                             
                   10  WS-GM             PIC X(01).                             
           05  FILLER                    PIC X(04)  VALUE SPACES.               
                                                                                
      *-------------------------*                                               
      *  COUNT - VARIABLES.     *                                               
      *-------------------------*                                               
       01  WS-COUNT.                                                            
           05  WS-READ-IPUT              PIC 9(05) COMP VALUE ZEROES.           
           05  WS-WRIT-F299              PIC 9(05) COMP VALUE ZEROES.           
                                                                                
      *-------------------------*                                               
      *  SWITCH - FLAG          *                                               
      *-------------------------*                                               
       01  WS-SWITCH.                                                           
           05  WS-IPUT-END               PIC X(01)   VALUE 'N'.                 
               88  CS-IPUT-END                       VALUE 'Y'.                 
                                                                                
      *----------------------------*                                            
      *  COPY-BOOK FOR INPUT-FILE  *                                            
      *----------------------------*                                            
       01 CP-IPUT-REC.                                                          
          05 CP-IPUT-KEY.                                                       
             10  CP-IPUT-NUM-SEL         PIC X(03)  VALUE SPACES.               
             10  CP-IPUT-NUM-NT          PIC X(20)  VALUE SPACES.               
             10  CP-IPUT-NUM-NEIGH       PIC X(04)  VALUE SPACES.               
             10  CP-IPUT-NUM-CNTRCT      PIC X(04)  VALUE SPACES.               
             10  CP-IPUT-NUM-FT          PIC X(09)  VALUE SPACES.               
          05 CP-IPUT-NUM-MAX-CAT1        PIC 9(03)  VALUE ZEROES.               
          05 CP-IPUT-NUM-MAX-CAT2        PIC 9(03)  VALUE ZEROES.               
          05 CP-IPUT-NUM-MAX-CAT3        PIC 9(03)  VALUE ZEROES.               
          05 CP-IPUT-NUM-TOTAL           PIC 9(04)  VALUE ZEROES.               
          05 CP-IPUT-NUM-TOT-CAT1        PIC 9(03)  VALUE ZEROES.               
          05 CP-IPUT-NUM-TOT-CAT2        PIC 9(03)  VALUE ZEROES.               
          05 CP-IPUT-NUM-TOT-CAT3        PIC 9(03)  VALUE ZEROES.               
          05 FILLER                      PIC X(18)  VALUE SPACES.               
                                                                                
      *-------------------------*                                               
      *  KEYS - VARIABLES.      *                                               
      *-------------------------*                                               
       01  WS-KEY.                                                              
           05  WS-IPUT-PREV-KEY.                                                
               10  WS-PREV-IPUT-NUM-SEL       PIC X(03)  VALUE SPACES.          
               10  WS-PREV-IPUT-NUM-NT        PIC X(20)  VALUE SPACES.          
               10  WS-PREV-IPUT-NUM-NEIGH     PIC X(04)  VALUE SPACES.          
               10  WS-PREV-IPUT-NUM-CNTRCT    PIC X(04)  VALUE SPACES.          
               10  WS-PREV-IPUT-NUM-FT        PIC X(09)  VALUE SPACES.          
                                                                                
      *-------------------------*                                               
      *  OTHER - VARIABLES.     *                                               
      *-------------------------*                                               
       01  WS-VARIABLES.                                                        
           05  WS-IPUT-NUM-TOTAL         PIC 9(05)  VALUE ZEROES.               
           05  WS-IPUT-NUM-TOT-CAT       PIC 9(04)  VALUE ZEROES.               
           05  WS-HALF-TOTAL             PIC 9(05)  VALUE ZEROES.               
           05  WS-NUM-UNIT-AVAIL-TOT     PIC 9(06)  VALUE ZEROES.               
           05  WS-NUM-AVAIL-CAT1         PIC 9(06)  VALUE ZEROES.               
           05  WS-NUM-AVAIL-CAT2         PIC 9(06)  VALUE ZEROES.               
           05  WS-NUM-AVAIL-CAT3         PIC 9(06)  VALUE ZEROES.               
           05  WS-TOT-CAT1               PIC 9(06)  VALUE ZEROES.               
           05  WS-TOT-CAT2               PIC 9(06)  VALUE ZEROES.               
           05  WS-TOT-CAT3               PIC 9(06)  VALUE ZEROES.               
           05  WS-NUM-TOT-CAT            PIC 9(06)  VALUE ZEROES.               
           05  WS-NUM-REMARKS            PIC X(01)  VALUE SPACES.               
                                                                                
      *------------------------------------------------------------*            
       PROCEDURE DIVISION.                                                      
      *------------------------------------------------------------*            
                                                                                
      *------------------------------------------------------------*            
       0000-MAIN.                                                               
      *------------------------------------------------------------*            
           PERFORM 1000-INIT-ROUTINE  THRU 1000-EXIT.                           
           PERFORM 2000-READ-IPUT     THRU 2000-EXIT.                           
           PERFORM 3000-MAIN-PROCESS  THRU 3000-EXIT                            
                   UNTIL CS-IPUT-END.                                           
           PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                           
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-INIT-ROUTINE.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT BP13IPUT                                                  
               OUTPUT BP13F299.                                                 
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE.                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-IPUT.                                                          
      *-------------------------------------------------------------            
           READ BP13IPUT AT END MOVE 'Y' TO WS-IPUT-END                         
                                GO TO 2000-EXIT.                                
                                                                                
           MOVE  IPUT-REC TO CP-IPUT-REC.                                       
           ADD 1 TO WS-READ-IPUT.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------------------------------------------*            
       3000-MAIN-PROCESS.                                                       
      *------------------------------------------------------------*            
           MOVE ZEROES                   TO WS-IPUT-NUM-TOTAL                   
                                            WS-IPUT-NUM-TOT-CAT                 
                                            WS-HALF-TOTAL                       
                                            WS-TOT-CAT1                         
                                            WS-TOT-CAT2                         
                                            WS-TOT-CAT3.                        
                                                                                
           MOVE CP-IPUT-KEY              TO WS-IPUT-PREV-KEY.                   
                                                                                
           PERFORM 3100-ACCUMULATE-FIELDS  THRU 3100-EXIT                       
                   UNTIL CP-IPUT-KEY NOT = WS-IPUT-PREV-KEY OR                  
                         CS-IPUT-END.                                           
                                                                                
           PERFORM 3300-MOVE-VALUES        THRU 3300-EXIT.                      
                                                                                
           PERFORM 4000-WRITE-F299         THRU 4000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *------------------------------------------------------------*            
       3100-ACCUMULATE-FIELDS.                                                  
      *------------------------------------------------------------*            
           IF CP-IPUT-NUM-TOTAL NOT NUMERIC                                     
              MOVE ZEROES                TO CP-IPUT-NUM-TOTAL                   
           END-IF.                                                              
                                                                                
           IF CP-IPUT-NUM-TOT-CAT1 NOT NUMERIC                                  
              MOVE ZEROES                TO CP-IPUT-NUM-TOT-CAT1                
           END-IF.                                                              
                                                                                
           IF CP-IPUT-NUM-TOT-CAT2 NOT NUMERIC                                  
              MOVE ZEROES                TO CP-IPUT-NUM-TOT-CAT2                
           END-IF.                                                              
                                                                                
           IF CP-IPUT-NUM-TOT-CAT3 NOT NUMERIC                                  
              MOVE ZEROES                TO CP-IPUT-NUM-TOT-CAT3                
           END-IF.                                                              
                                                                                
           ADD CP-IPUT-NUM-TOTAL         TO WS-IPUT-NUM-TOTAL.                  
           ADD CP-IPUT-NUM-TOT-CAT1      TO WS-IPUT-NUM-TOT-CAT.                
           ADD CP-IPUT-NUM-TOT-CAT2      TO WS-IPUT-NUM-TOT-CAT.                
           ADD CP-IPUT-NUM-TOT-CAT3      TO WS-IPUT-NUM-TOT-CAT.                
                                                                                
           PERFORM 3120-ACCUM-MAX-CAT-TOT THRU 3120-EXIT.                       
           PERFORM 2000-READ-IPUT         THRU 2000-EXIT.                       
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *------------------------------------------------------------*            
       3120-ACCUM-MAX-CAT-TOT.                                                  
      *------------------------------------------------------------*            
           MOVE ZEROES                   TO WS-NUM-UNIT-AVAIL-TOT               
                                            WS-NUM-AVAIL-CAT1                   
                                            WS-NUM-AVAIL-CAT2                   
                                            WS-NUM-AVAIL-CAT3.                  
                                                                                
           IF CP-IPUT-NUM-MAX-CAT1 NOT NUMERIC                                  
              MOVE ZEROES                TO CP-IPUT-NUM-MAX-CAT1                
           END-IF.                                                              
                                                                                
           IF CP-IPUT-NUM-MAX-CAT2 NOT NUMERIC                                  
              MOVE ZEROES                TO CP-IPUT-NUM-MAX-CAT2                
           END-IF.                                                              
                                                                                
           IF CP-IPUT-NUM-MAX-CAT3 NOT NUMERIC                                  
              MOVE ZEROES                TO CP-IPUT-NUM-MAX-CAT3                
           END-IF.                                                              
                                                                                
           COMPUTE WS-NUM-UNIT-AVAIL-TOT = CP-IPUT-NUM-TOTAL    -               
                                           CP-IPUT-NUM-TOT-CAT1 -               
                                           CP-IPUT-NUM-TOT-CAT2 -               
                                           CP-IPUT-NUM-TOT-CAT3.                
                                                                                
           COMPUTE WS-NUM-AVAIL-CAT1     = CP-IPUT-NUM-MAX-CAT1 -               
                                           CP-IPUT-NUM-TOT-CAT1.                
                                                                                
           COMPUTE WS-NUM-AVAIL-CAT2     = CP-IPUT-NUM-MAX-CAT2 -               
                                           CP-IPUT-NUM-TOT-CAT2.                
                                                                                
           COMPUTE WS-NUM-AVAIL-CAT3     = CP-IPUT-NUM-MAX-CAT3 -               
                                           CP-IPUT-NUM-TOT-CAT3.                
                                                                                
           IF WS-NUM-AVAIL-CAT1 > WS-NUM-UNIT-AVAIL-TOT                         
              COMPUTE WS-TOT-CAT1        = WS-NUM-UNIT-AVAIL-TOT  +             
                                           WS-TOT-CAT1                          
           ELSE                                                                 
              COMPUTE WS-TOT-CAT1        = WS-NUM-AVAIL-CAT1      +             
                                           WS-TOT-CAT1                          
           END-IF.                                                              
                                                                                
           IF WS-NUM-AVAIL-CAT2 > WS-NUM-UNIT-AVAIL-TOT                         
              COMPUTE WS-TOT-CAT2        = WS-NUM-UNIT-AVAIL-TOT  +             
                                           WS-TOT-CAT2                          
           ELSE                                                                 
              COMPUTE WS-TOT-CAT2        = WS-NUM-AVAIL-CAT2      +             
                                           WS-TOT-CAT2                          
           END-IF.                                                              
                                                                                
           IF WS-NUM-AVAIL-CAT3 > WS-NUM-UNIT-AVAIL-TOT                         
              COMPUTE WS-TOT-CAT3        = WS-NUM-UNIT-AVAIL-TOT  +             
                                           WS-TOT-CAT3                          
           ELSE                                                                 
              COMPUTE WS-TOT-CAT3        = WS-NUM-AVAIL-CAT3      +             
                                           WS-TOT-CAT3                          
           END-IF.                                                              
                                                                                
       3120-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *------------------------------------------------------------*            
       3200-DETERMINE-REMARKS.                                                  
      *------------------------------------------------------------*            
           MOVE SPACES                   TO WS-NUM-REMARKS.                     
                                                                                
           DIVIDE WS-IPUT-NUM-TOTAL BY 2 GIVING WS-HALF-TOTAL.                  
                                                                                
           IF  WS-NUM-TOT-CAT = ZEROES                                          
              MOVE 'A'                   TO WS-NUM-REMARKS                      
           ELSE                                                                 
              IF WS-NUM-TOT-CAT < 5                                             
                 MOVE 'L'                TO WS-NUM-REMARKS                      
              ELSE                                                              
      *FOR TEMPROARY                                                            
      *          IF WS-NUM-TOT-CAT > WS-HALF-TOTAL                              
      *             MOVE 'S'             TO WS-NUM-REMARKS                      
      *          ELSE                                                           
                    MOVE 'X'             TO WS-NUM-REMARKS                      
      *          END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *------------------------------------------------------------*            
       3210-DETERMINE-REMARKS.                                                  
      *------------------------------------------------------------*            
           MOVE SPACES                   TO WS-NUM-REMARKS.                     
                                                                                
           DIVIDE WS-IPUT-NUM-TOTAL BY 2 GIVING WS-HALF-TOTAL.                  
                                                                                
           IF WS-IPUT-NUM-TOTAL - WS-NUM-TOT-CAT = ZEROES                       
              MOVE 'A'                   TO WS-NUM-REMARKS                      
           ELSE                                                                 
              IF WS-IPUT-NUM-TOTAL - WS-NUM-TOT-CAT < 5                         
                 MOVE 'L'                TO WS-NUM-REMARKS                      
              ELSE                                                              
      *FOR TEMPROARY                                                            
      *          IF WS-NUM-TOT-CAT > WS-HALF-TOTAL                              
      *             MOVE 'S'             TO WS-NUM-REMARKS                      
      *          ELSE                                                           
                    MOVE 'X'             TO WS-NUM-REMARKS                      
      *          END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3210-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *------------------------------------------------------------*            
       3300-MOVE-VALUES.                                                        
      *------------------------------------------------------------*            
           MOVE SPACES                    TO BP13F299-REC.                      
           MOVE  WS-PREV-IPUT-NUM-SEL     TO F299-NUM-SELECTION.                
           MOVE  WS-PREV-IPUT-NUM-NT      TO F299-NUM-NEW-TOWN.                 
           MOVE  WS-PREV-IPUT-NUM-NEIGH   TO F299-NUM-NEIGH.                    
           MOVE  WS-PREV-IPUT-NUM-CNTRCT  TO F299-NUM-CNTRCT.                   
           IF WS-PREV-IPUT-NUM-SEL = 'SBF'  AND                                 
              WS-PREV-IPUT-NUM-NT = 'CENTRAL'                                   
              IF WS-PREV-IPUT-NUM-FT(1:2) = 'S1'                                
                MOVE  '4'                   TO F299-NUM-FLAT-TYPE               
              ELSE IF WS-PREV-IPUT-NUM-FT(1:2) = 'S2'                           
                MOVE  '5'                   TO F299-NUM-FLAT-TYPE               
              END-IF                                                            
           ELSE                                                                 
              IF WS-PREV-IPUT-NUM-SEL = 'BTO' AND                               
                 WS-PREV-IPUT-NUM-NT  = 'QUEENSTOWN'                            
                 IF WS-PREV-IPUT-NUM-FT = '4-ROOM-SP'                           
                    MOVE  'A'               TO F299-NUM-FLAT-TYPE               
                 ELSE IF WS-PREV-IPUT-NUM-FT = '5-ROOM-SP'                      
                      MOVE  'B'             TO F299-NUM-FLAT-TYPE               
                 ELSE IF WS-PREV-IPUT-NUM-FT = '4-ROOM-SA'                      
                      MOVE  'C'             TO F299-NUM-FLAT-TYPE               
                 ELSE IF WS-PREV-IPUT-NUM-FT = '5-ROOM-SA'                      
                      MOVE  'D'             TO F299-NUM-FLAT-TYPE               
                 ELSE                                                           
                      MOVE WS-PREV-IPUT-NUM-FT(1:1)                             
                                            TO F299-NUM-FLAT-TYPE               
                 END-IF                                                         
              ELSE                                                              
                 MOVE WS-PREV-IPUT-NUM-FT(1:1) TO F299-NUM-FLAT-TYPE            
              END-IF                                                            
           END-IF.                                                              
      *MOVING REMARKS FOR TOTAL                                                 
           COMPUTE  F299-NUM-UNITS =                                            
                                WS-IPUT-NUM-TOTAL - WS-IPUT-NUM-TOT-CAT.        
           MOVE WS-IPUT-NUM-TOT-CAT  TO WS-NUM-TOT-CAT.                         
           PERFORM 3210-DETERMINE-REMARKS  THRU 3210-EXIT.                      
           MOVE WS-NUM-REMARKS            TO F299-NUM-REMARKS.                  
                                                                                
      *MOVING REMARKS FOR CHINES                                                
           MOVE WS-TOT-CAT1          TO WS-NUM-TOT-CAT.                         
           PERFORM 3200-DETERMINE-REMARKS  THRU 3200-EXIT.                      
           MOVE WS-NUM-REMARKS            TO BP13F299-REC(43:1).                
                                                                                
      *MOVING REMARKS FOR MALAY                                                 
           MOVE WS-TOT-CAT2          TO WS-NUM-TOT-CAT.                         
           PERFORM 3200-DETERMINE-REMARKS  THRU 3200-EXIT.                      
           MOVE WS-NUM-REMARKS            TO BP13F299-REC(44:1).                
                                                                                
      *MOVING REMARKS FOR OTHERS                                                
           MOVE WS-TOT-CAT3          TO WS-NUM-TOT-CAT.                         
           PERFORM 3200-DETERMINE-REMARKS  THRU 3200-EXIT.                      
           MOVE WS-NUM-REMARKS            TO BP13F299-REC(45:1).                
                                                                                
           MOVE  WS-CURR-DATE             TO F299-DTE-UPDATE.                   
           MOVE  WS-CURR-TIME             TO F299-TME-UPDATE.                   
                                                                                
       3300-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-WRITE-F299.                                                         
      *-------------------------------------------------------------            
           WRITE   BP13F299-REC.                                                
           ADD +1                  TO WS-WRIT-F299.                             
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9999-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY '------- BP13C25F -------------'.                            
           DISPLAY 'NO OF RECORDS READ    (BP13IPUT) : '                        
                                               WS-READ-IPUT.                    
           DISPLAY 'NO OF RECORDS WRITTEN (BP13F299) : '                        
                                               WS-WRIT-F299.                    
                                                                                
           CLOSE BP13IPUT                                                       
                 BP13F299.                                                      
                                                                                
       9999-EXIT.                                                               
           STOP RUN.                                                            
                                                                                
      *-------------------------------------------------------------            
      *             END-PROGRAM.                                   *            
      *-------------------------------------------------------------            
