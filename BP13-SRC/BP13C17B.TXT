       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C17B.                                                  
       AUTHOR.       AM25.                                                      
       DATE-WRITTEN. 26/10/2020.                                                
      *****************************************************************         
      *                SOC - SYSTEM OF COMMITMENT (BP13)              *         
      *===============================================================*         
      *  OBJECTIVES :                                                 *         
      *     - TO SCREEN CITIZENSHIP OF THE 1ST 3 OLDEST CHILDREN      *         
      *       LISTED IN TCP APPLICATIONS                              *         
      *===============================================================*         
      * FILES DESCRIPTION :-                                          *         
      *                                                               *         
      *                ACCESS     REC                                 *         
      * DD NAME  I/O/E MODE       LEN  KEY FIELDS    REMARKS          *         
      * -------- ----- ---------- ---- ------------- ---------------- *         
      * BP13F730 I     SEQUENTIAL 500                                 *         
      * BP13F825 I     SEQUENTIAL 200                                 *         
      * P13F730A O     SEQUENTIAL 500                                 *         
      *===============================================================*         
      * REVISION HISTORY :                                            *         
      *                                                               *         
      * CHG-REQ#  DD/MM/YY   BY   DESCRIPTION                         *         
      * --------  --------  ----  ----------------------------------- *         
      * BP138495  26/10/20  AM25  NEW PROGRAM                         *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN      TO BP13F730.                             
           SELECT BP13F825 ASSIGN      TO BP13F825.                             
                                                                                
           SELECT P13F730A ASSIGN      TO P13F730A.                             
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F730                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F730.                                                           
                                                                                
       FD  BP13F825                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F825.                                                           
                                                                                
       FD  P13F730A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01  P13F730A-REC                PIC X(500).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-CHILD                PIC 99          VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F730-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-F825-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-F730A-WRITE          PIC 9(6)        VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES-FLAGS.                                                   
           05  WS-F730-EOF             PIC X           VALUE 'N'.               
           05  WS-F825-EOF             PIC X           VALUE 'N'.               
           05  WS-FLAG-OUT             PIC X           VALUE 'N'.               
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 2000-READ-BP13F730    THRU 2000-EXIT.                        
           PERFORM 2100-READ-BP13F825    THRU 2100-EXIT.                        
           PERFORM 3000-MAIN-PROCESSING  THRU 3000-EXIT                         
              UNTIL WS-F730-EOF = 'Y' AND WS-F825-EOF = 'Y'.                    
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13F825                                                 
                OUTPUT P13F730A.                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-READ-BP13F730.                                                      
      *****************************************************************         
                                                                                
           READ BP13F730                                                        
              AT END                                                            
                 MOVE HIGH-VALUES      TO F730-NUM-REGN                         
                 MOVE 'Y'              TO WS-F730-EOF                           
              NOT AT END                                                        
                 ADD 1                 TO WS-F730-READ                          
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2100-READ-BP13F825.                                                      
      *****************************************************************         
                                                                                
           READ BP13F825                                                        
              AT END                                                            
                 MOVE HIGH-VALUES      TO F825-NUM-REGN                         
                 MOVE 'Y'              TO WS-F825-EOF                           
              NOT AT END                                                        
                 ADD 1                 TO WS-F825-READ                          
           END-READ.                                                            
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-MAIN-PROCESSING.                                                    
      *****************************************************************         
                                                                                
           EVALUATE TRUE                                                        
              WHEN F730-NUM-REGN = F825-NUM-REGN                                
                 PERFORM 3100-PROCESS-CHILDREN THRU 3100-EXIT                   
                                                                                
              WHEN F730-NUM-REGN < F825-NUM-REGN                                
                 PERFORM 2000-READ-BP13F730 THRU 2000-EXIT                      
                                                                                
              WHEN F730-NUM-REGN > F825-NUM-REGN                                
                 PERFORM 2100-READ-BP13F825 THRU 2100-EXIT                      
                    UNTIL F730-NUM-REGN <= F825-NUM-REGN                        
                                                                                
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3100-PROCESS-CHILDREN.                                                   
      *****************************************************************         
                                                                                
           MOVE 'N'                    TO WS-FLAG-OUT.                          
           MOVE ZEROES                 TO WS-CHILD.                             
                                                                                
           PERFORM UNTIL WS-FLAG-OUT = 'Y' OR WS-CHILD = 03                     
                      OR F730-NUM-REGN NOT = F825-NUM-REGN                      
                                                                                
              ADD 1                    TO WS-CHILD                              
                                                                                
              EVALUATE WS-CHILD                                                 
                 WHEN 01                                                        
                 WHEN 02                                                        
                    IF F825-NUM-CITIZENSHIP = '10' OR '20'                      
                       CONTINUE                                                 
                    ELSE                                                        
                       WRITE P13F730A-REC FROM BP13F730-REC                     
                       ADD 1           TO WS-F730A-WRITE                        
                                                                                
                       MOVE 'Y'        TO WS-FLAG-OUT                           
                    END-IF                                                      
                                                                                
                 WHEN 03                                                        
                    IF F825-NUM-CITIZENSHIP = '10'                              
                       CONTINUE                                                 
                    ELSE                                                        
                       WRITE P13F730A-REC FROM BP13F730-REC                     
                       ADD 1           TO WS-F730A-WRITE                        
                                                                                
                       MOVE 'Y'        TO WS-FLAG-OUT                           
                    END-IF                                                      
                                                                                
              END-EVALUATE                                                      
                                                                                
              PERFORM 2100-READ-BP13F825 THRU 2100-EXIT                         
                                                                                
           END-PERFORM.                                                         
                                                                                
           PERFORM 2000-READ-BP13F730 THRU 2000-EXIT.                           
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           CLOSE BP13F730                                                       
                 BP13F825                                                       
                 P13F730A.                                                      
                                                                                
           DISPLAY '******************************'.                            
           DISPLAY '* PROGRAM BP13C17B           *'.                            
           DISPLAY '* CONTROL COUNTS             *'.                            
           DISPLAY '*----------------------------*'.                            
           DISPLAY '* REC READ F730     : ' WS-F730-READ ' *'.                  
           DISPLAY '* REC READ F825     : ' WS-F825-READ ' *'.                  
           DISPLAY '* REC WRITTEN F730A : ' WS-F730A-WRITE ' *'.                
           DISPLAY '******************************'.                            
                                                                                
       9000-EXIT.                                                               
           STOP RUN.                                                            
           EXIT.                                                                
                                                                                
      *======================  END OF PROGRAM  ======================*          
