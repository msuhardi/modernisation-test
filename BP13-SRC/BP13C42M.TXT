       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C42M.                                                 
      *AUTHOR.        KSJ3                                                      
      *DATE-WRITTEN.  08/09/2016.                                               
      * ========================================================== *            
      *                SYSTEM OF COMMITMENT (BP13)                 *            
      * ========================================================== *            
      *  OBJECTIVE : PROCESS AG07 VOUCHER RETURN FILE (AG07F640)   *            
      *              FOR FORFEITURE AND REFUND                     *            
      *                                                            *            
      *  INPUT   :  AG07F640 - AG07 INTERFACE FILE                 *            
      *             AG07K970 - AG07 REJECT CODES                   *            
      *                                                            *            
      *  I-O     :  BP13K62A - SOC STAMP FEE REFUND FILE           *            
      *                                                            *            
      *  LISTING :  P13L42MA - REJECTED VOUCHERS LIST              *            
      *          :  P13L42MB - ERROR LIST                          *            
      *                                                            *            
      * ========================================================== *            
      * CHG-NO    BY    DATE    DESCRIPTION                        *            
      * BP136483  KSJ3  080916  NEW PROGRAM                        *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT AG07F640 ASSIGN        TO AG07F640.                           
                                                                                
           SELECT BP13K62A ASSIGN        TO BP13K62A                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K62A-KEY-FLD                        
                           ALTERNATE KEY IS K62A-ALT-KEY2                       
                           FILE STATUS   IS WS-K62A-STATUS.                     
                                                                                
           SELECT AG07K970 ASSIGN        TO AG07K970                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K970-KEY-FLD                        
                           FILE STATUS   IS WS-K970-STATUS.                     
                                                                                
           SELECT P13L42MA ASSIGN        TO P13L42MA.                           
           SELECT P13L42MB ASSIGN        TO P13L42MB.                           
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  AG07F640                                                             
           RECORD CONTAINS 100 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
           COPY AG07F640.                                                       
                                                                                
       FD  BP13K62A                                                             
           RECORD CONTAINS 1200 CHARACTERS.                                     
           COPY BP13K62A.                                                       
                                                                                
       FD  AG07K970                                                             
           RECORD CONTAINS 40  CHARACTERS.                                      
           COPY AG07K970.                                                       
                                                                                
       FD  P13L42MA                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01  P13L42MA-REC                  PIC X(132).                            
                                                                                
       FD  P13L42MB                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01  P13L42MB-REC                  PIC X(132).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K62A-STATUS            PIC X(2)    VALUE SPACES.              
           05  WS-K970-STATUS            PIC X(2)    VALUE SPACES.              
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F640-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F640-SKIP          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F640-ACC           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F640-REJ           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K62A-UPD           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K62A-WRT           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K62A-DEL           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-PAGE-A             PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-PAGE-B             PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-LINE-A             PIC 9(2)   VALUE 60.                   
           05  WS-CNT-LINE-B             PIC 9(2)   VALUE 60.                   
           05  WS-CNT-SNO-A              PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-SNO-B              PIC 9(4)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F640-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K62A-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K62A-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K970-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-REJECT-FL              PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY           PIC 9(4)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY           PIC 9(4)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE SPACES.               
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-NUM-DDD-FILE-REF.                                             
               10  WS-NUM-REGN           PIC X(8)   VALUE SPACES.               
               10  FILLER                PIC X(1)   VALUE SPACES.               
               10  WS-NUM-TRANS          PIC X(3)   VALUE SPACES.               
               10  FILLER                PIC X(3)   VALUE SPACES.               
           05  WS-DTE-SUB                PIC X(8)   VALUE SPACES.               
                                                                                
      *-------------------------------------------------------------            
      *    P13L42MA REPORT (REJECT LIST)                                        
      *-------------------------------------------------------------            
       01  L42MA-HEADER1.                                                       
           05  FILLER                    PIC X(9)   VALUE 'BP13L42MA'.          
           05  FILLER                    PIC X(3)   VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE 'HDBCAT 3'.           
           05  FILLER                    PIC X(28)  VALUE SPACES.               
           05  FILLER                    PIC X(39)  VALUE                       
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                    PIC X(17)  VALUE SPACES.               
           05  FILLER                    PIC X(6)   VALUE 'DATE: '.             
           05  L42MA-DTE-CURRENT         PIC X(10)  VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE '  PAGE: '.           
           05  L42MA-CNT-PAGE            PIC ZZZ9.                              
                                                                                
       01  L42MA-HEADER2.                                                       
           05  FILLER                    PIC X(34)  VALUE SPACES.               
           05  FILLER                    PIC X(53)  VALUE                       
               'FORFEITURE AND REFUND VOUCHERS REJECTED BY ETLSVS ON '.         
           05  L42MA-DTE-CURRENT2        PIC X(10)  VALUE SPACES.               
                                                                                
       01  L42MA-HEADER3.                                                       
           05  FILLER                    PIC X(132) VALUE                       
               '    S/NO    REGN        VOUCHER NO       SUBMIT DATE            
      -        'REJECT REASON'.                                                 
                                                                                
       01  L42MA-HEADER4.                                                       
           05  FILLER                    PIC X(132) VALUE                       
               '    ----    --------    -------------    -----------            
      -        '-----------------------------------------'.                     
                                                                                
       01  L42MA-DETAIL.                                                        
           05  FILLER                    PIC X(4)   VALUE SPACES.               
           05  L42MA-SNO                 PIC ZZZ9.                              
           05  FILLER                    PIC X(4)   VALUE SPACES.               
           05  L42MA-NUM-REGN            PIC X(8)   VALUE SPACES.               
           05  FILLER                    PIC X(4)   VALUE SPACES.               
           05  L42MA-NUM-OLD-VR          PIC X(13)  VALUE SPACES.               
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  L42MA-DTE-SUBMITTED       PIC X(10)  VALUE SPACES.               
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  L42MA-NUM-DESC            PIC X(41)  VALUE SPACES.               
                                                                                
      *-------------------------------------------------------------            
      *    P13L42MB REPORT (EXCEPTION LIST)                                     
      *-------------------------------------------------------------            
       01  L42MB-HEADER1.                                                       
           05  FILLER                    PIC X(9)   VALUE 'BP13L42MB'.          
           05  FILLER                    PIC X(3)   VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE 'HDBCAT 3'.           
           05  FILLER                    PIC X(28)  VALUE SPACES.               
           05  FILLER                    PIC X(39)  VALUE                       
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                    PIC X(17)  VALUE SPACES.               
           05  FILLER                    PIC X(6)   VALUE 'DATE: '.             
           05  L42MB-DTE-CURRENT         PIC X(10)  VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE '  PAGE: '.           
           05  L42MB-CNT-PAGE            PIC ZZZ9.                              
                                                                                
       01  L42MB-HEADER2.                                                       
           05  FILLER                    PIC X(40)  VALUE SPACES.               
           05  FILLER                    PIC X(51)  VALUE                       
               'EXCEPTION REPORT ON FORFEIT AND REFUND VOUCHERS'.               
                                                                                
       01  L42MB-HEADER3.                                                       
                05  FILLER                    PIC X(132) VALUE                  
               '    S/NO    REGN NO     VOUCHER NO       ERROR DESCRIPTI        
      -        'ON'.                                                            
                                                                                
       01  L42MB-HEADER4.                                                       
           05  FILLER                    PIC X(132) VALUE                       
               '    ----    --------    -------------    ---------------        
      -        '-----------------------------------'.                           
                                                                                
       01  L42MB-DETAIL.                                                        
           05  FILLER                    PIC X(4)   VALUE SPACES.               
           05  L42MB-SNO                 PIC ZZZ9.                              
           05  FILLER                    PIC X(4)   VALUE SPACES.               
           05  L42MB-NUM-REGN            PIC X(8)   VALUE SPACES.               
           05  FILLER                    PIC X(4)   VALUE SPACES.               
           05  L42MB-NUM-VR              PIC X(13)  VALUE SPACES.               
           05  FILLER                    PIC X(4)   VALUE SPACES.               
           05  L42MB-NUM-DESC            PIC X(50)  VALUE SPACES.               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-AG07F640-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-F640-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  AG07F640                                                 
                       AG07K970                                                 
                I-O    BP13K62A                                                 
                OUTPUT P13L42MA                                                 
                       P13L42MB.                                                
                                                                                
           IF WS-K62A-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K62A ERROR ' WS-K62A-STATUS                  
              MOVE     WS-K62A-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K970-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K970 ERROR ' WS-K970-STATUS                  
              MOVE     WS-K970-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
                                                                                
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8           TO   WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-AG07F640-READ.                                                      
      *=============================================================            
                                                                                
           READ AG07F640 AT END                                                 
                MOVE 'Y'                   TO   WS-F640-EOF                     
                GO                         TO   2000-EXIT.                      
                                                                                
           ADD 1                           TO   WS-CNT-F640-READ.               
           MOVE F640-NUM-DDD-FILE-REF      TO   WS-NUM-DDD-FILE-REF.            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   L42MB-NUM-DESC.                 
                                                                                
           MOVE 'N'                        TO   WS-K62A-FND                     
                                                WS-K62A-EOF                     
                                                WS-REJECT-FL.                   
                                                                                
           MOVE SPACES                     TO   BP13K62A-REC.                   
           MOVE WS-NUM-REGN                TO   K62A-NUM-REGN.                  
           PERFORM 3100-BP13K62A-STARTBR   THRU 3100-EXIT.                      
                                                                                
           IF WS-K62A-FND = 'Y'                                                 
      *-------------------------------------------------------------            
      *       PROCESS FORFEIT                                                   
      *-------------------------------------------------------------            
              IF K62A-FORFEIT-VR = F640-NUM-VR                                  
                 IF K62A-FORFEIT-BATCH NOT = SPACES AND LOW-VALUES              
                    ADD 1                  TO   WS-CNT-F640-SKIP                
                    MOVE 'VOUCHER ALREADY PROCESSED'                            
                                           TO   L42MB-NUM-DESC                  
                    PERFORM 8900-PRINT-ERROR                                    
                                           THRU 8900-EXIT                       
                    GO TO 3000-PROC-NEXT-REC                                    
                 END-IF                                                         
                                                                                
                 EVALUATE F640-NUM-REJECT                                       
                 WHEN ZEROES                                                    
                 WHEN SPACES                                                    
                 WHEN LOW-VALUES                                                
                    ADD 1                  TO   WS-CNT-F640-ACC                 
                    PERFORM 4000-UPD-FORFEIT                                    
                                           THRU 4000-EXIT                       
                                                                                
                 WHEN OTHER                                                     
                    ADD   1                TO   WS-CNT-F640-REJ                 
                    MOVE 'Y'               TO   WS-REJECT-FL                    
                    MOVE K62A-DTE-FORFEIT-VR                                    
                                           TO   WS-DTE-SUB                      
                    PERFORM 4000-UPD-FORFEIT                                    
                                           THRU 4000-EXIT                       
                    PERFORM 5000-GET-REJ-REASON                                 
                                           THRU 5000-EXIT                       
                    PERFORM 5100-PRINT-REJ                                      
                                           THRU 5100-EXIT                       
                                                                                
                 END-EVALUATE                                                   
              END-IF                                                            
                                                                                
      *-------------------------------------------------------------            
      *       PROCESS REFUND                                                    
      *-------------------------------------------------------------            
              IF K62A-REFUND-VR = F640-NUM-VR                                   
                 IF K62A-REFUND-BATCH NOT = SPACES AND LOW-VALUES               
                    ADD 1                  TO   WS-CNT-F640-SKIP                
                    MOVE 'VOUCHER ALREADY PROCESSED'                            
                                           TO   L42MB-NUM-DESC                  
                    PERFORM 8900-PRINT-ERROR                                    
                                           THRU 8900-EXIT                       
                    GO TO 3000-PROC-NEXT-REC                                    
                 END-IF                                                         
                                                                                
                 EVALUATE F640-NUM-REJECT                                       
                 WHEN ZEROES                                                    
                 WHEN SPACES                                                    
                 WHEN LOW-VALUES                                                
                    ADD 1                  TO   WS-CNT-F640-ACC                 
                    PERFORM 4001-UPD-REFUND                                     
                                           THRU 4001-EXIT                       
                                                                                
                 WHEN OTHER                                                     
                    ADD   1                TO   WS-CNT-F640-REJ                 
                    MOVE 'Y'               TO   WS-REJECT-FL                    
                    MOVE K62A-DTE-REFUND-VR                                     
                                           TO   WS-DTE-SUB                      
                    PERFORM 4001-UPD-REFUND                                     
                                           THRU 4001-EXIT                       
                    PERFORM 5000-GET-REJ-REASON                                 
                                           THRU 5000-EXIT                       
                    PERFORM 5100-PRINT-REJ                                      
                                           THRU 5100-EXIT                       
                                                                                
                 END-EVALUATE                                                   
              END-IF                                                            
           ELSE                                                                 
              ADD 1                        TO   WS-CNT-F640-SKIP                
           END-IF.                                                              
                                                                                
       3000-PROC-NEXT-REC.                                                      
                                                                                
           PERFORM 2000-AG07F640-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3100-BP13K62A-STARTBR.                                                   
      *=============================================================            
                                                                                
           START BP13K62A KEY = K62A-ALT-KEY2.                                  
                                                                                
           EVALUATE WS-K62A-STATUS                                              
           WHEN '00'                                                            
              PERFORM 3200-BP13K62A-READNEXT    THRU 3200-EXIT                  
                      UNTIL WS-K62A-EOF = 'Y'                                   
                         OR WS-K62A-FND = 'Y'                                   
                                                                                
           WHEN '10'                                                            
           WHEN '23'                                                            
              MOVE 'N'                     TO   WS-K62A-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'START BP13K62A ERROR ' WS-K62A-STATUS                    
              MOVE     WS-K62A-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3200-BP13K62A-READNEXT.                                                  
      *=============================================================            
                                                                                
           READ BP13K62A NEXT                                                   
                AT END MOVE 'Y'            TO   WS-K62A-EOF                     
                GO TO 3200-EXIT.                                                
                                                                                
           IF K62A-NUM-REGN NOT = WS-NUM-REGN                                   
              MOVE 'Y'                     TO   WS-K62A-EOF                     
              GO TO 3200-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF K62A-NUM-DOC     = 'ALE' AND                                      
              K62A-NUM-DOC-TYP = 'O'   AND                                      
              K62A-NUM-DOC-CNT =  1                                             
              MOVE 'Y'                     TO   WS-K62A-FND                     
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4000-UPD-FORFEIT.                                                        
      *=============================================================            
                                                                                
           IF WS-REJECT-FL = 'Y'                                                
              MOVE SPACES                  TO   K62A-DTE-FORFEIT-VR             
                                                K62A-FORFEIT-VR                 
           ELSE                                                                 
              MOVE WS-DTE-CURR-8           TO   K62A-DTE-FORFEIT-BATCH          
              MOVE F640-NUM-BATCH          TO   K62A-FORFEIT-BATCH              
           END-IF.                                                              
                                                                                
           PERFORM 4010-REWRITE-BP13K62A   THRU 4010-EXIT.                      
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4001-UPD-REFUND.                                                         
      *=============================================================            
                                                                                
           IF WS-REJECT-FL = 'Y'                                                
              MOVE SPACES                  TO   K62A-DTE-REFUND-VR              
                                                K62A-REFUND-VR                  
           ELSE                                                                 
              MOVE WS-DTE-CURR-8           TO   K62A-DTE-REFUND-BATCH           
              MOVE F640-NUM-BATCH          TO   K62A-REFUND-BATCH               
           END-IF.                                                              
                                                                                
           PERFORM 4010-REWRITE-BP13K62A   THRU 4010-EXIT.                      
                                                                                
       4001-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4010-REWRITE-BP13K62A.                                                   
      *=============================================================            
                                                                                
           REWRITE BP13K62A-REC.                                                
                                                                                
           EVALUATE WS-K62A-STATUS                                              
           WHEN 00                                                              
              ADD 1                        TO   WS-CNT-K62A-UPD                 
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'REWRITE BP13K62A ERROR ' WS-K62A-STATUS                  
              MOVE     WS-K62A-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5000-GET-REJ-REASON.                                                     
      *=============================================================            
                                                                                
           MOVE F640-NUM-REJECT            TO   K970-NUM-CODE.                  
           PERFORM 5010-AG07K970-READ      THRU 5010-EXIT.                      
                                                                                
           IF WS-K970-FND = 'Y'                                                 
              STRING K970-NUM-CODE '-' K970-TXT-EMSG                            
                     DELIMITED BY SIZE     INTO L42MA-NUM-DESC                  
           ELSE                                                                 
              MOVE F640-NUM-REJECT         TO   L42MA-NUM-DESC                  
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5010-AG07K970-READ.                                                      
      *=============================================================            
                                                                                
           READ AG07K970.                                                       
                                                                                
           EVALUATE WS-K970-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K970-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K970-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ AG07K970 ERROR '    WS-K970-STATUS                  
              MOVE     WS-K970-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5100-PRINT-REJ.                                                          
      *=============================================================            
                                                                                
           IF WS-CNT-LINE-A > 55                                                
              PERFORM 5110-PRINT-REJ-HDR   THRU 5110-EXIT                       
           END-IF.                                                              
                                                                                
           ADD  1                          TO   WS-CNT-SNO-A                    
                                                WS-CNT-LINE-A.                  
           MOVE WS-CNT-SNO-A               TO   L42MA-SNO.                      
           MOVE WS-NUM-REGN                TO   L42MA-NUM-REGN.                 
           MOVE F640-NUM-VR                TO   L42MA-NUM-OLD-VR.               
           MOVE WS-DTE-SUB                 TO   L42MA-DTE-SUBMITTED.            
                                                                                
           WRITE P13L42MA-REC              FROM L42MA-DETAIL.                   
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5110-PRINT-REJ-HDR.                                                      
      *=============================================================            
                                                                                
           ADD 1                           TO   WS-CNT-PAGE-A.                  
           MOVE WS-CNT-PAGE-A              TO   L42MA-CNT-PAGE.                 
           MOVE WS-DTE-CURR-10             TO   L42MA-DTE-CURRENT               
                                                L42MA-DTE-CURRENT2.             
                                                                                
           WRITE P13L42MA-REC FROM L42MA-HEADER1 AFTER PAGE.                    
           WRITE P13L42MA-REC FROM L42MA-HEADER2.                               
           WRITE P13L42MA-REC FROM L42MA-HEADER3 AFTER 2.                       
           WRITE P13L42MA-REC FROM L42MA-HEADER4.                               
                                                                                
           MOVE SPACES                     TO   P13L42MA-REC.                   
           WRITE P13L42MA-REC.                                                  
                                                                                
           MOVE 6                          TO   WS-CNT-LINE-A.                  
                                                                                
       5110-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8900-PRINT-ERROR.                                                        
      *=============================================================            
                                                                                
           IF WS-CNT-LINE-B > 55                                                
              PERFORM 8910-PRINT-ERROR-HDR THRU 8910-EXIT                       
           END-IF.                                                              
                                                                                
           ADD  1                          TO   WS-CNT-SNO-B                    
                                                WS-CNT-LINE-B.                  
           MOVE WS-CNT-SNO-B               TO   L42MB-SNO.                      
           MOVE WS-NUM-REGN                TO   L42MB-NUM-REGN.                 
           MOVE F640-NUM-VR                TO   L42MB-NUM-VR.                   
                                                                                
           WRITE P13L42MB-REC             FROM L42MB-DETAIL.                    
                                                                                
       8900-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8910-PRINT-ERROR-HDR.                                                    
      *=============================================================            
                                                                                
           ADD 1                           TO   WS-CNT-PAGE-B.                  
           MOVE WS-CNT-PAGE-B              TO   L42MB-CNT-PAGE.                 
           MOVE WS-DTE-CURR-10             TO   L42MB-DTE-CURRENT.              
                                                                                
           WRITE P13L42MB-REC FROM L42MB-HEADER1 AFTER PAGE.                    
           WRITE P13L42MB-REC FROM L42MB-HEADER2.                               
           WRITE P13L42MB-REC FROM L42MB-HEADER3 AFTER 2.                       
           WRITE P13L42MB-REC FROM L42MB-HEADER4.                               
                                                                                
           MOVE SPACES                     TO   P13L42MB-REC.                   
           WRITE P13L42MB-REC.                                                  
                                                                                
           MOVE 6                          TO   WS-CNT-LINE-B.                  
                                                                                
       8910-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*------- BP13C42M CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF AG07F640 READ      : ' WS-CNT-F640-READ.          
           DISPLAY ' 2. NO OF AG07F640 ACCEPTED  : ' WS-CNT-F640-ACC.           
           DISPLAY ' 3. NO OF AG07F640 REJECTED  : ' WS-CNT-F640-REJ.           
           DISPLAY ' 4. NO OF AG07F640 SKIPPED   : ' WS-CNT-F640-SKIP.          
           DISPLAY ' 5. NO OF BP13K62A UPDATED   : ' WS-CNT-K62A-UPD.           
           DISPLAY ' '.                                                         
                                                                                
           CLOSE AG07F640                                                       
                 AG07K970                                                       
                 BP13K62A                                                       
                 P13L42MA                                                       
                 P13L42MB.                                                      
                                                                                
           IF WS-K62A-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K62A ERROR ' WS-K62A-STATUS                  
              MOVE     WS-K62A-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K970-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K970 ERROR ' WS-K970-STATUS                  
              MOVE     WS-K970-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
