      **********************************************************                
       IDENTIFICATION DIVISION.                                                 
      **********************************************************                
       PROGRAM-ID. BP13C962.                                                    
      *AUTHOR. SK.                                                              
      *DATE-WRITTEN.   30/05/2003.                                              
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE  : DETAILS OF FLAT                        *                
      *     INPUT FILES:                                       *                
      *           1. BM06F110                                  *                
      *           2. BP13K703                                  *                
      *                                                        *                
      *     OUTPUT FILE:                                       *                
      *           1. BP13F110                                  *                
      *                                                        *                
      * CHG-NO   BY    DATE     DETAILS                        *                
      * -------- ----  -------- -------------------------------*                
      * BP132360 SK    31062003 NEW PGM                        *                
      * BP132402 CKK   09092003 REMOVE HARDCODE AND ALLOW THE  *                
      *                         PRG TO BE USE FOR BE AND BTO   *                
      **********************************************************                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K703 ASSIGN  TO BP13K703                                  
                  ACCESS MODE      IS DYNAMIC                                   
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS K703-KEY-FLD                              
                  FILE STATUS      IS WS-K703-STATUS.                           
                                                                                
           SELECT BM06F110   ASSIGN  TO BM06F110.                               
           SELECT M06F110A   ASSIGN  TO M06F110A.                               
                                                                                
      *--------------------------------------------------------*                
       DATA DIVISION.                                                           
      *--------------------------------------------------------*                
       FILE SECTION.                                                            
                                                                                
       FD   BP13K703                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
                                                                                
       COPY BP13K703.                                                           
                                                                                
       FD   BM06F110                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BM06F110.                                                           
                                                                                
       FD   M06F110A                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       01  M06F110A-REC      PIC X(500).                                        
                                                                                
      *--------------------------------------------------------*                
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------*                
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F110-EOF         PIC X       VALUE 'N'.                       
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-MATCH            PIC 9(8)    VALUE 0.                         
           05  WS-NOT-MATCH        PIC 9(8)    VALUE 0.                         
           05  WS-READ-F110        PIC 9(8)    VALUE 0.                         
           05  WS-WRITE            PIC 9(8)    VALUE 0.                         
           05  WS-PIDB-NOTFND      PIC 9(8)    VALUE 0.                         
                                                                                
           05  WS-K703-STATUS      PIC 9(2)    VALUE 0.                         
           05  WS-K703-DTE         PIC X(6)    VALUE SPACES.                    
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY         PIC 9(4).                                    
           05  WS-SYS-MM           PIC 9(2).                                    
           05  WS-SYS-DD           PIC 9(2).                                    
                                                                                
       01  WS-EDITED-DATE.                                                      
           05  WS-EDITED-DD        PIC 9(2).                                    
           05  FILLER              PIC X(1)    VALUE '/'.                       
           05  WS-EDITED-MM        PIC 9(2).                                    
           05  FILLER              PIC X(1)    VALUE '/'.                       
           05  WS-EDITED-CCYY      PIC 9(4).                                    
                                                                                
       01  WS-SCH-ACC-NO                  PIC X(09) VALUE SPACES.               
                                                                                
       01  WS-SQL-CODES.                                                        
           05  WS-SQL-FOUND               PIC S9(4) COMP VALUE +000.            
           05  WS-SQL-NOT-FOUND           PIC S9(4) COMP VALUE +100.            
                                                                                
           EXEC SQL INCLUDE SQLCA    END-EXEC.                                  
           EXEC SQL INCLUDE P13VINTF END-EXEC.                                  
                                                                                
      *--------------------------------------------------------*                
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------*                
                                                                                
      *--------------------------------------------------------*                
       0000-MAIN.                                                               
      *--------------------------------------------------------*                
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-F110     THRU 2000-EXIT.                           
           PERFORM 1500-READ-K703     THRU 1500-EXIT.                           
           PERFORM 3000-PROCESS       THRU 3000-EXIT                            
                   UNTIL WS-F110-EOF = 'Y'                                      
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       1000-OPEN-FILES.                                                         
      *--------------------------------------------------------*                
                                                                                
           OPEN INPUT  BP13K703                                                 
                       BM06F110                                                 
                OUTPUT M06F110A.                                                
                                                                                
           EVALUATE WS-K703-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                   CONTINUE                                                     
              WHEN OTHER                                                        
                   MOVE WS-K703-STATUS      TO RETURN-CODE                      
                   DISPLAY 'OPEN BP13K703 FAIL. STATUS=' WS-K703-STATUS         
                   PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATE.                      
           MOVE WS-SYS-DD               TO WS-EDITED-DD.                        
           MOVE WS-SYS-MM               TO WS-EDITED-MM.                        
           MOVE WS-SYS-CCYY             TO WS-EDITED-CCYY.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       1500-READ-K703.                                                          
      *--------------------------------------------------------*                
                                                                                
           MOVE LOW-VALUES            TO K703-KEY-FLD.                          
           MOVE F110-NUM-SELECTION    TO K703-NUM-SOCLIST.                      
           MOVE F110-NUM-SELECTION    TO K703-NUM-NT.                           
           MOVE '00'                  TO K703-NUM-FLAT.                         
                                                                                
           READ BP13K703.                                                       
                                                                                
           EVALUATE WS-K703-STATUS                                              
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN 23                                                           
                 DISPLAY 'NOT FOUND IN BP13K703.'                               
                 PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                     
              WHEN OTHER                                                        
                 MOVE WS-K703-STATUS     TO RETURN-CODE                         
                 DISPLAY 'ERROR READING BP13K703, STATUS = '                    
                                        WS-K703-STATUS                          
                 PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
           MOVE K703-DTE-BALLOT-ORIGIN      TO WS-K703-DTE.                     
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2000-READ-F110.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ BM06F110                                                        
                AT END MOVE 'Y' TO WS-F110-EOF                                  
                GO TO 2000-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-READ-F110.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       3000-PROCESS.                                                            
      *--------------------------------------------------------*                
                                                                                
           IF F110-NUM-ALLOCN-QTR = WS-K703-DTE                                 
              PERFORM 3500-READ-PIDB     THRU 3500-EXIT                         
              PERFORM 4000-WRITE-F110A   THRU 4000-EXIT                         
              ADD 1                      TO WS-MATCH                            
           ELSE                                                                 
              ADD 1                      TO WS-NOT-MATCH                        
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F110        THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       3500-READ-PIDB.                                                          
      *--------------------------------------------------------*                
                                                                                
           MOVE F110-SCH-ACC-NO(1:9)    TO WS-SCH-ACC-NO.                       
                                                                                
           EXEC SQL SELECT NUM_CCC                                              
               INTO :DCLPIDB-INTERFACE.NUM-CCC                                  
                    FROM PIDB_INTERFACE                                         
                    WHERE NUM_SCHM_ACNT = :WS-SCH-ACC-NO                        
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
              WHEN 000                                                          
                 MOVE NUM-CCC    TO F110-NUM-WARD                               
              WHEN 100                                                          
                 ADD 1 TO WS-PIDB-NOTFND                                        
                 DISPLAY 'RECORD NOT FOUND IN PIDB TABLE : '                    
                                              WS-SCH-ACC-NO                     
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN READING PIDB INTERFACE TABLE'                
                 GO TO 9900-ABORT-PROCESS                                       
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4000-WRITE-F110A.                                                        
      *--------------------------------------------------------*                
                                                                                
           MOVE SPACES               TO M06F110A-REC.                           
           INITIALIZE                   M06F110A-REC.                           
                                                                                
           WRITE M06F110A-REC        FROM F110-REC.                             
           ADD 1                     TO WS-WRITE.                               
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       9000-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------*                
                                                                                
           CLOSE BP13K703                                                       
                 BM06F110                                                       
                 M06F110A.                                                      
                                                                                
           EVALUATE WS-K703-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K703-STATUS TO RETURN-CODE                             
                 DISPLAY 'BP13K703 CLOSE ERROR, STATUS='                        
                                                WS-K703-STATUS                  
           END-EVALUATE.                                                        
                                                                                
           DISPLAY '******* BP13C962 *************'.                            
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECORDS READ    : '.                                  
           DISPLAY '   BM06F110           : '  WS-READ-F110.                    
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECORDS WRITTEN : '.                                  
           DISPLAY '   M06F110A           : ' WS-WRITE.                         
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF MATCH          : ' WS-MATCH.                         
           DISPLAY 'NO. NOT MATCH         : ' WS-NOT-MATCH.                     
           DISPLAY 'PIDB NOT FOUND CASES  : ' WS-PIDB-NOTFND.                   
           DISPLAY SPACES.                                                      
           DISPLAY '******************************'.                            
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       9900-ABORT-PROCESS.                                                      
      *--------------------------------------------------------*                
                                                                                
           CALL 'DBATIAR' USING SQLCA.                                          
           MOVE SQLCODE   TO RETURN-CODE.                                       
           GOBACK.                                                              
                                                                                
