       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CE16.                                                 
      *AUTHOR.        KSJ3.                                                     
      *DATE-WRITTEN.  25/02/2016.                                               
                                                                                
      * ========================================================== *            
      *                SYSTEM OF COMMITMENT (BP13)                 *            
      * ========================================================== *            
      *  OBJECTIVE : PROCESS BP13F491 AND MATCH TO BP13K859. WRITE *            
      *              MISSING RECORDS TO VSAM FILE.                 *            
      *                                                            *            
      *  INPUT   :  BP13F491 - EMAIL FILE                          *            
      *             BM06K110 - FIS FLAT INVENTORY FILE             *            
      *                                                            *            
      *  I-O     :  BP13K859 - PCD LETTER EMAIL LOG                *            
      *                                                            *            
      * ========================================================== *            
      * CHG-NO    BY    DATE    DESCRIPTION                        *            
      * BP134933  KSJ3  250216  NEW PROGRAM                        *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F491  ASSIGN        TO BP13F491.                          
                                                                                
           SELECT BP13K859  ASSIGN        TO BP13K859                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K859-KEY-FLD                       
                            FILE STATUS   IS WS-K859-STATUS.                    
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K110-KEY-FLD                       
                            ALTERNATE KEY IS K110-REGN-NO                       
                            FILE STATUS   IS WS-K110-STATUS.                    
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F491                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 200 CHARACTERS.                                      
           COPY BP13F491.                                                       
                                                                                
       FD  BP13K859                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
           COPY BP13K859.                                                       
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
           COPY BM06K110.                                                       
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K859-STATUS            PIC 9(2)   VALUE 00.                   
           05  WS-K110-STATUS            PIC 9(2)   VALUE 00.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F491-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F491-FND           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F491-NFND          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K859-WRT           PIC 9(6)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F491-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K859-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K110-FND               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-FMT8-CCYY      PIC 9(4)   VALUE ZEROES.               
               10  WS-DTE-FMT8-MM        PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-FMT8-DD        PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-FMT10-DD       PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-FMT10-MM       PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-FMT10-CCYY.                                           
                   15  WS-DTE-FMT10-CC   PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-FMT10-YY   PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE SPACES.               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-BP13F491-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-F491-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13F491                                                 
                       BM06K110                                                 
                I-O    BP13K859.                                                
                                                                                
           IF WS-K859-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K859 ERROR ' WS-K859-STATUS                  
              MOVE     WS-K859-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT.                      
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT.                      
                                                                                
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
                                                                                
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           PERFORM 8100-CONV-DATE-TO-10    THRU 8100-EXIT.                      
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13F491-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13F491 AT END                                                 
                MOVE 'Y'                   TO   WS-F491-EOF                     
                GO                         TO   2000-EXIT.                      
                                                                                
           ADD 1                           TO   WS-CNT-F491-READ.               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   BP13K859-REC.                   
           MOVE F491-NUM-REGN              TO   K859-NUM-REGN.                  
           PERFORM 3100-READ-BP13K859      THRU 3100-EXIT.                      
                                                                                
           IF WS-K859-FND = 'Y'                                                 
              ADD 1                        TO   WS-CNT-F491-FND                 
           ELSE                                                                 
              ADD 1                        TO   WS-CNT-F491-NFND                
                                                                                
              MOVE SPACES                  TO   BP13K859-REC                    
              MOVE F491-NUM-REGN           TO   K859-NUM-REGN                   
              MOVE F491-DTE-PCD            TO   K859-DTE-PCD                    
              MOVE F491-NUM-EMAIL          TO   K859-NUM-EMAIL                  
              MOVE F491-NUM-BLK            TO   K859-NUM-BLK                    
              MOVE F491-NUM-LEVEL          TO   K859-NUM-LEVEL                  
              MOVE F491-NUM-MAIN-UNIT      TO   K859-NUM-MAIN-UNIT              
              MOVE F491-NUM-STREET         TO   K859-NUM-STREET                 
              MOVE F491-NUM-POSTAL         TO   K859-NUM-POSTAL                 
              MOVE F491-NME-OIC            TO   K859-NME-OIC                    
              MOVE F491-TAG-FORM           TO   K859-TAG-FORM                   
                                                                                
              MOVE SPACES                  TO   K110-REC                        
              MOVE F491-NUM-REGN           TO   K110-REGN-NO                    
              PERFORM 3200-READ-BM06K110   THRU 3200-EXIT                       
                                                                                
              IF WS-K110-FND = 'Y'                                              
                 MOVE K110-ESTATE          TO   K859-ESTATE                     
                 MOVE K110-NEIGHBOURHOOD   TO   K859-NEIGH                      
                 MOVE K110-CONTRACT-NO     TO   K859-CNTRACT                    
                 MOVE K110-BLK-NO          TO   K859-BLKNO                      
              END-IF                                                            
                                                                                
              MOVE F491-DTE-UPDATE         TO   K859-DTE-MAIL-SENT              
                                                                                
              PERFORM 3300-WRITE-BP13K859  THRU 3300-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 2000-BP13F491-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3100-READ-BP13K859.                                                      
      *=============================================================            
                                                                                
           READ BP13K859.                                                       
                                                                                
           EVALUATE WS-K859-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K859-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K859-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K859 ERROR '    WS-K859-STATUS                  
              MOVE    WS-K859-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3200-READ-BM06K110.                                                      
      *=============================================================            
                                                                                
           READ BM06K110 KEY IS K110-REGN-NO.                                   
                                                                                
           EVALUATE WS-K110-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K110-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K110-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BM06K110 ERROR '    WS-K110-STATUS                  
              MOVE    WS-K110-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3300-WRITE-BP13K859.                                                     
      *=============================================================            
                                                                                
           WRITE BP13K859-REC.                                                  
                                                                                
           EVALUATE WS-K859-STATUS                                              
           WHEN 00                                                              
              ADD 1                        TO   WS-CNT-K859-WRT                 
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'WRITE ERROR ' F491-KEY-FLD '-' WS-K859-STATUS            
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8000-CONV-DATE-TO-8.                                                     
      *=============================================================            
      *    INPUT  - WS-DTE-FMT10                                                
      *    OUTPUT - WS-DTE-FMT8                                                 
      *-------------------------------------------------------------            
                                                                                
           MOVE WS-DTE-FMT10-CCYY  TO  WS-DTE-FMT8-CCYY.                        
           MOVE WS-DTE-FMT10-MM    TO  WS-DTE-FMT8-MM.                          
           MOVE WS-DTE-FMT10-DD    TO  WS-DTE-FMT8-DD.                          
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8100-CONV-DATE-TO-10.                                                    
      *=============================================================            
      *    INPUT  - WS-DTE-FMT8                                                 
      *    OUTPUT - WS-DTE-FMT10                                                
      *-------------------------------------------------------------            
                                                                                
           MOVE WS-DTE-FMT8-CCYY   TO  WS-DTE-FMT10-CCYY.                       
           MOVE WS-DTE-FMT8-MM     TO  WS-DTE-FMT10-MM.                         
           MOVE WS-DTE-FMT8-DD     TO  WS-DTE-FMT10-DD.                         
                                                                                
       8100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*------- BP13AD23 CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1.  NO OF BP13F491 READ      : ' WS-CNT-F491-READ.         
           DISPLAY ' 2.  NO OF BP13K859 FOUND     : ' WS-CNT-F491-FND.          
           DISPLAY ' 2.  NO OF BP13K859 NOT FOUND : ' WS-CNT-F491-NFND.         
           DISPLAY ' 3.  NO OF BP13K859 WRITTEN   : ' WS-CNT-K859-WRT.          
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13F491                                                       
                 BM06K110                                                       
                 BP13K859.                                                      
                                                                                
           IF WS-K859-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K859 ERROR ' WS-K859-STATUS                  
              MOVE     WS-K859-STATUS      TO   RETURN-CODE.                    
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO   RETURN-CODE.                    
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
