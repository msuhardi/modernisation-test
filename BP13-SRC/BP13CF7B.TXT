       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF7B.                                                 
      *AUTHOR.        IMC1.                                                     
      *DATE-WRITTEN.  09/05/2013.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE    : LIST OF CASES WITH AST ELIGIBLE, AST NOT    *            
      *                ELIGIBLE AND AST PENDING                    *            
      *                                                            *            
      * INPUT  FILE  : BP13F730 - CASES PER REGN                   *            
      *                BP13K825 - BP13.K825.REQOCCP                *            
      *                BP13K060 - BP13.K060.CODETAB                *            
      * OUTPUT FILE  : P13LF73A - AST ELIGIBLE                     *            
      *                P13LF73B - AST NOT ELIGIBLE                 *            
      *                P13LF73C - AST PENDING                      *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP134922  IMC1  09052013  NEW PROGRAM                      *            
      * BP137132  KR13  30072018  ADDED PAGE BREAK FOR REPORTS     *            
      * BP137749  AM25  11032019  CATER FOR CHILD AGE INCR TO 18   *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN       TO BP13F730.                            
                                                                                
           SELECT BP13K825 ASSIGN       TO BP13K825                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K825-KEY-FLD                         
                           FILE STATUS  IS WS-K825-STATUS.                      
                                                                                
           SELECT BP13K060 ASSIGN       TO BP13K060                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K060-KEY-FLD                         
                           FILE STATUS  IS WS-K060-STATUS.                      
                                                                                
           SELECT P13LF73A ASSIGN       TO P13LF73A.                            
           SELECT P13LF73B ASSIGN       TO P13LF73B.                            
           SELECT P13LF73C ASSIGN       TO P13LF73C.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F730                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  500 CHARACTERS                                     
            RECORDING MODE    IS F                                              
            LABEL RECORD      IS STANDARD.                                      
       COPY BP13F730.                                                           
                                                                                
       FD   BP13K825                                                            
            RECORD CONTAINS  200 CHARACTERS.                                    
       COPY BP13K825.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS   25 CHARACTERS.                                    
       COPY BP13K060.                                                           
                                                                                
       FD   P13LF73A                                                            
            RECORDING MODE    IS F                                              
            RECORD CONTAINS  132 CHARACTERS.                                    
       01   P13LF73A-REC                   PIC X(132).                          
                                                                                
       FD   P13LF73B                                                            
            RECORDING MODE    IS F                                              
            RECORD CONTAINS  132 CHARACTERS.                                    
       01   P13LF73B-REC                   PIC X(132).                          
                                                                                
       FD   P13LF73C                                                            
            RECORDING MODE    IS F                                              
            RECORD CONTAINS  132 CHARACTERS.                                    
       01   P13LF73C-REC                   PIC X(132).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K825-STATUS              PIC 9(02)   VALUE ZEROES.            
           05  WS-K060-STATUS              PIC 9(02)   VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F730-READ                PIC 9(05)   VALUE ZEROES.            
           05  WS-YES-SNO                  PIC 9(05)   VALUE ZEROES.            
           05  WS-NO-SNO                   PIC 9(05)   VALUE ZEROES.            
           05  WS-NO-OTHERS                PIC 9(05)   VALUE ZEROES.            
           05  WS-PEND-SNO                 PIC 9(05)   VALUE ZEROES.            
           05  WS-LINE-A                   PIC 9(02)   VALUE ZEROES.            
           05  WS-LINE-B                   PIC 9(02)   VALUE ZEROES.            
           05  WS-LINE-C                   PIC 9(02)   VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F730-EOF                 PIC X(01)   VALUE 'N'.               
           05  WS-K825-EOF                 PIC X(01)   VALUE 'N'.               
           05  WS-K825-FND                 PIC X(01)   VALUE 'N'.               
           05  WS-OCCP-FND                 PIC X(01)   VALUE 'N'.               
           05  WS-YES-HDR                  PIC X(01)   VALUE 'N'.               
           05  WS-NO-HDR                   PIC X(01)   VALUE 'N'.               
           05  WS-PEND-HDR                 PIC X(01)   VALUE 'N'.               
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-CUR-DATE                 PIC X(08)   VALUE SPACES.            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NRIC                     PIC X(09)   VALUE SPACES.            
           05  WS-NUM-AGE-CHILD            PIC 9(02)   VALUE ZEROES.            
           05  WS-NUM-SYS                  PIC X(01)   VALUE ZEROES.            
                                                                                
       01 WS-REPORT-VARS.                                                       
          05 LF73-HEADER1.                                                      
             10 LF73-RPTNAME      PIC X(08) VALUE SPACES.                       
             10 FILLER            PIC X(05) VALUE SPACES.                       
             10 FILLER            PIC X(08) VALUE 'HDBCAT 3'.                   
             10 FILLER            PIC X(29) VALUE SPACES.                       
             10 FILLER            PIC X(20) VALUE                               
                'SYSTEM OF COMMITMENT'.                                         
             10 FILLER            PIC X(23) VALUE SPACES.                       
             10 FILLER            PIC X(07) VALUE 'DATE : '.                    
             10 LF73-DATE         PIC X(12) VALUE SPACES.                       
          05 LF73-HEADER2.                                                      
             10 FILLER            PIC X(47) VALUE SPACES.                       
             10 FILLER            PIC X(26) VALUE                               
                'LIST OF AST ELIGIBLE CASES'.                                   
          05 LF73-HEADER3.                                                      
             10 FILLER            PIC X(45) VALUE SPACES.                       
             10 FILLER            PIC X(30) VALUE                               
                'LIST OF AST NOT ELIGIBLE CASES'.                               
          05 LF73-HEADER4.                                                      
             10 FILLER            PIC X(47) VALUE SPACES.                       
             10 FILLER            PIC X(25) VALUE                               
                'LIST OF AST PENDING CASES'.                                    
          05 LF73-HEADER5.                                                      
             10 FILLER            PIC X(33) VALUE                               
                ' S/NO  REG NO        RELATIONSHIP'.                            
             10 FILLER            PIC X(38) VALUE                               
                '         MARITAL STATUS  BIRTHDATE    '.                       
             10 FILLER            PIC X(11) VALUE 'CITIZENSHIP'.                
             10 FILLER            PIC X(09) VALUE SPACES.                       
             10 FILLER            PIC X(11) VALUE ' SCHEME-ACC'.                
             10 FILLER            PIC X(01) VALUE SPACES.                       
             10 FILLER            PIC X(08) VALUE 'AST-DATE'.                   
             10 FILLER            PIC X(01) VALUE SPACES.                       
             10 FILLER            PIC X(03) VALUE 'SYS'.                        
             10 FILLER            PIC X(01) VALUE SPACES.                       
             10 FILLER            PIC X(08) VALUE 'FLAT-DTE'.                   
             10 FILLER            PIC X(01) VALUE SPACES.                       
             10 FILLER            PIC X(03) VALUE 'AST'.                        
          05 LF73-BLANK.                                                        
             10 FILLER            PIC X(132) VALUE SPACES.                      
          05 LF73-YES-REC.                                                      
             10 FILLER            PIC X(01) VALUE SPACES.                       
             10 LF73-YES-SNO      PIC X(05) VALUE SPACES.                       
             10 FILLER            PIC X(01) VALUE SPACES.                       
             10 LF73-YES-REGN     PIC X(08) VALUE SPACES.                       
             10 FILLER            PIC X(01) VALUE SPACES.                       
             10 LF73-YES-DET      PIC X(116) VALUE SPACES.                      
          05 LF73-NO-REC.                                                       
             10 FILLER            PIC X(01) VALUE SPACES.                       
             10 LF73-NO-SNO       PIC X(05) VALUE SPACES.                       
             10 FILLER            PIC X(01) VALUE SPACES.                       
             10 LF73-NO-REGN      PIC X(08) VALUE SPACES.                       
             10 FILLER            PIC X(01) VALUE SPACES.                       
             10 LF73-NO-DET       PIC X(116) VALUE SPACES.                      
          05 LF73-PEND-REC.                                                     
             10 FILLER            PIC X(01) VALUE SPACES.                       
             10 LF73-PEND-SNO     PIC X(05) VALUE SPACES.                       
             10 FILLER            PIC X(01) VALUE SPACES.                       
             10 LF73-PEND-REGN    PIC X(08) VALUE SPACES.                       
             10 FILLER            PIC X(01) VALUE SPACES.                       
             10 LF73-PEND-DET     PIC X(116) VALUE SPACES.                      
          05 LF73-DET-REC.                                                      
             10 LF73-DET-APPLN    PIC X(03) VALUE SPACES.                       
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 LF73-DET-REL      PIC X(17) VALUE SPACES.                       
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 LF73-DET-MS       PIC X(11) VALUE SPACES.                       
             10 FILLER            PIC X(05) VALUE SPACES.                       
             10 LF73-DET-BIRTH    PIC X(10) VALUE SPACES.                       
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 LF73-DET-CITZ     PIC X(19) VALUE SPACES.                       
             10 FILLER            PIC X(01) VALUE SPACES.                       
             10 LF73-SCH-ACC      PIC X(11) VALUE SPACES.                       
             10 FILLER            PIC X(01) VALUE SPACES.                       
             10 LF73-DTE-AST      PIC X(08) VALUE SPACES.                       
             10 FILLER            PIC X(01) VALUE SPACES.                       
             10 LF73-NUM-SYS      PIC X(03) VALUE SPACES.                       
             10 FILLER            PIC X(01) VALUE SPACES.                       
             10 LF73-DTE-FLAT     PIC X(08) VALUE SPACES.                       
             10 FILLER            PIC X(02) VALUE SPACES.                       
             10 LF73-NUM-AST      PIC X(01) VALUE SPACES.                       
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ********************                                                      
       0000-MAIN-ROUTINE.                                                       
      ********************                                                      
                                                                                
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F730      THRU 2000-EXIT                       
             UNTIL WS-F730-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       1000-OPEN-FILES.                                                         
      ******************                                                        
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13K825                                                 
                       BP13K060                                                 
                OUTPUT P13LF73A                                                 
                       P13LF73B                                                 
                       P13LF73C.                                                
                                                                                
           IF WS-K825-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K825 FILE ERROR ' WS-K825-STATUS             
              MOVE WS-K825-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K060-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K060 FILE ERROR ' WS-K060-STATUS             
              MOVE WS-K060-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-CUR-DATE.                      
           STRING WS-CUR-DATE(7:2), '/',                                        
                  WS-CUR-DATE(5:2), '/',                                        
                  WS-CUR-DATE(1:4) DELIMITED BY SIZE                            
             INTO LF73-DATE.                                                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       2000-READ-BP13F730.                                                      
      *********************                                                     
                                                                                
           READ BP13F730 AT END                                                 
                MOVE 'Y'                   TO WS-F730-EOF                       
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                           TO WS-F730-READ.                     
           PERFORM 3000-PROCESS-RECS       THRU 3000-EXIT.                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       3000-PROCESS-RECS.                                                       
      ********************                                                      
                                                                                
           MOVE SPACES                     TO BP13K825-REC.                     
           INITIALIZE                         BP13K825-REC.                     
                                                                                
           MOVE 'N'                        TO WS-K825-FND.                      
                                                                                
           IF F730-CDE-BALLOT-HOUSEHOLD = 'G'                                   
              EVALUATE F730-NUM-AST                                             
              WHEN 'Y'                                                          
                 ADD 1                     TO WS-YES-SNO                        
                                                                                
              WHEN 'N'                                                          
                 IF (F730-NUM-YOUNG-CHILD-AGE-SC  <= 18 OR                      
                     F730-NUM-YOUNG-CHILD-AGE-SC   = 'BB') OR                   
                    (F730-NUM-YOUNG-CHILD-AGE-NSC <= 18 OR                      
                     F730-NUM-YOUNG-CHILD-AGE-NSC  = 'BB')                      
                     ADD 1                  TO WS-NO-SNO                        
                 ELSE                                                           
                    ADD 1                   TO WS-NO-OTHERS                     
                    GO TO 3000-EXIT                                             
                 END-IF                                                         
                                                                                
              WHEN 'P'                                                          
                 ADD 1                     TO WS-PEND-SNO                       
              END-EVALUATE                                                      
                                                                                
              MOVE 'HA1'                   TO LF73-DET-APPLN                    
              MOVE BP13F730-REC(1:11)      TO LF73-SCH-ACC                      
              MOVE BP13F730-REC(20:8)      TO LF73-DTE-AST                      
              PERFORM 3100-MOVE-SYSTEM     THRU 3100-EXIT                       
              MOVE F730-NUM-NRIC1          TO WS-NRIC                           
              PERFORM 4000-READ-BP13K825   THRU 4000-EXIT                       
              IF WS-K825-FND = 'Y'                                              
                 PERFORM 8000-WRITE-BP13LF73  THRU 8000-EXIT                    
              END-IF                                                            
              PERFORM 4300-CHECK-OTHER-HA  THRU 4300-EXIT                       
              PERFORM 5000-STARTBR-K825    THRU 5000-EXIT                       
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       3100-MOVE-SYSTEM.                                                        
      *********************                                                     
           MOVE BP13F730-REC(12:8)      TO LF73-DTE-FLAT                        
           MOVE BP13F730-REC(28:1)      TO WS-NUM-SYS                           
                                                                                
           EVALUATE WS-NUM-SYS                                                  
            WHEN 'S'                                                            
                MOVE 'SMS'              TO LF73-NUM-SYS                         
            WHEN 'R'                                                            
                MOVE 'RSL'              TO LF73-NUM-SYS                         
            WHEN 'M'                                                            
                MOVE 'MAM'              TO LF73-NUM-SYS                         
            WHEN 'L'                                                            
                MOVE 'SLS'              TO LF73-NUM-SYS                         
            WHEN 'T'                                                            
                MOVE 'TRF'              TO LF73-NUM-SYS                         
            WHEN 'D'                                                            
                MOVE 'DBR'              TO LF73-NUM-SYS                         
            WHEN 'P'                                                            
                MOVE 'PPO'              TO LF73-NUM-SYS                         
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       4000-READ-BP13K825.                                                      
      *********************                                                     
                                                                                
           MOVE F730-NUM-REGN              TO K825-NUM-REGN.                    
           MOVE WS-NRIC                    TO K825-NUM-NRIC.                    
                                                                                
           READ BP13K825.                                                       
                                                                                
           EVALUATE WS-K825-STATUS                                              
           WHEN ZEROES                                                          
              MOVE 'Y'                     TO WS-K825-FND                       
              STRING K825-DTE-BIRTH(7:2), '/',                                  
                     K825-DTE-BIRTH(5:2), '/',                                  
                     K825-DTE-BIRTH(1:4) DELIMITED BY SIZE                      
                INTO LF73-DET-BIRTH                                             
                                                                                
              MOVE K825-NUM-AST            TO LF73-NUM-AST                      
              PERFORM 4100-MOVE-DECODED    THRU 4100-EXIT                       
                                                                                
           WHEN 23                                                              
              MOVE 'N'                     TO WS-K825-FND                       
              DISPLAY 'RECORD NOT FOUND IN BP13K825 : ' K825-KEY-FLD            
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING ON BP13K825'                               
              MOVE WS-K825-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       4100-MOVE-DECODED.                                                       
      ********************                                                      
                                                                                
           IF K825-NUM-RELATIONSHIP NOT = SPACES AND LOW-VALUES                 
              MOVE SPACES                  TO BP13K060-REC                      
              MOVE '35'                    TO K060-SERIAL-NO                    
              MOVE K825-NUM-RELATIONSHIP   TO K060-CODE                         
              PERFORM 4200-READ-BP13K060   THRU 4200-EXIT                       
              MOVE K060-DESC               TO LF73-DET-REL                      
           END-IF.                                                              
                                                                                
           IF K825-NUM-MARITAL-STATUS NOT = SPACES AND LOW-VALUES               
              MOVE SPACES                  TO BP13K060-REC                      
              MOVE '21'                    TO K060-SERIAL-NO                    
              MOVE K825-NUM-MARITAL-STATUS TO K060-CODE                         
              PERFORM 4200-READ-BP13K060   THRU 4200-EXIT                       
              MOVE K060-DESC               TO LF73-DET-MS                       
           END-IF.                                                              
                                                                                
           IF K825-NUM-CITIZENSHIP NOT = SPACES AND LOW-VALUES                  
              MOVE SPACES                  TO BP13K060-REC                      
              MOVE '25'                    TO K060-SERIAL-NO                    
              MOVE K825-NUM-CITIZENSHIP    TO K060-CODE                         
              PERFORM 4200-READ-BP13K060   THRU 4200-EXIT                       
              MOVE K060-DESC               TO LF73-DET-CITZ                     
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       4200-READ-BP13K060.                                                      
      *********************                                                     
                                                                                
           READ BP13K060.                                                       
                                                                                
           EVALUATE WS-K060-STATUS                                              
           WHEN '00'                                                            
              CONTINUE                                                          
                                                                                
           WHEN '23'                                                            
              DISPLAY 'BP13K060 RECORD NOT FOUND : '                            
                       K060-SERIAL-NO ' ' K060-CODE                             
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K060 : ' WS-K060-STATUS                
              MOVE WS-K060-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       4300-CHECK-OTHER-HA.                                                     
      **********************                                                    
                                                                                
           IF F730-NUM-NRIC2 NOT = SPACES AND LOW-VALUES                        
              IF F730-NUM-NRIC2(1:1) NOT = '#'                                  
                 MOVE SPACES               TO BP13K825-REC                      
                 INITIALIZE                   BP13K825-REC                      
                                                                                
                 MOVE 'HA2'                TO LF73-DET-APPLN                    
                 MOVE F730-NUM-NRIC2       TO WS-NRIC                           
                 PERFORM 4000-READ-BP13K825   THRU 4000-EXIT                    
                 IF WS-K825-FND = 'Y'                                           
                    PERFORM 8000-WRITE-BP13LF73  THRU 8000-EXIT                 
                 END-IF                                                         
              ELSE                                                              
                 GO TO 4300-EXIT                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F730-NUM-NRIC3 NOT = SPACES AND LOW-VALUES                        
              IF F730-NUM-NRIC3(1:1) NOT = '#'                                  
                 MOVE SPACES               TO BP13K825-REC                      
                 INITIALIZE                   BP13K825-REC                      
                                                                                
                 MOVE 'HA3'                TO LF73-DET-APPLN                    
                 MOVE F730-NUM-NRIC3       TO WS-NRIC                           
                 PERFORM 4000-READ-BP13K825   THRU 4000-EXIT                    
                 IF WS-K825-FND = 'Y'                                           
                    PERFORM 8000-WRITE-BP13LF73  THRU 8000-EXIT                 
                 END-IF                                                         
              ELSE                                                              
                 GO TO 4300-EXIT                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F730-NUM-NRIC4 NOT = SPACES AND LOW-VALUES                        
              IF F730-NUM-NRIC4(1:1) NOT = '#'                                  
                 MOVE SPACES               TO BP13K825-REC                      
                 INITIALIZE                   BP13K825-REC                      
                                                                                
                 MOVE 'HA4'                TO LF73-DET-APPLN                    
                 MOVE F730-NUM-NRIC4       TO WS-NRIC                           
                 PERFORM 4000-READ-BP13K825   THRU 4000-EXIT                    
                 IF WS-K825-FND = 'Y'                                           
                    PERFORM 8000-WRITE-BP13LF73  THRU 8000-EXIT                 
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       5000-STARTBR-K825.                                                       
      ********************                                                      
                                                                                
           MOVE SPACES                     TO BP13K825-REC.                     
           INITIALIZE                         BP13K825-REC.                     
                                                                                
           MOVE 'N'                        TO WS-K825-EOF                       
                                              WS-OCCP-FND.                      
                                                                                
           MOVE F730-NUM-REGN              TO K825-NUM-REGN.                    
                                                                                
           START BP13K825 KEY >= K825-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K825-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              PERFORM 5100-READNEXT-K825   THRU 5100-EXIT                       
              PERFORM 5200-PROCESS-K825    THRU 5200-EXIT                       
                UNTIL WS-K825-EOF = 'Y'                                         
                                                                                
           WHEN 23                                                              
           WHEN 10                                                              
              CONTINUE                                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'START ERROR BP13K825 : ' WS-K825-STATUS                  
                      ' REGN : ' K825-NUM-REGN                                  
              MOVE WS-K825-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       5100-READNEXT-K825.                                                      
      *********************                                                     
                                                                                
           READ BP13K825 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K825-STATUS                                              
           WHEN ZEROES                                                          
              CONTINUE                                                          
                                                                                
           WHEN 23                                                              
           WHEN 10                                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K825 : ' K825-KEY-FLD            
              MOVE 'Y'                     TO WS-K825-EOF                       
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING ON BP13K825'                               
              MOVE WS-K825-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       5200-PROCESS-K825.                                                       
      ********************                                                      
                                                                                
           IF F730-NUM-REGN = K825-NUM-REGN                                     
              IF K825-NUM-NRIC NOT = F730-NUM-NRIC1 AND                         
                                     F730-NUM-NRIC2 AND                         
                                     F730-NUM-NRIC3 AND                         
                                     F730-NUM-NRIC4                             
                 IF WS-OCCP-FND = 'N'                                           
                    MOVE 'OCC'             TO LF73-DET-APPLN                    
                    MOVE 'Y'               TO WS-OCCP-FND                       
                 ELSE                                                           
                    MOVE SPACES            TO LF73-DET-APPLN                    
                 END-IF                                                         
                 STRING K825-DTE-BIRTH(7:2), '/',                               
                        K825-DTE-BIRTH(5:2), '/',                               
                        K825-DTE-BIRTH(1:4) DELIMITED BY SIZE                   
                   INTO LF73-DET-BIRTH                                          
                 PERFORM 4100-MOVE-DECODED THRU 4100-EXIT                       
                 PERFORM 8000-WRITE-BP13LF73  THRU 8000-EXIT                    
              END-IF                                                            
           ELSE                                                                 
              MOVE 'Y'                     TO WS-K825-EOF                       
           END-IF.                                                              
                                                                                
           PERFORM 5100-READNEXT-K825      THRU 5100-EXIT.                      
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       8000-WRITE-BP13LF73.                                                     
      **********************                                                    
                                                                                
      *    EVALUATE F730-NUM-PPS-ELIG-TAG                                       
           EVALUATE F730-NUM-AST                                                
           WHEN 'Y'                                                             
              PERFORM 8100-WRITE-P13LF73A  THRU 8100-EXIT                       
                                                                                
           WHEN 'N'                                                             
              PERFORM 8200-WRITE-P13LF73B  THRU 8200-EXIT                       
                                                                                
           WHEN 'P'                                                             
              PERFORM 8300-WRITE-P13LF73C  THRU 8300-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       8100-WRITE-P13LF73A.                                                     
      **********************                                                    
                                                                                
           MOVE SPACES                     TO P13LF73A-REC.                     
                                                                                
           IF WS-YES-HDR = 'N' OR (WS-LINE-A >= 55)                             
              WRITE P13LF73A-REC      AFTER PAGE                                
              MOVE   0                     TO WS-LINE-A                         
              MOVE 'Y'                     TO WS-YES-HDR                        
              MOVE 'P13LF73A'              TO LF73-RPTNAME                      
              WRITE P13LF73A-REC           FROM LF73-HEADER1                    
              WRITE P13LF73A-REC           FROM LF73-HEADER2                    
              WRITE P13LF73A-REC           FROM LF73-BLANK                      
              WRITE P13LF73A-REC           FROM LF73-BLANK                      
              WRITE P13LF73A-REC           FROM LF73-HEADER5                    
              ADD 6                        TO WS-LINE-A                         
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO LF73-YES-REC.                     
           IF LF73-DET-APPLN = 'HA1'                                            
              MOVE WS-YES-SNO              TO LF73-YES-SNO                      
              MOVE F730-NUM-REGN           TO LF73-YES-REGN                     
              WRITE P13LF73A-REC           FROM LF73-BLANK                      
              ADD 1                        TO WS-LINE-A                         
           ELSE                                                                 
              MOVE SPACES                  TO LF73-YES-SNO                      
                                              LF73-YES-REGN                     
           END-IF.                                                              
           MOVE LF73-DET-REC               TO LF73-YES-DET.                     
           WRITE P13LF73A-REC              FROM LF73-YES-REC.                   
           ADD 1                           TO WS-LINE-A                         
                                                                                
           INITIALIZE LF73-SCH-ACC LF73-DTE-AST                                 
                      LF73-NUM-SYS LF73-NUM-AST.                                
                                                                                
       8100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       8200-WRITE-P13LF73B.                                                     
      **********************                                                    
                                                                                
           MOVE SPACES                     TO P13LF73B-REC.                     
                                                                                
           IF WS-NO-HDR = 'N' OR (WS-LINE-B >= 55)                              
              WRITE P13LF73B-REC     AFTER PAGE                                 
              MOVE  0                      TO WS-LINE-B                         
              MOVE 'Y'                     TO WS-NO-HDR                         
              MOVE 'P13LF73B'              TO LF73-RPTNAME                      
              WRITE P13LF73B-REC           FROM LF73-HEADER1                    
              WRITE P13LF73B-REC           FROM LF73-HEADER3                    
              WRITE P13LF73B-REC           FROM LF73-BLANK                      
              WRITE P13LF73B-REC           FROM LF73-BLANK                      
              WRITE P13LF73B-REC           FROM LF73-HEADER5                    
              ADD 6                        TO WS-LINE-B                         
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO LF73-NO-REC.                      
           IF LF73-DET-APPLN = 'HA1'                                            
              MOVE WS-NO-SNO               TO LF73-NO-SNO                       
              MOVE F730-NUM-REGN           TO LF73-NO-REGN                      
              WRITE P13LF73B-REC           FROM LF73-BLANK                      
              ADD 1                        TO WS-LINE-B                         
           ELSE                                                                 
              MOVE SPACES                  TO LF73-NO-SNO                       
                                              LF73-NO-REGN                      
           END-IF.                                                              
           MOVE LF73-DET-REC               TO LF73-NO-DET.                      
           WRITE P13LF73B-REC              FROM LF73-NO-REC.                    
           ADD 1                           TO WS-LINE-B                         
                                                                                
           INITIALIZE LF73-SCH-ACC LF73-DTE-AST                                 
                      LF73-NUM-SYS LF73-NUM-AST.                                
       8200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       8300-WRITE-P13LF73C.                                                     
      **********************                                                    
                                                                                
           MOVE SPACES                     TO P13LF73C-REC.                     
                                                                                
           IF WS-PEND-HDR = 'N' OR (WS-LINE-C >= 55)                            
              WRITE P13LF73C-REC       AFTER PAGE                               
              MOVE   0                     TO WS-LINE-C                         
              MOVE 'Y'                     TO WS-PEND-HDR                       
              MOVE 'P13LF73C'              TO LF73-RPTNAME                      
              WRITE P13LF73C-REC           FROM LF73-HEADER1                    
              WRITE P13LF73C-REC           FROM LF73-HEADER4                    
              WRITE P13LF73C-REC           FROM LF73-BLANK                      
              WRITE P13LF73C-REC           FROM LF73-BLANK                      
              WRITE P13LF73C-REC           FROM LF73-HEADER5                    
              ADD 6                        TO WS-LINE-C                         
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO LF73-PEND-REC.                    
           IF LF73-DET-APPLN = 'HA1'                                            
              MOVE WS-PEND-SNO             TO LF73-PEND-SNO                     
              MOVE F730-NUM-REGN           TO LF73-PEND-REGN                    
              WRITE P13LF73C-REC           FROM LF73-BLANK                      
              ADD 1                        TO WS-LINE-C                         
           ELSE                                                                 
              MOVE SPACES                  TO LF73-PEND-SNO                     
                                              LF73-PEND-REGN                    
           END-IF.                                                              
           MOVE LF73-DET-REC               TO LF73-PEND-DET.                    
           WRITE P13LF73C-REC              FROM LF73-PEND-REC.                  
           ADD 1                           TO WS-LINE-C                         
                                                                                
           INITIALIZE LF73-SCH-ACC LF73-DTE-AST                                 
                      LF73-NUM-SYS LF73-NUM-AST.                                
       8300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *******************                                                       
       9000-CLOSE-FILES.                                                        
      *******************                                                       
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13CF73 ================*'.              
           DISPLAY '  NO OF F730 RECORDS READ     = ' WS-F730-READ.             
           DISPLAY '  NO OF ELIGIBLE CASES        = ' WS-YES-SNO.               
           DISPLAY '  NO OF NOT ELIGIBLE (SC,<18) = ' WS-NO-SNO.                
           DISPLAY '  NO OF NOT ELIGIBLE (OTHERS) = ' WS-NO-OTHERS.             
           DISPLAY '  NO OF PENDING CASES         = ' WS-PEND-SNO.              
           DISPLAY '*==========================================*'.              
                                                                                
           CLOSE BP13F730                                                       
                 BP13K825                                                       
                 BP13K060                                                       
                 P13LF73A                                                       
                 P13LF73B                                                       
                 P13LF73C.                                                      
                                                                                
           IF WS-K825-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K825 FILE ERROR ' WS-K825-STATUS             
              MOVE WS-K825-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K060-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K060 FILE ERROR ' WS-K060-STATUS             
              MOVE WS-K060-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
        9000-EXIT.                                                              
            EXIT.                                                               
                                                                                
