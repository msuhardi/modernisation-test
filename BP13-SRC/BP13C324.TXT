       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C324.                                                 
      *AUTHOR.        JOEL E PARRENO.                                           
      *DATE-WRITTEN.  02/23/94.                                                 
                                                                                
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :                                              *            
      *    1.   TO UPDATE SOC MASTER (BP13K800) THAT CASES HAD     *            
      *           BEEN STAMPED FOR SOCA AND SUB-TRANSFER.          *            
      *                                                            *            
      *  INPUT      :  BP13F315  (TRANSACTION FILE)                *            
      *                                                            *            
      *  I-O        :  BP13K800  (SOC APPLICATION MASTER FILE)     *            
      * ---------------------------------------------------------- *            
      * CHG REF  BY   DATE    DESCRIPTION                          *            
      * -------- ---- ------  -----------                          *            
      * BP130418 MPA  980403  YEAR 2000 CHANGES                    *            
      * BP130418 SGK  980604  YEAR 2000 CHANGES.                   *            
      * BP130696 CLT  020899  CHANGE BP13F310 TO BP13F315.         *            
      * BP130821 CLT  240400  UPDATE NUM-RELEASE PAYMENT FOR       *            
      *                       SUB-TRANSFER CASES.                  *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F315  ASSIGN       TO BP13F315.                           
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS WS-K800-STATUS.                     
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F315                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F315.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-INDICATORS.                                                       
           05  WS-K800-STATUS                PIC 99    VALUE 00.                
           05  WS-EOF-F315                   PIC X     VALUE 'N'.               
               88  EOF-OF-F315                         VALUE 'Y'.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ-F315-CNT              PIC 9(6)  VALUE 0.                 
           05  WS-READ-K800-CNT              PIC 9(6)  VALUE 0.                 
           05  WS-UPDT-K800-CNT              PIC 9(6)  VALUE 0.                 
                                                                                
           05  WS-EDITED-READ-F315           PIC ZZZZZ9.                        
           05  WS-EDITED-READ-K800           PIC ZZZZZ9.                        
           05  WS-EDITED-UPDT-K800           PIC ZZZZZ9.                        
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           PERFORM  100-OPEN-ROUTINE          THRU   100-EXIT.                  
           PERFORM  200-READ-INPUT-BP13F315   THRU   200-EXIT.                  
           PERFORM  300-PROCESS-RECORD        THRU   300-EXIT                   
                        UNTIL   EOF-OF-F315.                                    
           PERFORM  600-DISPLAY-STATISTICS    THRU   600-EXIT.                  
           PERFORM  900-CLOSE-ROUTINE         THRU   900-EXIT.                  
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           OPEN INPUT   BP13F315                                                
                I-O     BP13K800.                                               
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K800, STATUS = ' WS-K800-STATUS        
              MOVE     WS-K800-STATUS    TO    RETURN-CODE                      
              PERFORM  900-CLOSE-ROUTINE THRU  900-EXIT                         
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
      * READ TRANSACTION FILE (BP13F315) AT END MOVE 'Y' TO EOF-IND  *          
      ****************************************************************          
       200-READ-INPUT-BP13F315.                                                 
      *-------------------------------------------------------------            
           READ BP13F315                                                        
                AT END                                                          
                   MOVE  'Y'         TO WS-EOF-F315                             
                   GO TO 200-EXIT.                                              
                                                                                
           ADD  1  TO WS-READ-F315-CNT.                                         
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
      *  CHECK FOR F315-CDE-TRANS = 'SA' THEN READ SOC MASTER FILE   *          
      ****************************************************************          
       300-PROCESS-RECORD.                                                      
      *-------------------------------------------------------------            
           IF F315-CDE-TRANS = 'SA' OR 'SF'                                     
              PERFORM  400-READ-BP13K800-RECORD   THRU   400-EXIT               
           END-IF.                                                              
                                                                                
           PERFORM     200-READ-INPUT-BP13F315    THRU   200-EXIT.              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
      *  CHECK F315-REGN-NO IF EXISTING IN SOC MASTER FILE           *          
      ****************************************************************          
       400-READ-BP13K800-RECORD.                                                
      *-------------------------------------------------------------            
           MOVE F315-REGN-NO TO K800-NUM-REGN.                                  
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = 00                                               
              ADD     1                    TO    WS-READ-K800-CNT               
              PERFORM 500-UPDATE-BP13K800  THRU  500-EXIT                       
           ELSE                                                                 
           IF WS-K800-STATUS = 23                                               
              DISPLAY 'RECORD NOT FOUND IN K800, STAT = ' WS-K800-STATUS        
              DISPLAY '           K800-NUM-REGN       = ' K800-NUM-REGN         
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K800, STATUS = ' WS-K800-STATUS        
              DISPLAY '           K800-NUM-REGN       = ' K800-NUM-REGN         
              MOVE     WS-K800-STATUS      TO     RETURN-CODE                   
              PERFORM  900-CLOSE-ROUTINE   THRU   900-EXIT.                     
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
      *  REWRITE SOC APPLICATION MASTER RECORD (BP13K800)            *          
      ****************************************************************          
       500-UPDATE-BP13K800.                                                     
      *-------------------------------------------------------------            
           MOVE   FUNCTION CURRENT-DATE(1:8)   TO   K800-DTE-UPDATE.            
           MOVE   'Y'  TO   K800-NUM-RELEASE-PAYMENT.                           
                                                                                
           REWRITE BP13K800-MASTER.                                             
                                                                                
           IF WS-K800-STATUS =  00                                              
              ADD     1   TO WS-UPDT-K800-CNT                                   
           ELSE                                                                 
              DISPLAY 'ERROR UPDATING BP13K800, STAT = ' WS-K800-STATUS         
              DISPLAY '               K800-NUM-REGN  = ' K800-NUM-REGN          
              MOVE     WS-K800-STATUS      TO     RETURN-CODE                   
              PERFORM  900-CLOSE-ROUTINE   THRU   900-EXIT                      
           END-IF.                                                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       600-DISPLAY-STATISTICS.                                                  
      *-------------------------------------------------------------            
           MOVE  WS-READ-F315-CNT   TO   WS-EDITED-READ-F315.                   
           MOVE  WS-READ-K800-CNT   TO   WS-EDITED-READ-K800.                   
           MOVE  WS-UPDT-K800-CNT   TO   WS-EDITED-UPDT-K800.                   
                                                                                
           DISPLAY '*--------- BP13C324 CONTROL TOTALS ---------*'.             
           DISPLAY '  '.                                                        
           DISPLAY ' NO. OF RECORDS READ  FROM BP13F315  = '                    
                     WS-EDITED-READ-F315.                                       
           DISPLAY '  '.                                                        
           DISPLAY ' NO. OF RECORDS READ  FROM BP13K800  = '                    
                     WS-EDITED-READ-K800.                                       
           DISPLAY ' NO. OF RECORDS UPDATED TO BP13K800  = '                    
                     WS-EDITED-UPDT-K800.                                       
           DISPLAY '*-------------------------------------------*'.             
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
      *-------------------------------------------------------------            
           CLOSE   BP13F315                                                     
                   BP13K800.                                                    
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K800, STATUS = ' WS-K800-STATUS        
              MOVE     WS-K800-STATUS    TO    RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
