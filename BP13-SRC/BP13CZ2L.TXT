       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CZ2L.                                                 
      *AUTHOR.        GURU.                                                     
      *DATE-WRITTEN.  05/12/17.                                                 
      *DATE-COMPILED.                                                           
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *    1. PROGRAM TO MATCH NRIC OF K825 WITH REGN NO FROM  *                
      *       F730 AGAINST P04K010 DEBARMENT FILE AND OUTPUT   *                
      *       TO FILE IF WITHIN VALID REASON CODE 'R4'.        *                
      *                                                        *                
      *    INPUT FILES : 1.   BP13F730                         *                
      *                  2.   BP13K825                         *                
      *                  3.   P04K010                          *                
      *    OUTPUT FILE : 1.   BP13F740                         *                
      *--------------------------------------------------------*                
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS       *                
      * -------- ---------- ---- -----------------------       *                
      * BP137101 310118     KR13 NEW PGM                       *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN TO BP13F730.                                  
                                                                                
           SELECT BP13K825 ASSIGN TO BP13K825                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K825-KEY-FLD                               
                  FILE STATUS     IS WS-K825-STATUS.                            
                                                                                
           SELECT P04K010  ASSIGN TO P04K010                                    
                  ACCESS MODE     IS SEQUENTIAL                                 
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K010-PRIME-KEY                             
                  FILE STATUS     IS WS-K010-STATUS.                            
                                                                                
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F730.                                                           
       COPY BP13F730.                                                           
                                                                                
       FD   BP13K825                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K825.                                                           
                                                                                
       FD   P04K010                                                             
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       COPY P04K010.                                                            
                                                                                
       FD   BP13F740.                                                           
       COPY BP13F740.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-K010-STATUS              PIC 9(2)   VALUE ZEROES.                 
       01  WS-K825-STATUS              PIC 9(2)   VALUE ZEROES.                 
       01  WS-F730-EOF                 PIC X(1)   VALUE 'N'.                    
       01  WS-K825-EOF                 PIC X(1)   VALUE 'N'.                    
       01  WS-K010-EOF                 PIC X(1)   VALUE 'N'.                    
       01  WS-FST-K010                 PIC X(1)   VALUE 'N'.                    
       01  WS-R4-TAG-FOUND             PIC X(1)   VALUE 'N'.                    
       01  WS-UIN-TABLE.                                                        
           05  WS-UIN OCCURS 15 TIMES  PIC X(9).                                
       01  WS-CURRENT-DATE             PIC 9(8)   VALUE ZEROES.                 
       01  WS-SYS-DTE-CCYYMMDD         PIC 9(8)   VALUE ZEROES.                 
       01  WS-CURR-DATE-INTEGER        PIC 9(8)   VALUE ZEROES.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F730-READ            PIC 9(7)   VALUE ZEROES.                 
           05  WS-K825-READ            PIC 9(7)   VALUE ZEROES.                 
           05  WS-K010-READ            PIC 9(7)   VALUE ZEROES.                 
           05  WS-PROCESS-CTR          PIC 9(7)   VALUE ZEROES.                 
           05  WS-F730-K825-M          PIC 9(7)   VALUE ZEROES.                 
           05  WS-F730-K825-NM         PIC 9(7)   VALUE ZEROES.                 
           05  WS-K825-K010-M          PIC 9(7)   VALUE ZEROES.                 
           05  WS-K825-K010-NM         PIC 9(7)   VALUE ZEROES.                 
           05  WS-I                    PIC 9(2)   VALUE ZEROES.                 
           05  WS-NON-R4               PIC 9(7)   VALUE ZEROES.                 
           05  WS-R4-CNT               PIC 9(7)   VALUE ZEROES.                 
           05  WS-R4-CNT-REGN          PIC 9(7)   VALUE ZEROES.                 
           05  WS-CNT                  PIC 9(7)   VALUE ZEROES.                 
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      ******************************************************************        
       MAIN-ROUTINE SECTION.                                                    
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
                                                                                
           PERFORM 2000-READ-BP13F730       THRU 2000-EXIT.                     
                                                                                
           PERFORM 3000-MAIN-PROCESS        THRU 3000-EXIT                      
                                    UNTIL WS-F730-EOF = 'Y'.                    
           PERFORM 9999-CLOSE-RTN           THRU 9999-EXIT.                     
                                                                                
       MAIN-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-FILES.                                                         
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13K825                                                 
                       P04K010                                                  
                OUTPUT BP13F740.                                                
                                                                                
           IF WS-K825-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K825 OPEN ERROR ' WS-K825-STATUS                     
              MOVE WS-K825-STATUS           TO RETURN-CODE                      
              PERFORM 9999-CLOSE-RTN        THRU 9999-EXIT                      
           END-IF                                                               
                                                                                
           IF WS-K010-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'P04K010 OPEN ERROR ' WS-K010-STATUS                      
              MOVE WS-K010-STATUS           TO RETURN-CODE                      
              PERFORM 9999-CLOSE-RTN        THRU 9999-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE (1:8) TO WS-CURRENT-DATE.                 
           COMPUTE WS-CURR-DATE-INTEGER =                                       
                   FUNCTION INTEGER-OF-DATE (WS-CURRENT-DATE).                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F730.                                                      
      ******************************************************************        
                                                                                
           READ BP13F730                                                        
           AT END                                                               
              MOVE 'Y'                      TO WS-F730-EOF                      
              GO TO 2000-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD 1                            TO WS-F730-READ.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3000-MAIN-PROCESS.                                                       
      ****************************************************************          
                                                                                
           PERFORM 3100-BROWSE-K825         THRU 3100-EXIT.                     
                                                                                
           PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > WS-CNT                 
              OR (WS-UIN(WS-I) = SPACES OR LOW-VALUES)                          
              PERFORM 3200-CHECK-DEBARMENT  THRU 3200-EXIT                      
           END-PERFORM.                                                         
                                                                                
           IF WS-R4-TAG-FOUND = 'Y'                                             
              ADD 1 TO WS-R4-CNT-REGN                                           
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F730       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3100-BROWSE-K825.                                                        
      ****************************************************************          
                                                                                
           INITIALIZE BP13K825-REC.                                             
                                                                                
           MOVE 'N'                         TO WS-K825-EOF                      
                                               WS-R4-TAG-FOUND.                 
           MOVE ZEROS                       TO WS-CNT.                          
           MOVE F730-NUM-REGN               TO K825-NUM-REGN.                   
                                                                                
           START BP13K825 KEY >= K825-KEY-FLD                                   
                                                                                
           EVALUATE WS-K825-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                   PERFORM 3110-READNEXT-K825 THRU 3110-EXIT                    
                   PERFORM 3115-PROCESS-OCCUP THRU 3115-EXIT                    
                        UNTIL K825-NUM-REGN > F730-NUM-REGN                     
                        OR WS-K825-EOF = 'Y'                                    
                   ADD 1                    TO WS-F730-K825-M                   
              WHEN 10                                                           
              WHEN 23                                                           
                   MOVE ZEROS               TO WS-CNT                           
                   ADD 1                    TO WS-F730-K825-NM                  
              WHEN OTHER                                                        
                DISPLAY 'ERROR WITH START READ OF BP13K825. STATUS IS '         
                                               WS-K825-STATUS                   
                MOVE WS-K825-STATUS         TO RETURN-CODE                      
                PERFORM 9999-CLOSE-RTN      THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3110-READNEXT-K825.                                                      
      ****************************************************************          
                                                                                
           READ BP13K825 NEXT RECORD                                            
           AT END                                                               
              MOVE 'Y'                      TO WS-K825-EOF                      
              GO TO 3110-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD 1                            TO WS-K825-READ.                    
                                                                                
       3110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3115-PROCESS-OCCUP.                                                      
      ****************************************************************          
                                                                                
           IF F730-NUM-REGN = K825-NUM-REGN                                     
              ADD 1                    TO WS-CNT                                
              MOVE K825-NUM-NRIC       TO WS-UIN(WS-CNT)                        
           ELSE                                                                 
              MOVE 'Y'                TO WS-K825-EOF                            
           END-IF.                                                              
                                                                                
           PERFORM 3110-READNEXT-K825 THRU 3110-EXIT.                           
                                                                                
       3115-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3200-CHECK-DEBARMENT.                                                    
      ******************************************************************        
                                                                                
           MOVE 'N'                         TO WS-R4-TAG-FOUND.                 
           MOVE 'N'                         TO WS-K010-EOF.                     
                                                                                
           PERFORM 3210-CHECK-DEBAR         THRU 3210-EXIT.                     
                                                                                
           IF WS-R4-TAG-FOUND = 'Y'                                             
              PERFORM 3320-WRITE-F740       THRU 3320-EXIT                      
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3210-CHECK-DEBAR.                                                        
      ******************************************************************        
           INITIALIZE P04K010-REC.                                              
                                                                                
           MOVE SPACES                      TO K010-PRIME-KEY.                  
           MOVE WS-UIN(WS-I)                TO K010-NUM-UINFIN.                 
                                                                                
           START P04K010 KEY >= K010-PRIME-KEY.                                 
                                                                                
           EVALUATE WS-K010-STATUS                                              
              WHEN 00                                                           
                   PERFORM 3215-READ-P04K010  THRU 3215-EXIT                    
                     UNTIL WS-K010-EOF = 'Y'                                    
              WHEN 23                                                           
                 MOVE 'Y'                   TO WS-K010-EOF                      
              WHEN OTHER                                                        
                DISPLAY 'ERROR WITH START READ OF P04K010. STATUS IS '          
                       WS-K010-STATUS                                           
                MOVE WS-K010-STATUS         TO RETURN-CODE                      
                PERFORM 9999-CLOSE-RTN      THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3215-READ-P04K010.                                                       
      ******************************************************************        
                                                                                
           READ P04K010 NEXT RECORD.                                            
                                                                                
           EVALUATE WS-K010-STATUS                                              
              WHEN 00                                                           
                IF K010-NUM-UINFIN = WS-UIN(WS-I)                               
                   IF K010-REASON-CODE = 'R4'                                   
                      MOVE 'Y'                TO WS-R4-TAG-FOUND                
                      ADD 1                   TO WS-R4-CNT                      
                   ELSE                                                         
                      ADD 1                   TO WS-NON-R4                      
                   END-IF                                                       
                   ADD 1                      TO WS-K825-K010-M                 
                                                 WS-K010-READ                   
                ELSE                                                            
                      ADD 1                   TO WS-K825-K010-NM                
                   MOVE 'Y'                   TO WS-K010-EOF                    
                END-IF                                                          
              WHEN 23                                                           
              WHEN 10                                                           
                 MOVE 'Y'                     TO WS-K010-EOF                    
              WHEN OTHER                                                        
                DISPLAY 'ERROR WITH READNEXT OF P04K010. STATUS IS '            
                                               WS-K010-STATUS                   
                MOVE WS-K010-STATUS         TO RETURN-CODE                      
                PERFORM 9999-CLOSE-RTN      THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3215-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3320-WRITE-F740.                                                         
      ******************************************************************        
                                                                                
           INITIALIZE BP13F740-REC.                                             
                                                                                
           MOVE WS-UIN(WS-I)                TO F730-NUM-NRIC1.                  
           MOVE BP13F730-REC                TO BP13F740-REC.                    
                                                                                
           WRITE BP13F740-REC.                                                  
                                                                                
           ADD 1                            TO WS-PROCESS-CTR.                  
                                                                                
       3320-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-RTN.                                                          
      ******************************************************************        
           DISPLAY 'PROGRAM : BP13CZ2L    '                                     
           DISPLAY 'DATE    : ' WS-CURRENT-DATE                                 
           DISPLAY '*=========== COUNTERS ===========*'                         
           DISPLAY 'F730 REC READ        : ' WS-F730-READ                       
           DISPLAY 'K825 REC READ        : ' WS-K825-READ                       
           DISPLAY 'K010 REC READ        : ' WS-K010-READ                       
           DISPLAY 'F730 - K825 MATCH    : ' WS-F730-K825-M                     
           DISPLAY 'F730 - K825 NO MATCH : ' WS-F730-K825-NM                    
           DISPLAY 'K825 - K010 MATCH    : ' WS-K825-K010-M                     
           DISPLAY 'K825 - K010 NO MATCH : ' WS-K825-K010-NM                    
           DISPLAY 'NUM OF NON R4        : ' WS-NON-R4                          
           DISPLAY 'NUM OF R4            : ' WS-R4-CNT                          
           DISPLAY 'NUM OF R4 PER REGN   : ' WS-R4-CNT-REGN.                    
           DISPLAY 'REC WRITTEN TO F740  : ' WS-PROCESS-CTR.                    
                                                                                
           CLOSE P04K010                                                        
                 BP13F730                                                       
                 BP13K825                                                       
                 BP13F740.                                                      
                                                                                
           IF WS-K010-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'P04K010 CLOSE ERROR ' WS-K010-STATUS                     
              MOVE WS-K010-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           IF WS-K825-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K825 CLOSE ERROR ' WS-K825-STATUS                    
              MOVE WS-K825-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
