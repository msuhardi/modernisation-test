      *-------------------------*                                               
       IDENTIFICATION DIVISION.                                                 
      *-------------------------*                                               
       PROGRAM-ID.    BP13C067.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  13 JUL 2010.                                              
      *===============================================================*         
      *                SYSTEM OF COMMITMENT  (BP13)                   *         
      *===============================================================*         
      *                                                               *         
      *  OBJECTIVE :                                                  *         
      *      1. PRINT DEBARMENT REPORT FOR MARCH BTO (SC & SP)        *         
      *---------------------------------------------------------------*         
      * CHG REF   BY    DATE        DESCRIPTION                       *         
      * --------  ---   ------      -----------                       *         
      * BP134054  ESA1  13/07/2010  NEW PROGRAM                       *         
      *===============================================================*         
                                                                                
      *---------------------*                                                   
       ENVIRONMENT DIVISION.                                                    
      *---------------------*                                                   
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F310  ASSIGN       TO BP13F310.                           
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS BP13K800-STATUS.                    
                                                                                
           SELECT BP13K820  ASSIGN       TO BP13K820                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K820-KEY-FLD                        
                            FILE STATUS  IS BP13K820-STATUS.                    
                                                                                
           SELECT BP04K010  ASSIGN       TO BP04K010                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K010-PRIME-KEY                      
                            FILE STATUS  IS BP04K010-STATUS.                    
                                                                                
           SELECT BP13L067  ASSIGN       TO BP13L067.                           
                                                                                
      *---------------*                                                         
       DATA DIVISION.                                                           
      *---------------*                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F310                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS   150 CHARACTERS.                                    
       COPY BP13F310.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS  2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS   400 CHARACTERS.                                    
       COPY BP13K820.                                                           
                                                                                
       FD  BP04K010                                                             
           RECORD CONTAINS   300 CHARACTERS.                                    
       COPY P04K010.                                                            
                                                                                
       FD  BP13L067                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS   132 CHARACTERS.                                    
       01  BP13L067-REC      PIC X(132).                                        
                                                                                
                                                                                
      *--------------------------*                                              
       WORKING-STORAGE SECTION.                                                 
      *--------------------------*                                              
       01 WS-FILE-STATUS-VARS.                                                  
          05 BP13K800-STATUS        PIC 9(02)  VALUE ZEROES.                    
          05 BP13K820-STATUS        PIC 9(02)  VALUE ZEROES.                    
          05 BP04K010-STATUS        PIC 9(02)  VALUE ZEROES.                    
          05 BP13F310-FLAG          PIC X(01)  VALUE SPACE.                     
             88 BP13F310-EOF        VALUE 'Y'.                                  
          05 BP04K010-FLAG          PIC X(01)  VALUE SPACE.                     
             88 BP04K010-EOF        VALUE 'Y'.                                  
          05 BP13K820-FLAG          PIC X(01)  VALUE SPACE.                     
             88 BP13K820-EOF        VALUE 'Y'.                                  
                                                                                
       01 WS-DATE-VARS.                                                         
          05 WS-CURR-DATE           PIC X(08)  VALUE SPACES.                    
                                                                                
       01 WS-COUNTERS.                                                          
          05 WS-F310-READ-CNT       PIC 9(08)  VALUE ZEROES.                    
          05 WS-K800-NOT-CASES      PIC 9(08)  VALUE ZEROES.                    
          05 WS-L067-WRITE-CNT      PIC 9(08)  VALUE ZEROES.                    
                                                                                
       01 WS-OTHER-VARS.                                                        
          05 WS-CDE-DEBAR           PIC X(02)  VALUE SPACES.                    
          05 WS-NUM-SCH-ACC         PIC X(11)  VALUE SPACES.                    
          05 WS-NUM-NT              PIC X(03)  VALUE SPACES.                    
          05 WS-NUM-FT              PIC X(02)  VALUE SPACES.                    
          05 WS-NUM-QUEUE           PIC X(05)  VALUE SPACES.                    
          05 WS-HH                  PIC X(01)  VALUE SPACES.                    
          05 WS-HH-CITZ             PIC X(03)  VALUE SPACES.                    
          05 WS-BALLOT-QTR          PIC X(07)  VALUE SPACES.                    
          05 WS-CTR                 PIC 9(02)  VALUE ZEROES.                    
          05 WS-MAX-LINES           PIC 9(02)  VALUE 55.                        
          05 WS-LINE-CNT            PIC 9(02)  VALUE 60.                        
          05 WS-PAGE-CNT            PIC 9(04)  VALUE ZEROES.                    
          05 WS-SLNO                PIC 9(04)  VALUE ZEROES.                    
          05 WS-EDIT1               PIC Z(07)9.                                 
                                                                                
       01 WS-OCCUP-TABLE.                                                       
          05 WS-OCCUP  OCCURS 12 TIMES.                                         
             10 WS-NME-OCCUP      PIC X(30)  VALUE SPACES.                      
             10 WS-UIN-OCCUP      PIC X(09)  VALUE SPACES.                      
             10 WS-DEB-REF        PIC X(13)  VALUE SPACES.                      
             10 WS-REASON-CDE     PIC X(02)  VALUE SPACES.                      
             10 WS-EFF-DATE       PIC X(08)  VALUE SPACES.                      
             10 WS-APPL           PIC X(03)  VALUE SPACES.                      
                                                                                
      *--------------------------------------------------------------           
      *   REPORT LAYOUT                                                         
      *--------------------------------------------------------------           
       01 WS-REPORT-VARS.                                                       
          05 L067-HEADER-1.                                                     
             10 FILLER              PIC X(08)  VALUE 'BP13L067'.                
             10 FILLER              PIC X(05)  VALUE SPACES .                   
             10 FILLER              PIC X(08)  VALUE 'HDBCAT 3'.                
             10 FILLER              PIC X(22)  VALUE SPACES.                    
             10 FILLER              PIC X(40)  VALUE                            
                'S Y S T E M   O F   C O M M I T M E N T'.                      
             10 FILLER              PIC X(28)  VALUE SPACES.                    
             10 FILLER              PIC X(07)  VALUE 'DATE :'.                  
             10 L067-DATE           PIC X(10)  VALUE SPACES.                    
                                                                                
          05 L067-HEADER-2.                                                     
             10 FILLER              PIC X(35)  VALUE SPACES .                   
             10 FILLER              PIC X(35)  VALUE                            
                'DEBARMENT REPORT FOR SC/SPR OPTION'.                           
             10 FILLER              PIC X(20)  VALUE                            
                '- CORRECT DEBARMENT'.                                          
             10 FILLER              PIC X(21)  VALUE SPACES.                    
             10 FILLER              PIC X(07)  VALUE 'PAGE : '.                 
             10 L067-PAGE           PIC ZZZ9 .                                  
                                                                                
          05 L067-HEADER-2A.                                                    
             10 FILLER              PIC X(47)  VALUE                            
                'S/NO  NT    FT   QUEUE  HH TYPE  HH CITIZENSHIP'.              
             10 FILLER              PIC X(02) VALUE SPACES.                     
             10 FILLER              PIC X(10) VALUE 'BALLOT QTR'.               
             10 FILLER              PIC X(69) VALUE SPACES.                     
                                                                                
          05 L067-HEADER-3.                                                     
             10 FILLER              PIC X(06)  VALUE SPACES .                   
             10 FILLER              PIC X(28)  VALUE                            
                'REGN      NAME OF APPLICANTS'.                                 
             10 FILLER              PIC X(11)  VALUE SPACES .                   
             10 FILLER              PIC X(38)  VALUE                            
                'NRIC         APPL      DEB-REF'.                               
             10 FILLER              PIC X(02)  VALUE SPACES .                   
             10 FILLER              PIC X(46)  VALUE                            
                'REASON    EFF-DTE        '.                                    
                                                                                
          05 L067-HEADER-3A.                                                    
             10 FILLER              PIC X(15)  VALUE                            
                '   SCH-ACC-NO'.                                                
             10 FILLER              PIC X(115) VALUE SPACES .                   
                                                                                
          05 L067-DETAIL-0.                                                     
             10 L067-SEQ-NO         PIC ZZ99.                                   
             10 FILLER              PIC X(02)  VALUE SPACES.                    
             10 L067-NEW-TOWN       PIC X(03)  VALUE SPACES.                    
             10 FILLER              PIC X(03)  VALUE SPACES.                    
             10 L067-FLAT-TYPE      PIC X(02)  VALUE SPACES.                    
             10 FILLER              PIC X(03)  VALUE SPACES.                    
             10 L067-QUEUE          PIC X(05)  VALUE SPACES.                    
             10 FILLER              PIC X(05)  VALUE SPACES.                    
             10 L067-HH             PIC X(01)  VALUE SPACES.                    
             10 FILLER              PIC X(10)  VALUE SPACES.                    
             10 L067-HH-CITZ        PIC X(03)  VALUE SPACES.                    
             10 FILLER              PIC X(09)  VALUE SPACES.                    
             10 L067-BALLOT-QTR     PIC X(07)  VALUE SPACES.                    
             10 FILLER              PIC X(78)  VALUE SPACES.                    
                                                                                
          05 L067-DETAIL-1.                                                     
             10 L067-HDB-REF.                                                   
                15 FILLER           PIC X(03).                                  
                15 L067-SCH-ACC     PIC X(11).                                  
             10 L067-SEQ-REGN  REDEFINES L067-HDB-REF.                          
                15 FILLER           PIC X(06).                                  
                15 L067-NUM-REGN    PIC X(08).                                  
             10 FILLER              PIC X(02)  VALUE SPACES.                    
             10 L067-NME-OCCUP      PIC X(27)  VALUE SPACES.                    
             10 FILLER              PIC X(02)  VALUE SPACES.                    
             10 L067-UIN-OCCUP      PIC X(09)  VALUE SPACES.                    
             10 FILLER              PIC X(05)  VALUE SPACES.                    
             10 L067-APPL           PIC X(03)  VALUE SPACES.                    
             10 FILLER              PIC X(06)  VALUE SPACES.                    
             10 L067-DEB-REF-NO     PIC X(13)  VALUE SPACES.                    
             10 FILLER              PIC X(06)  VALUE SPACES.                    
             10 L067-DEB-CODE       PIC X(02)  VALUE SPACES.                    
             10 FILLER              PIC X(05)  VALUE SPACES.                    
             10 L067-EFF-DATE       PIC X(10)  VALUE SPACES.                    
                                                                                
       01 L067-PR-LINE              PIC X(132) VALUE ALL '-'.                   
                                                                                
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      *---------------------------------------------------------------*         
       0000-MAIN-ROUTINE.                                                       
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F310      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-DATA       THRU 3000-EXIT                       
                                       UNTIL BP13F310-EOF.                      
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN  INPUT  BP13F310                                                
                        BP13K800                                                
                        BP13K820                                                
                        BP04K010                                                
                 OUTPUT BP13L067.                                               
                                                                                
           IF BP13K800-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BP13K800 OPEN ERROR, STATUS ' BP13K800-STATUS             
             MOVE BP13K800-STATUS   TO RETURN-CODE                              
             PERFORM 9000-CLOSE-ROUTINE                                         
           END-IF.                                                              
                                                                                
           IF BP13K820-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BP13K820 OPEN ERROR, STATUS ' BP13K820-STATUS             
             MOVE BP13K820-STATUS   TO RETURN-CODE                              
             PERFORM 9000-CLOSE-ROUTINE                                         
           END-IF.                                                              
                                                                                
           IF BP04K010-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BP04K010 OPEN ERROR, STATUS ' BP04K010-STATUS             
             MOVE BP04K010-STATUS   TO RETURN-CODE                              
             PERFORM 9000-CLOSE-ROUTINE                                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE.                          
           STRING WS-CURR-DATE(7:2), '/',                                       
                  WS-CURR-DATE(5:2), '/',                                       
                  WS-CURR-DATE(1:4)  DELIMITED BY SIZE                          
                  INTO L067-DATE.                                               
                                                                                
           MOVE  '55'        TO WS-MAX-LINES.                                   
           MOVE  '60'        TO WS-LINE-CNT.                                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13F310.                                                      
      *---------------------------------------------------------------*         
           READ BP13F310 AT END                                                 
                          MOVE  'Y'   TO BP13F310-FLAG                          
                         NOT AT END                                             
                          ADD  1      TO WS-F310-READ-CNT                       
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-PROCESS-DATA.                                                       
      *---------------------------------------------------------------*         
           MOVE    SPACES     TO WS-CDE-DEBAR, WS-NUM-SCH-ACC,                  
                                 WS-NUM-NT, WS-NUM-FT, WS-NUM-QUEUE             
                                 WS-HH, WS-HH-CITZ, WS-BALLOT-QTR.              
           MOVE    SPACES     TO WS-OCCUP-TABLE.                                
           MOVE    ZEROES                  TO WS-CTR.                           
                                                                                
           MOVE F310-REGN-NO               TO K800-NUM-REGN.                    
                                                                                
           PERFORM 4000-READ-BP13K800      THRU 4000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F310      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-READ-BP13K800.                                                      
      *---------------------------------------------------------------*         
           READ BP13K800.                                                       
                                                                                
           EVALUATE BP13K800-STATUS                                             
             WHEN 00                                                            
               IF (K800-NUM-SCSPR-OPT = 'Y' OR 'N') AND                         
                  (K800-CDE-CITIZENSHIP-HOUSEHOLD = 'SCH' OR 'SCP')             
                  MOVE K800-NUM-SCH-ACC        TO WS-NUM-SCH-ACC                
                  MOVE K800-NUM-NEW-TOWN       TO WS-NUM-NT                     
                  MOVE K800-NUM-FLAT-TYPE      TO WS-NUM-FT                     
                  MOVE K800-NUM-NT-FT-QUEUE    TO WS-NUM-QUEUE                  
                  MOVE K800-NUM-HOUSEHOLD      TO WS-HH                         
                  MOVE K800-CDE-CITIZENSHIP-HOUSEHOLD                           
                                               TO WS-HH-CITZ                    
                  STRING K800-DTE-BALLOT(1:4) '/'                               
                         K800-DTE-BALLOT(5:2)                                   
                         DELIMITED BY SIZE INTO WS-BALLOT-QTR                   
                  PERFORM 4100-START-BP13K820  THRU 4100-EXIT                   
               END-IF                                                           
             WHEN 23                                                            
               ADD    1      TO WS-K800-NOT-CASES                               
               DISPLAY 'RECORD NOT FOUND IN BP13K800, REGN : '                  
                                                      K800-NUM-REGN             
             WHEN OTHER                                                         
               DISPLAY 'BP13K800 READ ERROR, STATUS ' BP13K800-STATUS           
                       '  REGN : ' K800-NUM-REGN                                
               MOVE BP13K800-STATUS   TO RETURN-CODE                            
               PERFORM 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       4100-START-BP13K820.                                                     
      *---------------------------------------------------------------*         
           MOVE 'N'                          TO BP13K820-FLAG.                  
           MOVE SPACES                       TO K820-KEY-FLD.                   
           MOVE K800-NUM-REGN                TO K820-NUM-REGN.                  
           START BP13K820 KEY >= K820-KEY-FLD.                                  
                                                                                
           EVALUATE BP13K820-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
               PERFORM 4200-READNXT-BP13K820 THRU 4200-EXIT                     
                  UNTIL K820-NUM-REGN NOT = K800-NUM-REGN                       
                      OR BP13K820-EOF                                           
               IF WS-CTR > ZEROES                                               
                  PERFORM 7000-PRINT-REPORT      THRU 7000-EXIT                 
               END-IF                                                           
             WHEN 10                                                            
             WHEN 23                                                            
               DISPLAY 'RECORD NOT FOUND IN BP13K820, KEY : '                   
                                                    K820-KEY-FLD                
             WHEN OTHER                                                         
               DISPLAY 'BP18K820 START ERROR, STATUS ' BP13K820-STATUS          
                       '  KEY : ' K820-KEY-FLD                                  
               MOVE BP13K820-STATUS   TO RETURN-CODE                            
               PERFORM 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       4200-READNXT-BP13K820.                                                   
      *---------------------------------------------------------------*         
           READ BP13K820 NEXT.                                                  
                                                                                
           EVALUATE BP13K820-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
               IF K820-NUM-REGN = K800-NUM-REGN                                 
                  PERFORM 6000-CHK-DEBARMENT THRU 6000-EXIT                     
               END-IF                                                           
             WHEN 10                                                            
             WHEN 23                                                            
               MOVE  'Y'     TO BP13K820-FLAG                                   
             WHEN OTHER                                                         
               DISPLAY 'BP13K820 READNXT ERROR, STATUS '                        
                  BP13K820-STATUS  '  KEY : ' K820-KEY-FLD                      
               MOVE BP13K820-STATUS   TO RETURN-CODE                            
               PERFORM 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       6000-CHK-DEBARMENT.                                                      
      *---------------------------------------------------------------*         
           MOVE 'N'                  TO BP04K010-FLAG.                          
           MOVE SPACES               TO K010-PRIME-KEY.                         
           MOVE K820-NUM-NRIC        TO K010-NUM-UINFIN.                        
                                                                                
           PERFORM 6100-START-BP04K010       THRU 6100-EXIT.                    
                                                                                
           IF BP04K010-STATUS = 00                                              
              PERFORM 6300-READNXT-BP04K010  THRU 6300-EXIT                     
                    UNTIL BP04K010-EOF                                          
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       6100-START-BP04K010.                                                     
      *---------------------------------------------------------------*         
           START BP04K010 KEY >= K010-PRIME-KEY.                                
                                                                                
           EVALUATE BP04K010-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
               CONTINUE                                                         
             WHEN 10                                                            
             WHEN 23                                                            
               DISPLAY 'RECORD NOT FOUND IN BP04K010, KEY : '                   
                                                    K010-PRIME-KEY              
             WHEN OTHER                                                         
               DISPLAY 'BP04K010 START ERROR, STATUS ' BP04K010-STATUS          
                       '  KEY : ' K010-PRIME-KEY                                
               MOVE BP04K010-STATUS   TO RETURN-CODE                            
               PERFORM 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       6300-READNXT-BP04K010.                                                   
      *---------------------------------------------------------------*         
           READ BP04K010 NEXT.                                                  
                                                                                
           EVALUATE BP04K010-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
               IF K010-NUM-UINFIN = K820-NUM-NRIC                               
                  IF K010-EX-SCH-ACCTNO = K800-NUM-SCH-ACC(1:9)                 
                     IF K010-DEBREF-AO = 'SL'                                   
                        IF (K800-CDE-CITIZENSHIP-HOUSEHOLD = 'SCH' AND          
                            K010-REASON-CODE = 'SC') OR                         
                           (K800-CDE-CITIZENSHIP-HOUSEHOLD = 'SCP' AND          
                            K010-REASON-CODE = 'SP')                            
                           ADD   1             TO WS-CTR                        
                           MOVE K010-DEB-REF-NO                                 
                                            TO WS-DEB-REF(WS-CTR)               
                           MOVE K010-REASON-CODE                                
                                            TO WS-REASON-CDE(WS-CTR)            
                           IF (K010-EFF-DATE = LOW-VALUES OR SPACES) OR         
                              (K010-EFF-DATE NOT NUMERIC)                       
                              MOVE  ZEROES  TO WS-EFF-DATE(WS-CTR)              
                           ELSE                                                 
                              MOVE K010-EFF-DATE TO WS-EFF-DATE(WS-CTR)         
                           END-IF                                               
                           MOVE  K820-NME-OCCP TO WS-NME-OCCUP(WS-CTR)          
                           MOVE  K820-NUM-NRIC TO WS-UIN-OCCUP(WS-CTR)          
                           IF K820-NUM-NRIC = K800-NUM-NRIC1 OR                 
                              K820-NUM-NRIC = K800-NUM-NRIC2 OR                 
                              K820-NUM-NRIC = K800-NUM-NRIC3 OR                 
                              K820-NUM-NRIC = K800-NUM-NRIC4                    
                              MOVE 'HA '       TO WS-APPL(WS-CTR)               
                           ELSE                                                 
                              MOVE 'OCC'       TO WS-APPL(WS-CTR)               
                           END-IF                                               
                        END-IF                                                  
                     END-IF                                                     
                  END-IF                                                        
               ELSE                                                             
                  MOVE 'Y'   TO BP04K010-FLAG                                   
               END-IF                                                           
             WHEN 10                                                            
             WHEN 23                                                            
               MOVE  'Y'     TO BP04K010-FLAG                                   
             WHEN OTHER                                                         
               DISPLAY 'BP04K010 READNXT ERROR, STATUS '                        
                  BP04K010-STATUS  '  KEY : ' K010-PRIME-KEY                    
               MOVE BP04K010-STATUS   TO RETURN-CODE                            
               PERFORM 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       6300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       7000-PRINT-REPORT.                                                       
      *---------------------------------------------------------------*         
           PERFORM 7150-CHK-LINE-CTR    THRU 7150-EXIT.                         
           MOVE   SPACES        TO   L067-DETAIL-0.                             
           WRITE  BP13L067-REC  FROM L067-DETAIL-0.                             
           ADD  1     TO   WS-L067-WRITE-CNT                                    
                           WS-LINE-CNT.                                         
                                                                                
           MOVE  SPACES       TO  L067-DETAIL-0.                                
           ADD     1          TO  WS-SLNO.                                      
           MOVE WS-SLNO       TO  L067-SEQ-NO.                                  
           MOVE WS-NUM-NT     TO  L067-NEW-TOWN.                                
           MOVE WS-NUM-FT     TO  L067-FLAT-TYPE.                               
           MOVE WS-NUM-QUEUE  TO  L067-QUEUE.                                   
           MOVE WS-HH         TO  L067-HH.                                      
           MOVE WS-HH-CITZ    TO  L067-HH-CITZ.                                 
           MOVE WS-BALLOT-QTR TO  L067-BALLOT-QTR.                              
                                                                                
           PERFORM 7150-CHK-LINE-CTR    THRU 7150-EXIT.                         
           WRITE  BP13L067-REC  FROM L067-DETAIL-0.                             
           ADD  1     TO   WS-L067-WRITE-CNT                                    
                           WS-LINE-CNT.                                         
                                                                                
      *------------------------------------- *                                  
      * PRINT APPLICANT-1 AND OCCUPIER-1     *                                  
      *------------------------------------- *                                  
           MOVE    SPACES              TO   L067-DETAIL-1.                      
           MOVE F310-REGN-NO           TO   L067-NUM-REGN.                      
           MOVE WS-NME-OCCUP(1)        TO   L067-NME-OCCUP.                     
           MOVE WS-UIN-OCCUP(1)        TO   L067-UIN-OCCUP.                     
           MOVE WS-DEB-REF(1)          TO   L067-DEB-REF-NO.                    
           MOVE WS-REASON-CDE(1)       TO   L067-DEB-CODE.                      
           MOVE WS-APPL(1)             TO   L067-APPL.                          
           STRING WS-EFF-DATE(1)(7:2)  '/'                                      
                  WS-EFF-DATE(1)(5:2)  '/'                                      
                  WS-EFF-DATE(1)(1:4)                                           
                  DELIMITED BY SIZE    INTO L067-EFF-DATE.                      
                                                                                
           PERFORM 7150-CHK-LINE-CTR   THRU 7150-EXIT.                          
           WRITE  BP13L067-REC  FROM L067-DETAIL-1.                             
                                                                                
           ADD  1     TO   WS-LINE-CNT.                                         
                                                                                
      *------------------------------------- *                                  
      * PRINT APPLICANT-2 AND OCCUPIER-2     *                                  
      *------------------------------------- *                                  
           MOVE    SPACES            TO  L067-DETAIL-1.                         
           MOVE WS-NUM-SCH-ACC       TO  L067-SCH-ACC.                          
           IF (WS-UIN-OCCUP(2) NOT = SPACES AND LOW-VALUES)                     
                                                                                
              MOVE WS-NME-OCCUP(2)           TO  L067-NME-OCCUP                 
              MOVE WS-UIN-OCCUP(2)           TO  L067-UIN-OCCUP                 
              MOVE WS-DEB-REF(2)             TO  L067-DEB-REF-NO                
              MOVE WS-REASON-CDE(2)          TO  L067-DEB-CODE                  
              MOVE WS-APPL(2)                TO  L067-APPL                      
              STRING WS-EFF-DATE(2)(7:2)  '/'                                   
                     WS-EFF-DATE(2)(5:2)  '/'                                   
                     WS-EFF-DATE(2)(1:4)                                        
                     DELIMITED BY SIZE     INTO L067-EFF-DATE                   
           END-IF.                                                              
                                                                                
           PERFORM 7150-CHK-LINE-CTR   THRU 7150-EXIT.                          
           WRITE  BP13L067-REC  FROM L067-DETAIL-1.                             
                                                                                
           ADD  1     TO   WS-LINE-CNT.                                         
                                                                                
      *------------------------------------- *                                  
      * PRINT OCUCUCANTS AND OCCUPIERS (3-4) *                                  
      *------------------------------------- *                                  
           PERFORM VARYING WS-CTR FROM 3 BY 1 UNTIL WS-CTR > 12                 
              IF (WS-UIN-OCCUP(WS-CTR) NOT = SPACES AND LOW-VALUES)             
                 MOVE  SPACES     TO  L067-DETAIL-1                             
                                                                                
                 MOVE WS-NME-OCCUP(WS-CTR)   TO  L067-NME-OCCUP                 
                 MOVE WS-UIN-OCCUP(WS-CTR)   TO  L067-UIN-OCCUP                 
                 MOVE WS-DEB-REF(WS-CTR)     TO  L067-DEB-REF-NO                
                 MOVE WS-REASON-CDE(WS-CTR)  TO  L067-DEB-CODE                  
                 MOVE WS-APPL(WS-CTR)        TO  L067-APPL                      
                 STRING WS-EFF-DATE(WS-CTR)(7:2)  '/'                           
                        WS-EFF-DATE(WS-CTR)(5:2)  '/'                           
                        WS-EFF-DATE(WS-CTR)(1:4)                                
                        DELIMITED BY SIZE     INTO L067-EFF-DATE                
                                                                                
                 PERFORM 7150-CHK-LINE-CTR    THRU 7150-EXIT                    
                 WRITE  BP13L067-REC  FROM L067-DETAIL-1                        
                                                                                
                 ADD  1     TO   WS-LINE-CNT                                    
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------------------------------------------------------*         
       7150-CHK-LINE-CTR.                                                       
      *---------------------------------------------------------------*         
           IF WS-LINE-CNT > WS-MAX-LINES                                        
              PERFORM 7200-PRINT-L067-HEADER   THRU 7200-EXIT                   
           END-IF.                                                              
                                                                                
       7150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       7200-PRINT-L067-HEADER.                                                  
      *---------------------------------------------------------------*         
           ADD     1            TO   WS-PAGE-CNT.                               
           MOVE WS-PAGE-CNT     TO   L067-PAGE.                                 
           WRITE BP13L067-REC   FROM L067-HEADER-1 AFTER PAGE.                  
           WRITE BP13L067-REC   FROM L067-HEADER-2.                             
           WRITE BP13L067-REC   FROM L067-PR-LINE  AFTER 2 LINES.               
           WRITE BP13L067-REC   FROM L067-HEADER-2A.                            
           WRITE BP13L067-REC   FROM L067-HEADER-3.                             
           WRITE BP13L067-REC   FROM L067-HEADER-3A.                            
           WRITE BP13L067-REC   FROM L067-PR-LINE.                              
           MOVE    8            TO   WS-LINE-CNT.                               
                                                                                
       7200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-ROUTINE.                                                      
      *---------------------------------------------------------------*         
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY '      PROGRAM : BP13C067       ' L067-DATE.                 
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
           MOVE WS-F310-READ-CNT      TO WS-EDIT1.                              
           DISPLAY 'NO OF RECS READ FROM BP13F310      : ' WS-EDIT1.            
           MOVE WS-K800-NOT-CASES     TO WS-EDIT1.                              
           DISPLAY 'NO OF CASES NOT FOUND IN BP13K800  : ' WS-EDIT1.            
           MOVE WS-L067-WRITE-CNT     TO WS-EDIT1.                              
           DISPLAY 'NO OF CASES PRINTED TO BP13L067    : ' WS-EDIT1.            
           DISPLAY '*---------------------------------------------*'.           
                                                                                
           CLOSE   BP13F310                                                     
                   BP13K800                                                     
                   BP13K820                                                     
                   BP04K010                                                     
                   BP13L067.                                                    
                                                                                
           IF BP13K800-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BP13K800 CLOSE ERROR, STATUS ' BP13K800-STATUS            
             MOVE BP13K800-STATUS   TO RETURN-CODE                              
           END-IF.                                                              
                                                                                
           IF BP13K820-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BP13K820 CLOSE ERROR, STATUS ' BP13K820-STATUS            
             MOVE BP13K820-STATUS   TO RETURN-CODE                              
           END-IF.                                                              
                                                                                
           IF BP04K010-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BP04K010 CLOSE ERROR, STATUS ' BP04K010-STATUS            
             MOVE BP04K010-STATUS   TO RETURN-CODE                              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
************************** END OF BP13C067 ****************************         
                                                                                
