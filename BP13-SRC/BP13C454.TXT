       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C454.                                                 
       AUTHOR.        MTDULLONA.                                                
      *DATE-WRITTEN.  JAN2004.                                                  
      ***************************************************************           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      ***************************************************************           
      * OBJECTIVE    :  TO PRINT REPORT ON KEYS RETURNED TO SOU     *           
      *-------------------------------------------------------------*           
      * INPUT FILE : BP13F432 (EXTRACTED RECORDS FROM BP13C453)     *           
      * OUTPUT FILE: BP13L454 (DAILY REPORT ON KEYS RETURNED TO SOU)*           
      *-------------------------------------------------------------*           
      * CHG REF  OIC  DATE    DESCRIPTION                           *           
      *--------- ---  ------  --------------------------------------*           
      * BP132449 MTD  310104  PRINT REPORT ON KEYS RETURNED TO SOU  *           
      * BP132647 GAN1 201204  ADD COLUMN FOR FLAT STATUS            *           
      * BP133084 RB12 110607  REMOVE DTE-RETURN, DISPLAY HLE EXPIRY *           
      ***************************************************************           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F432  ASSIGN TO BP13F432.                                 
                                                                                
           SELECT BP13L454  ASSIGN TO BP13L454.                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F432                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01  BP13F432-RECORD.                                                     
           05  F432-NUM-REGN                 PIC X(08).                         
           05  F432-NUM-NRIC                 PIC X(09) OCCURS 4.                
           05  F432-NME-OCCP                 PIC X(66) OCCURS 4.                
           05  F432-NUM-BLK                  PIC X(05).                         
           05  F432-NUM-LEVEL                PIC X(02).                         
           05  F432-NUM-UNIT                 PIC X(04).                         
           05  F432-NME-STREET               PIC X(32).                         
           05  F432-NUM-CA-TAG               PIC X(01).                         
           05  F432-NUM-LOAN-TAG             PIC X(01).                         
           05  F432-DTE-ACCEPTANCE           PIC X(08).                         
           05  F432-DTE-HLE-EXPIRY           PIC X(08).                         
           05  F432-NUM-FLAT-STATUS          PIC X(02).                         
           05  FILLER                        PIC X(129).                        
                                                                                
       FD  BP13L454                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01  BP13L454-REC                     PIC X(132).                         
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-DATE             PIC X(08) VALUE SPACES.                  
           05  FILLER                  PIC X(13) VALUE SPACES.                  
                                                                                
       01  WS-DISP-DATE.                                                        
           05  WS-DISP-DD              PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE '/'.                     
           05  WS-DISP-MM              PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE '/'.                     
           05  WS-DISP-CCYY            PIC X(04) VALUE SPACES.                  
                                                                                
       01  WS-WORK-VAR.                                                         
           05  WS-END-OF-F432-SW       PIC X(01) VALUE SPACES.                  
               88  C-END-OF-F432       VALUE 'Y'.                               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F432-READ-CTR        PIC 9(08) VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(02) VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(04) VALUE ZEROES.                  
           05  WS-SNO-CTR              PIC 9(04) VALUE ZEROES.                  
                                                                                
      *-------------------------------------------------------------            
      * REPORT LAYOUT                                                           
      *-------------------------------------------------------------            
       01  L454-HEAD-01.                                                        
           05 FILLER                   PIC X(55)  VALUE 'BP13L454'.             
           05 FILLER                   PIC X(20)  VALUE                         
              'SYSTEM OF COMMITMENT'.                                           
           05 FILLER                   PIC X(39)  VALUE SPACES.                 
           05 FILLER                   PIC X(7)   VALUE 'DATE : '.              
           05 L454-DATE                PIC X(10)  VALUE SPACES.                 
                                                                                
       01  L454-HEAD-02.                                                        
           05 FILLER                   PIC X(47)  VALUE SPACES.                 
           05 FILLER                   PIC X(40)  VALUE                         
              'DAILY REPORT ON KEYS RETURNED TO SOU'.                           
           05 FILLER                   PIC X(27)  VALUE SPACES.                 
           05 FILLER                   PIC X(7)   VALUE 'PAGE : '.              
           05 L454-PAGE                PIC ZZZ9   VALUE ZEROS.                  
                                                                                
       01  L454-HEAD-03.                                                        
           05 FILLER                   PIC X(55)  VALUE                         
              'S/NO      REGN NO       NAME OF HA''S                '.          
           05 FILLER                   PIC X(54)  VALUE                         
              '                FLAT ADDRESS        FLAT STATUS CA LN'.          
           05 FILLER                   PIC X(22)  VALUE                         
              'DTE-ACCEPT DTE-EXPIRY'.                                          
                                                                                
       01  L454-DTL-LINE.                                                       
           05  L454-SNO                 PIC ZZZ9.                               
           05  FILLER                   PIC X(06) VALUE SPACES.                 
           05  L454-NUM-REGN            PIC X(08).                              
           05  FILLER                   PIC X(06) VALUE SPACES.                 
           05  L454-NME-OCCP            PIC X(44).                              
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  L454-ADDRESS             PIC X(30).                              
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  L454-FLAT-ST             PIC X(02).                              
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  L454-CA-TAG              PIC X(01).                              
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  L454-LOAN-TAG            PIC X(01).                              
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  L454-DTE-ACCEPTANCE      PIC X(10).                              
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  L454-DTE-HLE-EXPIRY      PIC X(10).                              
                                                                                
       01  L454-TOTAL-LINE.                                                     
           05  FILLER                   PIC X(18) VALUE SPACES.                 
           05  FILLER                   PIC X(30) VALUE                         
               'TOTAL NUMBER OF CASES: '.                                       
            05  L454-TOTAL-CASE         PIC Z(08).                              
                                                                                
       01  L454-LINE                    PIC X(132) VALUE ALL '-'.               
                                                                                
       PROCEDURE DIVISION.                                                      
      *--------------------------------*                                        
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 1000-INITIALIZATION    THRU 1000-EXIT.                       
           PERFORM 2000-PROCESS-RECORDS   THRU 2000-EXIT                        
             UNTIL C-END-OF-F432.                                               
           PERFORM 5000-PRINT-TOTALS      THRU 5000-EXIT.                       
           PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       1000-INITIALIZATION.                                                     
      *--------------------------------*                                        
                                                                                
           OPEN INPUT    BP13F432                                               
                OUTPUT   BP13L454.                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-SYSTEM-DATE.                       
           MOVE WS-SYS-DATE(1:4)       TO WS-DISP-CCYY.                         
           MOVE WS-SYS-DATE(5:2)       TO WS-DISP-MM.                           
           MOVE WS-SYS-DATE(7:2)       TO WS-DISP-DD.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2000-PROCESS-RECORDS.                                                    
      *--------------------------------*                                        
                                                                                
           READ BP13F432 AT END                                                 
                MOVE 'Y'   TO  WS-END-OF-F432-SW                                
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1        TO WS-F432-READ-CTR.                                    
           PERFORM 3000-PRINT-REPORT           THRU 3000-EXIT.                  
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3000-PRINT-REPORT.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 4000-CHECK-LINE-CTR          THRU 4000-EXIT.                 
                                                                                
           MOVE SPACES                TO L454-DTL-LINE.                         
           WRITE BP13L454-REC  FROM L454-DTL-LINE.                              
           ADD 1          TO WS-LINE-CTR.                                       
                                                                                
           PERFORM 4000-CHECK-LINE-CTR          THRU 4000-EXIT.                 
                                                                                
           ADD 1                      TO WS-SNO-CTR.                            
           MOVE WS-SNO-CTR            TO L454-SNO.                              
           MOVE F432-NUM-REGN         TO L454-NUM-REGN.                         
           MOVE F432-NME-OCCP (1)     TO L454-NME-OCCP.                         
           STRING 'BLK ' F432-NUM-BLK                                           
                  ' #'   F432-NUM-LEVEL                                         
                  '-'    F432-NUM-UNIT                                          
                  DELIMITED BY SIZE INTO L454-ADDRESS                           
           END-STRING.                                                          
           MOVE F432-NUM-CA-TAG       TO L454-CA-TAG.                           
           MOVE F432-NUM-LOAN-TAG     TO L454-LOAN-TAG.                         
           STRING F432-DTE-ACCEPTANCE (7:2) '/'                                 
                  F432-DTE-ACCEPTANCE (5:2) '/'                                 
                  F432-DTE-ACCEPTANCE (1:4)                                     
                  DELIMITED BY SIZE  INTO L454-DTE-ACCEPTANCE                   
           END-STRING.                                                          
           STRING F432-DTE-HLE-EXPIRY (7:2) '/'                                 
                  F432-DTE-HLE-EXPIRY (5:2) '/'                                 
                  F432-DTE-HLE-EXPIRY (1:4)                                     
                  DELIMITED BY SIZE  INTO L454-DTE-HLE-EXPIRY                   
           END-STRING.                                                          
                                                                                
           MOVE F432-NUM-FLAT-STATUS TO L454-FLAT-ST.                           
                                                                                
           WRITE BP13L454-REC  FROM L454-DTL-LINE.                              
           ADD 1          TO WS-LINE-CTR.                                       
                                                                                
      **-- PRINT STREET NAME AND HA2                                            
           MOVE SPACES                TO L454-DTL-LINE.                         
           PERFORM 4000-CHECK-LINE-CTR          THRU 4000-EXIT.                 
           MOVE F432-NME-STREET       TO L454-ADDRESS.                          
           IF F432-NME-OCCP (2) = SPACES OR LOW-VALUES                          
              MOVE SPACES             TO L454-NME-OCCP                          
           ELSE                                                                 
              MOVE F432-NME-OCCP (2)  TO L454-NME-OCCP                          
           END-IF.                                                              
           WRITE BP13L454-REC  FROM L454-DTL-LINE.                              
           ADD 1          TO WS-LINE-CTR.                                       
                                                                                
      **-- PRINT HA3                                                            
           IF F432-NME-OCCP (3) NOT = SPACES AND LOW-VALUES                     
              PERFORM 4000-CHECK-LINE-CTR        THRU 4000-EXIT                 
              MOVE SPACES             TO L454-DTL-LINE                          
              MOVE F432-NME-OCCP (3)  TO L454-NME-OCCP                          
              WRITE BP13L454-REC  FROM L454-DTL-LINE                            
              ADD 1          TO WS-LINE-CTR                                     
           END-IF.                                                              
                                                                                
      **-- PRINT HA4                                                            
           IF F432-NME-OCCP (4) NOT = SPACES AND LOW-VALUES                     
              PERFORM 4000-CHECK-LINE-CTR        THRU 4000-EXIT                 
              MOVE SPACES             TO L454-DTL-LINE                          
              MOVE F432-NME-OCCP (4)  TO L454-NME-OCCP                          
              WRITE BP13L454-REC  FROM L454-DTL-LINE                            
              ADD 1          TO WS-LINE-CTR                                     
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       4000-CHECK-LINE-CTR.                                                     
      *--------------------------------*                                        
                                                                                
           IF WS-LINE-CTR > 55  OR                                              
              WS-LINE-CTR = 0                                                   
              PERFORM 4100-PRINT-PAGE-BREAK   THRU 4100-EXIT                    
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       4100-PRINT-PAGE-BREAK.                                                   
      *--------------------------------*                                        
                                                                                
           ADD   1   TO WS-PAGE-CTR.                                            
           MOVE WS-PAGE-CTR    TO L454-PAGE.                                    
           MOVE WS-DISP-DATE   TO L454-DATE.                                    
           WRITE BP13L454-REC  FROM L454-HEAD-01 AFTER PAGE.                    
           WRITE BP13L454-REC  FROM L454-HEAD-02.                               
           WRITE BP13L454-REC  FROM L454-LINE    AFTER 2.                       
           WRITE BP13L454-REC  FROM L454-HEAD-03.                               
           WRITE BP13L454-REC  FROM L454-LINE.                                  
           MOVE 7         TO   WS-LINE-CTR.                                     
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       5000-PRINT-TOTALS.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 4000-CHECK-LINE-CTR          THRU 4000-EXIT.                 
           MOVE SPACES               TO L454-DTL-LINE.                          
           WRITE BP13L454-REC  FROM L454-DTL-LINE.                              
           ADD 1          TO WS-LINE-CTR.                                       
                                                                                
           PERFORM 4000-CHECK-LINE-CTR          THRU 4000-EXIT.                 
           MOVE WS-F432-READ-CTR     TO L454-TOTAL-CASE.                        
           WRITE BP13L454-REC  FROM L454-TOTAL-LINE.                            
           ADD 1          TO WS-LINE-CTR.                                       
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       9000-CLOSE-FILES.                                                        
      *--------------------------------*                                        
                                                                                
           DISPLAY 'PROGRAM ID   : BP13C454'.                                   
           DISPLAY 'CURRENT DATE : ' WS-DISP-DATE.                              
           DISPLAY SPACES.                                                      
           DISPLAY '*=============== COUNTERS ===============*'.                
           DISPLAY 'NO. OF F432 READ           : ' WS-F432-READ-CTR.            
                                                                                
           CLOSE BP13F432                                                       
                 BP13L454.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
