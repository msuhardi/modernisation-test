      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13C826.                                                 
      *AUTHOR.        EDWARD S CRUZ.                                            
      *DATE-WRITTEN.  04/10/2006.                                               
                                                                                
      * ========================================================== *            
      * SYSTEM OF COMMITMENT  (BP13)                               *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE   :     WRITE BP13FB78 RECORDS TO BP13KB78      *            
      *                    FOR ALL PROCESSED CASES OF THE DAY      *            
      *                                                            *            
      *  INPUT FILE  :     BP13FB78  -  REQ FILE                   *            
      *  OUTPUT FILE :     BP13KB78  -  REQ FILE                   *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF  BY    DATE    DESCRIPTION                         *            
      * -------- ---  ------   -----------                         *            
      * BP132886 CKK7 20061004 NEW PROGRAM.                        *            
      * ========================================================== *            
                                                                                
      ***********************                                                   
       ENVIRONMENT DIVISION.                                                    
      ***********************                                                   
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FB78  ASSIGN       TO BP13FB78.                           
                                                                                
           SELECT BP13KB78  ASSIGN       TO BP13KB78                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS KB78-KEY-FLD                        
                            FILE STATUS  IS BP13KB78-STATUS.                    
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13KB78                                                            
            RECORD CONTAINS 3300 CHARACTERS.                                    
       COPY BP13KB78.                                                           
                                                                                
       FD   BP13FB78                                                            
            RECORDING MODE  IS F                                                
            RECORD CONTAINS 3300 CHARACTERS.                                    
       COPY BP13FB78.                                                           
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-FB78-READ-CTR             PIC 9(06)  VALUE ZEROS.             
           05  WS-KB78-WRITE-CTR            PIC 9(06)  VALUE ZEROS.             
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-FB78-EOF                  PIC X(01)  VALUE 'N'.               
           05  BP13KB78-STATUS              PIC 99     VALUE 00.                
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-DATE.                                                         
               10  WS-CCYY                  PIC 9(4)   VALUE 0000.              
               10  WS-MM                    PIC 99     VALUE 00.                
               10  WS-DD                    PIC 99     VALUE 00.                
           05  WS-CURR-DATE                 PIC 9(08)  VALUE ZEROES.            
           05  FB78-RUN-DATE                PIC X(10)  VALUE ZEROES.            
                                                                                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************************************************************        
       0000-MAIN-ROUTINE.                                                       
      ******************************************************************        
           PERFORM 1000-OPEN-FILES             THRU 1000-EXIT.                  
           PERFORM 3000-PROCESS-RECORDS        THRU 3000-EXIT                   
                       UNTIL WS-FB78-EOF = 'Y'.                                 
           PERFORM 9000-CLOSE-ROUTINE          THRU 9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-FILES.                                                         
      ******************************************************************        
           OPEN INPUT   BP13FB78                                                
                I-O     BP13KB78.                                               
                                                                                
           IF BP13KB78-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP13KB78-STATUS ' BP13KB78-STATUS         
              MOVE     BP13KB78-STATUS  TO RETURN-CODE                          
              PERFORM  9000-CLOSE-ROUTINE.                                      
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-DATE WS-CURR-DATE.             
           STRING WS-DD '/' WS-MM '/' WS-CCYY DELIMITED BY SIZE                 
                  INTO FB78-RUN-DATE.                                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
           PERFORM 4000-READ-BP13FB78     THRU 4000-EXIT.                       
           PERFORM 5000-REWRITE-BP13KB78  THRU 5000-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       4000-READ-BP13FB78.                                                      
      ******************************************************************        
           READ BP13FB78                                                        
                AT END MOVE 'Y' TO WS-FB78-EOF                                  
                       GO TO 3000-EXIT.                                         
                                                                                
           ADD  1  TO  WS-FB78-READ-CTR.                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       5000-REWRITE-BP13KB78.                                                   
      ******************************************************************        
           MOVE  SPACES             TO BP13KB78-REC.                            
           INITIALIZE                  BP13KB78-REC.                            
                                                                                
           MOVE FB78-NUM-REGN       TO KB78-NUM-REGN.                           
           READ BP13KB78.                                                       
                                                                                
           EVALUATE BP13KB78-STATUS                                             
           WHEN 00                                                              
           WHEN 02                                                              
              MOVE  BP13FB78-REC       TO BP13KB78-REC                          
              REWRITE BP13KB78-REC                                              
              ADD   1                  TO WS-KB78-WRITE-CTR                     
           WHEN 23                                                              
              DISPLAY 'BP13KB78 REC MISSING, STATUS IS' BP13KB78-STATUS         
              MOVE     BP13KB78-STATUS  TO RETURN-CODE                          
           WHEN OTHER                                                           
              DISPLAY 'BP13KB78 WRITE ERROR, STATUS IS' BP13KB78-STATUS         
              MOVE     BP13KB78-STATUS  TO RETURN-CODE                          
              PERFORM  9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       9000-CLOSE-ROUTINE.                                                      
      ******************************************************************        
            DISPLAY ' '.                                                        
            DISPLAY ' ====  BP13C826 CONTROL TOTALS  ===='.                     
            DISPLAY ' '.                                                        
            DISPLAY ' FB78 RECORDS READ          : ' WS-FB78-READ-CTR.          
            DISPLAY ' KB78 RECORDS UPDATED       : ' WS-KB78-WRITE-CTR.         
            DISPLAY ' '.                                                        
            DISPLAY ' ==================================='.                     
                                                                                
            CLOSE BP13KB78                                                      
                  BP13FB78.                                                     
                                                                                
            IF BP13KB78-STATUS NOT = 00                                         
               DISPLAY 'CLOSING ERROR-BP13KB78-STAT ' BP13KB78-STATUS.          
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
