      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13C735.                                                 
       AUTHOR.        ABH.                                                      
       DATE-WRITTEN.  19/07/06.                                                 
      *****************************************************************         
      * OBJECTIVE - TO GENERATE REPORT ON TENANT PRIORITY SCHEME.               
      *                                                                         
      *     INPUT FILE : 1. BP13F500                                            
      *                  2. BP13F716                                            
      *                  3. SY02F001                                            
      *                                                                         
      *     OUTPUT FILE:                                                        
      *                  1. P13F716A                                            
      *                  2. BP13F730                                            
      *                                                                         
      *     LISTING:                                                            
      *                  1. BP13L735 (REPORT)                                   
      *                                                                         
      *                                                                         
      *----------------------------------------------------------------         
      * REF NO    DATE      BY   AMENDMENTS/ENHANCEMENTS                        
      * -------   --------  ---  ------------------------                       
      * BP132886  19072006  BA3  NEW PROGRAM                                    
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F500  ASSIGN  TO   BP13F500.                             
           SELECT  BP13F716  ASSIGN  TO   BP13F716.                             
           SELECT  SY02F001  ASSIGN  TO   SY02F001.                             
           SELECT  P13F716A  ASSIGN  TO   P13F716A.                             
           SELECT  BP13L735  ASSIGN  TO   BP13L735.                             
           SELECT  BP13F730  ASSIGN  TO   BP13F730.                             
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BP13F500                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 500 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F500.                                                           
                                                                                
       FD   BP13F716                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 350 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F716.                                                           
                                                                                
       FD   BP13F730                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 500 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F730.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   P13F716A                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 350 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       01  P13F716A-REC                PIC X(350).                              
                                                                                
       FD  BP13L735                    BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 132 CHARACTERS           
                                       LABEL RECORDS ARE OMITTED                
                                       RECORDING MODE IS F.                     
       01  PRINT-REC                   PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-F500-EOF                 PIC X     VALUE 'N'.                     
       01  WS-F716-EOF                 PIC X     VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS                           VALUE ZEROES.                  
           05  WS-CNT-F500             PIC 9(7).                                
           05  WS-CNT-F716             PIC 9(7).                                
           05  WS-CNT-F730             PIC 9(7).                                
           05  WS-CNT-F716A            PIC 9(7).                                
           05  WS-PAGE-CNT             PIC 9(5).                                
           05  WS-LINE-CNT             PIC 9(2).                                
           05  WS-SSN                  PIC 9(5).                                
           05  CNT-CASES               PIC 9(2).                                
                                                                                
       01  WS-MO-STAY                  PIC 9999  VALUE ZEROES.                  
       01  WS-F500-NOT-FOUND           PIC    X  VALUE SPACES.                  
       01  WS-PREV-NUM-REGN            PIC X(8)  VALUE SPACES.                  
                                                                                
       01  WS-CURR-DATE.                                                        
           05  WS-CURR-CCYY            PIC X(4)  VALUE SPACES.                  
           05  WS-CURR-MM              PIC X(2)  VALUE SPACES.                  
           05  WS-CURR-DD              PIC X(2)  VALUE SPACES.                  
                                                                                
       01  WS-TO-DATE.                                                          
           03  WS-TO-CCYYMM            PIC X(6).                                
           03  WS-TO-CCYYMM-B REDEFINES WS-TO-CCYYMM.                           
               05  WS-TO-CCYYMM        PIC 9(6).                                
               05  WS-TO-CCYYMM-C REDEFINES WS-TO-CCYYMM.                       
                   10 WS-TO-CCYY       PIC 9(4).                                
                   10 WS-TO-MM         PIC 9(2).                                
           03  WS-TO-DD                PIC 99.                                  
                                                                                
       01  WS-FROM-DATE.                                                        
           03  WS-FROM-CCYYMM-A        PIC X(6).                                
           03  WS-FROM-CCYYMM-B REDEFINES WS-FROM-CCYYMM-A.                     
               05  WS-FROM-CCYY        PIC 9(4).                                
               05  WS-FROM-MM          PIC 9(2).                                
           03  WS-FROM-DD              PIC 99.                                  
                                                                                
       01  L735-PR-HEAD-01.                                                     
           05  FILLER                  PIC X(8)  VALUE 'BP13L735'.              
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(8)  VALUE 'HDBCAT 3'.              
           05  FILLER                  PIC X(24) VALUE SPACES.                  
           05  FILLER                  PIC X(20) VALUE                          
                                       'S Y S T E M   O F   '.                  
           05  FILLER                  PIC X(19) VALUE                          
                                       'C O M M I T M E N T'.                   
           05  FILLER                  PIC X(15) VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'DATE :'.                
           05  L735-DATE               PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'PAGE : '.               
           05  L735-PAGENO             PIC ZZZZ9 .                              
                                                                                
       01  L735-PR-HEAD-02.                                                     
           05  FILLER                  PIC X(47) VALUE SPACES.                  
           05  FILLER                  PIC X(32) VALUE                          
               'REPORT ON TENANT PRIORITY SCHEME'.                              
                                                                                
       01  L735-PR-HEAD-03.                                                     
           05  FILLER                  PIC X(05) VALUE 'S/N'.                   
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(08) VALUE 'REGN    '.              
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(09) VALUE 'ALLOC-SCH'.             
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE 'HT'.                    
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(06) VALUE 'FT-REQ'.                
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(09) VALUE '  NRIC   '.             
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(10) VALUE 'SCH-ACC-NO'.            
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(08) VALUE 'SCH-RENT'.              
           05  FILLER                  PIC X(04) VALUE SPACES.                  
           05  FILLER                  PIC X(09) VALUE 'FT-RESIDE'.             
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  FILLER                  PIC X(03) VALUE 'NT'.                    
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(10) VALUE ' DTE-TENC '.            
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(11) VALUE 'DTE-MOVE-IN'.           
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(08) VALUE 'MTH-STAY'.              
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(07) VALUE 'REMARKS'.               
                                                                                
       01  L735-PR-DETAILS.                                                     
           05  L735-SSN                PIC ZZZZ9 VALUE ZEROES.                  
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  L735-REGN               PIC X(08) VALUE SPACES.                  
           05  FILLER                  PIC X(04) VALUE SPACES.                  
           05  L735-ALLOC-SCH          PIC X(03) VALUE SPACES.                  
           05  FILLER                  PIC X(04) VALUE SPACES.                  
           05  L735-HT                 PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(04) VALUE SPACES.                  
           05  L735-FT-REQ             PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  L735-NRIC               PIC X(09) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  L735-SCH-ACC-NO         PIC X(09) VALUE SPACES.                  
           05  FILLER                  PIC X(04) VALUE SPACES.                  
           05  L735-SCH-RENT           PIC X(03) VALUE SPACES.                  
           05  FILLER                  PIC X(10) VALUE SPACES.                  
           05  L735-FT-RESIDE          PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(09) VALUE SPACES.                  
           05  L735-NT                 PIC X(03) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  L735-DTE-TENC           PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  L735-DTE-MOVE-IN        PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  L735-MTH-STAY           PIC  ZZZ9 VALUE ZEROES.                  
           05  FILLER                  PIC X(04) VALUE SPACES.                  
           05  L735-REMARKS            PIC X(13) VALUE SPACES.                  
                                                                                
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN-PARA.                                                          
      ******************************************************************        
                                                                                
           PERFORM 1000-START-UP           THRU  1000-EXIT.                     
                                                                                
           PERFORM 1500-READ-SY02F001      THRU  1500-EXIT.                     
                                                                                
           PERFORM 2000-READ-F500          THRU  2000-EXIT.                     
                                                                                
           PERFORM 2500-READ-F716          THRU  2500-EXIT.                     
                                                                                
           PERFORM 3000-PROCESS-RECORD     THRU  3000-EXIT                      
                   UNTIL WS-F500-EOF       =     'Y'                            
                     AND WS-F716-EOF       =     'Y'.                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       1000-START-UP.                                                           
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F500                                                 
                       BP13F716                                                 
                       SY02F001                                                 
                OUTPUT P13F716A                                                 
                       BP13F730                                                 
                       BP13L735.                                                
                                                                                
           MOVE 60 TO WS-LINE-CNT.                                              
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       1500-READ-SY02F001.                                                      
      ******************************************************************        
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-CURRENT      TO WS-CURR-DATE.                          
                                                                                
           STRING WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CCYY                    
                  DELIMITED BY SIZE INTO L735-DATE.                             
                                                                                
       1500-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       2000-READ-F500.                                                          
      ******************************************************************        
                                                                                
           READ BP13F500           AT   END                                     
                MOVE 'Y'           TO   WS-F500-EOF                             
                MOVE HIGH-VALUES   TO   F500-NUM-REGN                           
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO   WS-CNT-F500.                            
                                                                                
       2000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       2500-READ-F716.                                                          
      ******************************************************************        
                                                                                
           READ BP13F716           AT   END                                     
                MOVE 'Y'           TO   WS-F716-EOF                             
                MOVE HIGH-VALUES   TO   F716-NUM-REGN                           
                GO                 TO   2500-EXIT.                              
                                                                                
           ADD  1                  TO   WS-CNT-F716.                            
                                                                                
       2500-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORD.                                                     
      ******************************************************************        
                                                                                
           MOVE 'N'                              TO WS-F500-NOT-FOUND.          
           IF F500-NUM-REGN = F716-NUM-REGN                                     
              PERFORM 3100-PROCESS-THE-SAME-REGN THRU 3100-EXIT                 
                      UNTIL F500-NUM-REGN NOT =  F716-NUM-REGN                  
                         OR WS-F716-EOF = 'Y'                                   
              PERFORM 2000-READ-F500             THRU 2000-EXIT                 
           ELSE                                                                 
              IF F500-NUM-REGN < F716-NUM-REGN                                  
                 MOVE 'Y'                        TO WS-F500-NOT-FOUND           
                 PERFORM 4000-PRINT-DETAILS      THRU 4000-EXIT                 
                 PERFORM 2000-READ-F500          THRU 2000-EXIT                 
              ELSE                                                              
                 PERFORM 2500-READ-F716          THRU 2500-EXIT                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       3100-PROCESS-THE-SAME-REGN.                                              
      ******************************************************************        
                                                                                
           IF F500-NUM-REGN = F716-NUM-REGN                                     
              PERFORM 3500-WRITE-F716A     THRU 3500-EXIT                       
              PERFORM 4000-PRINT-DETAILS   THRU 4000-EXIT                       
              PERFORM 2500-READ-F716       THRU 2500-EXIT                       
           END-IF.                                                              
                                                                                
       3100-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       3500-WRITE-F716A.                                                        
      ******************************************************************        
                                                                                
           PERFORM 6000-COMPUTE-MONTH-STAY THRU 6000-EXIT.                      
           MOVE WS-MO-STAY        TO F716-NUM-MTH-RESIDE.                       
                                                                                
           WRITE P13F716A-REC   FROM BP13F716-REC.                              
           ADD 1                  TO WS-CNT-F716A.                              
                                                                                
       3500-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       4000-PRINT-DETAILS.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                    TO L735-PR-DETAILS.                   
           IF WS-LINE-CNT > 50                                                  
              PERFORM 5000-PRINT-HEADING  THRU 5000-EXIT                        
           END-IF.                                                              
                                                                                
           IF F500-NUM-REGN NOT = WS-PREV-NUM-REGN                              
              ADD   1                     TO WS-SSN                             
              MOVE WS-SSN                 TO L735-SSN                           
              MOVE F500-NUM-REGN          TO L735-REGN                          
              MOVE F500-NUM-REGN          TO WS-PREV-NUM-REGN                   
           END-IF.                                                              
                                                                                
           ADD   1                        TO CNT-CASES.                         
                                                                                
           MOVE F500-CDE-ALLOC-SCH        TO L735-ALLOC-SCH.                    
           MOVE F500-CDE-HOUSEHOLD        TO L735-HT.                           
           MOVE F500-CDE-FLAT-TYPE        TO L735-FT-REQ.                       
           IF WS-F500-NOT-FOUND = 'N'                                           
              MOVE F716-NUM-NRIC             TO L735-NRIC                       
              MOVE F716-NUM-SCH-ACCNT        TO L735-SCH-ACC-NO                 
              MOVE F716-NUM-HSNG-SCH-RENT    TO L735-SCH-RENT                   
              MOVE F716-CDE-FLAT-TYPE-RESIDE TO L735-FT-RESIDE                  
              MOVE F716-CDE-NT-RESIDE        TO L735-NT                         
              STRING F716-DTE-ACCEPTED (7:2) '/'                                
                     F716-DTE-ACCEPTED (5:2) '/'                                
                     F716-DTE-ACCEPTED (1:4)                                    
                     DELIMITED BY SIZE  INTO L735-DTE-TENC                      
              STRING F716-DTE-MOVING-IN (7:2) '/'                               
                     F716-DTE-MOVING-IN (5:2) '/'                               
                     F716-DTE-MOVING-IN (1:4)                                   
                     DELIMITED BY SIZE  INTO L735-DTE-MOVE-IN                   
                                                                                
              MOVE WS-MO-STAY                TO  L735-MTH-STAY                  
              IF WS-MO-STAY < 24                                                
                 MOVE '< 24 MTH'             TO L735-REMARKS                    
              END-IF                                                            
           ELSE                                                                 
              MOVE 'RENTAL NOT FD'           TO L735-REMARKS                    
              WRITE BP13F730-REC           FROM BP13F500-REC                    
              ADD 1                          TO WS-CNT-F730                     
           END-IF.                                                              
                                                                                
           WRITE PRINT-REC              FROM L735-PR-DETAILS.                   
                                                                                
           ADD 1                          TO WS-LINE-CNT.                       
                                                                                
       4000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       5000-PRINT-HEADING.                                                      
      ******************************************************************        
                                                                                
           ADD 1               TO WS-PAGE-CNT.                                  
           MOVE WS-PAGE-CNT    TO L735-PAGENO.                                  
           MOVE SPACES         TO PRINT-REC                                     
           WRITE PRINT-REC FROM L735-PR-HEAD-01 AFTER PAGE.                     
           WRITE PRINT-REC FROM L735-PR-HEAD-02 AFTER 1.                        
           WRITE PRINT-REC FROM L735-PR-HEAD-03 AFTER 2.                        
           MOVE SPACES TO PRINT-REC.                                            
           WRITE PRINT-REC AFTER 1.                                             
           MOVE 5 TO WS-LINE-CNT.                                               
                                                                                
       5000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       6000-COMPUTE-MONTH-STAY.                                                 
      ******************************************************************        
                                                                                
           MOVE ZEROES            TO WS-MO-STAY.                                
                                                                                
           MOVE F500-DTE-REQUEST  TO WS-FROM-DATE.                              
           MOVE F716-DTE-ACCEPTED TO WS-TO-DATE.                                
           COMPUTE WS-MO-STAY = (((WS-FROM-CCYY * 12) + (WS-FROM-MM)) -         
                                 ((WS-TO-CCYY * 12) + (WS-TO-MM))).             
                                                                                
       6000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13C735'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY 'NO OF BP13F500 RECORDS READ..........: '                    
                    WS-CNT-F500.                                                
           DISPLAY 'NO OF BP13F716 RECORDS READ..........: '                    
                    WS-CNT-F716.                                                
           DISPLAY 'NO OF P13L716A RECORDS WRITTEN.......: '                    
                    WS-CNT-F716A.                                               
           DISPLAY 'NO OF BP13F730 RECORDS WRITTEN.......: '                    
                    WS-CNT-F730.                                                
           DISPLAY 'NO OF BP13L735 RECORDS WRITTEN.......: '                    
                    CNT-CASES.                                                  
                                                                                
           CLOSE BP13F500                                                       
                 BP13F716                                                       
                 SY02F001                                                       
                 BP13F730                                                       
                 P13F716A                                                       
                 BP13L735.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
