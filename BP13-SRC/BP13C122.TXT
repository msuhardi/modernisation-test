       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C122.                                                 
       AUTHOR.        SELVY.                                                    
      *DATE-WRITTEN.  21/01/99.                                                 
                                                                                
      *****************************************************************         
      *                SYSTEM NAME (BP13)                             *         
      *===============================================================*         
      *  OBJECTIVES : 1. TO UPDATE FS-TAG TO '1' IF APPLICANT PAY     *         
      *                  REGISTRATION FEE BY CASH.                    *         
      *               2. RUN AT METH-END USING CASH COLLECTION FILE.  *         
      *===============================================================*         
      * FILES DESCRIPTION :-                                          *         
      *                ACCESS      REC                                *         
      * DD NAME  I/O/E  MODE       LEN  KEY          REMARKS          *         
      * -------  ----- -------     ---  ---          --------         *         
      * BP13K800 I/O    RANDOM     2000 K800-NUM-REGN                 *         
      * BP13F136 O      SEQUENTIAL 150                                *         
      * BP13L122 O      SEQUENTIAL 133                                *         
      *===============================================================*         
      * MODIFICATIONS :                                               *         
      * CHGE REQ #  DATE      AUTHOR    REMARKS                       *         
      * ----------  ----      ------    -------                       *         
      * BP130605    21/01/99  SRA       NEW PROGRAM                   *         
      *                                                               *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE IS RANDOM                                
                           RECORD KEY IS K800-NUM-REGN                          
                           FILE STATUS IS WS-K800-STATUS.                       
                                                                                
           SELECT BP13F136 ASSIGN TO BP13F136.                                  
                                                                                
           SELECT BP13L122 ASSIGN TO BP13L122.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K800     RECORD CONTAINS 2000 CHARACTERS.                        
       COPY BP13K800.                                                           
                                                                                
       FD  BP13F136     BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 150 CHARACTERS                          
                        LABEL RECORD IS STANDARD                                
                        RECORDING MODE IS F.                                    
       COPY BP13F136.                                                           
                                                                                
       FD  BP13L122     BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 133 CHARACTERS                          
                        LABEL RECORD IS OMITTED                                 
                        RECORDING MODE IS F.                                    
       01 BP13L122-REC            PIC X(133).                                   
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FILE-FLAGS.                                                       
           05  WS-K800-STATUS      PIC 9(2)      VALUE ZEROS.                   
           05  WS-F136-EOF         PIC X         VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F136-CNT-READ    PIC 9(8)      VALUE ZEROS.                   
           05  WS-K800-CNT-READ    PIC 9(8)      VALUE ZEROS.                   
           05  WS-K800-CNT-REWRT   PIC 9(8)      VALUE ZEROS.                   
           05  WS-K800-CNT-RECNFND PIC 9(8)      VALUE ZEROS.                   
           05  WS-L122-CNT-LINE    PIC 9(8)      VALUE 99.                      
           05  WS-L122-CNT-PAGE    PIC 9(8)      VALUE ZEROS.                   
           05  WS-L122-CNT-WRT     PIC 9(8)      VALUE ZEROS.                   
                                                                                
       01  WS-DATE                 PIC X(08)     VALUE SPACES.                  
                                                                                
       01  L122-HD-LINE1.                                                       
           05  FILLER              PIC X(08)     VALUE 'BP13L122'.              
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(35)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                     'SYSTEM OF COMMITMENT'.                                    
           05  FILLER              PIC X(13)     VALUE SPACES.                  
           05  FILLER              PIC X(06)     VALUE 'DATE :'.                
           05  L122-DATE           PIC X(10).                                   
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE 'PAGE NO :'.             
           05  L122-PAGE-NO        PIC Z(03)9.                                  
           05  FILLER              PIC X(10)     VALUE SPACES.                  
                                                                                
       01  L122-HEADER1.                                                        
           05  FILLER              PIC X(40)     VALUE SPACES.                  
           05  FILLER              PIC X(56)     VALUE                          
           'MONTHLY REPORT ON UPDATE FS-TAG=1 FROM COLLECTION SYSTEM'.          
           05  FILLER              PIC X(09)     VALUE 'USER : '.               
           05  FILLER              PIC X(07)     VALUE 'USER : '.               
           05  FILLER              PIC X(16)     VALUE                          
                      'APPLICATION TEAM'.                                       
           05  FILLER              PIC X(04)     VALUE SPACES.                  
                                                                                
       01  L122-HEADER2.                                                        
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE '  S/NO  '.              
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'REGN-NO '.              
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'ELIG-SCH'.              
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'ZONE'.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE 'FT'.                    
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'TAG'.                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(13)     VALUE 'REGN-DEP-CASH'.         
           05  FILLER              PIC X(67)     VALUE SPACES.                  
                                                                                
       01  L122-HEADER3.                                                        
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE ALL '-'.                 
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE ALL '-'.                 
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE ALL '-'.                 
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE ALL '-'.                 
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE ALL '-'.                 
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE ALL '-'.                 
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(13)     VALUE ALL '-'.                 
           05  FILLER              PIC X(67)     VALUE SPACES.                  
                                                                                
       01  L122-REC.                                                            
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L122-SNO            PIC Z(07)9.                                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  L122-REGN-NO        PIC X(08).                                   
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  L122-ELIG-SCH       PIC X(03).                                   
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  L122-ZONE           PIC X(02).                                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  L122-FT             PIC X(02).                                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  L122-TAG            PIC X(01).                                   
           05  FILLER              PIC X(07)     VALUE SPACES.                  
           05  L122-REG-DEP-CASH   PIC 9(08).                                   
           05  FILLER              PIC X(69)     VALUE SPACES.                  
                                                                                
       01  L122-EMPTY-LINE         PIC X(132)    VALUE SPACES.                  
                                                                                
       01  WS-L122-SNO             PIC 9(08)     VALUE ZEROES.                  
                                                                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 1500-F136-READ       THRU 1500-EXIT.                         
           PERFORM 2000-MAIN-ROUTINE    THRU 2000-EXIT                          
                   UNTIL WS-F136-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN I-O    BP13K800                                                 
                INPUT  BP13F136                                                 
                OUTPUT BP13L122.                                                
                                                                                
           IF WS-K800-STATUS NOT = ZEROES AND 97                                
              MOVE WS-K800-STATUS TO RETURN-CODE                                
              DISPLAY 'OPEN BP13K800 FAIL STATUS=' WS-K800-STATUS               
              GO TO 9000-CLOSE-FILES.                                           
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO L122-DATE.                             
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
       1500-F136-READ.                                                          
      *****************************************************************         
                                                                                
      *----------------------------------------------------*                    
      * READING BP13F136.                                  *                    
      *----------------------------------------------------*                    
                                                                                
           READ BP13F136 AT END                                                 
                        MOVE 'Y' TO WS-F136-EOF                                 
                        GO TO 1500-EXIT.                                        
                                                                                
           ADD 1 TO WS-F136-CNT-READ.                                           
                                                                                
       1500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
       2000-MAIN-ROUTINE.                                                       
      *****************************************************************         
                                                                                
      *----------------------------------------------------*                    
      * MAIN-ROUTINE.                                      *                    
      *----------------------------------------------------*                    
                                                                                
           IF F136-CDE-TRANS-TYPE = '11'                                        
              AND F136-CDE-PAYMENT-TYPE = 'RDE'                                 
              MOVE F136-NUM-REGN TO K800-NUM-REGN                               
              READ BP13K800                                                     
              EVALUATE WS-K800-STATUS                                           
                  WHEN 00                                                       
                       ADD 1 TO WS-K800-CNT-READ                                
                       PERFORM 2500-REWRITE-PARA THRU 2500-EXIT                 
                  WHEN 23                                                       
                       ADD 1 TO WS-K800-CNT-RECNFND                             
                  WHEN OTHER                                                    
                       DISPLAY 'ERROR WHILE READING:' WS-K800-STATUS            
                       DISPLAY 'REGN NUMBER        :' K800-NUM-REGN             
              END-EVALUATE                                                      
           END-IF.                                                              
           PERFORM 1500-F136-READ THRU 1500-EXIT.                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
       2500-REWRITE-PARA.                                                       
      *****************************************************************         
                                                                                
      *---------------------------------------------------*                     
      * REWRITING FS-TAG.                                 *                     
      *---------------------------------------------------*                     
                                                                                
           IF K800-NUM-FS-DEFER-TAG NOT = '1'                                   
              MOVE '1' TO  K800-NUM-FS-DEFER-TAG                                
              REWRITE BP13K800-MASTER                                           
              EVALUATE WS-K800-STATUS                                           
                  WHEN 00                                                       
                       ADD 1 TO WS-K800-CNT-REWRT                               
                       PERFORM 3000-WRITE-REPORT THRU 3000-EXIT                 
                  WHEN OTHER                                                    
                       DISPLAY 'ERROR WHILE REWRITING:' WS-K800-STATUS          
                       DISPLAY 'REGN NUMBER          :' K800-NUM-REGN           
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
       2500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
       3000-WRITE-REPORT.                                                       
      *****************************************************************         
                                                                                
           IF WS-L122-CNT-LINE > 55                                             
              PERFORM 3500-WRITE-HEADER THRU 3500-EXIT                          
           END-IF.                                                              
           ADD 1                           TO WS-L122-CNT-LINE.                 
           ADD 1                           TO WS-L122-SNO.                      
           MOVE WS-L122-SNO                TO L122-SNO.                         
           MOVE K800-NUM-REGN              TO L122-REGN-NO.                     
           MOVE K800-NUM-ELIG-SCHEME       TO L122-ELIG-SCH.                    
           MOVE K800-NUM-ZONE              TO L122-ZONE.                        
           MOVE K800-NUM-FLAT-TYPE         TO L122-FT.                          
           MOVE K800-NUM-FS-DEFER-TAG      TO L122-TAG.                         
           MOVE K800-AMT-REGN-DEPOSIT-CASH TO L122-REG-DEP-CASH.                
           WRITE BP13L122-REC      FROM L122-REC.                               
           ADD 1                   TO WS-L122-CNT-WRT.                          
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
       3500-WRITE-HEADER.                                                       
      *****************************************************************         
                                                                                
      *----------------------------------------------------*                    
      * WRITES HEADER IN THE BP13L122                      *                    
      *----------------------------------------------------*                    
                                                                                
           ADD  1                TO WS-L122-CNT-PAGE.                           
           MOVE WS-L122-CNT-PAGE TO L122-PAGE-NO.                               
           WRITE BP13L122-REC FROM L122-HD-LINE1 AFTER PAGE.                    
           WRITE BP13L122-REC FROM L122-EMPTY-LINE.                             
           WRITE BP13L122-REC FROM L122-HEADER1.                                
           WRITE BP13L122-REC FROM L122-EMPTY-LINE.                             
           WRITE BP13L122-REC FROM L122-HEADER2.                                
           WRITE BP13L122-REC FROM L122-HEADER3.                                
           WRITE BP13L122-REC FROM L122-EMPTY-LINE.                             
           MOVE 7                TO WS-L122-CNT-LINE.                           
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           DISPLAY 'TOTAL F136 REC READ     = ' WS-F136-CNT-READ.               
           DISPLAY 'TOTAL K800 REC READ     = ' WS-K800-CNT-READ.               
           DISPLAY 'TOTAL K800 REC REWRT    = ' WS-K800-CNT-REWRT.              
           DISPLAY 'TOTAL K800 REC RECNFND  = ' WS-K800-CNT-RECNFND.            
           DISPLAY 'TOTAL L122 REC WRITTEN  = ' WS-L122-CNT-WRT.                
                                                                                
           CLOSE      BP13K800                                                  
                      BP13F136                                                  
                      BP13L122.                                                 
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *====================  END OF PROGRAM  ====================*              
