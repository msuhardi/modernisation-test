       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C42P.                                                 
      *AUTHOR.        RJB1                                                      
      *DATE-WRITTEN.  22/08/2017.                                               
      * ========================================================== *            
      *                SYSTEM OF COMMITMENT (BP13)                 *            
      * ========================================================== *            
      *  OBJECTIVE : PROCESS AG07 VOUCHER RETURN FILE (AG07F640)   *            
      *              SERS CONTRA EJV                               *            
      *                                                            *            
      *  INPUT   :  AG07F640 - AG07 INTERFACE FILE                 *            
      *             AG07K970 - AG07 REJECT CODES                   *            
      *                                                            *            
      *  I-O     :  BP13K300 - AGREEMENT ORDER FILE                *            
      *             BP13K410 - ACCUMSO FILE                        *            
      *                                                            *            
      *  LISTING :  P13L42PA - REJECTED VOUCHERS LIST              *            
      *                                                            *            
      * ========================================================== *            
      * CHG-NO    BY    DATE    DESCRIPTION                        *            
      * BP136400  RJB1  220817  NEW PROGRAM                        *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT AG07F640 ASSIGN        TO AG07F640.                           
                                                                                
           SELECT BP13K300 ASSIGN        TO BP13K300                            
                           ACCESS MODE   IS RANDOM                              
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K300-KEY-FLD                        
                           FILE STATUS   IS WS-K300-STATUS.                     
                                                                                
           SELECT BP13K410 ASSIGN        TO BP13K410                            
                           ACCESS MODE   IS RANDOM                              
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K410-KEY-FLD                        
                           FILE STATUS   IS WS-K410-STATUS.                     
                                                                                
           SELECT AG07K970 ASSIGN        TO AG07K970                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K970-KEY-FLD                        
                           FILE STATUS   IS WS-K970-STATUS.                     
                                                                                
           SELECT P13L42PA ASSIGN        TO P13L42PA.                           
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  AG07F640                                                             
           RECORD CONTAINS 100 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
           COPY AG07F640.                                                       
                                                                                
       FD  BP13K300                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
           COPY BP13K300.                                                       
                                                                                
       FD  BP13K410                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
           COPY BP13K410.                                                       
                                                                                
       FD  AG07K970                                                             
           RECORD CONTAINS 40  CHARACTERS.                                      
           COPY AG07K970.                                                       
                                                                                
       FD  P13L42PA                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01  P13L42PA-REC                  PIC X(132).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K300-STATUS            PIC X(2)    VALUE SPACES.              
           05  WS-K410-STATUS            PIC X(2)    VALUE SPACES.              
           05  WS-K970-STATUS            PIC X(2)    VALUE SPACES.              
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F640-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F640-ACC           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F640-REJ           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K300-UPD           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K410-UPD           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-PAGE-A             PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-LINE-A             PIC 9(2)   VALUE 60.                   
           05  WS-CNT-SNO-A              PIC 9(4)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F640-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K300-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K410-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K970-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-REJECT-FL              PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY           PIC 9(4)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY           PIC 9(4)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE SPACES.               
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-NUM-DDD-FILE-REF.                                             
               10  WS-NUM-REGN           PIC X(8)   VALUE SPACES.               
               10  FILLER                PIC X(1)   VALUE SPACES.               
               10  WS-NUM-TRANS          PIC X(3)   VALUE SPACES.               
               10  FILLER                PIC X(3)   VALUE SPACES.               
           05  WS-DTE-SUB                PIC X(8)   VALUE SPACES.               
                                                                                
      *-------------------------------------------------------------            
      *    P13L42PA REPORT (REJECT LIST)                                        
      *-------------------------------------------------------------            
       01  L42PA-HEADER1.                                                       
           05  FILLER                    PIC X(9)   VALUE 'BP13L42PA'.          
           05  FILLER                    PIC X(3)   VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE 'HDBCAT 3'.           
           05  FILLER                    PIC X(28)  VALUE SPACES.               
           05  FILLER                    PIC X(39)  VALUE                       
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                    PIC X(17)  VALUE SPACES.               
           05  FILLER                    PIC X(6)   VALUE 'DATE: '.             
           05  L42PA-DTE-CURRENT         PIC X(10)  VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE '  PAGE: '.           
           05  L42PA-CNT-PAGE            PIC ZZZ9.                              
                                                                                
       01  L42PA-HEADER2.                                                       
           05  FILLER                    PIC X(34)  VALUE SPACES.               
           05  FILLER                    PIC X(53)  VALUE                       
               '      SERS CONTRA EJV VOUCHERS REJECTED BY EWPC ON   '.         
           05  L42PA-DTE-CURRENT2        PIC X(10)  VALUE SPACES.               
                                                                                
       01  L42PA-HEADER3.                                                       
           05  FILLER                    PIC X(132) VALUE                       
               '    S/NO    REGN        VOUCHER NO       SUBMIT DATE            
      -        'REJECT REASON'.                                                 
                                                                                
       01  L42PA-HEADER4.                                                       
           05  FILLER                    PIC X(132) VALUE                       
               '    ----    --------    -------------    -----------            
      -        '-----------------------------------------'.                     
                                                                                
       01  L42PA-DETAIL.                                                        
           05  FILLER                    PIC X(4)   VALUE SPACES.               
           05  L42PA-SNO                 PIC ZZZ9.                              
           05  FILLER                    PIC X(4)   VALUE SPACES.               
           05  L42PA-NUM-REGN            PIC X(8)   VALUE SPACES.               
           05  FILLER                    PIC X(4)   VALUE SPACES.               
           05  L42PA-NUM-OLD-VR          PIC X(13)  VALUE SPACES.               
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  L42PA-DTE-SUBMITTED       PIC X(10)  VALUE SPACES.               
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  L42PA-NUM-DESC            PIC X(41)  VALUE SPACES.               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-AG07F640-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-F640-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  AG07F640                                                 
                       AG07K970                                                 
                I-O    BP13K300                                                 
                       BP13K410                                                 
                OUTPUT P13L42PA.                                                
                                                                                
           IF WS-K410-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K410 ERROR ' WS-K410-STATUS                  
              MOVE     WS-K410-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K300-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K300 ERROR ' WS-K300-STATUS                  
              MOVE     WS-K300-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K970-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K970 ERROR ' WS-K970-STATUS                  
              MOVE     WS-K970-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
                                                                                
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8           TO   WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-AG07F640-READ.                                                      
      *=============================================================            
                                                                                
           READ AG07F640 AT END                                                 
                MOVE 'Y'                   TO   WS-F640-EOF                     
                GO                         TO   2000-EXIT.                      
                                                                                
           ADD 1                           TO   WS-CNT-F640-READ.               
           MOVE F640-NUM-DDD-FILE-REF      TO   WS-NUM-DDD-FILE-REF.            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           MOVE 'N'                        TO   WS-K300-FND                     
                                                WS-REJECT-FL.                   
                                                                                
           MOVE SPACES                     TO   WS-DTE-SUB.                     
           MOVE SPACES                     TO   BP13K300-REC.                   
           MOVE WS-NUM-REGN                TO   K300-CUR-REG.                   
           PERFORM 3100-BP13K300-READ      THRU 3100-EXIT.                      
                                                                                
           IF WS-K300-FND = 'Y'                                                 
      *-------------------------------------------------------------            
      *       PROCESS SOCA                                                      
      *-------------------------------------------------------------            
              EVALUATE F640-NUM-REJECT                                          
              WHEN ZEROES                                                       
              WHEN SPACES                                                       
              WHEN LOW-VALUES                                                   
                 ADD 1                     TO   WS-CNT-F640-ACC                 
                 PERFORM 3200-UPDATE-K300  THRU 3200-EXIT                       
              WHEN OTHER                                                        
                 ADD 1                     TO   WS-CNT-F640-REJ                 
                 MOVE 'Y'                  TO   WS-REJECT-FL                    
                 MOVE K300-DTE-SERS-CONTRA-JV                                   
                                           TO   WS-DTE-SUB                      
                 PERFORM 3200-UPDATE-K300  THRU 3200-EXIT                       
                 PERFORM 5000-GET-REJ-REASON                                    
                                           THRU 5000-EXIT                       
                 PERFORM 5100-PRINT-REJ    THRU 5100-EXIT                       
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
                                                                                
           MOVE SPACES                     TO   WS-DTE-SUB.                     
           MOVE SPACES                     TO   BP13K410-REC.                   
           MOVE WS-NUM-REGN                TO   K410-NUM-REGN.                  
           PERFORM 4000-BP13K410-READ      THRU 4000-EXIT.                      
                                                                                
           IF WS-K410-FND = 'Y'                                                 
      *-------------------------------------------------------------            
      *       PROCESS TP / SATP                                                 
      *-------------------------------------------------------------            
              EVALUATE F640-NUM-REJECT                                          
              WHEN ZEROES                                                       
              WHEN SPACES                                                       
              WHEN LOW-VALUES                                                   
                 ADD 1                     TO   WS-CNT-F640-ACC                 
                 PERFORM 4200-UPDATE-K410  THRU 4200-EXIT                       
              WHEN OTHER                                                        
                 ADD 1                     TO   WS-CNT-F640-REJ                 
                 MOVE 'Y'                  TO   WS-REJECT-FL                    
                 MOVE K410-DTE-SERS-CONTRA-JV                                   
                                           TO   WS-DTE-SUB                      
                 PERFORM 4200-UPDATE-K410  THRU 4200-EXIT                       
                 PERFORM 5000-GET-REJ-REASON    THRU 5000-EXIT                  
                 PERFORM 5100-PRINT-REJ    THRU 5100-EXIT                       
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
           PERFORM 2000-AG07F640-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3100-BP13K300-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K300.                                                       
                                                                                
           EVALUATE WS-K300-STATUS                                              
           WHEN '00'                                                            
              MOVE 'Y'                     TO   WS-K300-FND                     
                                                                                
           WHEN '23'                                                            
              MOVE 'N'                     TO   WS-K300-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K300 ERROR ' WS-K300-STATUS                     
              MOVE     WS-K300-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3200-UPDATE-K300.                                                        
      *=============================================================            
                                                                                
           IF WS-REJECT-FL = 'Y'                                                
              MOVE 'R'                     TO   K300-CDE-PRINT                  
                                                K300-SERS-JV-STAT               
           ELSE                                                                 
              MOVE 'A'                     TO   K300-SERS-JV-STAT               
           END-IF.                                                              
                                                                                
           REWRITE BP13K300-REC.                                                
                                                                                
           EVALUATE WS-K300-STATUS                                              
           WHEN 00                                                              
              ADD 1                        TO   WS-CNT-K300-UPD                 
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'REWRITE BP13K300 ERROR ' WS-K300-STATUS                  
              MOVE     WS-K300-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4000-BP13K410-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K410.                                                       
                                                                                
           EVALUATE WS-K410-STATUS                                              
           WHEN '00'                                                            
              MOVE 'Y'                     TO   WS-K410-FND                     
                                                                                
           WHEN '23'                                                            
              MOVE 'N'                     TO   WS-K410-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K410 ERROR ' WS-K410-STATUS                     
              MOVE     WS-K410-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4200-UPDATE-K410.                                                        
      *=============================================================            
                                                                                
           IF WS-REJECT-FL = 'Y'                                                
              MOVE 'R'                     TO   K410-CDE-PRINT                  
                                                K410-SERS-JV-STAT               
           ELSE                                                                 
              MOVE 'A'                     TO   K410-SERS-JV-STAT               
           END-IF.                                                              
                                                                                
           REWRITE BP13K410-REC.                                                
                                                                                
           EVALUATE WS-K410-STATUS                                              
           WHEN 00                                                              
              ADD 1                        TO   WS-CNT-K410-UPD                 
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'REWRITE BP13K410 ERROR ' WS-K410-STATUS                  
              MOVE     WS-K410-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5000-GET-REJ-REASON.                                                     
      *=============================================================            
                                                                                
           MOVE F640-NUM-REJECT            TO   K970-NUM-CODE.                  
           PERFORM 5010-AG07K970-READ      THRU 5010-EXIT.                      
                                                                                
           IF WS-K970-FND = 'Y'                                                 
              STRING K970-NUM-CODE '-' K970-TXT-EMSG                            
                     DELIMITED BY SIZE     INTO L42PA-NUM-DESC                  
           ELSE                                                                 
              MOVE F640-NUM-REJECT         TO   L42PA-NUM-DESC                  
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5010-AG07K970-READ.                                                      
      *=============================================================            
                                                                                
           READ AG07K970.                                                       
                                                                                
           EVALUATE WS-K970-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K970-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K970-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ AG07K970 ERROR '    WS-K970-STATUS                  
              MOVE     WS-K970-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5100-PRINT-REJ.                                                          
      *=============================================================            
                                                                                
           IF WS-CNT-LINE-A > 55                                                
              PERFORM 5110-PRINT-REJ-HDR   THRU 5110-EXIT                       
           END-IF.                                                              
                                                                                
           ADD  1                          TO   WS-CNT-SNO-A                    
                                                WS-CNT-LINE-A.                  
           MOVE WS-CNT-SNO-A               TO   L42PA-SNO.                      
           MOVE WS-NUM-REGN                TO   L42PA-NUM-REGN.                 
           MOVE F640-NUM-VR                TO   L42PA-NUM-OLD-VR.               
           MOVE WS-DTE-SUB                 TO   L42PA-DTE-SUBMITTED.            
                                                                                
           WRITE P13L42PA-REC              FROM L42PA-DETAIL.                   
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5110-PRINT-REJ-HDR.                                                      
      *=============================================================            
                                                                                
           ADD 1                           TO   WS-CNT-PAGE-A.                  
           MOVE WS-CNT-PAGE-A              TO   L42PA-CNT-PAGE.                 
           MOVE WS-DTE-CURR-10             TO   L42PA-DTE-CURRENT               
                                                L42PA-DTE-CURRENT2.             
                                                                                
           WRITE P13L42PA-REC FROM L42PA-HEADER1 AFTER PAGE.                    
           WRITE P13L42PA-REC FROM L42PA-HEADER2.                               
           WRITE P13L42PA-REC FROM L42PA-HEADER3 AFTER 2.                       
           WRITE P13L42PA-REC FROM L42PA-HEADER4.                               
                                                                                
           MOVE SPACES                     TO   P13L42PA-REC.                   
           WRITE P13L42PA-REC.                                                  
                                                                                
           MOVE 6                          TO   WS-CNT-LINE-A.                  
                                                                                
       5110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*------- BP13C42P CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF AG07F640 READ      : ' WS-CNT-F640-READ.          
           DISPLAY ' 2. NO OF AG07F640 ACCEPTED  : ' WS-CNT-F640-ACC.           
           DISPLAY ' 3. NO OF AG07F640 REJECTED  : ' WS-CNT-F640-REJ.           
           DISPLAY ' 4. NO OF BP13K300 UPDATED   : ' WS-CNT-K300-UPD.           
           DISPLAY ' 5. NO OF BP13K410 UPDATED   : ' WS-CNT-K410-UPD.           
           DISPLAY ' '.                                                         
                                                                                
           CLOSE AG07F640                                                       
                 AG07K970                                                       
                 BP13K300                                                       
                 BP13K410                                                       
                 P13L42PA.                                                      
                                                                                
           IF WS-K300-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K300 ERROR ' WS-K300-STATUS                  
              MOVE     WS-K300-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K410-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K410 ERROR ' WS-K410-STATUS                  
              MOVE     WS-K410-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K970-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K970 ERROR ' WS-K970-STATUS                  
              MOVE     WS-K970-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
