      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C40D.                                                 
       AUTHOR.        DEEPIKA NALLAM.                                           
       DATE-WRITTEN.  JULY 07,2019.                                             
      *===============================================================*         
      * OBJECTIVE  :  TO UPDATE THE K40I FILE                         *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :  1. BP13K410                                     *         
      * IN-OUT FILE:  1. BP13K40I                                     *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP137827 15/07/2019 DN8  NEW PROGRAM.                         *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13K40I  ASSIGN        TO BP13K40I                          
                             ACCESS MODE   IS SEQUENTIAL                        
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K40I-NUM-REGN                     
                             FILE STATUS   IS WS-K40I-STATUS.                   
                                                                                
           SELECT  BP13K410  ASSIGN        TO BP13K410                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K410-NUM-REGN                     
                             FILE STATUS   IS WS-K410-STATUS.                   
                                                                                
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13K40I                                                            
            RECORD CONTAINS 100  CHARACTERS.                                    
       COPY BP13K40I.                                                           
                                                                                
       FD   BP13K410                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K410.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-K40I-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K410-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-EOF-K40I             PIC X(01) VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-K40I-READ        PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-K410-FND         PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-K410-NOT         PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-K40I-UPD         PIC 9(7)  VALUE ZEROES.                  
                                                                                
       01 WS-CURR-DATE.                                                         
          05  WS-CURR-CCYY                 PIC 9(04).                           
          05  WS-CURR-MM                   PIC 9(02).                           
          05  WS-CURR-DD                   PIC 9(02).                           
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13K40I      THRU 2000-EXIT                       
             UNTIL WS-EOF-K40I = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13K410                                                 
                I-O    BP13K40I.                                                
                                                                                
                                                                                
           IF WS-K40I-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K40I - ERROR OPENING : ' WS-K40I-STATUS              
              MOVE WS-K40I-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K410-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K410 - ERROR OPENING : ' WS-K410-STATUS              
              MOVE WS-K410-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13K40I.                                                      
      ******************************************************************        
                                                                                
           READ BP13K40I           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-K40I                             
                GO                 TO   2000-EXIT.                              
           ADD  1                  TO WS-CNT-K40I-READ.                         
           PERFORM 3000-READ-BP13K410         THRU 3000-EXIT.                   
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       3000-READ-BP13K410.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                   TO BP13K410-REC.                       
           INITIALIZE                       BP13K410-REC.                       
                                                                                
           MOVE K40I-NUM-REGN            TO K410-NUM-REGN.                      
                                                                                
           READ BP13K410.                                                       
                                                                                
           EVALUATE WS-K410-STATUS                                              
               WHEN 00                                                          
                   ADD  1                   TO WS-CNT-K410-FND                  
                   MOVE K410-DTE-KEY-ISSUE  TO K40I-DTE-KEY-ISSUE               
                   MOVE K410-NUM-LOAN-TAG   TO K40I-NUM-LOAN-TAG                
                   MOVE K410-AMT-LOAN       TO K40I-AMT-LOAN                    
                                                                                
                   REWRITE BP13K40I-REC                                         
                                                                                
                    EVALUATE WS-K40I-STATUS                                     
                       WHEN 00                                                  
                        ADD  1               TO WS-CNT-K40I-UPD                 
                                                                                
                       WHEN OTHER                                               
                         DISPLAY 'BP13K40I WRITE ERROR' WS-K40I-STATUS          
                         DISPLAY 'K40I KEY: ' K40I-KEY-FLD                      
                         MOVE WS-K40I-STATUS  TO RETURN-CODE                    
                         PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT              
                    END-EVALUATE                                                
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD  1               TO WS-CNT-K410-NOT                     
                    MOVE SPACES          TO K410-NUM-REGN                       
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K410 READ ERROR ' WS-K410-STATUS               
                    DISPLAY 'K410 KEY: ' K410-KEY-FLD                           
                    MOVE WS-K410-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C40D'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13K40I RECORDS READ............. : '            
                    WS-CNT-K40I-READ.                                           
           DISPLAY '(2) NO OF BP13K410 RECORDS PRESENT.......... : '            
                    WS-CNT-K410-FND.                                            
           DISPLAY '(3) NO OF BP13K410 RECORDS NOT FOUND........ : '            
                    WS-CNT-K410-NOT.                                            
           DISPLAY '(4) NO OF BP13K40I RECORDS UPDATED.......... : '            
                    WS-CNT-K40I-UPD.                                            
                                                                                
           CLOSE    BP13K410                                                    
                    BP13K40I.                                                   
                                                                                
           IF WS-K40I-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K40I - ERROR CLOSING : ' WS-K40I-STATUS              
              MOVE WS-K40I-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K410-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K410 - ERROR CLOSING : ' WS-K410-STATUS              
              MOVE WS-K410-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
