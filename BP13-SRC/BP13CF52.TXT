       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF52.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  09/02/2015.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVES   : TO OUTPUT REVISE SEQUENCE OF SHORTLISTING  *            
      *                 FOR SHORTFALL                              *            
      *                                                            *            
      *  INPUT FILE   : BP13F740 - FULL FILE                       *            
      *  OUTPUT FILES : BP13F74A - PPS                             *            
      *                 BP13F74B - MCPS (FT) - WITH                *            
      *                 BP13F74C - MCPS (FT) - NEAR                *            
      *                 BP13F74D - FT                              *            
      *                 BP13F74E - AST                             *            
      *                 BP13F74F - MCPS (ST) - WITH                *            
      *                 BP13F74G - MCPS (ST) - NEAR                *            
      *                 BP13F74H - ST                              *            
      *                 BP13F74I - FTS                             *            
      *                 BP13F74J - SA                              *            
      *                 BP13F74K - ELDERLY - SPS                   *            
      *                 BP13F74L - ELDERLY - NONSPS                *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG-REF# BY   DATE     DESCRIPTION                         *            
      * -------- ---- -------- ----------------------------------- *            
      * BP135625 SMR2 09022015 NEW PROGRAM                         *            
      * BP136096 SMR2 09122015 CATER FOR ELDERLY (SPS & NONSPS)    *            
      * BP139670 AM25 02102023 CATER FOR FT(PMC)                   *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
           SELECT BP13F74A ASSIGN TO BP13F74A.                                  
           SELECT BP13F74B ASSIGN TO BP13F74B.                                  
           SELECT BP13F74C ASSIGN TO BP13F74C.                                  
           SELECT BP13F74D ASSIGN TO BP13F74D.                                  
           SELECT BP13F74E ASSIGN TO BP13F74E.                                  
           SELECT BP13F74F ASSIGN TO BP13F74F.                                  
           SELECT BP13F74G ASSIGN TO BP13F74G.                                  
           SELECT BP13F74H ASSIGN TO BP13F74H.                                  
           SELECT BP13F74I ASSIGN TO BP13F74I.                                  
           SELECT BP13F74J ASSIGN TO BP13F74J.                                  
           SELECT BP13F74K ASSIGN TO BP13F74K.                                  
           SELECT BP13F74L ASSIGN TO BP13F74L.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F740                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F740.                                                           
                                                                                
       FD  BP13F74A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       01  BP13F74A-REC                    PIC X(500).                          
                                                                                
       FD  BP13F74B                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       01  BP13F74B-REC                    PIC X(500).                          
                                                                                
       FD  BP13F74C                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       01  BP13F74C-REC                    PIC X(500).                          
                                                                                
       FD  BP13F74D                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       01  BP13F74D-REC                    PIC X(500).                          
                                                                                
       FD  BP13F74E                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       01  BP13F74E-REC                    PIC X(500).                          
                                                                                
       FD  BP13F74F                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       01  BP13F74F-REC                    PIC X(500).                          
                                                                                
       FD  BP13F74G                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       01  BP13F74G-REC                    PIC X(500).                          
                                                                                
       FD  BP13F74H                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       01  BP13F74H-REC                    PIC X(500).                          
                                                                                
       FD  BP13F74I                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       01  BP13F74I-REC                    PIC X(500).                          
                                                                                
       FD  BP13F74J                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       01  BP13F74J-REC                    PIC X(500).                          
                                                                                
       FD  BP13F74K                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       01  BP13F74K-REC                    PIC X(500).                          
                                                                                
       FD  BP13F74L                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       01  BP13F74L-REC                    PIC X(500).                          
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-COUNTERS.                                                         
           05  WS-F740-READ                PIC 9(06)  VALUE ZEROES.             
           05  WS-F74A-WRITE               PIC 9(06)  VALUE ZEROES.             
           05  WS-F74B-WRITE               PIC 9(06)  VALUE ZEROES.             
           05  WS-F74C-WRITE               PIC 9(06)  VALUE ZEROES.             
           05  WS-F74D-WRITE               PIC 9(06)  VALUE ZEROES.             
           05  WS-F74E-WRITE               PIC 9(06)  VALUE ZEROES.             
           05  WS-F74F-WRITE               PIC 9(06)  VALUE ZEROES.             
           05  WS-F74G-WRITE               PIC 9(06)  VALUE ZEROES.             
           05  WS-F74H-WRITE               PIC 9(06)  VALUE ZEROES.             
           05  WS-F74I-WRITE               PIC 9(06)  VALUE ZEROES.             
           05  WS-F74J-WRITE               PIC 9(06)  VALUE ZEROES.             
           05  WS-F74K-WRITE               PIC 9(06)  VALUE ZEROES.             
           05  WS-F74L-WRITE               PIC 9(06)  VALUE ZEROES.             
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F740-EOF                 PIC X(01)  VALUE 'N'.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           PERFORM 1000-INIT-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F740      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
             UNTIL WS-F740-EOF = 'Y'.                                           
           PERFORM 4000-CLOSE-ROUTINE      THRU 4000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-INIT-ROUTINE.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT BP13F740                                                  
               OUTPUT BP13F74A                                                  
                      BP13F74B                                                  
                      BP13F74C                                                  
                      BP13F74D                                                  
                      BP13F74E                                                  
                      BP13F74F                                                  
                      BP13F74G                                                  
                      BP13F74H                                                  
                      BP13F74I                                                  
                      BP13F74J                                                  
                      BP13F74K                                                  
                      BP13F74L.                                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-BP13F740.                                                      
      *-------------------------------------------------------------            
           READ BP13F740                                                        
             AT END                                                             
                MOVE 'Y'                   TO WS-F740-EOF                       
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                           TO WS-F740-READ.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           IF F740-CDE-FLAT-TYPE(1:1) = '1'                                     
              WRITE BP13F74J-REC           FROM BP13F740-REC                    
              ADD 1                        TO WS-F74J-WRITE                     
           ELSE                                                                 
              IF F740-NUM-ELDERLY = 'F' OR 'S'                                  
                 IF ((F740-NUM-APPS NOT = SPACES AND LOW-VALUES) AND            
                     (F740-NUM-APPS-ELIG-TAG = '1' OR 'T')) OR                  
                    ((F740-NUM-MCPS = 'P' OR 'C' OR 'M') AND                    
                     (F740-CDE-RESIDENT-HA1 = '1' OR 'A' OR 'T'))               
                    WRITE BP13F74K-REC        FROM BP13F740-REC                 
                    ADD 1                     TO WS-F74K-WRITE                  
                 ELSE                                                           
                    WRITE BP13F74L-REC        FROM BP13F740-REC                 
                    ADD 1                     TO WS-F74L-WRITE                  
                 END-IF                                                         
              ELSE                                                              
                 IF F740-CDE-ALLOC-SCH = 'FTS'                                  
                    WRITE BP13F74I-REC        FROM BP13F740-REC                 
                    ADD 1                     TO WS-F74I-WRITE                  
                 ELSE                                                           
                    IF F740-CDE-BALLOT-HOUSEHOLD = 'G'                          
                       IF F740-NUM-AST = 'Y'                                    
                          WRITE BP13F74E-REC  FROM BP13F740-REC                 
                          ADD 1               TO WS-F74E-WRITE                  
                       ELSE                                                     
                          IF F740-CDE-RESIDENT-HA1 = '1' OR 'A' OR 'T'          
                             IF F740-NUM-MCPS = 'M' OR 'B'                      
                                WRITE BP13F74F-REC  FROM BP13F740-REC           
                                ADD 1               TO WS-F74F-WRITE            
                             ELSE                                               
                                IF F740-NUM-MCPS = 'C' OR 'P'                   
                                   WRITE BP13F74G-REC  FROM BP13F740-REC        
                                   ADD 1               TO WS-F74G-WRITE         
                                END-IF                                          
                             END-IF                                             
                          ELSE                                                  
                             WRITE BP13F74H-REC  FROM BP13F740-REC              
                             ADD 1               TO WS-F74H-WRITE               
                          END-IF                                                
                       END-IF                                                   
                    ELSE                                                        
                       IF F740-NUM-PPS-ELIG-TAG = 'Y' OR 'E' OR 'F'             
                          WRITE BP13F74A-REC  FROM BP13F740-REC                 
                          ADD 1               TO WS-F74A-WRITE                  
                       ELSE                                                     
                          IF F740-CDE-RESIDENT-HA1 = '1' OR 'A' OR 'T'          
                             IF F740-NUM-MCPS = 'M' OR 'B'                      
                                WRITE BP13F74B-REC  FROM BP13F740-REC           
                                ADD 1               TO WS-F74B-WRITE            
                             ELSE                                               
                                IF F740-NUM-MCPS = 'C' OR 'P'                   
                                   WRITE BP13F74C-REC  FROM BP13F740-REC        
                                   ADD 1               TO WS-F74C-WRITE         
                                END-IF                                          
                             END-IF                                             
                          ELSE                                                  
                             WRITE BP13F74D-REC  FROM BP13F740-REC              
                             ADD 1               TO WS-F74D-WRITE               
                          END-IF                                                
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F740      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY '********** SUMMARY REPORT FOR BP13CF52 **********'.         
           DISPLAY ' NO OF REC READ FROM BP13F740           = '                 
                     WS-F740-READ.                                              
           DISPLAY ' NO OF REC WRITTEN IN F74A(PPS)         = '                 
                     WS-F74A-WRITE.                                             
           DISPLAY ' NO OF REC WRITTEN IN F74B(MCP-FT-WITH) = '                 
                     WS-F74B-WRITE.                                             
           DISPLAY ' NO OF REC WRITTEN IN F74C(MCP-FT-NEAR) = '                 
                     WS-F74C-WRITE.                                             
           DISPLAY ' NO OF REC WRITTEN IN F74D(FT)          = '                 
                     WS-F74D-WRITE.                                             
           DISPLAY ' NO OF REC WRITTEN IN F74E(AST)         = '                 
                     WS-F74E-WRITE.                                             
           DISPLAY ' NO OF REC WRITTEN IN F74F(MCP-ST-WITH) = '                 
                     WS-F74F-WRITE.                                             
           DISPLAY ' NO OF REC WRITTEN IN F74G(MCP-ST-NEAR) = '                 
                     WS-F74G-WRITE.                                             
           DISPLAY ' NO OF REC WRITTEN IN F74H(ST)          = '                 
                     WS-F74H-WRITE.                                             
           DISPLAY ' NO OF REC WRITTEN IN F74I(FTS)         = '                 
                     WS-F74I-WRITE.                                             
           DISPLAY ' NO OF REC WRITTEN IN F74J(SA)          = '                 
                     WS-F74J-WRITE.                                             
           DISPLAY ' NO OF REC WRITTEN IN F74K(ELD-SPS)     = '                 
                     WS-F74K-WRITE.                                             
           DISPLAY ' NO OF REC WRITTEN IN F74L(ELD-NONSPS)  = '                 
                     WS-F74L-WRITE.                                             
                                                                                
           CLOSE  BP13F740                                                      
                  BP13F74A                                                      
                  BP13F74B                                                      
                  BP13F74C                                                      
                  BP13F74D                                                      
                  BP13F74E                                                      
                  BP13F74F                                                      
                  BP13F74G                                                      
                  BP13F74H                                                      
                  BP13F74I                                                      
                  BP13F74J                                                      
                  BP13F74K                                                      
                  BP13F74L.                                                     
                                                                                
           STOP RUN.                                                            
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
