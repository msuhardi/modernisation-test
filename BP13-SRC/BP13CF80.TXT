       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF80.                                                 
      *AUTHOR.        ZARITA DANE SARMIENTO.                                    
      *DATE-WRITTEN.  29/05/12.                                                 
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *  OBJECTIVE   :   TO SCHEDULE BOOKING APPOINTMENT FOR THE   *            
      *                  SELECTED NT/FT BASED ON THE APPOINTMENT   *            
      *                  WORK PLAN UPDATED BY USER.                *            
      *                                                            *            
      *  INPUT FILE   :   1.  BP13F800 - BOOKING APPMT FILE (SEQ)  *            
      *                   2.  BP13K452 -                           *            
      *                                                            *            
      *  I-O   FILES  :   1.  BP13K454 - BOOKING APPOINTMENT FILE  *            
      * ---------------------------------------------------------- *            
      * CHG-NO   BY   DATE      DESCRIPTION                        *            
      * -------- ---  --------  ------------------------------     *            
      * BP134646 ZDD1 29/05/12  NEW PROGRAM                        *            
      * BP134646 ZDD1 10/01/13  ADD NEW CONDITION FOR CASES NOT    *            
      *                         FOR AUTO SCHEDULING                *            
      * BP134646 ZDD1 15/01/13  CORRECT EXIT ON 710 PARA           *            
      * BP134868 ZDD1 07/03/13  CHANGE 21 DAYS TO 11 DAYS          *            
      *                         ADD REPORT                         *            
      * BP134809 ZDD1 07/06/13  CATER FOR PM SCHED                 *            
      * BP136430 KSJ3 19/11/16  EXPAND WORKPLAN TO 1000            *            
      * BP137196 PCL4 07/05/18  TO CATER NEW AUTO SCHEDULING - SOCA*            
      * BP137196 PCL4 20/06/18  TO ADD CHECK FOR AUTO SOCA         *            
      * BP138047 KAC1 25/11/19  RECOMPILE FOR BP27 COPYBOOK UPDATE *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT BP13F800  ASSIGN       TO BP13F800.                           
                                                                                
           SELECT BP13K454  ASSIGN        TO BP13K454                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K454-KEY-FLD                       
                            ALTERNATE KEY IS K454-ALT1-KEY                      
                            FILE STATUS   IS WS-K454-STATUS.                    
                                                                                
           SELECT BP13K452  ASSIGN       TO BP13K452                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K452-KEY-FLD                        
                            FILE STATUS  IS WS-K452-STATUS.                     
                                                                                
           SELECT BP13K022  ASSIGN        TO BP13K022                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K022-KEY-FLD                       
                            ALTERNATE KEY IS K022-NUM-REGN                      
                            FILE STATUS   IS WS-K022-STATUS.                    
                                                                                
           SELECT BP27K800  ASSIGN        TO BP27K800                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-HLA                       
                                                   OF BP27K800-MASTER           
                            FILE STATUS   IS WS-BP27-K800-STATUS.               
                                                                                
           SELECT BM06K110  ASSIGN       TO BM06K110                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K110-KEY-FLD                        
                            FILE STATUS  IS WS-K110-STATUS.                     
                                                                                
           SELECT BM06K100  ASSIGN       TO BM06K100                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K100-KEY-FLD                        
                            FILE STATUS  IS WS-K100-STATUS.                     
                                                                                
           SELECT BP13LF80 ASSIGN TO BP13LF80.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F800                                                            
            RECORDING  MODE IS  F                                               
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K452                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K452.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP27K800                                                            
            RECORD CONTAINS 1200 CHARACTERS.                                    
       COPY BP27K800.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K100.                                                           
                                                                                
       FD   BP13K454                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K454.                                                           
                                                                                
       FD  BP13LF80                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  BP13LF80-REC                    PIC X(132).                          
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-INDICATORS.                                                       
           05  WS-K022-STATUS               PIC 99    VALUE  00.                
           05  WS-BP27-K800-STATUS          PIC 99    VALUE  00.                
           05  WS-K452-STATUS               PIC 99    VALUE  00.                
           05  WS-K454-STATUS               PIC 99    VALUE  00.                
           05  WS-K110-STATUS               PIC 99    VALUE  00.                
           05  WS-K100-STATUS               PIC 99    VALUE  00.                
           05  WS-AUTO-SCHED                PIC X     VALUE 'N'.                
           05  WS-EOF-F800                  PIC X     VALUE 'N'.                
           05  WS-EOF-K454                  PIC X     VALUE 'N'.                
           05  WS-EOF-K452                  PIC X     VALUE 'N'.                
           05  WS-K454A-FND                 PIC X     VALUE 'N'.                
           05  WS-K454B-FND                 PIC X     VALUE 'N'.                
           05  WS-FND-K454-IND              PIC X     VALUE 'Y'.                
           05  WS-FND-K452-IND              PIC X     VALUE 'N'.                
           05  WS-SCHED-FND                 PIC X     VALUE 'N'.                
           05  WS-CNT-CASES                 PIC 9(2)  VALUE 0.                  
           05  WS-CNT-PAGE                  PIC 9(5)  VALUE 0.                  
           05  WS-CNT-LINE                  PIC 9(2)  VALUE 51.                 
           05  WS-SCHED-DATE                PIC X(8)  VALUE SPACES.             
           05  WS-SCHED-TIME                PIC X(4)  VALUE SPACES.             
           05  WS-TOT-SCHED                 PIC 9(3)  VALUE 0.                  
           05  WS-TOT-VAC                   PIC 9(3)  VALUE 0.                  
           05  WS-TEMP-MAX                  PIC 9(2)  VALUE 0.                  
           05  WS-TEMP-REM                  PIC 9(2)  VALUE 0.                  
                                                                                
       01  WS-COUNTERS-POINTERS.                                                
           05  WS-CNT-READ-F800             PIC 9(6)  VALUE ZEROS.              
           05  WS-CNT-WRTE-K454             PIC 9(6)  VALUE ZEROS.              
           05  WS-CNT-REWRTE-K452           PIC 9(6)  VALUE ZEROS.              
           05  WS-CNT-REWRTE-K454           PIC 9(6)  VALUE ZEROS.              
           05  WS-CNT-CREATE-REC            PIC 9(6)  VALUE ZEROS.              
           05  WS-ALREADY-SCHED             PIC 9(6)  VALUE ZEROS.              
           05  WS-LOANTAG-NOT-AH            PIC 9(6)  VALUE ZEROS.              
           05  WS-AHGSTAT-NOT-A             PIC 9(6)  VALUE ZEROS.              
           05  WS-BTOKIV                    PIC 9(6)  VALUE ZEROS.              
           05  WS-PROVTAG                   PIC 9(6)  VALUE ZEROS.              
           05  WS-1ST-SOCA                  PIC 9(6)  VALUE ZEROS.              
           05  WS-PAIRED                    PIC 9(6)  VALUE ZEROS.              
           05  WS-CTR                       PIC 9(2)  VALUE ZEROS.              
           05  WS-TEMP-TME-SLOT             PIC 9(4)  VALUE ZEROS.              
           05  WS-DTE-AO                    PIC 9(4)  VALUE ZEROS.              
           05  WS-KEY-AVAIL                 PIC 9(4)  VALUE ZEROS.              
           05  WS-SER-CASE                  PIC 9(4)  VALUE ZEROS.              
           05  WS-NO-CRM                    PIC 9(6)  VALUE ZEROS.              
           05  WS-NO-HLE                    PIC 9(6)  VALUE ZEROS.              
           05  WS-APPT-HLE                  PIC 9(6)  VALUE ZEROS.              
           05  WS-EXP-HLE                   PIC 9(6)  VALUE ZEROS.              
                                                                                
       01  WS-DTE-HLA-EXPIRY                PIC X(08) VALUE SPACES.             
       01  WS-COMM-APPT-DATE                PIC X(08) VALUE SPACES.             
       01  WS-ADD-DAYS                      PIC 9(02) VALUE ZEROES.             
       01  WS-RUN-DATE                      PIC 9(08) VALUE ZEROES.             
                                                                                
       01  WS-NUM-FIELD.                                                00840001
           05  WS-REM                   PIC 9(3).                       00850001
           05  WS-YEAR                  PIC 9(4).                       00860001
                                                                        00880001
       01  WS-CURR-DATE.                                                00840001
           05  WS-CURRENT-DATE.                                                 
               10  WS-CURR-CC           PIC 99     VALUE 00.                    
               10  WS-CURR-DATE.                                                
                   15  WS-CURR-YY       PIC 99     VALUE 00.                    
                   15  WS-CURR-MM       PIC 99     VALUE 00.                    
                   15  WS-CURR-DD       PIC 99     VALUE 00.                    
                                                                                
       01  DATE-RECORD.                                                 00890001
           05  DATE-REC                   PIC X(8).                     00900001
           05  DATE-REC1 REDEFINES DATE-REC.                            00910001
               10 CAL-DATE.                                             00920001
                   15 CAL-YEAR            PIC 9(4).                     00930001
                       88 VALID-YEAR          VALUE 1900 THRU 2100.     00940001
                   15 CAL-MONTH           PIC 9(2).                     00950001
                       88 31-DAY-MONTH        VALUE 01, 03, 05, 07, 08, 00960001
                                                        10, 12.         00970001
                       88 30-DAY-MONTH        VALUE 04, 06, 09, 11.     00980001
                       88 28-29-DAY-MONTH     VALUE 02.                 00990001
                       88 VALID-MONTH         VALUE 01 THRU 12.         01000001
                   15 CAL-DAY             PIC 9(2).                     01010001
                       88 VALID-DAY           VALUE 01 THRU 31.         01020001
                                                                        01030001
      *  REPORT LAYOUT FOR CONTROL LISTING (BP13LF80).                          
      *--------------------------------------------------------------           
       01  LF80-HEADER1.                                                        
           05  FILLER              PIC X(8)      VALUE 'BP13LF80'.              
           05  FILLER              PIC X(5)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
                         'S Y S T E M   O F   C O M M I T M E N T'.             
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE : '.               
           05  LF80-DATE           PIC X(10).                                   
           05  FILLER              PIC X(9)      VALUE '  PAGE : '.             
           05  LF80-PAGENO         PIC ZZZZ9 .                                  
                                                                                
       01  LF80-HEADER2.                                                        
           05  FILLER              PIC X(40)     VALUE SPACES .                 
           05  FILLER              PIC X(53)     VALUE                          
                '       CONTROL LISTING FOR SCHEDULED CASES    '.               
           05  FILLER              PIC X(40)     VALUE SPACES.                  
                                                                                
       01  LF80-HEADER3.                                                        
           05  FILLER              PIC X(03)     VALUE SPACES .                 
           05  FILLER              PIC X(05)     VALUE 'S/NO '.                 
           05  FILLER              PIC X(03)     VALUE SPACES .                 
           05  FILLER              PIC X(20)     VALUE 'DATE     '.             
           05  FILLER              PIC X(03)     VALUE SPACES .                 
           05  FILLER              PIC X(10)     VALUE 'TIME     '.             
           05  FILLER              PIC X(03)     VALUE SPACES .                 
           05  FILLER              PIC X(20)     VALUE                          
                '# OF NEW SCHED CASES'.                                         
           05  FILLER              PIC X(03)     VALUE SPACES .                 
           05  FILLER              PIC X(16)     VALUE                          
                '# OF CASES SCHED'.                                             
           05  FILLER              PIC X(03)     VALUE SPACES .                 
           05  FILLER              PIC X(13)     VALUE                          
                '# OF MAX CASE'.                                                
                                                                                
       01  LF80-DETAILS.                                                        
           05  FILLER              PIC X(03)     VALUE SPACES .                 
           05  LF80-SNO            PIC ZZZZ9.                                   
           05  FILLER              PIC X(03)     VALUE SPACES .                 
           05  LF80-SCHED-DATE     PIC X(20)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE SPACES .                 
           05  LF80-SCHED-TIME     PIC X(10)     VALUE SPACES .                 
           05  FILLER              PIC X(03)     VALUE SPACES .                 
           05  LF80-NO-SCHED-CASES PIC ZZZ9.                                    
           05  FILLER              PIC X(19)     VALUE SPACES .                 
           05  LF80-NO-VAC-SLOT    PIC ZZZ9.                                    
           05  FILLER              PIC X(19)     VALUE SPACES .                 
           05  LF80-NO-MAX-CASE    PIC ZZZ9.                                    
                                                                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
                                                                                
           PERFORM  100-OPEN-ROUTINE         THRU  100-EXIT.                    
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)     TO WS-RUN-DATE.                  
           PERFORM  400-READ-BP13K452        THRU  400-EXIT.                    
                                                                                
           IF K452-CDE-DAY-OF-WEEK = '1' OR '2'                                 
              MOVE 10                          TO WS-ADD-DAYS           02610036
           ELSE                                                                 
              MOVE 12                          TO WS-ADD-DAYS           02610036
           END-IF.                                                              
                                                                                
           PERFORM  200-READ-BP13F800        THRU  200-EXIT.                    
           PERFORM  500-PROCESS-RECORD       THRU  500-EXIT                     
              UNTIL (WS-EOF-F800 = 'Y').                                        
                                                                                
           IF LF80-SNO NOT = ZEROES AND SPACES                                  
              MOVE SPACES        TO LF80-DETAILS                                
              ADD 1              TO WS-CNT-CASES                                
              MOVE WS-CNT-CASES  TO LF80-SNO                                    
                                                                                
              PERFORM 650-MOVE-DETAILS   THRU 650-EXIT                          
              WRITE BP13LF80-REC         FROM LF80-DETAILS                      
           END-IF                                                               
                                                                                
           PERFORM  800-DISPLAY-STATISTICS   THRU  800-EXIT.                    
           PERFORM  900-CLOSE-ROUTINE        THRU  900-EXIT.                    
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       100-OPEN-ROUTINE.                                                        
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BM06K110                                                 
                       BM06K100                                                 
                       BP13K022                                                 
                       BP27K800                                                 
                I-O    BP13K452                                                 
                       BP13K454                                                 
                OUTPUT BP13LF80.                                                
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K022, STATUS = ' WS-K022-STATUS        
              MOVE     WS-K022-STATUS TO RETURN-CODE                            
              PERFORM  900-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
           IF WS-BP27-K800-STATUS NOT = 00 AND 97                               
              DISPLAY 'ERROR OPENING BP13K022, STATUS = '                       
                                WS-BP27-K800-STATUS                             
              MOVE WS-BP27-K800-STATUS TO RETURN-CODE                           
              PERFORM  900-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
           IF WS-K454-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K454, STATUS = ' WS-K454-STATUS        
              MOVE     WS-K454-STATUS TO RETURN-CODE                            
              PERFORM  900-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
           IF WS-K452-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K452, STATUS = ' WS-K452-STATUS        
              MOVE     WS-K452-STATUS TO RETURN-CODE                            
              PERFORM  900-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BM06K110, STATUS = ' WS-K110-STATUS        
              MOVE     WS-K110-STATUS TO RETURN-CODE                            
              PERFORM  900-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BM06K100, STATUS = ' WS-K100-STATUS        
              MOVE     WS-K100-STATUS TO RETURN-CODE                            
              PERFORM  900-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    READ SEQUENTIAL FILE (BP13F800) AT END SET EOF-IND TO 'Y'   *        
      ******************************************************************        
                                                                                
      *-------------------------------------------------------------            
       200-READ-BP13F800.                                                       
      *-------------------------------------------------------------            
                                                                                
           READ BP13F800                                                        
                AT END MOVE 'Y' TO WS-EOF-F800                                  
                GO TO  200-EXIT.                                                
                                                                                
           ADD  1  TO  WS-CNT-READ-F800.                                        
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    START BROWSE VSAM FILE BP13K454                             *        
      ******************************************************************        
                                                                                
      *-------------------------------------------------------------            
       300-READ-BP13K454.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES               TO WS-FND-K454-IND.                        
                                                                                
           MOVE SPACES               TO BP13K454-REC.                           
           INITIALIZE                   BP13K454-REC.                           
                                                                                
           MOVE 'SA'                 TO K454-CDE-APPT-TYPE.                     
           MOVE F800-NUM-REGN        TO K454-NUM-REGN.                          
                                                                                
           READ BP13K454 KEY IS K454-ALT1-KEY.                                  
                                                                                
           IF WS-K454-STATUS = 00 OR 02                                         
              MOVE    'Y'   TO WS-FND-K454-IND                                  
           ELSE                                                                 
           IF WS-K454-STATUS = 23                                               
              MOVE    'N'   TO WS-FND-K454-IND                                  
           ELSE                                                                 
           IF WS-K454-STATUS NOT = 00 AND 23 AND 02                             
              DISPLAY 'ERROR START BRWSE BP13K454, STAT ' WS-K454-STATUS        
              MOVE     WS-K454-STATUS  TO RETURN-CODE                           
              PERFORM  900-CLOSE-ROUTINE.                                       
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       400-READ-BP13K452.                                                       
      *--------------------------------------------------------------*          
                                                                                
           MOVE SPACES            TO BP13K452-REC.                              
           INITIALIZE                BP13K452-REC.                              
                                                                                
           MOVE WS-RUN-DATE       TO K452-KEY-FLD.                              
                                                                                
           READ BP13K452.                                                       
                                                                                
           EVALUATE WS-K452-STATUS                                              
               WHEN 00                                                          
                    CONTINUE                                                    
                                                                                
               WHEN 23                                                          
                    MOVE SPACES   TO BP13K452-REC                               
                    INITIALIZE       BP13K452-REC                               
                                                                                
               WHEN OTHER                                                       
                 DISPLAY 'ERROR READING BP13K452, STAT '                        
                 WS-K452-STATUS                                                 
                 MOVE     WS-K452-STATUS  TO RETURN-CODE                        
                 PERFORM  900-CLOSE-ROUTINE THRU 900-EXIT                       
                                                                                
           END-EVALUATE.                                                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *    PROCESS THE READ RECORD FROM BP13F800 FILE                  *        
      ******************************************************************        
      *-------------------------------------------------------------            
       500-PROCESS-RECORD.                                                      
      *-------------------------------------------------------------            
                                                                                
           PERFORM 501-VALIDATE-RECORD       THRU 501-EXIT.                     
                                                                                
           IF WS-AUTO-SCHED = 'Y'                                               
              PERFORM 502-PROCESS-AUTO-SCHED THRU 502-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM  200-READ-BP13F800        THRU  200-EXIT.                    
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       501-VALIDATE-RECORD.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE 'Y'                            TO WS-AUTO-SCHED.                
                                                                                
           PERFORM 560-READ-BM06K110         THRU 560-EXIT.                     
           PERFORM 570-READ-BM06K100         THRU 570-EXIT.                     
           PERFORM 590-READ-BP13K022         THRU 590-EXIT.                     
                                                                                
           IF F800-DTE-AO-APPT   NOT = SPACES AND LOW-VALUES AND ZEROES         
               ADD 1          TO WS-DTE-AO                                      
               MOVE 'N'       TO WS-AUTO-SCHED                                  
           END-IF.                                                              
                                                                                
           IF K110-DTE-KEY-AVAIL NOT = SPACES AND LOW-VALUES AND ZEROES         
               ADD 1          TO WS-KEY-AVAIL                                   
               MOVE 'N'       TO WS-AUTO-SCHED                                  
           END-IF.                                                              
                                                                                
           IF F800-NUM-ALLO-CAT      = 'SER' OR                                 
              F800-NUM-PORT-ELIG-TAG = 'Y'                                      
               ADD 1          TO WS-SER-CASE                                    
               MOVE 'N'       TO WS-AUTO-SCHED                                  
           END-IF.                                                              
                                                                                
           IF F800-NUM-LOAN-TAG NOT =  'H'                                      
               ADD 1          TO WS-LOANTAG-NOT-AH                              
               MOVE 'N'       TO WS-AUTO-SCHED                                  
           END-IF.                                                              
                                                                                
           IF F800-DTE-AO-APPT = SPACES OR LOW-VALUES                           
              IF K100-NUM-BTO-NEW-CONTRACT = 'Y'                                
                 ADD 1        TO WS-BTOKIV                                      
                 MOVE 'N'     TO WS-AUTO-SCHED                                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F800-CDE-APPT    = 'Z'                                            
                ADD 1          TO  WS-BTOKIV                                    
                MOVE 'N'       TO WS-AUTO-SCHED                                 
           END-IF.                                                              
                                                                                
           IF F800-NUM-PROV-TAG = 'Y'                                           
                ADD 1          TO  WS-PROVTAG                                   
                MOVE 'N'       TO WS-AUTO-SCHED                                 
           END-IF.                                                              
                                                                                
            IF ((F800-NUM-JT-BALLOT-REGN NOT = SPACES AND LOW-VALUES)           
                 AND (F800-NUM-ALLO-CAT = 'BTO'))                               
                ADD 1          TO  WS-PAIRED                                    
                MOVE 'N'       TO WS-AUTO-SCHED                                 
           END-IF.                                                              
                                                                                
           PERFORM  300-READ-BP13K454        THRU  300-EXIT.                    
                                                                                
           IF WS-FND-K454-IND = 'Y'                                             
               ADD 1          TO  WS-ALREADY-SCHED                              
               MOVE 'N'       TO WS-AUTO-SCHED                                  
           ELSE                                                                 
              IF F800-CDE-APPT-TYPE NOT = SPACES AND LOW-VALUES                 
                 ADD 1        TO  WS-ALREADY-SCHED                              
                 MOVE 'N'     TO WS-AUTO-SCHED                                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 503-PROCESS-DATE          THRU 503-EXIT.                     
                                                                                
       501-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       502-PROCESS-AUTO-SCHED.                                                  
      *-------------------------------------------------------------            
                                                                                
           MOVE 'N'                    TO WS-SCHED-FND.                         
           MOVE 'N'                    TO WS-FND-K452-IND.                      
                                                                        02620001
           PERFORM 510-READ-BP13K452 THRU 510-EXIT.                             
                                                                                
      *    IF WS-FND-K452-IND = 'N'                                             
      *       PERFORM 520-GET-NEXT-APPT        THRU 520-EXIT                    
      *    END-IF.                                                              
                                                                                
           IF WS-SCHED-FND = 'Y'                                                
              IF K100-DTE-1ST-SOCA >=  DATE-REC                                 
                  ADD 1        TO  WS-1ST-SOCA                                  
                  DISPLAY ' ------ REGN NUM-' F800-NUM-REGN                     
                  DISPLAY ' 1ST SOCA DATE- ' K100-DTE-1ST-SOCA                  
                  DISPLAY ' DATE APPT IS  -' DATE-REC                           
                  DISPLAY ' ----------------'                                   
              ELSE                                                              
                  PERFORM 600-PROCESS-APPT-DATE-TIME  THRU 600-EXIT             
              END-IF                                                            
                                                                                
           ELSE                                                                 
              DISPLAY 'NO AVAILABLE DATE FOR SCHEDULING - KEY IS '              
                 F800-NUM-REGN ' - ' WS-RUN-DATE                                
           END-IF.                                                              
                                                                                
       502-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       503-PROCESS-DATE.                                                        
      *-------------------------------------------------------------            
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE.                       
                                                                                
           MOVE WS-CURRENT-DATE       TO DATE-REC.                              
           MOVE ZEROS                 TO WS-NUM-FIELD.                  02570036
           ADD  WS-ADD-DAYS           TO CAL-DAY.                       02610036
                                                                        02620001
           PERFORM 710-PROCESS-DATE  THRU 710-EXIT.                     02630001
                                                                                
           PERFORM 591-READ-BP27K800 THRU 591-EXIT.                             
                                                                                
       503-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       510-READ-BP13K452.                                                       
      *--------------------------------------------------------------*          
                                                                                
           MOVE SPACES        TO  WS-SCHED-FND.                                 
           MOVE SPACES        TO  K452-KEY-FLD.                                 
           MOVE DATE-REC      TO  K452-KEY-FLD.                                 
                                                                                
           READ BP13K452.                                                       
                                                                                
           EVALUATE WS-K452-STATUS                                              
               WHEN 00                                                          
                   PERFORM 540-CHECK-AVAIL-SLOT  THRU 540-EXIT                  
                                                                                
                   IF WS-SCHED-FND = 'Y'                                        
                      MOVE 'Y'  TO WS-FND-K452-IND                              
                   END-IF                                                       
                                                                                
               WHEN 23                                                          
                   MOVE 'N'     TO WS-FND-K452-IND                              
                                                                                
               WHEN OTHER                                                       
                 DISPLAY 'ERROR READING BP13K452, STAT '                        
                 WS-K452-STATUS                                                 
                 MOVE     WS-K452-STATUS  TO RETURN-CODE                        
                 PERFORM  900-CLOSE-ROUTINE THRU 900-EXIT                       
                                                                                
           END-EVALUATE.                                                        
                                                                                
       510-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       520-GET-NEXT-APPT.                                                       
      *--------------------------------------------------------------*          
                                                                                
           MOVE SPACES           TO BP13K452-REC.                               
           MOVE DATE-REC         TO K452-KEY-FLD.                               
                                                                                
           START BP13K452 KEY >= K452-KEY-FLD                                   
                                                                                
           EVALUATE WS-K452-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                  PERFORM 530-READ-NEXT-K452  THRU 530-EXIT                     
                       UNTIL WS-SCHED-FND = 'Y' OR WS-EOF-K452 = 'Y'            
              WHEN 10                                                           
              WHEN 23                                                           
                 DISPLAY 'NO AVAILABLE SCHEDULE FOR DATE - '                    
                    DATE-REC '-' F800-NUM-REGN                                  
              WHEN OTHER                                                        
                 DISPLAY 'ERROR START READ BP13K452, STAT '                     
                 WS-K452-STATUS                                                 
                 MOVE     WS-K452-STATUS  TO RETURN-CODE                        
                 PERFORM  900-CLOSE-ROUTINE THRU 900-EXIT                       
                                                                                
           END-EVALUATE.                                                        
                                                                                
       520-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       530-READ-NEXT-K452.                                                      
      *--------------------------------------------------------------*          
                                                                                
           READ BP13K452 NEXT RECORD                                            
                                                                                
           EVALUATE WS-K452-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                  IF K452-DTE-APPT = DATE-REC                                   
                    CONTINUE                                                    
                  ELSE                                                          
                    IF K452-DTE-APPT > DATE-REC                                 
                       PERFORM 540-CHECK-AVAIL-SLOT  THRU 540-EXIT              
                    END-IF                                                      
                  END-IF                                                        
              WHEN 10                                                           
                 MOVE 'Y'     TO WS-EOF-K452                                    
                 DISPLAY 'NO AVAILABLE SCHEDULE FOR DATE - '                    
                    DATE-REC '-' F800-NUM-REGN                                  
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READ NEXT  BP13K452, STAT '                     
                 WS-K452-STATUS                                                 
                 MOVE     WS-K452-STATUS  TO RETURN-CODE                        
                 PERFORM  900-CLOSE-ROUTINE THRU 900-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       530-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       540-CHECK-AVAIL-SLOT.                                                    
      *--------------------------------------------------------------*          
                                                                                
           MOVE ZEROES          TO WS-TEMP-TME-SLOT                             
                                   WS-TEMP-REM.                                 
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 18                 
                                                 OR WS-SCHED-FND = 'Y'          
              IF K452-TME-SLOT(WS-CTR) = '0815' OR '0845' OR                    
                                         '1045'                                 
                                                                                
               IF K452-NUM-MAX-CASE(WS-CTR) NOT = ZEROES AND SPACES             
                                            AND LOW-VALUES                      
                 IF K452-NUM-TOT-SA(WS-CTR) < K452-NUM-MAX-CASE(WS-CTR)         
                    COMPUTE WS-TEMP-REM = K452-NUM-MAX-CASE(WS-CTR) -           
                                          K452-NUM-TOT-TP(WS-CTR)               
                                                                                
                   IF WS-TEMP-REM > K452-NUM-TOT-SA(WS-CTR)                     
                      IF K452-NUM-TOT-SA(WS-CTR) < 10                           
                         MOVE K452-NUM-MAX-CASE(WS-CTR) TO WS-TEMP-MAX          
                         MOVE K452-TME-SLOT(WS-CTR) TO WS-TEMP-TME-SLOT         
                         MOVE K452-DTE-APPT         TO DATE-REC                 
                                                                                
                         ADD 1               TO K452-NUM-TOT-SA(WS-CTR)         
                         MOVE 19             TO WS-CTR                          
                      END-IF                                                    
                   END-IF                                                       
                                                                                
                 END-IF                                                         
               END-IF                                                           
              END-IF                                                            
                                                                                
                                                                                
           END-PERFORM.                                                         
                                                                                
                                                                                
           IF WS-TEMP-TME-SLOT NOT = ZEROES                                     
              MOVE 'Y'          TO WS-SCHED-FND                                 
           ELSE                                                                 
                                                                                
            PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 18                
                                                  OR WS-SCHED-FND = 'Y'         
                                                                                
               IF K452-PM-TME-SLOT(WS-CTR) = '1415' OR '1430' OR '1445'         
                IF K452-PM-MAX-CASE(WS-CTR) NOT = ZEROES AND SPACES             
                                                 AND LOW-VALUES                 
                  IF K452-PM-TOT-SA(WS-CTR) < K452-PM-MAX-CASE(WS-CTR)          
                     COMPUTE WS-TEMP-REM = K452-PM-MAX-CASE(WS-CTR) -           
                                           K452-PM-TOT-TP(WS-CTR)               
                                                                                
                    IF WS-TEMP-REM > K452-PM-TOT-SA(WS-CTR)                     
                       IF K452-PM-TOT-SA(WS-CTR) < 10                           
                          MOVE K452-PM-MAX-CASE(WS-CTR) TO WS-TEMP-MAX          
                          MOVE K452-PM-TME-SLOT(WS-CTR)                         
                                                    TO WS-TEMP-TME-SLOT         
                          MOVE K452-DTE-APPT        TO DATE-REC                 
                                                                                
                          ADD 1            TO K452-PM-TOT-SA(WS-CTR)            
                          MOVE 19          TO WS-CTR                            
                      END-IF                                                    
                    END-IF                                                      
                                                                                
                  END-IF                                                        
                END-IF                                                          
               END-IF                                                           
            END-PERFORM                                                         
           END-IF.                                                              
                                                                                
           IF WS-TEMP-TME-SLOT NOT = ZEROES                                     
              MOVE 'Y'          TO WS-SCHED-FND                                 
           END-IF.                                                              
                                                                                
       540-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       560-READ-BM06K110.                                                       
      *--------------------------------------------------------------*          
                                                                                
           MOVE SPACES           TO  K110-REC.                                  
           MOVE F800-NUM-SCH-ACC TO  K110-SCH-ACC-NO.                           
                                                                                
           READ BM06K110                                                        
                                                                                
           EVALUATE WS-K110-STATUS                                              
               WHEN 00                                                          
                 CONTINUE                                                       
               WHEN 23                                                          
                 MOVE SPACES       TO K110-REC                                  
                 MOVE 'N'          TO WS-AUTO-SCHED                             
                                                                                
               WHEN OTHER                                                       
                 DISPLAY 'ERROR READING BM06K110, STAT '                        
                 WS-K110-STATUS                                                 
                 MOVE     WS-K110-STATUS  TO RETURN-CODE                        
                 PERFORM  900-CLOSE-ROUTINE THRU 900-EXIT                       
                                                                                
           END-EVALUATE.                                                        
                                                                                
       560-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       570-READ-BM06K100.                                                       
      *--------------------------------------------------------------*          
                                                                                
           MOVE SPACES           TO  K100-REC.                                  
           MOVE K110-AIX1        TO  K100-KEY-FLD.                              
                                                                                
           READ BM06K100                                                        
                                                                                
           EVALUATE WS-K100-STATUS                                              
               WHEN 00                                                          
                 CONTINUE                                                       
               WHEN 23                                                          
                 MOVE SPACES       TO K100-REC                                  
                 MOVE 'N'          TO WS-AUTO-SCHED                             
                                                                                
               WHEN OTHER                                                       
                 DISPLAY 'ERROR READING BM06K100, STAT '                        
                 WS-K100-STATUS                                                 
                 MOVE     WS-K100-STATUS  TO RETURN-CODE                        
                 PERFORM  900-CLOSE-ROUTINE THRU 900-EXIT                       
                                                                                
           END-EVALUATE.                                                        
                                                                                
       570-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       590-READ-BP13K022.                                                       
      *--------------------------------------------------------------*          
                                                                                
           MOVE SPACES           TO BP13K022-REC.                               
           INITIALIZE               BP13K022-REC.                               
                                                                                
           MOVE SPACES           TO K022-KEY-FLD.                               
           MOVE F800-NUM-REGN    TO K022-NUM-REGN.                              
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K022-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
               WHEN 10                                                          
                 CONTINUE                                                       
                                                                                
               WHEN 23                                                          
                 MOVE SPACES       TO BP13K022-REC                              
                 MOVE 'N'          TO WS-AUTO-SCHED                             
                 ADD 1             TO WS-NO-CRM                                 
                                                                                
               WHEN OTHER                                                       
                 DISPLAY 'ERROR READING BP13K022, STAT '                        
                 WS-K022-STATUS                                                 
                 MOVE     WS-K022-STATUS  TO RETURN-CODE                        
                 PERFORM  900-CLOSE-ROUTINE THRU 900-EXIT                       
                                                                                
           END-EVALUATE.                                                        
                                                                                
       590-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       591-READ-BP27K800.                                                       
      *--------------------------------------------------------------*          
                                                                                
           MOVE SPACES           TO BP27K800-MASTER.                            
           INITIALIZE               BP27K800-MASTER.                            
                                                                                
           MOVE SPACES           TO K022-KEY-FLD.                               
           MOVE F800-NUM-HLA     TO K800-NUM-HLA OF BP27K800-MASTER.            
                                                                                
           READ BP27K800.                                                       
                                                                                
           EVALUATE WS-BP27-K800-STATUS                                         
               WHEN 00                                                          
                    PERFORM 592-CHECK-HLA-EXPIRY THRU 592-EXIT                  
                                                                                
               WHEN 23                                                          
                    MOVE 'N'              TO WS-AUTO-SCHED                      
                    ADD 1                 TO WS-NO-HLE                          
                    MOVE SPACES           TO BP27K800-MASTER                    
                                                                                
               WHEN OTHER                                                       
                 DISPLAY 'ERROR READING BP27K800, STAT '                        
                 WS-BP27-K800-STATUS                                            
                 MOVE     WS-BP27-K800-STATUS  TO RETURN-CODE                   
                 PERFORM  900-CLOSE-ROUTINE THRU 900-EXIT                       
                                                                                
           END-EVALUATE.                                                        
                                                                                
       591-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       592-CHECK-HLA-EXPIRY.                                                    
      *--------------------------------------------------------------*          
                                                                                
           IF F800-NUM-ALLO-SCHEME = 'FSH'                                      
              MOVE K800-DTE-LSA-EXPIRY  TO WS-DTE-HLA-EXPIRY                    
           ELSE                                                                 
              MOVE K800-DTE-HLA-EXPIRY  TO WS-DTE-HLA-EXPIRY                    
           END-IF.                                                              
                                                                                
           MOVE DATE-REC                TO WS-COMM-APPT-DATE.                   
                                                                                
           IF (F800-DTE-BALLOT >= '201502')                                     
              IF WS-COMM-APPT-DATE > WS-DTE-HLA-EXPIRY                          
                 IF K800-DTE-1ST-HLA-APPROVAL NOT = SPACES AND                  
                                                LOW-VALUES AND ZEROES           
                    IF K110-DTE-ACCEPTANCE < K800-DTE-1ST-HLA-APPROVAL          
                       MOVE 'N'              TO WS-AUTO-SCHED                   
                       ADD 1                 TO WS-APPT-HLE                     
                    END-IF                                                      
                 ELSE                                                           
                    MOVE 'N'                 TO WS-AUTO-SCHED                   
                    ADD 1                    TO WS-APPT-HLE                     
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              IF F800-NUM-LOAN-TAG = 'H'                                        
                 IF K800-NUM-CASE-STATUS = 'EXP'                                
                    MOVE 'N'                 TO WS-AUTO-SCHED                   
                    ADD 1                    TO WS-EXP-HLE                      
                 END-IF                                                         
              END-IF                                                            
                                                                                
              IF WS-COMM-APPT-DATE <= WS-DTE-HLA-EXPIRY                         
                 CONTINUE                                                       
              ELSE                                                              
                 IF F800-NUM-HLA-STATUS NOT = 'CAN'                             
                    MOVE 'N'              TO WS-AUTO-SCHED                      
                    ADD 1                 TO WS-APPT-HLE                        
                 END-IF                                                         
              END-IF                                                            
                                                                                
           END-IF.                                                              
                                                                                
       592-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       600-PROCESS-APPT-DATE-TIME.                                              
      *--------------------------------------------------------------*          
                                                                                
           PERFORM 610-UPDATE-BP13K452   THRU 610-EXIT.                         
           PERFORM 620-MOVE-DETAILS      THRU 620-EXIT.                         
           PERFORM 630-WRITE-BP13K454    THRU 630-EXIT.                         
           PERFORM 640-WRITE-BP13LF80    THRU 640-EXIT.                         
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       610-UPDATE-BP13K452.                                                     
      *--------------------------------------------------------------*          
                                                                                
           REWRITE   BP13K452-REC.                                              
                                                                                
           IF WS-K452-STATUS = 00                                               
              ADD     1        TO  WS-CNT-REWRTE-K452                           
           ELSE                                                                 
              DISPLAY 'ERROR WRITE BP13K452, STAT = ' WS-K452-STATUS            
              DISPLAY '            K452-KEY-FLD   = ' K452-KEY-FLD              
              MOVE     WS-K452-STATUS TO  RETURN-CODE                           
              PERFORM  900-CLOSE-ROUTINE.                                       
       610-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       620-MOVE-DETAILS.                                                        
      *--------------------------------------------------------------*          
                                                                                
                                                                                
           MOVE SPACES       TO BP13K454-REC                                    
           INITIALIZE           BP13K454-REC.                                   
                                                                                
           MOVE DATE-REC             TO K454-DTE-APPT                           
           MOVE WS-TEMP-TME-SLOT     TO K454-TME-APPT                           
                                                                                
           MOVE 'SA'                 TO K454-CDE-APPT-TYPE                      
           MOVE F800-NUM-REGN        TO K454-NUM-REGN                           
                                                                                
           MOVE DATE-REC             TO K454-DTE-APPT2                          
           MOVE K110-ESTATE          TO K454-CDE-ESTATE                         
           MOVE F800-NUM-SCH-ACC     TO K454-SCH-ACCT                           
                                                                                
           MOVE 'N'                  TO K454-CDE-APPT                           
           MOVE 'N'                  TO K454-NUM-UPDATE-BP13K800                
           MOVE SPACE                TO K454-CDE-SUB-APPT-TYPE                  
           MOVE 'Y'                  TO K454-CDE-PHLINE-AVAIL                   
           MOVE 'Y'                  TO K454-CDE-AUTO-SCHEDULE                  
                                                                                
           MOVE WS-CURRENT-DATE      TO K454-DTE-UPDATE.                        
           MOVE 'P13CF80'            TO K454-NUM-USERID.                        
                                                                                
                                                                                
       620-EXIT.                                                                
           EXIT.                                                                
                                                                                
       630-WRITE-BP13K454.                                                      
      *-------------------------------------------------------------            
           WRITE   BP13K454-REC.                                                
                                                                                
           IF WS-K454-STATUS = 00 OR 02 OR 22                                   
              ADD     1        TO  WS-CNT-CREATE-REC                            
              ADD     1        TO  WS-CNT-WRTE-K454                             
           ELSE                                                                 
              DISPLAY 'ERROR WRITE BP13K454, STAT = ' WS-K454-STATUS            
              DISPLAY '            K454-KEY-FLD   = ' K454-KEY-FLD              
              MOVE     WS-K454-STATUS TO  RETURN-CODE                           
              PERFORM  900-CLOSE-ROUTINE.                                       
                                                                                
       630-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       640-WRITE-BP13LF80.                                                      
      *-------------------------------------------------------------            
                                                                                
           IF WS-CNT-LINE > 50                                                  
              ADD 1 TO WS-CNT-PAGE                                              
              STRING WS-CURRENT-DATE(7:2) '/'                                   
                     WS-CURRENT-DATE(5:2) '/'                                   
                     WS-CURRENT-DATE(1:4) DELIMITED BY SIZE INTO                
                     LF80-DATE                                                  
              MOVE WS-CNT-PAGE   TO LF80-PAGENO                                 
              WRITE BP13LF80-REC  FROM LF80-HEADER1                             
              WRITE BP13LF80-REC  FROM LF80-HEADER2                             
              WRITE BP13LF80-REC  FROM LF80-HEADER3                             
              MOVE 3    TO WS-CNT-LINE                                          
           END-IF.                                                              
                                                                                
           IF WS-SCHED-DATE =  SPACES OR LOW-VALUES                             
              MOVE DATE-REC    TO WS-SCHED-DATE                                 
           END-IF.                                                              
                                                                                
           IF WS-SCHED-TIME = SPACES OR LOW-VALUES                              
              MOVE WS-TEMP-TME-SLOT   TO WS-SCHED-TIME                          
           END-IF.                                                              
                                                                                
           IF DATE-REC = WS-SCHED-DATE                                          
              IF WS-TEMP-TME-SLOT = WS-SCHED-TIME                               
                 MOVE SPACES        TO LF80-DETAILS                             
                 ADD 1 TO WS-TOT-SCHED                                          
                 PERFORM 650-MOVE-DETAILS  THRU 650-EXIT                        
              ELSE                                                              
      *          PERFORM 650-MOVE-DETAILS  THRU 650-EXIT                        
                 ADD 1 TO WS-CNT-CASES                                          
                 MOVE WS-CNT-CASES  TO   LF80-SNO                               
                 WRITE BP13LF80-REC   FROM LF80-DETAILS                         
                 MOVE WS-TEMP-TME-SLOT   TO WS-SCHED-TIME                       
                 WRITE BP13LF80-REC   FROM LF80-DETAILS                         
                 MOVE 1        TO WS-TOT-SCHED                                  
                 MOVE ZEROES   TO WS-TOT-VAC                                    
                 PERFORM 650-MOVE-DETAILS  THRU 650-EXIT                        
                 ADD 1         TO WS-CNT-LINE                                   
              END-IF                                                            
           ELSE                                                                 
              ADD 1 TO WS-CNT-CASES                                             
              MOVE WS-CNT-CASES  TO   LF80-SNO                                  
              WRITE BP13LF80-REC   FROM LF80-DETAILS                            
              DISPLAY LF80-DETAILS                                              
              MOVE DATE-REC    TO WS-SCHED-DATE                                 
                 MOVE 1        TO WS-TOT-SCHED                                  
                 MOVE ZEROES   TO WS-TOT-VAC                                    
                 PERFORM 650-MOVE-DETAILS  THRU 650-EXIT                        
              ADD 1 TO WS-CNT-LINE                                              
           END-IF.                                                              
                                                                                
       640-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       650-MOVE-DETAILS.                                                02720001
      *-------------------------------------------------------------            
                                                                                
           STRING DATE-REC(7:2)'/'                                              
                  DATE-REC(5:2)'/'                                              
                  DATE-REC(1:4) DELIMITED BY SIZE INTO                          
                  LF80-SCHED-DATE.                                              
                                                                                
           STRING WS-TEMP-TME-SLOT(1:2) ':'                                     
                  WS-TEMP-TME-SLOT(3:2) DELIMITED BY SIZE INTO                  
                  LF80-SCHED-TIME.                                              
                                                                                
           MOVE WS-TOT-SCHED    TO LF80-NO-SCHED-CASES                          
           COMPUTE WS-TOT-VAC = WS-TEMP-MAX - WS-TOT-SCHED                      
           MOVE WS-TOT-VAC    TO   LF80-NO-VAC-SLOT.                            
           MOVE WS-TEMP-MAX   TO   LF80-NO-MAX-CASE.                            
                                                                                
       650-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       710-PROCESS-DATE.                                                02720001
      *-------------------------------------------------------------            
                                                                                
           IF 31-DAY-MONTH                                              02740001
               IF CAL-DAY > 31                                          02750002
               COMPUTE CAL-DAY    = CAL-DAY    - 31                     02760001
               COMPUTE CAL-MONTH  = CAL-MONTH  + 1                      02770002
               END-IF                                                   02771002
           END-IF.                                                      02772002
                                                                        02780001
           IF 30-DAY-MONTH                                              02790001
               IF CAL-DAY > 30                                          02800002
               COMPUTE CAL-DAY    = CAL-DAY    - 30                     02810001
               COMPUTE CAL-MONTH  = CAL-MONTH  + 1                      02820002
               END-IF                                                   02821002
           END-IF.                                                      02822002
                                                                        02830001
           IF 28-29-DAY-MONTH                                           02840001
               IF  CAL-DAY < 29                                         02850002
                   GO TO 710-EXIT                                       02860001
               ELSE                                                     02870001
                   PERFORM 720-FEBRUARY                                 02880001
                      THRU 720-EXIT                                     02890001
               END-IF                                                   02891002
           END-IF.                                                      02892002
                                                                        02910001
           IF CAL-MONTH  > 12                                           02920001
              MOVE 01 TO CAL-MONTH                                      02930001
              ADD  01 TO CAL-YEAR.                                      02940001
                                                                        02950001
       710-EXIT.                                                        02960001
           EXIT.                                                        02970001
                                                                        02980001
                                                                        02980001
                                                                        03140001
      *----------------*                                                03150001
       720-FEBRUARY.                                                    03160001
      *----------------*                                                03170001
                                                                                
           DIVIDE 4 INTO CAL-YEAR GIVING WS-YEAR REMAINDER WS-REM.      03180001
           IF (WS-REM = ZERO) AND (CAL-DAY > 29)                        03190002
              COMPUTE CAL-DAY = CAL-DAY - 29                            03210001
              MOVE 03 TO CAL-MONTH                                      03220001
           ELSE                                                         03230001
           IF (WS-REM NOT = ZERO) AND (CAL-DAY > 28)                    03250002
              COMPUTE CAL-DAY = CAL-DAY - 28                            03270001
              MOVE 03 TO CAL-MONTH.                                     03280001
                                                                        03290001
       720-EXIT.                                                        03300001
           EXIT.                                                        03310001
                                                                        03320001
                                                                                
      *-------------------------------------------------------------            
       800-DISPLAY-STATISTICS.                                                  
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '                                                 '.         
           DISPLAY '***********  BP13CF80 CONTROL LIST  ***********  '.         
           DISPLAY '                                                 '.         
           DISPLAY '                                                 '.         
           DISPLAY 'BP13F800 READ    RECORDS    = ' WS-CNT-READ-F800.           
           DISPLAY 'BP13K454 WRITTEN RECORDS    = ' WS-CNT-WRTE-K454.           
           DISPLAY 'BP13K452 UPDATED RECORDS    = ' WS-CNT-REWRTE-K452.         
           DISPLAY 'CASES WERE SCHEDULE EXIST   = ' WS-ALREADY-SCHED.           
           DISPLAY 'CASES LOAN TAG NOT A/H/     = ' WS-LOANTAG-NOT-AH.          
           DISPLAY 'CASES THAT ARE KIV(BTO)     = ' WS-BTOKIV.                  
           DISPLAY 'CASES APPT DATE < 1ST SOCA  = ' WS-1ST-SOCA.                
           DISPLAY 'CASES PAIRED APPLICATION    = ' WS-PAIRED.                  
           DISPLAY 'CASES PAIRED APPLICATION    = ' WS-PROVTAG.                 
           DISPLAY 'CASES WITH AO DTE           = ' WS-DTE-AO.                  
           DISPLAY 'CASES WITH KEY AVAIL DTE    = ' WS-KEY-AVAIL.               
           DISPLAY 'CASES IS SERS               = ' WS-SER-CASE.                
           DISPLAY 'CASES WITH NO CRM OFFICER   = ' WS-NO-CRM.                  
           DISPLAY 'CASES WITH NO HLE AVAILABLE = ' WS-NO-HLE.                  
           DISPLAY 'CASES APPT-DTE > HLE EXPIRY = ' WS-APPT-HLE.                
           DISPLAY 'CASES WITH HLE EXPIRED      = ' WS-EXP-HLE.                 
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       900-CLOSE-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           CLOSE BP13F800                                                       
                 BP13K022                                                       
                 BP27K800                                                       
                 BP13K454                                                       
                 BP13K452                                                       
                 BM06K110                                                       
                 BM06K100.                                                      
                                                                                
           IF WS-BP27-K800-STATUS NOT = 00 AND 97                               
              DISPLAY 'ERROR CLOSING BP27K800, STATUS = '                       
                                            WS-BP27-K800-STATUS                 
              MOVE   WS-BP27-K800-STATUS TO  RETURN-CODE                        
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K022, STATUS = ' WS-K022-STATUS        
              MOVE     WS-K022-STATUS TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           IF WS-K452-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K452, STATUS = ' WS-K452-STATUS        
              MOVE     WS-K452-STATUS TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           IF WS-K454-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K454, STATUS = ' WS-K454-STATUS        
              MOVE     WS-K454-STATUS TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BM06K110, STATUS = ' WS-K110-STATUS        
              MOVE     WS-K110-STATUS TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BM06K100, STATUS = ' WS-K100-STATUS        
              MOVE     WS-K100-STATUS TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
