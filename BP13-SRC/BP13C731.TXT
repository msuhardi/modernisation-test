       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C731.                                                 
      *AUTHOR.        EDWARD S. CRUZ.                                           
      *DATE-WRITTEN   03AUG2001.                                                
                                                                                
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE - TO COMPUTE NUMBER OF MCP AND NON-MCP    *                
      *                RESIDENT APPLICATION FROM BALLOT FILE   *                
      *                BP13F520 BY NT/FT/ALLOC-SCH.            *                
      *              - TO DIVIDE EACH PRIO-SCHEME INTO 2 HOUSE-*                
      *                HOLD TYPES (NHH/NPG)                    *                
      *              - THIS PROGRAM IS PATTERNED AFTER BP13C748*                
      *                                                        *                
      * CHG REQ  BY   DATE  REMARKS                            *                
      * -------  --- ------ ---------------------------------- *                
      * BP131064 ED  030801 NEW PROGRAM                        *                
      * BP132162 ANC 071102 CHECK IF RESIDENT-HA NOT BLANK     *                
      * BP132463 SK  091203 CATER FOR HA1,HA2,HA3 & HA4        *                
      * BP132496 SSD 220904 WRITE TO BP13F012 INSTD OF BP13F760*                
      **********************************************************                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.     IBM-3090.                                           
       OBJECT-COMPUTER.     IBM-3090.                                           
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F520    ASSIGN TO BP13F520.                               
                                                                                
           SELECT BP13F012    ASSIGN TO BP13F012.                               
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F520           BLOCK CONTAINS 0 RECORDS                          
                              RECORD CONTAINS 500 CHARACTERS                    
                              RECORDING MODE IS F                               
                              LABEL RECORDS ARE STANDARD.                       
           COPY BP13F520.                                                       
                                                                                
       FD  BP13F012           LABEL RECORDS ARE STANDARD                        
                              RECORD CONTAINS 400 CHARACTERS                    
                              RECORDING MODE IS F                               
                              BLOCK CONTAINS 0 RECORDS.                         
           COPY BP13F012.                                                       
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-VARIABLES.                                                        
           05  WS-CNT-F520-READ          PIC 9(8) VALUE 0.                      
           05  WS-CNT-F012-WRITE         PIC 9(8) VALUE 0.                      
           05  WS-CTR                    PIC 9    VALUE 0.                      
           05  WS-HHTY-PTR               PIC 9    VALUE 0.                      
           05  WS-PREV-NT                PIC X(3) VALUE SPACES.                 
           05  WS-PREV-FT                PIC X(2) VALUE SPACES.                 
           05  WS-F520-EOF               PIC X    VALUE 'N'.                    
               88 F520-EOF                        VALUE 'Y'.                    
                                                                                
       01  WS-TABLE.                                                            
           03  WS-HOUSEHOLD-TABLE  OCCURS 2 TIMES.                              
               05  WS-SCHEME-TABLE OCCURS 2 TIMES.                              
                   10  WS-CNT-MCP            PIC 9(5).                          
                   10  WS-CNT-NON-MCP        PIC 9(5).                          
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURR-CC                PIC XX   VALUE SPACES.                 
           05  WS-DTE-CURRENT.                                                  
               10  WS-CURR-YY            PIC XX   VALUE SPACES.                 
               10  WS-CURR-MM            PIC XX   VALUE SPACES.                 
               10  WS-CURR-DD            PIC XX   VALUE SPACES.                 
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-CONTROL-RTN.                                                        
                                                                                
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
                                                                                
           PERFORM 2000-INIT-TABLE       THRU 2000-EXIT                         
           VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 2.                         
                                                                                
           PERFORM 3000-READ-BP13F520    THRU 3000-EXIT.                        
                                                                                
           MOVE F520-CDE-NT1             TO WS-PREV-NT.                         
           MOVE F520-CDE-FLAT-TYPE       TO WS-PREV-FT.                         
                                                                                
           PERFORM 4000-MAIN-ROUTINE     THRU 4000-EXIT                         
             UNTIL F520-EOF.                                                    
                                                                                
           PERFORM 9999-CLOSE-FILES      THRU 9999-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-FILES.                                                         
                                                                                
           OPEN INPUT  BP13F520                                                 
               OUTPUT  BP13F012.                                                
                                                                                
           ACCEPT WS-DTE-CURRENT         FROM DATE.                             
                                                                                
           IF WS-CURR-YY < '90'                                                 
              MOVE  '20'                 TO WS-CURR-CC                          
           ELSE                                                                 
              MOVE  '19'                 TO WS-CURR-CC.                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-INIT-TABLE.                                                         
                                                                                
           MOVE ZEROS               TO WS-CNT-MCP     (1, WS-CTR)               
                                       WS-CNT-MCP     (2, WS-CTR)               
                                       WS-CNT-NON-MCP (1, WS-CTR)               
                                       WS-CNT-NON-MCP (2, WS-CTR).              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-READ-BP13F520.                                                      
                                                                                
           READ BP13F520                 AT END                                 
           MOVE 'Y'                      TO WS-F520-EOF                         
           GO TO 3000-EXIT.                                                     
                                                                                
           ADD 1                         TO WS-CNT-F520-READ.                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-MAIN-ROUTINE.                                                       
                                                                                
           IF F520-CDE-NT1 NOT = WS-PREV-NT OR                                  
              F520-CDE-FLAT-TYPE NOT = WS-PREV-FT                               
              PERFORM 5000-WRITE-BP13F012     THRU 5000-EXIT.                   
                                                                                
           IF F520-CDE-HOUSEHOLD = 'G'                                          
               MOVE 2            TO WS-HHTY-PTR                                 
           ELSE                                                                 
               MOVE 1            TO WS-HHTY-PTR                                 
           END-IF.                                                              
                                                                                
           IF (F520-CDE-RESIDENT-HA1 NOT = SPACES AND LOW-VALUES AND            
                                           'H' AND 'M' AND 'P')  OR             
              (F520-CDE-RESIDENT-HA2 NOT = SPACES AND LOW-VALUES AND            
                                           'H' AND 'M' AND 'P')  OR             
              (F520-CDE-RESIDENT-HA3 NOT = SPACES AND LOW-VALUES AND            
                                           'H' AND 'M' AND 'P')  OR             
              (F520-CDE-RESIDENT-HA4 NOT = SPACES AND LOW-VALUES AND            
                                           'H' AND 'M' AND 'P')                 
              ADD 1           TO WS-CNT-MCP     (WS-HHTY-PTR, 1)                
           ELSE                                                                 
              ADD 1           TO WS-CNT-NON-MCP (WS-HHTY-PTR, 2)                
           END-IF.                                                              
                                                                                
           MOVE F520-CDE-NT1             TO WS-PREV-NT.                         
           MOVE F520-CDE-FLAT-TYPE       TO WS-PREV-FT.                         
                                                                                
           PERFORM 3000-READ-BP13F520    THRU 3000-EXIT.                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5000-WRITE-BP13F012.                                                     
                                                                                
           MOVE SPACES    TO BP13F012-MCPSUNIT.                                 
           MOVE ZEROS     TO F012-NUM-TOT-UNIT                                  
                             F012-NUM-TOT-MCPH     F012-NUM-TOT-MCPG            
                             F012-NUM-MAX-MCPH1    F012-NUM-MAX-MCPG1           
                             F012-NUM-MAX-MCPH2    F012-NUM-MAX-MCPG2           
                             F012-NUM-MAX-MCPH3    F012-NUM-MAX-MCPG3           
                             F012-REG-MCPH1        F012-REG-MCPG1               
                             F012-REG-MCPH2        F012-REG-MCPG2               
                             F012-REG-MCPH3        F012-REG-MCPG3               
                             F012-NUM-TOT-NOMCH    F012-NUM-TOT-NOMCG           
                             F012-NUM-MAX-NOMCH1   F012-NUM-MAX-NOMCG1          
                             F012-NUM-MAX-NOMCH2   F012-NUM-MAX-NOMCG2          
                             F012-NUM-MAX-NOMCH3   F012-NUM-MAX-NOMCG3          
                             F012-REG-NOMCH1       F012-REG-NOMCG1              
                             F012-REG-NOMCH2       F012-REG-NOMCG2              
                             F012-REG-NOMCH3       F012-REG-NOMCG3              
                             F012-NUM-TOT-MCP      F012-NUM-TOT-PA              
                             F012-NUM-MAX-MCP1     F012-NUM-MAX-PA1             
                             F012-NUM-MAX-MCP2     F012-NUM-MAX-PA2             
                             F012-NUM-MAX-MCP3     F012-NUM-MAX-PA3             
                             F012-REG-MCP1         F012-REG-PA1                 
                             F012-REG-MCP2         F012-REG-PA2                 
                             F012-REG-MCP3         F012-REG-PA3                 
                             F012-NUM-TOT-NOMCP    F012-REG-NOMCP1              
                             F012-NUM-MAX-NOMCP1   F012-REG-NOMCP2              
                             F012-NUM-MAX-NOMCP2   F012-REG-NOMCP3              
                             F012-NUM-MAX-NOMCP3.                               
                                                                                
           MOVE ZEROS     TO F012-NUM-RDC      F012-NUM-C1                      
                             F012-NUM-RH       F012-NUM-C2                      
                             F012-NUM-REN      F012-NUM-C3                      
                             F012-NUM-MT       F012-NUM-DEBAR                   
                             F012-NUM-JB       F012-NUM-TOP-UP-CCC              
                             F012-NUM-PA       F012-NUM-NHH                     
                             F012-NUM-CC       F012-NUM-UPG                     
                             F012-NUM-TCP      F012-NUM-FLAT-UPG-APA3.          
                                                                                
           MOVE WS-PREV-NT            TO F012-CDE-NT-ZONE.                      
           MOVE WS-PREV-FT            TO F012-CDE-FLAT-TY.                      
                                                                                
           MOVE WS-CNT-MCP     (1, 1) TO F012-NUM-APP-MCPH.                     
           MOVE WS-CNT-MCP     (2, 1) TO F012-NUM-APP-MCPG.                     
           MOVE WS-CNT-NON-MCP (1, 2) TO F012-NUM-APP-NOMCH.                    
           MOVE WS-CNT-NON-MCP (2, 2) TO F012-NUM-APP-NOMCG.                    
                                                                                
           MOVE WS-CURRENT-DATE       TO F012-DTE-UPDATE.                       
           WRITE BP13F012-MCPSUNIT.                                             
           ADD 1                      TO WS-CNT-F012-WRITE.                     
                                                                                
           PERFORM 2000-INIT-TABLE  THRU 2000-EXIT                              
           VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 2.                         
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9999-CLOSE-FILES.                                                        
                                                                                
           PERFORM 5000-WRITE-BP13F012   THRU 5000-EXIT.                        
           CLOSE BP13F012                                                       
                 BP13F520.                                                      
                                                                                
           DISPLAY '>>>>>>>>>>>>>>>>> BP13C731 <<<<<<<<<<<<<<<<<<<<<<<'.        
           DISPLAY 'COMPUTE NO. OF RESIDENT & NON RESIDENT APPLICATION'.        
           DISPLAY '  '.                                                        
           DISPLAY 'NO OF RECORDS READ FROM BP13F520    : '                     
                                                      WS-CNT-F520-READ.         
           DISPLAY '                        BALLOT FILE.'.                      
           DISPLAY 'NO OF RECORDS WRITTEN IN BP13F012   : '                     
                                                      WS-CNT-F012-WRITE.        
           DISPLAY '  '.                                                        
                                                                                
       9999-EXIT.                                                               
           STOP RUN.                                                            
