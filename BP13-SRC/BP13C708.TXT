       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C708.                                                    
      *AUTHOR.     SURESH.                                                      
      *DATE-WRITTEN. 05/04/2000.                                                
      * ====================================================== *                
      *              SYSTEM OF COMMITMENT (BP13)               *                
      * ====================================================== *                
      * INPUT FILES  : BP13F558                                *                
      *                BP13F716                                *                
      * OUTPUT LIST  : BP13L708                                *                
      *                                                        *                
      * OBJECTIVE    : TO MATCH APPL RECS WITH RDB             *                
      * ------------------------------------------------------ *                
      * CHG-NO     OIC  DATE       DESCRIPTION                 *                
      * ======     ===  ========== ===========                 *                
      * BP130825   SRN  05/04/2000 NEW PROGRAM                 *                
      * BP130897   AKT  04/08/2000 EXPAND F558 RECL TO 600     *                
      * ====================================================== *                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F558   ASSIGN       TO BP13F558.                          
                                                                                
           SELECT BP13F716   ASSIGN       TO BP13F716.                          
                                                                                
           SELECT BP13K060   ASSIGN       TO BP13K060                           
                             ORGANIZATION INDEXED                               
                             ACCESS MODE  RANDOM                                
                             RECORD KEY   K060-KEY-FLD                          
                             FILE STATUS  WS-K060-STATUS.                       
                                                                                
           SELECT P13F558A   ASSIGN       TO P13F558A.                          
                                                                                
           SELECT BP13L708   ASSIGN       TO BP13L708.                          
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F558                                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F558.                                                           
                                                                                
       FD   BP13F716                                                            
            RECORD CONTAINS 350 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13F716.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS 25 CHARACTERS                                       
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13K060.                                                           
                                                                                
       FD   P13F558A                                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01 P13F558A-REC          PIC X(600).                                     
                                                                                
       FD   BP13L708                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01 BP13L708-REC             PIC X(132).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01 WS-L708-LAYOUT.                                                       
          05 L708-HEAD1.                                                        
             10  FILLER            PIC X(16)   VALUE                            
                    'BP13L708   HDB3'.                                          
             10  FILLER            PIC X(23)   VALUE SPACES.                    
             10  FILLER            PIC X(40)   VALUE                            
                 'S Y S T E M   O F   C O M M I T M E N T'.                     
             10  FILLER            PIC X(15)   VALUE SPACES.                    
             10  FILLER            PIC X(7)    VALUE 'DATE: '.                  
             10  L708-DATE         PIC X(10)   VALUE SPACES.                    
             10  FILLER            PIC X(2)    VALUE SPACES.                    
             10  FILLER            PIC X(7)    VALUE 'PAGE: '.                  
             10  L708-PAGE         PIC ZZZ9.                                    
                                                                                
          05 L708-HEAD2.                                                        
             10  FILLER            PIC X(37)   VALUE SPACES.                    
             10  FILLER            PIC X(45)   VALUE                            
             'REPORT ON APPLICANT NRIC MATCHED WITH RDBII'.                     
                                                                                
          05 L708-HEAD3.                                                        
             10  FILLER            PIC X(17)   VALUE SPACES.                    
             10  FILLER            PIC X(48)   VALUE                            
             '<---------------- APPLICATION ----------------->'.                
             10  FILLER            PIC X(2)    VALUE SPACES.                    
             10  FILLER            PIC X(58)   VALUE                            
               '<------------------------- RDBII -----------------------        
      -        '->'.                                                            
                                                                                
          05 L708-HEAD4.                                                        
             10  FILLER            PIC X(5)    VALUE ' SNO'.                    
             10  FILLER            PIC X(2)    VALUE SPACES.                    
             10  FILLER            PIC X(8)    VALUE 'REGN-NO'.                 
             10  FILLER            PIC X(5)    VALUE SPACES.                    
                                                                                
             10  FILLER            PIC X(9)    VALUE 'NRIC '.                   
             10  FILLER            PIC X(2)    VALUE SPACES.                    
             10  FILLER            PIC X(10)   VALUE 'MAR-ST'.                  
             10  FILLER            PIC X(2)    VALUE SPACES.                    
             10  FILLER            PIC X(15)   VALUE 'RLTNSP'.                  
             10  FILLER            PIC X(2)    VALUE SPACES.                    
             10  FILLER            PIC X(5)    VALUE 'APPLN'.                   
                                                                                
             10  FILLER            PIC X(3)    VALUE SPACES.                    
                                                                                
             10  FILLER            PIC X(14)   VALUE 'HDBREF'.                  
             10  FILLER            PIC X(5)    VALUE SPACES.                    
             10  FILLER            PIC X(9)    VALUE 'NRIC '.                   
             10  FILLER            PIC X(2)    VALUE SPACES.                    
             10  FILLER            PIC X(10)   VALUE 'MAR-ST'.                  
             10  FILLER            PIC X(2)    VALUE SPACES.                    
             10  FILLER            PIC X(15)   VALUE 'RLTNSP'.                  
                                                                                
             10  FILLER            PIC X(2)    VALUE SPACES.                    
                                                                                
          05 L708-HEAD5            PIC X(132)  VALUE ALL '-'.                   
                                                                                
          05 L708-DETAIL1.                                                      
             10  L708-SNO          PIC ZZZZ9.                                   
             10  FILLER            PIC X(2)    VALUE SPACES.                    
             10  L708-REGNO        PIC X(8)    VALUE SPACES.                    
             10  FILLER            PIC X(2)    VALUE SPACES.                    
             10  L708-APPLN-TAG    PIC X(2)    VALUE SPACES.                    
             10  FILLER            PIC X(1)    VALUE SPACES.                    
             10  L708-APPLN-NRIC   PIC X(9)    VALUE SPACES.                    
             10  FILLER            PIC X(2)    VALUE SPACES.                    
             10  L708-APPLN-MARST  PIC X(10)   VALUE SPACES.                    
             10  FILLER            PIC X(2)    VALUE SPACES.                    
             10  L708-APPLN-RLTNSP PIC X(15)   VALUE SPACES.                    
             10  FILLER            PIC X(4)    VALUE SPACES.                    
             10  L708-NUM-APPLN    PIC X(1)    VALUE SPACES.                    
             10  FILLER            PIC X(5)    VALUE SPACES.                    
             10  L708-RDBII-HDBREF PIC X(14)   VALUE SPACES.                    
             10  FILLER            PIC X(2)    VALUE SPACES.                    
             10  L708-RDBII-TAG    PIC X(2)    VALUE SPACES.                    
             10  FILLER            PIC X(1)    VALUE SPACES.                    
             10  L708-RDBII-NRIC   PIC X(9)    VALUE SPACES.                    
             10  FILLER            PIC X(2)    VALUE SPACES.                    
             10  L708-RDBII-MARST  PIC X(10)   VALUE SPACES.                    
             10  FILLER            PIC X(2)    VALUE SPACES.                    
             10  L708-RDBII-RLTNSP PIC X(15)   VALUE SPACES.                    
             10  FILLER            PIC X(2)    VALUE SPACES.                    
             10  L708-REASON       PIC X(5)    VALUE SPACES.                    
                                                                                
       01 WS-PRINT-CTR.                                                         
          05 WS-LINE-CTR           PIC 9(2)    VALUE 99.                        
          05 WS-PAGE-CTR           PIC 9(4)    VALUE ZEROS.                     
          05 WS-PRT-CTR            PIC 9(2)    VALUE ZEROS.                     
          05 WS-SNO                PIC 9(5)    VALUE ZEROS.                     
                                                                                
       01 WS-K060-STATUS           PIC 9(2)    VALUE ZEROS.                     
                                                                                
       01 WS-COUNTERS.                                                          
          05 WS-F558-READ-CTR      PIC 9(8)    VALUE 0.                         
          05 WS-F716-READ-CTR      PIC 9(8)    VALUE 0.                         
          05 WS-L708-WRITE-CTR     PIC 9(8)    VALUE 0.                         
          05 WS-APPLN-CTR          PIC 9(2)    VALUE 0.                         
          05 WS-RDBII-CTR          PIC 9(2)    VALUE 0.                         
                                                                                
       01 WS-VARIABLES.                                                         
          05 WS-PRINT-STATUS       PIC X(1)    VALUE 'N'.                       
          05 WS-NUM-REGN           PIC X(8)    VALUE SPACES.                    
          05 WS-F716-LSE1-MARST    PIC X(2)    VALUE SPACES.                    
          05 WS-F716-LSE1-NRIC     PIC X(9)    VALUE SPACES.                    
          05 WS-F558-LSE1-NRIC     PIC X(9)    VALUE SPACES.                    
                                                                                
          05 WS-F558-INFO OCCURS 15 TIMES INDEXED BY WS-F558-IDX.               
             10 WS-F558-TAG        PIC X(2)    VALUE SPACES.                    
             10 WS-F558-NRIC       PIC X(9)    VALUE SPACES.                    
             10 WS-F558-APPLICANT  PIC X(1)    VALUE SPACES.                    
             10 WS-F558-MARST      PIC X(1)    VALUE SPACES.                    
             10 WS-F558-RLTNSP     PIC X(2)    VALUE SPACES.                    
             10 WS-F558-REC        PIC X(600)  VALUE SPACES.                    
                                                                                
                                                                                
          05 WS-F716-INFO OCCURS 25 TIMES INDEXED BY WS-F716-IDX.               
             10 WS-F716-TAG        PIC X(2)    VALUE SPACES.                    
             10 WS-F716-NRIC       PIC X(9)    VALUE SPACES.                    
             10 WS-F716-APPLICANT  PIC X(1)    VALUE SPACES.                    
             10 WS-F716-HDBREF     PIC X(11)   VALUE SPACES.                    
             10 WS-F716-MARST      PIC X(2)    VALUE SPACES.                    
             10 WS-F716-RLTNSP     PIC X(2)    VALUE SPACES.                    
                                                                                
       01  WS-SYS-DATE             PIC X(8)    VALUE SPACES.                    
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *-----------------------------------------------------------*             
       0000-MAIN.                                                               
      *-----------------------------------------------------------*             
            PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                         
                                                                                
            PERFORM 2000-READ-BP13F558  THRU 2000-EXIT                          
                                                                                
            PERFORM 3000-READ-BP13F716  THRU 3000-EXIT                          
                                                                                
            PERFORM 4000-MAIN-PROCESS   THRU 4000-EXIT                          
                    UNTIL F558-NUM-REGN = HIGH-VALUES OR                        
                          F716-NUM-REGN = HIGH-VALUES.                          
                                                                                
            PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------------------------------------------------*             
       1000-OPEN-FILES.                                                         
      *-----------------------------------------------------------*             
            OPEN INPUT  BP13F558                                                
                        BP13F716                                                
                        BP13K060                                                
                 OUTPUT BP13L708                                                
                        P13F558A.                                               
                                                                                
            ACCEPT WS-SYS-DATE  FROM DATE YYYYMMDD.                             
                                                                                
            STRING WS-SYS-DATE (7:2), '-',                                      
                   WS-SYS-DATE (5:2), '-',                                      
                   WS-SYS-DATE (1:4)                                            
                   DELIMITED BY SIZE                                            
                   INTO                                                         
                   L708-DATE.                                                   
                                                                                
            IF WS-K060-STATUS NOT = 0 AND 97                                    
               DISPLAY 'ERROR READING BP13K060 '                                
               DISPLAY 'FILE STATUS ' WS-K060-STATUS                            
               MOVE WS-K060-STATUS TO RETURN-CODE                               
               GO TO 9000-CLOSE-FILES                                           
            END-IF.                                                             
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------------------------------------------------*             
       2000-READ-BP13F558.                                                      
      *-----------------------------------------------------------*             
            READ BP13F558  AT END                                               
                 MOVE HIGH-VALUES TO F558-NUM-REGN                              
                 GO   TO  2000-EXIT.                                            
                                                                                
            ADD 1                     TO  WS-F558-READ-CTR.                     
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------------------------------------------------*             
       3000-READ-BP13F716.                                                      
      *-----------------------------------------------------------*             
            READ BP13F716  AT END                                               
                 MOVE HIGH-VALUES TO F716-NUM-REGN                              
                 GO   TO  3000-EXIT.                                            
                                                                                
            ADD 1                     TO  WS-F716-READ-CTR.                     
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------------------------------------------------*             
       4000-MAIN-PROCESS.                                                       
      *-----------------------------------------------------------*             
            IF F558-NUM-REGN = F716-NUM-REGN                                    
               PERFORM 4500-MATCH-RECS           THRU 4500-EXIT                 
            ELSE                                                                
               IF F558-NUM-REGN < F716-NUM-REGN                                 
                  PERFORM 2000-READ-BP13F558     THRU 2000-EXIT                 
               ELSE                                                             
                  IF F558-NUM-REGN > F716-NUM-REGN                              
                     PERFORM 3000-READ-BP13F716  THRU 3000-EXIT                 
                  END-IF                                                        
               END-IF                                                           
            END-IF.                                                             
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------------------------------------------------*             
       4500-MATCH-RECS.                                                         
      *-----------------------------------------------------------*             
            INITIALIZE WS-VARIABLES                                             
            MOVE F558-NUM-REGN                TO WS-NUM-REGN                    
                                                                                
            MOVE ZEROS                        TO WS-APPLN-CTR                   
                                                 WS-RDBII-CTR                   
                                                                                
            MOVE 'N'                          TO WS-PRINT-STATUS                
                                                                                
            PERFORM 5000-PROCESS-BP13F558     THRU 5000-EXIT                    
                    UNTIL F558-NUM-REGN NOT = WS-NUM-REGN                       
                                                                                
            PERFORM 5100-PROCESS-BP13F716     THRU 5100-EXIT                    
                    UNTIL F716-NUM-REGN NOT = WS-NUM-REGN                       
                                                                                
            IF WS-PRINT-STATUS = 'Y'                                            
               ADD 1                          TO WS-SNO                         
               MOVE WS-SNO                    TO L708-SNO                       
               MOVE WS-NUM-REGN               TO L708-REGNO                     
               PERFORM 6000-OUTPUT-P13F558A   THRU 6000-EXIT                    
                       VARYING WS-PRT-CTR FROM 1 BY 1 UNTIL                     
                       WS-PRT-CTR > WS-APPLN-CTR AND                            
                       WS-PRT-CTR > WS-RDBII-CTR                                
            END-IF.                                                             
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------------------------------------------------*             
       5000-PROCESS-BP13F558.                                                   
      *-----------------------------------------------------------*             
            ADD 1                       TO WS-APPLN-CTR                         
                                                                                
            IF F558-CDE-RELATIONSHIP = '00'                                     
               MOVE F558-NUM-NRIC       TO WS-F558-LSE1-NRIC                    
            END-IF.                                                             
                                                                                
            MOVE F558-NUM-NRIC          TO WS-F558-NRIC(WS-APPLN-CTR).          
            MOVE F558-CDE-RELATIONSHIP  TO WS-F558-RLTNSP(WS-APPLN-CTR).        
            MOVE F558-NUM-APPLN         TO                                      
                                        WS-F558-APPLICANT(WS-APPLN-CTR).        
            MOVE F558-NUM-MARITAL-STATUS                                        
                                        TO WS-F558-MARST(WS-APPLN-CTR)          
                                                                                
            MOVE BP13F558-REC           TO WS-F558-REC(WS-APPLN-CTR).           
                                                                                
            PERFORM 2000-READ-BP13F558    THRU 2000-EXIT.                       
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------------------------------------------------*             
       5100-PROCESS-BP13F716.                                                   
      *-----------------------------------------------------------*             
            ADD 1                       TO WS-RDBII-CTR                         
                                                                                
            MOVE F716-NUM-NRIC          TO WS-F716-NRIC(WS-RDBII-CTR)           
            MOVE F716-NUM-RDB-MARITAL-STATUS                                    
                                        TO WS-F716-MARST(WS-RDBII-CTR)          
            MOVE F716-CDE-RELATIONSHIP  TO WS-F716-RLTNSP(WS-RDBII-CTR)         
            MOVE F716-NUM-HDB-REF       TO WS-F716-HDBREF(WS-RDBII-CTR)         
                                                                                
            IF F716-CDE-OCCUPIER = '0' OR '1'                                   
               MOVE 'Y'                 TO                                      
                                      WS-F716-APPLICANT(WS-RDBII-CTR)           
            ELSE                                                                
               MOVE 'N'                 TO                                      
                                      WS-F716-APPLICANT(WS-RDBII-CTR)           
            END-IF.                                                             
                                                                                
            IF F716-CDE-RELATIONSHIP = '00'                                     
               MOVE F716-NUM-RDB-MARITAL-STATUS                                 
                                        TO WS-F716-LSE1-MARST                   
               MOVE F716-NUM-NRIC       TO WS-F716-LSE1-NRIC                    
                                                                                
            END-IF                                                              
                                                                                
            SET WS-F558-IDX      TO 1.                                          
            SEARCH WS-F558-INFO                                                 
                   WHEN WS-F558-NRIC(WS-F558-IDX) = F716-NUM-NRIC               
                        PERFORM 5101-CHECK-DETAILS                              
                                          THRU 5101-EXIT                        
            END-SEARCH.                                                         
                                                                                
            PERFORM 3000-READ-BP13F716    THRU 3000-EXIT.                       
       5100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------------------------------------------------*             
       5101-CHECK-DETAILS.                                                      
      *-----------------------------------------------------------*             
      ** CHILD APPLYING WITH PARENT AND MARITAL STATUS OF CHILD IS              
      ** D/W/S/SINGLE AND CHILD NRIC EXIST IN RDB AS SPOUSE                     
                                                                                
            IF (WS-F558-APPLICANT(WS-F558-IDX) = 'Y') AND                       
               (WS-F558-RLTNSP(WS-F558-IDX) = '41') AND                         
               (WS-F558-MARST(WS-F558-IDX) = '3' OR '4' OR '5' OR '1')          
               AND (F716-CDE-RELATIONSHIP = '20')                               
      **       (F716-CDE-OCCUPIER = '2')                                        
               MOVE 'Y'                     TO WS-PRINT-STATUS                  
               MOVE '**'                    TO WS-F558-TAG(WS-F558-IDX)         
                                               WS-F716-TAG(WS-RDBII-CTR)        
            END-IF.                                                             
                                                                                
       5101-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------------------------------------------------*             
       6000-OUTPUT-P13F558A.                                                    
      *-----------------------------------------------------------*             
            IF WS-PRT-CTR <= WS-APPLN-CTR                                       
               WRITE P13F558A-REC         FROM WS-F558-REC(WS-PRT-CTR)          
            END-IF                                                              
                                                                                
            PERFORM 7000-PRINT-REPORT    THRU 7000-EXIT.                        
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------------------------------------------------*             
       7000-PRINT-REPORT.                                                       
      *-----------------------------------------------------------*             
            IF WS-LINE-CTR > 55                                                 
               ADD 1                        TO WS-PAGE-CTR                      
               MOVE WS-PAGE-CTR             TO L708-PAGE                        
               WRITE BP13L708-REC           FROM L708-HEAD1 AFTER PAGE          
               WRITE BP13L708-REC           FROM L708-HEAD2                     
               WRITE BP13L708-REC           FROM L708-HEAD3 AFTER 2             
               WRITE BP13L708-REC           FROM L708-HEAD4                     
               WRITE BP13L708-REC           FROM L708-HEAD5                     
               MOVE SPACES                  TO BP13L708-REC                     
               WRITE BP13L708-REC                                               
               MOVE 7                       TO WS-LINE-CTR                      
            END-IF.                                                             
                                                                                
            ADD 1                        TO WS-LINE-CTR                         
                                                                                
            IF WS-PRT-CTR <= WS-APPLN-CTR                                       
               MOVE WS-F558-TAG(WS-PRT-CTR) TO L708-APPLN-TAG                   
               MOVE WS-F558-NRIC(WS-PRT-CTR)                                    
                                            TO L708-APPLN-NRIC                  
               MOVE WS-F558-APPLICANT(WS-PRT-CTR)                               
                                            TO L708-NUM-APPLN                   
                                                                                
               MOVE '21'                    TO K060-SERIAL-NO                   
               MOVE WS-F558-MARST(WS-PRT-CTR)                                   
                                            TO K060-CODE                        
               PERFORM 6100-READ-BP13K060   THRU 6100-EXIT                      
               MOVE K060-DESC               TO L708-APPLN-MARST                 
                                                                                
               MOVE '35'                    TO K060-SERIAL-NO                   
               MOVE WS-F558-RLTNSP(WS-PRT-CTR)                                  
                                            TO K060-CODE                        
               PERFORM 6100-READ-BP13K060   THRU 6100-EXIT                      
               MOVE K060-DESC               TO L708-APPLN-RLTNSP                
            END-IF.                                                             
                                                                                
            IF WS-PRT-CTR <= WS-RDBII-CTR                                       
               MOVE WS-F716-NRIC(WS-PRT-CTR)                                    
                                            TO L708-RDBII-NRIC                  
               MOVE WS-F716-TAG(WS-PRT-CTR) TO L708-RDBII-TAG                   
               MOVE WS-F716-HDBREF(WS-PRT-CTR)                                  
                                            TO L708-RDBII-HDBREF                
               MOVE '21'                    TO K060-SERIAL-NO                   
               MOVE WS-F716-MARST(WS-PRT-CTR)                                   
                                            TO K060-CODE                        
               PERFORM 6100-READ-BP13K060   THRU 6100-EXIT                      
               MOVE K060-DESC               TO L708-RDBII-MARST                 
                                                                                
               MOVE '35'                    TO K060-SERIAL-NO                   
               MOVE WS-F716-RLTNSP(WS-PRT-CTR)                                  
                                            TO K060-CODE                        
               PERFORM 6100-READ-BP13K060   THRU 6100-EXIT                      
               MOVE K060-DESC               TO L708-RDBII-RLTNSP                
            END-IF.                                                             
                                                                                
            IF WS-PRT-CTR = 1                                                   
               WRITE BP13L708-REC           FROM L708-DETAIL1 AFTER 2           
               ADD 2                        TO WS-L708-WRITE-CTR                
            ELSE                                                                
               WRITE BP13L708-REC           FROM L708-DETAIL1                   
               ADD 1                        TO WS-L708-WRITE-CTR                
            END-IF.                                                             
                                                                                
            MOVE SPACES                  TO L708-DETAIL1.                       
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------------------------------------------------*             
       6100-READ-BP13K060.                                                      
      *-----------------------------------------------------------*             
            READ BP13K060.                                                      
            EVALUATE WS-K060-STATUS                                             
              WHEN 0                                                            
                CONTINUE                                                        
              WHEN 23                                                           
                MOVE K060-CODE       TO K060-DESC                               
                DISPLAY 'RECORD NOT FOUND IN BP13K060 '                         
                DISPLAY 'K060-KEY-FLD ' K060-KEY-FLD                            
              WHEN OTHER                                                        
                DISPLAY 'ERROR READING BP13K060 '                               
                DISPLAY 'FILE STATUS ' WS-K060-STATUS                           
                MOVE WS-K060-STATUS  TO RETURN-CODE                             
                GO TO 9000-CLOSE-FILES                                          
            END-EVALUATE.                                                       
                                                                                
       6100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------------------------------------------------*             
       9000-CLOSE-FILES.                                                        
      *-----------------------------------------------------------*             
                                                                                
            DISPLAY '____________________________________________'              
            DISPLAY ' '                                                         
            DISPLAY 'CONTROL TOTALS  PGM : BP13C708  DATE : ' L708-DATE         
            DISPLAY 'RECORDS READ FROM BP13F558  - ' WS-F558-READ-CTR           
            DISPLAY 'RECORDS READ FROM BP13F716  - ' WS-F716-READ-CTR           
            DISPLAY 'RECORDS PRINTED             - ' WS-L708-WRITE-CTR          
            DISPLAY '____________________________________________'              
                                                                                
            CLOSE      BP13F558                                                 
                       BP13K060                                                 
                       BP13F716                                                 
                       BP13L708                                                 
                       P13F558A.                                                
                                                                                
            IF WS-K060-STATUS NOT = 0                                           
               DISPLAY 'ERROR CLOSING BP17K060 '                                
               DISPLAY 'FILE STATUS ' WS-K060-STATUS                            
               MOVE WS-K060-STATUS    TO RETURN-CODE                            
            END-IF.                                                             
                                                                                
            STOP RUN.                                                           
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
