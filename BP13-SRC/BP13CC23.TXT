       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC23.                                                 
       AUTHOR. BALA AMUTHAN.                                                    
       DATE-WRITTEN.  19/05/06.                                                 
                                                                                
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE  :                                        *                
      *        1.  THIS PROGRAMME WILL GIVE THE REPORT FOR     *                
      *            DETAIL TRANSACTION POSTINGS.                *                
      *                                                        *                
      *     INPUT FILES:                                       *                
      *           1. BP13F136                                  *                
      *           2. BP13KD61                                  *                
      *                                                        *                
      *     PRINTING:                                          *                
      *           1. BP13LC23                                  *                
      *                                                        *                
      * BP132857 BA2 190506 - NEW PROGRAM                      *                
      * BP132888 BA3 170806 - INDICATE 'CR' IN AMT COLUMN.     *                
      *          BA3 080207 - INCLUDE INTEREST REBATE.         *                
      *          BA2 060307 - INCLUDE 'CR' FOR REBATE.         *                
      * BP133051 BA3 200407 - INCLUDE LUMP SUM PAYMENT ('LS')  *                
      *                       FOR CASH, CPF AND GIRO           *                
      *                     - INCLUDE INTEREST REBATE 'IT '    *                
      *                       FOR LUMP SUM PAYMENT (CPF-R)     *                
      * BP133052 BA3 070607 - CHANGE '-' TO 'CR' ON BAL COLUMN *                
      *              120607 - INCLUDE 'JV-ADJ'                 *                
      * BP133209 ED1 200208 - INCLUDE 'CPF-A'                  *                
      * BP133209 ED1 150408 - CHANGE 'JV-ADJ' INTO 'JV-N  '    *                
      * BP133535 JB8 100209 - INCLUDE '70' 'INS'               *                
      *                       FOR 69 'JVL' 'JVP' ONLY PROCESS  *                
      *                       IF CR-DR = 'C'                   *                
      * BP133588 LSB 060509 - TO CATER FOR 'JVI'               *                
      *                     - CHANGE 'JV-N' INTO 'JV    '    *                  
      * BP133662 JB8 061009 - TO PRINT ALL CRDR UNDER '69''JVI'*                
      * BP134245 JB8 150611 - TO CATER FOR ZERO LOAN BAL       *                
      * BP135161 IMC 120514 - CATER FOR JVL DEBIT TRANSACTIONS *                
      **********************************************************                
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F136 ASSIGN        TO BP13F136.                           
                                                                                
           SELECT BP13KD61 ASSIGN        TO BP13KD61                            
                           ACCESS MODE   IS RANDOM                              
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS KD61-KEY-FLD                        
                           FILE STATUS   IS WS-KD61-STATUS.                     
                                                                                
           SELECT BP13LC23 ASSIGN        TO BP13LC23.                           
                                                                                
           SELECT SY02F001 ASSIGN        TO SY02F001.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F136                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
                                                                                
       COPY BP13F136.                                                           
                                                                                
       FD  BP13KD61                                                             
           RECORD CONTAINS 250 CHARACTERS.                                      
       COPY BP13KD61.                                                           
                                                                                
       FD  BP13LC23                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  PRINT-REPORT-LIST              PIC X(132).                           
                                                                                
       COPY SY02F001.                                                           
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-KD61-STATUS             PIC 99        VALUE ZEROES.           
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-KD61-EOF                PIC X         VALUE 'N'.              
           05  WS-F136-EOF                PIC X         VALUE 'N'.              
                                                                                
       77  WS-KD61-AMT-OPEN-LOAN-BAL      PIC S9(7)V99  VALUE ZEROES.           
                                                                                
       01  WS-CNT-VAR.                                                          
           05  WS-CNT-BP13F136            PIC 9(5) COMP VALUE ZEROES.           
           05  WS-CNT-BP13KD61            PIC 9(5) COMP VALUE ZEROES.           
           05  WS-CNT-PRINT               PIC 9(5) COMP VALUE ZEROES.           
           05  WS-CNT-LINE                PIC 9(2)      VALUE 56.               
           05  WS-CNT-MAX-LINE            PIC 9(2)      VALUE 53.               
           05  WS-MATCH-FLG               PIC X(1)      VALUE 'N'.              
           05  WS-AMT-RECEIPT             PIC S9(7)V99  VALUE ZEROES.           
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05 WS-CCYY                     PIC 9999.                             
           05 WS-MM                       PIC 99.                               
           05 WS-DD                       PIC 99.                               
                                                                                
      ***************************************************************           
      *    WS-REPORT VARIABLE.                                      *           
      ***************************************************************           
       01  WS-REPORT-VAR.                                                       
           05  WS-CLS-BAL-TOT             PIC S9(10)V99  VALUE ZEROES.          
           05  WS-TOT-BAL-BF              PIC S9(10)V99  VALUE ZEROES.          
           05  WS-TOT-BAL-CF              PIC S9(10)V99  VALUE ZEROES.          
                                                                                
       01  WS-PAGE-SNO.                                                         
           05  WS-PAGE                    PIC 9(5) COMP  VALUE ZEROES.          
           05  WS-SNO                     PIC 9(4) COMP  VALUE 1.               
                                                                                
       01  WS-REGNO.                                                            
           05 WS-PREV-REGNO               PIC  X(8) VALUE SPACES.               
                                                                                
                                                                                
      ***************************************************************           
      *    WS-REPORT VARIBLE                                        *           
      ***************************************************************           
                                                                                
       01  WS-PRINT-MAIN-HD.                                                    
           05  FILLER              PIC X(08)  VALUE 'BP13LC23'.                 
           05  FILLER              PIC X(04)  VALUE SPACES.                     
           05  FILLER              PIC X(07)  VALUE 'HDBCAT3'.                  
           05  FILLER              PIC X(25)  VALUE SPACES.                     
           05  FILLER              PIC X(20)  VALUE                             
                                      'S Y S T E M   O F   '.                   
           05  FILLER              PIC X(19)  VALUE                             
                                      'C O M M I T M E N T'.                    
           05  FILLER              PIC X(15)  VALUE SPACES.                     
           05  FILLER              PIC X(07)  VALUE 'DATE : '.                  
           05  RPT-HEAD-DATE       PIC X(10)  VALUE SPACES.                     
           05  FILLER              PIC X(04)  VALUE SPACES.                     
           05  FILLER              PIC X(07)  VALUE 'PAGE : '.                  
           05  RPT-HEAD-PAGE       PIC Z(03)9 VALUE SPACES.                     
           05  FILLER              PIC X(12)  VALUE SPACES.                     
                                                                                
       01  WS-PRINT-REPORT-HD.                                                  
           05  FILLER              PIC X(44)  VALUE SPACES.                     
           05  FILLER              PIC X(32)  VALUE                             
               'DETAIL TRANSACTION POSTINGS FOR '.                              
           05  RPT-PRINT-DATE      PIC X(08).                                   
           05  FILLER              PIC X(48)  VALUE SPACES.                     
                                                                                
       01  WS-PRINT-REPORT-CL1.                                                 
           05  FILLER              PIC X(07)  VALUE SPACES.                     
           05  FILLER              PIC X(04)  VALUE 'S/NO'.                     
           05  FILLER              PIC X(04)  VALUE SPACES.                     
           05  FILLER              PIC X(06)  VALUE 'REG NO'.                   
           05  FILLER              PIC X(04)  VALUE SPACES.                     
           05  FILLER              PIC X(12)  VALUE 'SCH ACCNT NO'.             
           05  FILLER              PIC X(05)  VALUE SPACES.                     
           05  FILLER              PIC X(10)  VALUE 'BAL B/F($)'.               
           05  FILLER              PIC X(05)  VALUE SPACES.                     
           05  FILLER              PIC X(10)  VALUE 'TRANS DATE'.               
           05  FILLER              PIC X(03)  VALUE SPACES.                     
           05  FILLER              PIC X(04)  VALUE 'DESC'.                     
           05  FILLER              PIC X(19)  VALUE SPACES.                     
           05  FILLER              PIC X(09)  VALUE 'AMOUNT($)'.                
           05  FILLER              PIC X(09)  VALUE SPACES.                     
           05  FILLER              PIC X(10)  VALUE 'BAL C/F($)'.               
           05  FILLER              PIC X(11)  VALUE SPACES.                     
                                                                                
       01  WS-PRINT-REPORT-CL2.                                                 
           05  FILLER              PIC X(07)    VALUE SPACES.                   
           05  WS-RPT-SNO          PIC ZZZ9     VALUE ZEROES.                   
           05  FILLER              PIC X(03)    VALUE SPACES.                   
           05  WS-RPT-REGNO        PIC X(08)    VALUE SPACES.                   
           05  FILLER              PIC X(03)    VALUE SPACES.                   
           05  WS-RPT-SCH-NO       PIC X(11)    VALUE SPACES.                   
           05  FILLER              PIC X(03)    VALUE SPACES.                   
           05  WS-RPT-BAL-BF       PIC ZZ,ZZZ,ZZ9.99                            
                                                VALUE ZEROES.                   
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  WS-RPT-BAL-BF-SG    PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  WS-RPT-TRNS-DT      PIC X(10)    VALUE SPACES.                   
           05  FILLER              PIC X(03)    VALUE SPACES.                   
           05  WS-RPT-DESC         PIC X(15)    VALUE SPACES.                   
           05  FILLER              PIC X(03)    VALUE SPACES.                   
           05  WS-RPT-AMT          PIC Z,ZZZ,ZZ9.99                             
                                                VALUE ZEROES.                   
           05  FILLER              PIC X(1)     VALUE SPACES.                   
      *    05  WS-RPT-AMT-SG       PIC X(1)     VALUE SPACES.                   
           05  WS-RPT-AMT-CR       PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  WS-RPT-BAL-CPF      PIC Z,ZZZ,ZZZ,ZZ9.99                         
                                                VALUE ZEROES.                   
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  WS-RPT-BAL-CPF-SG   PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(08)    VALUE SPACES.                   
                                                                                
       01  WS-PRINT-ROLL.                                                       
           05  FILLER              PIC X(05)  VALUE SPACES.                     
           05  FILLER              PIC X(116) VALUE ALL '-'.                    
           05  FILLER              PIC X(09)  VALUE SPACES.                     
                                                                                
       01  WS-PRINT-TRAIL.                                                      
           05  FILLER              PIC X(07)  VALUE SPACES.                     
           05  FILLER              PIC X(12)  VALUE 'TOTAL (LOAN)'.             
           05  FILLER              PIC X(18)  VALUE SPACES.                     
           05  WS-RPT-OPN-TOTBAL   PIC Z,ZZZ,ZZZ,ZZ9.99                         
                                              VALUE ZEROES.                     
           05  WS-RPT-OPN-SG       PIC X(01)  VALUE SPACES.                     
           05  FILLER              PIC X(51)  VALUE SPACES.                     
           05  WS-RPT-AMT-CLS      PIC Z,ZZZ,ZZZ,ZZ9.99                         
                                              VALUE ZEROES.                     
           05  WS-RPT-AMT-CLS-SG   PIC X(01)  VALUE SPACES.                     
           05  FILLER              PIC X(16)  VALUE SPACES.                     
                                                                                
       01  WS-PRINT-TRAIL-LINE.                                                 
           05  FILLER              PIC X(40)  VALUE SPACES.                     
           05  FILLER              PIC X(13)  VALUE ALL '-'.                    
           05  FILLER              PIC X(55)  VALUE SPACES.                     
           05  FILLER              PIC X(13)  VALUE ALL '-'.                    
           05  FILLER              PIC X(12)  VALUE SPACES.                     
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
                                                                                
      ******************************************************************        
      *        MAIN LINE PROCESS                                       *        
      ******************************************************************        
       MAIN-PROCESS.                                                            
                                                                                
           PERFORM 0000-INIT-SETUP              THRU 0000-EXIT.                 
           PERFORM 0100-READ-BP13F136           THRU 0100-EXIT.                 
           PERFORM 0200-MAIN-PROCESS            THRU 0200-EXIT                  
                   UNTIL WS-F136-EOF  = 'Y'.                                    
           IF WS-PREV-REGNO NOT = SPACES AND                                    
              WS-MATCH-FLG = 'Y'                                                
              PERFORM 0500-WRITE-CLOSE-BAL      THRU 0500-EXIT                  
           END-IF.                                                              
           PERFORM 0800-WRITE-TRAIL             THRU 0800-EXIT.                 
           PERFORM 1000-CLOSE-ROUTINE           THRU 1000-EXIT.                 
                                                                                
      ***************************************************************           
      *    OPENING FILES SECTION                                    *           
      ***************************************************************           
                                                                                
       0000-INIT-SETUP.                                                         
           OPEN INPUT  BP13F136                                                 
                       BP13KD61                                                 
                       SY02F001                                                 
                OUTPUT BP13LC23.                                                
                                                                                
           IF WS-KD61-STATUS NOT = 0  AND                                       
              WS-KD61-STATUS NOT = 97                                           
              DISPLAY ' OPENING ERROR, BP13KD61 STATUS ' WS-KD61-STATUS         
              MOVE WS-KD61-STATUS           TO RETURN-CODE                      
              PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT.                        
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-CURRENT           TO WS-CURRENT-DATE.                  
           MOVE SPACES                     TO WS-PRINT-REPORT-CL2.              
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
      *    READ SEQUENTIAL FILE BP13F136 .                       *              
      ************************************************************              
       0100-READ-BP13F136.                                                      
                                                                                
           READ BP13F136                                                        
                AT END MOVE 'Y'         TO WS-F136-EOF                          
                GO TO 0100-EXIT.                                                
           ADD  +1                       TO WS-CNT-BP13F136.                    
                                                                                
       0100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       0200-MAIN-PROCESS.                                                       
           IF F136-NUM-REGN NOT = WS-PREV-REGNO                                 
              IF WS-PREV-REGNO NOT = SPACES AND                                 
                 WS-MATCH-FLG = 'Y'                                             
                PERFORM 0500-WRITE-CLOSE-BAL THRU 0500-EXIT                     
                MOVE 'N'     TO  WS-MATCH-FLG                                   
              END-IF                                                            
              MOVE  WS-SNO                   TO WS-RPT-SNO                      
              MOVE F136-NUM-REGN             TO WS-RPT-REGNO                    
              MOVE F136-NUM-SCH-ACCT         TO WS-RPT-SCH-NO                   
              PERFORM 0300-READ-BP13KD61     THRU 0300-EXIT                     
              MOVE F136-NUM-REGN             TO  WS-PREV-REGNO                  
           END-IF.                                                              
                                                                                
           PERFORM 0400-CHECK-TRANS          THRU 0400-EXIT.                    
           PERFORM 0100-READ-BP13F136        THRU 0100-EXIT.                    
                                                                                
       0200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
      *    READ BP13KD61 FILE FOR OPENING BALANCE.               *              
      ************************************************************              
                                                                                
       0300-READ-BP13KD61.                                                      
           MOVE SPACES                  TO BP13KD61-REC.                        
           STRING F136-NUM-REGN               DELIMITED BY SIZE                 
                  F001-DTE-LAST-DAY-PREV(1:6) DELIMITED BY SIZE                 
                  '01'  DELIMITED BY SIZE                                       
                  '001' DELIMITED BY SIZE INTO KD61-KEY-FLD.                    
                                                                                
           READ BP13KD61                                                        
           EVALUATE WS-KD61-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 PERFORM  0310-CHECK-OPENING-BALANCE  THRU 0310-EXIT            
              WHEN 23                                                           
                 DISPLAY 'REGNO NOT FOUND AT BP13KD61 - '                       
                          F136-NUM-REGN                                         
                 MOVE ZEROES    TO  KD61-AMT-OPEN-LOAN-BAL                      
                 PERFORM  0310-CHECK-OPENING-BALANCE  THRU 0310-EXIT            
              WHEN OTHER                                                        
                 DISPLAY 'ERROR WITH READ FILE-BP13KD61. STATUS IS '            
                          WS-KD61-STATUS                                        
                 PERFORM 1000-CLOSE-ROUTINE  THRU 1000-EXIT                     
           END-EVALUATE.                                                        
           ADD  +1                            TO   WS-CNT-BP13KD61.             
                                                                                
       0300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       0310-CHECK-OPENING-BALANCE.                                              
           IF KD61-AMT-OPEN-LOAN-BAL NOT NUMERIC                                
              MOVE ZEROES                TO KD61-AMT-OPEN-LOAN-BAL              
           END-IF.                                                              
                                                                                
           IF KD61-AMT-OPEN-LOAN-BAL < 0                                        
              MOVE 'CR'                  TO WS-RPT-BAL-BF-SG                    
           END-IF.                                                              
                                                                                
           MOVE  KD61-AMT-OPEN-LOAN-BAL  TO WS-CLS-BAL-TOT                      
                                            WS-RPT-BAL-BF.                      
                                                                                
           COMPUTE WS-TOT-BAL-BF = WS-TOT-BAL-BF +                      -BAL.   
                                   KD61-AMT-OPEN-LOAN-BAL.                      
                                                                                
       0310-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
      *    TO CHECK THE TRANSACTION TYPE.                        *              
      ************************************************************              
       0400-CHECK-TRANS.                                                        
           IF F136-DTE-TRANS IS NOT NUMERIC                                     
              MOVE ZEROES   TO  F136-DTE-TRANS                                  
           END-IF.                                                              
                                                                                
           EVALUATE F136-CDE-TRANS-TYPE ALSO                                    
                    F136-CDE-PAYMENT-TYPE                                       
                                                                                
             WHEN '11' ALSO 'INS'                                               
                MOVE 'CASH'              TO WS-RPT-DESC                         
                COMPUTE WS-CLS-BAL-TOT = WS-CLS-BAL-TOT -                       
                                       F136-AMT-RECEIPT                         
                MOVE 'CR'                TO WS-RPT-AMT-CR                       
                MOVE 'Y'                 TO WS-MATCH-FLG                        
                PERFORM 0410-MOVE-DTE-AMT THRU 0410-EXIT                        
                                                                                
             WHEN '11' ALSO 'LS '                                               
                MOVE 'CASH-L'            TO WS-RPT-DESC                         
                COMPUTE WS-CLS-BAL-TOT = WS-CLS-BAL-TOT -                       
                                       F136-AMT-RECEIPT                         
                MOVE 'CR'                TO WS-RPT-AMT-CR                       
                MOVE 'Y'                 TO WS-MATCH-FLG                        
                PERFORM 0410-MOVE-DTE-AMT THRU 0410-EXIT                        
                                                                                
             WHEN  '12' ALSO 'INS'                                              
                IF F136-NUM-DOC = 'ARR-L'                                       
                   MOVE 'CPF-A'             TO WS-RPT-DESC                      
                ELSE                                                            
                   MOVE 'CPF'               TO WS-RPT-DESC                      
                END-IF                                                          
                COMPUTE WS-CLS-BAL-TOT = WS-CLS-BAL-TOT -                       
                                       F136-AMT-RECEIPT                         
                MOVE 'CR'                TO WS-RPT-AMT-CR                       
                MOVE 'Y'                 TO WS-MATCH-FLG                        
                PERFORM 0410-MOVE-DTE-AMT THRU 0410-EXIT                        
                                                                                
             WHEN  '12' ALSO 'LS '                                              
                MOVE 'CPF-L'             TO WS-RPT-DESC                         
                COMPUTE WS-CLS-BAL-TOT = WS-CLS-BAL-TOT -                       
                                       F136-AMT-RECEIPT                         
                MOVE 'CR'                TO WS-RPT-AMT-CR                       
                MOVE 'Y'                 TO WS-MATCH-FLG                        
                PERFORM 0410-MOVE-DTE-AMT THRU 0410-EXIT                        
                                                                                
             WHEN '13' ALSO 'INS'                                               
             WHEN '14' ALSO 'INS'                                               
             WHEN '15' ALSO 'INS'                                               
             WHEN '16' ALSO 'INS'                                               
                MOVE 'VR  '              TO WS-RPT-DESC                         
                COMPUTE WS-CLS-BAL-TOT = WS-CLS-BAL-TOT +                       
                                       F136-AMT-RECEIPT                         
                MOVE 'Y'                 TO WS-MATCH-FLG                        
                PERFORM 0410-MOVE-DTE-AMT THRU 0410-EXIT                        
                                                                                
             WHEN '62' ALSO 'IT '                                               
                MOVE 'IT(MONTHLY)  '     TO WS-RPT-DESC                         
                COMPUTE WS-CLS-BAL-TOT = WS-CLS-BAL-TOT +                       
                                       F136-AMT-RECEIPT                         
                MOVE 'Y'                 TO WS-MATCH-FLG                        
                PERFORM 0410-MOVE-DTE-AMT THRU 0410-EXIT                        
                                                                                
             WHEN '64' ALSO 'INS'                                               
                MOVE 'GIRO'              TO WS-RPT-DESC                         
                COMPUTE WS-CLS-BAL-TOT = WS-CLS-BAL-TOT -                       
                                       F136-AMT-RECEIPT                         
                MOVE 'CR'                TO WS-RPT-AMT-CR                       
                MOVE 'Y'                 TO WS-MATCH-FLG                        
                PERFORM 0410-MOVE-DTE-AMT THRU 0410-EXIT                        
                                                                                
             WHEN '64' ALSO 'LS '                                               
                MOVE 'GIRO-L'            TO WS-RPT-DESC                         
                COMPUTE WS-CLS-BAL-TOT = WS-CLS-BAL-TOT -                       
                                       F136-AMT-RECEIPT                         
                MOVE 'CR'                TO WS-RPT-AMT-CR                       
                MOVE 'Y'                 TO WS-MATCH-FLG                        
                PERFORM 0410-MOVE-DTE-AMT THRU 0410-EXIT                        
                                                                                
             WHEN '66' ALSO 'LON'                                               
                MOVE 'NEW LOAN'          TO WS-RPT-DESC                         
                COMPUTE WS-CLS-BAL-TOT = WS-CLS-BAL-TOT +                       
                                       F136-AMT-RECEIPT                         
                MOVE 'Y'                 TO WS-MATCH-FLG                        
                PERFORM 0410-MOVE-DTE-AMT THRU 0410-EXIT                        
                                                                                
             WHEN '67' ALSO 'IT '                                               
                MOVE 'IT(NEW LOAN)'      TO WS-RPT-DESC                         
                COMPUTE WS-CLS-BAL-TOT = WS-CLS-BAL-TOT +                       
                                        F136-AMT-RECEIPT                        
                MOVE 'Y'                 TO WS-MATCH-FLG                        
                PERFORM 0410-MOVE-DTE-AMT THRU 0410-EXIT                        
                                                                                
             WHEN '68' ALSO 'IT '                                               
                MOVE 'IT(REBATE)  '      TO WS-RPT-DESC                         
                COMPUTE WS-CLS-BAL-TOT = WS-CLS-BAL-TOT -                       
                                        F136-AMT-RECEIPT                        
                MOVE 'CR'                TO WS-RPT-AMT-CR                       
                MOVE 'Y'                 TO WS-MATCH-FLG                        
                PERFORM 0410-MOVE-DTE-AMT THRU 0410-EXIT                        
                                                                                
             WHEN '69' ALSO 'JVL'                                               
                IF F136-CDE-CRDR = 'C'                                          
                   MOVE 'JV          '   TO WS-RPT-DESC                         
                   COMPUTE WS-CLS-BAL-TOT = WS-CLS-BAL-TOT -                    
                                           F136-AMT-RECEIPT                     
                   MOVE 'CR'             TO WS-RPT-AMT-CR                       
                   MOVE 'Y'              TO WS-MATCH-FLG                        
                   PERFORM 0410-MOVE-DTE-AMT THRU 0410-EXIT                     
                ELSE                                                            
                   MOVE 'JV          '   TO WS-RPT-DESC                         
                   INITIALIZE WS-AMT-RECEIPT                                    
                                                                                
                   IF F136-AMT-RECEIPT < 0                                      
                      COMPUTE WS-AMT-RECEIPT = F136-AMT-RECEIPT * -1            
                   ELSE                                                         
                      MOVE F136-AMT-RECEIPT TO WS-AMT-RECEIPT                   
                   END-IF                                                       
                                                                                
                   COMPUTE WS-CLS-BAL-TOT = WS-CLS-BAL-TOT +                    
                                            WS-AMT-RECEIPT                      
                   MOVE 'Y'              TO WS-MATCH-FLG                        
                   PERFORM 0410-MOVE-DTE-AMT THRU 0410-EXIT                     
                END-IF                                                          
             WHEN '69' ALSO 'JVP'                                               
                IF F136-CDE-CRDR = 'C'                                          
                   MOVE 'JV          '   TO WS-RPT-DESC                         
                   COMPUTE WS-CLS-BAL-TOT = WS-CLS-BAL-TOT -                    
                                           F136-AMT-RECEIPT                     
                   MOVE 'CR'             TO WS-RPT-AMT-CR                       
                   MOVE 'Y'              TO WS-MATCH-FLG                        
                   PERFORM 0410-MOVE-DTE-AMT THRU 0410-EXIT                     
                END-IF                                                          
             WHEN '69' ALSO 'JVI'                                               
                IF F136-CDE-CRDR = 'C'                                          
                   MOVE 'CR'             TO WS-RPT-AMT-CR                       
                END-IF                                                          
                MOVE 'JV-I        '      TO WS-RPT-DESC                         
                COMPUTE WS-CLS-BAL-TOT = WS-CLS-BAL-TOT -                       
                                         F136-AMT-RECEIPT                       
                MOVE 'Y'                 TO WS-MATCH-FLG                        
                PERFORM 0410-MOVE-DTE-AMT THRU 0410-EXIT                        
                                                                                
             WHEN '70' ALSO 'INS'                                               
                IF F136-AMT-RECEIPT NOT = ZEROES                                
                   MOVE 'DBSS-TRF    '      TO WS-RPT-DESC                      
                   COMPUTE WS-CLS-BAL-TOT = WS-CLS-BAL-TOT -                    
                                           F136-AMT-RECEIPT                     
                   MOVE 'CR'                TO WS-RPT-AMT-CR                    
                   MOVE 'Y'                 TO WS-MATCH-FLG                     
                   PERFORM 0410-MOVE-DTE-AMT THRU 0410-EXIT                     
                END-IF                                                          
                                                                                
             WHEN 'ZZ' ALSO 'ZZZ'                                               
                   MOVE ' -----      '      TO WS-RPT-DESC                      
                   COMPUTE WS-CLS-BAL-TOT = WS-CLS-BAL-TOT -                    
                                           F136-AMT-RECEIPT                     
                   MOVE 'Y'                 TO WS-MATCH-FLG                     
                   PERFORM 0410-MOVE-DTE-AMT THRU 0410-EXIT                     
                                                                                
           END-EVALUATE.                                                        
                                                                                
       0400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       0410-MOVE-DTE-AMT.                                                       
           IF F136-AMT-RECEIPT NOT NUMERIC                                      
              MOVE ZEROES   TO F136-AMT-RECEIPT                                 
           END-IF.                                                              
                                                                                
           IF F136-AMT-RECEIPT NOT = 0                                          
      *       IF F136-AMT-RECEIPT < 0                                           
      *          MOVE '-'                   TO WS-RPT-AMT-SG                    
      *       END-IF                                                            
              MOVE F136-AMT-RECEIPT         TO WS-RPT-AMT                       
              IF F136-DTE-TRANS NOT = ZEROES AND LOW-VALUES                     
                 STRING F136-DTE-TRANS(7:2) '/'                                 
                        F136-DTE-TRANS(5:2) '/'                                 
                        F136-DTE-TRANS(1:4)                                     
                          DELIMITED BY SIZE INTO WS-RPT-TRNS-DT                 
              END-IF                                                            
              PERFORM 0600-WRITE-REPORT     THRU 0600-EXIT                      
              ADD +1                        TO WS-CNT-PRINT                     
           END-IF.                                                              
                                                                                
       0410-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       0500-WRITE-CLOSE-BAL.                                                    
           IF WS-CLS-BAL-TOT < 0                                                
                 MOVE 'CR'   TO  WS-RPT-BAL-CPF-SG                              
           END-IF.                                                              
           COMPUTE WS-TOT-BAL-CF = WS-TOT-BAL-CF + WS-CLS-BAL-TOT.      -BAL.   
           MOVE WS-CLS-BAL-TOT              TO WS-RPT-BAL-CPF.                  
           PERFORM 0600-WRITE-REPORT        THRU 0600-EXIT.                     
           ADD +1                           TO WS-SNO.                          
                                                                                
       0500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************************************************              
      *    PRINTING RECORDS.                                     *              
      ************************************************************              
       0600-WRITE-REPORT.                                                       
      *PRINT HEADING.                                                           
           IF WS-CNT-LINE >= WS-CNT-MAX-LINE                                    
              PERFORM 0700-WRITE-HEAD       THRU 0700-EXIT                      
           END-IF.                                                              
           MOVE WS-PRINT-REPORT-CL2         TO PRINT-REPORT-LIST.               
           WRITE PRINT-REPORT-LIST.                                             
           MOVE SPACES                      TO WS-PRINT-REPORT-CL2              
                                               PRINT-REPORT-LIST.               
           ADD +1                           TO WS-CNT-LINE.                     
                                                                                
       0600-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
      *    PRINTING HEADER.                                      *              
      ************************************************************              
       0700-WRITE-HEAD.                                                         
           MOVE SPACES                TO PRINT-REPORT-LIST.                     
           WRITE PRINT-REPORT-LIST AFTER PAGE.                                  
           WRITE PRINT-REPORT-LIST.                                             
                                                                                
           ADD +1                           TO   WS-PAGE.                       
           STRING WS-DD    '/'                                                  
                  WS-MM    '/'                                                  
                  WS-CCYY                                                       
              DELIMITED BY SIZE             INTO RPT-HEAD-DATE.                 
                                                                                
           STRING F001-TXT-PREV-MTH(1:3)  ' '                                   
                  F001-DTE-LAST-DAY-PREV(1:4)                                   
                  DELIMITED BY SIZE INTO RPT-PRINT-DATE.                        
                                                                                
           MOVE WS-PAGE                     TO   RPT-HEAD-PAGE.                 
           MOVE WS-PRINT-MAIN-HD            TO   PRINT-REPORT-LIST.             
           WRITE PRINT-REPORT-LIST.                                             
                                                                                
           MOVE SPACES                TO PRINT-REPORT-LIST.                     
           WRITE PRINT-REPORT-LIST.                                             
                                                                                
           MOVE WS-PRINT-REPORT-HD    TO PRINT-REPORT-LIST.                     
           WRITE PRINT-REPORT-LIST.                                             
                                                                                
           MOVE SPACES                TO PRINT-REPORT-LIST.                     
           WRITE PRINT-REPORT-LIST.                                             
                                                                                
           MOVE WS-PRINT-REPORT-CL1   TO PRINT-REPORT-LIST.                     
           WRITE PRINT-REPORT-LIST.                                             
                                                                                
           MOVE WS-PRINT-ROLL         TO PRINT-REPORT-LIST.                     
           WRITE PRINT-REPORT-LIST.                                             
           MOVE   08                  TO  WS-CNT-LINE.                          
                                                                                
       0700-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       0800-WRITE-TRAIL.                                                        
           MOVE SPACES                TO  PRINT-REPORT-LIST.                    
                                                                                
           IF WS-TOT-BAL-BF < 0                                                 
              MOVE '-'                TO  WS-RPT-OPN-SG                         
           END-IF.                                                              
           MOVE WS-TOT-BAL-BF         TO  WS-RPT-OPN-TOTBAL.                    
           IF WS-TOT-BAL-CF < 0                                                 
              MOVE '-'                TO  WS-RPT-AMT-CLS-SG                     
           END-IF.                                                              
           MOVE WS-TOT-BAL-CF         TO  WS-RPT-AMT-CLS.                       
           MOVE WS-PRINT-TRAIL-LINE   TO  PRINT-REPORT-LIST.                    
           WRITE PRINT-REPORT-LIST.                                             
                                                                                
           MOVE WS-PRINT-TRAIL        TO  PRINT-REPORT-LIST.                    
           WRITE PRINT-REPORT-LIST.                                             
                                                                                
           MOVE WS-PRINT-TRAIL-LINE   TO  PRINT-REPORT-LIST.                    
           WRITE PRINT-REPORT-LIST.                                             
                                                                                
       0800-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       1000-CLOSE-ROUTINE.                                                      
                                                                                
           DISPLAY '******  BP13CC23 *************'.                            
           DISPLAY 'NO OF F136 READ  (BP13F136) : '                             
                                               WS-CNT-BP13F136.                 
           DISPLAY 'NO OF KD61 READ  (BP13KD61) : '                             
                                               WS-CNT-BP13KD61.                 
           DISPLAY 'NO OF RECORDS PRINT         : '                             
                                               WS-CNT-PRINT.                    
                                                                                
           CLOSE BP13F136                                                       
                 BP13KD61                                                       
                 SY02F001                                                       
                 BP13LC23.                                                      
                                                                                
       1000-EXIT.                                                               
           STOP RUN.                                                            
