      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
                                                                                
       PROGRAM-ID.    BP13C24H.                                                 
       AUTHOR.        RALSTON JAMES ELIEZER B. MEDRANO.                         
       DATE-WRITTEN.  13/06/2011.                                               
                                                                                
      *===============================================================*         
      *                                                               *         
      *    SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                 *         
      *                                                               *         
      *    SYSTEM ID   :  BP13                                        *         
      *                                                               *         
      *    OBJECTIVE   :  THIS PROGRAM PRINTS THE BOOKING APPOINTMENTS*         
      *                                                               *         
      *    INPUT FILES :  1. BP13F205 - CONTROL RECORDS FILE          *         
      *                   2. BP13K200 - BOOKING APPOINTMENT FILE      *         
      *                   3. BP13K800 - SOC APPLN MASTER FILE         *         
      *                                                               *         
      *    OUTPUT LIST :  1. BP13L24H - REPORT                        *         
      *                                                               *         
      *                                                               *         
      *    CHGE-NO    BY     DATE   DESCRIPTION                       *         
      *    --------   ----  ------  ------------------------------    *         
      *    BP134253   RJE1  130611  NEW PROGRAM                       *         
      *===============================================================*         
                                                                                
                                                                                
      ***********************                                                   
       ENVIRONMENT DIVISION.                                                    
      ***********************                                                   
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205 ASSIGN       TO BP13F205.                            
           SELECT BP13F200 ASSIGN       TO BP13F200.                            
           SELECT BP13K800 ASSIGN       TO BP13K800                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K800-NUM-REGN                        
                           FILE STATUS  IS WS-K800-STATUS.                      
           SELECT BP13L24H ASSIGN       TO BP13L24H.                            
                                                                                
                                                                                
      ****************                                                          
       DATA DIVISION.                                                           
      ****************                                                          
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F205                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 80  CHARACTERS.                                     
       COPY BP13F205.                                                           
                                                                                
       FD   BP13F200                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13F200.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13L24H                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 100 CHARACTERS.                                     
       01   PRINT-REC                       PIC X(100).                         
                                                                                
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-INDICATORS.                                                       
           05  FILLER                       PIC X(10)  VALUE                    
               'INDICATORS'.                                                    
           05  WS-K800-STATUS               PIC 99     VALUE ZEROS.             
           05  WS-F205-EOF                  PIC X      VALUE 'N'.               
           05  WS-F200-EOF                  PIC X      VALUE 'N'.               
                                                                                
       01  WS-MATCH-FIELDS.                                                     
           05  WS-F200-MATCH-FIELDS.                                            
               10  WS-F200-DTE-ALLOC        PIC X(8).                           
               10  WS-F200-NUM-NT-ZONE      PIC X(3).                           
               10  WS-F200-NUM-FLAT-TYPE    PIC X(2).                           
           05  WS-F205-MATCH-FIELDS.                                            
               10  WS-F205-DTE-ALLOCN       PIC X(8).                           
               10  WS-F205-NUM-NT-ZONE      PIC X(3).                           
               10  WS-F205-NUM-FLAT-TYPE    PIC X(2).                           
                                                                                
       01  WS-COUNTERS.                                                         
           05  FILLER                       PIC X(08)  VALUE                    
               'COUNTERS'.                                                      
           05  WS-F200-READ                 PIC 9(8)   VALUE ZEROES.            
           05  WS-K800-READ                 PIC 9(8)   VALUE ZEROES.            
           05  WS-L24H-WRITE                PIC 9(8)   VALUE ZEROES.            
                                                                                
       01  WS-SAVE-AREA.                                                        
           05  FILLER                       PIC X(09)  VALUE                    
               'SAVE AREA'.                                                     
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  FILLER                       PIC X(09)  VALUE                    
               'DATE VARS'.                                                     
                                                                                
                                                                                
      *---------------------------------------------------------------*         
      *        P R I N T     L A Y O U T                              *         
      *---------------------------------------------------------------*         
                                                                                
       01  L24H-TITLE.                                                          
           05  FILLER                       PIC X(4)   VALUE 'NRIC'.            
           05  FILLER                       PIC X(3)   VALUE SPACES.            
           05  FILLER                       PIC X      VALUE ';'.               
           05  FILLER                       PIC X(7)   VALUE 'MSGTYPE'.         
           05  FILLER                       PIC X      VALUE ';'.               
           05  FILLER                       PIC X(5)   VALUE 'QUEUE'.           
           05  FILLER                       PIC X      VALUE ';'.               
           05  FILLER                       PIC X(9)                            
                                            VALUE 'APPT DATE'.                  
           05  FILLER                       PIC X      VALUE ';'.               
           05  FILLER                       PIC X(9)                            
                                            VALUE 'APPT TIME'.                  
       01  L24H-DATA1.                                                          
           05  L24H-NRIC                    PIC X(7)   VALUE SPACES.            
           05  FILLER                       PIC X      VALUE ';'.               
           05  L24H-MSGTPE                  PIC X      VALUE '2'.               
           05  FILLER                       PIC X(6)   VALUE SPACES.            
           05  FILLER                       PIC X      VALUE ';'.               
           05  L24H-QUEUE                   PIC X(5)   VALUE SPACES.            
           05  FILLER                       PIC X      VALUE ';'.               
           05  L24H-APPTDTE                 PIC X(8)   VALUE SPACES.            
           05  FILLER                       PIC X      VALUE SPACES.            
           05  FILLER                       PIC X      VALUE ';'.               
           05  L24H-APPTTME                 PIC X(4)   VALUE SPACES.            
                                                                                
       01  L24H-DATA2.                                                          
           05  L24H-NRIC2                   PIC X(7)   VALUE SPACES.            
           05  FILLER                       PIC X      VALUE ';'.               
           05  L24H-MSGTPE2                 PIC X      VALUE '2'.               
           05  FILLER                       PIC X(6)   VALUE SPACES.            
           05  FILLER                       PIC X      VALUE ';'.               
           05  L24H-QUEUE2                  PIC X(5)   VALUE SPACES.            
           05  FILLER                       PIC X      VALUE ';'.               
           05  L24H-APPTDTE2                PIC X(8)   VALUE SPACES.            
           05  FILLER                       PIC X      VALUE SPACES.            
           05  FILLER                       PIC X      VALUE ';'.               
           05  L24H-APPTTME2                PIC X(4)   VALUE SPACES.            
                                                                                
       01  L24H-DATA3.                                                          
           05  L24H-NRIC3                   PIC X(7)   VALUE SPACES.            
           05  FILLER                       PIC X      VALUE ';'.               
           05  L24H-MSGTPE3                 PIC X      VALUE '2'.               
           05  FILLER                       PIC X(6)   VALUE SPACES.            
           05  FILLER                       PIC X      VALUE ';'.               
           05  L24H-QUEUE3                  PIC X(5)   VALUE SPACES.            
           05  FILLER                       PIC X      VALUE ';'.               
           05  L24H-APPTDTE3                PIC X(8)   VALUE SPACES.            
           05  FILLER                       PIC X      VALUE SPACES.            
           05  FILLER                       PIC X      VALUE ';'.               
           05  L24H-APPTTME3                PIC X(4)   VALUE SPACES.            
                                                                                
       01  L24H-DATA4.                                                          
           05  L24H-NRIC4                   PIC X(7)   VALUE SPACES.            
           05  FILLER                       PIC X      VALUE ';'.               
           05  L24H-MSGTPE4                 PIC X      VALUE '2'.               
           05  FILLER                       PIC X(6)   VALUE SPACES.            
           05  FILLER                       PIC X      VALUE ';'.               
           05  L24H-QUEUE4                  PIC X(5)   VALUE SPACES.            
           05  FILLER                       PIC X      VALUE ';'.               
           05  L24H-APPTDTE4                PIC X(8)   VALUE SPACES.            
           05  FILLER                       PIC X      VALUE SPACES.            
           05  FILLER                       PIC X      VALUE ';'.               
           05  L24H-APPTTME4                PIC X(4)   VALUE SPACES.            
                                                                                
                                                                                
                                                                                
      *********************                                                     
       PROCEDURE DIVISION.                                                      
      *********************                                                     
                                                                                
                                                                                
      ******************************************************************        
       000-MAIN-ROUTINE.                                                        
      ******************************************************************        
                                                                                
           PERFORM 100-OPEN-FILES-RTN      THRU   100-EXIT.                     
           PERFORM 200-READ-BP13F205       THRU   200-EXIT.                     
           MOVE    SPACES                  TO     WS-F200-MATCH-FIELDS.         
           PERFORM 300-PROCESS-F200        THRU   300-EXIT                      
                       UNTIL WS-F205-EOF = 'Y' OR                               
                             WS-F200-EOF = 'Y'.                                 
           PERFORM 900-CLOSE-FILES-RTN     THRU   900-EXIT.                     
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       100-OPEN-FILES-RTN.                                                      
      ******************************************************************        
           OPEN INPUT  BP13F205                                                 
                       BP13F200                                                 
                       BP13K800                                                 
                OUTPUT BP13L24H.                                                
                                                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BP13K800 = ' WS-K800-STATUS              
              MOVE    WS-K800-STATUS  TO RETURN-CODE                            
              PERFORM 900-CLOSE-FILES-RTN.                                      
                                                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       200-READ-BP13F205.                                                       
      ******************************************************************        
                                                                                
           READ BP13F205                                                        
                AT END MOVE 'Y' TO WS-F205-EOF                                  
                GO TO  200-EXIT.                                                
                                                                                
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       300-PROCESS-F200.                                                        
      ******************************************************************        
                                                                                
                                                                                
           MOVE F205-DTE-ALLOCN          TO WS-F205-DTE-ALLOCN.                 
           MOVE F205-NUM-NT-ZONE         TO WS-F205-NUM-NT-ZONE.                
           MOVE F205-NUM-FLAT-TYPE       TO WS-F205-NUM-FLAT-TYPE.              
                                                                                
           PERFORM UNTIL WS-F200-EOF = 'Y'                                      
                   OR    WS-F200-MATCH-FIELDS = WS-F205-MATCH-FIELDS            
              PERFORM 400-READ-F200           THRU  400-EXIT                    
           END-PERFORM.                                                         
                                                                                
           PERFORM 500-READ-K800           THRU  500-EXIT                       
             UNTIL WS-F200-EOF = 'Y'.                                           
                                                                                
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       400-READ-F200.                                                           
      ******************************************************************        
                                                                                
           READ BP13F200                                                        
              AT END MOVE 'Y'   TO WS-F200-EOF                                  
              IF WS-F200-READ = 0                                               
                 GO TO 900-CLOSE-FILES-RTN                                      
              END-IF                                                            
              GO TO  400-EXIT                                                   
           END-READ.                                                            
                                                                                
                                                                                
           ADD     1            TO WS-F200-READ.                                
                                                                                
           MOVE SPACES          TO L24H-NRIC   L24H-MSGTPE                      
                                   L24H-QUEUE  L24H-APPTDTE                     
                                   L24H-APPTTME                                 
                                   L24H-NRIC2  L24H-MSGTPE2                     
                                   L24H-QUEUE2 L24H-APPTDTE2                    
                                   L24H-APPTTME2                                
                                   L24H-NRIC3  L24H-MSGTPE3                     
                                   L24H-QUEUE3 L24H-APPTDTE3                    
                                   L24H-APPTTME3                                
                                   L24H-NRIC4  L24H-MSGTPE4                     
                                   L24H-QUEUE4 L24H-APPTDTE4                    
                                   L24H-APPTTME4.                               
                                                                                
           MOVE F200-DTE-ALLOC        TO WS-F200-DTE-ALLOC.                     
           MOVE F200-NUM-NT-ZONE      TO WS-F200-NUM-NT-ZONE.                   
           MOVE F200-NUM-FLAT-TYPE    TO WS-F200-NUM-FLAT-TYPE.                 
                                                                                
           IF WS-F200-MATCH-FIELDS  > WS-F205-MATCH-FIELDS                      
              MOVE 'Y'          TO WS-F200-EOF                                  
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       500-READ-K800.                                                           
      ******************************************************************        
                                                                                
           MOVE F200-NUM-REGN  TO   K800-NUM-REGN.                              
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = 00                                               
              ADD 1 TO WS-K800-READ                                             
              MOVE K800-NUM-NRIC1(2:7) TO L24H-NRIC                             
              PERFORM 800-PROCESS-PRINT  THRU 800-EXIT                          
           ELSE                                                                 
           IF WS-K800-STATUS = 23                                               
              DISPLAY 'RECORD NOT FOUND IN BP13K800 ' WS-K800-STATUS            
              DISPLAY '       K800-NUM-REGN = ' K800-NUM-REGN                   
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K800 = ' WS-K800-STATUS                
              DISPLAY '      K800-NUM-REGN    = ' K800-NUM-REGN                 
              MOVE    WS-K800-STATUS  TO  RETURN-CODE                           
              PERFORM 900-CLOSE-FILES-RTN.                                      
                                                                                
                                                                                
           PERFORM 400-READ-F200      THRU 400-EXIT.                            
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       800-PROCESS-PRINT.                                                       
      ******************************************************************        
                                                                                
           MOVE  '2'                        TO   L24H-MSGTPE.                   
           MOVE  F200-NUM-HHTY-QUEUE-SERIAL TO   L24H-QUEUE.                    
           MOVE  F200-DTE-BKAPPMT-DATE      TO   L24H-APPTDTE.                  
           MOVE  F200-TME-BKAPPMT           TO   L24H-APPTTME.                  
                                                                                
           IF WS-L24H-WRITE < 1                                                 
              WRITE PRINT-REC    FROM L24H-TITLE                                
           END-IF                                                               
                                                                                
           PERFORM 840-PRINT-DETAIL         THRU 840-EXIT.                      
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       840-PRINT-DETAIL.                                                        
      ******************************************************************        
                                                                                
           WRITE   PRINT-REC   FROM   L24H-DATA1                                
           ADD 1                            TO   WS-L24H-WRITE.                 
                                                                                
           IF K800-NUM-NRIC2(2:1)  NOT =  '#'    AND                            
              K800-NUM-NRIC2       NOT =  SPACES AND LOW-VALUES                 
              MOVE  K800-NUM-NRIC2(2:7)        TO   L24H-NRIC2                  
              MOVE  '2'                        TO   L24H-MSGTPE2                
              MOVE  F200-NUM-HHTY-QUEUE-SERIAL TO   L24H-QUEUE2                 
              MOVE  F200-DTE-BKAPPMT-DATE      TO   L24H-APPTDTE2               
              MOVE  F200-TME-BKAPPMT           TO   L24H-APPTTME2               
              WRITE PRINT-REC                FROM   L24H-DATA2                  
              ADD 1                            TO   WS-L24H-WRITE               
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC3(2:1)  NOT =  '#'    AND                            
              K800-NUM-NRIC3       NOT =  SPACES AND LOW-VALUES                 
              MOVE  K800-NUM-NRIC3(2:7)        TO   L24H-NRIC3                  
              MOVE  '2'                        TO   L24H-MSGTPE3                
              MOVE  F200-NUM-HHTY-QUEUE-SERIAL TO   L24H-QUEUE3                 
              MOVE  F200-DTE-BKAPPMT-DATE      TO   L24H-APPTDTE3               
              MOVE  F200-TME-BKAPPMT           TO   L24H-APPTTME3               
              WRITE PRINT-REC                FROM   L24H-DATA3                  
              ADD 1                            TO   WS-L24H-WRITE               
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC4(2:1)  NOT =  '#'    AND                            
              K800-NUM-NRIC4       NOT =  SPACES AND LOW-VALUES                 
              MOVE  K800-NUM-NRIC4(2:7)        TO   L24H-NRIC4                  
              MOVE  '2'                        TO   L24H-MSGTPE4                
              MOVE  F200-NUM-HHTY-QUEUE-SERIAL TO   L24H-QUEUE4                 
              MOVE  F200-DTE-BKAPPMT-DATE      TO   L24H-APPTDTE4               
              MOVE  F200-TME-BKAPPMT           TO   L24H-APPTTME4               
              WRITE PRINT-REC                FROM   L24H-DATA4                  
              ADD 1                            TO   WS-L24H-WRITE               
           END-IF.                                                              
                                                                                
       840-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       900-CLOSE-FILES-RTN.                                                     
      ******************************************************************        
           DISPLAY 'PROGRAM-ID : BP13C24H '.                                    
           DISPLAY '**********************'.                                    
           DISPLAY ' '.                                                         
           DISPLAY 'BP13F200 READ : '                                           
                          WS-F200-READ.                                         
           DISPLAY 'BP13K800 READ : '                                           
                          WS-K800-READ.                                         
           DISPLAY 'NO OF RECS WRITTEN  : '                                     
                          WS-L24H-WRITE.                                        
                                                                                
           CLOSE BP13F205                                                       
                 BP13F200                                                       
                 BP13K800                                                       
                 BP13L24H.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00                                           
              DISPLAY 'FILE BP13K800  CLOSE ERROR ' WS-K800-STATUS              
              MOVE    WS-K800-STATUS  TO    RETURN-CODE.                        
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
