       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C11E.                                                 
      *AUTHOR.        KSJ3.                                                     
      *DATE-WRITTEN.  28/09/2015.                                               
      *============================================================*            
      * OBJECTIVE    :  TO GENERATE FILES FOR TP PRE-ECHECK        *            
      *                 (BY PCD/DUE DATE)                          *            
      *============================================================*            
      * INPUT FILES  :  1.) BP13F800                               *            
      *                 2.) BM06K110                               *            
      *                 3.) BM06K100                               *            
      *                 4.) BP13K30A                               *            
      *                                                            *            
      * OUTPUT FILES :  1.) BP13FB48                               *            
      *                                                            *            
      * LISTING      :  1.) BP13L11E                               *            
      *============================================================*            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800  ASSIGN       TO BP13F800.                           
                                                                                
           SELECT BM06K110  ASSIGN       TO BM06K110                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K110-KEY-FLD                        
                            FILE STATUS  IS WS-K110-STATUS.                     
                                                                                
           SELECT BM06K100  ASSIGN       TO BM06K100                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K100-KEY-FLD                        
                            FILE STATUS  IS WS-K100-STATUS.                     
                                                                                
           SELECT BP13K30A  ASSIGN       TO BP13K30A                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K30A-KEY-FLD                        
                            FILE STATUS  IS WS-K30A-STATUS.                     
                                                                                
           SELECT BP13FB48  ASSIGN       TO BP13FB48.                           
                                                                                
           SELECT BP13L11E  ASSIGN       TO BP13L11E.                           
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F800                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 2000 CHARACTERS.                                     
           COPY BP13F800.                                                       
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
           COPY BM06K110.                                                       
                                                                                
       FD  BM06K100                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
           COPY BM06K100.                                                       
                                                                                
       FD  BP13K30A                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
           COPY BP13K30A.                                                       
                                                                                
       FD  BP13FB48                                                             
           RECORD CONTAINS 400 CHARACTERS                                       
           LABEL RECORD IS STANDARD                                             
           RECORDING MODE IS F.                                                 
           COPY BP13FB48.                                                       
                                                                                
       FD  BP13L11E                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  BP13L11E-REC                  PIC X(132).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K110-STATUS            PIC 9(2)   VALUE 00.                   
           05  WS-K100-STATUS            PIC 9(2)   VALUE 00.                   
           05  WS-K30A-STATUS            PIC 9(2)   VALUE 00.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F800-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F800-SKIP          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K110-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K110-NFND          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K100-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K100-NFND          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K30A-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K30A-NFND          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-FB48-WRITE         PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-RPT-PRINT          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-PAGE               PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-LINE               PIC 9(2)   VALUE 60.                   
           05  WS-CNT-SNO                PIC 9(4)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F800-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-SKIP-REC               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-FMT8-CCYY      PIC 9(4)   VALUE ZEROES.               
               10  WS-DTE-FMT8-MM        PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-FMT8-DD        PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-FMT10-DD       PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-FMT10-MM       PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-FMT10-CCYY.                                           
                   15  WS-DTE-FMT10-CC   PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-FMT10-YY   PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-TMP                PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-INT                PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE SPACES.               
                                                                                
      *--- BP13L11E REPORT -----------------------------------------            
       01  L11E-HEADER1.                                                        
           05  FILLER                    PIC X(8)   VALUE 'BP13L11E'.           
           05  FILLER                    PIC X(4)   VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE 'HDBCAT 3'.           
           05  FILLER                    PIC X(28)  VALUE SPACES.               
           05  FILLER                    PIC X(39)  VALUE                       
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                    PIC X(17)  VALUE SPACES.               
           05  FILLER                    PIC X(6)   VALUE 'DATE: '.             
           05  L11E-DTE-CURRENT          PIC X(10)  VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE '  PAGE: '.           
           05  L11E-CNT-PAGE             PIC ZZZ9.                              
                                                                                
       01  L11E-HEADER2.                                                        
           05  FILLER                    PIC X(41)  VALUE SPACES.               
           05  FILLER                    PIC X(93)  VALUE                       
               'ERROR LIST OF OUTSTANDING CASES (TP - APPLICATION)'.            
                                                                                
       01  L11E-HEADER3.                                                        
           05  FILLER                    PIC X(132) VALUE                       
               '     S/NO     SCH ACCNT NO     REGN NO      ERROR DESCRI        
      -        'PTION'.                                                         
                                                                                
       01  L11E-HEADER4.                                                        
           05  FILLER                    PIC X(132) VALUE                       
               '     ----     ------------     --------     ------------        
      -        '--------------------------------------'.                        
                                                                                
       01  L11E-DETAIL.                                                         
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  L11E-SNO                  PIC ZZZ9.                              
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  L11E-NUM-HDB-REF          PIC X(11)  VALUE SPACES.               
           05  FILLER                    PIC X(6)   VALUE SPACES.               
           05  L11E-NUM-REGN             PIC X(8)   VALUE SPACES.               
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  L11E-NUM-DESC             PIC X(50)  VALUE SPACES.               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-BP13F800-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-F800-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BM06K110                                                 
                       BM06K100                                                 
                       BP13K30A                                                 
                OUTPUT BP13FB48                                                 
                       BP13L11E.                                                
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT.                      
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BM06K100 ERROR ' WS-K100-STATUS                  
              MOVE     WS-K100-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT.                      
                                                                                
           IF WS-K30A-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K30A ERROR ' WS-K30A-STATUS                  
              MOVE     WS-K30A-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT.                      
                                                                                
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
                                                                                
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           PERFORM 8100-CONV-DATE-TO-10    THRU 8100-EXIT.                      
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13F800-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13F800 AT END                                                 
                MOVE 'Y'                   TO   WS-F800-EOF                     
                GO                         TO   2000-EXIT.                      
                                                                                
           ADD 1                           TO   WS-CNT-F800-READ.               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           MOVE 'N'                        TO   WS-SKIP-REC.                    
                                                                                
           MOVE SPACES                     TO   BP13K30A-REC.                   
           MOVE F800-NUM-REGN              TO   K30A-NUM-REGN.                  
           PERFORM 3100-READ-BP13K30A      THRU 3100-EXIT.                      
                                                                                
           IF WS-SKIP-REC NOT = 'Y'                                             
              MOVE SPACES                  TO   K110-REC                        
              MOVE F800-NUM-SCH-ACC        TO   K110-SCH-ACC-NO                 
              PERFORM 3110-READ-BM06K110   THRU 3110-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-SKIP-REC NOT = 'Y'                                             
              MOVE SPACES                  TO   K100-REC                        
              MOVE K110-ESTATE             TO   K100-ESTATE                     
              MOVE K110-NEIGHBOURHOOD      TO   K100-NEIGHBOURHOOD              
              MOVE K110-CONTRACT-NO        TO   K100-CONTRACT-NO                
              MOVE K110-BLK-NO             TO   K100-BLK-NO                     
              PERFORM 3120-READ-BM06K100   THRU 3120-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-SKIP-REC NOT = 'Y'                                             
              PERFORM 3200-VALIDATE-REC    THRU 3200-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-SKIP-REC NOT = 'Y'                                             
              PERFORM 3300-SAVE-REGN       THRU 3300-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 2000-BP13F800-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3100-READ-BP13K30A.                                                      
      *=============================================================            
                                                                                
           READ BP13K30A.                                                       
                                                                                
           EVALUATE WS-K30A-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              IF K30A-NUM-PREE-STATUS NOT = 'NEW'                               
                 MOVE 'PREECHECK STATUS NOT NEW'                                
                                           TO   L11E-NUM-DESC                   
                 ADD   1                   TO   WS-CNT-K110-NFND                
                 MOVE 'Y'                  TO   WS-SKIP-REC                     
      *          PERFORM 8900-PRINT-ERROR  THRU 8900-EXIT                       
              END-IF                                                            
           WHEN 23                                                              
              CONTINUE                                                          
           WHEN OTHER                                                           
              MOVE WS-K30A-STATUS          TO   RETURN-CODE                     
              DISPLAY 'ERROR READING BP13K30A, STATUS = '                       
                       WS-K30A-STATUS                                           
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3110-READ-BM06K110.                                                      
      *=============================================================            
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              ADD   1                      TO   WS-CNT-K110-READ                
           WHEN 23                                                              
              MOVE 'SCHEME ACCOUNT NOT FOUND ON FIS'                            
                                           TO   L11E-NUM-DESC                   
              ADD   1                      TO   WS-CNT-K110-NFND                
              MOVE 'Y'                     TO   WS-SKIP-REC                     
              PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
           WHEN OTHER                                                           
              MOVE WS-K110-STATUS          TO   RETURN-CODE                     
              DISPLAY 'ERROR READING BM06K110, STATUS =  '                      
                       WS-K110-STATUS                                           
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3110-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3120-READ-BM06K100.                                                      
      *=============================================================            
                                                                                
           READ BM06K100.                                                       
                                                                                
           EVALUATE WS-K100-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              ADD   1                      TO   WS-CNT-K100-READ                
           WHEN 23                                                              
              MOVE 'BLOCK NOT FOUND ON FIS'                                     
                                           TO   L11E-NUM-DESC                   
              ADD   1                      TO   WS-CNT-K100-NFND                
              MOVE 'Y'                     TO   WS-SKIP-REC                     
              PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
           WHEN OTHER                                                           
              MOVE WS-K100-STATUS          TO   RETURN-CODE                     
              DISPLAY 'ERROR READING BM06K100, STATUS = '                       
                       WS-K100-STATUS                                           
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3120-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3200-VALIDATE-REC.                                                       
      *=============================================================            
                                                                                
           IF K110-DTE-ACCEPTANCE = SPACES OR LOW-VALUES OR ZEROES              
              STRING 'INVALID ACCPT DTE = ' K110-DTE-ACCEPTANCE                 
                      DELIMITED BY SIZE    INTO L11E-NUM-DESC                   
              MOVE 'Y'                     TO   WS-SKIP-REC                     
      *       PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
           END-IF.                                                              
                                                                                
           IF K110-DTE-KEY-ISSUED NOT = SPACES AND LOW-VALUES AND ZEROES        
              MOVE 'KEY HAS ALREADY BEEN ISSUED'                                
                                           TO   L11E-NUM-DESC                   
              MOVE 'Y'                     TO   WS-SKIP-REC                     
      *       PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
           END-IF.                                                              
                                                                                
      *    IF K110-DTE-KEY-AVAIL = SPACES OR LOW-VALUES OR ZEROES               
      *       MOVE 'KEY IS NOT YET AVAILABLE'                                   
      *                                    TO   L11E-NUM-DESC                   
      *       MOVE 'Y'                     TO   WS-SKIP-REC                     
      *       PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
      *    END-IF.                                                              
                                                                                
           IF K100-DTE-PACD = SPACES OR LOW-VALUES OR ZEROES                    
              MOVE 'INVALID PACD'          TO   L11E-NUM-DESC                   
              MOVE 'Y'                     TO   WS-SKIP-REC                     
      *       PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
           END-IF.                                                              
                                                                                
           IF F800-NUM-SCH-ACC = SPACES OR LOW-VALUES OR ZEROES                 
              MOVE 'NO SCHEME ACCOUNT '    TO   L11E-NUM-DESC                   
              MOVE 'Y'                     TO   WS-SKIP-REC                     
      *       PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
           END-IF.                                                              
                                                                                
           IF F800-NUM-STATUS = 'C'                                             
              MOVE 'REGN IS CANCELLED '    TO   L11E-NUM-DESC                   
              MOVE 'Y'                     TO   WS-SKIP-REC                     
      *       PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
           END-IF.                                                              
                                                                                
           IF F800-DTE-SALE-AGMT = SPACES OR LOW-VALUES OR ZEROES               
              MOVE 'AO DATE NOT FOUND'     TO   L11E-NUM-DESC                   
              MOVE 'Y'                     TO   WS-SKIP-REC                     
      *       PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
           END-IF.                                                              
                                                                                
           IF F800-DTE-AO-APPT = SPACES OR LOW-VALUES OR ZEROES                 
              MOVE 'SATP RECORD'           TO   L11E-NUM-DESC                   
              MOVE 'Y'                     TO   WS-SKIP-REC                     
      *       PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
           END-IF.                                                              
                                                                                
           IF F800-DTE-SO-APPT = SPACES OR LOW-VALUES OR ZEROES                 
              MOVE 'SALES ORDER DATE NOT FOUND'                                 
                                           TO   L11E-NUM-DESC                   
              MOVE 'Y'                     TO   WS-SKIP-REC                     
      *       PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-SKIP-REC = 'Y'                                                 
              ADD 1                        TO   WS-CNT-F800-SKIP                
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3300-SAVE-REGN.                                                          
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   BP13FB48-REC.                   
                                                                                
           MOVE K100-DTE-PACD              TO   FB48-DTE-PACD.                  
           MOVE F800-NUM-REGN              TO   FB48-NUM-REGN.                  
                                                                                
           MOVE WS-DTE-CURR-8              TO   FB48-DTE-UPDATE.                
           MOVE 'BP13C11E'                 TO   FB48-NUM-USERID.                
                                                                                
           WRITE BP13FB48-REC.                                                  
           ADD 1                           TO   WS-CNT-FB48-WRITE.              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8000-CONV-DATE-TO-8.                                                     
      *=============================================================            
      *    INPUT  - WS-DTE-FMT10                                                
      *    OUTPUT - WS-DTE-FMT8                                                 
      *-------------------------------------------------------------            
                                                                                
           MOVE WS-DTE-FMT10-CCYY  TO  WS-DTE-FMT8-CCYY.                        
           MOVE WS-DTE-FMT10-MM    TO  WS-DTE-FMT8-MM.                          
           MOVE WS-DTE-FMT10-DD    TO  WS-DTE-FMT8-DD.                          
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8100-CONV-DATE-TO-10.                                                    
      *=============================================================            
      *    INPUT  - WS-DTE-FMT8                                                 
      *    OUTPUT - WS-DTE-FMT10                                                
      *-------------------------------------------------------------            
                                                                                
           MOVE WS-DTE-FMT8-CCYY   TO  WS-DTE-FMT10-CCYY.                       
           MOVE WS-DTE-FMT8-MM     TO  WS-DTE-FMT10-MM.                         
           MOVE WS-DTE-FMT8-DD     TO  WS-DTE-FMT10-DD.                         
                                                                                
       8100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8900-PRINT-ERROR.                                                        
      *=============================================================            
                                                                                
           IF WS-CNT-LINE > 50                                                  
              PERFORM 8910-PRINT-ERROR-HDR THRU 8910-EXIT                       
           END-IF.                                                              
                                                                                
           ADD  1                          TO   WS-CNT-SNO                      
                                                WS-CNT-LINE.                    
           MOVE WS-CNT-SNO                 TO   L11E-SNO.                       
           MOVE F800-NUM-SCH-ACC           TO   L11E-NUM-HDB-REF.               
           MOVE F800-NUM-REGN              TO   L11E-NUM-REGN.                  
                                                                                
           WRITE BP13L11E-REC              FROM L11E-DETAIL.                    
           ADD 1                           TO   WS-CNT-RPT-PRINT.               
                                                                                
       8900-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8910-PRINT-ERROR-HDR.                                                    
      *=============================================================            
                                                                                
           ADD 1                           TO   WS-CNT-PAGE.                    
           MOVE WS-CNT-PAGE                TO   L11E-CNT-PAGE.                  
           MOVE WS-DTE-CURR-10             TO   L11E-DTE-CURRENT.               
                                                                                
           WRITE BP13L11E-REC FROM L11E-HEADER1 AFTER PAGE.                     
           WRITE BP13L11E-REC FROM L11E-HEADER2.                                
           WRITE BP13L11E-REC FROM L11E-HEADER3 AFTER 2.                        
           WRITE BP13L11E-REC FROM L11E-HEADER4.                                
                                                                                
           MOVE SPACES                     TO   BP13L11E-REC.                   
           WRITE BP13L11E-REC.                                                  
                                                                                
           MOVE 6                          TO   WS-CNT-LINE.                    
                                                                                
       8910-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*------- BP13C11E CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13F800 READ      : ' WS-CNT-F800-READ.          
           DISPLAY ' 2. NO OF BP13F800 SKIPPED   : ' WS-CNT-F800-SKIP.          
           DISPLAY ' 3. NO OF BM06K110 READ      : ' WS-CNT-K110-READ.          
           DISPLAY ' 4. NO OF BM06K110 NOT FOUND : ' WS-CNT-K110-NFND.          
           DISPLAY ' 5. NO OF BM06K100 READ      : ' WS-CNT-K100-READ.          
           DISPLAY ' 6. NO OF BM06K100 NOT FOUND : ' WS-CNT-K100-NFND.          
           DISPLAY ' 7. NO OF BP13K30A READ      : ' WS-CNT-K30A-READ.          
           DISPLAY ' 8. NO OF BP13K30A NOT FOUND : ' WS-CNT-K30A-NFND.          
           DISPLAY ' 9. NO OF BP13FB48 WRITTEN   : ' WS-CNT-FB48-WRITE.         
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13F800                                                       
                 BM06K110                                                       
                 BM06K100                                                       
                 BP13K30A                                                       
                 BP13FB48                                                       
                 BP13L11E.                                                      
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO   RETURN-CODE.                    
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BM06K100 ERROR ' WS-K100-STATUS                  
              MOVE     WS-K100-STATUS      TO   RETURN-CODE.                    
                                                                                
           IF WS-K30A-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K30A ERROR ' WS-K30A-STATUS                  
              MOVE     WS-K30A-STATUS      TO   RETURN-CODE.                    
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
