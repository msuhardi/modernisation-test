      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.  BP13C074.                                                   
       AUTHOR.  AJITH KUMAR T.                                                  
       DATE-WRITTEN. 03/05/2000                                                 
                                                                                
      **************************************************************            
      *                         SOC                                *            
      **************************************************************            
      * OBJECTIVE : TO CALL BP13C913 AND TO OUTPUT ADDRESS DETAILS *            
      *             TO P13F716A USING SCH-ACC-NO FROM RDB           *           
      **************************************************************            
      * MODIFICATION :                                             *            
      * CHGE REQ    DATE     BY            REMAKS                  *            
      * -------- ---------- ---- --------------------------------- *            
      * BP130816 27/07/2000 AKT   NEW PROGRAM.                     *            
      * BP130883 03/08/2000 RASM  TO CHANGE BP13C904 TO BP13C913   *            
      **************************************************************            
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
      *-----------------------*                                                 
       INPUT-OUTPUT SECTION.                                                    
      *-----------------------*                                                 
       FILE-CONTROL.                                                            
                                                                                
            SELECT  BP13F716       ASSIGN TO BP13F716.                          
                                                                                
            SELECT  P13F716A       ASSIGN TO P13F716A.                          
                                                                                
                                                                                
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
       FILE SECTION.                                                            
      *-----------------------*                                                 
                                                                                
       FD  BP13F716                BLOCK CONTAINS 0 RECORDS                     
                                   RECORD CONTAINS 350 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F716.                                                           
                                                                                
       FD  P13F716A                BLOCK CONTAINS 0 RECORDS                     
                                   RECORD CONTAINS 350 CHARACTERS               
                                   LABEL RECORDS ARE OMITTED.                   
       01  P13F716A-REC            PIC X(350).                                  
                                                                                
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-FLAGS-SWITCHES.                                                   
           05  WS-F716-EOF-SW         PIC X(01)  VALUE 'N'.                     
               88  WS-F716-EOF                   VALUE 'Y'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F716-REC-READ       PIC 9(05)  VALUE 0.                       
           05  WS-F716-REC-WRITE      PIC 9(05)  VALUE 0.                       
           05  WS-ADDR-FOUND          PIC 9(05)  VALUE 0.                       
                                                                                
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH    PIC X(4).                                 
               10  WS-LINK-NUM-ACC    PIC X(5).                                 
                                                                                
       COPY P13COMM8.                                                           
                                                                                
                                                                                
      *-----------------------*                                                 
       PROCEDURE DIVISION.                                                      
      *-----------------------*                                                 
                                                                                
      ********************                                                      
       0000-MAIN.                                                               
      ********************                                                      
           PERFORM 1000-OPEN-FILES           THRU 1000-EXIT.                    
           PERFORM 2000-READ-BP13F716        THRU 2000-EXIT.                    
                                                                                
           PERFORM 3000-PROCESS-BP13F716-REC THRU 3000-EXIT                     
             UNTIL WS-F716-EOF.                                                 
                                                                                
           PERFORM 9000-CLOSE-FILES          THRU 9000-EXIT.                    
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      **************************                                                
       1000-OPEN-FILES.                                                         
      **************************                                                
           OPEN INPUT BP13F716                                                  
               OUTPUT P13F716A.                                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      **************************                                                
       2000-READ-BP13F716.                                                      
      **************************                                                
           READ BP13F716                                                        
             AT END                                                             
                MOVE 'Y'    TO WS-F716-EOF-SW                                   
            NOT AT END                                                          
                ADD 1       TO WS-F716-REC-READ                                 
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ***********************                                                   
       2050-WRITE-P13F716A.                                                     
      ***********************                                                   
           WRITE P13F716A-REC  FROM BP13F716-REC.                               
           ADD 1               TO   WS-F716-REC-WRITE.                          
                                                                                
       2050-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      **************************                                                
       3000-PROCESS-BP13F716-REC.                                               
      **************************                                                
           PERFORM 4000-CALL-BP13C913   THRU 4000-EXIT.                         
           PERFORM 2050-WRITE-P13F716A  THRU 2050-EXIT.                         
           PERFORM 2000-READ-BP13F716   THRU 2000-EXIT.                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      **************************                                                
       4000-CALL-BP13C913.                                                      
      **************************                                                
           MOVE SPACES                   TO BP13COMM8-REC.                      
           INITIALIZE                       BP13COMM8-REC.                      
                                                                                
           MOVE F716-NUM-SCH-ACCNT       TO WS-LINK-REC.                        
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC                                    
                                 BP13COMM8-REC                                  
             ON OVERFLOW                                                        
                DISPLAY 'ERROR IN CALLING BP13C913!'                            
           END-CALL.                                                            
                                                                                
           IF COMM8-CDE-SYSERR = 0                                              
              MOVE COMM8-NUM-BLK         TO  F716-NUM-BLK                       
              MOVE COMM8-NME-STREET      TO  F716-NME-STREET                    
              MOVE COMM8-NUM-LEVEL       TO  F716-NUM-HSELVL                    
              MOVE COMM8-NUM-UNIT-MAIN   TO  F716-NUM-HSEMAIN                   
              MOVE COMM8-NUM-POSTAL-CODE TO  F716-NUM-POSTAL-DSTRCT             
              MOVE COMM8-NUM-MGMT        TO  F716-NUM-BO-CURR                   
              ADD 1                      TO  WS-ADDR-FOUND                      
           ELSE                                                                 
              DISPLAY 'SQL CODE = '          COMM8-CDE-SYSERR                   
              MOVE SPACES                TO  F716-NUM-BLK                       
                                             F716-NME-STREET                    
                                             F716-NUM-HSELVL                    
                                             F716-NUM-HSEMAIN                   
                                             F716-NUM-POSTAL-DSTRCT             
                                             F716-NUM-BO-CURR                   
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      **************************                                                
       9000-CLOSE-FILES.                                                        
      **************************                                                
           DISPLAY ' '.                                                         
           DISPLAY '*************  BP13C074  ****************'.                 
           DISPLAY 'TOTAL BP13F716  RECORD READ       : '                       
                   WS-F716-REC-READ.                                            
           DISPLAY 'TOTAL P13F716A  RECORD WRITE      : '                       
                   WS-F716-REC-WRITE.                                           
           DISPLAY 'TOTAL ADDR FOUND IN PIDB          : '                       
                   WS-ADDR-FOUND.                                               
           DISPLAY '*************  --------  ****************'.                 
                                                                                
           CLOSE BP13F716                                                       
                 P13F716A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
