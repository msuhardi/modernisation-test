       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF74.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  25/01/2013.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE    : COPY YOUNGEST-SC-CHILD AND PPS-ELIG-TAG     *            
      *                                                            *            
      * INPUT FILE   :  BP13F730  -  SEQUENTIAL DATASET            *            
      * I-O FILE     :  BP13K730  -  BP13.K730.MATURERQ            *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP134626  SMR2  25012013  NEW PROGRAM                      *            
      * BP134906  SMR2  28052013  CHANGE NUM-YOUNGEST-SC-CHILD TO  *            
      *                           NUM-YOUNG-CHILD-AGE-SC           *            
      * BP135059  SMR2  17092013  UPDATE NUM-GEN3                  *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730  ASSIGN       TO BP13F730.                           
                                                                                
           SELECT BP13K730  ASSIGN       TO BP13K730                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K730-NUM-REGN                       
                            FILE STATUS  IS WS-K730-STATUS.                     
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F730                                                            
            RECORDING MODE   IS F                                               
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F730.                                                           
                                                                                
       FD   BP13K730                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K730.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  WS-K730-STATUS           PIC 9(02)  VALUE ZEROES.                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F730-READ             PIC 9(06)  VALUE ZEROES.                
           05  WS-K730-MATCH            PIC 9(06)  VALUE ZEROES.                
           05  WS-K730-UNMATCH          PIC 9(06)  VALUE ZEROES.                
           05  WS-K730-REWRITE          PIC 9(06)  VALUE ZEROES.                
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F730-EOF              PIC X(01)  VALUE 'N'.                   
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-DATE                  PIC X(08)  VALUE SPACES.                
           05  WS-DATE-EDIT             PIC X(10)  VALUE SPACES.                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F730       THRU 2000-EXIT                      
             UNTIL WS-F730-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT BP13F730                                                  
                I-O   BP13K730.                                                 
                                                                                
           IF WS-K730-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K730 ERROR ' WS-K730-STATUS                  
              MOVE WS-K730-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE       TO WS-DATE.                         
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO WS-DATE-EDIT.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-BP13F730.                                                      
      *-------------------------------------------------------------            
           READ BP13F730                                                        
             AT END                                                             
                MOVE 'Y'                    TO WS-F730-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F730-READ.                    
           PERFORM 3000-READ-BP13K730       THRU 3000-EXIT.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-READ-BP13K730.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                      TO BP13K730-REC.                    
           INITIALIZE                          BP13K730-REC.                    
                                                                                
           MOVE F730-NUM-REGN               TO K730-NUM-REGN.                   
                                                                                
           READ BP13K730.                                                       
                                                                                
           EVALUATE WS-K730-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                         TO WS-K730-MATCH                    
              PERFORM 4000-REWRITE-BP13K730 THRU 4000-EXIT                      
                                                                                
           WHEN 23                                                              
              ADD 1                         TO WS-K730-UNMATCH                  
              DISPLAY 'NUM-REGN NOT FOUND IN K730 : ' F730-NUM-REGN             
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K730   : ' WS-K730-STATUS              
              MOVE WS-K730-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-REWRITE-BP13K730.                                                   
      *-------------------------------------------------------------            
           MOVE F730-NUM-YOUNG-CHILD-AGE-SC                                     
                                        TO K730-NUM-YOUNG-CHILD-AGE-SC.         
           MOVE F730-NUM-PPS-ELIG-TAG      TO K730-NUM-PPS-ELIG-TAG.            
           MOVE F730-NUM-YOUNG-CHILD-AGE-NSC                                    
                                        TO K730-NUM-YOUNG-CHILD-AGE-NSC.        
           MOVE F730-NUM-GEN3              TO K730-NUM-GEN3.                    
                                                                                
           REWRITE BP13K730-REC.                                                
                                                                                
           EVALUATE WS-K730-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                        TO WS-K730-REWRITE                   
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR WRITING TO BP13K730. STATUS IS - '                 
                      WS-K730-STATUS                                            
              MOVE WS-K730-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY '  '.                                                        
           DISPLAY '    BP13CF74 CONTROL TOTAL   : ' WS-DATE-EDIT.              
           DISPLAY '  '.                                                        
           DISPLAY ' 1. NO OF BP13F730 READ      : ' WS-F730-READ.              
           DISPLAY ' 2. NO OF MATCHED RECORDS    : ' WS-K730-MATCH.             
           DISPLAY ' 3. NO OF UNMATCHED RECORDS  : ' WS-K730-UNMATCH.           
           DISPLAY ' 4. NO OF BP13K730 REWRITTEN : ' WS-K730-REWRITE.           
           DISPLAY '  '.                                                        
           DISPLAY ' CHECK '.                                                   
           DISPLAY '       1.  =   2.  +  3. '.                                 
           DISPLAY '       2.  =   4.        '.                                 
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13F730                                                       
                 BP13K730.                                                      
                                                                                
           IF WS-K730-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K730 ERROR ' WS-K730-STATUS                  
              MOVE WS-K730-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
