      *--------------------------------------------------------------*          
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------------*          
       PROGRAM-ID.    BP13C25Q.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  20 OCTOBER 2014.                                          
      *--------------------------------------------------------------*          
      *                                                              *          
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)                 *          
      *                                                              *          
      *    SYSTEM ID   : BP13                                        *          
      *                                                              *          
      *    OBJECTIVES :  SORT QUEUE FOR 100% AND 200%                *          
      *                                                              *          
      *    INPUT FILES  : BP13F205                                   *          
      *                   BP13K813                                   *          
      *                   BP13K800                                   *          
      *                   BP13F200                                   *          
      *    OUTPUT       : P13F200A                                   *          
      *                   P13F200B                                   *          
      *                   P13F200C                                   *          
      *                   P13F200D                                   *          
      *--------------------------------------------------------------*          
      * CHG-NO  BY   DATE     DETAILS                                *          
      *-------- ---- -------- ---------------------------------------*          
      *BP135517 ESA1 20102014 NEW PGM MODELLED AFTER BP13C23K        *          
      *BP136217 ESA1 27042016 TO REMOVE SORTING OF BROCHURE FOR      *          
      *                       BALLOT-QTR >= '201602'                 *          
      *-------- ---- -------- ---------------------------------------*          
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205 ASSIGN TO BP13F205.                                  
                                                                                
           SELECT BP13K813 ASSIGN TO BP13K813                                   
                  ACCESS MODE IS DYNAMIC                                        
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K813-KEY-FLD                                    
                  FILE STATUS IS BP13K813-STATUS.                               
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE IS DYNAMIC                                        
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K800-NUM-REGN                                   
                  FILE STATUS IS BP13K800-STATUS.                               
                                                                                
           SELECT BP13F200 ASSIGN TO BP13F200.                                  
           SELECT P13F200A ASSIGN TO P13F200A.                                  
           SELECT P13F200B ASSIGN TO P13F200B.                                  
                                                                                
      *--------------------------------------------------------------*          
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------------*          
                                                                                
       FD   BP13F205                                                            
            RECORD CONTAINS 80 CHARACTERS                                       
            RECORDING MODE  IS F.                                               
       COPY BP13F205.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13F200                                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       COPY BP13F200.                                                           
                                                                                
       FD   P13F200A                                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            RECORDING MODE  IS F.                                               
       01   P13F200A-REC         PIC X(300).                                    
                                                                                
       FD   P13F200B                                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            RECORDING MODE  IS F.                                               
       01   P13F200B-REC         PIC X(300).                                    
                                                                                
      *--------------------------------------------------------------*          
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------*          
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F205-EOF                PIC X(1)  VALUE 'N'.                  
           05  WS-F200-EOF                PIC X(1)  VALUE 'N'.                  
           05  WS-K813-EOF                PIC X(1)  VALUE 'N'.                  
           05  WS-K813-FND                PIC X(1)  VALUE 'N'.                  
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  BP13K813-STATUS            PIC 9(2)  VALUE 0.                    
           05  BP13K800-STATUS            PIC 9(2)  VALUE 0.                    
                                                                                
       01  WS-MISCELLANEOUS-VARS.                                               
           05  WS-EDITED-DATE             PIC X(10) VALUE SPACES.               
           05  WS-NUM-QUEUE               PIC X(05) VALUE SPACES.               
           05  WS-CNT-F200A-WRITE         PIC 9(8)  VALUE 0.                    
           05  WS-CNT-F200B-WRITE         PIC 9(8)  VALUE 0.                    
           05  WS-CNT-F200-READ           PIC 9(8)  VALUE 0.                    
           05  WS-CNT-F205-READ           PIC 9(8)  VALUE 0.                    
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY                PIC 9(4).                             
           05  WS-SYS-MM                  PIC 9(2).                             
           05  WS-SYS-DD                  PIC 9(2).                             
                                                                                
      *--------------------------------------------------------------*          
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------------*          
                                                                                
      *--------------------------------------------------------------*          
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------------------------------------*          
                                                                                
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
           PERFORM 1100-READ-F205         THRU 1100-EXIT.                       
           PERFORM 2000-PROCESS           THRU 2000-EXIT                        
                   UNTIL WS-F205-EOF = 'Y'.                                     
           PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1000-OPEN-ROUTINE.                                                       
      *--------------------------------------------------------------*          
                                                                                
           OPEN INPUT  BP13F205                                                 
                       BP13K813                                                 
                       BP13K800                                                 
                       BP13F200                                                 
                OUTPUT P13F200A                                                 
                       P13F200B.                                                
                                                                                
           IF BP13K813-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPEN FAILED FOR BP13K813,STATUS ' BP13K813-STATUS        
              MOVE BP13K813-STATUS TO RETURN-CODE                               
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
           IF BP13K800-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPEN FAILED FOR BP13K800,STATUS ' BP13K800-STATUS        
              MOVE BP13K800-STATUS TO RETURN-CODE                               
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-SYSTEM-DATE.                   
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1100-READ-F205.                                                          
      *--------------------------------------------------------------*          
           READ BP13F205                                                        
                AT END  MOVE 'Y' TO WS-F205-EOF                                 
                        MOVE 'Y' TO WS-F200-EOF                                 
                GO TO 1100-EXIT                                                 
           END-READ.                                                            
           ADD 1                 TO WS-CNT-F205-READ.                           
           PERFORM 1400-READ-K813        THRU 1400-EXIT.                        
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1200-READ-F200.                                                          
      *--------------------------------------------------------------*          
           READ BP13F200                                                        
                AT END  MOVE 'Y' TO WS-F200-EOF                                 
                GO TO 1200-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-CNT-F200-READ.                                           
                                                                                
       1200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------*          
       1300-CLOSE-OPEN-F200.                                                    
      *--------------------------------------------------------------*          
           CLOSE      BP13F200.                                                 
           OPEN INPUT BP13F200.                                                 
                                                                                
           MOVE 'N'   TO WS-F200-EOF.                                           
                                                                                
       1300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1400-READ-K813.                                                          
      *--------------------------------------------------------------*          
           MOVE 'N'                TO WS-K813-EOF.                              
           MOVE SPACES             TO K813-KEY-FLD.                             
           MOVE F205-NUM-NT-ZONE   TO K813-NUM-ZONE.                            
                                                                                
           START BP13K813 KEY >= K813-NUM-ZONE.                                 
                                                                                
           IF BP13K813-STATUS = 00 OR 02                                        
              PERFORM 1500-READNEXT-BP13K813 THRU 1500-EXIT                     
              UNTIL WS-K813-EOF = 'Y' OR WS-K813-FND = 'Y'                      
           ELSE                                                                 
              IF BP13K813-STATUS = 10 OR 23                                     
               DISPLAY 'RECORD NOT FOUND IN BP13K813:' F205-NUM-NT-ZONE         
              ELSE                                                              
               DISPLAY  'READ ERROR IN BP13K813 ' F205-NUM-NT-ZONE              
               PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       1400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1500-READNEXT-BP13K813.                                                  
      *****************************************************************         
                                                                                
           READ BP13K813 NEXT RECORD                                            
                AT END MOVE 'Y' TO WS-K813-EOF.                                 
                                                                                
           IF BP13K813-STATUS = 00 OR 02                                        
              IF K813-NUM-ZONE   =  F205-NUM-NT-ZONE                            
                 MOVE 'Y'          TO WS-K813-FND                               
              ELSE                                                              
                 MOVE SPACES       TO BP13K813-REC                              
                 MOVE 'Y'          TO WS-K813-EOF                               
              END-IF                                                            
           ELSE                                                                 
              IF BP13K813-STATUS = 10 OR 23                                     
               MOVE 'Y'            TO WS-K813-EOF                               
               DISPLAY 'RECORD NOT FOUND IN BP13K813:' F205-NUM-NT-ZONE         
              ELSE                                                              
               DISPLAY  'READ ERROR IN BP13K813 ' F205-NUM-NT-ZONE              
               PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       2000-PROCESS.                                                            
      *--------------------------------------------------------------*          
           PERFORM 4000-PROCESS           THRU 4000-EXIT                        
                   UNTIL WS-F200-EOF = 'Y'.                                     
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------         
       4000-PROCESS.                                                            
      *----------------------------------------------------------------         
           PERFORM 1200-READ-F200    THRU 1200-EXIT.                            
                                                                                
           IF WS-F200-EOF = 'Y'                                                 
              PERFORM 1100-READ-F205        THRU 1100-EXIT                      
                                                                                
              IF WS-F205-EOF = 'Y'                                              
                 GO TO 4000-EXIT                                                
              END-IF                                                            
                                                                                
              PERFORM 1300-CLOSE-OPEN-F200  THRU 1300-EXIT                      
              GO TO 4000-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE F200-NUM-HHTY-QUEUE-SERIAL  TO WS-NUM-QUEUE.                    
                                                                                
           IF WS-NUM-QUEUE(1:1) NOT NUMERIC                                     
              MOVE ZEROES       TO  WS-NUM-QUEUE(1:1)                           
           END-IF.                                                              
                                                                                
           IF WS-NUM-QUEUE(2:1) NOT NUMERIC                                     
              MOVE ZEROES       TO  WS-NUM-QUEUE(2:1)                           
           END-IF.                                                              
                                                                                
           PERFORM 7000-READ-BP13K800   THRU 7000-EXIT.                         
                                                                                
           IF (WS-NUM-QUEUE <= K813-NUM-QUEUE-MID)                              
              PERFORM 5000-WRITE-P13F200A   THRU 5000-EXIT                      
           ELSE                                                                 
              IF (WS-NUM-QUEUE > K813-NUM-QUEUE-MID)                            
                 PERFORM 5500-WRITE-P13F200B   THRU 5500-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------*          
       5000-WRITE-P13F200A.                                                     
      *--------------------------------------------------------------*          
            MOVE SPACES        TO   P13F200A-REC.                               
            WRITE P13F200A-REC FROM BP13F200-REC.                               
            ADD  1  TO WS-CNT-F200A-WRITE.                                      
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------*          
       5500-WRITE-P13F200B.                                                     
      *--------------------------------------------------------------*          
            MOVE SPACES        TO   P13F200B-REC.                               
            WRITE P13F200B-REC FROM BP13F200-REC.                               
            ADD  1  TO WS-CNT-F200B-WRITE.                                      
                                                                                
       5500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------*          
       7000-READ-BP13K800.                                                      
      *--------------------------------------------------------------*          
           MOVE SPACES               TO K800-NUM-REGN.                          
           MOVE F200-NUM-REGN        TO K800-NUM-REGN.                          
                                                                                
           READ BP13K800.                                                       
           EVALUATE BP13K800-STATUS                                             
             WHEN 00                                                            
                CONTINUE                                                        
             WHEN 23                                                            
                MOVE SPACES      TO BP13K800-MASTER                             
             WHEN OTHER                                                         
                DISPLAY 'READ BP13K800 ERROR ' F200-NUM-REGN                    
                DISPLAY 'STATUS ' BP13K800-STATUS                               
                MOVE BP13K800-STATUS TO RETURN-CODE                             
                PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------*          
       9999-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------------*          
                                                                                
           CLOSE BP13F205                                                       
                 BP13K813                                                       
                 BP13K800                                                       
                 BP13F200                                                       
                 P13F200A                                                       
                 P13F200B.                                                      
                                                                                
           IF BP13K813-STATUS NOT = 00 AND 97                                   
             DISPLAY 'CLOSE FAILED FOR BP13K813,STATUS ' BP13K813-STATUS        
             MOVE BP13K813-STATUS TO RETURN-CODE                                
             PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                          
           END-IF.                                                              
                                                                                
           IF BP13K800-STATUS NOT = 00 AND 97                                   
             DISPLAY 'CLOSE FAILED FOR BP13K800,STATUS ' BP13K800-STATUS        
             MOVE BP13K800-STATUS TO RETURN-CODE                                
             PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                          
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '********* BP13C25Q CONTROL *********'.                      
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF READ (BP13F205) = ' WS-CNT-F205-READ.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF READ (BP13F200) = ' WS-CNT-F200-READ.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF WRITE(100% S)   = ' WS-CNT-F200A-WRITE.              
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF WRITE(200% S)   = ' WS-CNT-F200B-WRITE.              
           DISPLAY SPACES.                                                      
           DISPLAY '************************************'.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************** E N D  O F  P R O G R A M *****************             
