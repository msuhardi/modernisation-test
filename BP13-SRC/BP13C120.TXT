       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C120.                                                 
      *AUTHOR.        CHEAH KH.                                                 
      *DATE-WRITTEN.  09/05/89.                                                 
      * =========================================================== *           
      *                SYSTEM OF COMMITMENT (BP13)                  *           
      * =========================================================== *           
      *   OBJECTIVE   :  PRINT MONTHLY EXCEPTION REPORT ON          *           
      *                  REGISTRATION FEES, BOOKING FEES, AND       *           
      *                  SOC COMMITMENT DEPOSIT.                    *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BP13K800 - SOC APPLICATION MASTER FILE                *           
      *   2.  BP13K820 - SOC OCCUPIER MASTER FILE                   *           
      *   3.  BP13K022 - OIC FILE                                   *           
      *   4.  BM06K110                                              *           
      *   5.  SY02F001                                              *           
      *                                                             *           
      *   OUTPUT FILES:                                             *           
      *   1.  P13L120A  - EXCEPTION REPORT ON REG FEES              *           
      *   2.  P13L120B  - EXCEPTION REPORT ON BOOKING FEES          *           
      *   3.  P13L120C  - OUTPUT FILE                               *           
      *                                                             *           
      *   SUBPROGRAM  :                                             *           
      *   1.  BP13C913  - RETRIEVE ADDRESS AND CDE-BUC              *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG REF  DATE     BY   DESCRIPTIONS                         *           
      * -------  -------- ---  ------------                         *           
      * I000001  08/06/89 CKH  AMENDMENT TO CALLING PROGRAM         *           
      * C910943  01/12/91 TJF  CHG SPC TO BUC                       *           
      * SOC-PH8  29/07/93 JEP  RENAME P10K020 TO BP13K800           *           
      *                        RENAME P10K040 TO BP13K820           *           
      *                        RETRIEVE ADDRESS FROM PBF TABLE      *           
      * BP130015 13/09/95 SSS  CONVERT TO LASER PRINT REPORT        *           
      *                        CHG HEADING TO ROW 1 AFTER PAGE      *           
      * BP130056 14/11/95 LSB  TO HAVE ADDITIONAL TWO BYTE          *           
      * BP130180 04/09/96 SSS  INCL ABOVE DUE TO IA COMMENTS        *           
      * BP130418 20/03/98 SIA  CATER FOR YEAR 2000                  *           
      * BP130922 21092000 MCC  INCL AO DTE AND DAYS OUTSTANDING,    *           
      *                        DELETE ADDRESS IN BP13L120.          *           
      * BP130883 03082000 RAS  CHG BP13C904 TO BP13C913             *           
      * BP132088 05022002 MCC  INCL SY02F001 FILE                   *           
      * BP132387 19082003 SSS  CHF BK-REQ = BK-PAID                 *           
      * BP132548 10062004 YCH2 PRT ALLO CAT AND INCL CANCEL REASON  *           
      *                        = 95 FOR PRINTING P13L120A AND OIC   *           
      * BP132651 28022005 SSD  INCLUDE STATUS AND PORT ELIG TAG IN  *           
      *                        P13L120A                             *           
      * BP132768 14112005 CG7  ADDED NEW OUTPUT FILE                *           
      * BP132846 01032006 SSS2 RENAME OUTPUT FILE                   *           
      * BP135548 01112016 RJB1 ADD MORE DIGIT TO AMT ON P13L120B    *           
      * =========================================================== *           
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K800   ASSIGN        TO  BP13K800                         
                             ACCESS MODE   IS  DYNAMIC                          
                             ORGANIZATION  IS  INDEXED                          
                             RECORD KEY    IS  K800-NUM-REGN                    
                             FILE STATUS   IS  BP13K800-STATUS.                 
                                                                                
           SELECT BP13K820   ASSIGN        TO  BP13K820                         
                             ACCESS MODE   IS  RANDOM                           
                             ORGANIZATION  IS  INDEXED                          
                             RECORD KEY    IS  K820-KEY-FLD                     
                             FILE STATUS   IS  BP13K820-STATUS.                 
                                                                                
           SELECT BP13K022   ASSIGN TO BP13K022                                 
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K022-KEY-FLD                      
                             ALTERNATE KEY IS K022-NUM-REGN                     
                             FILE STATUS   IS WS-K022-STATUS.                   
                                                                                
           SELECT BM06K110   ASSIGN        TO  BM06K110                         
                             ACCESS MODE   IS  RANDOM                           
                             ORGANIZATION  IS  INDEXED                          
                             RECORD KEY    IS  K110-KEY-FLD                     
                             FILE STATUS   IS  BM06K110-STATUS.                 
                                                                                
           SELECT SY02F001   ASSIGN        TO  SY02F001.                        
                                                                                
           SELECT P13L120A   ASSIGN        TO  P13L120A.                        
                                                                                
           SELECT P13L120B   ASSIGN        TO  P13L120B.                        
                                                                                
           SELECT P13L120C   ASSIGN        TO  P13L120C.                        
                                                                                
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
       FILE SECTION.                                                            
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                   00150000
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   P13L120A                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       01   PRINT-REC-1                 PIC X(132).                             
                                                                                
       FD   P13L120B                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       01   PRINT-REC-2                 PIC X(132).                             
                                                                                
       FD   P13L120C                                                            
            RECORD CONTAINS 120 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       01   PRINT-REC-3                 PIC X(120).                             
                                                                                
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-INDICATORS.                                                       
           05  BP13K800-STATUS          PIC 99      VALUE 00.                   
           05  BP13K820-STATUS          PIC 99      VALUE 00.                   
           05  BM06K110-STATUS          PIC 99      VALUE 00.                   
           05  WS-K022-STATUS           PIC 9(2)    VALUE 0.                    
           05  WS-K800-EOF              PIC X       VALUE 'N'.                  
           05  WS-K110-FND              PIC X       VALUE SPACE.                
           05  WS-SQL-CODE              PIC 9(4)    VALUE ZEROS.                
                                                                                
       01  WS-COUNTERS.                                                         
           05  CNT-K800-READ            PIC 9(5)    VALUE ZEROS.                
           05  WS-LISTING-CONTROLS .                                            
               10  LIST-COUNTERS OCCURS 2 TIMES.                                
                   15  CNT-LINE-ON-PAGE PIC 99.                                 
                   15  CNT-PAGE         PIC 9(5).                               
                   15  CNT-CASES        PIC 9(4).                               
                                                                                
       01  WS-ACCUMULATORS.                                                     
           05  WS-SOC-PAID              PIC S9(7)V99 VALUE ZEROS.               
           05  WS-BOOK-TOT-REQ          PIC S9(7)V99 VALUE ZEROS.               
           05  WS-BOOK-TOT-PAID         PIC S9(7)V99 VALUE ZEROS.               
           05  WS-SOC-DEBTS-TOT         PIC S9(7)V99 VALUE ZEROS.               
           05  WS-SOC-PAID-TOT          PIC S9(7)V99 VALUE ZEROS.               
                                                                                
       01  WS-PRT-TYP                   PIC 9       VALUE 0.                    
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURRENT-CCYY          PIC 9999.                               
           05  WS-CURRENT-MM            PIC 99.                                 
           05  WS-CURRENT-DD            PIC 99.                                 
       01  WS-CURRENT-DATE-9 REDEFINES WS-CURRENT-DATE PIC 9(8).                
                                                                                
       01  WS-EDITED-DATE.                                                      
           05  WS-EDITED-DD             PIC 9(02).                              
           05  FILLER                   PIC X(01)    VALUE '/'.                 
           05  WS-EDITED-MM             PIC 9(02).                              
           05  FILLER                   PIC X(01)    VALUE '/'.                 
           05  WS-EDITED-CCYY           PIC 9(04).                              
                                                                                
       01  WS-ACCEPT-DATE.                                                      
           05  WS-ACCEPT-YY             PIC 9(04).                              
           05  WS-ACCEPT-MM             PIC 9(02).                              
           05  WS-ACCEPT-DD             PIC 9(02).                              
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-YY                  PIC 9(04).                            
           05  WS-SYS-MM                  PIC 9(02).                            
           05  WS-SYS-DD                  PIC 9(02).                            
                                                                                
       01  WS-DATE-CHK.                                                         
           05  WS-CCYY-CHK              PIC 9999    VALUE 00.                   
           05  WS-MM-CHK                PIC 99      VALUE 00.                   
           05  WS-DD-CHK                PIC 99      VALUE 00.                   
       01  WS-DATE-CHK-9 REDEFINES WS-DATE-CHK PIC 9(8).                        
                                                                                
       01  WS-DATE-TO-DAYS.                                                     
           05  WS-LEAP-YEARS            PIC 999     VALUE ZEROS.                
           05  WS-LEAP-INDICATOR        PIC 9       VALUE ZEROS.                
           05  WS-YY-DAYS               PIC 9(6)    VALUE ZEROS.                
           05  WS-MM-DAYS               PIC 9(3)    VALUE ZEROS.                
           05  WS-DAYS                  PIC 9(8)    VALUE ZEROS.                
       01  WS-CUR-DAYS                  PIC 9(8)    VALUE ZEROS.                
       01  WS-MNTH-OUTS                 PIC 9(8)    VALUE ZEROS.                
                                                                                
       01 WS-F001-EOF                   PIC X VALUE 'N'.                        
          88 EOF-F001                         VALUE 'Y'.                        
                                                                                
       01  WS-WORK-VAR.                                                         
           05 WS-MTHLY-OS                 PIC 9(04)V99 VALUE ZEROES.            
           05 WS-DIFF-YR                  PIC 9(04)    VALUE ZEROES.            
           05 WS-DIFF-MTH                 PIC 9(04)    VALUE ZEROES.            
                                                                                
      *----------------------------------------------------------------*        
      *    LINKAGE VARIABLES USED BY THE CALLED SUB-PROGRAM            *        
      *            (BP13C913)  TO RETRIEVE ADDRESS  &  BUC-CODE        *        
      *----------------------------------------------------------------*        
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH      PIC X(4).                               
               10  WS-LINK-NUM-ACC      PIC X(5).                               
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *----------------------------------------------------------------*        
      *    REPORT PRINT HEADINGS LINES                                 *        
      *----------------------------------------------------------------*        
       01  L120-PR-HEAD-01.                                                     
           05  L120-LIST-ID             PIC X(8)    VALUE SPACES.               
           05  FILLER                   PIC X(5)    VALUE SPACES .              
           05  FILLER                   PIC X(8)    VALUE 'HDBCAT 3'.           
           05  FILLER                   PIC X(23)   VALUE SPACES.               
           05  FILLER                   PIC X(39)   VALUE                       
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(14)   VALUE SPACES.               
           05  FILLER                   PIC X(6)    VALUE 'DATE :'.             
           05  L120-DATE                PIC X(10)   VALUE SPACES.               
           05  FILLER                   PIC X(3)    VALUE SPACES.               
           05  FILLER                   PIC X(7)    VALUE 'PAGE : '.            
           05  L120-PAGE                PIC ZZZZ9.                              
                                                                                
       01  L120-PR-HEAD-2A.                                                     
           05  FILLER                   PIC X(43)   VALUE SPACES .              
           05  FILLER                   PIC X(39)   VALUE                       
               'MONTHLY EXCEPTION REPORT ON BOOKING FEE'.                       
                                                                                
       01  L120-PR-HEAD-2B.                                                     
           05  FILLER                   PIC X(32)   VALUE SPACES .              
           05  FILLER                   PIC X(49)   VALUE                       
               'MONTHLY EXCEPTION REPORT ON SOC DEBTOR FOR CASES '.             
           05  FILLER                   PIC X(19)   VALUE                       
               'MORE THAN 1 MONTH  '.                                           
                                                                                
       01  L120-PR-HEAD-3A.                                                     
           05  FILLER                   PIC X(97)   VALUE SPACES.               
           05  FILLER                   PIC X(30)   VALUE                       
               'DATE   MTH ALL ALL CNCL   PORT'.                                
                                                                                
       01  L120-PR-HEAD-3AA.                                                    
           05  FILLER                   PIC X(51)   VALUE                       
               'S/N  NT  REGN NO  CPF NO    NAME'.                              
           05  FILLER                   PIC X(44)   VALUE                       
               'FT SCH-ACCT-NO    AMT REQ        AMT PAID '.                    
           05  FILLER                   PIC X(36)   VALUE                       
               '  SELN   O/S SCH CAT RSN STAT    OIC'.                          
                                                                                
       01  L120-PR-HEAD-3B.                                                     
           05  FILLER                   PIC X(51)   VALUE                       
               'S/N  NT  REGN NO  CPF NO    NAME'.                              
           05  FILLER                   PIC X(41)   VALUE                       
               'FT SCH-ACCT-NO        AMT REQ        AMT '.                     
           05  FILLER                   PIC X(30)   VALUE                       
               'PAID      AO DATE    MTHS O/S'.                                 
                                                                                
      *----------------------------------------------------------------*        
      *    REPORT PRINT DETAIL LINE                                    *        
      *----------------------------------------------------------------*        
       01  L120-PR-DETAILS-A.                                                   
           05  L120A-SNO                PIC ZZZ9.                               
           05  FILLER                   PIC X(1)    VALUE SPACES.               
           05  L120A-NT                 PIC X(3)    VALUE SPACES.               
           05  FILLER                   PIC X(1)    VALUE SPACES.               
           05  L120A-REG-NO             PIC X(8)    VALUE SPACES.               
           05  FILLER                   PIC X(1)    VALUE SPACES.               
           05  L120A-CPF-NO             PIC X(9)    VALUE SPACES.               
           05  FILLER                   PIC X(1)    VALUE SPACES.               
           05  L120A-NAME               PIC X(22)   VALUE SPACES.               
           05  FILLER                   PIC X(1)    VALUE SPACES.               
           05  L120A-FLAT-TYPE          PIC X(2)    VALUE SPACES.               
           05  FILLER                   PIC X(1)    VALUE SPACES.               
           05  L120A-SCH-ACCT-NO        PIC X(11)   VALUE SPACES.               
           05  FILLER                   PIC X(2)    VALUE SPACES.               
           05  L120A-AMT-REQ            PIC $$$,$$9.99DB.                       
           05  FILLER                   PIC X(4)    VALUE SPACES.               
           05  L120A-AMT-PAID           PIC $$$,$$9.99DB.                       
           05  L120A-AMT-PAID-S REDEFINES                                       
               L120A-AMT-PAID           PIC $$$,$$9.99CR.                       
           05  FILLER                   PIC X(1)    VALUE SPACES.               
           05  L036-DTE-SEL             PIC X(06)   VALUE SPACES.               
           05  FILLER                   PIC X(01)   VALUE SPACES.               
           05  L036-MTHLY-OS            PIC ZZZ9    VALUE ZEROES.               
           05  FILLER                   PIC X(1)    VALUE SPACES.               
           05  L120A-NUM-ALLO-SCH       PIC X(3)    VALUE SPACES.               
           05  FILLER                   PIC X(1)    VALUE SPACES.               
           05  L120A-NUM-ALLO-CAT       PIC X(3)    VALUE SPACES.               
           05  FILLER                   PIC X(2)    VALUE SPACES.               
           05  L120A-NUM-CANCEL-REASON  PIC X(2)    VALUE SPACES.               
           05  FILLER                   PIC X(2)    VALUE SPACES.               
           05  L120A-NUM-STATUS         PIC X(2)    VALUE SPACES.               
           05  FILLER                   PIC X(1)    VALUE SPACES.               
           05  L120A-PORT-ELIG          PIC X(1)    VALUE SPACES.               
           05  FILLER                   PIC X(2)    VALUE SPACES.               
           05  L120A-NUM-OIC            PIC X(5)    VALUE SPACES.               
                                                                                
       01  L120-PR-DETAILS-B.                                                   
           05  13S1-SNO                 PIC ZZZ9.                               
           05  FILLER                   PIC X(1)    VALUE SPACES .              
           05  13S1-NT                  PIC X(3)    VALUE SPACES.               
           05  FILLER                   PIC X(1)    VALUE SPACES .              
           05  13S1-REG-NO              PIC X(8)    VALUE SPACES.               
           05  FILLER                   PIC X(1)    VALUE SPACES .              
           05  13S1-CPF-NO              PIC X(9)    VALUE SPACES.               
           05  FILLER                   PIC X(1)    VALUE SPACES .              
           05  13S1-NAME                PIC X(22)   VALUE SPACES.               
           05  FILLER                   PIC X(1)    VALUE SPACES .              
           05  13S1-FLAT-TYPE           PIC X(2)    VALUE SPACES.               
           05  FILLER                   PIC X(1)    VALUE SPACES.               
           05  13S1-SCH-ACCT-NO         PIC X(11)   VALUE SPACES.               
           05  FILLER                   PIC X(2)    VALUE SPACES.               
           05  13S1-AMT-REQ             PIC $,$$$,$$9.99DB.                     
           05  FILLER                   PIC X(4)    VALUE SPACES.               
           05  13S1-AMT-PAID            PIC $,$$$,$$9.99DB.                     
           05  13S1-AMT-PAID-S REDEFINES                                        
               13S1-AMT-PAID            PIC $,$$$,$$9.99CR.                     
           05  FILLER                   PIC X(2)    VALUE SPACES.               
           05  13S1-AO-DATE             PIC X(10)   VALUE SPACES.               
           05  FILLER                   PIC X(2)    VALUE SPACES.               
           05  13S1-MTHS-OS             PIC ZZZZ9.                              
                                                                                
      *----------------------------------------------------------------*        
      *    REPORT PRINT TOTAL LINE                                     *        
      *----------------------------------------------------------------*        
       01  L120-TOTAL-LINE.                                                     
           05  FILLER                   PIC X(12)   VALUE SPACES.               
           05  13S1-STR1                PIC X(6)    VALUE SPACES.               
           05  13S1-STR2                PIC X(14)  VALUE                        
               'NO OF CASES = '.                                                
           05  13S1-CASES-EDITED        PIC ZZZZ9 .                             
           05  FILLER REDEFINES 13S1-CASES-EDITED   PIC X(5) .                  
           05  FILLER                   PIC X(14)    VALUE SPACES.              
           05  13S1-STR3                PIC X(14)    VALUE                      
               'SUB-TOTAL :  $'.                                                
           05  13S1-TOT-REQ             PIC Z,ZZZ,ZZ9.99DB.                     
           05  FILLER                   PIC X(1)     VALUE SPACES.              
           05  FILLER                   PIC X(1)     VALUE '$'.                 
           05  13S1-TOT-PAID            PIC Z,ZZZ,ZZ9.99DB.                     
           05  13S1-TOT-PAIDS  REDEFINES                                        
               13S1-TOT-PAID            PIC Z,ZZZ,ZZ9.99CR.                     
                                                                                
       01  L120-DRAW-LINE.                                                      
           05  FILLER                   PIC X(65)    VALUE SPACES.              
           05  13S1-ULINE1              PIC X(14)    VALUE SPACES.              
           05  FILLER                   PIC X(02)    VALUE SPACE.               
           05  13S1-ULINE2              PIC X(14)    VALUE SPACES.              
                                                                                
       01  L120-CONTROL-LINE.                                                   
           05  FILLER                   PIC X(19)    VALUE SPACES.              
           05  CNTL-ITEM                PIC X(30)    VALUE SPACES.              
           05  CNTL-CNT                 PIC ZZZZ9 .                             
                                                                                
      *----------------------------------------------------------------*        
      *    FILE FORMAT FOR P13L120C FILE                               *        
      *----------------------------------------------------------------*        
       01  L120C-FILE-FORMAT.                                                   
           05  L120C-NT                 PIC X(3)    VALUE SPACES.               
           05  WS-SEMI01                PIC X(1)    VALUE SPACES .              
           05  L120C-REG-NO             PIC X(8)    VALUE SPACES.               
           05  WS-SEMI02                PIC X(1)    VALUE SPACES .              
           05  L120C-CPF-NO             PIC X(9)    VALUE SPACES.               
           05  WS-SEMI03                PIC X(1)    VALUE SPACES .              
           05  L120C-NAME               PIC X(22)   VALUE SPACES.               
           05  WS-SEMI04                PIC X(1)    VALUE SPACES .              
           05  L120C-FLAT-TYPE          PIC X(2)    VALUE SPACES.               
           05  WS-SEMI05                PIC X(1)    VALUE SPACES.               
           05  L120C-SCH-ACCT-NO        PIC X(11)   VALUE SPACES.               
           05  WS-SEMI06                PIC X(1)    VALUE SPACES.               
           05  L120C-AMT-REQ            PIC $$$,$$9.99DB.                       
           05  WS-SEMI07                PIC X(1)    VALUE SPACES.               
           05  L120C-AMT-PAID           PIC $$$,$$9.99DB.                       
           05  L120C-AMT-PAID-S REDEFINES                                       
               L120C-AMT-PAID           PIC $$$,$$9.99CR.                       
           05  WS-SEMI08                PIC X(1)    VALUE SPACES.               
           05  L120C-DTE-SEL            PIC X(06)   VALUE SPACES.               
           05  WS-SEMI09                PIC X(01)   VALUE SPACES.               
           05  L120C-MTHLY-OS           PIC ZZZ9    VALUE ZEROES.               
           05  WS-SEMI10                PIC X(1)    VALUE SPACES.               
           05  L120C-NUM-ALLO-SCH       PIC X(3)    VALUE SPACES.               
           05  WS-SEMI11                PIC X(1)    VALUE SPACES.               
           05  L120C-NUM-ALLO-CAT       PIC X(3)    VALUE SPACES.               
           05  WS-SEMI12                PIC X(1)    VALUE SPACES.               
           05  L120C-NUM-CANCEL-REASON  PIC X(2)    VALUE SPACES.               
           05  WS-SEMI13                PIC X(1)    VALUE SPACES.               
           05  L120C-NUM-STATUS         PIC X(2)    VALUE SPACES.               
           05  WS-SEMI14                PIC X(1)    VALUE SPACES.               
           05  L120C-PORT-ELIG          PIC X(1)    VALUE SPACES.               
           05  WS-SEMI15                PIC X(1)    VALUE SPACES.               
           05  L120C-NUM-OIC            PIC X(5)    VALUE SPACES.               
                                                                                
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      *    MAIN LOGIC PROCESS SECTION                                  *        
      ******************************************************************        
       000-MAIN-PROCESS.                                                        
                                                                                
           PERFORM  100-OPEN-ROUTINE              THRU  100-EXIT.               
           PERFORM  150-INITIALIZATION-ROUTINE    THRU  150-EXIT.               
           PERFORM  200-READ-BP13K800             THRU  200-EXIT.               
           PERFORM  300-PROCESS-EXCEPTION-REPORT  THRU  300-EXIT                
                        UNTIL WS-K800-EOF = 'Y'.                                
           PERFORM  600-PRINT-TOTALS              THRU  600-EXIT.               
           PERFORM  900-CLOSE-ROUTINE             THRU  900-EXIT.               
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    OPENING FILES SECTION                                       *        
      ******************************************************************        
       100-OPEN-ROUTINE.                                                        
           OPEN INPUT  BP13K800                                                 
                       BP13K820                                                 
                       BP13K022                                                 
                       BM06K110                                                 
                       SY02F001                                                 
                OUTPUT P13L120A                                                 
                       P13L120B                                                 
                       P13L120C.                                                
                                                                                
           IF BP13K800-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP13K800-STATUS ' BP13K800-STATUS         
              MOVE BP13K800-STATUS TO RETURN-CODE                               
              PERFORM 900-CLOSE-ROUTINE.                                        
                                                                                
           IF BP13K820-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP13K820-STATUS ' BP13K820-STATUS         
              MOVE BP13K820-STATUS TO RETURN-CODE                               
              PERFORM 900-CLOSE-ROUTINE.                                        
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K022 STATUS IS ' WS-K022-STATUS            
              MOVE     WS-K022-STATUS TO RETURN-CODE                            
              PERFORM  900-CLOSE-ROUTINE.                                       
                                                                                
           IF BM06K110-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BM06K110-STATUS ' BM06K110-STATUS         
              MOVE BM06K110-STATUS TO RETURN-CODE                               
              PERFORM 900-CLOSE-ROUTINE.                                        
                                                                                
           READ SY02F001 AT END MOVE 'Y'  TO WS-F001-EOF.                       
           IF EOF-F001                                                          
              DISPLAY 'RECORD NOT FOUND IN SY01F001 '                           
              DISPLAY 'PROCESSING TERMINATED '                                  
              PERFORM 900-CLOSE-ROUTINE                                         
           ELSE                                                                 
              MOVE F001-DTE-CURRENT       TO WS-CURRENT-DATE                    
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE                         
           MOVE   WS-SYS-YY                TO    WS-EDITED-CCYY                 
                                                 WS-CURRENT-CCYY.               
           MOVE   WS-SYS-MM                TO    WS-EDITED-MM                   
                                                 WS-CURRENT-MM.                 
           MOVE   WS-SYS-DD                TO    WS-EDITED-DD                   
                                                 WS-CURRENT-DD.                 
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    -   COMPUTING THE DAYS IN CURRENT DATE                      *        
      *    -   INITIALIZE WORKING VARIABLES USED                       *        
      *    -   SET POINTER AT BEGINNING OF THE VSAM FILE BP13K800      *        
      ******************************************************************        
       150-INITIALIZATION-ROUTINE.                                              
                                                                                
           COMPUTE WS-CUR-DAYS =                                                
                   FUNCTION INTEGER-OF-DATE(WS-CURRENT-DATE-9) + 0.             
                                                                                
           MOVE    66                TO    CNT-LINE-ON-PAGE (1)                 
                                           CNT-LINE-ON-PAGE (2).                
           MOVE    ZEROES            TO    CNT-PAGE (1)  CNT-CASES (1)          
                                           CNT-PAGE (2)  CNT-CASES (2).         
           MOVE    LOW-VALUES        TO    K800-NUM-REGN.                       
                                                                                
           START   BP13K800   KEY >  K800-NUM-REGN                              
                                                                                
           IF BP13K800-STATUS NOT = 00                                          
              DISPLAY 'ERROR IN START READING BP13K800, STATUS = '              
                                              BP13K800-STATUS                   
              MOVE BP13K800-STATUS TO RETURN-CODE                               
              PERFORM 900-CLOSE-ROUTINE.                                        
                                                                                
       150-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    READ VSAM FILE BP13K800 SEQUENTIALLY AT END SET IND TO 'Y'  *        
      ******************************************************************        
       200-READ-BP13K800.                                                       
           READ BP13K800 NEXT AT END                                            
                              MOVE 'Y' TO WS-K800-EOF                           
                              GO TO 200-EXIT.                                   
                                                                                
           ADD 1 TO CNT-K800-READ.                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    CHECKING FOR EXCEPTION CASES                                *        
      ******************************************************************        
       300-PROCESS-EXCEPTION-REPORT.                                            
                                                                                
           IF K800-AMT-BK-REQ IS NOT NUMERIC                                    
              MOVE ZEROES     TO K800-AMT-BK-REQ.                               
                                                                                
           INSPECT K800-DTE-RAISE-AO  REPLACING ALL SPACES BY ZEROES.           
                                                                                
           IF K800-DTE-RAISE-AO = '00000000'                                    
              IF K800-AMT-BK-REQ = ZEROES                                       
                 NEXT SENTENCE                                                  
              ELSE                                                              
                 PERFORM 320-READ-BM06K110  THRU 320-EXIT                       
                 PERFORM 340-CHECK-BOOK-FEE THRU 340-EXIT                       
           ELSE                                                                 
              PERFORM  320-READ-BM06K110    THRU 320-EXIT                       
              PERFORM  360-CHECK-DEBTOR-FEE THRU 360-EXIT.                      
                                                                                
           PERFORM 200-READ-BP13K800        THRU 200-EXIT.                      
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    READ VSAM FILE BM06K110 TO RETRIEVE ADDRESS & FLAT TYPE     *        
      ******************************************************************        
       320-READ-BM06K110.                                                       
           MOVE 'Y'                TO  WS-K110-FND.                             
           MOVE K800-NUM-SCH-ACC   TO  K110-KEY-FLD.                            
                                                                                
           READ BM06K110.                                                       
                                                                                
           IF BM06K110-STATUS = 00                                              
              NEXT SENTENCE                                                     
           ELSE                                                                 
              IF BM06K110-STATUS = 23                                           
                 MOVE 'N' TO   WS-K110-FND                                      
              ELSE                                                              
                 DISPLAY 'ERROR READING BM06K110, STATUS  =  '                  
                                        BM06K110-STATUS                         
                         ',     K110-KEY-FLD = ' K110-KEY-FLD                   
                 MOVE BM06K110-STATUS TO RETURN-CODE                            
                 PERFORM 900-CLOSE-ROUTINE.                                     
                                                                                
       320-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    CHECKING FOR BOOKING FEE EXCEPTION                          *        
      ******************************************************************        
       340-CHECK-BOOK-FEE.                                                      
                                                                                
           MOVE ZEROES           TO WS-DIFF-YR                                  
                                    WS-DIFF-MTH                                 
                                    WS-MTHLY-OS.                                
                                                                                
           IF WS-K110-FND NOT = 'Y'                                             
              GO TO 340-EXIT.                                                   
                                                                                
           IF K110-DTE-ACCEPTANCE IS NOT NUMERIC                                
              MOVE ZEROES               TO K110-DTE-ACCEPTANCE                  
           END-IF.                                                              
                                                                                
           MOVE K110-DTE-ACCEPTANCE     TO WS-ACCEPT-DATE.                      
                                                                                
           IF K110-DTE-ACCEPTANCE NOT = SPACES AND ZEROES AND LOW-VALUES        
              IF WS-CURRENT-MM > WS-ACCEPT-MM                                   
                 COMPUTE WS-DIFF-YR  = WS-CURRENT-CCYY - WS-ACCEPT-YY           
                 COMPUTE WS-DIFF-MTH = WS-CURRENT-MM - WS-ACCEPT-MM             
              ELSE                                                              
                 COMPUTE WS-DIFF-YR  = WS-CURRENT-CCYY -                        
                                       WS-ACCEPT-YY - 1                         
                 COMPUTE WS-DIFF-MTH = (12 - WS-ACCEPT-MM) +                    
                                       WS-CURRENT-MM                            
              END-IF                                                            
              IF WS-DIFF-YR NOT = ZEROES                                        
                 COMPUTE WS-MTHLY-OS = (WS-DIFF-YR * 12) + WS-DIFF-MTH          
              ELSE                                                              
                 MOVE WS-DIFF-MTH      TO WS-MTHLY-OS                           
              END-IF                                                            
           ELSE                                                                 
              MOVE ZEROES               TO WS-MTHLY-OS                          
           END-IF.                                                              
                                                                                
           PERFORM 520-CALL-BP13C913  THRU  520-EXIT.                           
                                                                                
           IF K800-AMT-BK-PAID IS NOT NUMERIC                                   
              MOVE ZEROES   TO K800-AMT-BK-PAID.                                
                                                                                
           IF K800-AMT-BK-PAID NOT  =   K800-AMT-BK-REQ                         
              MOVE    1                 TO WS-PRT-TYP                           
              ADD     1                 TO CNT-CASES(WS-PRT-TYP)                
              ADD     K800-AMT-BK-PAID  TO WS-BOOK-TOT-PAID                     
              ADD     K800-AMT-BK-REQ   TO WS-BOOK-TOT-REQ                      
              MOVE    K800-AMT-BK-PAID  TO 13S1-AMT-PAID L120A-AMT-PAID         
                                           L120C-AMT-PAID                       
              MOVE    K800-AMT-BK-REQ   TO 13S1-AMT-REQ  L120A-AMT-REQ          
                                           L120C-AMT-REQ                        
              MOVE    K800-NUM-ALLO-SCHEME                                      
                                        TO L120A-NUM-ALLO-SCH                   
                                           L120C-NUM-ALLO-SCH                   
              MOVE    K800-NUM-ALLO-CAT TO L120A-NUM-ALLO-CAT                   
                                           L120C-NUM-ALLO-CAT                   
                                                                                
              IF K800-NUM-CANCEL-REASON = '95'                                  
                 MOVE K800-NUM-CANCEL-REASON TO L120A-NUM-CANCEL-REASON         
                                                L120C-NUM-CANCEL-REASON         
              ELSE                                                              
                 MOVE SPACES TO L120A-NUM-CANCEL-REASON                         
                                L120C-NUM-CANCEL-REASON                         
              END-IF                                                            
                                                                                
              MOVE    K800-NUM-ALLO-CAT TO L120A-NUM-ALLO-CAT                   
                                           L120C-NUM-ALLO-CAT                   
              MOVE    K800-NUM-STATUS   TO L120A-NUM-STATUS                     
                                           L120C-NUM-STATUS                     
              MOVE    K800-NUM-PORT-ELIG-TAG                                    
                                        TO L120A-PORT-ELIG                      
                                           L120C-PORT-ELIG                      
              MOVE WS-ACCEPT-DATE       TO L036-DTE-SEL                         
                                           L120C-DTE-SEL                        
              MOVE WS-MTHLY-OS          TO L036-MTHLY-OS                        
                                           L120C-MTHLY-OS                       
                                                                                
              PERFORM 500-PRINT-ERROR-PROCESS   THRU  500-EXIT.                 
                                                                                
       340-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    CHECKING FOR OUTSTANDING DEBTS                              *        
      ******************************************************************        
       360-CHECK-DEBTOR-FEE.                                                    
                                                                                
           IF K800-DTE-RAISE-AO > ZEROES                                        
              MOVE K800-DTE-RAISE-AO   TO   WS-DATE-CHK                         
           ELSE                                                                 
               GO TO 360-EXIT                                                   
           END-IF.                                                              
                                                                                
           COMPUTE WS-DAYS =                                                    
                   FUNCTION INTEGER-OF-DATE(WS-DATE-CHK-9) + 30.                
                                                                                
           IF  WS-CUR-DAYS < WS-DAYS                                            
               GO TO 360-EXIT.                                                  
                                                                                
           IF K800-AMT-SOC-DEBTOR IS  NOT  NUMERIC                              
              MOVE ZEROES         TO  K800-AMT-SOC-DEBTOR.                      
           IF K800-AMT-SOC-CASH   IS  NOT  NUMERIC                              
              MOVE ZEROES         TO  K800-AMT-SOC-CASH.                        
           IF K800-AMT-CPF1-PAID  IS  NOT  NUMERIC                              
              MOVE ZEROES         TO  K800-AMT-CPF1-PAID.                       
           IF K800-AMT-CPF2-PAID  IS  NOT  NUMERIC                              
              MOVE ZEROES         TO  K800-AMT-CPF2-PAID.                       
           IF K800-AMT-CPF3-PAID  IS  NOT  NUMERIC                              
              MOVE ZEROES         TO  K800-AMT-CPF3-PAID.                       
           IF K800-AMT-CPF4-PAID  IS  NOT  NUMERIC                              
              MOVE ZEROES         TO  K800-AMT-CPF4-PAID.                       
                                                                                
           IF K800-AMT-SOC-DEBTOR  NOT  =  ZEROES                               
              MOVE     2           TO   WS-PRT-TYP                              
              ADD      1           TO   CNT-CASES(WS-PRT-TYP)                   
              COMPUTE  WS-SOC-PAID  =   K800-AMT-SOC-CASH  +                    
                                        K800-AMT-CPF1-PAID +                    
                                        K800-AMT-CPF2-PAID +                    
                                        K800-AMT-CPF3-PAID +                    
                                        K800-AMT-CPF4-PAID                      
               MOVE    WS-SOC-PAID               TO   13S1-AMT-PAID-S           
                                                      L120A-AMT-PAID-S          
                                                      L120C-AMT-PAID-S          
               MOVE    K800-AMT-SOC-DEBTOR       TO   13S1-AMT-REQ              
                                                      L120A-AMT-REQ             
                                                      L120C-AMT-REQ             
               ADD     K800-AMT-SOC-DEBTOR       TO   WS-SOC-DEBTS-TOT          
               ADD     WS-SOC-PAID               TO   WS-SOC-PAID-TOT           
               MOVE    ZEROES                    TO   WS-MNTH-OUTS              
               MOVE    F001-DTE-CURRENT          TO   WS-CURRENT-DATE           
               MOVE    K800-DTE-RAISE-AO         TO   WS-DATE-CHK               
               COMPUTE WS-MNTH-OUTS =                                           
                          (((12 * WS-CURRENT-CCYY) + WS-CURRENT-MM) -           
                          ((12 * WS-CCYY-CHK) + WS-MM-CHK ))                    
                                                                                
               PERFORM 500-PRINT-ERROR-PROCESS   THRU 500-EXIT.                 
                                                                                
       360-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    PRINTING OF EXCEPTION REPORT                                *        
      ******************************************************************        
       500-PRINT-ERROR-PROCESS.                                                 
           PERFORM 540-READ-BP13K820  THRU  540-EXIT.                           
           PERFORM 510-READ-BP13K022  THRU  510-EXIT.                           
                                                                                
           IF CNT-LINE-ON-PAGE (WS-PRT-TYP)  >  46                              
              MOVE  2  TO  CNT-LINE-ON-PAGE (WS-PRT-TYP)                        
              PERFORM  560-PRINT-ERROR-HEADING  THRU 560-EXIT                   
           ELSE                                                                 
              ADD   2  TO  CNT-LINE-ON-PAGE (WS-PRT-TYP).                       
                                                                                
           MOVE  COMM8-CDE-TYP-ACTLUSE TO 13S1-FLAT-TYPE                        
                                          L120A-FLAT-TYPE                       
                                          L120C-FLAT-TYPE.                      
           MOVE  CNT-CASES(WS-PRT-TYP) TO 13S1-SNO    L120A-SNO.                
           MOVE  K800-NUM-NEW-TOWN     TO 13S1-NT     L120A-NT                  
                                          L120C-NT.                             
           MOVE  K800-NUM-REGN         TO 13S1-REG-NO L120A-REG-NO              
                                          L120C-REG-NO.                         
           MOVE  K800-NUM-SCH-ACC      TO 13S1-SCH-ACCT-NO                      
                                          L120A-SCH-ACCT-NO                     
                                          L120C-SCH-ACCT-NO.                    
           MOVE  WS-MNTH-OUTS          TO 13S1-MTHS-OS.                         
                                                                                
           STRING K800-DTE-RAISE-AO(7:2) '/'                                    
                  K800-DTE-RAISE-AO(5:2) '/'                                    
                  K800-DTE-RAISE-AO(1:4) DELIMITED BY SIZE                      
                  INTO 13S1-AO-DATE.                                            
                                                                                
           MOVE ';'               TO   WS-SEMI01  WS-SEMI02                     
                                       WS-SEMI03  WS-SEMI04                     
                                       WS-SEMI05  WS-SEMI06                     
                                       WS-SEMI07  WS-SEMI08                     
                                       WS-SEMI09  WS-SEMI10                     
                                       WS-SEMI11  WS-SEMI12                     
                                       WS-SEMI13  WS-SEMI14                     
                                       WS-SEMI15                                
                                                                                
           IF WS-PRT-TYP = 1                                                    
              WRITE PRINT-REC-1 FROM L120-PR-DETAILS-A                          
              WRITE PRINT-REC-3 FROM L120C-FILE-FORMAT.                         
           IF WS-PRT-TYP = 2                                                    
              WRITE PRINT-REC-2 FROM L120-PR-DETAILS-B.                         
                                                                                
           MOVE SPACES            TO   L120-PR-DETAILS-A.                       
           MOVE SPACES            TO   L120-PR-DETAILS-B.                       
           MOVE SPACES            TO   L120C-FILE-FORMAT.                       
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       510-READ-BP13K022.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES        TO K022-NUM-REGN.                                 
           MOVE K800-NUM-REGN TO K022-NUM-REGN.                                 
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           IF WS-K022-STATUS = 00                                               
              MOVE K022-NUM-OIC           TO   L120A-NUM-OIC                    
                                               L120C-NUM-OIC                    
           ELSE                                                                 
              IF WS-K022-STATUS = 23                                            
                 DISPLAY 'K022 NOT FND, KEY ' K022-NUM-REGN                     
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K022 = '  WS-K022-STATUS            
                         ', KEY ' K022-NUM-REGN                                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
       510-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    CALL BP13C913 TO GET FLAT INFO                              *        
      ******************************************************************        
       520-CALL-BP13C913.                                                       
           MOVE SPACES                TO BP13COMM8-REC.                         
           INITIALIZE                    BP13COMM8-REC.                         
                                                                                
           MOVE K800-NUM-SCH-ACC  TO WS-LINK-REC.                               
                                                                                
           CALL 'BP13C913'  USING WS-LINK-REC,                                  
                                  BP13COMM8-REC.                                
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              MOVE COMM8-CDE-SYSERR TO WS-SQL-CODE                              
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PIDB TABLE  ' WS-SQL-CODE        
              ELSE                                                              
                 DISPLAY 'ERROR READING PIDB TABLE  ' WS-SQL-CODE               
                 MOVE 99 TO RETURN-CODE                                         
                 PERFORM 900-CLOSE-ROUTINE                                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       520-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    READ VSAM FILE BP13K820 USING K800-NUM-REGN AS A KEY        *        
      ******************************************************************        
       540-READ-BP13K820.                                                       
           MOVE K800-NUM-REGN   TO  K820-NUM-REGN.                              
           MOVE K800-NUM-NRIC1  TO  K820-NUM-NRIC.                              
                                                                                
           READ BP13K820.                                                       
                                                                                
           IF BP13K820-STATUS = 00                                              
              MOVE K820-NME-OCCP   TO 13S1-NAME   L120A-NAME                    
                                      L120C-NAME                                
              MOVE K820-NUM-CPF    TO 13S1-CPF-NO L120A-CPF-NO                  
                                      L120C-CPF-NO                              
           ELSE                                                                 
              IF BP13K820-STATUS = 23                                           
                 MOVE     SPACES   TO 13S1-NAME 13S1-CPF-NO                     
                 MOVE     SPACES   TO L120A-NAME L120A-CPF-NO                   
                                      L120C-NAME L120C-CPF-NO                   
                 DISPLAY  SPACES                                                
                 DISPLAY 'BP13K820 - REC NOT FOUND : ' K820-KEY-FLD             
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K820, STATUS = '                    
                                        BP13K820-STATUS                         
                         ', KEY-FLD = ' K820-KEY-FLD                            
                 MOVE BP13K820-STATUS TO RETURN-CODE                            
                 PERFORM 900-CLOSE-ROUTINE.                                     
                                                                                
       540-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    PRINT REPORT HEADING LINES                                  *        
      ******************************************************************        
       560-PRINT-ERROR-HEADING.                                                 
           ADD 1 TO CNT-PAGE(WS-PRT-TYP).                                       
                                                                                
           IF CNT-CASES(WS-PRT-TYP) > 2                                         
              PERFORM 580-PRINT-FOOTING    THRU  580-EXIT.                      
                                                                                
           MOVE CNT-PAGE(WS-PRT-TYP) TO L120-PAGE.                              
           MOVE WS-CURRENT-DD        TO WS-EDITED-DD                            
           MOVE WS-CURRENT-MM        TO WS-EDITED-MM                            
           MOVE WS-CURRENT-CCYY      TO WS-EDITED-CCYY                          
           MOVE WS-EDITED-DATE       TO L120-DATE.                              
                                                                                
           MOVE SPACES   TO    PRINT-REC-1 PRINT-REC-2.                         
           IF WS-PRT-TYP = 1                                                    
              MOVE 'P13L120A'   TO L120-LIST-ID                                 
              WRITE PRINT-REC-1 FROM L120-PR-HEAD-01 AFTER PAGE                 
              WRITE PRINT-REC-1 FROM L120-PR-HEAD-2A AFTER 2                    
              WRITE PRINT-REC-1 FROM L120-PR-HEAD-3A AFTER 2                    
              WRITE PRINT-REC-1 FROM L120-PR-HEAD-3AA AFTER 1                   
              MOVE  ALL '-'     TO   PRINT-REC-1                                
              WRITE PRINT-REC-1                                                 
              MOVE  SPACES      TO   PRINT-REC-1                                
           ELSE                                                                 
           IF WS-PRT-TYP = 2                                                    
              MOVE 'P13L120B'   TO L120-LIST-ID                                 
              WRITE PRINT-REC-2 FROM L120-PR-HEAD-01 AFTER PAGE                 
              WRITE PRINT-REC-2 FROM L120-PR-HEAD-2B AFTER 2                    
              WRITE PRINT-REC-2 FROM L120-PR-HEAD-3B AFTER 2                    
              MOVE  ALL '-'     TO   PRINT-REC-2                                
              WRITE PRINT-REC-2                                                 
              MOVE  SPACES      TO   PRINT-REC-2.                               
                                                                                
       560-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    PRINT REPORT FOOTINGS LINES                                 *        
      ******************************************************************        
       580-PRINT-FOOTING.                                                       
           MOVE SPACES  TO PRINT-REC-1 PRINT-REC-2.                             
                                                                                
           IF WS-PRT-TYP = 1                                                    
            MOVE ' EXCEPTION: BOOKING FEE NOT CONSISTENT WITH FLAT TYPE'        
                 TO PRINT-REC-1                                                 
              WRITE PRINT-REC-1 AFTER 2.                                        
                                                                                
           IF WS-PRT-TYP = 2                                                    
              MOVE ' EXCEPTION: SOC-DEBTS NOT PAID'  TO  PRINT-REC-2            
              WRITE PRINT-REC-2 AFTER 2.                                        
                                                                                
       580-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    PRINT REPORT TOTAL LINES                                    *        
      ******************************************************************        
       600-PRINT-TOTALS.                                                        
           MOVE SPACES TO PRINT-REC-1 PRINT-REC-2.                              
           MOVE ' EXCEPTION: BOOKING FEE NOT CONSISTENT WITH FLAT TYPE'         
                  TO PRINT-REC-1.                                               
           MOVE ' EXCEPTION: SOC-DEBTS NOT PAID'  TO  PRINT-REC-2.              
           WRITE PRINT-REC-1 AFTER 2.                                           
           WRITE PRINT-REC-2 AFTER 2.                                           
                                                                                
           MOVE  SPACES            TO    L120-TOTAL-LINE.                       
           MOVE  ALL '-'           TO    13S1-ULINE1 13S1-ULINE2.               
           WRITE PRINT-REC-1       FROM  L120-DRAW-LINE.                        
           WRITE PRINT-REC-2       FROM  L120-DRAW-LINE.                        
                                                                                
           MOVE  'NO OF CASES = '  TO    13S1-STR2.                             
           MOVE  'SUB-TOTAL'       TO    13S1-STR3.                             
           MOVE  CNT-CASES(1)      TO    13S1-CASES-EDITED.                     
           MOVE  WS-BOOK-TOT-PAID  TO    13S1-TOT-PAID.                         
           MOVE  WS-BOOK-TOT-REQ   TO    13S1-TOT-REQ.                          
           WRITE PRINT-REC-1       FROM  L120-TOTAL-LINE.                       
                                                                                
           MOVE  CNT-CASES(2)      TO    13S1-CASES-EDITED.                     
           MOVE  WS-SOC-PAID-TOT   TO    13S1-TOT-PAIDS.                        
           MOVE  WS-SOC-DEBTS-TOT  TO    13S1-TOT-REQ.                          
           WRITE PRINT-REC-2       FROM  L120-TOTAL-LINE.                       
                                                                                
           MOVE  SPACES            TO    L120-TOTAL-LINE.                       
           MOVE  ALL '-'           TO    13S1-ULINE1      13S1-ULINE2.          
           WRITE PRINT-REC-1       FROM  L120-DRAW-LINE.                        
           WRITE PRINT-REC-2       FROM  L120-DRAW-LINE.                        
                                                                                
           MOVE  'TOTAL'           TO    13S1-STR1.                             
           MOVE  'NO OF CASES ='   TO    13S1-STR2.                             
           ADD   CNT-CASES(1) CNT-CASES(2)                                      
                 GIVING 13S1-CASES-EDITED.                                      
                                                                                
           MOVE  'GRAND TOTAL'     TO 13S1-STR3.                                
           ADD   WS-BOOK-TOT-REQ   WS-SOC-DEBTS-TOT                             
                 GIVING 13S1-TOT-REQ.                                           
           ADD   WS-BOOK-TOT-PAID WS-SOC-PAID-TOT                               
                 GIVING 13S1-TOT-PAID.                                          
           WRITE PRINT-REC-2 FROM L120-TOTAL-LINE.                              
                                                                                
           MOVE  SPACES      TO   L120-TOTAL-LINE.                              
           MOVE  ALL '-'     TO   13S1-ULINE1 13S1-ULINE2.                      
           WRITE PRINT-REC-2 FROM L120-DRAW-LINE.                               
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*****  BP13C120 CONTROL TOTAL *****'.                       
           DISPLAY SPACES.                                                      
           DISPLAY 'K800 RECORDS READ         :  ' CNT-K800-READ.               
           DISPLAY 'BOOKING FEES ERROR CASES  :  ' CNT-CASES(1).                
           DISPLAY 'SOC DEBTORS  ERROR CASES  :  ' CNT-CASES(2).                
           DISPLAY 'RECS WRITTEN TO P13L120C  :  ' CNT-CASES(1).                
                                                                                
           CLOSE BP13K800                                                       
                 BP13K820                                                       
                 BP13K022                                                       
                 BM06K110                                                       
                 P13L120A                                                       
                 P13L120B                                                       
                 P13L120C.                                                      
                                                                                
           IF BP13K800-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, BP13K800-STATUS ' BP13K800-STATUS         
              MOVE BP13K800-STATUS TO RETURN-CODE.                              
                                                                                
           IF BP13K820-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, BP13K820-STATUS ' BP13K820-STATUS         
              MOVE BP13K820-STATUS TO RETURN-CODE.                              
                                                                                
           IF BM06K110-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, BM06K110-STATUS ' BM06K110-STATUS         
              MOVE BM06K110-STATUS TO RETURN-CODE.                              
                                                                                
           IF WS-K022-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR K022 STATUS ' WS-K022-STATUS               
              MOVE     WS-K022-STATUS TO RETURN-CODE.                           
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
*******************   END  OF  SOURCE  PROGRAM   ***********************        
