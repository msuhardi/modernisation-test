       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CRG2.                                                 
      *AUTHOR.        KARL MAGALONA CABUG.                                      
      *DATE-WRITTEN.  10 FEB 2017.                                              
      *============================================================*            
      * BP13 - SYSTEM OF COMMITMENT                                *            
      *============================================================*            
      * OBJECTIVE :                                                *            
      * INPUT     :  BM06F120  -                                   *            
      *              BM06K110  -                                   *            
      *              BP13K800  -                                   *            
      *              BP13K820  -                                   *            
      *              BP13K813  -                                   *            
      * OUTPUT    :  BP13FOUT  -                                   *            
      *                                                            *            
      *----------------------------------------------------------- *            
      * CHG REF  DATE     BY     DESCRIPTION                       *            
      * -------- -------- ----   ------------------------------    *            
      * BM06XXXX 10022017 KAM4   NEW PROGRAM.                      *            
      * BP137103 21122017 KAM4   REVISED AGE-HDR LEN FROM 17 TO 3  *            
      *                          REVISED READ POSITION OF BP13K800 *            
      * BP13XXXX 21022018 KAM4   ADDED CHECK FOR 3GEN              *            
      * BP13XXXX 27052019 CC37   ADDED HISTORY FILES               *            
      *============================================================*            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
      *-------------------------------------------------------------            
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BM06F120 ASSIGN TO BM06F120.                                  
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K110-KEY-FLD                               
                  FILE STATUS     IS WS-K110-STATUS.                            
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS WS-K800-STATUS.                            
                                                                                
           SELECT BP13K893 ASSIGN TO BP13K893                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K893-KEY-FLD                               
                  FILE STATUS     IS WS-K893-STATUS.                            
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K820-KEY-FLD                               
                  FILE STATUS     IS WS-K820-STATUS.                            
                                                                                
           SELECT BP13K895 ASSIGN TO BP13K895                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K895-KEY-FLD                               
                  FILE STATUS     IS WS-K895-STATUS.                            
                                                                                
           SELECT BP13K813 ASSIGN TO BP13K813                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K813-KEY-FLD                               
                  FILE STATUS     IS WS-K813-STATUS.                            
                                                                                
           SELECT BP13K060 ASSIGN TO BP13K060                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K060-KEY-FLD                               
                  FILE STATUS     IS WS-K060-STATUS.                            
                                                                                
           SELECT BP13FOUT ASSIGN TO BP13FOUT.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BM06F120                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS 700 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BM06F120.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K893                                                            
            RECORD CONTAINS 2050 CHARACTERS.                                    
       COPY BP13K893.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K895                                                            
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K895.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS 25 CHARACTERS.                                      
       COPY BP13K060.                                                           
                                                                                
       FD  BP13FOUT                                                             
           RECORD CONTAINS 250 CHARACTERS                                       
           RECORDING  MODE  IS F.                                               
       01  BP13FOUT-REC                  PIC X(250).                            
                                                                                
      *------------------------------------------------------------*            
       WORKING-STORAGE SECTION.                                                 
      *------------------------------------------------------------*            
                                                                                
       01  WS-COUNTER.                                                          
           05  WS-F120-CNT               PIC 9(07)  VALUE ZEROES.               
           05  WS-K110-NTFND-CTR         PIC 9(07)  VALUE ZEROES.               
           05  WS-FOUT-CNT               PIC 9(07)  VALUE ZEROES.               
           05  WS-K110-SKIP-REC          PIC 9(07)  VALUE ZEROES.               
                                                                                
       01  WS-OTHER-VAR.                                                        
           05  WS-TMP-REGN               PIC X(02)  VALUE SPACES.               
           05  WS-OK                     PIC X(01)  VALUE SPACES.               
           05  WS-F120-END               PIC X(01)  VALUE SPACES.               
           05  WS-K800-FND               PIC X(01)  VALUE SPACES.               
           05  WS-TMP-AGE                PIC 9(02)  VALUE ZEROES.               
           05  WS-K110-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-K800-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-K895-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-K893-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-K820-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-K820-FND               PIC X(01)  VALUE SPACES.               
           05  WS-K893-FND               PIC X(01)  VALUE SPACES.               
           05  WS-K895-FND               PIC X(01)  VALUE SPACES.               
           05  WS-K820-EOF               PIC X(01)  VALUE SPACES.               
           05  WS-K895-EOF               PIC X(01)  VALUE SPACES.               
           05  WS-K893-EOF               PIC X(01)  VALUE SPACES.               
           05  WS-K813-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-K060-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-CURRENT-DATE           PIC X(08)  VALUE SPACES.               
           05  WS-CURR-DATE REDEFINES WS-CURRENT-DATE.                          
               15 WS-CURR-CCYY           PIC 9(04).                             
               15 WS-CURR-MM             PIC 9(02).                             
               15 WS-CURR-DD             PIC 9(02).                             
           05  WS-DATE-OF-BIRTH          PIC X(08)  VALUE SPACES.               
           05  WS-BIRTH-DATE REDEFINES WS-DATE-OF-BIRTH.                        
               15 WS-DOB-CCYY            PIC 9(04).                             
               15 WS-DOB-MM              PIC 9(02).                             
               15 WS-DOB-DD              PIC 9(02).                             
                                                                                
       01 BP13FOUT-HEADER.                                                      
           05  WS-HRD-HDBREF             PIC X(11) VALUE                        
                                                  'HDB REF    '.                
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-CONTRACT-HDR           PIC X(11) VALUE                        
                                                  'CONTRACT   '.                
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-PROJECT-NAME-HDR       PIC X(60) VALUE                        
               'PROJECT NAME                                        '.          
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-REGN-HDR           PIC X(08) VALUE                        
                                                  'REGN NUM'.                   
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-SALES-MODE-HDR     PIC X(04) VALUE                        
                                                  'MODE'.                       
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-HH-ETHNIC-HDR          PIC X(09) VALUE                        
                                                  'HH_Ethnic'.                  
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-HH-INCOME-HDR          PIC x(09) VALUE                        
                                                  'HH_Income'.                  
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-FLAT-TYPE-HDR      PIC X(09) VALUE                        
                                                  'Flat Type'.                  
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-UIN-HDR            PIC X(09) VALUE                        
                                                  'UIN      '.                  
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NAME-APPOCC-HDR        PIC X(66) VALUE                        
                                                  'Name           '.            
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-SEX-HDR            PIC X(06) VALUE                        
                                                  'Gender'.                     
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-DOB-HDR            PIC X(08) VALUE                        
                                                  'DOB     '.                   
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-AGE-HDR            PIC X(03) VALUE                        
                                                  'Age'.                        
           05  FILLER                    PIC X(01) VALUE ';'.                   
           05  WS-NUM-ROLE-HDR           PIC X(09) VALUE 'Role     '.           
           05  FILLER                    PIC X(15) VALUE SPACES.                
                                                                                
       01 BP13FOUT-RECORD.                                                      
           05  WS-HDB-REF                PIC X(11).                             
           05  WS-FILLER01               PIC X(01).                             
           05  WS-CONTRACT.                                                     
               10 WS-ESTATE              PIC X(03).                             
               10 WS-NEIGHBOURHOOD       PIC X(04).                             
               10 WS-CONTRACT-NO         PIC X(04).                             
           05  WS-FILLER02               PIC X(01).                             
           05  WS-PROJECT-NAME           PIC X(60).                             
           05  WS-FILLER03               PIC X(01).                             
           05  WS-NUM-REGN               PIC X(08).                             
           05  WS-FILLER04               PIC X(01).                             
           05  WS-NUM-SALES-MODE         PIC X(03).                             
           05  FILLER                    PIC X(01).                             
           05  WS-FILLER05               PIC X(01).                             
           05  WS-HH-ETHNIC              PIC X(01).                             
           05  FILLER                    PIC X(08).                             
           05  WS-FILLER06               PIC X(01).                             
           05  WS-HH-INCOME              PIC 9(05).                             
           05  FILLER                    PIC X(04).                             
           05  WS-FILLER07               PIC X(01).                             
           05  WS-NUM-FLAT-TYPE          PIC X(04).                             
           05  FILLER                    PIC X(05).                             
           05  WS-FILLER08               PIC X(01).                             
           05  WS-NUM-UIN                PIC X(09).                             
           05  WS-FILLER09               PIC X(01).                             
           05  WS-NAME-APPOCC            PIC X(66).                             
           05  WS-FILLER10               PIC X(01).                             
           05  WS-NUM-SEX                PIC X(01).                             
           05  FILLER                    PIC X(05).                             
           05  WS-FILLER11               PIC X(01).                             
           05  WS-NUM-DOB                PIC X(08).                             
           05  WS-FILLER12               PIC X(01).                             
           05  WS-NUM-AGE                PIC X(02).                             
           05  FILLER                    PIC X(01).                             
           05  WS-FILLER13               PIC X(01).                             
           05  WS-NUM-ROLE               PIC X(09).                             
           05  FILLER                    PIC X(15).                             
                                                                                
      *------------------------------------------------------------*            
       PROCEDURE DIVISION.                                                      
      *------------------------------------------------------------*            
       0000-MAIN.                                                               
      *------------------------------------------------------------*            
           PERFORM 0100-INIT-ROUTINE        THRU 0100-EXIT.                     
           PERFORM 0200-READ-BM06F120       THRU 0200-EXIT.                     
           PERFORM 0300-MAIN-PROCESS        THRU 0300-EXIT                      
                   UNTIL WS-F120-END = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES.                                            
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       0100-INIT-ROUTINE.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT BM06F120                                                  
                      BM06K110                                                  
                      BP13K800                                                  
                      BP13K893                                                  
                      BP13K820                                                  
                      BP13K895                                                  
                      BP13K813                                                  
                      BP13K060                                                  
               OUTPUT BP13FOUT.                                                 
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, BM06K110-STATUS = '                       
                       WS-K110-STATUS                                           
              MOVE     WS-K110-STATUS         TO RETURN-CODE                    
              PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, BP13K800 STATUS = '                       
                       WS-K800-STATUS                                           
              MOVE     WS-K800-STATUS         TO RETURN-CODE                    
              PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K893-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, BP13K893 STATUS = '                       
                       WS-K893-STATUS                                           
              MOVE     WS-K893-STATUS         TO RETURN-CODE                    
              PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, BP13K820 STATUS = '                       
                       WS-K820-STATUS                                           
              MOVE     WS-K820-STATUS         TO RETURN-CODE                    
              PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K895-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, BP13K895 STATUS = '                       
                       WS-K895-STATUS                                           
              MOVE     WS-K895-STATUS         TO RETURN-CODE                    
              PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, BP13K813 STATUS = '                       
                       WS-K813-STATUS                                           
              MOVE     WS-K813-STATUS         TO RETURN-CODE                    
              PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)    TO WS-CURRENT-DATE.               
      *    move '20161231'                    to WS-CURRENT-DATE.               
           WRITE BP13FOUT-REC  FROM BP13FOUT-HEADER.                            
                                                                                
       0100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0200-READ-BM06F120.                                                      
      *-------------------------------------------------------------            
           READ BM06F120                                                        
             AT END                                                             
                MOVE 'Y'             TO WS-F120-END                             
                GO TO 0200-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1         TO WS-F120-CNT.                                        
                                                                                
       0200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       0300-MAIN-PROCESS.                                                       
      *------------------------------------------------------------*            
           MOVE SPACES                        TO BP13FOUT-RECORD.               
           INITIALIZE BP13FOUT-RECORD.                                          
                                                                                
           MOVE 'N'                           TO WS-OK                          
                                                 WS-K820-FND                    
                                                 WS-K893-FND                    
                                                 WS-K895-FND                    
                                                 WS-K800-FND                    
           MOVE SPACES                        TO K110-KEY-FLD.                  
           MOVE F120-SCH-ACC-NO               TO K110-SCH-ACC-NO.               
           PERFORM 0400-READ-BM06K110       THRU 0400-EXIT                      
           IF WS-OK = 'Y'                                                       
              MOVE K110-REGN-NO               TO K800-NUM-REGN                  
              PERFORM 0605-READ-BP13K800    THRU 0605-EXIT                      
              PERFORM 0500-READ-BP13K813    THRU 0500-EXIT                      
              PERFORM 0600-READ-OTHER-FILES THRU 0600-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 0200-READ-BM06F120       THRU 0200-EXIT.                     
                                                                                
       0300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0400-READ-BM06K110.                                                      
      *-------------------------------------------------------------            
           READ BM06K110.                                                       
                                                                                
           IF WS-K110-STATUS = ZEROES                                           
              IF (K110-REGN-NO NOT = SPACES AND LOW-VALUES) AND                 
                 (K110-REGN-NO(1:2) NOT = '##')                                 
                  MOVE 'Y'                    TO WS-OK                          
                  PERFORM 0405-MOVE-K110-DATA THRU 0405-EXIT                    
              ELSE                                                              
                 ADD 1                        TO WS-K110-SKIP-REC               
              END-IF                                                            
           ELSE                                                                 
             IF WS-K110-STATUS = 23                                             
                ADD  1                        TO WS-K110-NTFND-CTR              
             ELSE                                                               
                DISPLAY 'BM06K110 - READ FILE ERROR'                            
                DISPLAY 'KEY: ' K110-KEY-FLD                                    
                DISPLAY 'ERROR : ' WS-K110-STATUS                               
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
             END-IF                                                             
           END-IF.                                                              
       0400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0405-MOVE-K110-DATA.                                                     
      *-------------------------------------------------------------            
           MOVE K110-SCH-ACC-NO               TO WS-HDB-REF.                    
           MOVE K110-ESTATE                   TO WS-ESTATE.                     
           MOVE K110-NEIGHBOURHOOD            TO WS-NEIGHBOURHOOD.              
           MOVE K110-CONTRACT-NO              TO WS-CONTRACT-NO.                
           MOVE K110-REGN-NO                  TO WS-NUM-REGN.                   
       0405-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0500-READ-BP13K813.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                         TO BP13K813-REC.                 
           IF WS-K800-FND = 'Y'                                                 
              IF K800-NUM-ALLO-CAT = 'SBF' OR 'ROF'                             
                 MOVE K800-NUM-NEW-TOWN        TO K813-NUM-ZONE                 
                 MOVE K800-NUM-BTO-FLAT-TYPE   TO K813-NUM-FLAT-TYPE            
                 IF K800-NUM-ALLO-CAT = 'ROF'                                   
                    MOVE 'ROF'                 TO K813-NUM-ZONE                 
                    MOVE 'RF'                  TO K813-NUM-FLAT-TYPE            
                 END-IF                                                         
              ELSE                                                              
                 IF K800-NUM-ALLO-CAT = 'BTO'                                   
                    IF K800-NUM-BTO-ZONE NOT = SPACES AND LOW-VALUES            
                       MOVE K800-NUM-BTO-ZONE  TO K813-NUM-ZONE                 
                    ELSE                                                        
                       MOVE K800-NUM-ZONE      TO K813-NUM-ZONE                 
                    END-IF                                                      
                 END-IF                                                         
                 MOVE  K800-NUM-FLAT-TYPE      TO K813-NUM-FLAT-TYPE            
              END-IF                                                            
           MOVE K800-DTE-BALLOT                TO K813-DTE-BALLOT               
           END-IF.                                                              
                                                                                
           IF WS-K893-FND = 'Y'                                                 
              IF K893-NUM-ALLO-CAT = 'SBF' OR 'ROF'                             
                 MOVE K893-NUM-NEW-TOWN        TO K813-NUM-ZONE                 
                 MOVE K893-NUM-BTO-FLAT-TYPE   TO K813-NUM-FLAT-TYPE            
                 IF K893-NUM-ALLO-CAT = 'ROF'                                   
                    MOVE 'ROF'                 TO K813-NUM-ZONE                 
                    MOVE 'RF'                  TO K813-NUM-FLAT-TYPE            
                 END-IF                                                         
              ELSE                                                              
                 IF K893-NUM-ALLO-CAT = 'BTO'                                   
                    IF K893-NUM-BTO-ZONE NOT = SPACES AND LOW-VALUES            
                       MOVE K893-NUM-BTO-ZONE  TO K813-NUM-ZONE                 
                    ELSE                                                        
                       MOVE K893-NUM-ZONE      TO K813-NUM-ZONE                 
                    END-IF                                                      
                 END-IF                                                         
                 MOVE  K893-NUM-FLAT-TYPE      TO K813-NUM-FLAT-TYPE            
              END-IF                                                            
           MOVE K893-DTE-BALLOT                TO K813-DTE-BALLOT               
           END-IF.                                                              
                                                                                
           READ BP13K813.                                                       
                                                                                
           IF WS-K813-STATUS = ZEROES                                           
              MOVE K813-NME-PROJECT-LTR        TO WS-PROJECT-NAME               
           ELSE                                                                 
             IF WS-K813-STATUS = 23                                             
                MOVE SPACES                    TO WS-PROJECT-NAME               
             ELSE                                                               
                DISPLAY 'BP13K813 - READ FILE ERROR'                            
                DISPLAY 'KEY: ' K813-KEY-FLD                                    
                DISPLAY 'ERROR : ' WS-K813-STATUS                               
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
             END-IF                                                             
           END-IF.                                                              
       0500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0600-READ-OTHER-FILES.                                                   
      *-------------------------------------------------------------            
           MOVE SPACES                        TO BP13K820-REC.                  
           MOVE K110-REGN-NO                  TO K820-NUM-REGN.                 
           MOVE K110-REGN-NO                  TO K895-NUM-REGN-HIST             
                                                 K895-NUM-REGN                  
           PERFORM 0610-READ-BP13K820       THRU 0610-EXIT.                     
           IF WS-K820-FND = 'N'                                                 
              START BP13K895 KEY >= K895-KEY-FLD                                
              MOVE 'N'                        TO WS-K895-FND                    
              PERFORM 0617-READ-BP13K895 THRU 0617-EXIT UNTIL                   
                      WS-K895-EOF = 'Y' OR                                      
                      K895-NUM-REGN NOT = K110-REGN-NO                          
           END-IF.                                                              
                                                                                
       0600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0605-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
           READ BP13K800.                                                       
           IF WS-K800-STATUS = ZEROES                                           
              MOVE K800-NUM-ALLO-CAT          TO WS-NUM-SALES-MODE              
              MOVE K800-NUM-FLAT-TYPE         TO WS-NUM-FLAT-TYPE               
              MOVE 'Y'                        TO WS-K800-FND                    
                                                                                
              IF (K800-NUM-ROOM = '5') AND                                      
                 (F120-CDE-MODL-DESG = '90' OR '91' OR '95')                    
                  MOVE '3Gen'                 TO WS-NUM-FLAT-TYPE               
              END-IF                                                            
           ELSE                                                                 
             IF WS-K800-STATUS = 23                                             
                MOVE 'N'                      TO WS-K800-FND                    
                MOVE K110-REGN-NO             TO K893-NUM-REGN                  
                                                 K893-NUM-REGN-HIST             
                PERFORM 0607-READ-BP13K893 THRU 0607-EXIT                       
             ELSE                                                               
                DISPLAY 'BP13K800 - READ FILE ERROR'                            
                DISPLAY 'KEY: ' K800-NUM-REGN                                   
                DISPLAY 'ERROR : ' WS-K800-STATUS                               
                PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                      
             END-IF                                                             
           END-IF.                                                              
                                                                                
       0605-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0607-READ-BP13K893.                                                      
      *-------------------------------------------------------------            
           START BP13K893 KEY >= K893-KEY-FLD                                   
                                                                                
           PERFORM UNTIL WS-K893-FND = 'Y'                                      
                      OR WS-K893-EOF = 'Y'                                      
                                                                                
             READ BP13K893 NEXT                                                 
             AT END MOVE 'Y' TO WS-K893-EOF                                     
             END-READ                                                           
             IF WS-K893-STATUS = ZEROES                                         
                IF K893-NUM-REGN-HIST = K110-REGN-NO                            
                   MOVE K893-NUM-ALLO-CAT     TO WS-NUM-SALES-MODE              
                   MOVE K893-NUM-FLAT-TYPE    TO WS-NUM-FLAT-TYPE               
                                                                                
                   IF (K893-NUM-ROOM = '5') AND                                 
                      (F120-CDE-MODL-DESG = '90' OR '91' OR '95')               
                       MOVE '3Gen'            TO WS-NUM-FLAT-TYPE               
                   END-IF                                                       
                   MOVE 'Y'                   TO WS-K893-FND                    
                END-IF                                                          
             ELSE                                                               
                IF WS-K893-STATUS = 23                                          
                   MOVE 'N'                   TO WS-K893-FND                    
                   MOVE SPACES                TO BP13K893-MASTER                
                ELSE                                                            
                   DISPLAY 'BP13K893 - READ FILE ERROR'                         
                   DISPLAY 'KEY: ' K893-NUM-REGN                                
                   DISPLAY 'ERROR : ' WS-K893-STATUS                            
                   PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                   
                END-IF                                                          
             END-IF                                                             
           END-PERFORM.                                                         
                                                                                
       0607-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0610-READ-BP13K820.                                                      
      *-------------------------------------------------------------            
                                                                                
           START BP13K820 KEY >= K820-KEY-FLD                                   
           EVALUATE WS-K820-STATUS                                              
               WHEN 00                                                          
                    PERFORM 0615-READNXT-K820 THRU 0615-EXIT                    
                       UNTIL K820-NUM-REGN  NOT = K110-REGN-NO                  
                          OR WS-K820-EOF = 'Y'                                  
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES               TO BP13K820-REC                   
               WHEN OTHER                                                       
                    DISPLAY 'BP13K820 - START READ ERROR'                       
                    DISPLAY 'KEY: ' K820-NUM-REGN ' / ' K820-NUM-NRIC           
                    DISPLAY 'ERROR : ' WS-K820-STATUS                           
                    PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       0610-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0615-READNXT-K820.                                                       
      *-------------------------------------------------------------            
           READ BP13K820 NEXT                                                   
           AT END                                                               
              MOVE 'Y'                        TO WS-K820-EOF                    
           END-READ                                                             
                                                                                
           EVALUATE WS-K820-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    IF K820-NUM-REGN = K110-REGN-NO                             
                       MOVE 'Y'               TO WS-K820-FND                    
                       PERFORM 0620-MOVE-K800-DATA THRU 0620-EXIT               
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE 'N'                  TO WS-K820-FND                    
               WHEN OTHER                                                       
                    DISPLAY 'BP13K820 - READ NEXT ERROR'                        
                    DISPLAY 'KEY: ' K820-NUM-REGN ' / ' K820-NUM-NRIC           
                    DISPLAY 'ERROR : ' WS-K820-STATUS                           
                    PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       0615-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0617-READ-BP13K895.                                                      
      *-------------------------------------------------------------            
           READ BP13K895 NEXT                                                   
           EVALUATE WS-K895-STATUS                                              
               WHEN 00                                                          
                    IF K895-NUM-REGN = K110-REGN-NO                             
                       PERFORM 0620-MOVE-K800-DATA THRU 0620-EXIT               
                       MOVE 'Y'  TO  WS-K895-FND                                
                    END-IF                                                      
               WHEN 10                                                          
                    MOVE 'Y'  TO WS-K895-EOF                                    
               WHEN 23                                                          
                    MOVE SPACES               TO BP13K895-REC                   
               WHEN OTHER                                                       
                    DISPLAY 'BP13K895 - START READ ERROR'                       
                    DISPLAY 'KEY: ' K895-NUM-REGN ' / ' K895-NUM-NRIC           
                    DISPLAY 'ERROR : ' WS-K895-STATUS                           
                    PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       0617-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0620-MOVE-K800-DATA.                                                     
      *-------------------------------------------------------------            
           MOVE SPACES                        TO K060-KEY-FLD.                  
           MOVE 09                            TO K060-SERIAL-NO.                
           IF WS-K800-FND = 'Y'                                                 
              MOVE K800-NUM-CAT               TO K060-CODE                      
              MOVE K800-AMT-HSE-INCOME        TO WS-HH-INCOME                   
           ELSE                                                                 
              MOVE K893-NUM-CAT               TO K060-CODE                      
              MOVE K893-AMT-HSE-INCOME        TO WS-HH-INCOME                   
           END-IF                                                               
           PERFORM 0625-READ-BP13K060       THRU 0625-EXIT.                     
           MOVE K060-DESC1(1:1)               TO WS-HH-ETHNIC.                  
           MOVE SPACES                        TO K060-KEY-FLD.                  
                                                                                
           IF WS-K820-FND = 'Y'                                                 
              MOVE K820-NUM-NRIC              TO WS-NUM-UIN                     
              MOVE K820-NME-OCCP              TO WS-NAME-APPOCC                 
              MOVE K820-DTE-BIRTH             TO WS-NUM-DOB                     
                                                 WS-DATE-OF-BIRTH               
              MOVE K820-NUM-SEX               TO K060-CODE                      
           ELSE                                                                 
              MOVE K895-NUM-NRIC              TO WS-NUM-UIN                     
              MOVE K895-NME-OCCP              TO WS-NAME-APPOCC                 
              MOVE K895-DTE-BIRTH             TO WS-NUM-DOB                     
                                                 WS-DATE-OF-BIRTH               
              MOVE K895-NUM-SEX               TO K060-CODE                      
           END-IF                                                               
                                                                                
           MOVE 43                            TO K060-SERIAL-NO.                
           PERFORM 0625-READ-BP13K060       THRU 0625-EXIT.                     
           MOVE K060-DESC1(1:1)               TO WS-NUM-SEX.                    
                                                                                
           COMPUTE WS-TMP-AGE = WS-CURR-CCYY - WS-DOB-CCYY.                     
           IF WS-DOB-MM = WS-CURR-MM                                            
              IF WS-DOB-DD > WS-CURR-DD                                         
                 COMPUTE WS-TMP-AGE = WS-TMP-AGE - 1                            
              END-IF                                                            
           ELSE                                                                 
             IF WS-DOB-MM > WS-CURR-MM                                          
                COMPUTE WS-TMP-AGE = WS-TMP-AGE - 1                             
             END-IF                                                             
           END-IF.                                                              
                                                                                
           MOVE WS-TMP-AGE                    TO WS-NUM-AGE.                    
                                                                                
           IF WS-K820-FND = 'Y'                                                 
              IF K820-NUM-NRIC = (K800-NUM-NRIC1 OR K893-NUM-NRIC1) OR          
                 K820-NUM-NRIC = (K800-NUM-NRIC2 OR K800-NUM-NRIC2) OR          
                 K820-NUM-NRIC = (K800-NUM-NRIC3 OR K800-NUM-NRIC3) OR          
                 K820-NUM-NRIC = (K800-NUM-NRIC4 OR K800-NUM-NRIC4)             
                 MOVE 'APPLICANT'             TO WS-NUM-ROLE                    
              ELSE                                                              
                 MOVE 'OCCUPIER'              TO WS-NUM-ROLE                    
              END-IF                                                            
           ELSE                                                                 
              IF K895-NUM-NRIC = (K800-NUM-NRIC1 OR K893-NUM-NRIC1) OR          
                 K895-NUM-NRIC = (K800-NUM-NRIC2 OR K800-NUM-NRIC2) OR          
                 K895-NUM-NRIC = (K800-NUM-NRIC3 OR K800-NUM-NRIC3) OR          
                 K895-NUM-NRIC = (K800-NUM-NRIC4 OR K800-NUM-NRIC4)             
                 MOVE 'APPLICANT'             TO WS-NUM-ROLE                    
              ELSE                                                              
                 MOVE 'OCCUPIER'              TO WS-NUM-ROLE                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE ';'               TO WS-FILLER01 WS-FILLER02                    
                                     WS-FILLER03 WS-FILLER04                    
                                     WS-FILLER05 WS-FILLER06                    
                                     WS-FILLER07 WS-FILLER08                    
                                     WS-FILLER09 WS-FILLER10                    
                                     WS-FILLER11 WS-FILLER12                    
                                     WS-FILLER13.                               
                                                                                
           MOVE 'XXXX'        TO WS-NUM-UIN(2:4)                                
                                                                                
           WRITE BP13FOUT-REC  FROM BP13FOUT-RECORD.                            
           ADD 1   TO WS-FOUT-CNT.                                              
       0620-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0625-READ-BP13K060.                                                      
      *-------------------------------------------------------------            
           READ BP13K060.                                                       
                                                                                
           IF WS-K060-STATUS = ZEROES                                           
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-K060-STATUS = 23                                            
                 MOVE SPACES                  TO K060-DESC1                     
                 DISPLAY 'RECORD NOT FOUND IN BP13K060, KEY: '                  
                         K060-KEY-FLD ' ' K820-KEY-FLD                          
              ELSE                                                              
                 DISPLAY 'BP13K060 - READ FILE ERROR'                           
                 DISPLAY 'KEY: ' K820-NUM-REGN ' / ' K820-NUM-NRIC              
                 DISPLAY 'ERROR : ' WS-K060-STATUS                              
                 PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       0625-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           DISPLAY '-------------BP13CRG2 CONTROL TOTAL -----------'.           
           DISPLAY 'NO OF RECORDS READ  (BM06F120) : ' WS-F120-CNT.             
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF SKIP RECORDS  (BM06K110) : ' WS-K110-SKIP-REC         
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS NOTFND(BM06K110) : ' WS-K110-NTFND-CTR        
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS WRITTEN (BP13FOUT) : ' WS-FOUT-CNT.           
           DISPLAY ' '.                                                         
           CLOSE BM06F120                                                       
                 BM06K110                                                       
                 BP13K800                                                       
                 BP13K893                                                       
                 BP13K820                                                       
                 BP13K895                                                       
                 BP13K813                                                       
                 BP13K060                                                       
                 BP13FOUT.                                                      
           STOP RUN.                                                            
       9000-EXIT.                                                               
                                                                                
