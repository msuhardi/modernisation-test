       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CY20.                                                 
      *AUTHOR.        IRVIN CHING.                                              
      *DATE-WRITTEN.  06/06/2012.                                               
      ******************************************************************        
      *                SYSTEM OF COMMITMENT (BP13)                     *        
      ******************************************************************        
      * OBJECTIVE : MATCH HARDLUCK SECOND TIMER FILE WITH              *        
      *           :       HARDLUCK FT2T         FILE                   *        
      *           : THE UNMATCH IS PURE SECOND TIMER                   *        
      *                                                                *        
      * INPUT  FILES : BP13F595                                        *        
      *                BP13K595                                        *        
      * OUTPUT FILES : BP13F730                                        *        
      *                                                                *        
      * CHG-NO   BY    ON    - DESCRIPTION                             *        
      * -------------------------------------------------------------- *        
      * BP135237 IMC1 05/05/12 NEW PROGRAM                             *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT   BP13F595      ASSIGN TO BP13F595.                           
                                                                                
           SELECT   BP13K595      ASSIGN TO BP13K595.                           
                                                                                
           SELECT   BP13F730      ASSIGN TO BP13F730.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F595                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13F595.                                                           
                                                                                
       FD  BP13K595                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13K595.                                                           
                                                                                
       FD  BP13F730                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13F730.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F595-EOF                  PIC X(1)  VALUE 'N'.                
           05  WS-K595-EOF                  PIC X(1)  VALUE 'N'.                
           05  CNT-F595-READ                PIC 9(7)  VALUE ZEROS.              
           05  CNT-K595-READ                PIC 9(7)  VALUE ZEROS.              
           05  CNT-F730-WRITE               PIC 9(7)  VALUE ZEROS.              
           05  CNT-MATCH                    PIC 9(7)  VALUE ZEROS.              
           05  CNT-K595-NOTFND              PIC 9(7)  VALUE ZEROS.              
                                                                                
                                                                                
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
                                                                                
      *===============================================================*         
       0000-MAIN.                                                               
      *===============================================================*         
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F595       THRU 2000-EXIT.                     
           PERFORM 2100-READ-BP13K595       THRU 2100-EXIT.                     
           PERFORM 3000-PROCESS-RECORD      THRU 3000-EXIT                      
             UNTIL WS-F595-EOF = 'Y' AND                                        
                   WS-K595-EOF = 'Y'.                                           
                                                                                
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       1000-OPEN-FILES.                                                         
      *===============================================================*         
                                                                                
           OPEN INPUT BP13F595                                                  
                      BP13K595                                                  
               OUTPUT BP13F730.                                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       2000-READ-BP13F595.                                                      
      *===============================================================*         
                                                                                
           READ BP13F595                                                        
             AT END                                                             
                MOVE 'Y'                TO WS-F595-EOF                          
                MOVE HIGH-VALUES        TO F595-NUM-REGN                        
                GO                      TO 2000-EXIT                            
           END-READ.                                                            
                                                                                
           ADD  1                       TO CNT-F595-READ.                       
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       2100-READ-BP13K595.                                                      
      *===============================================================*         
                                                                                
           READ BP13K595                                                        
             AT END                                                             
                MOVE 'Y'                TO WS-K595-EOF                          
                MOVE HIGH-VALUES        TO K595-NUM-REGN                        
                GO                      TO 2100-EXIT                            
           END-READ.                                                            
                                                                                
           ADD  1                       TO CNT-K595-READ.                       
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       2200-WRITE-BP13F730.                                                     
      *===============================================================*         
                                                                                
           WRITE BP13F730-REC FROM BP13F595-REC.                                
                                                                                
           ADD  1                       TO CNT-F730-WRITE.                      
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       3000-PROCESS-RECORD.                                                     
      *===============================================================*         
                                                                                
           IF   F595-NUM-REGN = K595-NUM-REGN                                   
              ADD 1                 TO CNT-MATCH                                
              PERFORM 2000-READ-BP13F595   THRU 2000-EXIT                       
              PERFORM 2100-READ-BP13K595   THRU 2100-EXIT                       
           ELSE                                                                 
              IF F595-NUM-REGN < K595-NUM-REGN                                  
                 PERFORM 2200-WRITE-BP13F730  THRU 2200-EXIT                    
                 PERFORM 2000-READ-BP13F595   THRU 2000-EXIT                    
              ELSE                                                              
                 IF F595-NUM-REGN > K595-NUM-REGN                               
                    ADD 1                 TO CNT-K595-NOTFND                    
                 PERFORM 2100-READ-BP13K595   THRU 2100-EXIT                    
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
      *===============================================================*         
       9000-CLOSE-FILES.                                                        
      *===============================================================*         
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '***************************************'.                   
           DISPLAY 'PROGRAM ID     :  BP13CY20'.                                
           DISPLAY '***************************************'.                   
           DISPLAY 'NO. OF BP13F595 READ          : ' CNT-F595-READ.            
           DISPLAY 'NO. OF BP13K595 READ          : ' CNT-K595-READ.            
           DISPLAY 'NO. OF BP13K595-NRIC FOUND    : ' CNT-MATCH.                
           DISPLAY 'NO. OF BP13K595-NRIC NOT FOUND: ' CNT-K595-NOTFND.          
           DISPLAY 'NO. OF BP13F730-NRIC WRITE    : ' CNT-F730-WRITE.           
                                                                                
           CLOSE BP13F595                                                       
                 BP13K595                                                       
                 BP13F730.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
