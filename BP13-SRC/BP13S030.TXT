/******************************************************************/            
/* SYSTEM OF COMMITMENT (SOC)                                     */            
/*================================================================*/            
/*                                                                */            
/* OBJECTIVES :                                                   */            
/*  OUTPUT FLATS REJECTED AFTER SOCA & UNDER CONSTRUCTION         */            
/*                                                                */            
/*----------------------------------------------------------------*/            
/* CHG REF  DATE     BY    DESCRIPTION                            */            
/* -------- -------- ----  -----------                            */            
/* BP132874 20060612 SSS2  NEW PROGRAM                            */            
/* BP133630 20090723 LMS1  ADDITIONAL FIELDS                      */            
/* BP135561 20150106 KAM4  ADD POW TAG                            */            
/* BP135695 20150402 ESA1  TO ADD POF OPTION                      */            
/* BP138004 20191125 SMP3  REMOVE UNIT WHEN DATE ACCEPT NOT ZERO  */            
/******************************************************************/            
                                                                                
  OPTIONS NODATE CAPS;                                                          
                                                                                
  DATA  BP13F310;                                                               
     INFILE BP13F310 END=EOF;                                                   
     INPUT @1    NUM_REGN        $CHAR8.                                        
           @9    TRANS           $CHAR2.                                        
           @61   CDE_NT          $CHAR3.                                        
           @17   HDBREF          $CHAR11.                                       
           @66   BLK             $CHAR5.                                        
           @71   NME_STR         $CHAR20.                                       
           @91   LEVEL           $CHAR2.                                        
           @94   MAIN_UNIT       $CHAR5.                                        
           @113  FT              $CHAR2.                                        
           @30   DTE_REJ         $CHAR8.                                        
           ;                                                                    
                                                                                
      LENGTH K110KEY $11.;                                                      
      K110KEY=HDBREF;                                                           
                                                                                
      INFILE BM06K110 VSAM KEY=K110KEY;                                         
      INPUT @;                                                                  
      RC=_IORC_; FBC=_FDBK_;                                                    
      IF RC=0 THEN                                                              
       DO;                                                                      
         INPUT @437 POD    $CHAR1.                                              
               @444 POF    $CHAR1.                                              
               @485 POW    $CHAR1.                                              
               @486 POF_O  $CHAR1.                                              
               @251 HND    $CHAR8.                                              
               @299 DTEACC $CHAR8.;                                             
         IF DTEACC = '00000000';                                                
       END;                                                                     
      ELSE                                                                      
         DO;                                                                    
            _ERROR_=0;                                                          
            FILE LOG;                                                           
            PUT @1 'HDBREF NOTFND IN K110 ' K110KEY;                            
         END;                                                                   
                                                                                
   PROC SORT DATA=BP13F310; BY HDBREF;                                          
                                                                                
   DATA FLAT1;                                                                  
      SET BP13F310;BY HDBREF;                                                   
      HSENO='#'||LEVEL||'-'||MAIN_UNIT;                                         
                                                                                
   PROC SORT DATA=FLAT1; BY CDE_NT BLK HDBREF ;                                 
   DATA _NULL_;                                                                 
   SET FLAT1  END=EOF;                                                          
   FILE BP13FOUT NOTITLES;                                                      
      CNT+1;                                                                    
      IF CNT=1 THEN                                                             
         LINK HEADER1;                                                          
                                                                                
      PUT @1     NUM_REGN                                                       
          @10    CDE_NT                                                         
          @14    BLK                                                            
          @20    HSENO                                                          
          @30    NME_STR                                                        
          @51    FT                                                             
          @54    POD                                                            
          @58    POF                                                            
          @62    POF_O                                                          
          @68    POW                                                            
          @72    TRANS                                                          
          @78    HDBREF                                                         
          @90    DTE_REJ                                                        
          @99    TRANS                                                          
          @105   HND                                                            
          ;                                                                     
      RETURN;                                                                   
                                                                                
   HEADER1:                                                                     
      PUT @1    'REGN'                                                          
          @10   'NT'                                                            
          @14   'BLK'                                                           
          @20   'UNIT'                                                          
          @30   'STREET'                                                        
          @51   'FT'                                                            
          @54   'POD'                                                           
          @58   'POF'                                                           
          @62   'POF_O'                                                         
          @68   'POW'                                                           
          @72   'TRANS'                                                         
          @78   'HDB_REF'                                                       
          @90   'REJ DTE'                                                       
          @99   'ALTAG'                                                         
          @105  'HANDOVER'                                                      
          ;                                                                     
      RETURN;                                                                   
                                                                                
