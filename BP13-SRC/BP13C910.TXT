       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C910.                                                 
      *AUTHOR.        LMS.                                                      
      *DATE-WRITTEN.  16/08/01.                                                 
      ***************************************************************           
      *                                                             *           
      *     THIS PROGRAM IS TO READ BP13K813 - BTO SITE INFO FILE   *           
      *     TO GET SITE INFO.                                       *           
      *                                                             *           
      *     COMM-LENGTH = 500 {BP13K813}                            *           
      *                                                             *           
      *-------------------------------------------------------------*           
      *     INPUT PARAMETERS AND RETURN VALUES:                     *           
      *     ENTRY NAME   PARAMETER PASSED       RETURN VALUES       *           
      *-------------------------------------------------------------*           
      *     'GBTOSITE'   WS-LINK-ZONE           WS-BP13K813-REC     *           
      *                                         WS-ZONE-FND-FLAG    *           
      *     'OP13K816'   NONE                   WS-RETURN-CODE      *           
      *     'CP13K816'   NONE                   WS-RETURN-CODE      *           
      *                                                             *           
      ***************************************************************           
      *  AMENDMENTS                                                 *           
      ***************************************************************           
      *  CHG REQ  BY     DATE   DESCRIPTION                         *           
      *  --------------------------------------------------------   *           
      *  BP131059 JAM  16/08/01 NEW SUB-ROUTINE.                    *           
      *  BP133610 ESA1 27/04/09 TO REPLACE BP13K767 W/ BP13K816     *           
      *  BP134681 ESA1 29/09/11 TO REPLACE BP13K816 W/ BP13K813     *           
      *  BP135190 SMR2 04/12/13 EXPAND BP13K813 TO 1000             *           
      ***************************************************************           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13K813 ASSIGN TO BP13K813                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K813-KEY-FLD                               
                  FILE STATUS     IS BP13K813-STATUS.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13K813                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13K813.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                    0017
           05  BP13K813-STATUS               PIC 99.                            
           05  WS-EOF-K813                   PIC X(01).                         
                                                                                
      *-------------------------------------------------------------*           
       LINKAGE SECTION.                                                         
      *-------------------------------------------------------------*           
       01  WS-LINK-REC.                                                         
           05  WS-LINK-ZONE           PIC X(03).                                
                                                                                
       01  WS-BP13K813-REC            PIC X(1000).                              
                                                                                
       01  WS-ZONE-FND-FLAG           PIC X(01).                                
                                                                                
       01  WS-RETURN-CODE             PIC 9(02).                                
                                                                                
                                                                                
      *-------------------------------------------------------------*           
       PROCEDURE DIVISION USING WS-LINK-REC, WS-BP13K813-REC,                   
                                WS-ZONE-FND-FLAG.                               
      *-------------------------------------------------------------*           
       0000-MAIN-ROUTINE.                                                       
                                                                                
           MOVE 'N'             TO WS-EOF-K813.                                 
           MOVE SPACES          TO K813-KEY-FLD.                                
           MOVE WS-LINK-ZONE    TO K813-NUM-ZONE.                               
                                                                                
           START BP13K813 KEY >= K813-NUM-ZONE.                                 
                                                                                
           IF BP13K813-STATUS = 00 OR 02                                        
              PERFORM 0500-READNEXT-BP13K813 THRU 0500-EXIT                     
              UNTIL WS-EOF-K813 = 'Y' OR WS-ZONE-FND-FLAG = 'Y'                 
           ELSE                                                                 
              MOVE 'N' TO WS-ZONE-FND-FLAG                                      
              MOVE SPACES    TO WS-BP13K813-REC                                 
           END-IF.                                                              
                                                                                
           PERFORM 9999-GOBACK THRU 9999-EXIT.                                  
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       0500-READNEXT-BP13K813.                                                  
      *****************************************************************         
                                                                                
           READ BP13K813 NEXT RECORD                                            
                AT END MOVE 'Y' TO WS-EOF-K813.                                 
                                                                                
           IF BP13K813-STATUS = 00 OR 02                                        
              IF K813-NUM-ZONE   = WS-LINK-ZONE                                 
                 MOVE 'Y'          TO WS-ZONE-FND-FLAG                          
                 MOVE BP13K813-REC TO WS-BP13K813-REC                           
              ELSE                                                              
                 MOVE 'N'          TO WS-ZONE-FND-FLAG                          
                 MOVE BP13K813-REC TO WS-BP13K813-REC                           
                 MOVE 'Y'          TO WS-EOF-K813                               
              END-IF                                                            
           ELSE                                                                 
              MOVE 'N'             TO WS-ZONE-FND-FLAG                          
              MOVE BP13K813-REC    TO WS-BP13K813-REC                           
              MOVE 'Y'             TO WS-EOF-K813                               
           END-IF.                                                              
                                                                                
       0500-EXIT.                                                               
           EXIT.                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                 00820029
      *****************************************************************         
                                                                        00810029
      *-- PERFORM OPENING BP13K813 AT CALLING FILE -------------*               
                                                                                
           ENTRY 'OP13K813' USING WS-RETURN-CODE.                       00840029
                                                                        00810029
           OPEN INPUT BP13K813.                                                 
           MOVE BP13K813-STATUS TO WS-RETURN-CODE.                              
                                                                                
           PERFORM 9999-GOBACK THRU 9999-EXIT.                                  
                                                                        00810029
       1000-EXIT.                                                       00820029
           EXIT.                                                                
      *****************************************************************         
       2000-CLOSE-FILES.                                                00820029
      *****************************************************************         
                                                                        00810029
      *-- PERFORM CLOSING BP13K813 AT CALLING FILE -------------*               
           ENTRY 'CP13K813' USING WS-RETURN-CODE.                       00840029
                                                                        00810029
           CLOSE BP13K813.                                                      
           MOVE BP13K813-STATUS TO WS-RETURN-CODE.                              
                                                                        00810029
           PERFORM 9999-GOBACK THRU 9999-EXIT.                                  
                                                                                
       2000-EXIT.                                                       00820029
           EXIT.                                                                
      ***************************************************************           
      *  RETURN TO CALLING PROGRAM                                  *           
      ***************************************************************           
                                                                                
       9999-GOBACK.                                                             
                                                                                
           GOBACK.                                                              
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
      *  END BP13C910                                               *           
      ***************************************************************           
