       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C22G.                                                 
      *AUTHOR.        SAUL SIO.                                                 
      *DATE-WRITTEN.  25/02/2006.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE  : DELETE KEY BORROWED RECORDS                   *            
      *                                                            *            
      * INPUT      : BM06F110                                      *            
      * I-O        : BP13K435                                      *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTIONS                       *            
      * -------- -------- ----- ------------                       *            
      * BP132828 20060225 SSS2  NEW PROGRAM                        *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
            SELECT BM06F110 ASSIGN TO BM06F110.                                 
                                                                                
            SELECT BP13K435 ASSIGN TO BP13K435                                  
                   ACCESS MODE     IS DYNAMIC                                   
                   ORGANIZATION    IS INDEXED                                   
                   RECORD KEY      IS K435-KEY-FLD                              
                   FILE STATUS     IS BP13K435-STATUS.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BM06F110                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       COPY BM06F110.                                                           
                                                                                
       FD   BP13K435.                                                           
       COPY BP13K435.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01 BP13K435-STATUS                PIC 9(2)   VALUE ZEROS.                
       01 WS-F110-EOF                    PIC X      VALUE 'N'.                  
       01 WS-K435-EOF                    PIC X      VALUE 'N'.                  
       01 WS-F110-READ                   PIC 9(8)   VALUE ZEROS.                
       01 WS-K435-DELETED                PIC 9(8)   VALUE ZEROS.                
       01 WS-K435-DUPKEY                 PIC 9(8)   VALUE ZEROS.                
                                                                                
       01 WS-PREV-NUM-HDB-REF            PIC X(11)  VALUE SPACES.               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
            PERFORM 1000-OPEN-ROUTINE     THRU 1000-EXIT.                       
            PERFORM 2000-READ-BM06F110    THRU 2000-EXIT.                       
            PERFORM 3000-PROCESS-DETAIL   THRU 3000-EXIT                        
                    UNTIL WS-F110-EOF = 'Y'.                                    
            PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            OPEN  INPUT  BM06F110                                               
                  I-O    BP13K435.                                              
                                                                                
            IF BP13K435-STATUS NOT = 00 AND 97                                  
               DISPLAY 'OPENING ERROR K435-STATUS IS ' BP13K435-STATUS          
               MOVE BP13K435-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                        
            END-IF.                                                             
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BM06F110.                                                      
      *-------------------------------------------------------------            
            READ BM06F110                                                       
                 AT END MOVE 'Y' TO WS-F110-EOF                                 
                 GO TO 2000-EXIT.                                               
                                                                                
            ADD 1   TO WS-F110-READ.                                            
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DETAIL.                                                     
      *-------------------------------------------------------------            
            PERFORM 4000-CHECK-BP13K435    THRU 4000-EXIT.                      
                                                                                
            PERFORM 2000-READ-BM06F110  THRU 2000-EXIT.                         
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       4000-CHECK-BP13K435.                                                     
      *-------------------------------------------------------------            
           MOVE 'N'             TO  WS-K435-EOF.                                
           MOVE SPACES          TO  K435-KEY-FLD.                               
           MOVE F110-KEY-FLD    TO  K435-NUM-HDB-REF                            
                                    WS-PREV-NUM-HDB-REF.                        
                                                                                
           START BP13K435 KEY >= K435-KEY-FLD.                                  
           EVALUATE BP13K435-STATUS                                             
           WHEN 00                                                              
                PERFORM 4500-READNEXT-BP13K435 THRU 4500-EXIT                   
                   UNTIL K435-NUM-HDB-REF NOT = WS-PREV-NUM-HDB-REF             
                      OR WS-K435-EOF = 'Y'                                      
           WHEN 23                                                              
                CONTINUE                                                        
           WHEN OTHER                                                           
                DISPLAY 'ERROR START BP13K435, STATUS  '                        
                        BP13K435-STATUS                                         
                        ',  HDB REF ' K435-NUM-HDB-REF                          
                MOVE BP13K435-STATUS TO RETURN-CODE                             
                PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       4500-READNEXT-BP13K435.                                                  
      *-------------------------------------------------------------            
                                                                                
           READ BP13K435 NEXT RECORD                                            
                AT END MOVE 'Y' TO WS-K435-EOF.                                 
                                                                                
           EVALUATE BP13K435-STATUS                                             
           WHEN 00                                                              
                IF K435-NUM-HDB-REF = F110-KEY-FLD                              
                   PERFORM 5000-DELETE-RECORD   THRU 5000-EXIT                  
                ELSE                                                            
                   MOVE K435-NUM-HDB-REF TO WS-PREV-NUM-HDB-REF                 
                END-IF                                                          
           WHEN 10                                                              
           WHEN 23                                                              
                MOVE 'Y' TO WS-K435-EOF                                         
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K435, STATUS  '                      
                        BP13K435-STATUS                                         
                        ',  HDB REF ' K435-NUM-HDB-REF                          
                MOVE BP13K435-STATUS TO RETURN-CODE                             
                PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       5000-DELETE-RECORD.                                                      
      *-------------------------------------------------------------            
           DELETE BP13K435.                                                     
                                                                                
           EVALUATE BP13K435-STATUS                                             
           WHEN 00                                                              
                DISPLAY ' ' K435-KEY-FLD ' DELETED!'                            
                ADD 1 TO WS-K435-DELETED                                        
           WHEN 23                                                              
                DISPLAY 'K435 NOT FND AT DELETE ' K435-KEY-FLD                  
           WHEN OTHER                                                           
                DISPLAY 'ERROR DELETING BP13K435, STATUS  '                     
                        BP13K435-STATUS                                         
                        ',  KEY ' K435-KEY-FLD                                  
                MOVE BP13K435-STATUS TO RETURN-CODE                             
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
            DISPLAY ' '.                                                        
            DISPLAY '- BP13C22G CONTROL LIST ----------'                        
            DISPLAY '---------------------------------------------'.            
            DISPLAY ' 1) TOT BM06F110 READ     : ' WS-F110-READ.                
            DISPLAY ' 2) TOT BP13K435 DELETED  : ' WS-K435-DELETED.             
            DISPLAY ' 3) TOT BP13K435 DUP KEY  : ' WS-K435-DUPKEY.              
            DISPLAY ' '.                                                        
                                                                                
            CLOSE  BM06F110                                                     
                   BP13K435.                                                    
                                                                                
            IF BP13K435-STATUS NOT = 00 AND 97                                  
               DISPLAY 'CLOSING ERROR, BP13K435   ' BP13K435-STATUS             
               MOVE BP13K435-STATUS TO RETURN-CODE                              
            END-IF.                                                             
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
