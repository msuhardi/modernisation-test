       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C181.                                                 
       AUTHOR.        LSB.                                                      
       DATE-WRITTEN.  18/08/98.                                                 
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    SIMILAR PGM  : BP13U730                             *                
      *                                                        *                
      *    OBJECTIVE   :                                       *                
      *                  FOR LESSEE FOUND IN TRANSFER HISTORY  *                
      *                  AS TRANSFEREE UPDATE AS UPGRADER      *                
      *                  IF DTE-APPLN IS BEFORE DTE-TRANSFER.  *                
      *                                                        *                
      *                  IF FOUND IN DEBARMENT AS CREDIT       *                
      *                  ASSESSMENT SHOULD CANCEL APPLICATION. *                
      *                                                        *                
      *    INPUT FILE  : BP13F500                              *                
      *    INPUT FILE  : BP13F558                              *                
      *                                                        *                
      *   OUTPUT FILE  : BP13F520                              *                
      *   REPORT       : BP13L181                              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY   DATE     DETAILS                          *                
      * ------  ---  ----     -------                          *                
      *BP130542 LSB  180898   NEW PGM                          *                
      *BP130623 LSB  040599 - IF HH=G, OR DTE-REG NOT< DTE-TRF *                
      *                       ALSO PRINT REPORT                *                
      *BP130652 NOR  19021999 EXPAND F500 & F520 RECL FROM     *                
      *                       300 TO 500 CHARACTERS            *                
      *BP130897 AKT  31072000 EXPAND F558 RECL FROM 400 TO 600 *                
      *BP132626 SK   24112004 CHANGE DTE-REGN TO DTE-REQUEST   *                
      * ====================================================== *                
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F558  ASSIGN       TO BP13F558.                           
           SELECT BP13F500  ASSIGN       TO BP13F500.                           
           SELECT BP13F520  ASSIGN       TO BP13F520.                           
           SELECT BP13L181  ASSIGN       TO BP13L181.                           
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BP13F558                                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
                                                                                
       COPY BP13F558.                                                           
                                                                                
       FD   BP13F500                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
                                                                                
       COPY BP13F500.                                                           
                                                                                
       FD   BP13F520                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
                                                                                
       COPY BP13F520.                                                           
                                                                                
       FD   BP13L181                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 132 CHARACTERS.                                     
                                                                                
       01   BP13L181-REC              PIC X(132).                               
                                                                                
                                                                                
      ***************************                                               
       WORKING-STORAGE SECTION.                                                 
      ***************************                                               
                                                                                
       01  WS-SYS-DATE.                                                         
           05 WS-SYS-YY             PIC X(04) .                                 
           05 WS-SYS-MM             PIC X(02) .                                 
           05 WS-SYS-DD             PIC X(02) .                                 
                                                                                
       01  WS-EDIT-DATE.                                                        
           05 WS-EDIT-DAY           PIC X(02) .                                 
           05 FILLER                PIC X(01) VALUE '/'.                        
           05 WS-EDIT-MONTH         PIC X(02) .                                 
           05 FILLER                PIC X(01) VALUE '/'.                        
           05 WS-EDIT-YEAR          PIC X(04) .                                 
                                                                                
       01  WS-ACCUMULATORS.                                                     
           05 WS-I                       PIC S9(4) COMP VALUE ZEROES.           
           05 WS-F500-READ               PIC 9(8)  VALUE ZEROES.                
           05 WS-F500-MATCH              PIC 9(8)  VALUE ZEROES.                
           05 WS-F500-NOMATCH            PIC 9(8)  VALUE ZEROES.                
           05 WS-F500-WRITE              PIC 9(8)  VALUE ZEROES.                
           05 WS-F558-READ               PIC 9(8)  VALUE ZEROES.                
           05 WS-F558-NOMATCH            PIC 9(8)  VALUE ZEROES.                
                                                                                
       01  WS-SWITCHES.                                                         
           05 EOF-F558-IND          PIC X(3)  VALUE SPACES.                     
              88 EOF-F558                     VALUE 'YES'.                      
           05 EOF-F500-IND          PIC X(3)  VALUE SPACES.                     
              88 EOF-F500                     VALUE 'YES'.                      
                                                                                
                                                                                
                                                                                
       01  L181-COUNTERS.                                                       
           05 WS-L181-PAGE         PIC 9(5)  VALUE ZEROES.                      
           05 WS-L181-LINE         PIC 9(2)  VALUE 60.                          
           05 WS-L181-SER-NO       PIC 9(5)  VALUE ZEROES.                      
                                                                                
       01  L181-HDG1.                                                           
           05 FILLER               PIC X(14) VALUE 'BP13L181'.                  
           05 FILLER               PIC X(8)  VALUE 'HDB3'.                      
           05 FILLER               PIC X(18) VALUE SPACES.                      
           05 FILLER               PIC X(39) VALUE                              
              'S Y S T E M   O F   C O M M I T M E N T'.                        
           05 FILLER               PIC X(18) VALUE SPACES.                      
           05 FILLER               PIC X(7)  VALUE 'DATE: '.                    
           05 L181-DATE            PIC X(10) VALUE SPACES.                      
           05 FILLER               PIC X(3)  VALUE SPACES.                      
           05 FILLER               PIC X(7)  VALUE 'PAGE: '.                    
           05 L181-PAGENO          PIC ZZZZ9.                                   
                                                                                
       01  L181-HDG2.                                                           
           05  FILLER               PIC X(15) VALUE SPACES.                     
           05  FILLER               PIC X(40) VALUE                             
               'FOUND IN TRANSFER SYSTEM AS TRANSFEREE, '.                      
           05  FILLER               PIC X(50) VALUE                             
               ' UPDATE HOUSEHOLD-TYPE AS UPGRADER'.                            
           05  FILLER               PIC X(18) VALUE SPACES.                     
                                                                                
                                                                                
       01  L181-HDG2A.                                                          
           05 FILLER                PIC X(33)  VALUE SPACES.                    
           05 FILLER                PIC X(15)  VALUE SPACES.                    
           05 FILLER                PIC X(01)  VALUE SPACES.                    
           05 FILLER                PIC X(22)  VALUE                            
              '-------- SMS --------'.                                          
           05 FILLER                PIC X(02)  VALUE SPACES.                    
           05 FILLER                PIC X(24)  VALUE                            
              '--------- RSL ---------'.                                        
           05 FILLER                PIC X(03)  VALUE SPACES.                    
           05 FILLER                PIC X(24)  VALUE                            
              '--------- TRF ---------'.                                        
                                                                                
       01  L181-HDG3.                                                           
           05 FILLER                PIC X(06)  VALUE 'SER NO'.                  
           05 FILLER                PIC X(01)  VALUE SPACES.                    
           05 FILLER                PIC X(07)  VALUE 'REG NO'.                  
           05 FILLER                PIC X(02)  VALUE SPACES.                    
           05 FILLER                PIC X(05)  VALUE 'NRIC1'.                   
           05 FILLER                PIC X(05)  VALUE SPACES.                    
           05 FILLER                PIC X(05)  VALUE 'NRIC2'.                   
           05 FILLER                PIC X(04)  VALUE SPACES.                    
           05 FILLER                PIC X(02)  VALUE 'HH'.                      
           05 FILLER                PIC X(01)  VALUE SPACES.                    
           05 FILLER                PIC X(08)  VALUE 'DTE-REGN'.                
           05 FILLER                PIC X(03)  VALUE SPACES.                    
           05 FILLER                PIC X(09)  VALUE 'SCH-ACC'.                 
           05 FILLER                PIC X(01)  VALUE SPACES.                    
           05 FILLER                PIC X(02)  VALUE 'TY'.                      
           05 FILLER                PIC X(02)  VALUE SPACES.                    
           05 FILLER                PIC X(03)  VALUE 'MTH'.                     
           05 FILLER                PIC X(02)  VALUE SPACES.                    
           05 FILLER                PIC X(03)  VALUE 'SUB'.                     
           05 FILLER                PIC X(02)  VALUE SPACES.                    
           05 FILLER                PIC X(11)  VALUE 'SCH-ACC'.                 
           05 FILLER                PIC X(02)  VALUE SPACES.                    
           05 FILLER                PIC X(02)  VALUE 'ST'.                      
           05 FILLER                PIC X(02)  VALUE SPACES.                    
           05 FILLER                PIC X(03)  VALUE 'MTH'.                     
           05 FILLER                PIC X(02)  VALUE SPACES.                    
           05 FILLER                PIC X(03)  VALUE 'SUB'.                     
           05 FILLER                PIC X(02)  VALUE SPACES.                    
           05 FILLER                PIC X(11)  VALUE 'SCH-ACC'.                 
           05 FILLER                PIC X(02)  VALUE SPACES.                    
           05 FILLER                PIC X(02)  VALUE 'ST'.                      
           05 FILLER                PIC X(02)  VALUE SPACES.                    
           05 FILLER                PIC X(10)  VALUE 'DTE-TRF'.                 
                                                                                
       01  L181-DETAIL.                                                         
           05 FILLER                PIC X(01)  VALUE SPACES.                    
           05 L181-SER-NO           PIC ZZZZ9.                                  
           05 FILLER                PIC X(01)  VALUE SPACES.                    
           05 L181-NUM-REGN         PIC X(08)  VALUE SPACES.                    
           05 FILLER                PIC X(01)  VALUE SPACES.                    
           05 L181-NUM-NRIC1        PIC X(09)  VALUE SPACES.                    
           05 FILLER                PIC X(01)  VALUE SPACES.                    
           05 L181-NUM-NRIC2        PIC X(09)  VALUE SPACES.                    
           05 FILLER                PIC X(01)  VALUE SPACES.                    
           05 L181-HH               PIC X(01)  VALUE SPACES.                    
           05 FILLER                PIC X(02)  VALUE SPACES.                    
           05 L181-DTE-REGN         PIC X(08)  VALUE SPACES.                    
           05 FILLER                PIC X(02)  VALUE SPACES.                    
           05 L181-SCH-SMS          PIC X(11)  VALUE SPACES.                    
           05 FILLER                PIC X(01)  VALUE SPACES.                    
           05 L181-SMS-TYPE         PIC X(02)  VALUE SPACES.                    
           05 FILLER                PIC X(01)  VALUE SPACES.                    
           05 L181-MTH-SMS          PIC X(03)  VALUE SPACES.                    
           05 FILLER                PIC X(02)  VALUE SPACES.                    
           05 L181-SUBLET-SMS       PIC X(03)  VALUE SPACES.                    
           05 FILLER                PIC X(02)  VALUE SPACES.                    
           05 L181-SCH-RSL          PIC X(11)  VALUE SPACES.                    
           05 FILLER                PIC X(01)  VALUE SPACES.                    
           05 L181-ST-RSL           PIC X(03)  VALUE SPACES.                    
           05 FILLER                PIC X(02)  VALUE SPACES.                    
           05 L181-MTH-RSL          PIC X(03)  VALUE SPACES.                    
           05 FILLER                PIC X(02)  VALUE SPACES.                    
           05 L181-SUBLET-RSL       PIC X(03)  VALUE SPACES.                    
           05 FILLER                PIC X(02)  VALUE SPACES.                    
           05 L181-SCH-TRF          PIC X(11)  VALUE SPACES.                    
           05 FILLER                PIC X(01)  VALUE SPACES.                    
           05 L181-ST-TRF           PIC X(03)  VALUE SPACES.                    
           05 FILLER                PIC X(02)  VALUE SPACES.                    
           05 L181-DTE-TRF          PIC X(08)  VALUE SPACES.                    
                                                                                
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
      *--------*                                                                
       000-MAIN.                                                                
      *--------*                                                                
                                                                                
           PERFORM 100-OPEN-FILES      THRU 100-EXIT.                           
           PERFORM 200-READ-BP13F500   THRU 200-EXIT.                           
           PERFORM 300-READ-BP13F558   THRU 300-EXIT.                           
           PERFORM 400-PROCESS-INDATA  THRU 400-EXIT                            
             UNTIL F500-NUM-REGN = HIGH-VALUE                                   
               AND F558-NUM-REGN = HIGH-VALUE.                                  
           PERFORM 900-CLOSE-FILES     THRU 900-EXIT.                           
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***********************************************************               
      *  THIS PARA OPENS ALL THE FILES AND INITIALIZES THE      *               
      *  COUNTERS.                                              *               
      ***********************************************************               
                                                                                
      *------------------                                                       
       100-OPEN-FILES.                                                          
      *------------------                                                       
                                                                                
           OPEN INPUT  BP13F558                                                 
                       BP13F500                                                 
                OUTPUT BP13F520                                                 
                       BP13L181.                                                
                                                                                
           ACCEPT WS-SYS-DATE     FROM DATE YYYYMMDD.                           
           MOVE   WS-SYS-DD       TO WS-EDIT-DAY                                
           MOVE   WS-SYS-MM       TO WS-EDIT-MONTH                              
           MOVE   WS-SYS-YY       TO WS-EDIT-YEAR.                              
                                                                                
           MOVE WS-EDIT-DATE      TO L181-DATE.                                 
                                                                                
           MOVE 'NO '     TO EOF-F558-IND.                                      
           MOVE 'NO '     TO EOF-F500-IND.                                      
                                                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***********************************************************               
      *  THIS PARA READS THE INPUT FILE AND GETS THE NAME AND   *               
      *  MAILING ADDRESS AND PRINTS LETTERS AND LISTING         *               
      ***********************************************************               
                                                                                
      *-----------------*                                                       
       200-READ-BP13F500.                                                       
      *-----------------*                                                       
                                                                                
           READ BP13F500  AT END                                                
                MOVE 'YES'       TO EOF-F500-IND                                
                MOVE HIGH-VALUES TO F500-NUM-REGN                               
                GO TO 200-EXIT                                                  
           END-READ                                                             
                                                                                
           ADD  1  TO WS-F500-READ.                                             
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       300-READ-BP13F558.                                                       
      *-----------------*                                                       
                                                                                
           READ BP13F558  AT END                                                
                MOVE 'YES'       TO EOF-F558-IND                                
                MOVE HIGH-VALUES TO F558-NUM-REGN                               
                GO TO 300-EXIT                                                  
           END-READ                                                             
                                                                                
           ADD  1  TO WS-F558-READ.                                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------                                                       
       400-PROCESS-INDATA.                                                      
      *------------------*                                                      
                                                                                
                                                                                
           IF F500-NUM-REGN     =  F558-NUM-REGN                                
                                                                                
              ADD 1 TO  WS-F500-MATCH                                           
              IF (F500-DTE-REQUEST <  F558-DTE-TRANSFER-TR) AND                 
                 (F500-CDE-HOUSEHOLD = 'H')                                     
                  MOVE 'G' TO  F500-CDE-HOUSEHOLD                               
              END-IF                                                            
              PERFORM 495-PRINT-REPORT THRU 495-EXIT                            
                      UNTIL F558-NUM-REGN NOT =  F500-NUM-REGN                  
                      OR  F558-NUM-REGN =  HIGH-VALUES                          
              PERFORM 490-WRITE-BP13F520   THRU 490-EXIT                        
              PERFORM 200-READ-BP13F500    THRU 200-EXIT                        
                                                                                
           ELSE                                                                 
              IF F500-NUM-REGN  <  F558-NUM-REGN                                
                 ADD  1         TO  WS-F500-NOMATCH                             
                 PERFORM 490-WRITE-BP13F520  THRU 490-EXIT                      
                 PERFORM 200-READ-BP13F500   THRU 200-EXIT                      
              ELSE                                                              
                 ADD  1         TO  WS-F558-NOMATCH                             
                 PERFORM 300-READ-BP13F558 THRU 300-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       490-WRITE-BP13F520.                                                      
                                                                                
           WRITE  BP13F520-REC  FROM BP13F500-REC.                              
           ADD  1  TO WS-F500-WRITE.                                            
                                                                                
       490-EXIT.                                                                
           EXIT.                                                                
                                                                                
       495-PRINT-REPORT.                                                        
                                                                                
           PERFORM 500-WRITE-P13L181 THRU 500-EXIT.                             
           PERFORM 300-READ-BP13F558 THRU 300-EXIT.                             
                                                                                
       495-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       500-WRITE-P13L181.                                                       
      *-------------------*                                                     
                                                                                
           IF WS-L181-LINE > 53                                                 
              ADD 1 TO WS-L181-PAGE                                             
              MOVE WS-L181-PAGE TO L181-PAGENO                                  
              MOVE ZEROES        TO WS-L181-LINE                                
              WRITE BP13L181-REC FROM L181-HDG1 AFTER PAGE                      
              WRITE BP13L181-REC FROM L181-HDG2 AFTER 1                         
              WRITE BP13L181-REC FROM L181-HDG2A AFTER 1                        
              WRITE BP13L181-REC FROM L181-HDG3 AFTER 2                         
              MOVE SPACES TO BP13L181-REC                                       
              WRITE BP13L181-REC AFTER 1.                                       
                                                                                
           ADD 1                    TO WS-L181-LINE.                            
           ADD 1                    TO WS-L181-SER-NO.                          
           MOVE WS-L181-SER-NO      TO L181-SER-NO.                             
           MOVE F500-NUM-REGN       TO L181-NUM-REGN.                           
           MOVE F500-NUM-NRIC1      TO L181-NUM-NRIC1.                          
           MOVE F500-NUM-NRIC2      TO L181-NUM-NRIC2.                          
           MOVE F500-CDE-HOUSEHOLD  TO L181-HH.                                 
           MOVE F500-DTE-REQUEST    TO L181-DTE-REGN.                           
           MOVE F558-NUM-HDB-REF-SMS TO L181-SCH-SMS.                           
           MOVE F558-CDE-SALES-TYPE-SMS TO L181-SMS-TYPE.                       
           MOVE F558-NUM-MTH-RESIDE-SMS TO L181-MTH-SMS.                        
           MOVE F558-NUM-SUBLET-SMS TO L181-SUBLET-SMS.                         
           MOVE F558-NUM-HDB-REF-RSL TO L181-SCH-RSL.                           
           MOVE F558-CDE-FILE-STATUS-RSL TO L181-ST-RSL.                        
           MOVE F558-NUM-MTH-RESIDE-RSL TO L181-MTH-RSL.                        
           MOVE F558-NUM-SUBLET-RSL TO L181-SUBLET-RSL.                         
           MOVE F558-NUM-HDB-REF-TR TO L181-SCH-TRF.                            
           MOVE F558-NUM-SALES-TYPE-TR TO  L181-ST-TRF.                         
           MOVE F558-DTE-TRANSFER-TR TO  L181-DTE-TRF                           
           WRITE BP13L181-REC FROM L181-DETAIL.                                 
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *----------------                                                         
       900-CLOSE-FILES.                                                         
      *----------------                                                         
                                                                                
           CLOSE BP13F500                                                       
                 BP13F520                                                       
                 BP13F558                                                       
                 BP13L181.                                                      
                                                                                
                                                                                
           DISPLAY 'PROGRAM-ID : BP13U730                 '.                    
           DISPLAY '                                      '.                    
           DISPLAY 'DATE : ' WS-EDIT-DATE.                                      
           DISPLAY '                                      '.                    
                                                                                
           DISPLAY '                                      '.                    
           DISPLAY ' *BP13C181                                  '.              
           DISPLAY '                                      '.                    
           DISPLAY 'NO. OF RECORDS READ FROM F500  = ' WS-F500-READ.            
           DISPLAY 'NO. OF RECORDS READ FROM F558  = ' WS-F558-READ.            
           DISPLAY '                                      '.                    
           DISPLAY 'NO. OF RECORDS WRITEN TO F520  = ' WS-F500-WRITE.           
           DISPLAY '                                      '.                    
           DISPLAY 'NO. OF RECORDS OF F500 MATCHD  = ' WS-F500-MATCH.           
           DISPLAY '                                      '.                    
           DISPLAY 'NO. OF BP13F500 NOT FD IN F558 = ' WS-F500-NOMATCH.         
           DISPLAY 'NO. OF RECORDS NOT FOUND F500  = ' WS-F558-NOMATCH.         
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
