       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C137.                                                 
       AUTHOR.        LJL.                                                      
      *DATE-WRITTEN.  29/09/89.                                                 
      *DATE-COMPILED.                                                           
      *REMARKS.                                                                 
      * ====================================================== *                
      *               SYSTEM OF COMMITMENT  (BP13)             *                
      * ====================================================== *                
      *    OBJECTIVE   :                                       *                
      *                  THIS PROGRAMME WILL UPDATE DAILY KIV  *                
      *                  TRANSACTION (FILE BP13F140) TO        *                
      *                  BALANCING FILE (BP13K160).            *                
      *     INPUT FILE :                                       *                
      *                  1. BP13F140 (DAILY KIV TRANS)         *                
      *                                                        *                
      *     OUTPUT FILE:                                       *                
      *                  1. BP13K160 (BALANCING FILE)          *                
      *                                                        *                
      *     LISTING:                                           *                
      *                  1. BP13L137 (REPORT)                  *                
      *                                                        *                
      * ====================================================== *                
      * CHG-NO   BY  ON       DESCRIPTION                      *                
      * -------- --- ------   -----------                      *                
      * SOC-PH8  LSB 930723 - COBOL II AND EXPAND REGN         *                
      * BP130020 SSS 951012 - REPOSITION HEAD-01 TO ROW 1      *                
      * BP130103 EG1 960328 - TO CATER FOR SUB-TRANSFER.       *                
      * BP130320 LJL 970828 - TO CATER FOR REGN DEPOSIT.       *                
      * BP132473 MJB 040120 - TO CATER FOR PAYMENT-TYPE NOT EQ *                
      *                       'RDE' 'BK ' 'SOC' 'SUS' & 'CD '  *                
      * ====================================================== *                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K160 ASSIGN TO  BP13K160                                  
                ACCESS MODE IS RANDOM                                           
                ORGANIZATION IS INDEXED                                         
                RECORD KEY IS K160-KEY-FLD                                      
                FILE STATUS IS BP13K160-STATUS.                                 
                                                                                
           SELECT BP13F140 ASSIGN TO  BP13F140.                                 
           SELECT BP13L137 ASSIGN TO BP13L137.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F140                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F140.                                                           
                                                                                
       FD  BP13K160                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 100 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13K160.                                                           
                                                                                
       FD  BP13L137                                                             
           RECORD CONTAINS 133 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  PRINT-REC-2             PIC X(133).                                  
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  BP13K160-STATUS                PIC 99         VALUE ZEROS.           
       01  WS-K160-AMT-OPEN               PIC S9(10)V99  VALUE ZEROS.           
       01  WS-K160-AMT-TRANS              PIC S9(10)V99  VALUE ZEROS.           
       01  WS-K160-AMT-CLOSE              PIC S9(10)V99  VALUE ZEROS.           
       01  WS-F140-AMT-RECEIPT            PIC S9(7)V99   VALUE ZEROS.           
       01  WS-F140-CDE-PAYMENT-TYPE       PIC X(3)       VALUE SPACES.          
       01  WS-BP13K160-WRITE              PIC 9(5)       VALUE ZEROS.           
       01  WS-BP13K160-BAL                PIC 9(5)       VALUE ZEROS.           
       01  WS-BP13K160-REWRITE            PIC 9(5)       VALUE ZEROS.           
       01  WS-BP13F140-READ               PIC 9(5)       VALUE ZEROS.           
       01  WS-K160-FOUND                  PIC X          VALUE 'N'.             
       01  WS-CNT-CASES-ON-PAGEA          PIC 9(3)       VALUE 60.              
       01  WS-CNT-PAGEA                   PIC 9(2)       VALUE ZEROES.          
       01  WS-CNT-PAGE                    PIC 9(2)       VALUE ZEROES.          
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-DTE-YY              PIC 99.                               
           05  WS-CUR-DTE-MM              PIC 99.                               
           05  WS-CUR-DTE-DD              PIC 99.                               
       01  WS-EDITED-DATE.                                                      
           05  WS-CUR-DTE-DD              PIC 99.                               
           05  FILLER PIC X VALUE '/'.                                          
           05  WS-CUR-DTE-MM              PIC 99.                               
           05  FILLER PIC X VALUE '/'.                                          
           05  WS-CUR-DTE-YY              PIC 99.                               
       01  WS-DTE-TRANS.                                                        
           05  WS-DTE-TRANS-YY1           PIC 99.                               
           05  WS-DTE-TRANS-YY2           PIC 99.                               
           05  WS-DTE-TRANS-MM            PIC 99.                               
           05  WS-DTE-TRANS-DD            PIC 99.                               
       01  WS-F140-DATE.                                                        
           05  WS-DTE-TRANS-MM            PIC 99.                               
           05  WS-DTE-TRANS-DD            PIC 99.                               
           05  WS-DTE-TRANS-YY1           PIC 99.                               
           05  WS-DTE-TRANS-YY2           PIC 99.                               
                                                                                
       01  WS-F140-KEY-FLD.                                                     
           05  WS-F140-NUM-ORIG-REGN      PIC X(8)       VALUE SPACES.          
           05  WS-F140-DTE-POST           PIC X(8)       VALUE SPACES.          
           05  WS-F140-COUNT-HISTORY      PIC 9(3)       VALUE ZEROS.           
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F140-EOF                PIC X          VALUE 'N'.             
                                                                                
      ***************************************************************           
      * ERROR LISTING                                               *           
      ***************************************************************           
       01  L14S2-PR-HEAD-01.                                                    
           05  FILLER              PIC X(8)      VALUE 'BP13L137'.              
           05  FILLER              PIC X(5)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
                'S Y S T E M   O F   C O M M I T M E N T'.                      
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  14S2-DATE           PIC X(8).                                    
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  14S2-PAGENO         PIC ZZZZ9 .                                  
                                                                                
       01  L14S2-PR-HEAD-02.                                                    
           05  FILLER              PIC X(40)     VALUE SPACES .                 
           05  FILLER              PIC X(20)     VALUE                          
                                                 '         ERROR LISTI'.        
           05  FILLER              PIC X(20)     VALUE                          
                                                 'NG FOR DAILY TRANSAC'.        
           05  FILLER              PIC X(13)     VALUE                          
                                                 'TION'.                        
           05  FILLER              PIC X(40)     VALUE SPACES.                  
                                                                                
       01  L14S2-PR-HEAD-03.                                                    
           05  FILLER              PIC X(13)                                    
                  VALUE '     REGN    '.                                        
           05  FILLER              PIC X(12)                                    
                  VALUE 'PAYMENT-TY  '.                                         
           05  FILLER              PIC X(25)                                    
                  VALUE 'TRANS-TY  SCH-ACCN-NO    '.                            
           05  FILLER              PIC X(25)                                    
                  VALUE 'NRIC    CDE-APPLN  AMT   '.                            
           05  FILLER              PIC X(16)                                    
                  VALUE '   HISTORY-COUNT'.                                     
           05  FILLER              PIC X(5)      VALUE SPACES.                  
           05  FILLER              PIC X(15)                                    
                  VALUE 'RECEIPT/VOUCHER'.                                      
           05  FILLER              PIC X(22)                                    
                  VALUE '       RECEIPT-DATE   '.                               
                                                                                
       01  L14S2-PR-DETAILS.                                                    
           05  FILLER              PIC X(03)     VALUE SPACES .                 
           05  14S2-REGN           PIC X(08).                                   
           05  FILLER              PIC X(07)     VALUE SPACES .                 
           05  14S2-PAY-TY         PIC X(03).                                   
           05  FILLER              PIC X(08)     VALUE SPACES .                 
           05  14S2-TRANS-TY       PIC X(02).                                   
           05  FILLER              PIC X(06)     VALUE SPACES .                 
           05  14S2-SCH-ACCN-NO    PIC X(11).                                   
           05  FILLER              PIC X(02)     VALUE SPACES .                 
           05  14S2-NRIC           PIC X(09).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  14S2-CDE-APPLN      PIC 9(01).                                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  14S2-AMT            PIC 9(10).                                   
           05  FILLER              PIC X(08)     VALUE SPACES .                 
           05  14S2-HISTORY-CNT    PIC 9(03).                                   
           05  FILLER              PIC X(10)     VALUE SPACES .                 
           05  14S2-RECEIPTVOUCHER PIC X(14).                                   
           05  FILLER              PIC X(10)     VALUE SPACES .                 
           05  14S2-RECEIPT-DATE   PIC X(08).                                   
           05  FILLER              PIC X(05)     VALUE SPACES .                 
                                                                                
           EJECT                                                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      *        MAIN PROCESS                                            *        
      ******************************************************************        
       000-MAIN.                                                                
      *-------------*                                                           
           PERFORM 100-OPEN-FILE          THRU 100-EXIT.                        
           PERFORM 200-READ-DATA-BP13F140 THRU 200-EXIT.                        
           PERFORM 250-PROCESS-BP13F140   THRU 250-EXIT                         
                   UNTIL WS-F140-EOF = 'Y'.                                     
           PERFORM 900-CLOSE-ROUTINE      THRU 900-EXIT.                        
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *    OPENING FILES SECTION                                      *         
      ******************************************************************        
       100-OPEN-FILE.                                                           
      *-----------------*                                                       
           OPEN INPUT  BP13F140                                                 
                I-O    BP13K160                                                 
                OUTPUT BP13L137.                                                
                                                                                
           IF BP13K160-STATUS NOT = 00 AND                                      
              BP13K160-STATUS NOT = 97                                          
              DISPLAY 'OPENING ERROR - BP13K160-STATUS : '                      
              BP13K160-STATUS                                                   
              MOVE BP13K160-STATUS    TO   RETURN-CODE                          
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                          
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *    200-READ-DATA-BP13F140                                      *        
      ******************************************************************        
       200-READ-DATA-BP13F140.                                                  
      *---------------------------*                                             
           READ BP13F140   AT END                                               
                MOVE 'Y' TO WS-F140-EOF                                         
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1 TO WS-BP13F140-READ.                                           
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       250-PROCESS-BP13F140.                                                    
      *------------------------*                                                
           PERFORM 300-MAIN-ROUTINE       THRU 300-EXIT.                        
           PERFORM 200-READ-DATA-BP13F140 THRU 200-EXIT.                        
                                                                                
       250-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *    300-MAIN-ROUTINE.                                           *        
      ******************************************************************        
       300-MAIN-ROUTINE.                                                        
      *-------------------*                                                     
           MOVE SPACES         TO WS-F140-CDE-PAYMENT-TYPE.                     
                                                                                
           IF F140-CDE-PAYMENT-TYPE = 'RDE'                                     
              MOVE 'KR ' TO WS-F140-CDE-PAYMENT-TYPE                            
           ELSE                                                                 
           IF F140-CDE-PAYMENT-TYPE = 'BK '                                     
              MOVE 'KB ' TO WS-F140-CDE-PAYMENT-TYPE                            
           ELSE                                                                 
           IF F140-CDE-PAYMENT-TYPE = 'SOC'                                     
              MOVE 'KS ' TO WS-F140-CDE-PAYMENT-TYPE                            
           ELSE                                                                 
           IF F140-CDE-PAYMENT-TYPE = 'SUS'                                     
              MOVE 'KU ' TO WS-F140-CDE-PAYMENT-TYPE                            
           ELSE                                                                 
           IF F140-CDE-PAYMENT-TYPE = 'CD '                                     
              MOVE 'KC ' TO WS-F140-CDE-PAYMENT-TYPE                            
           ELSE                                                                 
              PERFORM 9100-ERROR-LIST-PRINT THRU 9100-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-F140-CDE-PAYMENT-TYPE NOT = SPACES                             
              PERFORM 400-UPDATE-BP13K160 THRU 400-EXIT                         
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-UPDATE-BP13K160.                                                     
      *------------------------*                                                
           MOVE WS-F140-CDE-PAYMENT-TYPE  TO   K160-KEY-FLD.                    
           PERFORM 410-READ-BP13K160      THRU 410-EXIT.                        
                                                                                
           ADD   F140-AMT-RECEIPT         TO   K160-AMT-TRANS                   
           ADD   F140-AMT-RECEIPT         TO   K160-AMT-CLOSE                   
                                                                                
           PERFORM 500-PROCESS-UPDATE     THRU 500-EXIT.                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       410-READ-BP13K160.                                                       
      *------------------------*                                                
           READ BP13K160.                                                       
                                                                                
           EVALUATE BP13K160-STATUS                                             
             WHEN 00                                                            
                MOVE 'Y'                   TO   WS-K160-FOUND                   
             WHEN 23                                                            
                MOVE SPACES                TO   BP13K160-BAL                    
                MOVE 'N'                   TO   WS-K160-FOUND                   
                MOVE ZEROES                TO   K160-AMT-OPEN                   
                MOVE ZEROES                TO   K160-AMT-TRANS                  
                MOVE ZEROES                TO   K160-AMT-CLOSE                  
             WHEN OTHER                                                         
                MOVE BP13K160-STATUS       TO   RETURN-CODE                     
                DISPLAY 'READING ERROR - BP13K160-STATUS : '                    
                        BP13K160-STATUS                                         
                PERFORM 900-CLOSE-ROUTINE  THRU 900-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *    500-PROCESS-UPDATE                                         *         
      *    IF REC FOUND, UPDATE AMT-TRANS, AMT-CLOSE                  *         
      *    IF REC NOT FOUND, WRITE NEW REC WITH AMT-TRANS, AMT-CLOSE  *         
      *****************************************************************         
       500-PROCESS-UPDATE.                                                      
      *---------------------*                                                   
           IF WS-K160-FOUND     = 'Y'                                           
              PERFORM 600-REWRITE-K160    THRU 600-EXIT                         
           ELSE                                                                 
              MOVE WS-F140-CDE-PAYMENT-TYPE  TO K160-KEY-FLD                    
              PERFORM 510-WRITE-REC       THRU 510-EXIT.                        
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       510-WRITE-REC.                                                           
      *-----------------*                                                       
           WRITE BP13K160-BAL.                                                  
                                                                                
           IF BP13K160-STATUS NOT = ZEROS                                       
              DISPLAY 'PAYMENT-TYPE  '          F140-CDE-PAYMENT-TYPE           
              DISPLAY 'WRITING ERROR-BP13K160 ' BP13K160-STATUS                 
              MOVE BP13K160-STATUS              TO RETURN-CODE                  
              PERFORM 900-CLOSE-ROUTINE         THRU 900-EXIT                   
           ELSE                                                                 
              ADD 1 TO WS-BP13K160-WRITE.                                       
                                                                                
       510-EXIT.                                                                
           EXIT.                                                                
                                                                                
       600-REWRITE-K160.                                                        
      *--------------------*                                                    
           REWRITE BP13K160-BAL.                                                
                                                                                
           IF BP13K160-STATUS NOT = ZEROS                                       
              DISPLAY 'PAYMENT-TYPE  '            F140-CDE-PAYMENT-TYPE         
              DISPLAY 'REWRITING ERROR-BP13K160 ' BP13K160-STATUS               
              MOVE BP13K160-STATUS                TO RETURN-CODE                
              PERFORM 900-CLOSE-ROUTINE           THRU 900-EXIT.                
           ADD 1 TO WS-BP13K160-REWRITE.                                        
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      **********************************************************                
      *     PRINT ERROR LISTING.                               *                
      **********************************************************                
       9100-ERROR-LIST-PRINT.                                                   
      *------------------------*                                                
            IF WS-CNT-CASES-ON-PAGEA > 50                                       
               PERFORM 9200-PRINT-HEADING THRU 9200-EXIT                        
            END-IF.                                                             
                                                                                
            PERFORM 9300-PRINT-DETAIL THRU 9300-EXIT.                           
                                                                                
       9100-EXIT.                                                               
            EXIT.                                                               
                                                                                
       9200-PRINT-HEADING.                                                      
      *---------------------*                                                   
            ADD 1                  TO WS-CNT-PAGEA.                             
            MOVE ZERO              TO WS-CNT-CASES-ON-PAGEA.                    
            MOVE WS-CNT-PAGE       TO 14S2-PAGENO.                              
            ACCEPT  WS-CUR-DATE FROM DATE.                                      
            MOVE CORR WS-CUR-DATE  TO WS-EDITED-DATE.                           
            MOVE WS-EDITED-DATE    TO 14S2-DATE.                                
            MOVE SPACES            TO PRINT-REC-2.                              
            WRITE PRINT-REC-2 FROM L14S2-PR-HEAD-01 AFTER PAGE.                 
            WRITE PRINT-REC-2 FROM L14S2-PR-HEAD-02 AFTER 2.                    
            WRITE PRINT-REC-2 FROM L14S2-PR-HEAD-03 AFTER 2.                    
            INSPECT PRINT-REC-2 REPLACING CHARACTERS BY '-'.                    
            WRITE PRINT-REC-2.                                                  
            MOVE SPACES            TO PRINT-REC-2.                              
            WRITE PRINT-REC-2 AFTER 2.                                          
                                                                                
       9200-EXIT.                                                               
            EXIT.                                                               
                                                                                
       9300-PRINT-DETAIL.                                                       
      *-------------------------*                                               
            ADD 1                       TO WS-CNT-CASES-ON-PAGEA.               
            MOVE F140-NUM-REGN          TO 14S2-REGN.                           
            MOVE F140-CDE-PAYMENT-TYPE  TO 14S2-PAY-TY.                         
            MOVE F140-CDE-TRANS-TYPE    TO 14S2-TRANS-TY.                       
            MOVE F140-NUM-NRIC          TO 14S2-NRIC.                           
            MOVE F140-AMT-RECEIPT       TO 14S2-AMT.                            
            MOVE F140-NUM-RECPT-JRNO    TO 14S2-RECEIPTVOUCHER.                 
            MOVE F140-DTE-TRANS         TO WS-DTE-TRANS.                        
            MOVE CORR WS-DTE-TRANS      TO WS-F140-DATE.                        
            MOVE WS-F140-DATE           TO 14S2-RECEIPT-DATE.                   
            WRITE PRINT-REC-2 FROM L14S2-PR-DETAILS AFTER 1.                    
                                                                                
       9300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
      *     CLOSE ALL FILES AND STOP RUN.                           *           
      ***************************************************************           
       900-CLOSE-ROUTINE.                                                       
      *---------------------*                                                   
            DISPLAY 'CONTROL COUNT FOR BP13C137'.                               
            DISPLAY 'NO OF RECORDS PROCESSED (BP13F140) = '                     
                    WS-BP13F140-READ.                                           
            DISPLAY 'NO OF RECORDS WRITTEN   (BP13K160) = '                     
                    WS-BP13K160-WRITE.                                          
            DISPLAY 'NO OF RECORDS REWRITTEN (BP13K160) = '                     
                    WS-BP13K160-REWRITE.                                        
                                                                                
            CLOSE BP13F140                                                      
                  BP13K160                                                      
                  BP13L137.                                                     
                                                                                
            IF BP13K160-STATUS NOT = '00'                                       
               DISPLAY 'CLOSING ERROR-BP13K160 ' BP13K160-STATUS                
               MOVE BP13K160-STATUS TO RETURN-CODE.                             
                                                                                
            STOP RUN.                                                           
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
