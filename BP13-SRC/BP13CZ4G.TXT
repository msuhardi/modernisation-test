       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13CZ4G.                                                  
       AUTHOR.       AM25.                                                      
       DATE-WRITTEN. 11/03/2021.                                                
      *****************************************************************         
      *                SOC - SYSTEM OF COMMITMENT (BP13)              *         
      *===============================================================*         
      *  OBJECTIVES : TO UPDATE QUEUE FOR PAIRED APPLICATIONS         *         
      *===============================================================*         
      * FILES DESCRIPTION :-                                          *         
      *                                                               *         
      *                ACCESS     REC                                 *         
      * DD NAME  I/O/E MODE       LEN  KEY FIELDS    REMARKS          *         
      * -------- ----- ---------- ---- ------------- ---------------- *         
      * BP13INP1 I     SEQUENTIAL 500                                 *         
      * BP13K740 I/O   RANDOM     500  K740-KEY-FLD                   *         
      * BP13LZ4G O     SEQUENTIAL 132                REPORT           *         
      *===============================================================*         
      * REVISION HISTORY :                                            *         
      *                                                               *         
      * CHG-REQ#  DD/MM/YY   BY   DESCRIPTION                         *         
      * --------  --------  ----  ----------------------------------- *         
      * BP138600  11/03/21  AM25  NEW PROGRAM                         *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13INP1 ASSIGN      TO BP13INP1.                             
                                                                                
           SELECT BP13K740 ASSIGN      TO BP13K740                              
              ORGANIZATION  IS INDEXED                                          
              ACCESS MODE   IS RANDOM                                           
              RECORD KEY    IS K740-KEY-FLD                                     
              FILE STATUS   IS WS-K740-STATUS.                                  
                                                                                
           SELECT BP13LZ4G ASSIGN      TO BP13LZ4G.                             
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13INP1                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  BP13INP1-REC.                                                        
           05  INP1-NUM-REGN           PIC X(8).                                
           05  INP1-NUM-RNDM           PIC X(28).                               
           05  INP1-NUM-RNDM-ORIG      PIC X(28).                               
           05  INP1-NUM-QUEUE          PIC X(5).                                
           05  INP1-NUM-JB-REGN        PIC X(8).                                
           05  INP1-NUM-JB-RNDM-ORIG   PIC X(28).                               
           05  INP1-NUM-JB-QUEUE       PIC X(5).                                
           05  FILLER                  PIC X(90).                               
                                                                                
       FD  BP13K740                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13K740.                                                           
                                                                                
       FD  BP13LZ4G                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01  BP13LZ4G-REC                PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-K740-STATUS          PIC 9(2)        VALUE ZEROES.            
           05  WS-SYS-DTE-TME.                                                  
               10 WS-SYS-DATE          PIC 9(8)        VALUE ZEROES.            
               10 WS-SYS-TIME          PIC 9(8)        VALUE ZEROES.            
           05  WS-UPD-QUEUE            PIC X(5)        VALUE SPACES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-INP1-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-K740-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-K740-UPDT            PIC 9(6)        VALUE ZEROES.            
           05  WS-LZ4G-WRTN            PIC 9(6)        VALUE ZEROES.            
           05  WS-LZ4G-PAGE-CNT        PIC 9(4)        VALUE ZEROES.            
           05  WS-LZ4G-LINE-CNT        PIC 9(2)        VALUE 60.                
                                                                                
       01  WS-SWITCHES-FLAGS.                                                   
           05  WS-INP1-EOF             PIC X           VALUE 'N'.               
                                                                                
      *------------------------------------------------------------*            
      *    REPORT LAYOUT - BP13LZ4G                                *            
      *------------------------------------------------------------*            
       01  LZ4G-HDR1.                                                           
           05  FILLER                  PIC X(8)        VALUE 'BP13LZ4G'.        
           05  FILLER                  PIC X(6)        VALUE SPACES.            
           05  FILLER                  PIC X(4)        VALUE 'HDB3'.            
           05  FILLER                  PIC X(22)       VALUE SPACES.            
           05  FILLER                  PIC X(39)       VALUE                    
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                  PIC X(18)       VALUE SPACES.            
           05  FILLER                  PIC X(6)        VALUE 'DATE: '.          
           05  LZ4G-DATE               PIC X(10)       VALUE SPACES.            
           05  FILLER                  PIC X(7)        VALUE ' PAGE: '.         
           05  LZ4G-PAGE               PIC Z,ZZ9.                               
           05  FILLER                  PIC X(7)        VALUE SPACES.            
                                                                                
       01  LZ4G-HDR2.                                                           
           05  FILLER                  PIC X(35)       VALUE SPACES.            
           05  FILLER                  PIC X(49)       VALUE                    
               'REPORT ON UPDATE OF QUEUE FOR PAIRED APPLICATIONS'.             
           05  FILLER                  PIC X(48)       VALUE SPACES.            
                                                                                
       01  LZ4G-HDR3.                                                           
           05  FILLER                  PIC X(77)       VALUE SPACES.            
           05  FILLER                  PIC X(8)        VALUE 'PAIRED'.          
           05  FILLER                  PIC X(32)       VALUE SPACES.            
           05  FILLER                  PIC X(6)        VALUE 'PAIRED'.          
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  FILLER                  PIC X(5)        VALUE 'UPD'.             
           05  FILLER                  PIC X(3)        VALUE SPACES.            
                                                                                
       01  LZ4G-HDR4.                                                           
           05  FILLER                  PIC X(8)        VALUE 'REGN'.            
           05  FILLER                  PIC X(2)        VALUE SPACES.            
           05  FILLER                  PIC X(28)       VALUE                    
               'RANDOM NO FOR BALLOT'.                                          
           05  FILLER                  PIC X(2)        VALUE SPACES.            
           05  FILLER                  PIC X(28)       VALUE                    
               'ORIGINAL RN'.                                                   
           05  FILLER                  PIC X(2)        VALUE SPACES.            
           05  FILLER                  PIC X(5)        VALUE 'QUEUE'.           
           05  FILLER                  PIC X(2)        VALUE SPACES.            
           05  FILLER                  PIC X(8)        VALUE 'REGN'.            
           05  FILLER                  PIC X(2)        VALUE SPACES.            
           05  FILLER                  PIC X(28)       VALUE                    
               'PAIRED ORIGINAL RN'.                                            
           05  FILLER                  PIC X(2)        VALUE SPACES.            
           05  FILLER                  PIC X(6)        VALUE 'QUEUE'.           
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  FILLER                  PIC X(5)        VALUE 'QUEUE'.           
           05  FILLER                  PIC X(3)        VALUE SPACES.            
                                                                                
       01  LZ4G-DTL1.                                                           
           05  LZ4G-REGN               PIC X(8)        VALUE SPACES.            
           05  FILLER                  PIC X(2)        VALUE SPACES.            
           05  LZ4G-RNDM               PIC X(28)       VALUE SPACES.            
           05  FILLER                  PIC X(2)        VALUE SPACES.            
           05  LZ4G-RNDM-ORIG          PIC X(28)       VALUE SPACES.            
           05  FILLER                  PIC X(2)        VALUE SPACES.            
           05  LZ4G-QUEUE              PIC X(5)        VALUE SPACES.            
           05  FILLER                  PIC X(2)        VALUE SPACES.            
           05  LZ4G-JB-REGN            PIC X(8)        VALUE SPACES.            
           05  FILLER                  PIC X(2)        VALUE SPACES.            
           05  LZ4G-JB-RNDM-ORIG       PIC X(28)       VALUE SPACES.            
           05  FILLER                  PIC X(2)        VALUE SPACES.            
           05  LZ4G-JB-QUEUE           PIC X(5)        VALUE SPACES.            
           05  FILLER                  PIC X(2)        VALUE SPACES.            
           05  LZ4G-UPD-QUEUE          PIC X(5)        VALUE SPACES.            
           05  FILLER                  PIC X(3)        VALUE SPACES.            
                                                                                
       01  LZ4G-BLANK.                                                          
           05  FILLER                  PIC X(132)      VALUE SPACES.            
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 2000-READ-BP13INP1    THRU 2000-EXIT.                        
           PERFORM 3000-MAIN-PROCESSING  THRU 3000-EXIT                         
              UNTIL WS-INP1-EOF = 'Y'.                                          
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN INPUT  BP13INP1                                                 
                I-O    BP13K740                                                 
                OUTPUT BP13LZ4G.                                                
                                                                                
           IF (WS-K740-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K740 FAIL STATUS=' WS-K740-STATUS               
              MOVE WS-K740-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-SYS-DTE-TME.                       
                                                                                
           STRING WS-SYS-DATE(7:2) '/'                                          
                  WS-SYS-DATE(5:2) '/'                                          
                  WS-SYS-DATE(1:4)                                              
              DELIMITED BY SIZE INTO LZ4G-DATE.                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-READ-BP13INP1.                                                      
      *****************************************************************         
                                                                                
           READ BP13INP1                                                        
              AT END                                                            
                 MOVE 'Y'              TO WS-INP1-EOF                           
              NOT AT END                                                        
                 ADD 1                 TO WS-INP1-READ                          
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-MAIN-PROCESSING.                                                    
      *****************************************************************         
                                                                                
           MOVE SPACES                 TO WS-UPD-QUEUE.                         
                                                                                
           IF INP1-NUM-QUEUE = SPACES OR LOW-VALUES                             
              CONTINUE                                                          
           ELSE                                                                 
              PERFORM 3100-PROCESS-RECORDS THRU 3100-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 8000-WRITE-BP13LZ4G THRU 8000-EXIT.                          
                                                                                
           PERFORM 2000-READ-BP13INP1 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3100-PROCESS-RECORDS.                                                    
      *****************************************************************         
                                                                                
           IF INP1-NUM-RNDM-ORIG > INP1-NUM-JB-RNDM-ORIG                        
              IF INP1-NUM-QUEUE < INP1-NUM-JB-QUEUE                             
                 PERFORM 4000-READ-BP13K740 THRU 4000-EXIT                      
              END-IF                                                            
           ELSE                                                                 
              IF INP1-NUM-RNDM-ORIG < INP1-NUM-JB-RNDM-ORIG                     
                 IF INP1-NUM-QUEUE > INP1-NUM-JB-QUEUE                          
                    PERFORM 4000-READ-BP13K740 THRU 4000-EXIT                   
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4000-READ-BP13K740.                                                      
      *****************************************************************         
                                                                                
           MOVE INP1-NUM-RNDM          TO K740-NUM-RANDOM.                      
                                                                                
           READ BP13K740.                                                       
                                                                                
           EVALUATE WS-K740-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K740-READ                          
                 PERFORM 4100-UPDATE-BP13K740 THRU 4100-EXIT                    
              WHEN 23                                                           
                 DISPLAY 'REC NOT FOUND IN BP13K740: ' INP1-NUM-RNDM            
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K740 FAIL STATUS=' WS-K740-STATUS            
                 MOVE WS-K740-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4100-UPDATE-BP13K740.                                                    
      *****************************************************************         
                                                                                
           MOVE INP1-NUM-JB-QUEUE      TO K740-NUM-NT-FT-QUEUE                  
                                          WS-UPD-QUEUE.                         
                                                                                
           REWRITE BP13K740-REC.                                                
                                                                                
           IF WS-K740-STATUS = ZEROES                                           
              ADD 1                    TO WS-K740-UPDT                          
           ELSE                                                                 
              DISPLAY 'REWRITE BP13K740 FAIL STATUS=' WS-K740-STATUS            
              MOVE WS-K740-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       8000-WRITE-BP13LZ4G.                                                     
      *****************************************************************         
                                                                                
           IF WS-LZ4G-LINE-CNT > 59                                             
              ADD 1                    TO WS-LZ4G-PAGE-CNT                      
              MOVE WS-LZ4G-PAGE-CNT    TO LZ4G-PAGE                             
                                                                                
              WRITE BP13LZ4G-REC FROM LZ4G-HDR1 AFTER PAGE                      
              WRITE BP13LZ4G-REC FROM LZ4G-HDR2                                 
              WRITE BP13LZ4G-REC FROM LZ4G-BLANK                                
              WRITE BP13LZ4G-REC FROM LZ4G-HDR3                                 
              WRITE BP13LZ4G-REC FROM LZ4G-HDR4                                 
              WRITE BP13LZ4G-REC FROM LZ4G-BLANK                                
                                                                                
              MOVE 6                   TO WS-LZ4G-LINE-CNT                      
           END-IF.                                                              
                                                                                
           MOVE INP1-NUM-REGN          TO LZ4G-REGN.                            
           MOVE INP1-NUM-RNDM          TO LZ4G-RNDM.                            
           MOVE INP1-NUM-RNDM-ORIG     TO LZ4G-RNDM-ORIG.                       
           MOVE INP1-NUM-QUEUE         TO LZ4G-QUEUE.                           
           MOVE INP1-NUM-JB-REGN       TO LZ4G-JB-REGN.                         
           MOVE INP1-NUM-JB-RNDM-ORIG  TO LZ4G-JB-RNDM-ORIG.                    
           MOVE INP1-NUM-JB-QUEUE      TO LZ4G-JB-QUEUE.                        
           MOVE WS-UPD-QUEUE           TO LZ4G-UPD-QUEUE.                       
                                                                                
           WRITE BP13LZ4G-REC FROM LZ4G-DTL1.                                   
           ADD 1                       TO WS-LZ4G-LINE-CNT                      
                                          WS-LZ4G-WRTN.                         
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           CLOSE BP13INP1                                                       
                 BP13K740                                                       
                 BP13LZ4G.                                                      
                                                                                
           IF WS-K740-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K740 FAIL STATUS=' WS-K740-STATUS              
              MOVE WS-K740-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           DISPLAY '******************************'.                            
           DISPLAY '* PROGRAM BP13CZ4G           *'.                            
           DISPLAY '* CONTROL COUNTS             *'.                            
           DISPLAY '*----------------------------*'.                            
           DISPLAY '* REC READ INP1     : ' WS-INP1-READ ' *'.                  
           DISPLAY '* REC READ K740     : ' WS-K740-READ ' *'.                  
           DISPLAY '* REC UPDT K740     : ' WS-K740-UPDT ' *'.                  
           DISPLAY '* REC WRTN LZ4G     : ' WS-LZ4G-WRTN ' *'.                  
           DISPLAY '******************************'.                            
                                                                                
       9000-EXIT.                                                               
           STOP RUN.                                                            
           EXIT.                                                                
                                                                                
      *======================  END OF PROGRAM  ======================*          
