       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD47.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  22/07/08.                                                 
      *****************************************************************         
      *OBJECTIVE - PRINT DBSS TOP LETTER TOGETHER PARTICULAR FORM     *         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *                  1. BP13F800                                  *         
      *                  2. BP13K820                                  *         
      *                  3. BP13K830                                  *         
      *                                                               *         
      *     OUTPUT FILE: 1. P13PD47D  (BP13ID) - COVER LETTER         *         
      *                  2. P13PD47E  (BP13IE,BP13IF,BP13IG,BP13IH)   *         
      *                                                               *         
      *---------------------------------------------------------------*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------- ---- -----------------------              *         
      * BP133387 22/07/2008 JB8  NEW PROGRAM.                         *         
      * BP134124 05/04/2011 ZDD1 REALLIGNMENT FOR FORM REVISION       *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
      *-------------------------------------------------------------            
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800  ASSIGN        TO BP13F800.                          
           SELECT SY02F001  ASSIGN        TO SY02F001.                          
                                                                                
           SELECT BP13K820  ASSIGN        TO BP13K820                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K820-KEY-FLD                       
                            FILE STATUS   IS WS-K820-STATUS.                    
                                                                                
           SELECT BP13K830  ASSIGN        TO BP13K830                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K830-KEY-FLD                       
                            ALTERNATE KEY IS K830-NUM-REGN                      
                            FILE STATUS   IS WS-K830-STATUS.                    
                                                                                
           SELECT BP13K857  ASSIGN        TO BP13K857                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K857-KEY-FLD                       
                            FILE STATUS   IS WS-K857-STATUS.                    
                                                                                
           SELECT BP13KD00  ASSIGN        TO BP13KD00                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KD00-KEY-FLD                       
                            FILE STATUS   IS WS-KD00-STATUS.                    
                                                                                
           SELECT P13PD47D  ASSIGN        TO P13PD47D.                          
           SELECT P13PD47E  ASSIGN        TO P13PD47E.                          
           SELECT P13PD47F  ASSIGN        TO P13PD47F.                          
           SELECT P13PD47G  ASSIGN        TO P13PD47G.                          
           SELECT P13PD47H  ASSIGN        TO P13PD47H.                          
           SELECT BP13LD47  ASSIGN        TO BP13LD47.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       COPY SY02F001.                                                           
                                                                                
       FD   BP13F800                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K830.                                                           
                                                                                
       FD   BP13K857                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K857.                                                           
                                                                                
       FD   BP13KD00                                                            
            RECORD CONTAINS 1600 CHARACTERS.                                    
       COPY BP13KD00.                                                           
                                                                                
       FD  BP13LD47                                                             
           RECORDING MODE  IS  F.                                               
       01  LD47-PRTREC                      PIC X(132).                         
                                                                                
       FD  P13PD47D                                                             
           RECORDING MODE  IS  F.                                               
       01  PD47D-PRTREC                     PIC X(110).                         
                                                                                
       FD  P13PD47E                                                             
           RECORDING MODE  IS  F.                                               
       01  PD47E-PRTREC                     PIC X(95).                          
                                                                                
       FD  P13PD47F                                                             
           RECORDING MODE  IS  F.                                               
       01  PD47F-PRTREC                     PIC X(90).                          
                                                                                
       FD  P13PD47G                                                             
           RECORDING MODE  IS  F.                                               
       01  PD47G-PRTREC                     PIC X(90).                          
                                                                                
       FD  P13PD47H                                                             
           RECORDING MODE  IS  F.                                               
       01  PD47H-PRTREC                     PIC X(90).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-STATUS.                                                           
           05  WS-K820-STATUS               PIC 99    VALUE ZEROS.              
           05  WS-K830-STATUS               PIC 99    VALUE ZEROS.              
           05  WS-K857-STATUS               PIC 99    VALUE ZEROS.              
           05  WS-KD00-STATUS               PIC 99    VALUE ZEROS.              
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F800-READ-CTR             PIC 9(06)  VALUE ZEROS.             
           05  WS-PD47-WRT-CTR              PIC 9(06)  VALUE ZEROS.             
           05  WS-CNT-PAGE-LD47             PIC 9(04)  VALUE ZEROS.             
           05  WS-CNT-LINE-LD47             PIC 9(06)  VALUE ZEROS.             
           05  WS-CNT-CUR-LINE              PIC 9(02)  VALUE 56.                
           05  WS-CTR                       PIC 9(03)  VALUE ZEROS.             
           05  WS-SKIP                      PIC 9(06)  VALUE ZEROS.             
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F800-EOF                  PIC X     VALUE 'N'.                
           05  WS-K800-FND                  PIC X     VALUE 'N'.                
           05  WS-K820-NME-OCCP             PIC X(66) VALUE SPACES.             
           05  WS-K820-NME-HA1              PIC X(66) VALUE SPACES.             
           05  WS-F001-EOF                  PIC X(1)  VALUE 'N'.                
               88 EOF-F001                            VALUE 'Y'.                
           05  WS-RETCODE                   PIC S9(8)                           
                                            SIGN IS LEADING SEPARATE.           
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-DATE.                                                         
               10  WS-CCYY                  PIC 9(4)  VALUE 0000.               
               10  WS-MM                    PIC 99    VALUE 00.                 
               10  WS-DD                    PIC 99    VALUE 00.                 
           05  WS-CURR-DATE                 PIC 9(8)  VALUE ZEROES.             
           05  WS-NUM-DATE                  PIC X(10) VALUE SPACES.             
                                                                                
       01  WS-ADDRESS.                                                          
           05 FILLER                     PIC X(3)  VALUE 'BLK'.                 
           05 FILLER                     PIC X(1)  VALUE SPACES.                
           05 CD47-BLK-NO                PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X     VALUE SPACES.                
           05 CD47-FLAT-NO.                                                     
              10 FILLER                  PIC X     VALUE '#'.                   
              10 CD47-LVL-NO             PIC X(2)  VALUE SPACES.                
              10 FILLER                  PIC X     VALUE '-'.                   
              10 CD47-UNIT-NO            PIC X(7)  VALUE SPACES.                
           05 FILLER                     PIC X(1)  VALUE SPACES.                
           05 CD47-STREET                PIC X(47) VALUE SPACES.                
           05 FILLER                     PIC X(1)  VALUE SPACES.                
           05 CD47-SINGAPORE             PIC X(09) VALUE 'SINGAPORE'.           
           05 FILLER                     PIC X(1)  VALUE SPACES.                
           05 CD47-POSTAL-NO             PIC X(06) VALUE SPACES.                
           05 FILLER                     PIC X(1)  VALUE SPACES.                
           05 FILLER                     PIC X(19) VALUE                        
              '("the Development")'.                                            
           05 FILLER                     PIC X(1)  VALUE SPACES.                
           05 CD47-SINGAPORE1            PIC X(09) VALUE SPACES.                
                                                                                
       01  WS-ADDRESS-IE.                                                       
           05  WS-ADDR-IE                   PIC X(91) VALUE SPACES.             
                                                                                
       01  WS-PARSING.                                                          
           05 WS-STRING-TO-PARSE            PIC X(130).                         
           05 WS-PARSED-STRING              PIC X(130).                         
           05 WS-STRING-LENGTH              PIC 9(03).                          
           05 WS-CUR-POS-SP                 PIC 9(03).                          
           05 WS-CUR-POS-PS                 PIC 9(03).                          
           05 WS-SPACE-FLAG                 PIC X    VALUE 'N'.                 
           05 WS-INSERT-SPACE-FLAG          PIC X    VALUE 'N'.                 
                                                                                
      *-------------------------------------------------------------            
      * BP13C913 PARAMETERS                                                     
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH-ACC.                                              
              10 WS-LINK-NUM-SCH       PIC X(4).                                
              10 WS-LINK-NUM-ACC       PIC X(5).                                
       COPY P13COMM8.                                                           
      *===============================                                          
      *   LINKAGE SECTION FOR AV02C015                                          
      *===============================                                          
       01  WS-LINK-AV02C015.                                                    
           05  AV02C015-IN-POSTCODE             PIC X(6).                       
           05  AV02C015-OUT-POSTCODE-CHKSUM     PIC X(9).                       
                                                                                
      *------------------------------------------------------------*            
      *        LAY-OUT FOR LETTER BP13ID                                        
      *------------------------------------------------------------*            
       01  PD47D-LINE01.                                                        
           05  FILLER                   PIC X(72) VALUE SPACES.                 
           05  PD47D-DATE               PIC X(10) VALUE SPACES.                 
                                                                                
       01  PD47D-LINE02.                                                        
           05  FILLER                   PIC X(72) VALUE SPACES.                 
           05  PD47D-NUM-REGN           PIC X(08) VALUE SPACES.                 
                                                                                
       01  PD47D-LINE03.                                                        
           05  FILLER                   PIC X(11) VALUE SPACES.                 
           05  PD47D-NAME               PIC X(66) VALUE SPACES.                 
                                                                                
       01  PD47D-LINE04.                                                        
           05  FILLER                   PIC X(11) VALUE SPACES.                 
           05  PD47D-NAME2              PIC X(66) VALUE SPACES.                 
                                                                                
       01  PD47D-LINE05.                                                        
           05  FILLER                   PIC X(11) VALUE SPACES.                 
           05  PD47D-BLK                PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  PD47D-UNIT               PIC X(11) VALUE SPACES.                 
                                                                                
       01  PD47D-LINE06.                                                        
           05  FILLER                   PIC X(11) VALUE SPACES.                 
           05  PD47D-ADDR-L2            PIC X(47) VALUE SPACES.                 
                                                                                
       01  PD47D-LINE07.                                                        
           05  FILLER                   PIC X(11) VALUE SPACES.                 
           05  PD47D-ADDR-L3            PIC X(20) VALUE SPACES.                 
                                                                                
       01  PD47D-LINE08.                                                        
           05  FILLER                   PIC X(10) VALUE SPACES.                 
           05  PD47D-BAR-CODE           PIC X(09) VALUE SPACES.                 
                                                                                
       01  PD47D-LINE09.                                                        
           05  FILLER                   PIC X(11) VALUE SPACES.                 
           05  CD47-CORR-ADDR           PIC X(110) VALUE SPACES.                
                                                                                
      *------------------------------------------------------------*            
      *        LAY-OUT FOR LETTER BP13IE                                        
      *------------------------------------------------------------*            
       01  PD47E-LINE0A.                                                        
           05  FILLER                   PIC X(31) VALUE SPACES.                 
           05  PD47E-ADDRESS            PIC X(80) VALUE SPACES.                 
                                                                                
       01  PD47E-LINE01.                                                        
           05  FILLER                   PIC X(31) VALUE SPACES.                 
           05  PD47E-NUM-REGN           PIC X(08) VALUE SPACES.                 
                                                                                
       01  PD47E-LINE02.                                                        
           05  FILLER                   PIC X(31) VALUE SPACES.                 
           05  PD47E-DTE-APPLN          PIC X(10) VALUE SPACES.                 
                                                                                
       01  PD47E-LINE02A.                                                       
           05  FILLER                   PIC X(31) VALUE SPACES.                 
           05  PD47E-EMAIL              PIC X(50) VALUE SPACES.                 
                                                                                
       01  PD47E-LINE02B.                                                       
           05  FILLER                   PIC X(31) VALUE SPACES.                 
           05  PD47E-PHONE              PIC X(50) VALUE SPACES.                 
                                                                                
       01  PD47E-LINE03.                                                        
           05  FILLER                   PIC X(14) VALUE SPACES.                 
           05  PD47E-HA1                PIC X(76) VALUE SPACES.                 
                                                                                
       01  PD47E-LINE04.                                                        
           05  FILLER                   PIC X(14) VALUE SPACES.                 
           05  PD47E-HA2                PIC X(76) VALUE SPACES.                 
                                                                                
       01  PD47E-LINE05.                                                        
           05  FILLER                   PIC X(14) VALUE SPACES.                 
           05  PD47E-HA3                PIC X(76) VALUE SPACES.                 
                                                                                
       01  PD47E-LINE06.                                                        
           05  FILLER                   PIC X(14) VALUE SPACES.                 
           05  PD47E-HA4                PIC X(76) VALUE SPACES.                 
                                                                                
      *------------------------------------------------------------*            
      *        LAY-OUT FOR LETTER BP13IH                                        
      *------------------------------------------------------------*            
       01  PD47H-LINE01.                                                        
           05  FILLER                   PIC X(53) VALUE SPACES.                 
           05  PD47H-ALLOC-SCH          PIC X(01) VALUE SPACES.                 
                                                                                
       01  PD47H-LINE02.                                                        
           05  FILLER                   PIC X(53) VALUE SPACES.                 
           05  PD47H-AHG                PIC X(01) VALUE SPACES.                 
      *------------------------------------------------------------*            
      *        LAY-OUT FOR CONTROL LISTING                         *            
      *------------------------------------------------------------*            
       01  LD47-HDG1.                                                           
           05  LD47-LISTING             PIC X(14) VALUE 'BP13LD47'.             
           05  FILLER                   PIC X(8)  VALUE 'HDB3'.                 
           05  FILLER                   PIC X(18) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(18) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE: '.               
           05  LD47-DATE                PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(8)  VALUE ' PAGE: '.              
           05  LD47-PAGE                PIC ZZZZ9.                              
                                                                                
       01  LD47-HDG2.                                                           
           05  FILLER                   PIC X(41) VALUE SPACES.                 
           05  FILLER                   PIC X(37) VALUE                         
               'CONTROL LIST FOR NOTIFICATION OF TOP '.                         
                                                                                
       01  LD47-HDG3.                                                           
           05  FILLER                   PIC X(4)  VALUE SPACES.                 
           05  FILLER                   PIC X(5)  VALUE '  S/N'.                
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  FILLER                   PIC X(8)  VALUE 'REGN    '.             
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  FILLER                   PIC X(09) VALUE 'NRIC1'.                
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  FILLER                   PIC X(30) VALUE 'NAME1'.                
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  FILLER                   PIC X(18) VALUE 'ADDRESS'.              
                                                                                
       01  LD47-DTL.                                                            
           05  FILLER                   PIC X(4)  VALUE SPACES.                 
           05  LD47-SN                  PIC ZZZZ9 VALUE SPACES.                 
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  LD47-REGN                PIC X(8)  VALUE SPACES.                 
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  LD47-NRIC1               PIC X(09) VALUE SPACES.                 
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  LD47-NAME1               PIC X(30) VALUE SPACES.                 
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  LD47-PIDB-ADDR           PIC X(26) VALUE SPACES.                 
                                                                                
       01  DJDE-LINE-IE                   PIC X(80)  VALUE                      
           '$DJDE$ PMODE=PORTRAIT,FORMS=BP13IE,BOF=80,END;'.                    
                                                                                
       01  DJDE-LINE-IF                   PIC X(80)  VALUE                      
           '$DJDE$ PMODE=PORTRAIT,FORMS=BP13IF,BOF=80,END;'.                    
                                                                                
       01  DJDE-LINE-IG                   PIC X(80)  VALUE                      
           '$DJDE$ PMODE=PORTRAIT,FORMS=BP13IG,BOF=80,END;'.                    
                                                                                
       01  DJDE-LINE-IH                   PIC X(80)  VALUE                      
           '$DJDE$ PMODE=PORTRAIT,FORMS=BP13IH,BOF=80,END;'.                    
                                                                                
      *=============================================================            
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-FILES             THRU 1000-EXIT.                  
           PERFORM 3000-PROCESS-RECORDS        THRU 3000-EXIT                   
                   UNTIL WS-F800-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE          THRU 9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-FILES.                                                         
      *=============================================================            
           OPEN INPUT BP13F800                                                  
                      SY02F001                                                  
                      BP13K820                                                  
                      BP13K830                                                  
                      BP13K857                                                  
                      BP13KD00                                                  
               OUTPUT P13PD47D                                                  
                      P13PD47E                                                  
                      P13PD47F                                                  
                      P13PD47G                                                  
                      P13PD47H                                                  
                      BP13LD47.                                                 
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K820 STATUS ' WS-K820-STATUS               
              MOVE     WS-K820-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE.                                       
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K830 STATUS ' WS-K830-STATUS               
              MOVE     WS-K830-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE.                                       
                                                                                
           IF WS-K857-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K857 STATUS ' WS-K857-STATUS               
              MOVE     WS-K857-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE.                                       
                                                                                
           IF WS-KD00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR KD00 STATUS ' WS-KD00-STATUS               
              MOVE     WS-KD00-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE.                                       
                                                                                
           READ SY02F001 AT END MOVE 'Y'  TO WS-F001-EOF.                       
           IF EOF-F001                                                          
              DISPLAY 'RECORD NOT FOUND IN SY01F001 '                           
              DISPLAY 'PROCESSING TERMINATED '                                  
              GO TO 9000-CLOSE-ROUTINE                                          
           ELSE                                                                 
              MOVE F001-DTE-CURRENT      TO   WS-DATE WS-CURR-DATE              
           END-IF.                                                              
           STRING WS-DD '/'                                                     
                  WS-MM '/'                                                     
                  WS-CCYY DELIMITED BY SIZE                                     
                  INTO WS-NUM-DATE.                                             
      *    MOVE WS-NUM-DATE              TO PD47D-DATE                          
      *                                     LD47-DATE.                          
                                                                                
           MOVE '21/04/2011'             TO PD47D-DATE                          
                                            LD47-DATE.                          
                                                                                
                                                                                
                                                                                
      *    MOVE SPACES           TO   PD47F-PRTREC.                             
      *    WRITE PD47F-PRTREC AFTER PAGE.                                       
      *    MOVE SPACES           TO   PD47G-PRTREC.                             
      *    WRITE PD47G-PRTREC AFTER PAGE.                                       
      *    MOVE SPACES           TO   PD47H-PRTREC.                             
      *    WRITE PD47H-PRTREC AFTER PAGE.                                       
                                                                                
           PERFORM 2000-READ-BP13F800  THRU 2000-EXIT.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       2000-READ-BP13F800.                                                      
      *=============================================================            
                                                                                
            READ BP13F800 AT END                                                
                 MOVE 'Y'  TO  WS-F800-EOF                                      
                 GO TO 2000-EXIT.                                               
                                                                                
            ADD  1  TO WS-F800-READ-CTR.                                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-PROCESS-RECORDS.                                                    
      *=============================================================            
                                                                                
           INITIALIZE     PD47D-LINE02 PD47D-LINE03                             
                          PD47D-LINE04 PD47D-LINE05 PD47D-LINE06                
                          PD47D-LINE07 PD47D-LINE08 PD47D-LINE09                
                          PD47E-LINE0A PD47E-LINE02A                            
                          PD47E-LINE01 PD47E-LINE02 PD47E-LINE03                
                          PD47E-LINE04 PD47E-LINE05 PD47E-LINE06                
                          PD47H-LINE01 PD47H-LINE02.                            
                                                                                
           MOVE F800-NUM-REGN               TO PD47D-NUM-REGN                   
                                               PD47E-NUM-REGN                   
           MOVE F800-NUM-SCH-ACC(1:9)       TO WS-LINK-NUM-SCH-ACC              
           STRING F800-DTE-REGN(7:2) '/'                                        
                  F800-DTE-REGN(5:2) '/'                                        
                  F800-DTE-REGN(1:4)                                            
              DELIMITED BY SIZE INTO PD47E-DTE-APPLN.                           
                                                                                
           MOVE F800-NUM-REGN               TO K820-NUM-REGN                    
           MOVE F800-NUM-NRIC1              TO K820-NUM-NRIC                    
           PERFORM 4600-READ-K820         THRU 4600-EXIT                        
           MOVE WS-K820-NME-OCCP            TO PD47D-NAME                       
                                               WS-K820-NME-HA1.                 
           STRING F800-NUM-NRIC1 ' ' WS-K820-NME-OCCP                           
              DELIMITED BY SIZE INTO PD47E-HA1.                                 
           MOVE K820-NUM-PHONE    TO PD47E-PHONE.                               
                                                                                
           IF (F800-NUM-NRIC2  = SPACES OR LOW-VALUES) or                       
              F800-NUM-NRIC2(1:1) = '#'                                         
              MOVE SPACES                   TO PD47D-NAME2                      
           ELSE                                                                 
              MOVE F800-NUM-NRIC2           TO K820-NUM-NRIC                    
              PERFORM 4600-READ-K820      THRU 4600-EXIT                        
              MOVE WS-K820-NME-OCCP         TO PD47D-NAME2                      
              STRING F800-NUM-NRIC2 ' ' WS-K820-NME-OCCP                        
                 DELIMITED BY SIZE INTO PD47E-HA2                               
           END-IF.                                                              
                                                                                
           IF F800-NUM-NRIC3  = SPACES OR LOW-VALUES                            
              CONTINUE                                                          
           ELSE                                                                 
              MOVE F800-NUM-NRIC3           TO K820-NUM-NRIC                    
              PERFORM 4600-READ-K820      THRU 4600-EXIT                        
              STRING F800-NUM-NRIC3 ' ' WS-K820-NME-OCCP                        
                 DELIMITED BY SIZE INTO PD47E-HA3                               
           END-IF.                                                              
                                                                                
           IF F800-NUM-NRIC4  = SPACES OR LOW-VALUES                            
              CONTINUE                                                          
           ELSE                                                                 
              MOVE F800-NUM-NRIC4           TO K820-NUM-NRIC                    
              PERFORM 4600-READ-K820      THRU 4600-EXIT                        
              STRING F800-NUM-NRIC4 ' ' WS-K820-NME-OCCP                        
                 DELIMITED BY SIZE INTO PD47E-HA4                               
           END-IF.                                                              
                                                                                
           IF F800-NUM-ELIG-SCHEME = 'FS '                                      
              MOVE 'Y'    TO PD47H-ALLOC-SCH                                    
           ELSE                                                                 
              MOVE 'N'    TO PD47H-ALLOC-SCH                                    
           END-IF.                                                              
                                                                                
           IF F800-NUM-AHG NOT  = SPACES AND LOW-VALUES                         
              MOVE 'Y'    TO PD47H-AHG                                          
           ELSE                                                                 
              MOVE 'N'    TO PD47H-AHG                                          
              PERFORM 4950-READ-KD00      THRU 4950-EXIT                        
              IF KD00-DTE-HSGNT-APPROVE NOT = SPACES AND LOW-VALUES             
                 MOVE 'Y' TO PD47H-AHG                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 4800-GET-ADDR          THRU 4800-EXIT.                       
           PERFORM 4900-READ-K857         THRU 4900-EXIT.                       
           PERFORM 6000-READ-PIDB         THRU 6000-EXIT.                       
                                                                                
           PERFORM 7000-PRT-LETTER        THRU 7000-EXIT                        
           PERFORM 8000-PRN-CNTRL-LIST    THRU 8000-EXIT.                       
                                                                                
           PERFORM 2000-READ-BP13F800     THRU 2000-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       4600-READ-K820.                                                          
      *=============================================================            
           MOVE    SPACES                TO  WS-K820-NME-OCCP                   
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = 00 OR 02                                         
              MOVE    K820-NME-OCCP      TO  WS-K820-NME-OCCP                   
           ELSE                                                                 
           IF WS-K820-STATUS = 23                                               
              DISPLAY 'K820 REC NOT FOUND : '   K820-KEY-FLD                    
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K820 = '   WS-K820-STATUS              
                      ', K820-KEY-FLD = '   K820-KEY-FLD                        
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                      
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       4800-GET-ADDR.                                                           
      *=============================================================            
                                                                                
           MOVE F800-NUM-REGN TO K830-NUM-REGN                                  
           READ BP13K830  KEY IS K830-NUM-REGN.                                 
                                                                                
           IF WS-K830-STATUS = 00                                               
              IF (K830-NUM-MAIL-ADDR =  SPACES OR LOW-VALUES) OR                
                 ((K830-NUM-MAIL-LVL-CHAR =  '#') AND                           
                 (K830-NUM-MAIL-LVL-NUM =  SPACES OR LOW-VALUES))               
                 MOVE K830-NUM-BLK         TO PD47D-BLK                         
                 IF K830-NUM-LVL-NUM = SPACES AND                               
                    K830-NUM-HSEMAIN = SPACES                                   
                    MOVE SPACES            TO PD47D-UNIT                        
                 ELSE                                                           
                    MOVE K830-NUM-FLAT     TO PD47D-UNIT                        
                 END-IF                                                         
                 MOVE K830-NME-STREET-BLDG(1:47) TO PD47D-ADDR-L2               
                 IF K830-NUM-POSTAL  = SPACES OR LOW-VALUES OR ZEROES           
                    MOVE SPACES            TO PD47D-ADDR-L3                     
                    DISPLAY ' RES POSTAL CDE IS ' K830-NUM-POSTAL               
                            ' FOR REC KEY-FLD ' K830-KEY-FLD                    
                 ELSE                                                           
                    STRING 'SINGAPORE '  K830-NUM-POSTAL                        
                       DELIMITED BY SIZE INTO PD47D-ADDR-L3                     
                 END-IF                                                         
      *------ TO GET THE BAR CODE ------                                        
                 MOVE SPACES                    TO WS-LINK-AV02C015             
                 MOVE K830-NUM-POSTAL           TO AV02C015-IN-POSTCODE         
                 PERFORM 4850-CALL-AV02C015     THRU 4850-EXIT                  
                 MOVE AV02C015-OUT-POSTCODE-CHKSUM TO PD47D-BAR-CODE            
                                                                                
              ELSE                                                              
                 MOVE K830-NUM-MAIL-BLK        TO PD47D-BLK                     
                 IF K830-NUM-MAIL-LVL-NUM = SPACES AND                          
                    K830-NUM-MAIL-HSEMAIN = SPACES                              
                    MOVE SPACES                TO PD47D-UNIT                    
                 ELSE                                                           
                    MOVE K830-NUM-MAIL-FLAT    TO PD47D-UNIT                    
                 END-IF                                                         
                 MOVE K830-NME-MAIL-STR-BLDG(1:47) TO PD47D-ADDR-L2             
                 IF K830-NUM-MAIL-POSTAL = SPACES OR LOW-VALUES OR ZEROS        
                    MOVE SPACES                TO PD47D-ADDR-L3                 
                    DISPLAY ' MAIL POSTAL CDE IS ' K830-NUM-POSTAL              
                            ' FOR REC KEY-FLD ' K830-KEY-FLD                    
                 ELSE                                                           
                    STRING 'SINGAPORE '      K830-NUM-MAIL-POSTAL               
                           DELIMITED BY SIZE INTO PD47D-ADDR-L3                 
                 END-IF                                                         
      *------ TO GET THE BAR CODE ------                                        
                 MOVE SPACES                    TO WS-LINK-AV02C015             
                 MOVE K830-NUM-MAIL-POSTAL      TO AV02C015-IN-POSTCODE         
                 PERFORM 4850-CALL-AV02C015     THRU 4850-EXIT                  
                 MOVE AV02C015-OUT-POSTCODE-CHKSUM TO PD47D-BAR-CODE            
              END-IF                                                            
                                                                                
           ELSE                                                                 
           IF WS-K830-STATUS = 23                                               
              INITIALIZE     PD47D-LINE05                                       
                             PD47D-LINE06                                       
                             PD47D-LINE07                                       
                             PD47D-LINE08                                       
              DISPLAY 'BP13K830 RECORD NOT FOUND = ' WS-K830-STATUS             
                      ',  K830-KEY-FLD IS ' K830-NUM-REGN                       
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K830 = ' WS-K830-STATUS                
                      ',  K830-KEY-FLD IS ' K830-NUM-REGN                       
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                        
                                                                                
       4800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       4900-READ-K857.                                                          
      *=============================================================            
           MOVE    SPACES                TO  BP13K857-REC.                      
           MOVE F800-NUM-REGN         TO  K857-KEY-FLD.                         
           READ BP13K857.                                                       
                                                                                
           IF WS-K820-STATUS = 00 OR 02                                         
              MOVE    K857-NUM-EMAIL     TO  PD47E-EMAIL                        
           ELSE                                                                 
           IF WS-K820-STATUS = 23                                               
              DISPLAY 'K857 REC NOT FOUND : '   K857-KEY-FLD                    
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K857 = '   WS-K857-STATUS              
                      ', K857-KEY-FLD = '   K857-KEY-FLD                        
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                      
                                                                                
       4900-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       4950-READ-KD00.                                                          
      *=============================================================            
           MOVE    SPACES                TO  BP13KD00-REC.                      
           MOVE F800-NUM-REGN         TO  KD00-KEY-FLD.                         
           READ BP13KD00.                                                       
                                                                                
           IF WS-KD00-STATUS = 00 OR 02                                         
              CONTINUE                                                          
           ELSE                                                                 
           IF WS-KD00-STATUS = 23                                               
              DISPLAY 'KD00 REC NOT FOUND : '   KD00-KEY-FLD                    
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13KD00 = '   WS-KD00-STATUS              
                      ', KD00-KEY-FLD = '   KD00-KEY-FLD                        
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                      
                                                                                
       4950-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       4850-CALL-AV02C015.                                                      
      *=============================================================            
                                                                                
           CALL 'AV02C015'          USING                                       
                                    AV02C015-IN-POSTCODE                        
                                    AV02C015-OUT-POSTCODE-CHKSUM.               
       4850-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-READ-PIDB.                                                          
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                  TO CD47-BLK-NO                          
                                           CD47-LVL-NO                          
                                           CD47-UNIT-NO                         
                                           CD47-STREET                          
                                           CD47-POSTAL-NO                       
           CALL 'BP13C913' USING WS-LINK-REC,                                   
                                 BP13COMM8-REC.                                 
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
           WHEN +000                                                            
              MOVE COMM8-NUM-BLK        TO CD47-BLK-NO                          
              MOVE COMM8-NUM-LEVEL      TO CD47-LVL-NO                          
              STRING COMM8-NUM-UNIT-MAIN                                        
                     COMM8-NUM-UNIT-SUB                                         
                     DELIMITED BY SIZE INTO CD47-UNIT-NO                        
              MOVE COMM8-NME-STREET         TO CD47-STREET                      
              MOVE COMM8-NUM-POSTAL-CODE    TO CD47-POSTAL-NO                   
              MOVE SPACES                 TO WS-STRING-TO-PARSE                 
              MOVE WS-ADDRESS             TO WS-STRING-TO-PARSE                 
              MOVE LENGTH OF WS-ADDRESS                                         
                                          TO WS-STRING-LENGTH                   
              MOVE 'Y'                    TO WS-INSERT-SPACE-FLAG               
              PERFORM 6010-PARSE-ADDRESS  THRU 6010-EXIT                        
              MOVE WS-PARSED-STRING       TO  CD47-CORR-ADDR                    
                                                                                
              MOVE WS-ADDRESS             TO WS-ADDR-IE                         
              MOVE SPACES                 TO WS-STRING-TO-PARSE                 
              MOVE WS-ADDR-IE             TO WS-STRING-TO-PARSE                 
              MOVE LENGTH OF WS-ADDRESS                                         
                                          TO WS-STRING-LENGTH                   
              MOVE 'Y'                    TO WS-INSERT-SPACE-FLAG               
              PERFORM 6010-PARSE-ADDRESS  THRU 6010-EXIT                        
              MOVE WS-PARSED-STRING       TO  PD47E-ADDRESS                     
           WHEN +100                                                            
              MOVE SPACES   TO BP13COMM8-REC                                    
              INITIALIZE       BP13COMM8-REC                                    
              DISPLAY 'REC NOT FOUND IN PIDB: ' WS-LINK-NUM-SCH-ACC             
           WHEN OTHER                                                           
              MOVE COMM8-CDE-SYSERR      TO WS-RETCODE                          
              DISPLAY 'ERROR IN CALLING BP13C913, RETCODE: '                    
                      WS-RETCODE                                                
              MOVE 99                    TO RETURN-CODE                         
              PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       6010-PARSE-ADDRESS.                                                      
      *--------------------------------------------------------------           
           MOVE SPACES                    TO   WS-PARSED-STRING.                
           MOVE 1                         TO   WS-CUR-POS-PS.                   
           ADD  1                         TO   WS-STRING-LENGTH.                
           PERFORM 6020-GET-CHAR          THRU 6020-EXIT                        
              VARYING WS-CUR-POS-SP FROM 1 BY 1                                 
              UNTIL WS-CUR-POS-SP = WS-STRING-LENGTH.                           
                                                                                
                                                                                
       6010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       6020-GET-CHAR.                                                           
      *---------------------------------------------------------------          
           IF WS-STRING-TO-PARSE(WS-CUR-POS-SP:1) NOT = SPACES                  
              IF WS-SPACE-FLAG = 'Y' AND                                        
                 WS-INSERT-SPACE-FLAG = 'Y' AND                                 
                 WS-PARSED-STRING NOT = SPACES                                  
                 MOVE ' ' TO WS-PARSED-STRING(WS-CUR-POS-PS:1)                  
                 ADD 1           TO  WS-CUR-POS-PS                              
              END-IF                                                            
              MOVE WS-STRING-TO-PARSE(WS-CUR-POS-SP:1)                          
                              TO  WS-PARSED-STRING(WS-CUR-POS-PS:1)             
              ADD 1           TO  WS-CUR-POS-PS                                 
              MOVE    'N'      TO WS-SPACE-FLAG                                 
           ELSE                                                                 
              MOVE 'Y'     TO WS-SPACE-FLAG                                     
           END-IF.                                                              
                                                                                
       6020-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       7000-PRT-LETTER.                                                         
      *=============================================================            
                                                                                
      * BP13ID                                                                  
            MOVE SPACES           TO   PD47D-PRTREC.                            
            WRITE PD47D-PRTREC AFTER PAGE.                                      
                                                                                
            WRITE PD47D-PRTREC    FROM  PD47D-LINE01 AFTER 2.                   
            WRITE PD47D-PRTREC    FROM  PD47D-LINE02.                           
            WRITE PD47D-PRTREC    FROM  PD47D-LINE03 AFTER 3.                   
            WRITE PD47D-PRTREC    FROM  PD47D-LINE04.                           
            WRITE PD47D-PRTREC    FROM  PD47D-LINE05.                           
            WRITE PD47D-PRTREC    FROM  PD47D-LINE06.                           
            WRITE PD47D-PRTREC    FROM  PD47D-LINE07.                           
            WRITE PD47D-PRTREC    FROM  PD47D-LINE08 AFTER 4.                   
            WRITE PD47D-PRTREC    FROM  PD47D-LINE09 AFTER 6.                   
                                                                                
      * BP13IE                                                                  
            MOVE DJDE-LINE-IE     TO   PD47E-PRTREC.                            
            WRITE PD47E-PRTREC AFTER PAGE.                                      
            WRITE PD47E-PRTREC    FROM  PD47E-LINE0A AFTER 29.                  
            WRITE PD47E-PRTREC    FROM  PD47E-LINE01.                           
            WRITE PD47E-PRTREC    FROM  PD47E-LINE02.                           
            WRITE PD47E-PRTREC    FROM  PD47E-LINE02A.                          
            WRITE PD47E-PRTREC    FROM  PD47E-LINE02B.                          
            WRITE PD47E-PRTREC    FROM  PD47E-LINE03 AFTER 3.                   
            WRITE PD47E-PRTREC    FROM  PD47E-LINE04.                           
            WRITE PD47E-PRTREC    FROM  PD47E-LINE05.                           
            WRITE PD47E-PRTREC    FROM  PD47E-LINE06.                           
                                                                                
      * BP13IF                                                                  
            MOVE  SPACES          TO   PD47E-PRTREC.                            
            WRITE PD47E-PRTREC FROM DJDE-LINE-IF AFTER PAGE.                    
            MOVE  SPACES          TO   PD47E-PRTREC.                            
            WRITE PD47E-PRTREC AFTER PAGE.                                      
      * BP13IG                                                                  
            MOVE  SPACES          TO   PD47E-PRTREC.                            
            WRITE PD47E-PRTREC FROM DJDE-LINE-IG AFTER PAGE.                    
            MOVE  SPACES          TO   PD47E-PRTREC.                            
            WRITE PD47E-PRTREC AFTER PAGE.                                      
      * BP13IH                                                                  
            MOVE  SPACES          TO   PD47E-PRTREC.                            
            WRITE PD47E-PRTREC FROM DJDE-LINE-IH AFTER PAGE.                    
                                                                                
            WRITE PD47E-PRTREC    FROM PD47H-LINE01 AFTER 59.                   
            WRITE PD47E-PRTREC    FROM PD47H-LINE02.                            
                                                                                
            ADD   1               TO   WS-PD47-WRT-CTR.                         
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *=============================================================            
       8000-PRN-CNTRL-LIST.                                                     
      *=============================================================            
                                                                                
           INITIALIZE LD47-DTL.                                                 
                                                                                
           MOVE F800-NUM-REGN             TO LD47-REGN                          
           MOVE F800-NUM-NRIC1            TO LD47-NRIC1                         
           MOVE WS-K820-NME-HA1           TO LD47-NAME1                         
           STRING 'BLK' COMM8-NUM-BLK                                           
                  ' # ' COMM8-NUM-LEVEL                                         
                  ' - ' COMM8-NUM-UNIT-MAIN COMM8-NUM-UNIT-SUB                  
              DELIMITED BY SIZE INTO LD47-PIDB-ADDR.                            
      *    MOVE 'BLK 518C # 05 - 60'      TO LD47-PIDB-ADDR.                    
                                                                                
           ADD 1                          TO WS-CNT-LINE-LD47.                  
           ADD 1                          TO WS-CNT-CUR-LINE.                   
           MOVE WS-CNT-LINE-LD47          TO LD47-SN                            
           IF WS-CNT-CUR-LINE > 55                                              
              ADD 1                       TO WS-CNT-PAGE-LD47                   
              MOVE WS-CNT-PAGE-LD47       TO LD47-PAGE                          
              WRITE LD47-PRTREC FROM LD47-HDG1 AFTER PAGE                       
              WRITE LD47-PRTREC FROM LD47-HDG2 AFTER 2                          
              WRITE LD47-PRTREC FROM LD47-HDG3 AFTER 3                          
              MOVE 6                      TO WS-CNT-CUR-LINE                    
           END-IF                                                               
                                                                                
           WRITE LD47-PRTREC FROM LD47-DTL.                                     
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       9000-CLOSE-ROUTINE.                                                      
      **************************************************************            
           DISPLAY ' '.                                                         
           DISPLAY ' ====  BP13CD47 CONTROL TOTALS  ======'.                    
           DISPLAY ' '.                                                         
           DISPLAY ' F800 RECORDS READ          : ' WS-F800-READ-CTR.           
           DISPLAY ' NO OF CASES PRINTED        : ' WS-PD47-WRT-CTR.            
           DISPLAY ' NO OF CASES WRITE          : ' WS-CNT-LINE-LD47.           
           DISPLAY ' '.                                                         
           DISPLAY ' ====================================='.                    
                                                                                
           CLOSE BP13F800                                                       
                 SY02F001                                                       
                 BP13K820                                                       
                 BP13K830                                                       
                 BP13K857                                                       
                 BP13KD00                                                       
                 P13PD47D                                                       
                 P13PD47E                                                       
                 P13PD47F                                                       
                 P13PD47G                                                       
                 P13PD47H                                                       
                 BP13LD47.                                                      
                                                                                
           IF WS-K820-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR BP13K820-STAT ' WS-K820-STATUS.            
                                                                                
           IF WS-K830-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR BP13K830-STAT ' WS-K830-STATUS.            
                                                                                
           IF WS-K857-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR BP13K857-STAT ' WS-K857-STATUS.            
                                                                                
           IF WS-KD00-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR BP13KD00-STAT ' WS-KD00-STATUS.            
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
