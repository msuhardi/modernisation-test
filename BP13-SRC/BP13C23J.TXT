       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C23J.                                                 
      *AUTHOR.        RACHELLE SAN BUENAVENTURA.                                
      *DATE-WRITTEN.  22 APR 2008.                                              
      * ========================================================== *            
      * SYSTEM OF COMMITMENT  (BP13)                               *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE   : LETTER TO INFORM APPLICANT THAT HDB WILL    *            
      *                PROCEED WITH CONSTRUCTION OF BTO FLAT       *            
      *                + PRINTING OF WARRANT TO ACT                *            
      *                + NOTIFICATION FORM                         *            
      *                                                            *            
      *  INPUT FILES : 1.  BP13F800                                *            
      *                2.  BP13K820                                *            
      *                3.  BP13K830                                *            
      *                4.  BP13K022                                *            
      *                5.  BP13K024                                *            
      *                                                            *            
      *  LISTINGS    : 1.  BP13P000  - LETTER                      *            
      *                2.  BP13L000  - CONTROL LIST                *            
      *                                                            *            
      *  SUBROUNTINE : 1.  BP13C913 FOR BOOKED FLAT ADDRESS        *            
      *              : 2.  AV02COMM FOR POSTAL CODE BARCODE        *            
      *                                                            *            
      *  FORM ID: BP13TX - LETTER                                  *            
      *           BP13TL - WARRANT TO ACT                          *            
      *           BP13TM - NOTIFICATION FORM                       *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP133307 20080422 RB12  NEW PROGRAM PATTERNED FROM BP13BT08*            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
      *-------------------------------------------------------------            
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F800  ASSIGN        TO BP13F800.                          
                                                                                
           SELECT BP13K820  ASSIGN        TO BP13K820                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K820-KEY-FLD                       
                            FILE STATUS   IS BP13K820-STATUS.                   
                                                                                
           SELECT BP13K830  ASSIGN        TO BP13K830                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K830-KEY-FLD                       
                            ALTERNATE KEY IS K830-NUM-REGN                      
                            FILE STATUS   IS BP13K830-STATUS.                   
                                                                                
           SELECT BP13K022  ASSIGN        TO BP13K022                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K022-KEY-FLD                       
                            ALTERNATE KEY IS K022-NUM-REGN                      
                            FILE STATUS   IS BP13K022-STATUS.                   
                                                                                
           SELECT BP13K024  ASSIGN        TO BP13K024                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K024-KEY-FLD                       
                            FILE STATUS   IS BP13K024-STATUS.                   
                                                                                
           SELECT BP13P000  ASSIGN        TO BP13P000.                          
           SELECT BP13L000  ASSIGN        TO BP13L000.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F800                                                            
            RECORDING MODE  IS    F                                             
            LABEL RECORDS   ARE   STANDARD                                      
            BLOCK  CONTAINS    0  RECORDS                                       
            RECORD CONTAINS 2000  CHARACTERS.                                   
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS  400  CHARACTERS.                                   
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS  250  CHARACTERS.                                   
       COPY BP13K830.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS  100  CHARACTERS.                                   
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K024                                                            
            RECORD CONTAINS  100  CHARACTERS.                                   
       COPY BP13K024.                                                           
                                                                                
       FD   BP13P000                                                            
            RECORDING MODE    IS  F                                             
            RECORD CONTAINS  100  CHARACTERS.                                   
       01   BP13P000-REC     PIC X(100).                                        
                                                                                
       FD   BP13L000                                                            
            RECORDING MODE    IS  F                                             
            RECORD CONTAINS  132  CHARACTERS.                                   
       01   BP13L000-REC     PIC X(132).                                        
                                                                                
      *-------------------------------------------------------------            
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS-VARS.                                                 
           05  BP13K820-STATUS           PIC 9(02)  VALUE ZEROES.               
           05  BP13K830-STATUS           PIC 9(02)  VALUE ZEROES.               
           05  BP13K022-STATUS           PIC 9(02)  VALUE ZEROES.               
           05  BP13K024-STATUS           PIC 9(02)  VALUE ZEROES.               
           05  BP13F800-STATUS           PIC X(01)  VALUE SPACE.                
               88  BP13F800-EOF          VALUE 'Y'.                             
                                                                                
       01  WS-DATE-VARS.                                                        
           05  WS-CURR-DATE              PIC X(08).                             
           05  WS-CURR-DATE-NUM REDEFINES WS-CURR-DATE                          
                                         PIC 9(08).                             
           05  WS-EDIT-CURR-DATE         PIC X(10)  VALUE SPACES.               
           05  WS-INTEGER-DATE           PIC 9(07)  VALUE ZEROES.               
           05  WS-DATE-14AFTER           PIC 9(08)  VALUE ZEROES.               
           05  WS-DATE-ALPHA.                                                   
               10  WS-DATE-DD            PIC X(02)  VALUE SPACES.               
               10  FILLER                PIC X(01)  VALUE SPACE.                
               10  WS-DATE-MM            PIC X(03)  VALUE SPACES.               
               10  FILLER                PIC X(01)  VALUE SPACE.                
               10  WS-DATE-YYYY          PIC X(04)  VALUE SPACES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F800-READ-CNT          PIC 9(08)  VALUE ZEROES.               
           05  WS-NO-CONTACT-ADDR        PIC 9(08)  VALUE ZEROES.               
           05  WS-PIDB-NOT-CASES         PIC 9(08)  VALUE ZEROES.               
           05  WS-LETTER-PRINTED         PIC 9(08)  VALUE ZEROES.               
                                                                                
       01  WS-OTHER-VARS.                                                       
           05  WS-POSTAL-CODE            PIC X(06)  VALUE SPACES.               
           05  WS-NAME                   PIC X(66)  VALUE SPACES.               
           05  WS-MAX-LINES              PIC 9(02)  VALUE 55.                   
           05  WS-LINE-CNT               PIC 9(02)  VALUE 60.                   
           05  WS-PAGE-CNT               PIC 9(05)  VALUE ZEROES.               
           05  WS-SLNO                   PIC 9(06)  VALUE ZEROES.               
           05  WS-LETTER-TAG             PIC X(01)  VALUE SPACE.                
               88  PRINT-LETTER          VALUE 'Y'.                             
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR AV02C015                                                   
      *-------------------------------------------------------------            
        01  WS-LINK-AV02C015.                                                   
            05  AV02C015-IN-POSTCODE          PIC X(6).                         
            05  AV02C015-OUT-POSTCODE-CHKSUM  PIC X(9).                         
                                                                                
       COPY AV02COMM.                                                           
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR BP13C913                                                   
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH            PIC X(4).                              
           05 WS-LINK-NUM-ACC            PIC X(4).                              
           05 WS-LINK-NUM-CHK            PIC X(1).                              
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *-------------------------------------------------------------            
      *  COVER LETTER                                                           
      *-------------------------------------------------------------            
       01  P000-LINE01.                                                         
           05 FILLER                 PIC X(72)  VALUE SPACES.                   
           05 P000-REF-NO            PIC X(15)  VALUE SPACES.                   
                                                                                
       01  P000-LINE02.                                                         
           05 FILLER                 PIC X(72)  VALUE SPACES.                   
           05 P000-DATE              PIC X(11)  VALUE SPACES.                   
                                                                                
       01  P000-LINE03.                                                         
           05 FILLER                 PIC X(11)  VALUE SPACES.                   
           05 P000-NME-OCCP1         PIC X(66)  VALUE SPACES.                   
                                                                                
       01  P000-LINE03B.                                                        
           05 FILLER                 PIC X(11)  VALUE SPACES.                   
           05 P000-NME-OCCP2         PIC X(66)  VALUE SPACES.                   
                                                                                
       01  P000-LINE04.                                                         
           05 FILLER                 PIC X(11)  VALUE SPACES.                   
           05 P000-NUM-BLOCK         PIC X(11)  VALUE SPACES.                   
           05 P000-HOUSE-LEVEL       PIC X(11)  VALUE SPACES.                   
                                                                                
       01  P000-LINE04B.                                                        
           05 FILLER                 PIC X(11)  VALUE SPACES.                   
           05 P000-STREET-NAME       PIC X(65)  VALUE SPACES.                   
                                                                                
       01  P000-LINE04C.                                                        
           05 FILLER                 PIC X(11)  VALUE SPACES.                   
           05 P000-NUM-POSTAL        PIC X(30)  VALUE SPACES.                   
                                                                                
       01  P000-LINE05.                                                         
           05 FILLER                 PIC X(10)  VALUE SPACES.                   
           05 P000-BAR-CODE          PIC X(09)  VALUE SPACES.                   
                                                                                
       01  P000-LINE06.                                                         
           05 FILLER                 PIC X(10)  VALUE SPACES.                   
           05 P000-UNIT-ADDRESS      PIC X(60)  VALUE SPACES.                   
                                                                                
       01  P000-LINE07.                                                         
           05 FILLER                 PIC X(74)  VALUE SPACES.                   
           05 P000-DATE07            PIC X(12)  VALUE SPACES.                   
                                                                                
       01  P000-LINE07B.                                                        
           05 FILLER                 PIC X(20)  VALUE SPACES.                   
           05 P000-DATE07B           PIC X(12)  VALUE SPACES.                   
                                                                                
       01  P000-LINE08.                                                         
           05 FILLER                 PIC X(73)  VALUE SPACES.                   
           05 P000-TEL-CSM           PIC X(08)  VALUE SPACES.                   
                                                                                
       01  P000-LINE09.                                                         
           05 FILLER                 PIC X(17)  VALUE SPACES.                   
           05 P000-MOBILE-CSM        PIC X(08)  VALUE SPACES.                   
                                                                                
       01  P000-LINE10.                                                         
           05 FILLER                 PIC X(10)  VALUE SPACES.                   
           05 P000-NME-CSM           PIC X(30)  VALUE SPACES.                   
                                                                                
      *-------------------------------------------------------------            
      *  WARRANT TO ACT                                                         
      *-------------------------------------------------------------            
       01  P000-P2-LINE01.                                                      
           05 FILLER                      PIC X(19) VALUE SPACES.               
           05 P000-P2-UNIT-BLK            PIC X(5)  VALUE SPACES.               
           05 FILLER                      PIC X(2)  VALUE SPACES.               
           05 P000-P2-UNIT-FLAT-NO.                                             
              10 FILLER                   PIC X     VALUE '#'.                  
              10 P000-P2-UNIT-LVL-NO      PIC X(2)  VALUE SPACES.               
              10 FILLER                   PIC X     VALUE '-'.                  
              10 P000-P2-UNIT-UNIT-NO     PIC X(5)  VALUE SPACES.               
           05 FILLER                      PIC X(2)  VALUE SPACES.               
           05 P000-P2-UNIT-STREET-POSTAL  PIC X(35) VALUE SPACES.               
                                                                                
       01  P000-P2-LINE02.                                                      
           05 FILLER                      PIC X(12) VALUE SPACES.               
           05 P000-P2-OCCP1               PIC X(51) VALUE SPACES.               
           05 FILLER                      PIC X(2)  VALUE SPACES.               
           05 P000-P2-NRIC1               PIC X(9)  VALUE SPACES.               
                                                                                
       01  P000-P2-LINE03.                                                      
           05 FILLER                      PIC X(12) VALUE SPACES.               
           05 P000-P2-OCCP2               PIC X(51) VALUE SPACES.               
           05 FILLER                      PIC X(2)  VALUE SPACES.               
           05 P000-P2-NRIC2               PIC X(9)  VALUE SPACES.               
                                                                                
       01  P000-P2-LINE04.                                                      
           05 FILLER                      PIC X(12) VALUE SPACES.               
           05 P000-P2-OCCP3               PIC X(51) VALUE SPACES.               
           05 FILLER                      PIC X(2)  VALUE SPACES.               
           05 P000-P2-NRIC3               PIC X(9)  VALUE SPACES.               
                                                                                
       01  P000-P2-LINE05.                                                      
           05 FILLER                      PIC X(12) VALUE SPACES.               
           05 P000-P2-OCCP4               PIC X(51) VALUE SPACES.               
           05 FILLER                      PIC X(2)  VALUE SPACES.               
           05 P000-P2-NRIC4               PIC X(9)  VALUE SPACES.               
                                                                                
       01  P000-P2-LINE06.                                                      
           05 FILLER                      PIC X(20) VALUE SPACES.               
           05 P000-RES-NUM-BLK            PIC X(16) VALUE SPACES.               
           05 P000-RES-HSE-LVL            PIC X(11) VALUE SPACES.               
                                                                                
       01  P000-P2-LINE07.                                                      
           05 FILLER                      PIC X(20) VALUE SPACES.               
           05 P000-RES-NME-ST             PIC X(65) VALUE SPACES.               
                                                                                
       01  P000-P2-LINE08.                                                      
           05 FILLER                      PIC X(20) VALUE SPACES.               
           05 P000-RES-NUM-POSTAL         PIC X(16) VALUE SPACES.               
                                                                                
       01  P000-P2-LINE09.                                                      
           05 FILLER                      PIC X(10) VALUE SPACES.               
           05 P000-DASH1                  PIC X(36) VALUE ALL '_'.              
           05 FILLER                      PIC X(4)  VALUE SPACES.               
           05 P000-DASH2                  PIC X(36) VALUE SPACES.               
                                                                                
       01  P000-P2-LINE10.                                                      
           05 FILLER                      PIC X(10) VALUE SPACES.               
           05 P000-SIGN1                  PIC X(36) VALUE                       
                                          'SIGNATURE OF'.                       
           05 FILLER                      PIC X(4)  VALUE SPACES.               
           05 P000-SIGN2                  PIC X(36) VALUE SPACES.               
                                                                                
       01  P000-P2-LINE11.                                                      
           05 FILLER                      PIC X(10) VALUE SPACES.               
           05 P000-OCCP1                  PIC X(36) VALUE SPACES.               
           05 FILLER                      PIC X(4)  VALUE SPACES.               
           05 P000-OCCP2                  PIC X(36) VALUE SPACES.               
                                                                                
       01  P000-P2-LINE12.                                                      
           05 FILLER                      PIC X(10) VALUE SPACES.               
           05 P000-DASH3                  PIC X(36) VALUE SPACES.               
           05 FILLER                      PIC X(4)  VALUE SPACES.               
           05 P000-DASH4                  PIC X(36) VALUE SPACES.               
                                                                                
       01  P000-P2-LINE13.                                                      
           05 FILLER                      PIC X(10) VALUE SPACES.               
           05 P000-SIGN3                  PIC X(36) VALUE SPACES.               
           05 FILLER                      PIC X(4)  VALUE SPACES.               
           05 P000-SIGN4                  PIC X(36) VALUE SPACES.               
                                                                                
       01  P000-P2-LINE14.                                                      
           05 FILLER                      PIC X(10) VALUE SPACES.               
           05 P000-OCCP3                  PIC X(36) VALUE SPACES.               
           05 FILLER                      PIC X(4)  VALUE SPACES.               
           05 P000-OCCP4                  PIC X(36) VALUE SPACES.               
                                                                                
      *-------------------------------------------------------------            
      *  NOTIFICATION FORM                                                      
      *-------------------------------------------------------------            
       01  P000-P3-LINE01.                                                      
      *    05 FILLER                     PIC X(68)  VALUE SPACES.               
           05 FILLER                     PIC X(76)  VALUE SPACES.               
           05 P000-P3-DATE               PIC X(11)  VALUE SPACES.               
                                                                                
       01  P000-P3-LINE02.                                                      
           05 FILLER                     PIC X(27) VALUE SPACES.                
           05 P000-P3-REGN-NO            PIC X(8)  VALUE SPACES.                
                                                                                
       01  P000-P3-LINE03.                                                      
           05 FILLER                     PIC X(10) VALUE SPACES.                
           05 P000-P3-DETAIL2            PIC X(71) VALUE SPACES.                
                                                                                
      *-------------------------------------------------------------            
      * REPORT LAYOUT                                                           
      *-------------------------------------------------------------            
       01  L000-HEADER-1.                                                       
           05 FILLER                     PIC X(08)  VALUE 'BP13C23J'.           
           05 FILLER                     PIC X(47)  VALUE SPACES.               
           05 FILLER                     PIC X(20)  VALUE                       
              'SYSTEM OF COMMITMENT'.                                           
           05 FILLER                     PIC X(38)  VALUE SPACES.               
           05 FILLER                     PIC X(7)   VALUE 'DATE : '.            
           05 L000-DATE                  PIC X(10)  VALUE SPACES.               
                                                                                
       01  L000-HEADER-2.                                                       
           05 FILLER                     PIC X(48)  VALUE SPACES.               
           05 FILLER                     PIC X(40)  VALUE                       
              'PROCEED TO BUILD BTO FLAT LETTER PRINTED'.                       
           05 FILLER                     PIC X(25)  VALUE SPACES.               
           05 FILLER                     PIC X(7)   VALUE 'PAGE : '.            
           05 L000-PAGE                  PIC Z(4)9.                             
                                                                                
       01  L000-HEADER-3.                                                       
           05 FILLER                     PIC X(26)  VALUE                       
              ' S/NO  REGN      NT/FT    '.                                     
           05 FILLER                     PIC X(72)  VALUE                       
              'NRIC/NAME 1 - 4'.                                                
           05 FILLER                     PIC X(33)  VALUE                       
              'LOAN TAG  OIC   BK FEE PAID  PROV'.                              
                                                                                
       01  L000-HEADER-4.                                                       
           05 FILLER                     PIC X(132) VALUE ALL '-'.              
                                                                                
       01  L000-DETAIL-LINES.                                                   
           02  L000-DETAIL-1.                                                   
               05 L000-SLNO              PIC Z(5)9.                             
               05 FILLER                 PIC X(2)   VALUE SPACES.               
               05 L000-NUM-REGN          PIC X(8)   VALUE SPACES.               
               05 FILLER                 PIC X(2)   VALUE SPACES.               
               05 L000-NTFT              PIC X(6)   VALUE SPACES.               
               05 FILLER                 PIC X(2)   VALUE SPACES.               
               05 L000-NUM-NRIC1         PIC X(9)   VALUE SPACES.               
               05 FILLER                 PIC X(2)   VALUE SPACES.               
               05 L000-NME-OCCP1         PIC X(60)  VALUE SPACES.               
               05 FILLER                 PIC X(5)   VALUE SPACES.               
               05 L000-NUM-LOAN-TAG      PIC X(1)   VALUE SPACES.               
               05 FILLER                 PIC X(5)   VALUE SPACES.               
               05 L000-NUM-OIC           PIC X(5)   VALUE SPACES.               
               05 FILLER                 PIC X(5)   VALUE SPACES.               
               05 L000-AMT-BK-FEE-PAID   PIC 9(4).99 VALUE ZERO.                
               05 FILLER                 PIC X(2)   VALUE SPACES.               
               05 L000-NUM-PROV-TAG      PIC X      VALUE SPACE.                
               05 FILLER                 PIC X      VALUE SPACE.                
               05 L000-NUM-PROV-REASON   PIC XX     VALUE SPACES.               
                                                                                
           02  L000-DETAIL-2.                                                   
               05 FILLER                 PIC X(26)  VALUE SPACES.               
               05 L000-NUM-NRIC2         PIC X(9)   VALUE SPACES.               
               05 FILLER                 PIC X(2)   VALUE SPACES.               
               05 L000-NME-OCCP2         PIC X(60)  VALUE SPACES.               
                                                                                
           02  L000-DETAIL-3.                                                   
               05 FILLER                 PIC X(26)  VALUE SPACES.               
               05 L000-NUM-NRIC3         PIC X(9)   VALUE SPACES.               
               05 FILLER                 PIC X(2)   VALUE SPACES.               
               05 L000-NME-OCCP3         PIC X(60)  VALUE SPACES.               
                                                                                
           02  L000-DETAIL-4.                                                   
               05 FILLER                 PIC X(26)  VALUE SPACES.               
               05 L000-NUM-NRIC4         PIC X(9)   VALUE SPACES.               
               05 FILLER                 PIC X(2)   VALUE SPACES.               
               05 L000-NME-OCCP4         PIC X(60)  VALUE SPACES.               
                                                                                
           02  L000-DETAIL-5.                                                   
               05 FILLER                 PIC X(8)   VALUE SPACES.               
               05 FILLER                 PIC X(7)   VALUE 'MAIL:'.              
               05 L000-MAIL-ADDRESS      PIC X(110) VALUE SPACES.               
                                                                                
           02  L000-DETAIL-6.                                                   
               05 FILLER                 PIC X(8)   VALUE SPACES.               
               05 FILLER                 PIC X(7)   VALUE 'FLAT:'.              
               05 L000-FLAT-ADDRESS      PIC X(110) VALUE SPACES.               
                                                                                
       01  L000-FOOTER-1.                                                       
           05 FILLER                     PIC X(25)  VALUE SPACES.               
           05 FILLER                     PIC X(34)  VALUE                       
              'TOTAL NUMBER OF BTO CASES READ  : '.                             
           05 L000-TOTAL-CASES           PIC Z(7)9.                             
                                                                                
       01  L000-FOOTER-2.                                                       
           05 FILLER                     PIC X(25)  VALUE SPACES.               
           05 FILLER                     PIC X(34)  VALUE                       
              'CASES NOT FOUND IN PIDB         : '.                             
           05 L000-PIDB-NOT-CASES        PIC Z(7)9.                             
                                                                                
       01  L000-FOOTER-3.                                                       
           05 FILLER                     PIC X(25)  VALUE SPACES.               
           05 FILLER                     PIC X(34)  VALUE                       
              'CASES WITH NO CONTACT ADDRESS   : '.                             
           05 L000-NO-CONTACT-ADDR       PIC Z(7)9.                             
                                                                                
       01  L000-FOOTER-4.                                                       
           05 FILLER                     PIC X(25)  VALUE SPACES.               
           05 FILLER                     PIC X(34)  VALUE                       
              'TOTAL NUMBER OF LETTERS PRINTED : '.                             
           05 L000-TOTAL-PRINTED         PIC Z(7)9.                             
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------*           
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------*           
            PERFORM 1000-OPEN-ROUTINE         THRU  1000-EXIT.                  
            PERFORM 2000-READ-BP13F800        THRU  2000-EXIT.                  
            PERFORM 3000-PROCESS-PARA         THRU  3000-EXIT                   
                         UNTIL BP13F800-EOF.                                    
            PERFORM 8800-PRINT-CONTROL        THRU  8800-EXIT.                  
            PERFORM 9000-CLOSE-ROUTINE        THRU  9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------*           
            OPEN  INPUT  BP13F800  BP13K820  BP13K830                           
                         BP13K022  BP13K024                                     
                 OUTPUT  BP13P000  BP13L000.                                    
                                                                                
           IF BP13K820-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'BP13K820 OPEN ERROR, STATUS ' BP13K820-STATUS            
              MOVE     BP13K820-STATUS  TO RETURN-CODE                          
              PERFORM  9000-CLOSE-ROUTINE                                       
           END-IF.                                                              
                                                                                
           IF BP13K830-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'BP13K830 OPEN ERROR, STATUS ' BP13K830-STATUS            
              MOVE     BP13K830-STATUS  TO RETURN-CODE                          
              PERFORM  9000-CLOSE-ROUTINE                                       
           END-IF.                                                              
                                                                                
           IF BP13K022-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'BP13K022 OPEN ERROR, STATUS ' BP13K022-STATUS            
              MOVE     BP13K022-STATUS  TO RETURN-CODE                          
              PERFORM  9000-CLOSE-ROUTINE                                       
           END-IF.                                                              
                                                                                
           IF BP13K024-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'BP13K024 OPEN ERROR, STATUS ' BP13K024-STATUS            
              MOVE     BP13K024-STATUS  TO RETURN-CODE                          
              PERFORM  9000-CLOSE-ROUTINE                                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE.                          
                                                                                
           STRING  WS-CURR-DATE(7:2)  '/'                                       
                   WS-CURR-DATE(5:2)  '/'                                       
                   WS-CURR-DATE(1:4)                                            
                   DELIMITED BY SIZE INTO L000-DATE.                            
                                                                                
           MOVE SPACES TO WS-DATE-ALPHA.                                        
                                                                                
           MOVE WS-CURR-DATE(7:2)    TO WS-DATE-DD.                             
           MOVE WS-CURR-DATE(1:4)    TO WS-DATE-YYYY.                           
                                                                                
           EVALUATE WS-CURR-DATE(5:2)                                           
              WHEN 01                                                           
                 MOVE 'JAN'          TO WS-DATE-MM                              
              WHEN 02                                                           
                 MOVE 'FEB'          TO WS-DATE-MM                              
              WHEN 03                                                           
                 MOVE 'MAR'          TO WS-DATE-MM                              
              WHEN 04                                                           
                 MOVE 'APR'          TO WS-DATE-MM                              
              WHEN 05                                                           
                 MOVE 'MAY'          TO WS-DATE-MM                              
              WHEN 06                                                           
                 MOVE 'JUN'          TO WS-DATE-MM                              
              WHEN 07                                                           
                 MOVE 'JUL'          TO WS-DATE-MM                              
              WHEN 08                                                           
                 MOVE 'AUG'          TO WS-DATE-MM                              
              WHEN 09                                                           
                 MOVE 'SEP'          TO WS-DATE-MM                              
              WHEN 10                                                           
                 MOVE 'OCT'          TO WS-DATE-MM                              
              WHEN 11                                                           
                 MOVE 'NOV'          TO WS-DATE-MM                              
              WHEN 12                                                           
                 MOVE 'DEC'          TO WS-DATE-MM                              
           END-EVALUATE.                                                        
                                                                                
           MOVE WS-DATE-ALPHA        TO P000-DATE.                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       2000-READ-BP13F800.                                                      
      *-------------------------------------------------------------*           
           READ BP13F800  AT END                                                
                           MOVE 'Y' TO BP13F800-STATUS                          
                          NOT AT END                                            
                           ADD  1   TO WS-F800-READ-CNT                         
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       3000-PROCESS-PARA.                                                       
      *-------------------------------------------------------------*           
           INITIALIZE   L000-DETAIL-LINES.                                      
           MOVE SPACES  TO    P000-REF-NO                                       
                              P000-NME-OCCP1                                    
                              P000-NME-OCCP2                                    
                              P000-LINE04                                       
                              P000-LINE04B                                      
                              P000-LINE04C                                      
                              P000-LINE05                                       
                              P000-LINE06                                       
                              P000-LINE08                                       
                              P000-LINE09                                       
                              P000-LINE10                                       
                              P000-P2-UNIT-BLK                                  
                              P000-P2-UNIT-LVL-NO                               
                              P000-P2-UNIT-UNIT-NO                              
                              P000-P2-UNIT-STREET-POSTAL                        
                              P000-P2-LINE02                                    
                              P000-P2-LINE03                                    
                              P000-P2-LINE04                                    
                              P000-P2-LINE05                                    
                              P000-P2-LINE06                                    
                              P000-P2-LINE07                                    
                              P000-P2-LINE08                                    
                              P000-P3-REGN-NO                                   
                              P000-P3-DETAIL2                                   
                              P000-DASH2                                        
                              P000-DASH3                                        
                              P000-DASH4                                        
                              P000-SIGN2                                        
                              P000-SIGN3                                        
                              P000-SIGN4                                        
                              P000-OCCP1                                        
                              P000-OCCP2                                        
                              P000-OCCP3                                        
                              P000-OCCP4                                        
                              L000-DETAIL-LINES.                                
                                                                                
           MOVE   'Y'    TO WS-LETTER-TAG.                                      
                                                                                
           PERFORM 4000-GET-OCCP-NAME     THRU 4000-EXIT.                       
           PERFORM 5000-GET-MAIL-ADDR     THRU 5000-EXIT.                       
           PERFORM 6000-GET-FLAT-ADDR     THRU 6000-EXIT.                       
           PERFORM 6500-GET-OIC-DETAILS   THRU 6500-EXIT.                       
           PERFORM 6800-GET-DTE-14DAYS    THRU 6800-EXIT.                       
                                                                                
           IF PRINT-LETTER                                                      
              PERFORM 7000-PRINT-LETTER   THRU 7000-EXIT                        
              PERFORM 8000-PRINT-LISTING  THRU 8000-EXIT                        
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F800        THRU 2000-EXIT.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       4000-GET-OCCP-NAME.                                                      
      *-------------------------------------------------------------*           
           STRING  F800-NUM-NEW-TOWN  '/'                                       
                   F800-NUM-ROOM      '/'                                       
                   F800-NUM-REGN                                                
                   DELIMITED BY SIZE INTO P000-REF-NO.                          
                                                                                
           STRING  F800-NUM-NEW-TOWN  '/'                                       
                   F800-NUM-ROOM                                                
                   DELIMITED BY SIZE INTO L000-NTFT.                            
                                                                                
           MOVE    F800-NUM-REGN TO P000-P3-REGN-NO.                            
                                                                                
                                                                                
      ** NAME OF OCCUPIER 1 **                                                  
      ** ------------------ **                                                  
           MOVE F800-NUM-REGN           TO K820-NUM-REGN                        
                                           L000-NUM-REGN.                       
                                                                                
           MOVE F800-NUM-NRIC1          TO K820-NUM-NRIC                        
                                           P000-P2-NRIC1                        
                                           L000-NUM-NRIC1.                      
           PERFORM 4100-READ-BP13K820   THRU 4100-EXIT.                         
           MOVE WS-NAME                 TO P000-NME-OCCP1                       
                                           P000-P2-OCCP1                        
                                           P000-OCCP1                           
                                           L000-NME-OCCP1.                      
                                                                                
      ** NAME OF OCCUPIER 2 **                                                  
      ** ------------------ **                                                  
           IF F800-NUM-NRIC2  NOT =  SPACES AND LOW-VALUES                      
              MOVE     F800-NUM-NRIC2       TO K820-NUM-NRIC                    
                                               P000-P2-NRIC2                    
                                               L000-NUM-NRIC2                   
              PERFORM  4100-READ-BP13K820   THRU 4100-EXIT                      
              MOVE     WS-NAME              TO P000-NME-OCCP2                   
                                               P000-P2-OCCP2                    
                                               P000-OCCP2                       
                                               L000-NME-OCCP2                   
              MOVE ALL '_'                  TO P000-DASH2                       
              MOVE 'SIGNATURE OF'           TO P000-SIGN2                       
           END-IF.                                                              
                                                                                
      ** NAME OF OCCUPIER 3 **                                                  
      ** ------------------ **                                                  
           IF F800-NUM-NRIC3  NOT =  SPACES AND LOW-VALUES                      
              MOVE     F800-NUM-NRIC3       TO K820-NUM-NRIC                    
                                               P000-P2-NRIC3                    
                                               L000-NUM-NRIC3                   
              PERFORM  4100-READ-BP13K820   THRU 4100-EXIT                      
              MOVE     WS-NAME              TO P000-P2-OCCP3                    
                                               L000-NME-OCCP3                   
                                               P000-OCCP3                       
              MOVE ALL '_'                  TO P000-DASH3                       
              MOVE 'SIGNATURE OF'           TO P000-SIGN3                       
           END-IF.                                                              
                                                                                
      ** NAME OF OCCUPIER 4 **                                                  
      ** ------------------ **                                                  
           IF F800-NUM-NRIC4  NOT =  SPACES AND LOW-VALUES                      
              MOVE     F800-NUM-NRIC4       TO K820-NUM-NRIC                    
                                               P000-P2-NRIC4                    
                                               L000-NUM-NRIC4                   
              PERFORM  4100-READ-BP13K820   THRU 4100-EXIT                      
              MOVE     WS-NAME              TO P000-P2-OCCP4                    
                                               L000-NME-OCCP4                   
                                               P000-OCCP4                       
              MOVE ALL '_'                  TO P000-DASH4                       
              MOVE 'SIGNATURE OF'           TO P000-SIGN4                       
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       4100-READ-BP13K820.                                                      
      *-------------------------------------------------------------*           
           READ  BP13K820.                                                      
                                                                                
           EVALUATE BP13K820-STATUS                                             
             WHEN 00                                                            
                MOVE    K820-NME-OCCP       TO WS-NAME                          
             WHEN 23                                                            
                MOVE    SPACES              TO WS-NAME                          
                DISPLAY 'REC NOT FOUND IN BP13K820'                             
                      ', K820-KEY-FLD : ' K820-KEY-FLD                          
             WHEN OTHER                                                         
                DISPLAY 'BP13K820 READ ERROR,STATUS : ' BP13K820-STATUS         
                      ', K820-KEY-FLD : ' K820-KEY-FLD                          
                MOVE BP13K820-STATUS   TO RETURN-CODE                           
                PERFORM 9000-CLOSE-ROUTINE                                      
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       5000-GET-MAIL-ADDR.                                                      
      *-------------------------------------------------------------*           
           MOVE    SPACES                TO WS-POSTAL-CODE.                     
           MOVE F800-NUM-REGN            TO K830-NUM-REGN.                      
           PERFORM 5100-READ-BP13K830    THRU 5100-EXIT.                        
                                                                                
           IF BP13K830-STATUS = 00                                              
              IF K830-NUM-MAIL-ADDR-TYPE  NOT =  SPACES AND LOW-VALUES          
                 PERFORM 5200-GET-MAILING-ADDR   THRU 5200-EXIT                 
              ELSE                                                              
                 PERFORM 5400-GET-HOUSE-ADDR     THRU 5400-EXIT                 
              END-IF                                                            
              IF WS-POSTAL-CODE  NOT =  SPACES AND LOW-VALUES                   
                 PERFORM 5800-GET-POSTAL-BARCODE    THRU 5800-EXIT              
              END-IF                                                            
           END-IF.                                                              
                                                                                
      * GET RESIDENTIAL ADDRESS FOR PRINTING OF WARRANT TO ACT                  
      *-------------------------------------------------------------            
           MOVE K830-NUM-BLK           TO P000-RES-NUM-BLK.                     
           MOVE K830-NUM-FLAT          TO P000-RES-HSE-LVL.                     
           MOVE K830-NME-STREET-BLDG   TO P000-RES-NME-ST.                      
           IF K830-NUM-POSTAL = SPACES OR ZEROS                                 
              MOVE SPACES            TO P000-RES-NUM-POSTAL                     
              DISPLAY ' RES POSTAL EMPTY ' K830-NUM-POSTAL                      
                      ', REGN IS ' K830-NUM-REGN                                
           ELSE                                                                 
              STRING 'SINGAPORE ' K830-NUM-POSTAL                               
                 DELIMITED BY SIZE INTO P000-RES-NUM-POSTAL.                    
                                                                                
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       5100-READ-BP13K830.                                                      
      *-------------------------------------------------------------*           
           READ BP13K830 KEY IS K830-NUM-REGN.                                  
                                                                                
           EVALUATE BP13K830-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
                CONTINUE                                                        
             WHEN 23                                                            
                MOVE   SPACES    TO WS-LETTER-TAG                               
                ADD      1       TO WS-NO-CONTACT-ADDR                          
                DISPLAY 'REC NOT FOUND IN BP13K830'                             
                      ', K830-NUM-REGN: ' K830-NUM-REGN                         
             WHEN OTHER                                                         
                DISPLAY 'BP13K830 READ ERROR,STATUS : ' BP13K830-STATUS         
                      ', K830-KEY-FLD : ' K830-NUM-REGN                         
                MOVE BP13K830-STATUS   TO RETURN-CODE                           
                PERFORM 9000-CLOSE-ROUTINE                                      
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       5200-GET-MAILING-ADDR.                                                   
      *-------------------------------------------------------------*           
           MOVE K830-NUM-MAIL-BLK(1:11) TO P000-NUM-BLOCK.                      
           MOVE K830-NUM-MAIL-FLAT      TO P000-HOUSE-LEVEL.                    
           MOVE K830-NME-MAIL-STR-BLDG  TO P000-STREET-NAME.                    
           IF K830-NUM-MAIL-POSTAL  =  SPACES OR ZEROS                          
              MOVE   SPACES   TO P000-NUM-POSTAL                                
           ELSE                                                                 
              MOVE   K830-NUM-MAIL-POSTAL  TO WS-POSTAL-CODE                    
              STRING 'SINGAPORE('  K830-NUM-MAIL-POSTAL  ')'                    
                      DELIMITED BY SIZE  INTO P000-NUM-POSTAL                   
           END-IF.                                                              
                                                                                
           STRING  K830-NUM-MAIL-BLK(1:11)  ' '                                 
                   K830-NUM-MAIL-FLAT       ' ' DELIMITED BY SIZE               
                   K830-NME-MAIL-STR-BLDG       DELIMITED BY '   '              
                   '  SINGAPORE('  K830-NUM-MAIL-POSTAL  ')'                    
                   DELIMITED BY SIZE  INTO L000-MAIL-ADDRESS.                   
                                                                                
       5200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------*           
       5400-GET-HOUSE-ADDR.                                                     
      *-------------------------------------------------------------*           
           MOVE K830-NUM-BLK(1:11)      TO P000-NUM-BLOCK.                      
           MOVE K830-NUM-FLAT           TO P000-HOUSE-LEVEL.                    
           MOVE K830-NME-STREET-BLDG    TO P000-STREET-NAME.                    
           IF K830-NUM-POSTAL  =  SPACES OR ZEROS                               
              MOVE   SPACES   TO P000-NUM-POSTAL                                
           ELSE                                                                 
              MOVE   K830-NUM-POSTAL       TO WS-POSTAL-CODE                    
              STRING 'SINGAPORE('  K830-NUM-POSTAL  ')'                         
                      DELIMITED BY SIZE  INTO P000-NUM-POSTAL                   
           END-IF.                                                              
                                                                                
           STRING  K830-NUM-BLK(1:11)  ' '                                      
                   K830-NUM-FLAT       ' ' DELIMITED BY SIZE                    
                   K830-NME-STREET-BLDG    DELIMITED BY '   '                   
                   '  SINGAPORE('  K830-NUM-POSTAL  ')'                         
                   DELIMITED BY SIZE  INTO L000-MAIL-ADDRESS.                   
                                                                                
       5400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------*           
       5800-GET-POSTAL-BARCODE.                                                 
      *-------------------------------------------------------------*           
           MOVE SPACES                 TO WS-LINK-AV02C015.                     
           MOVE WS-POSTAL-CODE         TO AV02C015-IN-POSTCODE.                 
           CALL 'AV02C015'   USING                                              
                             AV02C015-IN-POSTCODE                               
                             AV02C015-OUT-POSTCODE-CHKSUM.                      
           MOVE AV02C015-OUT-POSTCODE-CHKSUM   TO P000-BAR-CODE.                
                                                                                
       5800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       6000-GET-FLAT-ADDR.                                                      
      *-------------------------------------------------------------*           
           MOVE    SPACES             TO BP13COMM8-REC.                         
           MOVE F800-NUM-SCH-ACC(1:9) TO WS-LINK-REC.                           
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC, BP13COMM8-REC.                    
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
             WHEN 000                                                           
                STRING 'BLK '  COMM8-NUM-BLK  '  '                              
                       '#'  COMM8-NUM-LEVEL   '-'                               
                       COMM8-NUM-UNIT-MAIN    '  ' DELIMITED BY SIZE            
                       COMM8-NME-STREET       DELIMITED BY '   '                
                       INTO P000-UNIT-ADDRESS                                   
                STRING COMM8-NUM-BLK  '  '                                      
                       '#'  COMM8-NUM-LEVEL   '-'                               
                       COMM8-NUM-UNIT-MAIN    '  ' DELIMITED BY SIZE            
                       COMM8-NME-STREET       DELIMITED BY '   '                
                       INTO L000-FLAT-ADDRESS                                   
                MOVE COMM8-NUM-BLK        TO P000-P2-UNIT-BLK                   
                MOVE COMM8-NUM-LEVEL      TO P000-P2-UNIT-LVL-NO                
                MOVE COMM8-NUM-UNIT-MAIN  TO P000-P2-UNIT-UNIT-NO               
                STRING COMM8-NME-STREET(1:20) DELIMITED BY '   '                
                    ' S('  COMM8-NUM-POSTAL-CODE ')'                            
                    DELIMITED BY '   '                                          
                    INTO P000-P2-UNIT-STREET-POSTAL                             
                                                                                
                MOVE P000-P2-LINE01 TO P000-P3-DETAIL2                          
                                                                                
             WHEN 100                                                           
                MOVE  'Y'       TO WS-LETTER-TAG                                
                ADD     1       TO WS-PIDB-NOT-CASES                            
                DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE - '                     
                         F800-NUM-SCH-ACC ', REGN : ' F800-NUM-REGN             
             WHEN OTHER                                                         
                MOVE  SPACES    TO WS-LETTER-TAG                                
                ADD     1       TO WS-PIDB-NOT-CASES                            
                DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR              
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------*           
       6500-GET-OIC-DETAILS.                                                    
      *-------------------------------------------------------------*           
           MOVE F800-NUM-REGN         TO K022-NUM-REGN.                         
           PERFORM 6600-READ-BP13K022       THRU 6600-EXIT.                     
                                                                                
           IF BP13K022-STATUS  =  00                                            
              MOVE K022-NUM-OIC       TO K024-NUM-OIC                           
                                         L000-NUM-OIC                           
              PERFORM 6700-READ-BP13K024    THRU 6700-EXIT                      
                                                                                
              IF BP13K024-STATUS  =  00                                         
                 MOVE K024-NME-OIC     TO P000-NME-CSM                          
                 MOVE K024-NUM-DID     TO P000-TEL-CSM                          
                 MOVE K024-NUM-HP      TO P000-MOBILE-CSM                       
              END-IF                                                            
                                                                                
           END-IF.                                                              
                                                                                
       6500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       6600-READ-BP13K022.                                                      
      *-------------------------------------------------------------*           
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE BP13K022-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
                CONTINUE                                                        
             WHEN 23                                                            
                DISPLAY 'REC NOT FOUND IN BP13K022'                             
                      ', K022-NUM-REGN: ' K022-NUM-REGN                         
             WHEN OTHER                                                         
                DISPLAY 'BP13K022 READ ERROR,STATUS : ' BP13K022-STATUS         
                      ', K022-NUM-REGN: ' K022-NUM-REGN                         
                MOVE BP13K022-STATUS   TO RETURN-CODE                           
                PERFORM 9000-CLOSE-ROUTINE                                      
           END-EVALUATE.                                                        
                                                                                
       6600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       6700-READ-BP13K024.                                                      
      *-------------------------------------------------------------*           
           READ BP13K024.                                                       
                                                                                
           EVALUATE BP13K024-STATUS                                             
             WHEN 00                                                            
                CONTINUE                                                        
             WHEN 23                                                            
                DISPLAY 'REC NOT FOUND IN BP13K024'                             
                      ', K024-NUM-OIC: ' K024-NUM-OIC                           
             WHEN OTHER                                                         
                DISPLAY 'BP13K024 READ ERROR,STATUS : ' BP13K024-STATUS         
                      ', K024-NUM-OIC: ' K024-NUM-OIC                           
                MOVE BP13K024-STATUS   TO RETURN-CODE                           
                PERFORM 9000-CLOSE-ROUTINE                                      
           END-EVALUATE.                                                        
                                                                                
       6700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       6800-GET-DTE-14DAYS.                                                     
      *-------------------------------------------------------------*           
                                                                                
           COMPUTE WS-INTEGER-DATE =                                            
              FUNCTION INTEGER-OF-DATE(WS-CURR-DATE-NUM) + 14.                  
                                                                                
           COMPUTE WS-DATE-14AFTER =                                            
              FUNCTION DATE-OF-INTEGER(WS-INTEGER-DATE).                        
                                                                                
           MOVE SPACES TO WS-DATE-ALPHA.                                        
                                                                                
           MOVE WS-DATE-14AFTER(7:2) TO WS-DATE-DD.                             
           MOVE WS-DATE-14AFTER(1:4) TO WS-DATE-YYYY.                           
                                                                                
           EVALUATE WS-DATE-14AFTER(5:2)                                        
              WHEN 01                                                           
                 MOVE 'JAN'          TO WS-DATE-MM                              
              WHEN 02                                                           
                 MOVE 'FEB'          TO WS-DATE-MM                              
              WHEN 03                                                           
                 MOVE 'MAR'          TO WS-DATE-MM                              
              WHEN 04                                                           
                 MOVE 'APR'          TO WS-DATE-MM                              
              WHEN 05                                                           
                 MOVE 'MAY'          TO WS-DATE-MM                              
              WHEN 06                                                           
                 MOVE 'JUN'          TO WS-DATE-MM                              
              WHEN 07                                                           
                 MOVE 'JUL'          TO WS-DATE-MM                              
              WHEN 08                                                           
                 MOVE 'AUG'          TO WS-DATE-MM                              
              WHEN 09                                                           
                 MOVE 'SEP'          TO WS-DATE-MM                              
              WHEN 10                                                           
                 MOVE 'OCT'          TO WS-DATE-MM                              
              WHEN 11                                                           
                 MOVE 'NOV'          TO WS-DATE-MM                              
              WHEN 12                                                           
                 MOVE 'DEC'          TO WS-DATE-MM                              
           END-EVALUATE.                                                        
                                                                                
           STRING WS-DATE-ALPHA '.'                                             
              DELIMITED BY SIZE    INTO P000-DATE07.                            
           MOVE P000-DATE07          TO P000-DATE07B                            
                                        P000-P3-DATE.                           
                                                                                
       6800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       7000-PRINT-LETTER.                                                       
      *-------------------------------------------------------------*           
      *  COVER LETTER                                                           
      *-------------------------------------------------------------            
           MOVE      SPACES         TO BP13P000-REC.                            
           WRITE BP13P000-REC       AFTER PAGE.                                 
                                                                                
           WRITE BP13P000-REC       FROM P000-LINE01.                           
           WRITE BP13P000-REC       FROM P000-LINE02.                           
                                                                                
           WRITE BP13P000-REC       FROM P000-LINE03 AFTER 5.                   
           WRITE BP13P000-REC       FROM P000-LINE03B.                          
           WRITE BP13P000-REC       FROM P000-LINE04.                           
           WRITE BP13P000-REC       FROM P000-LINE04B.                          
           WRITE BP13P000-REC       FROM P000-LINE04C.                          
           WRITE BP13P000-REC       FROM P000-LINE05 AFTER 2.                   
                                                                                
           WRITE BP13P000-REC       FROM P000-LINE06 AFTER 6.                   
           WRITE BP13P000-REC       FROM P000-LINE07 AFTER 18.                  
           WRITE BP13P000-REC       FROM P000-LINE07B AFTER 9.                  
           WRITE BP13P000-REC       FROM P000-LINE08 AFTER 2.                   
           WRITE BP13P000-REC       FROM P000-LINE09.                           
           WRITE BP13P000-REC       FROM P000-LINE10 AFTER 7.                   
                                                                                
                                                                                
      *-------------------------------------------------------------            
      *  WARRANT TO ACT                                                         
      *-------------------------------------------------------------            
                                                                                
           MOVE  SPACES             TO BP13P000-REC.                            
                                                                                
           WRITE BP13P000-REC       AFTER PAGE.                                 
           WRITE BP13P000-REC       FROM P000-LINE01    AFTER 2.                
           WRITE BP13P000-REC       FROM P000-P2-LINE01 AFTER 13.               
           WRITE BP13P000-REC       FROM P000-P2-LINE02 AFTER 4.                
           WRITE BP13P000-REC       FROM P000-P2-LINE03.                        
           WRITE BP13P000-REC       FROM P000-P2-LINE04.                        
           WRITE BP13P000-REC       FROM P000-P2-LINE05.                        
           WRITE BP13P000-REC       FROM P000-P2-LINE06 AFTER 2.                
           WRITE BP13P000-REC       FROM P000-P2-LINE07.                        
           WRITE BP13P000-REC       FROM P000-P2-LINE08.                        
                                                                                
           WRITE BP13P000-REC       FROM P000-P2-LINE09 AFTER 19.               
           WRITE BP13P000-REC       FROM P000-P2-LINE10.                        
           WRITE BP13P000-REC       FROM P000-P2-LINE11.                        
           WRITE BP13P000-REC       FROM P000-P2-LINE12 AFTER 4.                
           WRITE BP13P000-REC       FROM P000-P2-LINE13.                        
           WRITE BP13P000-REC       FROM P000-P2-LINE14.                        
                                                                                
      *-------------------------------------------------------------            
      *  NOTIFICATION FORM                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE  SPACES             TO BP13P000-REC.                            
                                                                                
           WRITE BP13P000-REC       AFTER PAGE.                                 
           WRITE BP13P000-REC       FROM P000-P3-LINE01 AFTER 12.               
           WRITE BP13P000-REC       FROM P000-P3-LINE02 AFTER 6.                
           WRITE BP13P000-REC       FROM P000-P3-LINE03.                        
                                                                                
           WRITE BP13P000-REC       FROM P000-P2-LINE09 AFTER 20.               
           WRITE BP13P000-REC       FROM P000-P2-LINE10.                        
           WRITE BP13P000-REC       FROM P000-P2-LINE11.                        
           WRITE BP13P000-REC       FROM P000-P2-LINE12 AFTER 4.                
           WRITE BP13P000-REC       FROM P000-P2-LINE13.                        
           WRITE BP13P000-REC       FROM P000-P2-LINE14.                        
                                                                                
           ADD 1 TO WS-LETTER-PRINTED.                                          
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------*           
       8000-PRINT-LISTING.                                                      
      *-------------------------------------------------------------*           
           IF WS-LINE-CNT  >  WS-MAX-LINES                                      
              PERFORM 8500-PRINT-HEADER  THRU 8500-EXIT                         
           END-IF.                                                              
                                                                                
           ADD     1        TO WS-SLNO.                                         
           MOVE  WS-SLNO    TO L000-SLNO.                                       
                                                                                
           MOVE F800-NUM-LOAN-TAG     TO L000-NUM-LOAN-TAG.                     
           MOVE F800-AMT-BK-PAID      TO L000-AMT-BK-FEE-PAID.                  
           MOVE F800-NUM-PROV-TAG     TO L000-NUM-PROV-TAG.                     
           MOVE F800-NUM-PROV-REASON  TO L000-NUM-PROV-REASON.                  
                                                                                
           WRITE BP13L000-REC       FROM L000-DETAIL-1 AFTER 2.                 
           ADD   2          TO WS-LINE-CNT.                                     
                                                                                
           IF L000-NUM-NRIC2  NOT =  SPACES                                     
              WRITE BP13L000-REC    FROM L000-DETAIL-2                          
              ADD  1  TO WS-LINE-CNT                                            
           END-IF.                                                              
                                                                                
           IF L000-NUM-NRIC3  NOT =  SPACES                                     
              WRITE BP13L000-REC    FROM L000-DETAIL-3                          
              ADD  1  TO WS-LINE-CNT                                            
           END-IF.                                                              
                                                                                
           IF L000-NUM-NRIC4  NOT =  SPACES                                     
              WRITE BP13L000-REC    FROM L000-DETAIL-4                          
              ADD  1  TO WS-LINE-CNT                                            
           END-IF.                                                              
                                                                                
           WRITE BP13L000-REC       FROM L000-DETAIL-5.                         
           WRITE BP13L000-REC       FROM L000-DETAIL-6.                         
           ADD   2    TO WS-LINE-CNT.                                           
                                                                                
       8000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------*           
       8500-PRINT-HEADER.                                                       
      *-------------------------------------------------------------*           
           ADD      1                 TO WS-PAGE-CNT.                           
           MOVE    WS-PAGE-CNT        TO L000-PAGE.                             
                                                                                
           WRITE BP13L000-REC       FROM L000-HEADER-1 AFTER PAGE.              
           WRITE BP13L000-REC       FROM L000-HEADER-2.                         
           WRITE BP13L000-REC       FROM L000-HEADER-3 AFTER 2.                 
           WRITE BP13L000-REC       FROM L000-HEADER-4.                         
                                                                                
           MOVE  5          TO WS-LINE-CNT.                                     
                                                                                
       8500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------*           
       8800-PRINT-CONTROL.                                                      
      *-------------------------------------------------------------*           
           IF WS-LINE-CNT  >  (WS-MAX-LINES - 9)                                
              ADD      1                 TO WS-PAGE-CNT                         
              MOVE    WS-PAGE-CNT        TO L000-PAGE                           
              WRITE BP13L000-REC       FROM L000-HEADER-1 AFTER PAGE            
              WRITE BP13L000-REC       FROM L000-HEADER-2                       
              WRITE BP13L000-REC       FROM L000-HEADER-4 AFTER 2               
           END-IF.                                                              
                                                                                
           MOVE WS-F800-READ-CNT      TO L000-TOTAL-CASES.                      
           MOVE WS-PIDB-NOT-CASES     TO L000-PIDB-NOT-CASES.                   
           MOVE WS-NO-CONTACT-ADDR    TO L000-NO-CONTACT-ADDR.                  
           MOVE WS-LETTER-PRINTED     TO L000-TOTAL-PRINTED.                    
           WRITE BP13L000-REC       FROM L000-FOOTER-1 AFTER 3.                 
           WRITE BP13L000-REC       FROM L000-FOOTER-2.                         
           WRITE BP13L000-REC       FROM L000-FOOTER-3.                         
           WRITE BP13L000-REC       FROM L000-FOOTER-4.                         
                                                                                
       8800-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------*           
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------*           
            CLOSE  BP13F800  BP13K820  BP13K830                                 
                   BP13K022  BP13K024                                           
                   BP13P000  BP13L000.                                          
                                                                                
           IF BP13K820-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'BP13K820 CLOSE ERROR, STATUS ' BP13K820-STATUS           
              MOVE     BP13K820-STATUS  TO RETURN-CODE                          
           END-IF.                                                              
                                                                                
           IF BP13K830-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'BP13K830 CLOSE ERROR, STATUS ' BP13K830-STATUS           
              MOVE     BP13K830-STATUS  TO RETURN-CODE                          
           END-IF.                                                              
                                                                                
           IF BP13K022-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'BP13K022 CLOSE ERROR, STATUS ' BP13K022-STATUS           
              MOVE     BP13K022-STATUS  TO RETURN-CODE                          
           END-IF.                                                              
                                                                                
           IF BP13K024-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'BP13K024 CLOSE ERROR, STATUS ' BP13K024-STATUS           
              MOVE     BP13K024-STATUS  TO RETURN-CODE                          
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
