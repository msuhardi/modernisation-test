       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF26.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  18/12/2013.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT  (BP13)                               *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE    :  FACILITATE PIGEONHOLING OF JB FOR CHILD    *            
      *                 UNITS, ADD MAXIMUM ALLOWABLE PA1, PA2, PA3 *            
      *                 TO MAXIMUM ALLOWABLE JB1, JB2, JB3 SO THAT *            
      *                 JB CAN TAKE UP THE ETHNIC QUOTA AVAILABLE  *            
      *                 FOR PA SCHEME                              *            
      *                                                            *            
      * I-O FILE     :  BP13K010  BP13.K010.TENDER                 *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP135170  SMR2  18122013  SIMILAR TO BP13C750              *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER.     IBM-3090.                                           
       OBJECT-COMPUTER.     IBM-3090.                                           
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K010 ASSIGN TO BP13K010                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS SEQUENTIAL                                 
                  RECORD KEY      IS K010-KEY-FLD                               
                  FILE STATUS     IS WS-K010-STATUS.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13K010.                                                           
       COPY BP13K010.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
        01  WS-VARIABLES.                                                       
            05 WS-CNT-K010-READ            PIC 9(08) VALUE 0.                   
            05 WS-CNT-K010-REWRITE         PIC 9(08) VALUE 0.                   
            05 WS-K010-EOF                 PIC X(01) VALUE 'N'.                 
               88 K010-EOF                     VALUE 'Y'.                       
            05 WS-K010-STATUS              PIC 9(02) VALUE 0.                   
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************                                                        
       0000-CONTROL-RTN.                                                        
      ******************                                                        
                                                                                
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 1500-START-BP13K010-KEY THRU 1500-EXIT.              01790000
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
             UNTIL K010-EOF.                                                    
                                                                                
           PERFORM 9999-CLOSE-FILES        THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************                                                         
       1000-OPEN-FILES.                                                         
      *****************                                                         
                                                                                
           OPEN I-O    BP13K010.                                                
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K010, STATUS ' WS-K010-STATUS          
              MOVE WS-K010-STATUS        TO RETURN-CODE                         
              PERFORM 9999-CLOSE-FILES   THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                        01780000
      *************************                                                 
       1500-START-BP13K010-KEY.                                         01790000
      *************************                                                 
                                                                        01780000
           MOVE SPACES TO K010-KEY-FLD                                  01800000
                                                                        01780000
           START BP13K010 KEY > K010-KEY-FLD                            01810000
                                                                                
           IF WS-K010-STATUS  = 00                                              
              PERFORM 2000-READ-BP13K010    THRU 2000-EXIT                      
           ELSE                                                                 
              DISPLAY 'START READ BP13K010, STATUS ' WS-K010-STATUS             
              MOVE WS-K010-STATUS        TO RETURN-CODE                         
              PERFORM 9999-CLOSE-FILES   THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
       1500-EXIT.                                                               
            EXIT.                                                               
                                                                        01860000
      ********************                                                      
       2000-READ-BP13K010.                                                      
      ********************                                                      
                                                                                
           READ BP13K010 NEXT AT END                                            
                MOVE 'Y' TO WS-K010-EOF                                         
                GO TO 2000-EXIT                                                 
                END-READ.                                                       
                                                                                
           IF WS-K010-STATUS NOT = 00                                           
              DISPLAY 'ERROR READING BP13K010, STATUS ' WS-K010-STATUS          
              MOVE WS-K010-STATUS        TO RETURN-CODE                         
              PERFORM 9999-CLOSE-FILES   THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
           ADD 1                         TO WS-CNT-K010-READ.                   
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *******************                                                       
       3000-MAIN-ROUTINE.                                                       
      *******************                                                       
                                                                                
           PERFORM 4000-ADD-ROUTINE   THRU 4000-EXIT.                           
                                                                                
           REWRITE BP13K010-TENDER.                                             
                                                                                
           IF WS-K010-STATUS = 00                                               
              ADD 1                   TO WS-CNT-K010-REWRITE                    
           ELSE                                                                 
              DISPLAY 'ERROR REWRITING BP13K010, STATUS '                       
                                       WS-K010-STATUS                           
              DISPLAY '                          KEY IS '                       
                                       K010-KEY-FLD                             
              MOVE WS-K010-STATUS        TO RETURN-CODE                         
              PERFORM 9999-CLOSE-FILES   THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13K010    THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************                                                        
       4000-ADD-ROUTINE.                                                        
      ******************                                                        
                                                                                
           IF K010-NUM-TOT-PA NOT NUMERIC                                       
              MOVE ZEROS TO K010-NUM-TOT-PA                                     
           END-IF.                                                              
                                                                                
           IF K010-NUM-TOT-JB NOT NUMERIC                                       
              MOVE ZEROS TO K010-NUM-TOT-JB                                     
           END-IF.                                                              
                                                                                
           ADD K010-NUM-TOT-PA TO K010-NUM-TOT-JB.                              
                                                                                
           IF K010-NUM-MAX-PA1 NOT NUMERIC                                      
              MOVE ZEROS TO K010-NUM-MAX-PA1                                    
           END-IF.                                                              
                                                                                
           IF K010-NUM-MAX-JB1 NOT NUMERIC                                      
              MOVE ZEROS TO K010-NUM-MAX-JB1                                    
           END-IF.                                                              
                                                                                
           ADD K010-NUM-MAX-PA1 TO K010-NUM-MAX-JB1.                            
                                                                                
           IF K010-NUM-MAX-PA2 NOT NUMERIC                                      
              MOVE ZEROS TO K010-NUM-MAX-PA2                                    
           END-IF.                                                              
                                                                                
           IF K010-NUM-MAX-JB2 NOT NUMERIC                                      
              MOVE ZEROS TO K010-NUM-MAX-JB2                                    
           END-IF.                                                              
                                                                                
           ADD K010-NUM-MAX-PA2 TO K010-NUM-MAX-JB2.                            
                                                                                
           IF K010-NUM-MAX-PA3 NOT NUMERIC                                      
              MOVE ZEROS TO K010-NUM-MAX-PA3                                    
           END-IF.                                                              
                                                                                
           IF K010-NUM-MAX-JB3 NOT NUMERIC                                      
              MOVE ZEROS TO K010-NUM-MAX-JB3                                    
           END-IF.                                                              
                                                                                
           ADD K010-NUM-MAX-PA3 TO K010-NUM-MAX-JB3.                            
                                                                                
           MOVE ZEROES TO K010-NUM-TOT-PA                                       
                          K010-NUM-MAX-PA1                                      
                          K010-NUM-MAX-PA2                                      
                          K010-NUM-MAX-PA3.                                     
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       9999-CLOSE-FILES.                                                        
      ******************                                                        
                                                                                
           DISPLAY '>>>>>>>>>>>>>>>> BP13CF26 <<<<<<<<<<<<<<<<<<'.              
           DISPLAY '- ADD MAX-CAT QUOTA FROM PA TO JB - '.                      
           DISPLAY '  '.                                                        
           DISPLAY 'K010 RECORDS READ    : '  WS-CNT-K010-READ.                 
           DISPLAY 'K010 RECORDS REWRITE : '  WS-CNT-K010-REWRITE.              
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13K010.                                                      
                                                                                
           IF WS-K010-STATUS NOT = 00                                           
              DISPLAY 'ERROR CLOSING BP13K010, STATUS ' WS-K010-STATUS          
              MOVE WS-K010-STATUS       TO RETURN-CODE                          
           END-IF.                                                              
                                                                                
       9999-EXIT.                                                               
           STOP RUN.                                                            
                                                                                
