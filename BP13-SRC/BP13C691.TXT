       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C691.                                                 
      *AUTHOR.        NOR.                                                      
      *DATE-WRITTEN.  12/11/1997.                                               
      * =========================================================== *           
      *                   SYSTEM OF COMMITMENT (SOC)                *           
      * =========================================================== *           
      *  OBJECTIVES : 1. TO CHECK THE MOVE-IN-DATE OF RELATED       *           
      *                  PARENT, CHILD, IN-LAW                      *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG REF  DATE     BY   DESCRIPTION                          *           
      * -------- -------- ---- -----------                          *           
      * BP130397 19971112 NOR  NEW PRGM                             *           
      * BP130414 19971126 YCH  - PARENT/CHILD CHANGES               *           
      * BP130445 19980205 SSS  EXPAND F558 RECL FROM 150 TO 400     *           
      * BP130435 19980610 YCH  CHK MOVE IN DTE OF IN-LAW AND CHILD  *           
      * BP130630 19990114 NOR  CHGE F715 TO F716 EXPAND TO 350      *           
      * BP130833 20001006 YCH  CHECK HH-TYPE BASED ON EARLIEST      *           
      *                        MOVEIN-DATE                          *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
                                                                                
           SELECT BP13F716 ASSIGN TO BP13F716.                                  
                                                                                
                                                                                
           SELECT P13F558O ASSIGN TO P13F558O.                                  
           SELECT BP13L691 ASSIGN TO BP13L691.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F558     BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 600 CHARACTERS                          
                        LABEL RECORD IS STANDARD                                
                        RECORDING MODE IS F.                                    
       COPY BP13F558.                                                           
                                                                                
       FD  BP13F716     BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 350 CHARACTERS                          
                        LABEL RECORD IS STANDARD                                
                        RECORDING MODE IS F.                                    
       COPY BP13F716.                                                           
                                                                                
       FD  P13F558O     BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 600 CHARACTERS                          
                        LABEL RECORD IS OMITTED                                 
                        RECORDING MODE IS F.                                    
       01  P13F558O-REC            PIC X(600).                                  
                                                                                
       FD  BP13L691     RECORD CONTAINS 133 CHARACTERS                          
                        LABEL RECORD IS OMITTED.                                
                                                                                
       01  BP13L691-REC            PIC X(133).                                  
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FILE-FLAGS.                                                       
           05  WS-F558-EOF         PIC X         VALUE 'N'.                     
           05  WS-F716-EOF         PIC X         VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F558-CNT-READ    PIC 9(8)      VALUE ZEROS.                   
           05  WS-F716-CNT-READ    PIC 9(8)      VALUE ZEROS.                   
           05  WS-F716A-CNT-READ   PIC 9(8)      VALUE ZEROS.                   
           05  WS-F558O-CNT-WRT    PIC 9(8)      VALUE ZEROS.                   
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-DTE-CURRENT.                                                  
               10  WS-DTE-YY       PIC X(4).                                    
               10  WS-DTE-MM       PIC X(2).                                    
               10  WS-DTE-DD       PIC X(2).                                    
           05  WS-TME-SYSTEM.                                                   
               10  WS-TME-HH       PIC X(2).                                    
               10  WS-TME-MM       PIC X(2).                                    
               10  WS-TME-SS       PIC X(2).                                    
               10  WS-TME-HHSS     PIC X(2).                                    
           05  FILLER              PIC X(5).                                    
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-LINE             PIC 9(2)     VALUE 60.                       
           05  WS-PAGE             PIC 9(2)     VALUE 0.                        
           05  WS-SNO              PIC 9(4)     VALUE 0.                        
           05  WS-CTR              PIC 9(2)     VALUE ZEROES.                   
           05  WS-CTR2             PIC 9(2)     VALUE ZEROES.                   
           05  WS-NUM-REGN         PIC X(8)     VALUE SPACES.                   
           05  WS-OCCUPIER         PIC 9(2)     VALUE ZEROES.                   
           05  WS-APPLNT           PIC 9(2)     VALUE ZEROES.                   
           05  WS-DTE-PARENT-IN    PIC X(8)     VALUE SPACES.                   
           05  WS-DTE-CHILD-IN     PIC X(8)     VALUE SPACES.                   
           05  WS-DTE-CHILD-IN2    PIC X(8)     VALUE SPACES.                   
           05  WS-DTE-LAW-IN       PIC X(8)     VALUE SPACES.                   
           05  WS-DTE-SPOUSE-IN    PIC X(8)     VALUE SPACES.                   
           05  WS-PC-TAG           PIC X(1)     VALUE SPACES.                   
           05  WS-CHILD1-NRIC      PIC X(9)     VALUE SPACES.                   
                                                                                
       COPY WSF716.                                                             
       COPY WSF558.                                                             
                                                                                
       01  L691-HDG1.                                                           
           05  PRT-REPORT-TITLE         PIC X(9)  VALUE 'BP13L691'.             
           05  FILLER                   PIC X(38) VALUE SPACES.                 
           05  FILLER                   PIC X(40) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(26) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE :'.               
           05  PRT-DATE                 PIC X(10).                              
                                                                                
       01  L691-HDG2.                                                           
           05  FILLER                   PIC X(45) VALUE SPACES.                 
           05  FILLER                   PIC X(43) VALUE                         
               'LIST OF UNDETERMINE PARENT CHILD HOUSEHOLD '.                   
           05  FILLER                   PIC X(29) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'PAGE : '.              
           05  PRT-PAGE                 PIC ZZZ9.                               
                                                                                
       01  L691-HDG3.                                                           
           05  FILLER                   PIC X(48) VALUE SPACES.                 
           05  FILLER                   PIC X(36) VALUE                         
               '    SYSTEM WILL TAG AS UPGRADER     '.                          
                                                                                
       01  L691-HDG4.                                                           
           05  FILLER                   PIC X(03) VALUE   SPACES.               
           05  FILLER                   PIC X(10) VALUE                         
               'S/NO  '.                                                        
           05  FILLER                   PIC X(38) VALUE                         
               'REGNO         NRIC       P/C-TAG      '.                        
           05  FILLER                   PIC X(29) VALUE                         
               'SCH/ACC       DATE-IN  '.                                       
           05  FILLER                   PIC X(53) VALUE                         
               'STATUS   RELN   REMARK '.                                       
                                                                                
      *----------------------------------------------------------------*        
      *   DETAIL RECORD                                                *        
      *----------------------------------------------------------------*        
       01  L691-DTL.                                                            
           05  FILLER                   PIC X(03) VALUE SPACES.                 
           05  PRT-SERNO                PIC ZZZ9.                               
           05  PRT-SNO REDEFINES PRT-SERNO PIC X(4).                            
           05  FILLER                   PIC X(6)  VALUE SPACES.                 
           05  PRT-REGNO                PIC X(08) VALUE SPACES.                 
           05  FILLER                   PIC X(6)  VALUE SPACES.                 
           05  PRT-NRIC                 PIC X(09) VALUE SPACES.                 
           05  FILLER                   PIC X(6)  VALUE SPACES.                 
           05  PRT-PC-TAG               PIC X     VALUE SPACES.                 
           05  FILLER                   PIC X(6)  VALUE SPACES.                 
           05  PRT-SCH-ACC              PIC X(9)  VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE SPACES.                 
           05  PRT-MOVE-IN              PIC X(8)  VALUE SPACES.                 
           05  FILLER                   PIC X(09) VALUE SPACES.                 
           05  PRT-STATUS               PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(6)  VALUE SPACES.                 
           05  PRT-RELATIONSHIP         PIC X(2).                               
           05  FILLER                   PIC X(4)  VALUE SPACES.                 
           05  PRT-REMARK               PIC X(20) VALUE SPACES.                 
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13F558   THRU 2000-EXIT.                         
           PERFORM 3000-READ-BP13F716   THRU 3000-EXIT.                         
           PERFORM 5000-MAIN-ROUTINE    THRU 5000-EXIT                          
                   UNTIL F558-NUM-REGN = HIGH-VALUE.                            
           PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
      ** BP13F558 -> RECORDS TAGGED AS 'H'                                      
      ** BP13F716 -> RDB CURRENT RECORDS WITH PARENT-CHILD TAG                  
                                                                                
           OPEN INPUT  BP13F558                                                 
                       BP13F716                                                 
                OUTPUT BP13L691                                                 
                       P13F558O.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE                         
                                                                                
           STRING WS-DTE-DD '/' WS-DTE-MM '/' WS-DTE-YY                         
                DELIMITED BY SIZE INTO PRT-DATE.                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-READ-BP13F558.                                                      
      *****************************************************************         
                                                                                
           READ BP13F558 AT END                                                 
                        MOVE 'Y' TO WS-F558-EOF                                 
                        MOVE HIGH-VALUE TO F558-NUM-REGN                        
                        GO TO 2000-EXIT.                                        
                                                                                
           ADD 1 TO WS-F558-CNT-READ.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-READ-BP13F716.                                                      
      *****************************************************************         
                                                                                
           READ BP13F716 AT END                                                 
                        MOVE 'Y' TO WS-F716-EOF                                 
                        MOVE HIGH-VALUE TO F716-NUM-REGN                        
                        GO TO 3000-EXIT.                                        
                                                                                
           ADD 1 TO WS-F716-CNT-READ.                                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       5000-MAIN-ROUTINE.                                                       
      *****************************************************************         
                                                                                
           IF F558-NUM-REGN = F716-NUM-REGN                                     
                                                                                
              PERFORM 6010-INITIALIZE    THRU 6010-EXIT                         
                                                                                
                                                                                
              MOVE ZEROS                 TO WS-OCCUPIER                         
                                            WS-APPLNT                           
              MOVE SPACES                TO WS-PC-TAG                           
              MOVE F558-NUM-REGN         TO WS-NUM-REGN                         
                                                                                
              PERFORM 5200-STORE-F558    THRU 5200-EXIT                         
                UNTIL F558-NUM-REGN NOT = WS-NUM-REGN                           
                                                                                
              PERFORM 5210-STORE-F716    THRU 5210-EXIT                         
                UNTIL F716-NUM-REGN NOT = WS-NUM-REGN                           
                                                                                
              PERFORM 6000-MATCH-SCHACC   THRU 6000-EXIT                        
                                                                                
              PERFORM 6300-CHECK-OUTPUT   THRU 6300-EXIT                        
                                                                                
           ELSE                                                                 
              IF F558-NUM-REGN > F716-NUM-REGN                                  
                 PERFORM 3000-READ-BP13F716           THRU 3000-EXIT            
              ELSE                                                              
                 IF F558-NUM-REGN < F716-NUM-REGN                               
                    MOVE F558-NUM-REGN TO WS-NUM-REGN                           
                    MOVE 'H'              TO   F558-CDE-HOUSEHOLD               
                    WRITE P13F558O-REC    FROM BP13F558-REC                     
                    ADD 1 TO WS-F558O-CNT-WRT                                   
                    PERFORM 2000-READ-BP13F558 THRU 2000-EXIT                   
                     UNTIL  F558-NUM-REGN NOT = WS-NUM-REGN                     
                       OR   WS-F558-EOF = 'Y'                                   
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       5200-STORE-F558.                                                         
      *****************************************************************         
                                                                                
           ADD 1                     TO WS-APPLNT                               
           MOVE BP13F558-REC         TO WS-F558-REC (WS-APPLNT)                 
           PERFORM 2000-READ-BP13F558          THRU 2000-EXIT.                  
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       5210-STORE-F716.                                                         
      *****************************************************************         
                                                                                
           ADD 1                       TO WS-OCCUPIER                           
                                                                                
           MOVE BP13F716-REC           TO WS-F716-REC (WS-OCCUPIER)             
           PERFORM 3000-READ-BP13F716  THRU 3000-EXIT.                          
                                                                                
       5210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       6000-MATCH-SCHACC.                                                       
      *****************************************************************         
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > WS-APPLNT          
                                                                                
             PERFORM 6010-INITIALIZE   THRU 6010-EXIT                           
                                                                                
             PERFORM 6100-CHECK-F716   THRU 6100-EXIT                           
               VARYING WS-CTR2 FROM 1 BY 1 UNTIL WS-CTR2 > WS-OCCUPIER          
                                                                                
             PERFORM 6200-CHECK-DATES  THRU 6200-EXIT                           
                                                                                
           END-PERFORM.                                                         
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       6010-INITIALIZE.                                                         
      *****************************************************************         
           MOVE SPACES             TO WS-DTE-PARENT-IN                          
                                      WS-DTE-CHILD-IN                           
                                      WS-DTE-CHILD-IN2                          
                                      WS-DTE-SPOUSE-IN                          
                                      WS-DTE-LAW-IN                             
                                      WS-CHILD1-NRIC                            
                                      PRT-REMARK.                               
       6010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       6100-CHECK-F716.                                                         
      *****************************************************************         
                                                                                
           IF WS-F558-NUM-HDB-REF-SMS(WS-CTR)                                   
              NOT = WS-F716-NUM-HDB-REF(WS-CTR2)                                
              GO TO 6100-EXIT                                                   
           END-IF                                                               
                                                                                
           EVALUATE WS-F716-CDE-PARENT-CHILD(WS-CTR2)                           
             WHEN 'P'                                                           
               IF WS-DTE-PARENT-IN < WS-F716-DTE-MOVING-IN(WS-CTR2)             
                  MOVE WS-F716-DTE-MOVING-IN(WS-CTR2)                           
                                                 TO WS-DTE-PARENT-IN            
               END-IF                                                           
                                                                                
             WHEN 'C'                                                           
               IF WS-CHILD1-NRIC = SPACES                                       
                  MOVE WS-F716-NUM-NRIC(WS-CTR2) TO WS-CHILD1-NRIC              
               END-IF                                                           
                                                                                
               IF WS-CHILD1-NRIC = WS-F716-NUM-NRIC(WS-CTR2)                    
                 IF WS-DTE-CHILD-IN  < WS-F716-DTE-MOVING-IN(WS-CTR2)           
                    MOVE WS-F716-DTE-MOVING-IN(WS-CTR2)                         
                                                 TO WS-DTE-CHILD-IN             
                 END-IF                                                         
               ELSE                                                             
                 IF WS-DTE-CHILD-IN2 < WS-F716-DTE-MOVING-IN(WS-CTR2)           
                    MOVE WS-F716-DTE-MOVING-IN (WS-CTR2)                        
                                                 TO WS-DTE-CHILD-IN2            
                 END-IF                                                         
               END-IF                                                           
                                                                                
             WHEN 'L'                                                           
               IF WS-DTE-LAW-IN  < WS-F716-DTE-MOVING-IN(WS-CTR2)               
                  MOVE WS-F716-DTE-MOVING-IN (WS-CTR2)                          
                                                TO WS-DTE-LAW-IN                
               END-IF                                                           
             WHEN 'S'                                                           
               IF WS-DTE-SPOUSE-IN < WS-F716-DTE-MOVING-IN(WS-CTR2)             
                  MOVE WS-F716-DTE-MOVING-IN (WS-CTR2)                          
                                                TO WS-DTE-SPOUSE-IN             
               END-IF                                                           
           END-EVALUATE.                                                        
       6100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       6200-CHECK-DATES.                                                        
      *****************************************************************         
      * TAG AS FIRST-TIMER                                                      
      * IF EARLIEST ROLE-DATE OF CHILD <= IN-LAW/SPOUSE                         
                                                                                
            IF WS-PC-TAG = 'N'                                                  
               GO TO 6200-EXIT                                                  
            END-IF.                                                             
                                                                                
            IF WS-DTE-SPOUSE-IN NOT = SPACES                                    
               IF (WS-DTE-SPOUSE-IN(1:6) <= WS-DTE-CHILD-IN (1:6)) AND          
                   WS-DTE-CHILD-IN NOT = SPACES                                 
                   MOVE 'N'                   TO WS-PC-TAG                      
                   GO TO 6200-EXIT                                              
               END-IF                                                           
               IF (WS-DTE-SPOUSE-IN(1:6) <= WS-DTE-CHILD-IN2(1:6)) AND          
                   WS-DTE-CHILD-IN2 NOT = SPACES                                
                   MOVE 'N'                   TO WS-PC-TAG                      
                   GO TO 6200-EXIT                                              
               END-IF                                                           
            END-IF.                                                             
                                                                                
            IF WS-DTE-LAW-IN NOT = SPACES                                       
               IF (WS-DTE-LAW-IN(1:6) <= WS-DTE-CHILD-IN(1:6)) AND              
                  WS-DTE-CHILD-IN NOT = SPACES                                  
                  MOVE 'IN-LAW SAME AS CHILD' TO PRT-REMARK                     
                  MOVE 'N'                    TO WS-PC-TAG                      
                  GO TO 6200-EXIT                                               
               END-IF                                                           
                                                                                
               IF (WS-DTE-LAW-IN(1:6) <= WS-DTE-CHILD-IN2(1:6)) AND             
                  WS-DTE-CHILD-IN2 NOT = SPACES                                 
                  MOVE 'IN-LAW SAME AS CHILD' TO PRT-REMARK                     
                  MOVE 'N'                    TO WS-PC-TAG                      
                  GO TO 6200-EXIT                                               
               END-IF                                                           
            END-IF.                                                             
                                                                                
            MOVE 'Y'                          TO WS-PC-TAG.                     
                                                                                
       6200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       6300-CHECK-OUTPUT.                                                       
      *****************************************************************         
                                                                                
            IF WS-PC-TAG = 'N'                                                  
               ADD 1                         TO WS-SNO                          
               MOVE WS-SNO                   TO PRT-SERNO                       
               MOVE WS-NUM-REGN              TO PRT-REGNO                       
                                                                                
               PERFORM 6400-PRINT-HSEHOLD    THRU 6400-EXIT                     
               VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > WS-OCCUPIER            
            ELSE                                                                
              IF WS-PC-TAG = 'Y'                                                
                MOVE 'H'              TO   WS-F558-CDE-HOUSEHOLD(1)             
                WRITE P13F558O-REC    FROM WS-F558-REC(1)                       
                ADD 1 TO WS-F558O-CNT-WRT                                       
              END-IF                                                            
            END-IF.                                                             
                                                                                
       6300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       6400-PRINT-HSEHOLD.                                                      
      *****************************************************************         
           IF WS-LINE > 50                                                      
              PERFORM 6410-PRINT-HDG THRU 6410-EXIT                             
           END-IF.                                                              
                                                                                
           IF PRT-SNO NOT = SPACES                                              
              MOVE SPACES TO BP13L691-REC                                       
              ADD 1 TO WS-LINE                                                  
              WRITE BP13L691-REC AFTER 1.                                       
                                                                                
           MOVE WS-F716-NUM-NRIC (WS-CTR)         TO PRT-NRIC.                  
           MOVE WS-F716-CDE-PARENT-CHILD (WS-CTR) TO PRT-PC-TAG.                
           MOVE WS-F716-NUM-SCH-ACCNT (WS-CTR)    TO PRT-SCH-ACC.               
           MOVE WS-F716-DTE-MOVING-IN (WS-CTR)    TO PRT-MOVE-IN.               
           MOVE WS-F716-CDE-OCCUPIER (WS-CTR)     TO PRT-STATUS.                
           MOVE WS-F716-CDE-RELATIONSHIP (WS-CTR) TO PRT-RELATIONSHIP.          
                                                                                
           WRITE BP13L691-REC FROM L691-DTL.                                    
           MOVE SPACES TO PRT-SNO PRT-REMARK.                                   
           MOVE SPACES TO PRT-REGNO.                                            
                                                                                
           ADD 1 TO WS-LINE.                                                    
                                                                                
       6400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       6410-PRINT-HDG.                                                          
      *****************************************************************         
           ADD 1 TO WS-PAGE.                                                    
           MOVE WS-PAGE   TO PRT-PAGE.                                          
                                                                                
           WRITE BP13L691-REC FROM L691-HDG1 AFTER PAGE.                        
           WRITE BP13L691-REC FROM L691-HDG2 AFTER 1.                           
           WRITE BP13L691-REC FROM L691-HDG3 AFTER 1.                           
           WRITE BP13L691-REC FROM L691-HDG4 AFTER 2.                           
           MOVE 6 TO WS-LINE.                                                   
                                                                                
       6410-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '***************  BP13C691  ******************'.             
           DISPLAY 'TOTAL  F558 REC READ     = ' WS-F558-CNT-READ.              
           DISPLAY 'TOTAL  F716 REC READ     = ' WS-F716-CNT-READ.              
           DISPLAY 'TOTAL F558O REC WRITTEN  = ' WS-F558O-CNT-WRT.              
                                                                                
           CLOSE      BP13F558                                                  
                      BP13F716                                                  
                      BP13L691                                                  
                      P13F558O.                                                 
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
