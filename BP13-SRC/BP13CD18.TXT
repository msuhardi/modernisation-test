       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD18.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  08/03/2016.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE    : GENERATE REPORT ON BTO/SBF SITE INFORMATION *            
      *                                                            *            
      * INPUT  FILES : BP13F813 - CASES PER REGN                   *            
      * OUTPUT FILE  : BP13LD18 - GENERATED REPORT                 *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP136134  SMR2  08032016  NEW PROGRAM                      *            
      * BP136588  SMR2  08022017  CHANGE QUEUE MID & END           *            
      * BP136880  SMR2  19072017  CHANGE QUEUE RANGE FOR BTO & SBF *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F813 ASSIGN       TO BP13F813.                            
                                                                                
           SELECT BP13LD18 ASSIGN       TO BP13LD18.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F813                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS 1000 CHARACTERS                                     
            RECORDING MODE    IS F                                              
            LABEL RECORD      IS STANDARD.                                      
       COPY BP13F813.                                                           
                                                                                
       FD   BP13LD18                                                            
            RECORDING MODE    IS F                                              
            RECORD CONTAINS  132 CHARACTERS.                                    
       01   BP13LD18-REC                   PIC X(132).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F813-READ                PIC 9(05)   VALUE ZEROES.            
           05  WS-LD18-SNO                 PIC 9(05)   VALUE ZEROES.            
           05  WS-CTR                      PIC 9(01)   VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F813-EOF                 PIC X(01)   VALUE 'N'.               
           05  WS-LD18-HDR                 PIC X(01)   VALUE 'N'.               
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-CUR-DATE                 PIC X(08)   VALUE SPACES.            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-UNIT-OFFER               PIC 9(04)   VALUE ZEROES.            
           05  WS-3X-SUPPLY                PIC 9(05)   VALUE ZEROES.            
                                                                                
       01 WS-REPORT-VARS.                                                       
          05 LD18-HEADER.                                                       
             10 FILLER            PIC X(53)  VALUE                              
                'S-NO;SALES MODE;ZONE;NEW TOWN;FLAT TYPE;DATE BALLOT;M'.        
             10 FILLER            PIC X(53)  VALUE                              
                'SG;COHORT;FIRST QUEUE;LAST QUEUE;3 TIMES FLAT SUPPLY;'.        
             10 FILLER            PIC X(12)  VALUE                              
                'FLAT SUPPLY;'.                                                 
          05 LD18-RECORD.                                                       
             10 LD18-SNO          PIC X(05)  VALUE SPACES.                      
             10 FIL01             PIC X(01)  VALUE SPACES.                      
             10 LD18-SMODE        PIC X(03)  VALUE SPACES.                      
             10 FIL02             PIC X(01)  VALUE SPACES.                      
             10 LD18-ZN           PIC X(03)  VALUE SPACES.                      
             10 FIL03             PIC X(01)  VALUE SPACES.                      
             10 LD18-NT           PIC X(03)  VALUE SPACES.                      
             10 FIL04             PIC X(01)  VALUE SPACES.                      
             10 LD18-FT           PIC X(02)  VALUE SPACES.                      
             10 FIL05             PIC X(01)  VALUE SPACES.                      
             10 LD18-DTE-BAL      PIC X(06)  VALUE SPACES.                      
             10 FIL06             PIC X(01)  VALUE SPACES.                      
             10 LD18-DETAIL       PIC X(22)  VALUE SPACES.                      
             10 FIL07             PIC X(01)  VALUE SPACES.                      
             10 LD18-3X-SUPPLY    PIC X(05)  VALUE SPACES.                      
             10 FIL08             PIC X(01)  VALUE SPACES.                      
             10 LD18-SUPPLY       PIC X(04)  VALUE SPACES.                      
             10 FIL09             PIC X(01)  VALUE SPACES.                      
          05 LD18-DET-REC.                                                      
             10 LD18-MSG          PIC X(01)  VALUE SPACES.                      
             10 FIL10             PIC X(01)  VALUE SPACES.                      
             10 LD18-COHORT       PIC X(08)  VALUE SPACES.                      
             10 FIL11             PIC X(01)  VALUE SPACES.                      
             10 LD18-FIRST-Q      PIC 9(05)  VALUE ZEROES.                      
             10 FIL12             PIC X(01)  VALUE SPACES.                      
             10 LD18-LAST-Q       PIC 9(05)  VALUE ZEROES.                      
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ********************                                                      
       0000-MAIN-ROUTINE.                                                       
      ********************                                                      
                                                                                
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F813      THRU 2000-EXIT                       
             UNTIL WS-F813-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       1000-OPEN-FILES.                                                         
      ******************                                                        
                                                                                
           OPEN INPUT  BP13F813                                                 
                OUTPUT BP13LD18.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-CUR-DATE.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       2000-READ-BP13F813.                                                      
      *********************                                                     
                                                                                
           READ BP13F813 AT END                                                 
                MOVE 'Y'                   TO WS-F813-EOF                       
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                           TO WS-F813-READ.                     
                                                                                
           PERFORM 3000-PROCESS-REPORT     THRU 3000-EXIT.                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       3000-PROCESS-REPORT.                                                     
      **********************                                                    
                                                                                
           ADD 1                           TO WS-LD18-SNO.                      
                                                                                
           IF F813-NUM-SALES-MODE = 'BTO'                                       
              PERFORM 4000-WRITE-BP13LD18  THRU 4000-EXIT                       
              VARYING WS-CTR FROM 1 BY 1                                        
                UNTIL WS-CTR > 4                                                
           END-IF.                                                              
                                                                                
           IF F813-NUM-SALES-MODE = 'SBF'                                       
              PERFORM 5000-WRITE-BP13LD18  THRU 5000-EXIT                       
              VARYING WS-CTR FROM 1 BY 1                                        
                UNTIL WS-CTR > 3                                                
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       4000-WRITE-BP13LD18.                                                     
      **********************                                                    
                                                                                
           MOVE SPACES                     TO BP13LD18-REC.                     
           INITIALIZE                         BP13LD18-REC.                     
                                                                                
           IF WS-LD18-HDR = 'N'                                                 
              MOVE 'Y'                     TO WS-LD18-HDR                       
              WRITE BP13LD18-REC           FROM LD18-HEADER                     
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO LD18-RECORD.                      
           MOVE F813-NUM-UNIT-OFFER        TO WS-UNIT-OFFER.                    
                                                                                
           MOVE ';'                        TO FIL01  FIL02  FIL03               
                                              FIL04  FIL05  FIL06               
                                              FIL07  FIL08  FIL09               
                                              FIL10  FIL11  FIL12.              
                                                                                
           EVALUATE WS-CTR                                                      
           WHEN 1                                                               
              MOVE WS-LD18-SNO             TO LD18-SNO                          
              MOVE F813-NUM-SALES-MODE     TO LD18-SMODE                        
              MOVE F813-NUM-ZONE           TO LD18-ZN                           
              MOVE F813-CDE-NT             TO LD18-NT                           
              MOVE F813-NUM-FLAT-TYPE      TO LD18-FT                           
              MOVE F813-DTE-BALLOT         TO LD18-DTE-BAL                      
              COMPUTE WS-3X-SUPPLY = WS-UNIT-OFFER * 3                          
              MOVE WS-3X-SUPPLY            TO LD18-3X-SUPPLY                    
              MOVE WS-UNIT-OFFER           TO LD18-SUPPLY                       
                                                                                
              MOVE '1-50%   '             TO LD18-COHORT                        
              MOVE 1                       TO LD18-FIRST-Q                      
              IF WS-UNIT-OFFER = 1                                              
                 MOVE WS-UNIT-OFFER        TO LD18-LAST-Q                       
              ELSE                                                              
                 COMPUTE LD18-LAST-Q = WS-UNIT-OFFER / 2                        
              END-IF                                                            
                                                                                
              MOVE 'S'                     TO LD18-MSG                          
                                                                                
           WHEN 2                                                               
              MOVE SPACES                  TO LD18-SNO                          
                                              LD18-SMODE                        
                                              LD18-ZN                           
                                              LD18-NT                           
                                              LD18-FT                           
                                              LD18-DTE-BAL                      
                                              LD18-3X-SUPPLY                    
                                              LD18-SUPPLY                       
                                                                                
              MOVE '51-100% '             TO LD18-COHORT                        
              COMPUTE LD18-FIRST-Q = (WS-UNIT-OFFER / 2) + 1                    
              MOVE WS-UNIT-OFFER           TO LD18-LAST-Q                       
                                                                                
              MOVE 'S'                     TO LD18-MSG                          
                                                                                
           WHEN 3                                                               
              MOVE SPACES                  TO LD18-SNO                          
                                              LD18-SMODE                        
                                              LD18-ZN                           
                                              LD18-NT                           
                                              LD18-FT                           
                                              LD18-DTE-BAL                      
                                              LD18-3X-SUPPLY                    
                                              LD18-SUPPLY                       
                                                                                
              MOVE '101-200%'             TO LD18-COHORT                        
              COMPUTE LD18-FIRST-Q = WS-UNIT-OFFER + 1                          
              COMPUTE LD18-LAST-Q = WS-UNIT-OFFER * 2                           
                                                                                
              MOVE 'R'                     TO LD18-MSG                          
                                                                                
           WHEN 4                                                               
              MOVE SPACES                  TO LD18-SNO                          
                                              LD18-SMODE                        
                                              LD18-ZN                           
                                              LD18-NT                           
                                              LD18-FT                           
                                              LD18-DTE-BAL                      
                                              LD18-3X-SUPPLY                    
                                              LD18-SUPPLY                       
                                                                                
              MOVE '201-300%'             TO LD18-COHORT                        
              COMPUTE LD18-FIRST-Q = (WS-UNIT-OFFER * 2) + 1                    
              MOVE WS-3X-SUPPLY            TO LD18-LAST-Q                       
                                                                                
              MOVE 'W'                     TO LD18-MSG                          
                                                                                
           END-EVALUATE.                                                        
                                                                                
           MOVE LD18-DET-REC               TO LD18-DETAIL.                      
           WRITE BP13LD18-REC              FROM LD18-RECORD.                    
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       5000-WRITE-BP13LD18.                                                     
      **********************                                                    
                                                                                
           MOVE SPACES                     TO BP13LD18-REC.                     
           INITIALIZE                         BP13LD18-REC.                     
                                                                                
           IF WS-LD18-HDR = 'N'                                                 
              MOVE 'Y'                     TO WS-LD18-HDR                       
              WRITE BP13LD18-REC           FROM LD18-HEADER                     
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO LD18-RECORD.                      
           MOVE F813-NUM-UNIT-OFFER        TO WS-UNIT-OFFER.                    
                                                                                
           MOVE ';'                        TO FIL01  FIL02  FIL03               
                                              FIL04  FIL05  FIL06               
                                              FIL07  FIL08  FIL09               
                                              FIL10  FIL11  FIL12.              
                                                                                
           EVALUATE WS-CTR                                                      
           WHEN 1                                                               
              MOVE WS-LD18-SNO             TO LD18-SNO                          
              MOVE F813-NUM-SALES-MODE     TO LD18-SMODE                        
              MOVE F813-NUM-ZONE           TO LD18-NT                           
              MOVE F813-NUM-FLAT-TYPE      TO LD18-FT                           
              MOVE F813-DTE-BALLOT         TO LD18-DTE-BAL                      
              COMPUTE WS-3X-SUPPLY = WS-UNIT-OFFER * 3                          
              MOVE WS-3X-SUPPLY            TO LD18-3X-SUPPLY                    
              MOVE WS-UNIT-OFFER           TO LD18-SUPPLY                       
                                                                                
              MOVE '1-100%  '              TO LD18-COHORT                       
              MOVE 1                       TO LD18-FIRST-Q                      
              MOVE WS-UNIT-OFFER           TO LD18-LAST-Q                       
                                                                                
              MOVE 'S'                     TO LD18-MSG                          
                                                                                
           WHEN 2                                                               
              MOVE SPACES                  TO LD18-SNO                          
                                              LD18-SMODE                        
                                              LD18-NT                           
                                              LD18-FT                           
                                              LD18-DTE-BAL                      
                                              LD18-3X-SUPPLY                    
                                              LD18-SUPPLY                       
                                                                                
              MOVE '101-200%'              TO LD18-COHORT                       
              COMPUTE LD18-FIRST-Q = WS-UNIT-OFFER + 1                          
              COMPUTE LD18-LAST-Q = WS-UNIT-OFFER * 2                           
                                                                                
              MOVE 'R'                     TO LD18-MSG                          
                                                                                
           WHEN 3                                                               
              MOVE SPACES                  TO LD18-SNO                          
                                              LD18-SMODE                        
                                              LD18-NT                           
                                              LD18-FT                           
                                              LD18-DTE-BAL                      
                                              LD18-3X-SUPPLY                    
                                              LD18-SUPPLY                       
                                                                                
              MOVE '201-300%'              TO LD18-COHORT                       
              COMPUTE LD18-FIRST-Q = (WS-UNIT-OFFER * 2) + 1                    
              MOVE WS-3X-SUPPLY            TO LD18-LAST-Q                       
                                                                                
              MOVE 'W'                     TO LD18-MSG                          
                                                                                
           END-EVALUATE.                                                        
                                                                                
           MOVE LD18-DET-REC               TO LD18-DETAIL.                      
           WRITE BP13LD18-REC              FROM LD18-RECORD.                    
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *******************                                                       
       9000-CLOSE-FILES.                                                        
      *******************                                                       
                                                                                
           DISPLAY 'RUN DATE : ' WS-CUR-DATE.                                   
           DISPLAY '*================ BP13CD18 ================*'.              
           DISPLAY '  NO OF F813 RECORDS READ     = ' WS-F813-READ.             
           DISPLAY '  NO OF LD18 RECORDS WRITTEN  = ' WS-LD18-SNO.              
           DISPLAY '*==========================================*'.              
                                                                                
           CLOSE BP13F813                                                       
                 BP13LD18.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
        9000-EXIT.                                                              
            EXIT.                                                               
                                                                                
