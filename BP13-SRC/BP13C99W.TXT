       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C99W.                                                  
       AUTHOR.       RJB1.                                                      
      *DATE-WRITTEN. 16/12/2016.                                                
                                                                                
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE: 1) TO CREATE IN-TRAY ALERT TO TEAM LEAD FOR    *            
      *                STATUS REPORT OF SATP/TP CASES PENDING > 3  *            
      *                MONTHS USING BE08C008                       *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP136574 20161216 RJB1  NEW PROGRAM                        *            
      **************************************************************            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
            SELECT BP13FC40 ASSIGN TO BP13FC40.                                 
                                                                                
            SELECT BP13K024 ASSIGN TO BP13K024                                  
                   ACCESS MODE  IS RANDOM                                       
                   ORGANIZATION IS INDEXED                                      
                   RECORD KEY   IS K024-KEY-FLD                                 
                   FILE STATUS  IS BP13K024-STATUS.                             
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13FC40                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13FC40.                                                           
                                                                                
       FD   BP13K024                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K024.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-CONSTANTS.                                                        
           05  WS-TXN-CREATE           PIC X(02)  VALUE 'C'.                    
           05  WS-SYSTEM-ID            PIC X(04)  VALUE 'BP13'.                 
           05  WS-NUM-PRCS-CTGRY       PIC X(04)  VALUE '0137'.                 
           05  WS-NUM-BRNCH            PIC X(06)  VALUE '000001'.               
           05  WS-DEFAULT-DTE          PIC X(10)  VALUE '0001-01-01'.           
           05  WS-PROGRAM-ID           PIC X(08)  VALUE 'BP13C99W'.             
           05  WS-STATUS-NEW           PIC X(03)  VALUE '000'.                  
           05  WS-PLANNED-INSPTN       PIC X(03)  VALUE '101'.                  
           05  WS-14-DAYS              PIC 9(02)  VALUE 14.                     
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-CURRENT-DTE-TME      PIC 9(14)  VALUE ZEROES.                 
           05  WS-DTE-CURRENT          PIC X(10)  VALUE SPACES.                 
           05  WS-TME-CURRENT          PIC X(08)  VALUE SPACES.                 
           05  WS-DTE-TEMP             PIC 9(08)  VALUE ZEROES.                 
           05  WS-INT-TEMP             PIC 9(08)  VALUE ZEROES.                 
           05  WS-COMP-DTE             PIC 9(08)  VALUE ZEROES.                 
           05  WS-COMP-DTE-TXT         PIC X(10)  VALUE SPACES.                 
           05  WS-DTE-DUE.                                                      
               10 WS-DTE-CCYY          PIC X(04).                               
               10 FILLER               PIC X(01)  VALUE '-'.                    
               10 WS-DTE-MM            PIC X(02).                               
               10 FILLER               PIC X(01)  VALUE '-'.                    
               10 WS-DTE-DD            PIC X(02).                               
                                                                                
       01  WS-STATUS.                                                           
           05  WS-OIC-EXIST-TAG        PIC X(01)  VALUE 'N'.                    
           05  WS-BP13FC40-EOF         PIC X(01)  VALUE 'N'.                    
           05  WS-K024-FND             PIC X(01)  VALUE 'N'.                    
           05  BP13K024-STATUS         PIC 9(02)  VALUE ZEROES.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ-CNT             PIC 9(07)  VALUE ZEROES.                 
           05  WS-WRITE-CNT            PIC 9(07)  VALUE ZEROES.                 
           05  WS-UPDATE-CNT           PIC 9(07)  VALUE ZEROES.                 
                                                                                
       01  WS-FIELDS.                                                           
           05  WS-STAFF                PIC X(06)  VALUE SPACES.                 
           05  WS-OIC                  PIC X(05)  VALUE SPACES.                 
           05  WS-PREV-TEAM            PIC X(01)  VALUE SPACES.                 
                                                                                
       01  WS-TEAM-LEADS.                                                       
           05  WS-TL1                  PIC X(06)  VALUE '22785D'.               
           05  WS-TL2                  PIC X(06)  VALUE '15897F'.               
           05  WS-TL3                  PIC X(06)  VALUE '23924J'.               
           05  WS-TL4                  PIC X(06)  VALUE '21880D'.               
           05  WS-TL5                  PIC X(06)  VALUE '26296Z'.               
           05  WS-TL6                  PIC X(06)  VALUE '26840B'.               
                                                                                
      ******************************************************************        
      *  TEXT URL VARIABLES                                                     
      ******************************************************************        
       01  WS-INBOX-TXT-TARGET-URL.                                             
           05  FILLER              PIC X(49) VALUE                              
              '/be10/home/Newsales/sssCrm/pendingKeyIssue/myTeam'.              
                                                                                
       01  WS-INBOX-TXT-REMARK.                                                 
           05  FILLER              PIC X(51) VALUE                              
              'Status Report of SATP/TP Cases Pending > 3 months. '.            
                                                                                
                                                                                
       COPY BE08W008.                                                           
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-LOGIC.                                                         
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13FC40       THRU 2000-EXIT.                     
           IF WS-BP13FC40-EOF = 'Y'                                             
              DISPLAY 'FILE BP13FC40 IS  EMPTY'                                 
           END-IF                                                               
           PERFORM 2500-PROCESS             THRU 2500-EXIT                      
             UNTIL WS-BP13FC40-EOF = 'Y'.                                       
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-FILES.                                                         
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13FC40                                                 
                       BP13K024.                                                
                                                                                
           IF BP13K024-STATUS NOT = ZEROES AND 97                               
              DISPLAY 'OPENING ERROR, K024-STATUS ' BP13K024-STATUS             
              MOVE BP13K024-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2000-READ-BP13FC40.                                                      
      ****************************************************************          
                                                                                
           READ BP13FC40 AT END                                                 
                         MOVE 'Y'  TO WS-BP13FC40-EOF                           
                         NOT AT END                                             
                         ADD  1    TO WS-READ-CNT                               
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2500-PROCESS.                                                            
      ****************************************************************          
                                                                                
           PERFORM 2510-READ-K024       THRU 2510-EXIT.                         
           IF WS-K024-FND = 'Y'                                                 
              IF K024-CDE-TEAM NOT = WS-PREV-TEAM                               
                 EVALUATE K024-CDE-TEAM                                         
                 WHEN '1'                                                       
                    MOVE  WS-TL1          TO WS-STAFF                           
                 WHEN '2'                                                       
                    MOVE  WS-TL2          TO WS-STAFF                           
                 WHEN '3'                                                       
                    MOVE  WS-TL3          TO WS-STAFF                           
                 WHEN '4'                                                       
                    MOVE  WS-TL4          TO WS-STAFF                           
                 WHEN '5'                                                       
                    MOVE  WS-TL5          TO WS-STAFF                           
                 WHEN '6'                                                       
                    MOVE  WS-TL6          TO WS-STAFF                           
                 WHEN OTHER                                                     
                    MOVE SPACES           TO WS-STAFF                           
                 END-EVALUATE                                                   
                                                                                
                 IF WS-STAFF NOT EQUAL SPACES AND LOW-VALUES                    
                    PERFORM 3000-INSERT-INBOX-INTRY THRU 3000-EXIT              
                    MOVE K024-CDE-TEAM    TO WS-PREV-TEAM                       
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13FC40   THRU 2000-EXIT.                         
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2510-READ-K024.                                                          
      ****************************************************************          
           INITIALIZE  BP13K024-REC.                                            
           MOVE 'N'               TO  WS-K024-FND.                              
           MOVE FC40-NUM-OIC      TO  K024-NUM-OIC.                             
                                                                                
           READ BP13K024.                                                       
                                                                                
           EVALUATE BP13K024-STATUS                                             
           WHEN 00                                                              
                 MOVE 'Y'         TO  WS-K024-FND                               
           WHEN 23                                                              
                 DISPLAY 'RECORD NOT FOUND IN BP13K024 : '                      
                                                K024-NUM-OIC                    
           WHEN  OTHER                                                          
                 DISPLAY 'ERROR READING BP13K024 : ' BP13K024-STATUS            
                 DISPLAY 'K024 KEY FIELD         : ' K024-NUM-OIC               
                 MOVE     BP13K024-STATUS       TO   RETURN-CODE                
                 PERFORM  9000-CLOSE-ROUTINE    THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       2510-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3000-INSERT-INBOX-INTRY.                                                 
      ****************************************************************          
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:14) TO WS-CURRENT-DTE-TME.              
                                                                                
           STRING WS-CURRENT-DTE-TME(1:4) '-'                                   
                  WS-CURRENT-DTE-TME(5:2) '-'                                   
                  WS-CURRENT-DTE-TME(7:2) DELIMITED BY SIZE                     
                  INTO WS-DTE-CURRENT.                                          
                                                                                
           STRING WS-CURRENT-DTE-TME(9:2)                                       
                  '.'                                                           
                  WS-CURRENT-DTE-TME(11:2)                                      
                  '.'                                                           
                  WS-CURRENT-DTE-TME(13:2) DELIMITED BY SIZE                    
                  INTO  WS-TME-CURRENT.                                         
                                                                                
           MOVE SPACES                      TO  BE08W008-COMMAREA.              
           INITIALIZE                           BE08W008-COMMAREA.              
                                                                                
           PERFORM 3020-COMPUTE-DATE-DUE  THRU  3020-EXIT.                      
                                                                                
           MOVE WS-TXN-CREATE               TO  W008-IN-TXN-TYPE.               
           MOVE WS-SYSTEM-ID                TO  W008-IN-SYSTEM-ID.              
           MOVE WS-NUM-PRCS-CTGRY           TO  W008-IN-NUM-PRCS-CTGRY.         
           MOVE WS-NUM-BRNCH                TO  W008-IN-NUM-BRNCH.              
           MOVE WS-STAFF                    TO  W008-IN-NUM-CASE-REF            
                                                W008-IN-NUM-STAFF.              
           MOVE WS-INBOX-TXT-TARGET-URL     TO  W008-IN-TXT-TARGET-URL.         
           MOVE WS-INBOX-TXT-REMARK         TO  W008-IN-TXT-REMARK.             
           MOVE WS-DTE-CURRENT              TO  W008-IN-DTE-INBOX.              
           MOVE WS-TME-CURRENT              TO  W008-IN-TME-INBOX.              
           MOVE WS-DTE-DUE                  TO  W008-IN-DUE-DATE.               
                                                                                
           CALL 'BE08C008'    USING BE08W008-COMMAREA.                          
           IF W008-OUT-RTN-MSGCDE = '0' OR 'W'                                  
              ADD 1                         TO  WS-WRITE-CNT                    
           ELSE                                                                 
              DISPLAY 'ERROR RETURNED CALLING BE08C008 SUBR (C) : '             
                                          W008-IN-NUM-CASE-REF                  
              DISPLAY '{ ' W008-OUT-RTN-MSGCDE ' }'                             
              DISPLAY '{ ' W008-OUT-RTN-MSGNO  ' }'                             
              DISPLAY '{ ' W008-OUT-RTN-DESC   ' }'                             
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3020-COMPUTE-DATE-DUE.                                                   
      ****************************************************************          
                                                                                
           MOVE WS-CURRENT-DTE-TME(1:8) TO WS-DTE-TEMP.                         
                                                                                
           COMPUTE WS-INT-TEMP =                                                
                   FUNCTION INTEGER-OF-DATE (WS-DTE-TEMP) + WS-14-DAYS.         
                                                                                
           COMPUTE WS-COMP-DTE =                                                
                   FUNCTION DATE-OF-INTEGER (WS-INT-TEMP).                      
                                                                                
           MOVE WS-COMP-DTE             TO  WS-COMP-DTE-TXT.                    
                                                                                
           MOVE WS-COMP-DTE-TXT(1:4)    TO  WS-DTE-CCYY.                        
           MOVE WS-COMP-DTE-TXT(5:2)    TO  WS-DTE-MM.                          
           MOVE WS-COMP-DTE-TXT(7:2)    TO  WS-DTE-DD.                          
                                                                                
       3020-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9000-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           DISPLAY '---- BP13C99W CONTROL TOTAL -----'.                         
           DISPLAY ' NO OF REC READ FROM BP13FC40   = '                         
                     WS-READ-CNT.                                               
           DISPLAY ' NO OF RECS SENT TO BE08        = '                         
                     WS-WRITE-CNT.                                              
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE       BP13FC40                                                 
                       BP13K024.                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
