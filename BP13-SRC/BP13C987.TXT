      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C987.                                                 
      *AUTHOR.        SATHAPPAN.                                                
      *DATE-WRITTEN.  22/09/2001.                                               
      *DATE-COMPILED.                                                           
                                                                                
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITTMENT                 *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE  :                                        *                
      *        1.  WHEN SALES ORDER RAISED, SOC TO CREATE REC  *                
      *            TO UPDATE CDB TO END-ROLE AS APPLN/OCCUP.   *                
      *                                                        *                
      *     INPUT FILES:                                       *                
      *           1. BP13F410 (SALES ORDER    FILE)            *                
      *           2. BP13K820 (SOC   OCCUPIER FILE)            *                
      *                                                        *                
      *     OUTPUT FILES:                                      *                
      *           1. BP13F915 (CDB TRANSFILE)                  *                
      *                                                        *                
      *  CHG REG.   BY   DATE       DESCRIPTION                *                
      *  =======    ==   ====       ===========                *                
      *  BP130850   AS1  22/09/2001 NEW PROGRAM                *                
      *  BP137326   FNP  24/12/2018 BLANK OFF OTHER DETAILS WHEN                
      *                             CREATING 'TE' FOR F915                      
      * ====================================================== *                
      *--------------------*                                                    
       ENVIRONMENT DIVISION.                                                    
      *--------------------*                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3900.                                               
       OBJECT-COMPUTER. IBM-3900.                                               
      *--------------------*                                                    
       INPUT-OUTPUT SECTION.                                                    
      *--------------------*                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F410   ASSIGN       TO BP13F410.                          
                                                                                
           SELECT BP13F915   ASSIGN       TO BP13F915.                          
                                                                                
           SELECT BP13K820   ASSIGN       TO BP13K820                           
                             ORGANIZATION IS INDEXED                            
                             ACCESS MODE  IS DYNAMIC                            
                             RECORD KEY   IS K820-KEY-FLD                       
                             FILE STATUS  IS WS-K820-STATUS.                    
      *--------------*                                                          
       DATA DIVISION.                                                           
      *--------------*                                                          
       FILE SECTION.                                                            
      *--------------*                                                          
                                                                                
       FD  BP13F410                                                             
           BLOCK  CONTAINS    0 CHARACTERS                                      
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL  RECORDS  ARE  STANDARD                                        
           RECORDING MODE  IS   F.                                              
       COPY BP13F410.                                                           
                                                                                
       FD  BP13F915                                                             
           BLOCK  CONTAINS    0 CHARACTERS                                      
           RECORD CONTAINS  800 CHARACTERS                                      
           LABEL  RECORDS   ARE STANDARD                                        
           RECORDING MODE   IS  F.                                              
       COPY BP13F915.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-PROGRAM-SWITCHES.                                                 
           03  WS-EOF-F410-IND          PIC X(2)  VALUE 'N'.                    
               88  WS-EOF-F410          VALUE 'Y'.                              
           03  WS-K820-EOF-SW           PIC X(01)  VALUE 'N'.                   
               88  WS-K820-EOF          VALUE 'Y'.                              
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K820-STATUS           PIC 9(02)  VALUE ZEROS.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F410-READ-CNT         PIC 9(06)  VALUE ZEROS.                 
           05  WS-K820-READ-CNT         PIC 9(06)  VALUE ZEROS.                 
           05  WS-F915-WRITE-CNT        PIC 9(06)  VALUE ZEROS.                 
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           03  WS-YY                    PIC 9(4).                               
           03  WS-MM                    PIC 9(2).                               
           03  WS-DD                    PIC 9(2).                               
                                                                                
      *------------------*                                                      
       PROCEDURE DIVISION.                                                      
      *------------------*                                                      
                                                                                
      *---------*                                                               
       1000-MAIN.                                                               
      *---------*                                                               
                                                                                
           PERFORM 2000-OPENING-ROUTINE   THRU 2000-EXIT.                       
           PERFORM 3000-READ-F410-REC     THRU 3000-EXIT.                       
           PERFORM 4000-OUTPUT-TO-MAF     THRU 4000-EXIT                        
                   UNTIL WS-EOF-F410.                                           
           PERFORM 6000-CLOSING-ROUTINE   THRU 6000-EXIT.                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       2000-OPENING-ROUTINE.                                                    
      *-------------------*                                                     
                                                                                
            OPEN INPUT BP13F410                                                 
                       BP13K820                                                 
                OUTPUT BP13F915.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)   TO  WS-SYSTEM-DATE.                
                                                                                
           IF WS-K820-STATUS NOT = ZEROS AND 97                                 
              MOVE WS-K820-STATUS        TO  RETURN-CODE                        
              DISPLAY 'ERROR OPENING BP13K820! FILE STATUS IS '                 
                       WS-K820-STATUS '.'                                       
              PERFORM 6000-CLOSING-ROUTINE THRU 6000-EXIT.                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *              READING OF BP13F410 FILE                          *        
      ******************************************************************        
      *-----------------*                                                       
       3000-READ-F410-REC.                                                      
      *-----------------*                                                       
                                                                                
           READ BP13F410 AT END                                                 
                         MOVE 'Y' TO WS-EOF-F410-IND                            
                         GO TO 3000-EXIT.                                       
                                                                                
                ADD 1    TO WS-F410-READ-CNT.                                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       4000-OUTPUT-TO-MAF.                                                      
      *------------------*                                                      
                                                                                
           PERFORM 4100-START-BP13K820    THRU 4100-EXIT                        
                                                                                
           PERFORM 4300-BROWSE-BP13K820   THRU 4300-EXIT                        
                 UNTIL WS-K820-EOF OR                                           
                       (K820-NUM-REGN NOT = F410-NUM-REGN).                     
                                                                                
           PERFORM 3000-READ-F410-REC        THRU 3000-EXIT.                    
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       4100-START-BP13K820.                                                     
      *------------------*                                                      
                                                                                
           MOVE 'N'           TO  WS-K820-EOF-SW.                               
           MOVE F410-NUM-REGN TO  K820-NUM-REGN.                                
           MOVE SPACES        TO  K820-NUM-NRIC.                                
                                                                                
           START BP13K820 KEY >= K820-KEY-FLD                                   
           IF WS-K820-STATUS = ZEROS OR 02                                      
              PERFORM 4200-READ-BP13K820   THRU 4200-EXIT                       
           ELSE                                                                 
              DISPLAY 'START BROWSE ERROR, BP13K820 STATUS IS '                 
                      WS-K820-STATUS                                            
              MOVE WS-K820-STATUS           TO RETURN-CODE                      
              PERFORM 6000-CLOSING-ROUTINE THRU 6000-EXIT                       
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       4200-READ-BP13K820.                                                      
      *------------------*                                                      
                                                                                
           READ BP13K820 NEXT AT END                                            
                              MOVE 'Y'      TO WS-K820-EOF-SW                   
                              GO TO 4200-EXIT.                                  
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4300-BROWSE-BP13K820.                                                    
      *-------------------*                                                     
                                                                                
           IF F410-NUM-REGN  = K820-NUM-REGN                                    
              ADD  1       TO  WS-K820-READ-CNT                                 
              PERFORM 4400-WRITE-BP13F915   THRU 4400-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 4200-READ-BP13K820  THRU 4200-EXIT.                          
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       4400-WRITE-BP13F915.                                                     
      *------------------*                                                      
                                                                                
           INITIALIZE  BP13F915-REC.                                            
           MOVE SPACES                   TO   BP13F915-REC.                     
                                                                                
           MOVE  'TE'                    TO   F915-TXN-TYPE.                    
           MOVE  'P'                     TO   F915-CSTMR-TYPE.                  
           MOVE  'BP13'                  TO   F915-NUM-UPDATE-SOURCE.           
           MOVE  'BP13C987'              TO   F915-NUM-UPDATE-USERID.           
                                                                                
      **** PERSON PARTICULARS...                                                
                                                                                
           MOVE  K820-NUM-CSTMR-SOURCE   TO   F915-NUM-CSTMR-SOURCE.            
                                                                                
                                                                                
           PERFORM 3410-CONVERT-CUSTOMER-ROLE  THRU 3410-EXIT.                  
           MOVE  K820-NUM-REGN           TO   F915-NUM-BSNS-REF.                
           STRING F410-DTE-KEY-ISSUE(1:4) '-'                                   
                  F410-DTE-KEY-ISSUE(5:2) '-'                                   
                  F410-DTE-KEY-ISSUE(7:2)                                       
                  DELIMITED BY SIZE INTO                                        
                  F915-DTE-ROLE-END.                                            
                                                                                
           STRING F410-DTE-KEY-ISSUE(1:4) '-'                                   
                  F410-DTE-KEY-ISSUE(5:2) '-'                                   
                  F410-DTE-KEY-ISSUE(7:2)                                       
                  DELIMITED BY SIZE INTO                                        
                  F915-DTE-UPDATE-SOURCE.                                       
                                                                                
      ******   WRITING CDB  INFO.  INTO   BP13F915  *******                     
                                                                                
           WRITE BP13F915-REC.                                                  
           ADD 1 TO WS-F915-WRITE-CNT.                                          
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------*                                            
       3410-CONVERT-CUSTOMER-ROLE.                                              
      *----------------------------*                                            
                                                                                
           IF K820-NUM-NRIC = F410-NRIC1                                        
               MOVE '011'                 TO F915-NUM-ROLE                      
           ELSE                                                                 
           IF (K820-NUM-NRIC = F410-NRIC2) OR                                   
              (K820-NUM-NRIC = F410-NRIC3) OR                                   
              (K820-NUM-NRIC = F410-NRIC4)                                      
               MOVE '012'                 TO F915-NUM-ROLE                      
           ELSE                                                                 
               MOVE '013'                 TO F915-NUM-ROLE                      
           END-IF.                                                              
                                                                                
       3410-EXIT.                                                               
            EXIT.                                                               
      *-------------------*                                                     
       6000-CLOSING-ROUTINE.                                                    
      *-------------------*                                                     
                                                                                
           DISPLAY '******************* BP13C987 *********************'.        
           DISPLAY 'RUN DATE : ' WS-DD '/' WS-MM '/' WS-YY.                     
           DISPLAY SPACES.                                                      
           DISPLAY ' NO. OF BP13F410 RECORDS READ    : '                        
                     WS-F410-READ-CNT.                                          
           DISPLAY ' NO. OF BP13K820 RECORDS READ    : '                        
                     WS-K820-READ-CNT.                                          
           DISPLAY ' NO. OF BP13F915 RECORDS WRITTEN : '                        
                     WS-F915-WRITE-CNT.                                         
           DISPLAY '******************* -------- *********************'.        
                                                                                
           CLOSE BP13F410                                                       
                 BP13K820                                                       
                 BP13F915.                                                      
                                                                                
           IF WS-K820-STATUS NOT = ZEROS AND 97                                 
              MOVE WS-K820-STATUS        TO  RETURN-CODE                        
              DISPLAY 'ERROR CLOSING BP13K820!  FILE STATUS IS '                
                       WS-K820-STATUS '.'.                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
