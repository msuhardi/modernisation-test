       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF99.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  15/11/2013.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE    :  DETERMINE START QUEUE PER REQ-STAT/NT/FT   *            
      *                                                            *            
      * INPUT FILE   :  BP13F740  -  SEQUENTIAL DATASET            *            
      * I-O FILE     :  BP13K703  -  BP13.K703.SOCDATE             *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP135170  SMR2  15112013  NEW PROGRAM                      *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740  ASSIGN       TO BP13F740.                           
                                                                                
           SELECT BP13K703  ASSIGN       TO BP13K703                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K703-KEY-FLD                        
                            FILE STATUS  IS WS-K703-STATUS.                     
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F740                                                            
            RECORDING MODE   IS F                                               
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K703                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K703.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  WS-K703-STATUS           PIC 9(02)  VALUE ZEROES.                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F740-READ             PIC 9(06)  VALUE ZEROES.                
           05  WS-K703-FOUND            PIC 9(06)  VALUE ZEROES.                
           05  WS-K703-WRITE            PIC 9(06)  VALUE ZEROES.                
           05  WS-SURVEY                PIC 9(05)  VALUE ZEROES.                
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F740-EOF              PIC X(01)  VALUE 'N'.                   
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-DATE                  PIC X(08)  VALUE SPACES.                
           05  WS-DATE-EDIT             PIC X(10)  VALUE SPACES.                
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F740-KEY-FLD.                                                 
               10  WS-F740-REQ-STAT     PIC X(01)  VALUE SPACES.                
               10  WS-F740-NT           PIC X(03)  VALUE SPACES.                
               10  WS-F740-FT           PIC X(02)  VALUE SPACES.                
           05  WS-TEMP-KEY-FLD.                                                 
               10  WS-TEMP-REQ-STAT     PIC X(01)  VALUE SPACES.                
               10  WS-TEMP-NT           PIC X(03)  VALUE SPACES.                
               10  WS-TEMP-FT           PIC X(02)  VALUE SPACES.                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F740       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-RECS        THRU 3000-EXIT                      
             UNTIL WS-F740-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT BP13F740                                                  
                I-O   BP13K703.                                                 
                                                                                
           IF WS-K703-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K703 ERROR ' WS-K703-STATUS                  
              MOVE WS-K703-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE       TO WS-DATE.                         
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO WS-DATE-EDIT.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-BP13F740.                                                      
      *-------------------------------------------------------------            
           READ BP13F740                                                        
             AT END                                                             
                MOVE 'Y'                    TO WS-F740-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           MOVE F740-CDE-REQUEST-STATUS     TO WS-F740-REQ-STAT.                
           MOVE F740-CDE-NT1                TO WS-F740-NT.                      
           MOVE F740-CDE-FLAT-TYPE          TO WS-F740-FT.                      
           ADD 1                            TO WS-F740-READ.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-PROCESS-RECS.                                                       
      *-------------------------------------------------------------            
           EVALUATE F740-CDE-REQUEST-STATUS                                     
           WHEN '1'                                                             
              IF WS-F740-KEY-FLD NOT = WS-TEMP-KEY-FLD                          
                 MOVE WS-F740-KEY-FLD       TO WS-TEMP-KEY-FLD                  
                 MOVE 1                     TO WS-SURVEY                        
                 PERFORM 4000-READ-BP13K703 THRU 4000-EXIT                      
              ELSE                                                              
                 ADD 1                      TO WS-SURVEY                        
              END-IF                                                            
              PERFORM 2000-READ-BP13F740    THRU 2000-EXIT                      
                                                                                
           WHEN '2'                                                             
              IF WS-F740-KEY-FLD NOT = WS-TEMP-KEY-FLD                          
                 MOVE WS-F740-KEY-FLD       TO WS-TEMP-KEY-FLD                  
                 ADD 1                      TO WS-SURVEY                        
                 PERFORM 4000-READ-BP13K703 THRU 4000-EXIT                      
              ELSE                                                              
                 ADD 1                      TO WS-SURVEY                        
              END-IF                                                            
              PERFORM 2000-READ-BP13F740    THRU 2000-EXIT                      
                                                                                
           WHEN '3'                                                             
              IF WS-F740-KEY-FLD NOT = WS-TEMP-KEY-FLD                          
                 MOVE WS-F740-KEY-FLD       TO WS-TEMP-KEY-FLD                  
                 ADD 1                      TO WS-SURVEY                        
                 PERFORM 4000-READ-BP13K703 THRU 4000-EXIT                      
              END-IF                                                            
              PERFORM 2000-READ-BP13F740    THRU 2000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-READ-BP13K703.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                      TO BP13K703-REC.                    
           INITIALIZE                          BP13K703-REC.                    
                                                                                
           EVALUATE F740-CDE-REQUEST-STATUS                                     
           WHEN '1'                                                             
              MOVE 'SQUEUENO'               TO K703-NUM-SOCLIST                 
           WHEN '2'                                                             
              MOVE 'RQUEUENO'               TO K703-NUM-SOCLIST                 
           WHEN '3'                                                             
              MOVE 'UQUEUENO'               TO K703-NUM-SOCLIST                 
           END-EVALUATE.                                                        
                                                                                
           MOVE F740-CDE-NT1                TO K703-NUM-NT.                     
           MOVE F740-CDE-FLAT-TYPE          TO K703-NUM-FLAT.                   
                                                                                
           READ BP13K703.                                                       
                                                                                
           EVALUATE WS-K703-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              ADD 1                         TO WS-K703-FOUND                    
              DISPLAY 'RECORD ALREADY EXIST IN K703 : ' K703-KEY-FLD            
              PERFORM 6000-REWRITE-BP13K703   THRU 6000-EXIT                    
                                                                                
           WHEN 23                                                              
              PERFORM 5000-WRITE-BP13K703   THRU 5000-EXIT                      
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K703   : ' WS-K703-STATUS              
              MOVE WS-K703-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5000-WRITE-BP13K703.                                                     
      *-------------------------------------------------------------            
           MOVE F740-DTE-BALLOT            TO K703-DTE-BALLOT-ORIGIN            
                                              K703-DTE-BALLOT-NEW.              
           MOVE WS-SURVEY                  TO K703-NUM-SURVEY.                  
           MOVE WS-DATE                    TO K703-DTE-UPDATE.                  
           MOVE 'BP13CF99'                 TO K703-CDE-USERID.                  
                                                                                
           WRITE BP13K703-REC.                                                  
                                                                                
           EVALUATE WS-K703-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                        TO WS-K703-WRITE                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR WRITING TO BP13K703. STATUS IS - '                 
                      WS-K703-STATUS                                            
              MOVE WS-K703-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       6000-REWRITE-BP13K703.                                                   
      *-------------------------------------------------------------            
           MOVE F740-DTE-BALLOT            TO K703-DTE-BALLOT-ORIGIN            
                                              K703-DTE-BALLOT-NEW.              
           MOVE WS-SURVEY                  TO K703-NUM-SURVEY.                  
           MOVE WS-DATE                    TO K703-DTE-UPDATE.                  
           MOVE 'BP13CF99'                 TO K703-CDE-USERID.                  
                                                                                
           REWRITE BP13K703-REC.                                                
                                                                                
           EVALUATE WS-K703-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                        TO WS-K703-WRITE                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR WRITING TO BP13K703. STATUS IS - '                 
                      WS-K703-STATUS                                            
              MOVE WS-K703-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY '  '.                                                        
           DISPLAY '    BP13CF99 CONTROL TOTAL   : ' WS-DATE-EDIT.              
           DISPLAY '  '.                                                        
           DISPLAY ' 1. NO OF BP13F740 READ      : ' WS-F740-READ.              
           DISPLAY ' 2. NO OF BP13K703 WRITTEN   : ' WS-K703-WRITE.             
           DISPLAY ' 3. NO OF BP13K703 FOUND     : ' WS-K703-FOUND.             
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13F740                                                       
                 BP13K703.                                                      
                                                                                
           IF WS-K703-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K703 ERROR ' WS-K703-STATUS                  
              MOVE WS-K703-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
