       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C11F.                                                 
      *AUTHOR.        KSJ3.                                                     
      *DATE-WRITTEN.  30/09/2015.                                               
      *============================================================*            
      * OBJECTIVE    :  TO GENERATE FILES FOR SATP PRE-ECHECK      *            
      *============================================================*            
      * INPUT FILES  :  1.) BP13F800                               *            
      *                 2.) BM06K110                               *            
      *                                                            *            
      * OUTPUT FILES :  1.) BP13FB41                               *            
      *                 2.) BP13FB42                               *            
      *                                                            *            
      * LISTING      :  1.) BP13L11F                               *            
      *============================================================*            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800  ASSIGN       TO BP13F800.                           
                                                                                
           SELECT BM06K110  ASSIGN       TO BM06K110                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K110-KEY-FLD                        
                            FILE STATUS  IS WS-K110-STATUS.                     
                                                                                
           SELECT BP13FB41  ASSIGN       TO BP13FB41.                           
                                                                                
           SELECT BP13FB42  ASSIGN       TO BP13FB42.                           
                                                                                
           SELECT BP13L11F  ASSIGN       TO BP13L11F.                           
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F800                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 2000 CHARACTERS.                                     
           COPY BP13F800.                                                       
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
           COPY BM06K110.                                                       
                                                                                
       FD  BP13FB41                                                             
           RECORD CONTAINS 400 CHARACTERS                                       
           LABEL RECORD IS STANDARD                                             
           RECORDING MODE IS F.                                                 
           COPY BP13FB41.                                                       
                                                                                
       FD  BP13FB42                                                             
           RECORD CONTAINS 400 CHARACTERS                                       
           LABEL RECORD IS STANDARD                                             
           RECORDING MODE IS F.                                                 
           COPY BP13FB42.                                                       
                                                                                
       FD  BP13L11F                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  BP13L11F-REC                  PIC X(132).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K110-STATUS            PIC 9(2)   VALUE 00.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F800-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F800-SKIP          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K110-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K110-NFND          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-FB41-WRITE         PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-FB42-WRITE         PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-RPT-PRINT          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-PAGE               PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-LINE               PIC 9(2)   VALUE 60.                   
           05  WS-CNT-SNO                PIC 9(4)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F800-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K110-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-SKIP-REC               PIC X(1)   VALUE 'N'.                  
           05  WS-NUM-SERS               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-FMT8-CCYY      PIC 9(4)   VALUE ZEROES.               
               10  WS-DTE-FMT8-MM        PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-FMT8-DD        PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-FMT10-DD       PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-FMT10-MM       PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-FMT10-CCYY.                                           
                   15  WS-DTE-FMT10-CC   PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-FMT10-YY   PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-TMP                PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-INT                PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE SPACES.               
                                                                                
       01  WS-LOOP-VARS.                                                        
           05  WS-IDX                    PIC 9(3)   VALUE ZEROES.               
           05  WS-IDX-MAX                PIC 9(3)   VALUE 100.                  
           05  WS-DATE-FOUND             PIC X(1)   VALUE SPACES.               
           05  WS-DTE-DUE.                                                      
               10  WS-DTE-DUE-CCYYMM     PIC 9(6)   VALUE ZEROES.               
               10  WS-DTE-DUE-DD         PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-TBL-LOAN-DETAILS.                                                 
           05  FILLER OCCURS 100 TIMES.                                         
               10  WS-TBL-NUM-SERS       PIC X(1)   VALUE SPACES.               
               10  WS-TBL-DTE-DUE        PIC 9(6)   VALUE ZEROES.               
               10  WS-TBL-LOAN-CNT.                                             
                   15  WS-TBL-NUM-N-CNT  PIC 9(5)   VALUE ZEROES.               
                   15  WS-TBL-NUM-B-CNT  PIC 9(5)   VALUE ZEROES.               
                   15  WS-TBL-NUM-H-CNT  PIC 9(5)   VALUE ZEROES.               
                   15  WS-TBL-NUM-P-CNT  PIC 9(5)   VALUE ZEROES.               
                                                                                
      *--- BP13L11F REPORT -----------------------------------------            
       01  L11F-HEADER1.                                                        
           05  FILLER                    PIC X(8)   VALUE 'BP13L11F'.           
           05  FILLER                    PIC X(4)   VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE 'HDBCAT 3'.           
           05  FILLER                    PIC X(28)  VALUE SPACES.               
           05  FILLER                    PIC X(39)  VALUE                       
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                    PIC X(17)  VALUE SPACES.               
           05  FILLER                    PIC X(6)   VALUE 'DATE: '.             
           05  L11F-DTE-CURRENT          PIC X(10)  VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE '  PAGE: '.           
           05  L11F-CNT-PAGE             PIC ZZZ9.                              
                                                                                
       01  L11F-HEADER2.                                                        
           05  FILLER                    PIC X(40)  VALUE SPACES.               
           05  FILLER                    PIC X(93)  VALUE                       
               'ERROR LIST OF OUTSTANDING CASES (SOCA - APPLICATION)'.          
                                                                                
       01  L11F-HEADER3.                                                        
           05  FILLER                    PIC X(132) VALUE                       
               '     S/NO     SCH ACCNT NO     REGN NO      ERROR DESCRI        
      -        'PTION'.                                                         
                                                                                
       01  L11F-HEADER4.                                                        
           05  FILLER                    PIC X(132) VALUE                       
               '     ----     ------------     --------     ------------        
      -        '--------------------------------------'.                        
                                                                                
       01  L11F-DETAIL.                                                         
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  L11F-SNO                  PIC ZZZ9.                              
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  L11F-NUM-HDB-REF          PIC X(11)  VALUE SPACES.               
           05  FILLER                    PIC X(6)   VALUE SPACES.               
           05  L11F-NUM-REGN             PIC X(8)   VALUE SPACES.               
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  L11F-NUM-DESC             PIC X(50)  VALUE SPACES.               
                                                                                
      *--- SERS/NON-SERS -------------------------------------------            
       COPY SERSCH.                                                             
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-BP13F800-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-F800-EOF = 'Y'.                                     
           PERFORM 4000-PROCESS-TABLE      THRU 4000-EXIT.                      
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BM06K110                                                 
                OUTPUT BP13FB41                                                 
                       BP13FB42                                                 
                       BP13L11F.                                                
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT.                      
                                                                                
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
                                                                                
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           PERFORM 8100-CONV-DATE-TO-10    THRU 8100-EXIT.                      
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13F800-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13F800 AT END                                                 
                MOVE 'Y'                   TO   WS-F800-EOF                     
                GO                         TO   2000-EXIT.                      
                                                                                
           ADD 1                           TO   WS-CNT-F800-READ.               
           MOVE SPACES                     TO   L11F-NUM-DESC.                  
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           MOVE 'N'                        TO   WS-SKIP-REC.                    
                                                                                
           MOVE SPACES                     TO   K110-REC.                       
           MOVE F800-NUM-SCH-ACC           TO   K110-SCH-ACC-NO.                
           PERFORM 3100-READ-BM06K110      THRU 3100-EXIT.                      
                                                                                
           IF WS-SKIP-REC NOT = 'Y'                                             
              PERFORM 3200-VALIDATE-REC    THRU 3200-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-SKIP-REC NOT = 'Y'                                             
              PERFORM 3300-SAVE-COUNT      THRU 3300-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-SKIP-REC NOT = 'Y'                                             
              PERFORM 3400-SAVE-REGN       THRU 3400-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 2000-BP13F800-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3100-READ-BM06K110.                                                      
      *=============================================================            
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              ADD   1                      TO   WS-CNT-K110-READ                
           WHEN 23                                                              
              MOVE 'SCHEME ACCOUNT NOT FOUND ON FIS'                            
                                           TO   L11F-NUM-DESC                   
              ADD   1                      TO   WS-CNT-K110-NFND                
              MOVE 'Y'                     TO   WS-SKIP-REC                     
              PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
           WHEN OTHER                                                           
              MOVE WS-K110-STATUS          TO   RETURN-CODE                     
              DISPLAY 'ERROR READING BM06K110, STATUS = '                       
                       WS-K110-STATUS                                           
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3200-VALIDATE-REC.                                                       
      *=============================================================            
                                                                                
           IF K110-DTE-ACCEPTANCE = SPACES OR LOW-VALUES OR ZEROES              
              STRING 'INVALID ACCPT DTE = ' K110-DTE-ACCEPTANCE                 
                      DELIMITED BY SIZE    INTO L11F-NUM-DESC                   
              MOVE 'Y'                     TO   WS-SKIP-REC                     
      *       PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
           END-IF.                                                              
                                                                                
           IF K110-DTE-KEY-ISSUED NOT = SPACES AND LOW-VALUES AND ZEROES        
              MOVE 'KEY HAS ALREADY BEEN ISSUED'                                
                                           TO   L11F-NUM-DESC                   
              MOVE 'Y'                     TO   WS-SKIP-REC                     
      *       PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
           END-IF.                                                              
                                                                                
           IF K110-DTE-KEY-AVAIL = SPACES OR LOW-VALUES OR ZEROES               
              MOVE 'KEY IS NOT YET AVAILABLE'                                   
                                           TO   L11F-NUM-DESC                   
              MOVE 'Y'                     TO   WS-SKIP-REC                     
      *       PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
           END-IF.                                                              
                                                                                
           IF F800-NUM-SCH-ACC = SPACES OR LOW-VALUES OR ZEROES                 
              MOVE 'NO SCHEME ACCOUNT '    TO   L11F-NUM-DESC                   
              MOVE 'Y'                     TO   WS-SKIP-REC                     
      *       PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
           END-IF.                                                              
                                                                                
           IF F800-NUM-STATUS = 'C'                                             
              MOVE 'REGN IS CANCELLED '    TO   L11F-NUM-DESC                   
              MOVE 'Y'                     TO   WS-SKIP-REC                     
      *       PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
           END-IF.                                                              
                                                                                
           IF F800-DTE-SALE-AGMT NOT = SPACES AND LOW-VALUES AND ZEROES         
              MOVE 'AO DATE NOT FOUND'     TO   L11F-NUM-DESC                   
              MOVE 'Y'                     TO   WS-SKIP-REC                     
      *       PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
           END-IF.                                                              
                                                                                
           IF F800-DTE-AO-APPT NOT = SPACES AND LOW-VALUES AND ZEROES           
              MOVE 'TP RECORD'             TO   L11F-NUM-DESC                   
              MOVE 'Y'                     TO   WS-SKIP-REC                     
      *       PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
           END-IF.                                                              
                                                                                
           IF F800-DTE-SO-APPT = SPACES OR LOW-VALUES OR ZEROES                 
              MOVE 'SALES ORDER DATE NOT FOUND'                                 
                                           TO   L11F-NUM-DESC                   
              MOVE 'Y'                     TO   WS-SKIP-REC                     
      *       PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-SKIP-REC = 'Y'                                                 
              ADD 1                        TO   WS-CNT-F800-SKIP                
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3300-SAVE-COUNT.                                                         
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    TAG FOR SERS OR NON-SERS                                             
      *-------------------------------------------------------------            
           SET WS-SERS-PTR                 TO   1.                              
           SEARCH WS-SERS-SCH                                                   
              AT END                                                            
                 MOVE 'N'                  TO   WS-NUM-SERS                     
              WHEN WS-SERS-SCHEME (WS-SERS-PTR) = F800-NUM-ALLO-SCHEME          
                 MOVE 'Y'                  TO   WS-NUM-SERS                     
           END-SEARCH.                                                          
                                                                                
      *-------------------------------------------------------------            
      *    COMPUTE FOR DUE DATE (ACCEPTANCE DATE + 4 MONTHS)                    
      *-------------------------------------------------------------            
           MOVE K110-DTE-ACCEPTANCE        TO   WS-DTE-FMT8.                    
           PERFORM 8200-ADD-4-MONTHS       THRU 8200-EXIT.                      
           MOVE WS-DTE-FMT8                TO   WS-DTE-DUE.                     
                                                                                
      *-------------------------------------------------------------            
      *    SEARCH SERS TAG AND DUE DATE IN LOOP. AT END, WS-IDX WILL            
      *    HOLD MATCHING INDEX OR NEXT AVAILABLE INDEX                          
      *-------------------------------------------------------------            
           MOVE 'N'                        TO   WS-DATE-FOUND.                  
           PERFORM VARYING WS-IDX FROM 1 BY 1                                   
             UNTIL (WS-TBL-DTE-DUE(WS-IDX) = ZEROES)                            
                OR (WS-DATE-FOUND = 'Y')                                        
                OR (WS-IDX > WS-IDX-MAX)                                        
              IF WS-TBL-NUM-SERS(WS-IDX) = WS-NUM-SERS AND                      
                 WS-TBL-DTE-DUE(WS-IDX)  = WS-DTE-DUE-CCYYMM                    
                 MOVE 'Y'                  TO   WS-DATE-FOUND                   
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
      *-------------------------------------------------------------            
      *    CHECK TABLE INDEX IF MAX IS ALREADY REACHED FOR NEW DATE.            
      *    IF REACHED, WRITE RECORD TO EXCEPTION REPORT                         
      *-------------------------------------------------------------            
           IF (WS-IDX > WS-IDX-MAX) AND WS-DATE-FOUND = 'N'                     
              MOVE 'TABLE RESTRICTION. REGN/DATE NOT COUNTED.'                  
                                           TO   L11F-NUM-DESC                   
              MOVE 'Y'                     TO   WS-SKIP-REC                     
              ADD   1                      TO   WS-CNT-F800-SKIP                
              PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
              GO TO 3300-EXIT                                                   
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    IF MATCH NOT FOUND, MOVE ALLO CAT AND DUE DATE TO TABLE              
      *    ELSE, SUBTRACT 1 TO INDEX TO GET MATCHING RECORD                     
      *-------------------------------------------------------------            
           IF WS-DATE-FOUND = 'N'                                               
              MOVE WS-NUM-SERS             TO   WS-TBL-NUM-SERS(WS-IDX)         
              MOVE WS-DTE-DUE-CCYYMM       TO   WS-TBL-DTE-DUE(WS-IDX)          
           ELSE                                                                 
              SUBTRACT 1                   FROM WS-IDX                          
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    ACCUMULATE COUNTERS BASED ON LOAN TAG                                
      *-------------------------------------------------------------            
           EVALUATE F800-NUM-LOAN-TAG                                           
           WHEN 'B'                                                             
              ADD 1                   TO   WS-TBL-NUM-B-CNT(WS-IDX)             
           WHEN 'H'                                                             
              ADD 1                   TO   WS-TBL-NUM-H-CNT(WS-IDX)             
           WHEN 'P'                                                             
              ADD 1                   TO   WS-TBL-NUM-P-CNT(WS-IDX)             
           WHEN OTHER                                                           
              ADD 1                   TO   WS-TBL-NUM-N-CNT(WS-IDX)             
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3400-SAVE-REGN.                                                          
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   BP13FB42-REC.                   
                                                                                
           MOVE WS-NUM-SERS                TO   FB42-NUM-SERS.                  
           MOVE WS-DTE-DUE-CCYYMM          TO   FB42-DTE-DUE.                   
           MOVE F800-NUM-LOAN-TAG          TO   FB42-NUM-LOAN-TAG.              
           MOVE F800-NUM-REGN              TO   FB42-NUM-REGN.                  
           MOVE K110-DTE-ACCEPTANCE        TO   FB42-DTE-ACCEPTANCE.            
           MOVE WS-DTE-CURR-8              TO   FB42-DTE-UPDATE.                
           MOVE 'BP13C11F'                 TO   FB42-NUM-USERID.                
                                                                                
           WRITE BP13FB42-REC.                                                  
           ADD 1                           TO   WS-CNT-FB42-WRITE.              
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4000-PROCESS-TABLE.                                                      
      *=============================================================            
                                                                                
           PERFORM VARYING WS-IDX FROM 1 BY 1                                   
             UNTIL (WS-TBL-DTE-DUE(WS-IDX) = ZEROES)                            
                OR (WS-IDX > WS-IDX-MAX)                                        
              MOVE SPACES                  TO   BP13FB41-REC                    
                                                                                
              MOVE WS-TBL-NUM-SERS(WS-IDX) TO   FB41-NUM-SERS                   
              MOVE WS-TBL-DTE-DUE(WS-IDX)  TO   FB41-DTE-DUE                    
              MOVE WS-TBL-NUM-N-CNT(WS-IDX) TO  FB41-NUM-N-CNT                  
              MOVE WS-TBL-NUM-B-CNT(WS-IDX) TO  FB41-NUM-B-CNT                  
              MOVE WS-TBL-NUM-H-CNT(WS-IDX) TO  FB41-NUM-H-CNT                  
              MOVE WS-TBL-NUM-P-CNT(WS-IDX) TO  FB41-NUM-P-CNT                  
                                                                                
              MOVE 'BP13C11F'              TO   FB41-NUM-USERID                 
              MOVE WS-DTE-CURR-8           TO   FB41-DTE-UPDATE                 
                                                                                
              WRITE BP13FB41-REC                                                
              ADD 1                        TO   WS-CNT-FB41-WRITE               
           END-PERFORM.                                                         
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8000-CONV-DATE-TO-8.                                                     
      *=============================================================            
      *    INPUT  - WS-DTE-FMT10                                                
      *    OUTPUT - WS-DTE-FMT8                                                 
      *-------------------------------------------------------------            
                                                                                
           MOVE WS-DTE-FMT10-CCYY  TO  WS-DTE-FMT8-CCYY.                        
           MOVE WS-DTE-FMT10-MM    TO  WS-DTE-FMT8-MM.                          
           MOVE WS-DTE-FMT10-DD    TO  WS-DTE-FMT8-DD.                          
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8100-CONV-DATE-TO-10.                                                    
      *=============================================================            
      *    INPUT  - WS-DTE-FMT8                                                 
      *    OUTPUT - WS-DTE-FMT10                                                
      *-------------------------------------------------------------            
                                                                                
           MOVE WS-DTE-FMT8-CCYY   TO  WS-DTE-FMT10-CCYY.                       
           MOVE WS-DTE-FMT8-MM     TO  WS-DTE-FMT10-MM.                         
           MOVE WS-DTE-FMT8-DD     TO  WS-DTE-FMT10-DD.                         
                                                                                
       8100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8200-ADD-4-MONTHS.                                                       
      *=============================================================            
      *    INPUT  - WS-DTE-FMT8                                                 
      *    OUTPUT - WS-DTE-FMT8                                                 
      *-------------------------------------------------------------            
                                                                                
           ADD  4                  TO  WS-DTE-FMT8-MM.                          
                                                                                
           IF WS-DTE-FMT8-MM > 12                                               
              ADD 1                TO  WS-DTE-FMT8-CCYY                         
              COMPUTE WS-DTE-FMT8-MM = WS-DTE-FMT8-MM - 12                      
           END-IF.                                                              
                                                                                
       8200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8900-PRINT-ERROR.                                                        
      *=============================================================            
                                                                                
           IF WS-CNT-LINE > 50                                                  
              PERFORM 8910-PRINT-ERROR-HDR THRU 8910-EXIT                       
           END-IF.                                                              
                                                                                
           ADD  1                          TO   WS-CNT-SNO                      
                                                WS-CNT-LINE.                    
           MOVE WS-CNT-SNO                 TO   L11F-SNO.                       
           MOVE F800-NUM-SCH-ACC           TO   L11F-NUM-HDB-REF.               
           MOVE F800-NUM-REGN              TO   L11F-NUM-REGN.                  
                                                                                
           WRITE BP13L11F-REC              FROM L11F-DETAIL.                    
           ADD 1                           TO   WS-CNT-RPT-PRINT.               
                                                                                
       8900-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8910-PRINT-ERROR-HDR.                                                    
      *=============================================================            
                                                                                
           ADD 1                           TO   WS-CNT-PAGE.                    
           MOVE WS-CNT-PAGE                TO   L11F-CNT-PAGE.                  
           MOVE WS-DTE-CURR-10             TO   L11F-DTE-CURRENT.               
                                                                                
           WRITE BP13L11F-REC FROM L11F-HEADER1 AFTER PAGE.                     
           WRITE BP13L11F-REC FROM L11F-HEADER2.                                
           WRITE BP13L11F-REC FROM L11F-HEADER3 AFTER 2.                        
           WRITE BP13L11F-REC FROM L11F-HEADER4.                                
                                                                                
           MOVE SPACES                     TO   BP13L11F-REC.                   
           WRITE BP13L11F-REC.                                                  
                                                                                
           MOVE 6                          TO   WS-CNT-LINE.                    
                                                                                
       8910-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*------- BP13C11F CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13F800 READ      : ' WS-CNT-F800-READ.          
           DISPLAY ' 2. NO OF BP13F800 SKIPPED   : ' WS-CNT-F800-SKIP.          
           DISPLAY ' 3. NO OF BM06K110 READ      : ' WS-CNT-K110-READ.          
           DISPLAY ' 4. NO OF BM06K110 NOT FOUND : ' WS-CNT-K110-NFND.          
           DISPLAY ' 5. NO OF BP13FB41 WRITTEN   : ' WS-CNT-FB41-WRITE.         
           DISPLAY ' 6. NO OF BP13FB42 WRITTEN   : ' WS-CNT-FB42-WRITE.         
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13F800                                                       
                 BM06K110                                                       
                 BP13FB41                                                       
                 BP13FB42                                                       
                 BP13L11F.                                                      
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO   RETURN-CODE.                    
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
