       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C41B.                                                 
       AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  23 JUN 2010.                                              
      ***************************************************************           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      ***************************************************************           
      * OBJECTIVE: PRINT REPORT ON CLOSING CEREMONY FILE            *           
      *-------------------------------------------------------------*           
      * INPUT FILES: BM06F190                                       *           
      *              BM06K510                                       *           
      *              BP13K421                                       *           
      *                                                             *           
      * OUTPUT FILE: BP13L41B (REPORT)                              *           
      *-------------------------------------------------------------*           
      * CHG REF  OIC  DATE    DESCRIPTION                           *           
      *--------- ---  ------  --------------------------------------*           
      * BP133926 ESA1 230610  NEW PROGRAM                           *           
      ***************************************************************           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06K510  ASSIGN       TO BM06K510                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS RANDOM                              
                            RECORD KEY   IS K510-ESTCDE                         
                            FILE STATUS  IS WS-K510-STATUS.                     
                                                                                
           SELECT BP13K421  ASSIGN       TO BP13K421                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K421-KEY-FLD                        
                            FILE STATUS  IS WS-K421-STATUS.                     
                                                                                
           SELECT BM06F190  ASSIGN TO BM06F190.                                 
                                                                                
           SELECT BP13L41B  ASSIGN TO BP13L41B.                                 
                                                                                
       DATA DIVISION.                                                           
      *--------------------------------------------------------------           
       FILE SECTION.                                                            
      *--------------------------------------------------------------           
                                                                                
       FD  BM06K510                                                             
           RECORD CONTAINS 60  CHARACTERS.                                      
       COPY BM06K510.                                                           
                                                                                
       FD  BP13K421                                                             
           RECORD CONTAINS 450 CHARACTERS.                                      
       COPY BP13K421.                                                           
                                                                                
       FD  BM06F190                                                             
           RECORD CONTAINS 650 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       COPY BM06F190.                                                           
                                                                                
       FD  BP13L41B                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS OMITTED                                                
           RECORDING MODE IS F.                                                 
       01  BP13L41B-REC                   PIC X(132).                           
                                                                                
      *--------------------------------------------------------------           
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------           
       01  WS-WORK-VAR.                                                         
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-DATE            PIC X(08) VALUE SPACES.               
               10  FILLER              PIC X(13) VALUE SPACES.                  
           05  WS-DISP-DATE.                                                    
               10  WS-DISP-DD          PIC X(02) VALUE SPACES.                  
               10  FILLER              PIC X(01) VALUE '/'.                     
               10  WS-DISP-MM          PIC X(02) VALUE SPACES.                  
               10  FILLER              PIC X(01) VALUE '/'.                     
               10  WS-DISP-CCYY        PIC X(04) VALUE SPACES.                  
           05  WS-RETCODE              PIC -ZZZZ.                               
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K100-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K510-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K421-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-END-OF-F190-SW       PIC X(01) VALUE SPACES.                  
               88  C-END-OF-F190       VALUE 'Y'.                               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F190-READ-CTR        PIC 9(06) VALUE ZEROES.                  
           05  WS-SNO                  PIC 9(04) VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(02) VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(04) VALUE ZEROES.                  
                                                                                
      *--------------------------------------------------------------           
      *   REPORT LAYOUT                                                         
      *--------------------------------------------------------------           
       01 L41B-HEADER-1.                                                        
          05 FILLER              PIC X(08)  VALUE 'BP13L41B'.                   
          05 FILLER              PIC X(05)  VALUE SPACES .                      
          05 FILLER              PIC X(08)  VALUE 'HDBCAT3'.                    
          05 FILLER              PIC X(22)  VALUE SPACES.                       
          05 FILLER              PIC X(40)  VALUE                               
             'S Y S T E M   O F   C O M M I T M E N T'.                         
          05 FILLER              PIC X(28)  VALUE SPACES.                       
          05 FILLER              PIC X(07)  VALUE 'DATE :'.                     
          05 L41B-DATE           PIC X(10)  VALUE SPACES.                       
                                                                                
       01 L41B-HEADER-2.                                                        
          05 FILLER              PIC X(45)  VALUE SPACES .                      
          05 FILLER              PIC X(50)  VALUE                               
             'REPORT ON CLOSING CEREMONY FILE'.                                 
          05 FILLER              PIC X(16)  VALUE SPACES .                      
          05 FILLER              PIC X(07)  VALUE 'PAGE : '.                    
          05 L41B-PAGE           PIC ZZZ9 .                                     
                                                                                
       01 L41B-HEADER-3.                                                        
          05 FILLER              PIC X(13)  VALUE                               
             'ESTATE    :  '.                                                   
          05 L41B-ESTATE         PIC X(27)  VALUE SPACES.                       
          05 FILLER              PIC X(20)  VALUE SPACES.                       
          05 FILLER              PIC X(13)  VALUE                               
             'NGH / CNTR : '.                                                   
          05 L41B-NEIGH          PIC X(04)  VALUE SPACES.                       
          05 FILLER              PIC X(03)  VALUE                               
             ' / '.                                                             
          05 L41B-CONTRACT       PIC X(04)  VALUE SPACES.                       
          05 FILLER              PIC X(20)  VALUE SPACES.                       
          05 FILLER              PIC X(13)  VALUE                               
             'CONFIRM    : '.                                                   
          05 L41B-CONFIRM        PIC X(01)  VALUE SPACES.                       
                                                                                
       01 L41B-HEADER-4.                                                        
          05 FILLER              PIC X(13)  VALUE                               
             'PROJECT   :  '.                                                   
          05 L41B-PROJECT        PIC X(100) VALUE SPACES.                       
                                                                                
       01 L41B-HEADER-5.                                                        
          05 FILLER              PIC X(13)  VALUE                               
             'BLK NO    :  '.                                                   
          05 L41B-BLK            PIC X(05)  VALUE SPACES.                       
                                                                                
       01 L41B-HEADER-6.                                                        
          05 FILLER              PIC X(27)  VALUE                               
             'DATE COMPLETION CEREMONY : '.                                     
          05 L41B-CC             PIC X(10)  VALUE SPACES.                       
                                                                                
       01 L41B-HEADER-7.                                                        
          05 FILLER              PIC X(04)  VALUE 'S/NO'.                       
          05 FILLER              PIC X(10)  VALUE SPACES.                       
          05 FILLER              PIC X(09)  VALUE 'REGN NO. '.                  
                                                                                
       01 L41B-DETAIL.                                                          
          05 L41B-SNO            PIC ZZZ9.                                      
          05 FILLER              PIC X(10)  VALUE SPACES.                       
          05 L41B-REGN-NUM       PIC X(09)  VALUE SPACES.                       
                                                                                
       01  L41B-LINE             PIC X(132)    VALUE ALL '-'.                   
                                                                                
      *--------------------------------------------------------------           
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------------           
                                                                                
      *--------------------------------*                                        
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 1000-INITIALIZATION    THRU 1000-EXIT.                       
           PERFORM 2000-PROCESS-RECORDS   THRU 2000-EXIT                        
             UNTIL C-END-OF-F190.                                               
           PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       1000-INITIALIZATION.                                                     
      *--------------------------------*                                        
                                                                                
           OPEN INPUT    BM06F190                                               
                         BM06K510                                               
                         BP13K421                                               
                OUTPUT   BP13L41B.                                              
                                                                                
           IF WS-K510-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR-BM06K510-STATUS ' WS-K510-STATUS              
              MOVE WS-K510-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K421-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR-BP13K421-STATUS ' WS-K421-STATUS              
              MOVE WS-K421-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-SYSTEM-DATE.                       
           MOVE WS-SYS-DATE(1:4)       TO WS-DISP-CCYY.                         
           MOVE WS-SYS-DATE(5:2)       TO WS-DISP-MM.                           
           MOVE WS-SYS-DATE(7:2)       TO WS-DISP-DD.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2000-PROCESS-RECORDS.                                                    
      *--------------------------------*                                        
                                                                                
           READ BM06F190 AT END                                                 
                MOVE 'Y'                TO WS-END-OF-F190-SW                    
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1   TO WS-F190-READ-CTR.                                         
                                                                                
           PERFORM 2200-READ-BM06K510              THRU 2200-EXIT.              
           PERFORM 2300-START-BP13K421             THRU 2300-EXIT.              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2200-READ-BM06K510.                                                      
      *--------------------------------*                                        
                                                                                
           MOVE F190-2-ESTATE         TO K510-ESTCDE.                           
           READ BM06K510.                                                       
                                                                                
           EVALUATE WS-K510-STATUS                                              
              WHEN 00                                                           
                   CONTINUE                                                     
              WHEN 23                                                           
                   DISPLAY 'RECORD NOT FOUND IN BM06K510: ' K510-ESTCDE         
                   MOVE SPACES     TO K510-REC                                  
              WHEN OTHER                                                        
                   DISPLAY 'READ ERROR BM06K510,STATUS ' WS-K510-STATUS         
                   MOVE WS-K510-STATUS        TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2300-START-BP13K421.                                                     
      *--------------------------------*                                        
                                                                                
            MOVE SPACES               TO K421-KEY-FLD.                          
            MOVE F190-REC2(1:16)      TO K421-KEY-FLD.                          
                                                                                
            START BP13K421 KEY >= K421-KEY-FLD.                                 
            EVALUATE WS-K421-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
               PERFORM 2400-READ-BP13K421      THRU 2400-EXIT                   
                 UNTIL K421-KEY-FLD(1:16) NOT = F190-REC2(1:16)                 
                      OR K421-KEY-FLD = HIGH-VALUE                              
             WHEN 23                                                            
               DISPLAY 'K421-KEY-FLD : ' K421-KEY-FLD ' NOT FOUND IN '          
                       'BP13K421!'                                              
             WHEN OTHER                                                         
               DISPLAY 'BP13K421 START ERROR ' WS-K421-STATUS                   
               DISPLAY 'BP13K421 RECORD KEY IS ' K421-KEY-FLD                   
               PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                        
            END-EVALUATE.                                                       
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2400-READ-BP13K421.                                                      
      *--------------------------------*                                        
           READ BP13K421 NEXT AT END                                            
             MOVE HIGH-VALUE TO K421-KEY-FLD.                                   
                                                                                
           EVALUATE WS-K421-STATUS                                              
             WHEN 00                                                            
             WHEN 02                                                            
                 IF K421-KEY-FLD(1:16) = F190-REC2(1:16)                        
                    PERFORM 3000-PRINT-REPORT  THRU 3000-EXIT                   
                 ELSE                                                           
                    MOVE HIGH-VALUE TO K421-KEY-FLD                             
                 END-IF                                                         
             WHEN 10                                                            
             WHEN 23                                                            
                  MOVE HIGH-VALUE TO K421-KEY-FLD                               
             WHEN OTHER                                                         
                  DISPLAY 'BP13K421 READ ERROR  : ' WS-K421-STATUS              
                  DISPLAY 'BP13K421 RECORD KEY  : ' K421-KEY-FLD                
                  MOVE WS-K421-STATUS          TO  RETURN-CODE                  
                  PERFORM  9000-CLOSE-FILES    THRU  9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       2400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3000-PRINT-REPORT.                                                       
      *--------------------------------*                                        
                                                                                
           IF WS-LINE-CTR > 56  OR                                              
              WS-LINE-CTR = 0                                                   
              PERFORM 3110-PRINT-PAGE-BREAK   THRU 3110-EXIT                    
           END-IF.                                                              
                                                                                
           ADD 1                      TO WS-SNO.                                
           MOVE WS-SNO                TO L41B-SNO.                              
           MOVE K421-NUM-REGN         TO L41B-REGN-NUM.                         
           WRITE BP13L41B-REC FROM L41B-DETAIL.                                 
           ADD 1                      TO WS-LINE-CTR.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------*                                        
       3110-PRINT-PAGE-BREAK.                                                   
      *--------------------------------*                                        
                                                                                
           MOVE K510-ESTATE-NAME              TO L41B-ESTATE.                   
           MOVE F190-2-NEIGHBOURHOOD          TO L41B-NEIGH.                    
           MOVE F190-2-CONTRACT-NO            TO L41B-CONTRACT.                 
           MOVE F190-2-BLK-NO                 TO L41B-BLK.                      
                                                                                
           MOVE K421-NME-PROJECT      TO L41B-PROJECT.                          
           MOVE K421-NUM-CONFIRM-TAG  TO L41B-CONFIRM.                          
           STRING K421-DTE-COMPLETION-CEREMONY(7:2) '/'                         
                  K421-DTE-COMPLETION-CEREMONY(5:2) '/'                         
                  K421-DTE-COMPLETION-CEREMONY(1:4)                             
                  DELIMITED BY SIZE INTO L41B-CC.                               
                                                                                
           ADD   1                            TO WS-PAGE-CTR.                   
           MOVE WS-PAGE-CTR                   TO L41B-PAGE.                     
           MOVE WS-DISP-DATE                  TO L41B-DATE.                     
           WRITE BP13L41B-REC  FROM L41B-HEADER-1 AFTER PAGE.                   
           WRITE BP13L41B-REC  FROM L41B-HEADER-2.                              
           WRITE BP13L41B-REC  FROM L41B-LINE     AFTER 2.                      
           WRITE BP13L41B-REC  FROM L41B-HEADER-3.                              
           WRITE BP13L41B-REC  FROM L41B-HEADER-4.                              
           WRITE BP13L41B-REC  FROM L41B-HEADER-5.                              
           WRITE BP13L41B-REC  FROM L41B-HEADER-6.                              
           WRITE BP13L41B-REC  FROM L41B-LINE.                                  
           WRITE BP13L41B-REC  FROM L41B-HEADER-7 AFTER 3.                      
           MOVE SPACES                        TO BP13L41B-REC.                  
           WRITE BP13L41B-REC.                                                  
           MOVE 13                            TO WS-LINE-CTR.                   
                                                                                
       3110-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------*                                        
       9000-CLOSE-FILES.                                                        
      *--------------------------------*                                        
                                                                                
           DISPLAY 'PROGRAM ID   : BP13C41B'.                                   
           DISPLAY 'CURRENT DATE : ' WS-DISP-DATE.                              
           DISPLAY SPACES.                                                      
           DISPLAY '*=============== COUNTERS ===============*'.                
           DISPLAY 'BM06F190 RECORDS READ       : ' WS-F190-READ-CTR.           
           DISPLAY 'NO. OF RECORDS PRINTED      : ' WS-SNO.                     
                                                                                
           CLOSE BM06K510                                                       
                 BP13K421                                                       
                 BM06F190                                                       
                 BP13L41B.                                                      
                                                                                
            IF WS-K510-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR-BM06K510-STATUS ' WS-K510-STATUS            
            END-IF.                                                             
                                                                                
            IF WS-K421-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR-BP13K421-STATUS ' WS-K421-STATUS            
            END-IF.                                                             
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
