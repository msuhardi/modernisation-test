       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C059.                                                 
       AUTHOR.        MCC.                                                      
      *DATE-WRITTEN : 26 JAN 2000.                                              
      *----------------------------------------------------------------*        
      *                                                                *        
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                            *        
      *                                                                *        
      *  SYSTEM ID   : BP13                                            *        
      *                                                                *        
      *  OBJECTIVE   : PROGRAM TO MATCH WITH THE BALLOT FILE TO EXTRACT*        
      *                THE WAITING LIST OCCUPIER INFORMATION FOR HOUSE-*        
      *                HOLD CHECK                                      *        
      *                                                                *        
      *----------------------------------------------------------------*        
      *  REQ NO   OIC   DATE   DESCRIPTION                             *        
      *  ======   ===   ====   ===========                             *        
      * BP130718  MCC  26012000 NEW PROGRAM                            *        
      * BP130897  AKT  31072000 F558 EXPAND TO X(600)                  *        
      * BP131064  ANC1 14012002 TO CATER FOR BLANK DTE-ISSUE-REGN      *        
      * BP132171  ED   13112002 CHG HARD-CODE FOR RUN-DATE COMPARE     *        
      * BP132360  CKK  12062003 TO READ BP13K703 FOR DTE-BALLOT        *        
      * BP132583  CKK  26082004 TO OUTPUT ALL OCCUPIER FOR MCPS CASES  *        
      * BP132626  SK   20041124 CHANGE DTE-REGN TO DTE-REQ             *        
      * BP133893  ZDD1 20100518 REMOVED THE USE OF BP13K703            *        
      *----------------------------------------------------------------*        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F500 ASSIGN TO BP13F500.                                  
           SELECT BP13F825 ASSIGN TO BP13F825.                                  
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F500                RECORD CONTAINS 500 CHARACTERS               
                                   RECORDING MODE IS F.                         
       COPY BP13F500.                                                           
                                                                                
       FD  BP13F825                RECORD CONTAINS 200 CHARACTERS               
                                   RECORDING MODE IS F.                         
       COPY BP13F825.                                                           
                                                                                
       FD  BP13F558                RECORD CONTAINS 600 CHARACTERS               
                                   RECORDING MODE IS F                          
                                   BLOCK CONTAINS  0 RECORDS                    
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F558.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-F500-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-F825-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-F500-NMR             PIC 9(8)    VALUE ZERO.                      
       01  WS-F825-NMR             PIC 9(8)    VALUE ZERO.                      
       01  WS-F558-WRITE           PIC 9(8)    VALUE ZERO.                      
       01  WS-F825-BYPASS          PIC 9(8)    VALUE ZERO.                      
       01  WS-MATCH-CTR            PIC 9(8)    VALUE ZERO.                      
                                                                                
       01  WS-RUN-DATE.                                                         
           05  WS-YY               PIC X(4).                                    
           05  WS-MM               PIC X(2).                                    
           05  WS-DD               PIC X(2).                                    
                                                                                
       01  WS-EOF-F500             PIC X(3).                                    
           88  EOF-OF-F500                     VALUE 'YES'.                     
       01  WS-EOF-F825             PIC X(3).                                    
           88  EOF-OF-F825                     VALUE 'YES'.                     
                                                                                
       01  WS-DTE-REGN.                                                         
           05  WS-REGN-YYMM        PIC X(6).                                    
           05  WS-REGN-DD          PIC X(2).                                    
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       000-MAIN-LOGIC.                                                          
           PERFORM 100-INITIALIZATION                                           
              THRU 100-EXIT.                                                    
           PERFORM 200-READ-F500                                                
              THRU 200-EXIT.                                                    
           PERFORM 300-READ-F825                                                
              THRU 300-EXIT.                                                    
           PERFORM 400-PROCESS                                                  
              THRU 400-EXIT                                                     
             UNTIL F500-NUM-REGN = HIGH-VALUE                                   
              AND  F825-NUM-REGN = HIGH-VALUE.                                  
           PERFORM 900-CLOSE-ROUTINE                                            
              THRU 900-EXIT.                                                    
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-INITIALIZATION.                                                      
                                                                                
           OPEN INPUT  BP13F500                                                 
                       BP13F825                                                 
                OUTPUT BP13F558.                                                
                                                                                
                                                                                
           MOVE 'NO' TO WS-EOF-F500.                                            
           MOVE 'NO' TO WS-EOF-F825.                                            
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-RUN-DATE.                    
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-F500.                                                           
                                                                                
           READ BP13F500 AT END MOVE 'YES' TO WS-EOF-F500                       
                                MOVE HIGH-VALUE TO F500-NUM-REGN                
               GO TO 200-EXIT.                                                  
            ADD 1 TO WS-F500-READ.                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-READ-F825.                                                           
                                                                                
           READ BP13F825 AT END MOVE 'YES' TO WS-EOF-F825                       
                                MOVE HIGH-VALUE TO F825-NUM-REGN                
               GO TO 300-EXIT.                                                  
            ADD 1 TO WS-F825-READ.                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *  MATCH  BY REF-NO                                             *         
      *****************************************************************         
                                                                                
       400-PROCESS.                                                             
                                                                                
           IF F500-NUM-REGN = F825-NUM-REGN                                     
              ADD  +1          TO WS-MATCH-CTR                                  
      *       IF F500-NUM-ALLO-CAT = 'BE ' OR 'BTO'                             
      *          PERFORM 450-READ-BP13K703 THRU 450-EXIT                        
      *       END-IF                                                            
              PERFORM 500-WRITE-F558  THRU 500-EXIT                             
                UNTIL F825-NUM-REGN NOT = F500-NUM-REGN                         
                   OR WS-EOF-F825 = 'YES'                                       
              PERFORM 200-READ-F500 THRU 200-EXIT                               
           ELSE                                                                 
              IF F500-NUM-REGN   < F825-NUM-REGN                                
                 ADD 1  TO WS-F500-NMR                                          
                 PERFORM 200-READ-F500 THRU 200-EXIT                            
              ELSE                                                              
                 ADD 1  TO WS-F825-NMR                                          
                 PERFORM 300-READ-F825 THRU 300-EXIT.                           
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       500-WRITE-F558.                                                          
                                                                                
           IF (F825-NUM-LESSEE-CHECK = 'Y') OR                                  
              (F500-NUM-MCPS NOT = SPACES AND LOW-VALUES)                       
              MOVE SPACES                   TO BP13F558-REC                     
              MOVE F825-NUM-REGN            TO F558-NUM-REGN                    
              MOVE F825-NUM-NRIC            TO F558-NUM-NRIC                    
              MOVE F825-NUM-ETHNIC          TO F558-NUM-ETHNIC                  
              MOVE F825-NUM-SEX             TO F558-NUM-SEX                     
              MOVE F825-NUM-MARITAL-STATUS  TO F558-NUM-MARITAL-STATUS          
              MOVE F825-DTE-BIRTH           TO F558-DTE-BIRTH                   
              MOVE F825-NUM-CITIZENSHIP     TO F558-NUM-CITIZENSHIP             
              MOVE F825-NUM-RELATIONSHIP    TO F558-CDE-RELATIONSHIP            
              MOVE F825-NUM-LESSEE-CHECK    TO F558-CDE-LESSEE-CHECK            
                                                                                
              MOVE F500-CDE-FLAT-TYPE TO F558-NUM-FLAT-TY-SOC                   
              IF  F500-CDE-NT1 = SPACES OR LOW-VALUES                           
                  MOVE F500-CDE-ZONE        TO F558-NUM-ZONE-NT-SOC             
              ELSE                                                              
                  MOVE F500-CDE-NT1         TO F558-NUM-ZONE-NT-SOC             
              END-IF                                                            
                                                                                
              IF F500-DTE-REQUEST NOT = SPACES AND LOW-VALUES                   
                                               AND ALL '0'                      
                 MOVE F500-DTE-REQUEST      TO F558-DTE-ISSUE-REGN              
              ELSE                                                              
                 IF F500-DTE-BALLOT NOT = SPACES AND LOW-VALUES AND             
                                          ALL '0'                               
                    MOVE F500-DTE-BALLOT       TO WS-REGN-YYMM                  
                    MOVE '21'                  TO WS-REGN-DD                    
                    MOVE WS-DTE-REGN           TO F558-DTE-ISSUE-REGN           
                 ELSE                                                           
                    MOVE WS-RUN-DATE           TO F558-DTE-ISSUE-REGN           
                 END-IF                                                         
              END-IF                                                            
                                                                                
              IF  F825-NUM-NRIC = F500-NUM-NRIC1 OR                             
                                  F500-NUM-NRIC2 OR                             
                                  F500-NUM-NRIC3 OR                             
                                  F500-NUM-NRIC4                                
                  MOVE 'Y'                  TO F558-NUM-APPLN                   
              END-IF                                                            
                                                                                
              IF F825-NUM-NRIC = 'X88888888' OR 'X77777777'                     
                ADD 1                         TO WS-F825-BYPASS                 
              ELSE                                                              
                WRITE BP13F558-REC                                              
                ADD +1                        TO WS-F558-WRITE                  
              END-IF                                                            
           ELSE                                                                 
              ADD 1                         TO WS-F825-BYPASS.                  
                                                                                
           PERFORM 300-READ-F825  THRU 300-EXIT.                                
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
                                                                                
             DISPLAY '*--------BP13C059  -----------*'.                         
             DISPLAY ' '.                                                       
             DISPLAY ' RUN DATE          : ' WS-DD '/' WS-MM '/' WS-YY.         
             DISPLAY ' '.                                                       
             DISPLAY ' BP13F500-READ     : ' WS-F500-READ.                      
             DISPLAY ' BP13F825-READ     : ' WS-F825-READ.                      
             DISPLAY ' MATCHED RECORDS   : ' WS-MATCH-CTR.                      
             DISPLAY ' BP13F558-WRITTEN  : ' WS-F558-WRITE.                     
             DISPLAY ' BP13F825-BYPASS   : ' WS-F825-BYPASS.                    
             DISPLAY ' BP13F825-NOMATCH  : ' WS-F825-NMR.                       
             DISPLAY ' BP13F500-NOMATCH  : ' WS-F500-NMR.                       
                                                                                
      *----> WS-MATCH-CTR - WS-F558-WRITE = NUMBER OF MATCHED RECORDS           
      *                                     WITH LESSEE-CHECK NOT = 'Y'         
                                                                                
           CLOSE      BP13F500                                                  
                      BP13F825                                                  
                      BP13F558.                                                 
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
