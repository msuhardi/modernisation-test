       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB78.                                                 
      *AUTHOR.        LLOYD CAPARRO.                                            
      *                                                                         
      ************************************************************              
      *                   SOC SYSTEM (SOC)                       *              
      *==========================================================*              
      * DATE WRITTEN      : 20 FEB 2005                          *              
      *                                                          *              
      * 1. OBJECTIVE      : CREATE 2 FILES FROM BP13F595 WITH    *              
      *                   : FOUND AND NOT FOUND CASES IN         *              
      *                   : BP13K820 AND BP13K800 FILES          *              
      *                                                          *              
      * 2. INPUT          : BP13F595 - BP13.F595.C996.NOTFOUND   *              
      *                   : BP13K820 - BP13.K820.OCCUP           *              
      *                   : BP13K800 - BP13.K800.APPLN           *              
      *                                                          *              
      * 3. OUTPUT         : P13F595F - BP13.F595.C995.FOUND      *              
      *                   : P13F595N - BP13.F595.C995.NOTFOUND   *              
      *==========================================================*              
      *  MODIFICATIONS:                                          *              
      *                                                          *              
      *  CHGE-NO  DATE       OIC  DESCRIPTION                    *              
      *  -------- ---------- ---- ------------------------------ *              
      *  BP132667 20/02/2005 CG7  NEW PROGRAM.                   *              
      *  BP134247 09/06/2011 PCL3 TO READ BP13K893 AND BP13K895  *              
      *  BP134780 21/02/2013 IMC1 ADD CHECKING OF NRIC           *              
      ************************************************************              
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F595     ASSIGN        TO BP13F595.                       
                                                                                
           SELECT BP13K820     ASSIGN        TO BP13K820                        
                               ACCESS MODE   IS DYNAMIC                         
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K820-KEY-FLD                    
                               ALTERNATE RECORD KEY IS K820-NUM-NRIC            
                                                WITH DUPLICATES                 
                               FILE STATUS   IS WS-K820-STATUS.                 
                                                                                
           SELECT BP13K800     ASSIGN        TO BP13K800                        
                               ACCESS MODE   IS RANDOM                          
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K800-NUM-REGN                   
                               FILE STATUS   IS WS-K800-STATUS.                 
                                                                                
           SELECT  BP13K895    ASSIGN        TO BP13K895                        
                               ACCESS MODE   IS DYNAMIC                         
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K895-KEY-FLD                    
                        ALTERNATE RECORD KEY IS K895-NUM-NRIC-HIST              
                                                WITH DUPLICATES                 
                               FILE STATUS   IS WS-K895-STATUS.                 
                                                                                
           SELECT  BP13K893    ASSIGN        TO BP13K893                        
                               ACCESS MODE   IS DYNAMIC                         
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K893-KEY-FLD                    
                               FILE STATUS   IS WS-K893-STATUS.                 
                                                                                
           SELECT P13F595F     ASSIGN        TO P13F595F.                       
                                                                                
           SELECT P13F595N     ASSIGN        TO P13F595N.                       
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F595                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       COPY BP13F595.                                                           
                                                                                
       FD  BP13K820                                                             
                               RECORD CONTAINS 400 CHARACTERS.                  
       COPY BP13K820.                                                           
                                                                                
       FD  BP13K895                                                             
                               RECORD CONTAINS 450 CHARACTERS.                  
       COPY BP13K895.                                                           
                                                                                
       FD  BP13K893                                                             
                               RECORD CONTAINS 2050 CHARACTERS.                 
       COPY BP13K893.                                                           
                                                                                
       FD  BP13K800                                                             
                               RECORD CONTAINS 2000 CHARACTERS.                 
       COPY BP13K800.                                                           
                                                                                
       FD  P13F595F                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       01  P13F595F-REC                  PIC X(500).                            
                                                                                
       FD  P13F595N                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       01  P13F595N-REC                  PIC X(500).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILE-STATUS.                                                         
           05  WS-K820-STATUS            PIC 9(2)  VALUE ZEROES.                
           05  WS-K800-STATUS            PIC 9(2)  VALUE ZEROES.                
           05  WS-K893-STATUS            PIC 9(2)  VALUE ZEROES.                
           05  WS-K895-STATUS            PIC 9(2)  VALUE ZEROES.                
                                                                                
       01  END-OF-FILE-FLAGS.                                                   
           05  WS-F595-EOF               PIC X     VALUE 'N'.                   
           05  WS-K820-EOF               PIC X     VALUE 'N'.                   
           05  WS-K895-EOF               PIC X     VALUE 'N'.                   
                                                                                
       01  COUNTERS.                                                            
           05  WS-F595-READ-CNT          PIC 9(6)  VALUE ZEROES.                
           05  WS-595F-WRITE-CNT         PIC 9(6)  VALUE ZEROES.                
           05  WS-595N-WRITE-CNT         PIC 9(6)  VALUE ZEROES.                
           05  WS-NRIC-LIST-CNT          PIC 9(1)  VALUE ZEROES.                
           05  WS-CNT                    PIC 9(1)  VALUE ZERO.                  
           05  WS-CANCEL-NOTELIG         PIC 9(6)  VALUE ZERO.                  
           05  WS-CANCEL-NOTBK           PIC 9(6)  VALUE ZERO.                  
           05  WS-CANCEL-BALLOT          PIC 9(6)  VALUE ZERO.                  
           05  WS-BE-AUG                 PIC 9(6)  VALUE ZERO.                  
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURRENT-DATE1          PIC X(8) VALUE SPACES.                 
           05  WS-CURRENT-TIME           PIC X(9) VALUE SPACES.                 
               05  WS-TIME REDEFINES WS-CURRENT-TIME.                           
                   10  WS-HHMMSSMS       PIC X(8).                              
                   10  WS-GM             PIC X(1).                              
           05  FILLER                    PIC X(4) VALUE SPACES.                 
                                                                                
       01  TEMPORARY-VARIABLES.                                                 
           05  WS-NRIC OCCURS 2 TIMES.                                          
               10 WS-NRIC-LIST           PIC X(9)  VALUE SPACES.                
           05  WS-RUN-DATE               PIC X(10) VALUE SPACES.                
           05  WS-NRIC-FOUND             PIC X(1)  VALUE 'N'.                   
           05  WS-K820-REC-PROCESS       PIC X(1)  VALUE 'N'.                   
           05  WS-K820-NRIC-PREV         PIC X(9)  VALUE SPACES.                
           05  WS-K895-REC-PROCESS       PIC X(1)  VALUE 'N'.                   
           05  WS-K895-NRIC-PREV         PIC X(9)  VALUE SPACES.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *************************************************************             
       0000-MAIN.                                                               
      *************************************************************             
           PERFORM 1000-OPEN-ROUTINE THRU 1000-EXIT.                            
           PERFORM 2000-PROCESS-RECORDS THRU 2000-EXIT                          
             UNTIL WS-F595-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       1000-OPEN-ROUTINE.                                                       
      *************************************************************             
           OPEN INPUT  BP13F595                                                 
                       BP13K820                                                 
                       BP13K800                                                 
                       BP13K893                                                 
                       BP13K895                                                 
                OUTPUT P13F595F                                                 
                       P13F595N.                                                
                                                                                
           IF WS-K820-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K820 OPEN ERROR ' WS-K820-STATUS                     
              MOVE WS-K820-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K800 OPEN ERROR ' WS-K800-STATUS                     
              MOVE WS-K800-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K893-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K893 OPEN ERROR ' WS-K893-STATUS                     
              MOVE WS-K893-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K895-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K895 OPEN ERROR ' WS-K895-STATUS                     
              MOVE WS-K895-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE    TO WS-CURRENT-DATE.                    
                                                                                
           STRING WS-CURRENT-DATE1(7:2) '/'                                     
                  WS-CURRENT-DATE1(5:2) '/'                                     
                  WS-CURRENT-DATE1(1:4)                                         
                  DELIMITED BY SIZE                                             
             INTO WS-RUN-DATE                                                   
                                                                                
           PERFORM 1100-READ-BP13F595 THRU 1100-EXIT.                           
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       1100-READ-BP13F595.                                                      
      *************************************************************             
           READ BP13F595                                                        
             AT END                                                             
                MOVE 'Y'                 TO WS-F595-EOF                         
            NOT AT END                                                          
                ADD 1                    TO WS-F595-READ-CNT                    
           END-READ.                                                            
                                                                                
       1100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       2000-PROCESS-RECORDS.                                                    
      *************************************************************             
           MOVE SPACES                   TO WS-NRIC-LIST(1)                     
                                            WS-NRIC-LIST(2).                    
                                                                                
           MOVE F595-NUM-NRIC1           TO WS-NRIC-LIST(1).                    
           MOVE 1                        TO WS-NRIC-LIST-CNT.                   
                                                                                
           IF F595-NUM-NRIC2 NOT = SPACES AND LOW-VALUES AND                    
              F595-NUM-NRIC2(1:1) NOT = '#'                                     
              MOVE F595-NUM-NRIC2        TO WS-NRIC-LIST(2)                     
              ADD 1                      TO WS-NRIC-LIST-CNT                    
           END-IF.                                                              
                                                                                
           MOVE 'N'                      TO WS-NRIC-FOUND.                      
                                                                                
           PERFORM VARYING WS-CNT FROM 1 BY 1                                   
             UNTIL WS-CNT > WS-NRIC-LIST-CNT                                    
                OR WS-NRIC-FOUND = 'Y'                                          
                   MOVE SPACES           TO P13F595F-REC                        
                                            P13F595N-REC                        
                                                                                
                   PERFORM 3000-START-BP13K820 THRU 3000-EXIT                   
           END-PERFORM.                                                         
                                                                                
           IF WS-NRIC-FOUND = 'N'                                               
              PERFORM VARYING WS-CNT FROM 1 BY 1                                
                UNTIL WS-CNT > WS-NRIC-LIST-CNT                                 
                   OR WS-NRIC-FOUND = 'Y'                                       
                      MOVE SPACES           TO P13F595F-REC                     
                                               P13F595N-REC                     
                                                                                
                      PERFORM 3200-START-BP13K895 THRU 3200-EXIT                
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
           IF WS-NRIC-FOUND = 'N'                                               
              MOVE BP13F595-REC          TO P13F595N-REC                        
                                                                                
              WRITE P13F595N-REC                                                
                                                                                
              ADD 1                      TO WS-595N-WRITE-CNT                   
           END-IF.                                                              
                                                                                
           PERFORM 1100-READ-BP13F595 THRU 1100-EXIT.                           
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3000-START-BP13K820.                                                     
      *************************************************************             
           MOVE SPACES                   TO BP13K820-REC.                       
           INITIALIZE                       BP13K820-REC.                       
                                                                                
           MOVE WS-NRIC-LIST(WS-CNT)     TO K820-NUM-NRIC                       
                                            WS-K820-NRIC-PREV.                  
           MOVE 'N'                      TO WS-K820-EOF                         
                                            WS-K820-REC-PROCESS.                
                                                                                
           START BP13K820 KEY >= K820-NUM-NRIC.                                 
                                                                                
           EVALUATE WS-K820-STATUS                                              
               WHEN 00                                                          
                    PERFORM 3100-READNEXT-BP13K820 THRU 3100-EXIT               
                      UNTIL K820-NUM-NRIC NOT = WS-K820-NRIC-PREV               
                         OR WS-K820-EOF = 'Y'                                   
                         OR WS-NRIC-FOUND = 'Y'                                 
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'BP13K820 START ERROR ' WS-K820-STATUS              
                    DISPLAY 'K820 KEY: ' K820-KEY-FLD                           
                    MOVE WS-K820-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3100-READNEXT-BP13K820.                                                  
      *************************************************************             
                                                                                
           READ BP13K820 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'                 TO WS-K820-EOF                         
           END-READ.                                                            
                                                                                
           EVALUATE WS-K820-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    IF K820-NUM-NRIC = WS-K820-NRIC-PREV                        
                       PERFORM 3500-READ-BP13K800 THRU 3500-EXIT                
                                                                                
                       MOVE 'Y'          TO WS-K820-REC-PROCESS                 
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'BP13K820 READ NEXT ERROR ' WS-K820-STATUS          
                    DISPLAY 'K820 KEY: ' K820-KEY-FLD                           
                    MOVE WS-K820-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3200-START-BP13K895.                                                     
      *************************************************************             
           MOVE SPACES                   TO BP13K895-REC.                       
           INITIALIZE                       BP13K895-REC.                       
                                                                                
           MOVE WS-NRIC-LIST(WS-CNT)     TO K895-NUM-NRIC-HIST                  
                                            WS-K895-NRIC-PREV.                  
           MOVE 'N'                      TO WS-K895-EOF                         
                                            WS-K895-REC-PROCESS.                
                                                                                
           START BP13K895 KEY >= K895-NUM-NRIC-HIST.                            
                                                                                
           EVALUATE WS-K895-STATUS                                              
               WHEN 00                                                          
                    PERFORM 3300-READNEXT-BP13K895 THRU 3300-EXIT               
                      UNTIL K895-NUM-NRIC-HIST NOT = WS-K895-NRIC-PREV          
                         OR WS-K895-EOF = 'Y'                                   
                         OR WS-NRIC-FOUND = 'Y'                                 
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'BP13K895 START ERROR ' WS-K895-STATUS              
                    DISPLAY 'K895 KEY: ' K895-KEY-FLD                           
                    MOVE WS-K895-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3300-READNEXT-BP13K895.                                                  
      *************************************************************             
                                                                                
           READ BP13K895 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'                 TO WS-K895-EOF                         
           END-READ.                                                            
                                                                                
           EVALUATE WS-K895-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    IF K895-NUM-NRIC = WS-K895-NRIC-PREV                        
                       PERFORM 5000-START-BP13K893 THRU 5000-EXIT               
                                                                                
                       MOVE 'Y'                 TO WS-K895-REC-PROCESS          
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'BP13K895 READ NEXT ERROR ' WS-K895-STATUS          
                    DISPLAY 'K895 KEY: ' K895-KEY-FLD                           
                    MOVE WS-K895-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3500-READ-BP13K800.                                                      
      *************************************************************             
           MOVE SPACES                   TO BP13K800-MASTER.                    
           INITIALIZE                       BP13K800-MASTER.                    
                                                                                
           MOVE K820-NUM-REGN            TO K800-NUM-REGN.                      
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    PERFORM 4000-PROCESS-FOUND-CASES THRU 4000-EXIT             
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 READ ERROR ' WS-K800-STATUS               
                    DISPLAY 'K800 KEY: ' K800-NUM-REGN                          
                    MOVE WS-K800-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       4000-PROCESS-FOUND-CASES.                                                
      *************************************************************             
                                                                                
           IF K800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES                      
              IF (WS-NRIC-LIST(WS-CNT) = K800-NUM-NRIC1 OR                      
                  WS-NRIC-LIST(WS-CNT) = K800-NUM-NRIC2 OR                      
                  WS-NRIC-LIST(WS-CNT) = K800-NUM-NRIC3 OR                      
                  WS-NRIC-LIST(WS-CNT) = K800-NUM-NRIC4)                        
                  PERFORM 6000-WRITE-BP13F595  THRU 6000-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       5000-START-BP13K893.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                           TO BP13K893-MASTER.            
           INITIALIZE                               BP13K893-MASTER.            
                                                                                
           MOVE SPACES                           TO K893-KEY-FLD.               
           MOVE K895-NUM-REGN-HIST               TO K893-NUM-REGN-HIST.         
                                                                                
           START BP13K893 KEY >= K893-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K893-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 5100-READ-BP13K893  THRU 5100-EXIT                  
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR STARTING BP13K893: ' WS-K893-STATUS          
                            ' REGN = ' K893-NUM-REGN-HIST                       
                    PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5100-READ-BP13K893.                                                      
      ******************************************************************        
                                                                                
            READ  BP13K893 NEXT AT END                                          
                  MOVE SPACES               TO K893-KEY-FLD.                    
                                                                                
            EVALUATE WS-K893-STATUS                                             
                WHEN 00                                                         
                WHEN 02                                                         
                     IF K893-NUM-REGN-HIST = K895-NUM-REGN-HIST                 
                        IF K893-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES         
                                                         AND ZEROES             
                           PERFORM 5200-CHECK-DTE-BALLOT THRU 5200-EXIT         
                        END-IF                                                  
                     END-IF                                                     
                WHEN 23                                                         
                     CONTINUE                                                   
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K893 : ' WS-K893-STATUS         
                             ' REGN = ' K893-NUM-REGN-HIST                      
                     PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                
            END-EVALUATE.                                                       
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5200-CHECK-DTE-BALLOT.                                                   
      ******************************************************************        
                                                                                
            IF K893-NUM-STATUS NOT = 'C'                                        
               IF K893-DTE-SO-APPT(1:6) >= F595-DTE-BALLOT                      
                  IF (WS-NRIC-LIST(WS-CNT) = K893-NUM-NRIC1 OR                  
                      WS-NRIC-LIST(WS-CNT) = K893-NUM-NRIC2 OR                  
                      WS-NRIC-LIST(WS-CNT) = K893-NUM-NRIC3 OR                  
                      WS-NRIC-LIST(WS-CNT) = K893-NUM-NRIC4)                    
                      PERFORM 6000-WRITE-BP13F595     THRU 6000-EXIT            
                  END-IF                                                        
               ELSE                                                             
                  IF K893-DTE-CREATE-HIST(1:6) >= F595-DTE-BALLOT               
                     IF (WS-NRIC-LIST(WS-CNT) = K893-NUM-NRIC1 OR               
                         WS-NRIC-LIST(WS-CNT) = K893-NUM-NRIC2 OR               
                         WS-NRIC-LIST(WS-CNT) = K893-NUM-NRIC3 OR               
                         WS-NRIC-LIST(WS-CNT) = K893-NUM-NRIC4)                 
                         PERFORM 6000-WRITE-BP13F595   THRU 6000-EXIT           
                     END-IF                                                     
                  END-IF                                                        
               END-IF                                                           
            END-IF.                                                             
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       6000-WRITE-BP13F595.                                                     
      *************************************************************             
                                                                                
            MOVE SPACES                  TO P13F595F-REC.                       
            INITIALIZE                      P13F595F-REC.                       
                                                                                
            WRITE P13F595F-REC         FROM BP13F595-REC.                       
                                                                                
            ADD 1                        TO WS-595F-WRITE-CNT.                  
            MOVE 'Y'                     TO WS-NRIC-FOUND.                      
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       9000-CLOSE-ROUTINE.                                                      
      *************************************************************             
           DISPLAY '   '.                                                       
           DISPLAY 'RUN DATE : ' WS-RUN-DATE.                                   
           DISPLAY '*--------- BP13CB78 CONTROL TOTAL ---------*'.              
           DISPLAY '   '.                                                       
           DISPLAY 'NO. OF READS FROM F595   : ' WS-F595-READ-CNT.              
           DISPLAY 'RECS WRITTEN TO P13F595F : ' WS-595F-WRITE-CNT.             
           DISPLAY 'RECS WRITTEN TO P13F595N : ' WS-595N-WRITE-CNT.             
           DISPLAY 'RECS CANCEL NOT BOOK     : ' WS-CANCEL-NOTBK.               
           DISPLAY 'RECS CANCEL OTHER        : ' WS-CANCEL-NOTELIG.             
           DISPLAY 'RECS CANCEL -THIS BALLOT : ' WS-CANCEL-BALLOT.              
           DISPLAY 'RECS AUG BE              : ' WS-BE-AUG.                     
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13F595                                                       
                 BP13K820                                                       
                 BP13K800                                                       
                 BP13K893                                                       
                 BP13K895                                                       
                 P13F595F                                                       
                 P13F595N.                                                      
                                                                                
           IF WS-K820-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K820 CLOSE ERROR ' WS-K820-STATUS                    
              MOVE WS-K820-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K800 CLOSE ERROR ' WS-K800-STATUS                    
              MOVE WS-K800-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K893-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K893 CLOSE ERROR ' WS-K893-STATUS                    
              MOVE WS-K893-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K895-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K895 CLOSE ERROR ' WS-K895-STATUS                    
              MOVE WS-K895-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
