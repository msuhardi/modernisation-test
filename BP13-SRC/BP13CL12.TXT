       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CL12.                                                 
       AUTHOR.        ZARITA DANE SARMIENTO.                                    
      *DATE-WRITTEN.  03/07/2023.                                               
      * ===============================================================*        
      * SYSTEM NAME (SYSTEM ID)                                        *        
      * ===============================================================*        
      * OBJECTIVE   : PROGRAM TO GET ALL CASES THAT WERE UPDATE WITH   *        
      *               VOLUNTARY REFUND TO CPFB                         *        
      *                                                                *        
      * OUTPUT DESCRIPTION:                                            *        
      *                                                                *        
      *   INPUT FILES :  1.  BP13F310                                  *        
      *                                                                *        
      *   OUTPUT      :  1.  BP13MAIL  - CASE NOT FILED REPORT         *        
      *                                                                *        
      *   CHGE-NO   BY     DATE    DESCRIPTION                         *        
      *   -------   ---   ------   ------------------------------------*        
      *                                                                *        
      *   BP139632  ZAR7  20/06/23 NEW PROGRAM                         *        
      *   BP139276  KAC1  05/04/24 REPLACING LIM_Kok_Chun BY           *        
      *                                      KATHIJAH_BEE_ALI_MOHAMED  *        
      *================================================================*        
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F310  ASSIGN        TO BP13F310.                          
           SELECT BP13MAIL  ASSIGN        TO BP13MAIL.                          
                                                                                
           SELECT BP13K885 ASSIGN TO BP13K885                                   
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K885-KEY-FLD                                    
                  ACCESS MODE IS DYNAMIC                                        
                  FILE STATUS IS WS-K885-FILE-STATUS.                           
                                                                                
      ****************                                                          
       DATA DIVISION.                                                           
      ****************                                                          
                                                                                
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13K885                                                             
                                   BLOCK CONTAINS 0 RECORDS                     
                                   RECORD CONTAINS 400 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13K885.                                                           
                                                                                
       FD  BP13F310                                                             
                                   BLOCK CONTAINS 0 RECORDS                     
                                   RECORD CONTAINS 150 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F310.                                                           
                                                                                
       FD  BP13MAIL                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  BP13MAIL-REC        PIC X(132).                                      
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY                  PIC 9(04).                          
           05  WS-CUR-MM                    PIC 9(02).                          
           05  WS-CUR-DD                    PIC 9(02).                          
                                                                                
       01  WS-COUNT.                                                            
           05  WS-F310-INPUT                PIC 9(08)  VALUE 0.                 
           05  WS-F310-OUT                  PIC 9(08)  VALUE 0.                 
           05  WS-UPD-CNT                   PIC 9(08)  VALUE 0.                 
                                                                                
       01  WS-TIME                          PIC 9(08)  VALUE 0.                 
       01  WS-K885-AMT-VOLUNTARY            PIC 9(07)V99 VALUE ZEROES.          
       01  WS-F310-EOF                      PIC X(01)  VALUE SPACES.            
       01  WS-EOF-K885                      PIC X(01)  VALUE SPACES.            
       01  WS-FOUND-KL00                    PIC X(01)  VALUE SPACES.            
       01  WS-FILE-STATUS.                                                      
           05  WS-K885-FILE-STATUS        PIC 9(2).                             
       01  WS-BP13C913                   PIC X(8)  VALUE 'BP13C913'.            
       01  WS-SQL-CODE                   PIC 9(4).                              
                                                                                
      *-------------------------------------------------------------            
      * LAYOUT BP13MAIL  -  DATASET FOR EMAIL                                   
      *-------------------------------------------------------------            
       01  MAIL-HDR1.                                                           
           05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB1'.                 
           05  FILLER          PIC X(68)  VALUE SPACES.                         
                                                                                
       01  MAIL-HDR2.                                                           
           05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
           05  MAIL-SENDID     PIC X(04)  VALUE 'OPCP'.                         
           05  FILLER          PIC X(09)  VALUE '@SGPHDB1>'.                    
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTLAA.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(36)  VALUE                                 
               'KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                          
                                                                                
       01  MAIL-DTL1B.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(25)  VALUE                                 
               'HE_Jialing@hdb.gov.sg>'.                                        
                                                                                
       01  MAIL-DTL1C.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(27)  VALUE                                 
               'Suzanne_SC_HENG@hdb.gov.sg>'.                                   
                                                                                
      *01  MAIL-DTL1D.                                                          
      *    05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
      *    05  FILLER          PIC X(36)  VALUE                                 
      *        'Kathijah_Bee_ALI_MOHAMED@hdb.gov.sg>'.                          
                                                                                
       01  MAIL-DTL1E.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(35)  VALUE                                 
               'OH_Meng_Huat@hdb.gov.sg>'.                                      
                                                                                
                                                                                
       01  MAIL-DTL1F.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(35)  VALUE                                 
               'TOH_LAY_YEN@HDB.GOV.SG>'.                                       
                                                                                
       01  MAIL-DTL3.                                                           
           05  FILLER          PIC X(04)  VALUE 'DATA'.                         
           05  FILLER          PIC X(76)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL4.                                                           
           05  FILLER          PIC X(05)  VALUE 'FROM:'.                        
           05  MAIL-SENDMAILID PIC X(26)  VALUE                                 
               'SOC System - Email Alert'.                                      
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTLAB.                                                          
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  FILLER          PIC X(36)  VALUE                                 
               'KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                          
                                                                                
       01  MAIL-DTL5B.                                                          
           05  FILLER          PIC X(4)   VALUE 'CC:<'.                         
           05  FILLER          PIC X(25)  VALUE                                 
               'HE_Jialing@hdb.gov.sg>'.                                        
                                                                                
       01  MAIL-DTL5C.                                                          
           05  FILLER          PIC X(4)   VALUE 'CC:<'.                         
           05  FILLER          PIC X(27)  VALUE                                 
               'Suzanne_SC_HENG@hdb.gov.sg>'.                                   
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
      *01  MAIL-DTL5D.                                                          
      *    05  FILLER          PIC X(4)   VALUE 'CC:<'.                         
      *    05  FILLER          PIC X(36)  VALUE                                 
      *        'Kathijah_Bee_ALI_MOHAMED@hdb.gov.sg>'.                          
                                                                                
       01  MAIL-DTL5E.                                                          
           05  FILLER          PIC X(4)   VALUE 'CC:<'.                         
           05  FILLER          PIC X(35)  VALUE                                 
               'OH_Meng_Huat@hdb.gov.sg>'.                                      
                                                                                
       01  MAIL-DTL5F.                                                          
           05  FILLER          PIC X(4)   VALUE 'CC:<'.                         
           05  FILLER          PIC X(35)  VALUE                                 
               'TOH_LAY_YEN@HDB.GOV.SG>'.                                       
                                                                                
       01  MAIL-DTL7.                                                           
           05  FILLER          PIC X(10)  VALUE 'SUBJECT : '.                   
           05  FILLER          PIC X(11)  VALUE ' VOLUNTARY '.                  
           05  FILLER          PIC X(20)  VALUE 'REFUND TO CPFB'.               
                                                                                
       01  MAIL-DTL8.                                                           
           05 FILLER                   PIC X(6)   VALUE 'DATE: '.               
           05 MAIL-DATE                PIC X(10)  VALUE SPACES.                 
                                                                                
       01  MAIL-END.                                                            
           05 FILLER                   PIC X      VALUE '.'.                    
                                                                                
       01  MAIL-SPACES.                                                         
           05 FILLER                   PIC X(80)  VALUE SPACES.                 
                                                                                
       01  WS-PRINT-VARIABLES.                                                  
           05 F310-HDG1.                                                        
              10 FILLER                   PIC X(55)   VALUE SPACES.             
              10 FILLER                   PIC X(22)   VALUE                     
                'SYSTEM  OF  COMMITMENT'.                                       
              10 FILLER                   PIC X(55)   VALUE SPACES.             
                                                                                
           05 F310-HDG2.                                                        
              10 FILLER                   PIC X(40)   VALUE 'BP13F310'.         
              10 FILLER                   PIC X(46)   VALUE                     
                '   CASES UPDATED WITH VOLUNTARY REFUND TO CPFB'.               
              10 FILLER                   PIC X(20)   VALUE SPACES.             
              10 FILLER                   PIC X(07)   VALUE 'DATE : '.          
              10 F310-SYS-DATE            PIC X(10).                            
              10 FILLER                   PIC X(06)   VALUE SPACES.             
                                                                                
           05 F310-HDG3.                                                        
              10 FILLER                   PIC X(06)   VALUE                     
                'S/NO. '.                                                       
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(20)   VALUE                     
                'REGISTRATION NO.    '.                                         
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(14)   VALUE                     
                 'SCHEME ACCOUNT'.                                              
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(50)   VALUE                     
                'ADDRESS  '.                                                    
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(10)   VALUE                     
                'AMT REFUND'.                                                   
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(09)   VALUE                     
                'CRE ID   '.                                                    
                                                                                
           05 F310-HDG4.                                                        
              10 FILLER                   PIC X(06)   VALUE                     
                '======'.                                                       
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(20)   VALUE                     
                '===================='.                                         
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(14)   VALUE                     
                 '=============='.                                              
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(50)   VALUE                     
                '========='.                                                    
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(09)   VALUE                     
                '========='.                                                    
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(09)   VALUE                     
                '========='.                                                    
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(20)   VALUE                     
                '===================='.                                         
                                                                                
           05 F310-DTL1.                                                        
              10 WS-F310-SNO              PIC ZZZZZ9  VALUE ZEROES.             
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 WS-F310-REG-NUM          PIC X(20)   VALUE SPACES.             
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 WS-F310-SCH-ACCT         PIC X(14)   VALUE SPACES.             
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 WS-F310-ADDR             PIC X(50)   VALUE SPACES.             
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 WS-F310-AMT-REFUND       PIC ZZZZZZ9.99.                       
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 WS-F310-CRE-ID           PIC X(09)   VALUE SPACES.             
                                                                                
      *=======================================================*                 
      *    LINKAGE VARIABLES                                  *                 
      *=======================================================*                 
       01   WS-LINK-REC.                                                        
            05 WS-LINK-NUM-SCH            PIC X(4).                             
            05 WS-LINK-NUM-ACC            PIC X(4).                             
            05 WS-LINK-NUM-CHK            PIC X(1).                             
                                                                                
       COPY P13COMM8.                                                           
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================*           
       MAIN.                                                                    
      *=============================================================*           
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 2000-READ-INPUT      THRU 2000-EXIT.                         
           PERFORM 3000-PROCESS-DATA    THRU 3000-EXIT                          
           UNTIL WS-F310-EOF = 'Y'.                                             
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
      *=============================================================*           
       1000-OPEN-ROUTINE.                                                       
      *=============================================================*           
                                                                                
           OPEN INPUT  BP13F310                                                 
                       BP13K885                                                 
                OUTPUT BP13MAIL.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CUR-DATE.                      
           MOVE FUNCTION CURRENT-DATE(9:8) TO WS-TIME.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       2000-READ-INPUT.                                                         
      *=============================================================*           
                                                                                
            READ BP13F310                                                       
                 AT END MOVE 'Y'  TO WS-F310-EOF                                
            END-READ.                                                           
                                                                                
            IF WS-F310-EOF NOT = 'Y'                                            
               ADD 1 TO WS-F310-INPUT                                           
            END-IF.                                                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       3000-PROCESS-DATA.                                                       
      *=============================================================*           
                                                                                
           ADD 1 TO WS-F310-OUT                                                 
           PERFORM 4000-CREATE-REPORT THRU 4000-EXIT                            
                                                                                
           PERFORM 2000-READ-INPUT      THRU 2000-EXIT.                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3100-READ-K885.                                                          
      *================================================================*        
                                                                                
           MOVE SPACES          TO BP13K885-REC                                 
                                   WS-EOF-K885.                                 
           MOVE ZEROES          TO WS-K885-AMT-VOLUNTARY.                       
                                                                                
           MOVE F310-REGN-NO    TO K885-NUM-REGN                                
                                                                                
           START  BP13K885 KEY  >= K885-KEY-FLD                                 
                                                                                
           EVALUATE WS-K885-FILE-STATUS                                         
           WHEN 00                                                              
           WHEN 02                                                              
              PERFORM 3200-NEXT-K885 THRU 3200-EXIT                             
                 UNTIL WS-EOF-K885 = 'Y' OR                                     
                       F310-REGN-NO NOT = K885-NUM-REGN                         
           WHEN 23                                                              
                MOVE ZEROES  TO K885-AMT-VOLUNTARY                              
           WHEN OTHER                                                           
               DISPLAY 'BP13K885 - FILE ERROR ' WS-K885-FILE-STATUS             
               MOVE WS-K885-FILE-STATUS TO RETURN-CODE                          
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       3200-NEXT-K885.                                                          
      *================================================================*        
                                                                                
                                                                                
           READ BP13K885 NEXT RECORD                                            
                                                                                
           EVALUATE WS-K885-FILE-STATUS                                         
           WHEN 00                                                              
           WHEN 02                                                              
                 IF F310-REGN-NO = K885-NUM-REGN                                
                    DISPLAY 'READNEXT - '                                       
                         'F310 - ' F310-REGN-NO                                 
                         ' K885 - ' K885-NUM-REGN                               
                         ' AMT  - ' K885-AMT-VOLUNTARY                          
                   IF K885-AMT-VOLUNTARY  NOT NUMERIC                           
                      MOVE ZEROES  TO K885-AMT-VOLUNTARY                        
                   END-IF                                                       
                   IF K885-AMT-VOLUNTARY  > ZEROES                              
                     MOVE K885-AMT-VOLUNTARY  TO WS-K885-AMT-VOLUNTARY          
                   END-IF                                                       
                 ELSE                                                           
                  MOVE 'Y'     TO WS-EOF-K885                                   
                 END-IF                                                         
           WHEN 10                                                              
                MOVE 'Y'     TO WS-EOF-K885                                     
           WHEN OTHER                                                           
               DISPLAY 'BP13K885 - FILE ERROR ' WS-K885-FILE-STATUS             
               MOVE WS-K885-FILE-STATUS TO RETURN-CODE                          
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       3300-GET-PIDB.                                                           
      *================================================================*        
                                                                                
           MOVE SPACES           TO BP13COMM8-REC.                              
           MOVE F310-SCH         TO WS-LINK-NUM-SCH.                            
           MOVE F310-ACC(1:4)    TO WS-LINK-NUM-ACC.                            
           MOVE F310-ACC(5:1)    TO WS-LINK-NUM-CHK.                            
                                                                                
           CALL WS-BP13C913 USING                                               
                 WS-LINK-REC, BP13COMM8-REC                                     
           END-CALL.                                                            
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              MOVE COMM8-CDE-SYSERR  TO WS-SQL-CODE                             
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE' WS-SQL-CODE           
              ELSE                                                              
                 DISPLAY 'ERROR READING PBF TABLE' WS-SQL-CODE                  
              END-IF                                                            
              MOVE SPACES        TO BP13COMM8-REC                               
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4000-CREATE-REPORT.                                                      
      *================================================================*        
                                                                                
           IF WS-F310-OUT = 1                                                   
              STRING WS-CUR-DATE(7:2) '/'                                       
                     WS-CUR-DATE(5:2) '/'                                       
                     WS-CUR-DATE(1:4)                                           
                     DELIMITED BY SIZE                                          
                     INTO F310-SYS-DATE                                         
              END-STRING                                                        
              MOVE F310-SYS-DATE TO MAIL-DATE                                   
              WRITE BP13MAIL-REC FROM MAIL-HDR1                                 
              WRITE BP13MAIL-REC FROM MAIL-HDR2                                 
              WRITE BP13MAIL-REC FROM MAIL-DTLAA                                
              WRITE BP13MAIL-REC FROM MAIL-DTL1B                                
              WRITE BP13MAIL-REC FROM MAIL-DTL1C                                
      *       WRITE BP13MAIL-REC FROM MAIL-DTL1D                                
              WRITE BP13MAIL-REC FROM MAIL-DTL1E                                
      *       WRITE BP13MAIL-REC FROM MAIL-DTL1F                                
              WRITE BP13MAIL-REC FROM MAIL-DTL3                                 
              WRITE BP13MAIL-REC FROM MAIL-DTL4                                 
              WRITE BP13MAIL-REC FROM MAIL-DTLAB                                
              WRITE BP13MAIL-REC FROM MAIL-DTL5B                                
              WRITE BP13MAIL-REC FROM MAIL-DTL5C                                
      *       WRITE BP13MAIL-REC FROM MAIL-DTL5D                                
              WRITE BP13MAIL-REC FROM MAIL-DTL5E                                
      *       WRITE BP13MAIL-REC FROM MAIL-DTL5F                                
              WRITE BP13MAIL-REC FROM MAIL-DTL7                                 
              WRITE BP13MAIL-REC FROM MAIL-DTL8                                 
              WRITE BP13MAIL-REC FROM MAIL-SPACES                               
              WRITE BP13MAIL-REC FROM F310-HDG1                                 
              WRITE BP13MAIL-REC FROM F310-HDG2                                 
              MOVE SPACES TO BP13MAIL-REC                                       
              WRITE BP13MAIL-REC                                                
              WRITE BP13MAIL-REC FROM F310-HDG3                                 
              WRITE BP13MAIL-REC FROM F310-HDG4                                 
              MOVE SPACES TO BP13MAIL-REC                                       
              WRITE BP13MAIL-REC                                                
           END-IF.                                                              
           PERFORM 3100-READ-K885  THRU 3100-EXIT.                              
           PERFORM 3300-GET-PIDB   THRU 3300-EXIT.                              
           MOVE WS-F310-OUT          TO WS-F310-SNO                             
           MOVE F310-REGN-NO         TO WS-F310-REG-NUM                         
           MOVE F310-SCH-ACC         TO WS-F310-SCH-ACCT                        
           STRING 'BLK ' COMM8-NUM-BLK ' #' COMM8-NUM-LEVEL '-'                 
                   COMM8-NUM-UNIT-MAIN ' ' COMM8-NME-STREET                     
           DELIMITED BY SIZE INTO WS-F310-ADDR.                                 
           MOVE WS-K885-AMT-VOLUNTARY   TO WS-F310-AMT-REFUND                   
           MOVE F310-USERID          TO WS-F310-CRE-ID                          
           WRITE BP13MAIL-REC    FROM F310-DTL1.                                
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       9000-CLOSE-ROUTINE.                                                      
      *================================================================*        
           IF WS-F310-OUT > 0                                                   
              WRITE BP13MAIL-REC FROM MAIL-END                                  
           ENd-IF                                                               
                                                                                
           DISPLAY 'TOTAL INPUT RECORDS        : ' WS-F310-INPUT                
           DISPLAY 'TOTAL RECORD NOT FILED     : ' WS-F310-OUT.                 
           DISPLAY '        '                                                   
                                                                                
           CLOSE BP13F310                                                       
                 BP13K885                                                       
                 BP13MAIL.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
