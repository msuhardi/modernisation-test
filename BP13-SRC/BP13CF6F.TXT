       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13CF6F.                                                  
       AUTHOR.       AM25.                                                      
       DATE-WRITTEN. 03/02/2020.                                                
      *****************************************************************         
      *                SOC - SYSTEM OF COMMITMENT (BP13)              *         
      *===============================================================*         
      *  OBJECTIVES :                                                 *         
      *     - TO CREATE RAW-DATA OF RESULT LETTERS (EIP - BTO/SBF)    *         
      *       USING IOMS; FH10, FB10 AND FTP FILES                    *         
      *===============================================================*         
      * FILES DESCRIPTION :-                                          *         
      *                                                               *         
      *                ACCESS     REC                                 *         
      * DD NAME  I/O/E MODE       LEN  KEY FIELDS    REMARKS          *         
      * -------- ----- ---------- ---- ------------- ---------------- *         
      * BP13F740 I     SEQUENTIAL 500                                 *         
      * BP13K813 I     RANDOM     1000 K813-KEY-FLD                   *         
      * BP13K825 I     RANDOM     200  K825-KEY-FLD                   *         
      * BP13K82P I     RANDOM     200  K82P-KEY-FLD                   *         
      * BP13K830 I     RANDOM     250  K830-KEY-FLD                   *         
      * P13B6FWN O     SEQUENTIAL 1500               IOMS             *         
      * P13H6FWN O     SEQUENTIAL 72                 IOMS             *         
      * P13B6FW5 O     SEQUENTIAL 1500               IOMS             *         
      * P13H6FW5 O     SEQUENTIAL 72                 IOMS             *         
      * BP13FTP  O     SEQUENTIAL 72                 IOMS             *         
      * P13LF6FA O     SEQUENTIAL 132                CTRL-LIST        *         
      *===============================================================*         
      * REVISION HISTORY :                                            *         
      *                                                               *         
      * CHG-REQ#  DD/MM/YY   BY   DESCRIPTION                         *         
      * --------  --------  ----  ----------------------------------- *         
      * BP138184  03/02/20  AM25  NEW PROGRAM                         *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN      TO BP13F740.                             
                                                                                
           SELECT BP13K813 ASSIGN      TO BP13K813                              
              ORGANIZATION  IS INDEXED                                          
              ACCESS MODE   IS RANDOM                                           
              RECORD KEY    IS K813-KEY-FLD                                     
              FILE STATUS   IS WS-K813-STATUS.                                  
                                                                                
           SELECT BP13K825 ASSIGN      TO BP13K825                              
              ORGANIZATION  IS INDEXED                                          
              ACCESS MODE   IS RANDOM                                           
              RECORD KEY    IS K825-KEY-FLD                                     
              FILE STATUS   IS WS-K825-STATUS.                                  
                                                                                
           SELECT BP13K82P ASSIGN      TO BP13K82P                              
              ORGANIZATION  IS INDEXED                                          
              ACCESS MODE   IS RANDOM                                           
              RECORD KEY    IS K82P-KEY-FLD                                     
              FILE STATUS   IS WS-K82P-STATUS.                                  
                                                                                
           SELECT BP13K830 ASSIGN      TO BP13K830                              
              ORGANIZATION  IS INDEXED                                          
              ACCESS MODE   IS RANDOM                                           
              RECORD KEY    IS K830-KEY-FLD                                     
              ALTERNATE KEY IS K830-NUM-REGN                                    
              FILE STATUS   IS WS-K830-STATUS.                                  
                                                                                
           SELECT P13B6FWN ASSIGN      TO P13B6FWN.                             
           SELECT P13H6FWN ASSIGN      TO P13H6FWN.                             
           SELECT P13B6FW5 ASSIGN      TO P13B6FW5.                             
           SELECT P13H6FW5 ASSIGN      TO P13H6FW5.                             
           SELECT BP13FTP  ASSIGN      TO BP13FTP.                              
                                                                                
           SELECT P13LF6FA ASSIGN      TO P13LF6FA.                             
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F740                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F740.                                                           
                                                                                
       FD  BP13K813                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13K813.                                                           
                                                                                
       FD  BP13K825                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
       COPY BP13K825.                                                           
                                                                                
       FD  BP13K82P                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
       01  BP13K82P-REC.                                                        
           05  K82P-KEY-FLD.                                                    
               10  K82P-NUM-REGN       PIC X(8).                                
               10  K82P-NUM-MATURE-KEY REDEFINES K82P-NUM-REGN.                 
                   15  K82P-NUM-REF    PIC X(8).                                
               10  K82P-NUM-NRIC       PIC X(9).                                
           05  FILLER                  PIC X(1).                                
           05  K82P-NUM-CPF            PIC X(9).                                
           05  K82P-NME-OCCP           PIC X(66).                               
           05  K82P-NUM-ETHNIC         PIC X(1).                                
           05  K82P-NUM-SEX            PIC X(1).                                
           05  K82P-NUM-MARITAL-STATUS PIC X(1).                                
           05  K82P-DTE-BIRTH          PIC X(8).                                
           05  K82P-NUM-CITIZENSHIP    PIC X(2).                                
           05  K82P-NUM-RELATIONSHIP   PIC X(2).                                
           05  K82P-AMT-INCOME         PIC 9(5).                                
           05  K82P-NUM-NATIONALITY    PIC X(2).                                
           05  K82P-NUM-RESIDENT-NT    PIC X(1).                                
           05  K82P-AMT-INCOME-TP      PIC 9(5).                                
           05  K82P-DTE-UPDATE         PIC X(8).                                
           05  K82P-NUM-5YR-OCCUPN     PIC X(1).                                
           05  K82P-NUM-LESSEE-CHECK   PIC X(1).                                
           05  K82P-DTE-ISSUE-REGN     PIC X(8).                                
           05  K82P-NUM-TAG2           PIC X(5).                                
           05  FILLER                  PIC X(14).                               
           05  K82P-NUM-FREEZE-OCCP    PIC X(1).                                
           05  K82P-NUM-NRIC-SPOUSE    PIC X(9).                                
           05  K82P-NUM-PHONE          PIC X(8).                                
           05  K82P-NUM-HP-PGR         PIC X(8).                                
           05  K82P-NUM-PHONE-OFF      PIC X(8).                                
           05  K82P-NUM-EDUCATION      PIC X(1).                                
           05  K82P-NUM-USERID         PIC X(7).                                
                                                                                
       FD  BP13K830                                                             
           RECORD CONTAINS 250 CHARACTERS.                                      
       COPY BP13K830.                                                           
                                                                                
       FD  P13B6FWN                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 1500 CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       01  P13B6FWN-REC                PIC X(1500).                             
                                                                                
       FD  P13H6FWN                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 72 CHARACTERS                                        
           RECORDING MODE IS F.                                                 
       01  P13H6FWN-REC                PIC X(72).                               
                                                                                
       FD  P13B6FW5                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 1500 CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       01  P13B6FW5-REC                PIC X(1500).                             
                                                                                
       FD  P13H6FW5                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 72 CHARACTERS                                        
           RECORDING MODE IS F.                                                 
       01  P13H6FW5-REC                PIC X(72).                               
                                                                                
       FD  BP13FTP                                                              
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 72 CHARACTERS                                        
           RECORDING MODE IS F.                                                 
       01  BP13FTP-REC                 PIC X(72).                               
                                                                                
       FD  P13LF6FA                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01  P13LF6FA-REC                PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       COPY P13FH10A.                                                           
       COPY P13PF6FA.                                                           
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-K813-STATUS          PIC 9(2)        VALUE ZEROES.            
           05  WS-K825-STATUS          PIC 9(2)        VALUE ZEROES.            
           05  WS-K82P-STATUS          PIC 9(2)        VALUE ZEROES.            
           05  WS-K830-STATUS          PIC 9(2)        VALUE ZEROES.            
           05  WS-SYS-DTE-TME.                                                  
               10 WS-SYS-DATE          PIC 9(8)        VALUE ZEROES.            
               10 WS-SYS-TIME          PIC 9(8)        VALUE ZEROES.            
           05  WS-DTE-BAL-MTH-9        PIC X(9)        VALUE SPACES.            
           05  WS-GROUP-ID             PIC X(8)        VALUE SPACES.            
           05  WS-PRINT-SETTING        PIC X(1)        VALUE SPACES.            
           05  WS-NME-OCCP             PIC X(66)       VALUE SPACES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F740-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-K813-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-K825-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-K82P-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-K830-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-FH10-WRITE           PIC 9(6)        VALUE ZEROES.            
           05  WS-FB10-WRITE           PIC 9(6)        VALUE ZEROES.            
           05  WS-FTP-WRITE            PIC 9(6)        VALUE ZEROES.            
           05  WS-B6FWN-SNO            PIC 9(7)        VALUE ZEROES.            
           05  WS-B6FW5-SNO            PIC 9(7)        VALUE ZEROES.            
           05  WS-H6FWN-WRITE          PIC 9(7)        VALUE ZEROES.            
           05  WS-H6FW5-WRITE          PIC 9(7)        VALUE ZEROES.            
           05  WS-BP13WN-CNT           PIC 9(6)        VALUE ZEROES.            
           05  WS-BP13W5-CNT           PIC 9(6)        VALUE ZEROES.            
           05  WS-LF6FA-CNT            PIC 9(4)        VALUE ZEROES.            
           05  WS-LF6FA-PAGE-CNT       PIC 9(4)        VALUE ZEROES.            
           05  WS-LF6FA-LINE-CNT       PIC 9(2)        VALUE 60.                
                                                                                
       01  WS-SWITCHES-FLAGS.                                                   
           05  WS-F740-EOF             PIC X           VALUE 'N'.               
                                                                                
       01  WS-MONTHS-IN-DIGITS-AND-WORDS.                                       
           05  FILLER                  PIC X(12)       VALUE                    
               '01 JANUARY  '.                                                  
           05  FILLER                  PIC X(12)       VALUE                    
               '02 FEBRUARY '.                                                  
           05  FILLER                  PIC X(12)       VALUE                    
               '03 MARCH    '.                                                  
           05  FILLER                  PIC X(12)       VALUE                    
               '04 APRIL    '.                                                  
           05  FILLER                  PIC X(12)       VALUE                    
               '05 MAY      '.                                                  
           05  FILLER                  PIC X(12)       VALUE                    
               '06 JUNE     '.                                                  
           05  FILLER                  PIC X(12)       VALUE                    
               '07 JULY     '.                                                  
           05  FILLER                  PIC X(12)       VALUE                    
               '08 AUGUST   '.                                                  
           05  FILLER                  PIC X(12)       VALUE                    
               '09 SEPTEMBER'.                                                  
           05  FILLER                  PIC X(12)       VALUE                    
               '10 OCTOBER  '.                                                  
           05  FILLER                  PIC X(12)       VALUE                    
               '11 NOVEMBER '.                                                  
           05  FILLER                  PIC X(12)       VALUE                    
               '12 DECEMBER '.                                                  
       01  FILLER REDEFINES WS-MONTHS-IN-DIGITS-AND-WORDS.                      
           05  WS-MONTH-TABLE OCCURS 12 INDEXED BY WS-MTH-IND.                  
               10  WS-MONTH-IN-DIGITS  PIC X(2).                                
               10  FILLER              PIC X(1).                                
               10  WS-MONTH-IN-WORDS   PIC X(9).                                
                                                                                
       01  WS-IOMS-VARIABLES.                                                   
           05  WS-UPD-TMESTAMP.                                                 
               10  WS-UPD-DATE         PIC X(8)        VALUE SPACES.            
               10  WS-UPD-TME          PIC X(8)        VALUE SPACES.            
           05  WS-FTP-DEST-REC.                                                 
               10  WS-FTP-JOBNAME      PIC X(8)        VALUE SPACES.            
               10  FILLER              PIC X           VALUE '_'.               
               10  WS-FTP-DESC         PIC X(6)        VALUE SPACES.            
               10  FILLER              PIC X           VALUE '_'.               
               10  WS-FTP-TMESTAMP     PIC X(12)       VALUE SPACES.            
           05  WS-P13FT10A-WRITE       PIC 9(7)        VALUE ZEROES.            
           05  WS-PARM.                                                         
               10  WS-PARM-JOBNAME     PIC X(8)        VALUE SPACES.            
               10  WS-PARM-B10-SOURCE  PIC X(70)       VALUE SPACES.            
               10  WS-PARM-H10-SOURCE  PIC X(70)       VALUE SPACES.            
                                                                                
       01  WS-BP13FTP-REC.                                                      
           05  WS-FTP-LINE1.                                                    
               10  FILLER              PIC X(2)        VALUE SPACES.            
               10  FILLER              PIC X(12)       VALUE                    
                   'CD /DATAFILE'.                                              
               10  FILLER              PIC X(58)       VALUE SPACES.            
           05  WS-FTP-LINE2.                                                    
               10  FILLER              PIC X(2)        VALUE SPACES.            
               10  FILLER              PIC X(12)       VALUE                    
                   'DIR         '.                                              
               10  FILLER              PIC X(58)       VALUE SPACES.            
           05  WS-FTP-LINE3.                                                    
               10  FILLER              PIC X(2)        VALUE SPACES.            
               10  FILLER              PIC X(12)       VALUE                    
                   'ASCII       '.                                              
               10  FILLER              PIC X(58)       VALUE SPACES.            
           05  WS-FTP-LINE4.                                                    
               10  FILLER              PIC X(2)        VALUE SPACES.            
               10  WS-FTP-SOURCE       PIC X(70)       VALUE SPACES.            
           05  WS-FTP-LINE5.                                                    
               10  FILLER              PIC X(2)        VALUE SPACES.            
               10  FILLER              PIC X(12)       VALUE                    
                   'DIR         '.                                              
               10  FILLER              PIC X(66)       VALUE SPACES.            
           05  WS-FTP-LINE6.                                                    
               10  FILLER              PIC X(2)        VALUE SPACES.            
               10  FILLER              PIC X(12)       VALUE                    
                   'QUIT        '.                                              
               10  FILLER              PIC X(66)       VALUE SPACES.            
                                                                                
       01  WS-LINK-AV02C015.                                                    
           05  AV02C015-IN-POSTCODE    PIC X(6)        VALUE SPACES.            
           05  AV02C015-OUT-POSTCODE-CHKSUM                                     
                                       PIC X(9)        VALUE SPACES.            
                                                                                
      *------------------------------------------------------------*            
      *    REPORT LAYOUT FOR CONTROL LISTINGS                      *            
      *------------------------------------------------------------*            
       01  LF6F-HDR1.                                                           
           05  LF6F-LISTING            PIC X(8)        VALUE SPACES.            
           05  FILLER                  PIC X(6)        VALUE SPACES.            
           05  FILLER                  PIC X(4)        VALUE 'HDB3'.            
           05  FILLER                  PIC X(22)       VALUE SPACES.            
           05  FILLER                  PIC X(39)       VALUE                    
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                  PIC X(18)       VALUE SPACES.            
           05  FILLER                  PIC X(6)        VALUE 'DATE: '.          
           05  LF6F-DATE               PIC X(10)       VALUE SPACES.            
           05  FILLER                  PIC X(7)        VALUE ' PAGE: '.         
           05  LF6F-PAGE               PIC Z,ZZ9.                               
           05  FILLER                  PIC X(7)        VALUE SPACES.            
                                                                                
       01  LF6F-HDR2.                                                           
           05  FILLER                  PIC X(40)       VALUE SPACES.            
           05  LF6F-LISTING-TYPE       PIC X(89)       VALUE SPACES.            
           05  FILLER                  PIC X(3)        VALUE SPACES.            
                                                                                
       01  LF6F-HDR4.                                                           
           05  FILLER                  PIC X(7)        VALUE ' S/N   '.         
           05  FILLER                  PIC X(9)        VALUE                    
               'REF-NO   '.                                                     
           05  FILLER                  PIC X(15)       VALUE                    
               'APPLICANT NAMES'.                                               
           05  FILLER                  PIC X(8)        VALUE SPACES.            
           05  FILLER                  PIC X(7)        VALUE '  UIN  '.         
           05  FILLER                  PIC X(3)        VALUE SPACES.            
           05  FILLER                  PIC X(5)        VALUE 'CAT  '.           
           05  FILLER                  PIC X(5)        VALUE 'ALLOC'.           
           05  FILLER                  PIC X(3)        VALUE SPACES.            
           05  FILLER                  PIC X(4)        VALUE 'FLAT'.            
           05  FILLER                  PIC X(3)        VALUE SPACES.            
           05  FILLER                  PIC X(7)        VALUE 'HHTY   '.         
           05  FILLER                  PIC X(4)        VALUE 'MCPS'.            
           05  FILLER                  PIC X(4)        VALUE SPACES.            
           05  FILLER                  PIC X(7)        VALUE 'RES-HA1'.         
           05  FILLER                  PIC X(4)        VALUE SPACES.            
           05  FILLER                  PIC X(6)        VALUE 'SUCC  '.          
           05  FILLER                  PIC X(3)        VALUE SPACES.            
           05  FILLER                  PIC X(6)        VALUE 'NT1   '.          
           05  FILLER                  PIC X(6)        VALUE 'UNSUCC'.          
           05  FILLER                  PIC X(3)        VALUE SPACES.            
           05  FILLER                  PIC X(6)        VALUE 'BALLOT'.          
           05  FILLER                  PIC X(2)        VALUE SPACES.            
           05  FILLER                  PIC X(2)        VALUE 'ZE'.              
           05  FILLER                  PIC X(3)        VALUE SPACES.            
                                                                                
       01  LF6F-HDR5.                                                           
           05  FILLER                  PIC X(7)        VALUE SPACES.            
           05  FILLER                  PIC X(8)        VALUE 'REGN-NO'.         
           05  FILLER                  PIC X(40)       VALUE SPACES.            
           05  FILLER                  PIC X(3)        VALUE 'SCH'.             
           05  FILLER                  PIC X(4)        VALUE SPACES.            
           05  FILLER                  PIC X(4)        VALUE 'TYPE'.            
           05  FILLER                  PIC X(5)        VALUE SPACES.            
           05  FILLER                  PIC X(4)        VALUE '    '.            
           05  FILLER                  PIC X(3)        VALUE SPACES.            
           05  FILLER                  PIC X(8)        VALUE '       '.         
           05  FILLER                  PIC X(2)        VALUE SPACES.            
           05  FILLER                  PIC X(5)        VALUE '     '.           
           05  FILLER                  PIC X(2)        VALUE SPACES.            
           05  FILLER                  PIC X(7)        VALUE 'ZONE   '.         
           05  FILLER                  PIC X(4)        VALUE SPACES.            
           05  FILLER                  PIC X(3)        VALUE '   '.             
           05  FILLER                  PIC X(2)        VALUE SPACES.            
           05  FILLER                  PIC X(4)        VALUE 'ZONE'.            
           05  FILLER                  PIC X(5)        VALUE SPACES.            
           05  FILLER                  PIC X(3)        VALUE 'POS'.             
           05  FILLER                  PIC X(2)        VALUE SPACES.            
           05  FILLER                  PIC X(6)        VALUE 'Q-POS'.           
           05  FILLER                  PIC X(1)        VALUE SPACES.            
                                                                                
       01  LF6F-HDR6.                                                           
           05  FILLER                  PIC X(7)        VALUE '       '.         
           05  FILLER                  PIC X(9)        VALUE                    
               'LOAN-TAG '.                                                     
           05  FILLER                  PIC X(15)       VALUE                    
               'REQ-STATUS     '.                                               
           05  FILLER                  PIC X(101)      VALUE SPACES.            
                                                                                
       01  LF6F-DTL1.                                                           
           05  LF6F-SERIAL-NO          PIC ZZZ9.                                
           05  FILLER                  PIC X(3)        VALUE SPACES.            
           05  LF6F-REF-NO             PIC X(8)        VALUE SPACES.            
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  LF6F-NAME-1             PIC X(22)       VALUE SPACES.            
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  LF6F-NRIC-1             PIC X(9)        VALUE SPACES.            
           05  FILLER                  PIC X(3)        VALUE SPACES.            
           05  LF6F-CAT                PIC X(1)        VALUE SPACES.            
           05  FILLER                  PIC X(3)        VALUE SPACES.            
           05  LF6F-ALLOC              PIC X(3)        VALUE SPACES.            
           05  FILLER                  PIC X(6)        VALUE SPACES.            
           05  LF6F-FLAT               PIC X(2)        VALUE SPACES.            
           05  FILLER                  PIC X(4)        VALUE SPACES.            
           05  LF6F-HHTY               PIC X(1)        VALUE SPACES.            
           05  FILLER                  PIC X(6)        VALUE SPACES.            
           05  LF6F-MCPS               PIC X(1)        VALUE SPACES.            
           05  FILLER                  PIC X(9)        VALUE SPACES.            
           05  LF6F-RES-HA1            PIC X(1)        VALUE SPACES.            
           05  FILLER                  PIC X(7)        VALUE SPACES.            
           05  LF6F-SUCC               PIC X(3)        VALUE SPACES.            
           05  FILLER                  PIC X(6)        VALUE SPACES.            
           05  LF6F-NT1                PIC X(3)        VALUE SPACES.            
           05  FILLER                  PIC X(5)        VALUE SPACES.            
           05  LF6F-UNSUCC             PIC X(3)        VALUE SPACES.            
           05  FILLER                  PIC X(4)        VALUE SPACES.            
           05  LF6F-BALLOT-POS         PIC X(5)        VALUE SPACES.            
           05  FILLER                  PIC X(3)        VALUE SPACES.            
           05  LF6F-ZONE               PIC X(2)        VALUE SPACES.            
           05  FILLER                  PIC X(3)        VALUE SPACES.            
                                                                                
       01  LF6F-DTL2.                                                           
           05  FILLER                  PIC X(7)        VALUE SPACES.            
           05  LF6F-REGN-NO            PIC X(8)        VALUE SPACES.            
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  LF6F-NAME-2             PIC X(22)       VALUE SPACES.            
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  LF6F-NRIC-2             PIC X(9)        VALUE SPACES.            
           05  FILLER                  PIC X(79)       VALUE SPACES.            
           05  LF6F-QUEUE              PIC X(5)        VALUE SPACES.            
                                                                                
       01  LF6F-DTL3.                                                           
           05  FILLER                  PIC X(16)       VALUE SPACES.            
           05  LF6F-BLK                PIC X(10)       VALUE SPACES.            
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  LF6F-UNIT               PIC X(11)       VALUE SPACES.            
           05  FILLER                  PIC X(1)        VALUE SPACES.            
           05  LF6F-ADDR2              PIC X(65)       VALUE SPACES.            
           05  FILLER                  PIC X(3)        VALUE SPACES.            
           05  LF6F-ADDR3              PIC X(20)       VALUE SPACES.            
           05  FILLER                  PIC X(5)        VALUE SPACES.            
                                                                                
       01  LF6F-DTL4.                                                           
           05  FILLER                  PIC X(4)        VALUE SPACES.            
           05  FILLER                  PIC X(3)        VALUE SPACES.            
           05  LF6F-LOAN-TAG           PIC X(6)        VALUE SPACES.            
           05  FILLER                  PIC X(3)        VALUE SPACES.            
           05  LF6F-REQ-STATUS         PIC X(22)       VALUE SPACES.            
           05  FILLER                  PIC X(94)       VALUE SPACES.            
                                                                                
       01  LF6F-BLANK.                                                          
           05  FILLER                  PIC X(132)      VALUE SPACES.            
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           ACCEPT WS-PARM-JOBNAME.                                              
           ACCEPT WS-PARM-B10-SOURCE.                                           
           ACCEPT WS-PARM-H10-SOURCE.                                           
                                                                                
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 2000-READ-BP13F740    THRU 2000-EXIT.                        
           PERFORM 3000-PROCESS-LETTERS  THRU 3000-EXIT                         
              UNTIL WS-F740-EOF = 'Y'.                                          
           PERFORM 8900-CREATE-FIOMS-REC THRU 8900-EXIT.                        
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN INPUT  BP13F740                                                 
                       BP13K813                                                 
                       BP13K825                                                 
                       BP13K82P                                                 
                       BP13K830                                                 
                OUTPUT P13B6FWN                                                 
                       P13H6FWN                                                 
                       P13B6FW5                                                 
                       P13H6FW5                                                 
                       BP13FTP                                                  
                       P13LF6FA.                                                
                                                                                
           IF (WS-K813-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K813 FAIL STATUS=' WS-K813-STATUS               
              MOVE WS-K813-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF (WS-K825-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K825 FAIL STATUS=' WS-K825-STATUS               
              MOVE WS-K825-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF (WS-K82P-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K82P FAIL STATUS=' WS-K82P-STATUS               
              MOVE WS-K82P-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF (WS-K830-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K830 FAIL STATUS=' WS-K830-STATUS               
              MOVE WS-K830-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-SYS-DTE-TME                        
                                          WS-UPD-TMESTAMP.                      
                                                                                
           STRING WS-SYS-DATE(7:2) '/'                                          
                  WS-SYS-DATE(5:2) '/'                                          
                  WS-SYS-DATE(1:4)                                              
              DELIMITED BY SIZE INTO LF6F-DATE.                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-READ-BP13F740.                                                      
      *****************************************************************         
                                                                                
           READ BP13F740                                                        
              AT END                                                            
                 MOVE 'Y'              TO WS-F740-EOF                           
              NOT AT END                                                        
                 ADD 1                 TO WS-F740-READ                          
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-PROCESS-LETTERS.                                                    
      *****************************************************************         
                                                                                
           PERFORM 4000-PROCESS-B10-REC THRU 4000-EXIT.                         
                                                                                
           IF F740-NUM-ALLO-CAT = 'BTO'                                         
              IF F740-CDE-FLAT-TYPE NOT = '1A'                                  
                 PERFORM 7005-BP13WN THRU 7005-EXIT                             
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F740-NUM-ALLO-CAT = 'SBF'                                         
              IF F740-CDE-FLAT-TYPE NOT = '1A'                                  
                 PERFORM 7010-BP13W5 THRU 7010-EXIT                             
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 8000-PRT-CTRL-LIST THRU 8000-EXIT.                           
                                                                                
           PERFORM 2000-READ-BP13F740 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4000-PROCESS-B10-REC.                                                    
      *****************************************************************         
                                                                                
           MOVE SPACES                 TO WS-P13PF6FA-B10-REC.                  
           INITIALIZE WS-P13PF6FA-B10-REC.                                      
                                                                                
           MOVE '_'                    TO WS-PF6FA-UNDERSCORE1                  
                                          WS-PF6FA-UNDERSCORE2                  
                                          WS-PF6FA-UNDERSCORE3.                 
           MOVE 'B10'                  TO WS-PF6FA-RECORD-TYPE.                 
           MOVE WS-PARM-JOBNAME        TO WS-PF6FA-JOBNAME.                     
           MOVE WS-UPD-TMESTAMP(1:12)  TO WS-PF6FA-TMESTAMP.                    
           MOVE F740-NUM-REGN          TO WS-PF6FA-NUM-REGN                     
                                          WS-PF6FA-OUR-REF.                     
           MOVE F740-NUM-NT-FT-QUEUE   TO WS-PF6FA-TXT-QUEUE.                   
                                                                                
           PERFORM 4100-READ-BP13K813 THRU 4100-EXIT.                           
           PERFORM 4200-READ-BP13K830 THRU 4200-EXIT.                           
           PERFORM 4300-GET-NME-APPLT THRU 4300-EXIT.                           
           PERFORM 4400-GET-TXT-EXER  THRU 4400-EXIT.                           
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4100-READ-BP13K813.                                                      
      *****************************************************************         
                                                                                
           MOVE SPACES                 TO BP13K813-REC.                         
           INITIALIZE BP13K813-REC.                                             
                                                                                
           MOVE F740-CDE-NT1           TO K813-NUM-ZONE.                        
           MOVE F740-CDE-FLAT-TYPE     TO K813-NUM-FLAT-TYPE.                   
           MOVE F740-DTE-BALLOT        TO K813-DTE-BALLOT.                      
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K813-READ                          
                                                                                
                 STRING K813-DTE-LETTER-PRINT(7:2) '/'                          
                        K813-DTE-LETTER-PRINT(5:2) '/'                          
                        K813-DTE-LETTER-PRINT(1:4)                              
                    DELIMITED BY SIZE INTO WS-PF6FA-DTE-PRINT                   
                                                                                
                 MOVE K813-NME-FLAT-TYPE-LTR                                    
                                       TO WS-PF6FA-TXT-APPLIED-FT               
                 MOVE K813-NME-PROJECT-LTR                                      
                                       TO WS-PF6FA-TXT-APPLIED-ESTATE           
                 MOVE K813-TXT-SEL-PERIOD-LTR                                   
                                       TO WS-PF6FA-TXT-DTE-SEL                  
              WHEN 23                                                           
                 DISPLAY 'REC NOT FOUND IN BP13K813: '                          
                    'REGN=' F740-NUM-REGN ',KEY=' K813-KEY-FLD                  
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K813 FAIL STATUS=' WS-K813-STATUS            
                 MOVE WS-K813-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4200-READ-BP13K830.                                                      
      *****************************************************************         
                                                                                
           MOVE F740-NUM-REGN          TO K830-NUM-REGN.                        
                                                                                
           READ BP13K830 KEY IS K830-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K830-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 ADD 1                 TO WS-K830-READ                          
                 PERFORM 4250-GET-ADDRESS THRU 4250-EXIT                        
              WHEN 23                                                           
                 DISPLAY 'REC NOT FOUND IN BP13K830: ' K830-NUM-REGN            
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K830 FAIL STATUS=' WS-K830-STATUS            
                 MOVE WS-K830-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4250-GET-ADDRESS.                                                        
      *****************************************************************         
                                                                                
           IF ((K830-NUM-MAIL-ADDR = SPACES OR LOW-VALUES)                      
            OR (K830-NME-MAIL-STR-BLDG(1:47) = SPACES OR LOW-VALUES))           
              MOVE K830-NUM-BLK        TO WS-PF6FA-BLK                          
                                                                                
              IF ((K830-NUM-LVL-NUM = SPACES OR LOW-VALUES)                     
              AND (K830-NUM-HSEMAIN = SPACES OR LOW-VALUES))                    
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE K830-NUM-FLAT    TO WS-PF6FA-HSE-NO                       
              END-IF                                                            
                                                                                
              MOVE K830-NME-STREET-BLDG(1:47)                                   
                                       TO WS-PF6FA-STR-BLDG                     
                                                                                
              IF (K830-NUM-POSTAL = SPACES OR LOW-VALUES OR ZEROES)             
                 CONTINUE                                                       
              ELSE                                                              
                 STRING 'SINGAPORE ' K830-NUM-POSTAL                            
                    DELIMITED BY SIZE INTO WS-PF6FA-POSTAL                      
              END-IF                                                            
                                                                                
              MOVE SPACES              TO WS-LINK-AV02C015                      
              MOVE K830-NUM-POSTAL     TO AV02C015-IN-POSTCODE                  
              CALL 'AV02C015' USING AV02C015-IN-POSTCODE                        
                                    AV02C015-OUT-POSTCODE-CHKSUM                
              MOVE AV02C015-OUT-POSTCODE-CHKSUM                                 
                                       TO WS-PF6FA-BAR-CODE                     
           ELSE                                                                 
              MOVE K830-NUM-MAIL-BLK   TO WS-PF6FA-BLK                          
                                                                                
              IF ((K830-NUM-MAIL-LVL-NUM = SPACES OR LOW-VALUES)                
              AND (K830-NUM-MAIL-HSEMAIN = SPACES OR LOW-VALUES))               
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE K830-NUM-MAIL-FLAT                                        
                                       TO WS-PF6FA-HSE-NO                       
              END-IF                                                            
                                                                                
              MOVE K830-NME-MAIL-STR-BLDG(1:47)                                 
                                       TO WS-PF6FA-STR-BLDG                     
                                                                                
              IF (K830-NUM-MAIL-POSTAL = SPACES                                 
               OR LOW-VALUES OR ZEROES)                                         
                 CONTINUE                                                       
              ELSE                                                              
                 STRING 'SINGAPORE ' K830-NUM-MAIL-POSTAL                       
                    DELIMITED BY SIZE INTO WS-PF6FA-POSTAL                      
              END-IF                                                            
                                                                                
              MOVE SPACES              TO WS-LINK-AV02C015                      
              MOVE K830-NUM-MAIL-POSTAL                                         
                                       TO AV02C015-IN-POSTCODE                  
              CALL 'AV02C015' USING AV02C015-IN-POSTCODE                        
                                    AV02C015-OUT-POSTCODE-CHKSUM                
              MOVE AV02C015-OUT-POSTCODE-CHKSUM                                 
                                       TO WS-PF6FA-BAR-CODE                     
           END-IF.                                                              
                                                                                
       4250-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4300-GET-NME-APPLT.                                                      
      *****************************************************************         
                                                                                
           MOVE F740-NUM-REGN          TO K825-NUM-REGN                         
                                          K82P-NUM-REGN.                        
           MOVE F740-NUM-NRIC1         TO K825-NUM-NRIC                         
                                          K82P-NUM-NRIC.                        
           PERFORM 4350-READ-BP13K825 THRU 4350-EXIT.                           
           MOVE WS-NME-OCCP            TO WS-PF6FA-NME-APPLT1.                  
                                                                                
           IF (F740-NUM-NRIC2(2:1) = '#' OR SPACES OR LOW-VALUES)               
              CONTINUE                                                          
           ELSE                                                                 
              MOVE F740-NUM-NRIC2      TO K825-NUM-NRIC                         
                                          K82P-NUM-NRIC                         
              PERFORM 4350-READ-BP13K825 THRU 4350-EXIT                         
              MOVE WS-NME-OCCP         TO WS-PF6FA-NME-APPLT2                   
           END-IF.                                                              
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4350-READ-BP13K825.                                                      
      *****************************************************************         
                                                                                
           MOVE SPACES                 TO WS-NME-OCCP.                          
                                                                                
           READ BP13K825.                                                       
                                                                                
           EVALUATE WS-K825-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K825-READ                          
                 MOVE K825-NME-OCCP    TO WS-NME-OCCP                           
              WHEN 23                                                           
                 PERFORM 4355-READ-BP13K82P THRU 4355-EXIT                      
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K825 FAIL STATUS=' WS-K825-STATUS            
                 MOVE WS-K825-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       4350-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4355-READ-BP13K82P.                                                      
      *****************************************************************         
                                                                                
           READ BP13K82P.                                                       
                                                                                
           EVALUATE WS-K82P-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K82P-READ                          
                 MOVE K82P-NME-OCCP    TO WS-NME-OCCP                           
              WHEN 23                                                           
                 DISPLAY 'REC NOT FOUND IN BP13K82P: ' K82P-KEY-FLD             
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K82P FAIL STATUS=' WS-K82P-STATUS            
                 MOVE WS-K82P-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       4355-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4400-GET-TXT-EXER.                                                       
      *****************************************************************         
                                                                                
           PERFORM 4450-SEARCH-MONTH THRU 4450-EXIT.                            
                                                                                
           EVALUATE F740-NUM-ALLO-CAT                                           
              WHEN 'BTO'                                                        
                 STRING 'FLAT APPLICATION IN ' DELIMITED BY SIZE                
                        WS-DTE-BAL-MTH-9 DELIMITED BY SPACES                    
                        ' ' F740-DTE-BALLOT(1:4) ' '                            
                        'BUILD-TO-ORDER (BTO) EXERCISE'                         
                    DELIMITED BY SIZE INTO WS-PF6FA-TXT-EXERCISE                
              WHEN 'SBF'                                                        
                 STRING 'FLAT APPLICATION IN ' DELIMITED BY SIZE                
                        WS-DTE-BAL-MTH-9 DELIMITED BY SPACES                    
                        ' ' F740-DTE-BALLOT(1:4) ' '                            
                        'SALE OF BALANCE FLATS (SBF) EXERCISE'                  
                    DELIMITED BY SIZE INTO WS-PF6FA-TXT-EXERCISE                
              WHEN OTHER                                                        
                 CONTINUE                                                       
           END-EVALUATE.                                                        
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4450-SEARCH-MONTH.                                                       
      *****************************************************************         
                                                                                
           SET WS-MTH-IND              TO 1.                                    
           SEARCH WS-MONTH-TABLE                                                
              AT END                                                            
                 MOVE SPACES           TO WS-DTE-BAL-MTH-9                      
              WHEN WS-MONTH-IN-DIGITS(WS-MTH-IND) =                             
                                               F740-DTE-BALLOT(5:2)             
                 MOVE WS-MONTH-IN-WORDS(WS-MTH-IND)                             
                                       TO WS-DTE-BAL-MTH-9                      
           END-SEARCH.                                                          
                                                                                
       4450-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       7005-BP13WN.                                                             
      *****************************************************************         
                                                                                
           ADD 1                       TO WS-B6FWN-SNO.                         
           MOVE WS-B6FWN-SNO           TO WS-PF6FA-NUM-SNO.                     
                                                                                
           MOVE 'BP13WN'               TO WS-PF6FA-DESC                         
                                          WS-PF6FA-NUM-FORMID1.                 
           IF F740-CDE-ALLOC-SCH = 'FTS' OR 'STS'                               
              MOVE 'BP13HP'            TO WS-PF6FA-NUM-FORMID2                  
           ELSE                                                                 
              MOVE 'BP13V2'            TO WS-PF6FA-NUM-FORMID2                  
           END-IF.                                                              
                                                                                
           WRITE P13B6FWN-REC FROM WS-P13PF6FA-B10-REC.                         
           ADD 1                       TO WS-H6FWN-WRITE                        
                                          WS-BP13WN-CNT                         
                                          WS-FB10-WRITE.                        
                                                                                
       7005-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       7010-BP13W5.                                                             
      *****************************************************************         
                                                                                
           ADD 1                       TO WS-B6FW5-SNO.                         
           MOVE WS-B6FW5-SNO           TO WS-PF6FA-NUM-SNO.                     
                                                                                
           MOVE 'BP13W5'               TO WS-PF6FA-DESC                         
                                          WS-PF6FA-NUM-FORMID1.                 
           IF (F740-CDE-ALLOC-SCH = 'FTS' OR 'STS') OR                          
              (F740-CDE-ALLOC-SCH = 'SAP' AND                                   
              (F740-CDE-REQ-ALLOC-SCH = 'FTS' OR 'STS'))                        
              MOVE 'BP13W8'            TO WS-PF6FA-NUM-FORMID2                  
           ELSE                                                                 
              MOVE 'BP13W2'            TO WS-PF6FA-NUM-FORMID2                  
           END-IF.                                                              
           MOVE K813-NUM-UNIT-OFFER    TO WS-PF6FA-TXT-UNIT-OFFER.              
                                                                                
           WRITE P13B6FW5-REC FROM WS-P13PF6FA-B10-REC.                         
           ADD 1                       TO WS-H6FW5-WRITE                        
                                          WS-BP13W5-CNT                         
                                          WS-FB10-WRITE.                        
                                                                                
       7010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       8000-PRT-CTRL-LIST.                                                      
      *****************************************************************         
                                                                                
           IF F740-NUM-ALLO-CAT = 'BTO' OR 'SBF'                                
              PERFORM 8005-PRT-CTRL-LF6FA THRU 8005-EXIT                        
           END-IF.                                                              
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       8005-PRT-CTRL-LF6FA.                                                     
      *****************************************************************         
                                                                                
           IF WS-LF6FA-LINE-CNT > 56                                            
              ADD 1                    TO WS-LF6FA-PAGE-CNT                     
              MOVE WS-LF6FA-PAGE-CNT   TO LF6F-PAGE                             
                                                                                
              MOVE 'P13LF6FA'          TO LF6F-LISTING                          
              MOVE 'CONTROL LISTING FOR EIP RESULT LETTER'                      
                                       TO LF6F-LISTING-TYPE                     
                                                                                
              WRITE P13LF6FA-REC FROM LF6F-HDR1 AFTER PAGE                      
              WRITE P13LF6FA-REC FROM LF6F-HDR2                                 
              WRITE P13LF6FA-REC FROM LF6F-BLANK                                
              WRITE P13LF6FA-REC FROM LF6F-HDR4                                 
              WRITE P13LF6FA-REC FROM LF6F-HDR5                                 
              WRITE P13LF6FA-REC FROM LF6F-HDR6                                 
              WRITE P13LF6FA-REC FROM LF6F-BLANK                                
                                                                                
              MOVE 7                   TO WS-LF6FA-LINE-CNT                     
           END-IF.                                                              
                                                                                
           ADD 1                       TO WS-LF6FA-CNT.                         
           MOVE WS-LF6FA-CNT           TO LF6F-SERIAL-NO.                       
                                                                                
           PERFORM 8300-POPULATE-LF6F THRU 8300-EXIT.                           
                                                                                
           WRITE P13LF6FA-REC FROM LF6F-DTL1.                                   
           WRITE P13LF6FA-REC FROM LF6F-DTL2.                                   
           WRITE P13LF6FA-REC FROM LF6F-DTL3.                                   
           WRITE P13LF6FA-REC FROM LF6F-DTL4.                                   
           WRITE P13LF6FA-REC FROM LF6F-BLANK.                                  
           ADD 5                       TO WS-LF6FA-LINE-CNT.                    
                                                                                
       8005-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       8300-POPULATE-LF6F.                                                      
      *****************************************************************         
                                                                                
           MOVE F740-NUM-REF           TO LF6F-REF-NO.                          
           MOVE F740-NUM-REGN          TO LF6F-REGN-NO.                         
           MOVE WS-PF6FA-NME-APPLT1    TO LF6F-NAME-1.                          
           MOVE F740-NUM-NRIC1         TO LF6F-NRIC-1.                          
           MOVE F740-CDE-NT1           TO LF6F-NT1.                             
           MOVE F740-CDE-HOUSEHOLD     TO LF6F-HHTY.                            
           MOVE F740-NUM-MCPS          TO LF6F-MCPS.                            
           MOVE F740-CDE-RESIDENT-HA1  TO LF6F-RES-HA1.                         
           MOVE F740-CDE-FLAT-TYPE     TO LF6F-FLAT.                            
           MOVE F740-CDE-ALLOC-SCH     TO LF6F-ALLOC.                           
           MOVE F740-CDE-UNSUCC        TO LF6F-UNSUCC.                          
           MOVE F740-CDE-CATEGORY      TO LF6F-CAT.                             
           MOVE F740-CDE-SUCC-NT-ZONE  TO LF6F-SUCC.                            
           MOVE F740-CDE-ZONE          TO LF6F-ZONE.                            
           MOVE WS-PF6FA-NME-APPLT2    TO LF6F-NAME-2.                          
                                                                                
           IF F740-NUM-NRIC2 (2:1) NOT = '#'                                    
              MOVE F740-NUM-NRIC2      TO LF6F-NRIC-2                           
           ELSE                                                                 
              MOVE SPACES              TO LF6F-NRIC-2                           
           END-IF.                                                              
                                                                                
           MOVE F740-NUM-NT-FT-QUEUE   TO LF6F-QUEUE.                           
           MOVE F740-NUM-BALLOT-POSITION                                        
                                       TO LF6F-BALLOT-POS.                      
           MOVE F740-NUM-LOAN-TAG      TO LF6F-LOAN-TAG.                        
           MOVE F740-CDE-REQUEST-STATUS                                         
                                       TO LF6F-REQ-STATUS.                      
                                                                                
           MOVE WS-PF6FA-BLK           TO LF6F-BLK.                             
           MOVE WS-PF6FA-HSE-NO        TO LF6F-UNIT.                            
           MOVE WS-PF6FA-STR-BLDG      TO LF6F-ADDR2.                           
           MOVE WS-PF6FA-POSTAL        TO LF6F-ADDR3.                           
                                                                                
       8300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       8900-CREATE-FIOMS-REC.                                                   
      *****************************************************************         
                                                                                
           PERFORM 8910-WRITE-BP13FH10 THRU 8910-EXIT.                          
                                                                                
      * FTP                                                                     
           WRITE BP13FTP-REC FROM WS-FTP-LINE1.                                 
           WRITE BP13FTP-REC FROM WS-FTP-LINE2.                                 
           WRITE BP13FTP-REC FROM WS-FTP-LINE3.                                 
           ADD 3                       TO WS-FTP-WRITE.                         
                                                                                
           MOVE 'BP13WN'               TO WS-FTP-DESC.                          
           PERFORM 8920-WRITE-BP13FTP-SRC THRU 8920-EXIT.                       
           MOVE 'BP13W5'               TO WS-FTP-DESC.                          
           PERFORM 8920-WRITE-BP13FTP-SRC THRU 8920-EXIT.                       
                                                                                
           WRITE BP13FTP-REC FROM WS-FTP-LINE5.                                 
           WRITE BP13FTP-REC FROM WS-FTP-LINE6.                                 
           ADD 2                       TO WS-FTP-WRITE.                         
                                                                                
       8900-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       8910-WRITE-BP13FH10.                                                     
      *****************************************************************         
                                                                                
           MOVE 'EASSSS1D'             TO WS-GROUP-ID.                          
           MOVE 'D'                    TO WS-PRINT-SETTING.                     
           MOVE WS-H6FWN-WRITE         TO WS-P13FT10A-WRITE.                    
           PERFORM 8915-POPULATE-FH10A THRU 8915-EXIT.                          
           WRITE P13H6FWN-REC FROM WS-P13FH10A-REC.                             
           WRITE P13H6FWN-REC FROM WS-P13FT10A-REC.                             
           ADD 2                       TO WS-FH10-WRITE.                        
                                                                                
           MOVE 'EASSSS1F'             TO WS-GROUP-ID.                          
           MOVE 'D'                    TO WS-PRINT-SETTING.                     
           MOVE WS-H6FW5-WRITE         TO WS-P13FT10A-WRITE.                    
           PERFORM 8915-POPULATE-FH10A THRU 8915-EXIT.                          
           WRITE P13H6FW5-REC FROM WS-P13FH10A-REC.                             
           WRITE P13H6FW5-REC FROM WS-P13FT10A-REC.                             
           ADD 2                       TO WS-FH10-WRITE.                        
                                                                                
       8910-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       8915-POPULATE-FH10A.                                                     
      *****************************************************************         
                                                                                
      * H10                                                                     
           MOVE SPACES                 TO WS-P13FH10A-REC.                      
           INITIALIZE WS-P13FH10A-REC.                                          
                                                                                
           MOVE 'H10'                  TO WS-FH10-RECORD-TYPE.                  
           MOVE WS-PARM-JOBNAME        TO WS-FH10-JOBNAME.                      
           MOVE WS-UPD-TMESTAMP(1:14)  TO WS-FH10-JOBDATE.                      
           MOVE 'BP13W004'             TO WS-FH10-WFD-ID.                       
           MOVE 'Y'                    TO WS-FH10-OUTPUT-PRINT.                 
           MOVE WS-GROUP-ID            TO WS-FH10-GROUP-ID.                     
                                                                                
      * FEEDBACK FROM MOOI ON 18/5/2017                                         
      * IOMS NOT CATER FOR THE PRINTING OF 2 COPIES                             
      * USING PROGRAM TO TRIGGER TWICE INSTEAD                                  
      *    MOVE 2                      TO WS-FH10-COPIES.                       
      *                                                                         
           MOVE 1                      TO WS-FH10-COPIES.                       
           MOVE 'B'                    TO WS-FH10-COLOUR.                       
           MOVE 'A4W'                  TO WS-FH10-PAPER-TYPE.                   
           MOVE WS-PRINT-SETTING       TO WS-FH10-PRINT-SETTING.                
           MOVE 'N'                    TO WS-FH10-AMIS                          
                                          WS-FH10-OMR                           
                                          WS-FH10-OUTPUT-FILING                 
                                          WS-FH10-OUTPUT-ONEINBOX               
                                          WS-FH10-OUTPUT-SMS                    
                                          WS-FH10-OUTPUT-EMAIL                  
                                          WS-FH10-OUTPUT-FAX.                   
                                                                                
      * T10                                                                     
           MOVE SPACES                 TO WS-P13FT10A-REC.                      
           INITIALIZE WS-P13FT10A-REC.                                          
                                                                                
           MOVE 'T10'                  TO WS-FT10-RECORD-TYPE.                  
           MOVE WS-P13FT10A-WRITE      TO WS-FT10-TOTAL-ACCOUNTS.               
                                                                                
       8915-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       8920-WRITE-BP13FTP-SRC.                                                  
      *****************************************************************         
                                                                                
           MOVE WS-PARM-JOBNAME        TO WS-FTP-JOBNAME.                       
           MOVE WS-UPD-TMESTAMP(1:12)  TO WS-FTP-TMESTAMP.                      
                                                                                
      * B10                                                                     
           MOVE SPACES                 TO  WS-FTP-SOURCE.                       
           STRING "PUT '"                                                       
                  WS-PARM-B10-SOURCE "." WS-FTP-DESC                            
                  "' "                                                          
                  WS-FTP-DEST-REC ".TXT"                                        
              DELIMITED BY '  ' INTO WS-FTP-SOURCE                              
           END-STRING.                                                          
           WRITE BP13FTP-REC FROM WS-FTP-LINE4.                                 
           ADD 1                       TO WS-FTP-WRITE.                         
                                                                                
      * H10                                                                     
           MOVE SPACES                 TO WS-FTP-SOURCE.                        
           STRING "PUT '"                                                       
                  WS-PARM-H10-SOURCE "." WS-FTP-DESC                            
                  "' "                                                          
                  WS-FTP-DEST-REC ".H10"                                        
              DELIMITED BY '  ' INTO WS-FTP-SOURCE                              
           END-STRING.                                                          
           WRITE BP13FTP-REC FROM WS-FTP-LINE4.                                 
           ADD 1                       TO WS-FTP-WRITE.                         
                                                                                
       8920-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           CLOSE BP13F740                                                       
                 BP13K813                                                       
                 BP13K825                                                       
                 BP13K82P                                                       
                 BP13K830                                                       
                 P13B6FWN                                                       
                 P13H6FWN                                                       
                 P13B6FW5                                                       
                 P13H6FW5                                                       
                 BP13FTP                                                        
                 P13LF6FA.                                                      
                                                                                
           IF WS-K813-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K813 FAIL STATUS=' WS-K813-STATUS              
              MOVE WS-K813-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF WS-K825-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K825 FAIL STATUS=' WS-K825-STATUS              
              MOVE WS-K825-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF WS-K82P-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K82P FAIL STATUS=' WS-K82P-STATUS              
              MOVE WS-K82P-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K830 FAIL STATUS=' WS-K830-STATUS              
              MOVE WS-K830-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           DISPLAY '******************************'.                            
           DISPLAY '* PROGRAM BP13CF6F           *'.                            
           DISPLAY '* CONTROL COUNTS             *'.                            
           DISPLAY '*----------------------------*'.                            
           DISPLAY '* REC READ F740     : ' WS-F740-READ ' *'.                  
           DISPLAY '* REC READ K813     : ' WS-K813-READ ' *'.                  
           DISPLAY '* REC READ K825     : ' WS-K825-READ ' *'.                  
           DISPLAY '* REC READ K82P     : ' WS-K82P-READ ' *'.                  
           DISPLAY '* REC READ K830     : ' WS-K830-READ ' *'.                  
           DISPLAY '* REC WRITTEN FH10  : ' WS-FH10-WRITE ' *'.                 
           DISPLAY '* REC WRITTEN FB10  : ' WS-FB10-WRITE ' *'.                 
           DISPLAY '* REC WRITTEN FTP   : ' WS-FTP-WRITE ' *'.                  
           DISPLAY '* CASES FOR BP13WN  : ' WS-BP13WN-CNT ' *'.                 
           DISPLAY '* CASES FOR BP13W5  : ' WS-BP13W5-CNT ' *'.                 
           DISPLAY '******************************'.                            
                                                                                
       9000-EXIT.                                                               
           STOP RUN.                                                            
           EXIT.                                                                
                                                                                
      *======================  END OF PROGRAM  ======================*          
