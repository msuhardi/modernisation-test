       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CH33.                                                 
      *AUTHOR.        ZARITA DANE SARMIENTO.                                    
      *DATE-WRITTEN.  21JUL2014.                                                
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)           *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES :                                        *                
      *                                                        *                
      *    1. RE-ASSIGNMENT USING OIC-LIST                     *                
      *    2. ALSO UPDATE FOR ALL NRIC WITH NEW OIC            *                
      *    3. UPDATE PRE-OIC ONLY, BYPASS DBSS/EC              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY  DATE      DETAILS                          *                
      *=======  === ========= =================================*                
      *BP135406 ZDD 20140725  NEW PGM                          *                
      *BP135765 ZDD 20150429  CATER FOR POS OIC                *                
      *BP137796 ZDD 20190626  TO RE ASSIGN PREB OIC            *                
      *========================================================*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F022 ASSIGN TO BP13F022.                                  
           SELECT BP13F026 ASSIGN TO BP13F026.                                  
                                                                                
           SELECT BP13K022 ASSIGN TO BP13K022                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K022-KEY-FLD                               
                  ALTERNATE KEY   IS K022-NUM-REGN                              
                  FILE STATUS     IS WS-K022-STATUS.                            
                                                                                
           SELECT BP13K024 ASSIGN TO BP13K024                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K024-KEY-FLD                               
                  FILE STATUS     IS WS-K024-STATUS.                            
                                                                                
           SELECT BP13K034 ASSIGN TO BP13K034                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K034-KEY-FLD                               
                  ALTERNATE KEY   IS K034-NUM-NRIC                              
                  FILE STATUS     IS WS-K034-STATUS.                            
                                                                                
                                                                                
                                                                                
           SELECT BP13K028 ASSIGN TO BP13K028                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K028-KEY-FLD                               
                  FILE STATUS     IS WS-K028-STATUS.                            
                                                                                
           SELECT P13LH33A ASSIGN TO P13LH33A.                                  
           SELECT BP13RPT1 ASSIGN TO BP13RPT1.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F022                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 100  CHARACTERS.                                    
       COPY BP13F022.                                                           
                                                                                
       FD   BP13F026                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 100  CHARACTERS.                                    
       COPY BP13F026.                                                           
                                                                                
       FD   BP13RPT1                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 100  CHARACTERS.                                    
       01   BP13RPT1-REC   PIC X(100).                                          
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K024                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K024.                                                           
                                                                                
                                                                                
       FD   BP13K034                                                            
            RECORD CONTAINS 30 CHARACTERS.                                      
       COPY BP13K034.                                                           
                                                                                
       FD   BP13K028                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K028.                                                           
                                                                                
       FD   P13LH33A                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE  IS F.                                               
       01   P13LH33A-REC    PIC X(132).                                         
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F022-EOF-SW                 PIC X(1)  VALUE 'N'.              
           05  WS-F026-EOF-SW                 PIC X(1)  VALUE 'N'.              
           05  WS-K022-EOF-SW                 PIC X(1)  VALUE 'N'.              
           05  WS-K024-EOF-SW                 PIC X(1)  VALUE 'N'.              
           05  WS-K034-EOF-SW                 PIC X(1)  VALUE 'N'.              
           05  WS-K022-STATUS                 PIC 9(2)  VALUE 0.                
           05  WS-K024-STATUS                 PIC 9(2)  VALUE 0.                
           05  WS-K034-STATUS                 PIC 9(2)  VALUE 0.                
           05  WS-K028-STATUS                 PIC 9(2)  VALUE 0.                
           05  WS-CNT-READ-F022               PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-READ-F026               PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-READ-K034               PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-WRIT-LH33               PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-K022-WRT                PIC 9(6)  VALUE ZEROS.            
           05  WS-OIC-APPL-UPD                PIC 9(6)  VALUE ZEROS.            
           05  WS-SKIPPED                     PIC 9(6)  VALUE ZEROS.            
           05  WS-OIC-ASSIGNED                PIC 9(6)  VALUE ZEROS.            
           05  WS-REC-DELETED                 PIC 9(6)  VALUE ZEROS.            
           05  WS-PRE-OIC-UPD                 PIC 9(6)  VALUE ZEROS.            
           05  WS-K028-UPDATED                PIC 9(6)  VALUE ZEROS.            
           05  WS-RPT-WRITE                   PIC 9(6)  VALUE ZEROS.            
           05  WS-REG-NUM                     PIC X(8)  VALUE SPACES.           
           05  WS-PREV-NRIC                   PIC X(9)  VALUE SPACES.           
           05  WS-PREV-REGN                   PIC X(8)  VALUE SPACES.           
           05  WS-BP13F022-REC                PIC X(100) VALUE SPACES.          
                                                                                
       01  WS-LH33-CONTROLS.                                                    
           05  WS-LH33-LINECTR-A              PIC 9(2)  VALUE 60.               
           05  WS-LH33-PAGECTR-A              PIC 9(5)  VALUE ZEROS.            
           05  WS-LH33-SNO-A                  PIC 9(5)  VALUE 0.                
           05  WS-OLD-OIC                     PIC X(5)  VALUE SPACES.           
           05  WS-NEW-OIC                     PIC X(5)  VALUE SPACES.           
           05  WS-CANCEL                      PIC 9(5)  VALUE ZEROES.           
                                                                                
       01  WS-MISCELLANEOUS-VARS.                                               
           05  WS-BLANK-LINE                  PIC X(132) VALUE SPACES.          
           05  WS-K024-KEY-FLD                PIC X(05) VALUE SPACES.           
           05  WS-REGN-ALLOC-CTR              PIC 9(6)  VALUE ZEROS.            
           05  WS-PREV-OIC                    PIC X(5).                         
           05  WS-ALLO-CAT-PREV               PIC X(1).                         
           05  WS-CNT                         PIC 9(3)  VALUE 0.                
           05  WS-SKIP                        PIC 9(1)  VALUE 0.                
           05  WS-FLAG                        PIC X(1)  VALUE 'N'.              
           05  WS-FLAG-K595                   PIC X(1)  VALUE 'N'.              
           05  WS-FLAG-K730                   PIC X(1)  VALUE 'N'.              
           05  WS-DONE                        PIC X(1)  VALUE 'N'.              
           05  WS-OIC-FND                     PIC X(1)  VALUE 'N'.              
           05  WS-READ-FLAG-BL                PIC X(1)  VALUE 'N'.              
           05  WS-FOUND                       PIC X(1)  VALUE 'N'.              
           05  WS-WRITE-K022                  PIC X(1)  VALUE 'N'.              
           05  WS-NOT-EXISTING                PIC X(1)  VALUE 'N'.              
           05  WS-PREV-OIC-TAG                PIC X(1)  VALUE 'N'.              
           05  WS-K730-FOUND                  PIC X(1)  VALUE 'N'.              
           05  WS-TEAM1                       PIC X(1)  VALUE 'N'.              
           05  WS-TEAM2                       PIC X(1)  VALUE 'N'.              
           05  WS-TEAM3                       PIC X(1)  VALUE 'N'.              
           05  WS-TEAM4                       PIC X(1)  VALUE 'N'.              
           05  WS-TEAM5                       PIC X(1)  VALUE 'N'.              
           05  WS-TEAM6                       PIC X(1)  VALUE 'N'.              
           05  WS-TEAM7                       PIC X(1)  VALUE 'N'.              
           05  WS-TEAM8                       PIC X(1)  VALUE 'N'.              
                                                                                
       01  WS-TIME-DATES.                                                       
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY.                                                 
                   15  WS-SYS-CC              PIC 9(2).                         
                   15  WS-SYS-YY              PIC 9(2).                         
               10  WS-SYS-MM                  PIC 9(2).                         
               10  WS-SYS-DD                  PIC 9(2).                         
                                                                                
           05  WS-EDITED-DATE.                                                  
               10  WS-EDITED-DD               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-MM               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-YY               PIC 9(4).                         
                                                                                
       77 WS-POST-BOOK                        PIC X(01) VALUE 'N'.              
                                                                                
      *============================================================*            
      *         BP13LH33  -  R E P O R T    L A Y O U T            *            
      *============================================================*            
                                                                                
       01  LH33-HEADER-1A.                                                      
           05  FILLER              PIC X(08)     VALUE 'P13LH33A'.              
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(25)     VALUE SPACES.                  
           05  FILLER              PIC X(41)     VALUE                          
               'S Y S T E M    O F    C O M M I T M E N T'.                     
           05  FILLER              PIC X(12)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE :'.                
           05  LH33-RUN-DATE-A     PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LH33-PAGE-NO-A      PIC ZZZZ9 .                                  
                                                                                
       01  LH33-HEADER-2A.                                                      
           05  FILLER              PIC X(41)     VALUE SPACES.                  
           05  FILLER              PIC X(55)     VALUE                          
           '        REPORT ON CASES ASSIGNED TO OIC          '.                 
                                                                                
                                                                                
       01  LH33-HEADER-03.                                                      
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE ' S/N '.                 
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'OLD OIC'.               
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE 'NRIC     '.             
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'REGNO   '.              
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE '         ' .            
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'NEW OIC'.               
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(30)     VALUE 'OIC NAME'.              
           05  FILLER              PIC X(05)     VALUE SPACES.                  
                                                                                
       01  LH33-HEADER-04.                                                      
           05  FILLER              PIC X(130)    VALUE ALL '-'.                 
                                                                                
       01  LH33-DETAIL.                                                         
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LH33-SNO            PIC ZZZZ9.                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LH33-OLD-OIC        PIC X(07).                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LH33-NRIC           PIC X(09).                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LH33-REGNO          PIC X(08).                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(10).                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LH33-OIC            PIC X(05).                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LH33-NME-OIC        PIC X(30).                                   
           05  FILLER              PIC X(05)     VALUE SPACES.                  
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
                                                                                
           PERFORM 2000-READ-F022         THRU 2000-EXIT.                       
           PERFORM 2100-READ-F026         THRU 2100-EXIT.                       
           PERFORM 3000-PROCESS-F022      THRU 3000-EXIT                        
                  UNTIL WS-F022-EOF-SW = 'Y'.                                   
                                                                                
           PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F022                                                 
                       BP13K034                                                 
                       BP13F026                                                 
                I-O    BP13K022                                                 
                       BP13K024                                                 
                       BP13K028                                                 
                OUTPUT P13LH33A                                                 
                       BP13RPT1.                                                
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K022 OPEN ERROR, STATUS IS ' WS-K022-STATUS          
              MOVE WS-K022-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-K024-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K024 OPEN ERROR, STATUS IS ' WS-K024-STATUS          
              MOVE WS-K024-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
                                                                                
           IF WS-K034-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K034 OPEN ERROR, STATUS IS ' WS-K034-STATUS          
              MOVE WS-K034-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-K028-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K028 OPEN ERROR, STATUS IS ' WS-K028-STATUS          
              MOVE WS-K028-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-SYSTEM-DATE.                 
           MOVE   WS-SYS-DD           TO WS-EDITED-DD.                          
           MOVE   WS-SYS-MM           TO WS-EDITED-MM.                          
           MOVE   WS-SYS-CCYY         TO WS-EDITED-YY.                          
           MOVE   WS-EDITED-DATE      TO LH33-RUN-DATE-A.                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2000-READ-F022.                                                          
      ****************************************************************          
                                                                                
           READ BP13F022                                                        
              AT END MOVE 'Y' TO WS-F022-EOF-SW                                 
              GO TO 2000-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD  +1                TO WS-CNT-READ-F022.                          
                                                                                
                                                                                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2100-READ-F026.                                                          
      ****************************************************************          
                                                                                
           READ BP13F026                                                        
              AT END MOVE 'Y' TO WS-F026-EOF-SW                                 
              GO TO 2100-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD  +1                TO WS-CNT-READ-F026.                          
                                                                                
           IF F026-NUM-TEAM1 = 'Y'                                              
              MOVE '1'    TO WS-TEAM1                                           
           END-IF                                                               
                                                                                
           IF F026-NUM-TEAM2 = 'Y'                                              
              MOVE '2'    TO WS-TEAM2                                           
           END-IF                                                               
                                                                                
           IF F026-NUM-TEAM3 = 'Y'                                              
              MOVE '3'    TO WS-TEAM3                                           
           END-IF                                                               
                                                                                
           IF F026-NUM-TEAM4 = 'Y'                                              
              MOVE '4'    TO WS-TEAM4                                           
           END-IF                                                               
                                                                                
           IF F026-NUM-TEAM5 = 'Y'                                              
              MOVE '5'    TO WS-TEAM5                                           
           END-IF                                                               
                                                                                
           IF F026-NUM-TEAM6 = 'Y'                                              
              MOVE '6'    TO WS-TEAM6                                           
           END-IF.                                                              
                                                                                
           IF F026-NUM-TEAM7 = 'Y'                                              
              MOVE '7'    TO WS-TEAM7                                           
           END-IF.                                                              
                                                                                
           IF F026-NUM-TEAM8 = 'Y'                                              
              MOVE '8'    TO WS-TEAM8                                           
           END-IF.                                                              
                                                                                
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2500-READ-K024.                                                          
      ****************************************************************          
                                                                                
           MOVE  LOW-VALUES   TO K024-KEY-FLD.                                  
           MOVE  'N'          TO WS-FLAG.                                       
           START BP13K024 KEY >  K024-KEY-FLD.                                  
                                                                                
           IF WS-K024-STATUS = 00                                               
                 PERFORM UNTIL WS-FLAG = 'Y'                                    
                    READ BP13K024 NEXT RECORD                                   
                         AT END MOVE 'Y' TO WS-FLAG                             
                    END-READ                                                    
                    IF WS-K024-STATUS = 00               AND                    
                       K024-CDE-LAST-REC-BL = 'Y'        AND                    
                       K024-CDE-POST-BOOK = F026-NUM-POS-OIC                    
                       MOVE 'N'                TO K024-CDE-LAST-REC-BL          
                       PERFORM 5100-REWRITE-K024  THRU 5100-EXIT                
                       MOVE 'Y' TO WS-FLAG                                      
                    END-IF                                                      
                 END-PERFORM                                                    
           ELSE                                                                 
              DISPLAY 'ERROR WITH START READ OF BP13K024. STATUS IS '           
                       WS-K024-STATUS                                           
              MOVE WS-K024-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2580-READ-K022.                                                          
      ****************************************************************          
                                                                                
           READ BP13K022.                                                       
                                                                                
           EVALUATE WS-K022-STATUS                                              
             WHEN 00                                                            
             WHEN 23                                                            
                CONTINUE                                                        
                                                                                
             WHEN OTHER                                                         
              DISPLAY 'ERROR WITH READ OF BP13K022. STATUS IS '                 
                       WS-K022-STATUS                                           
              MOVE WS-K022-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
             END-EVALUATE.                                                      
                                                                                
       2580-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2600-GET-K022.                                                           
      ****************************************************************          
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           IF WS-K022-STATUS = 00  OR 02                                        
              MOVE 'Y'    TO  WS-FOUND                                          
           ELSE                                                                 
             IF WS-K022-STATUS = 10  OR 23                                      
                MOVE 'N'    TO  WS-FOUND                                        
                DISPLAY 'REGN NOT FOUND  BP13K022. STATUS IS '                  
                         WS-K022-STATUS '/' K022-NUM-REGN                       
             ELSE                                                               
                DISPLAY 'ERROR WITH  READ OF BP13K022. STATUS IS '              
                         WS-K022-STATUS '/' K022-NUM-REGN                       
                MOVE WS-K022-STATUS              TO RETURN-CODE                 
                PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                   
             END-IF                                                             
           END-IF.                                                              
                                                                                
       2600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3000-PROCESS-F022.                                                       
      ****************************************************************          
                                                                                
           MOVE  LOW-VALUES   TO K034-KEY-FLD.                                  
           MOVE  'N'          TO WS-K034-EOF-SW.                                
           MOVE F022-NUM-NRIC             TO   WS-PREV-NRIC                     
                                               K034-NUM-NRIC.                   
           MOVE F022-NUM-REGN             TO   WS-PREV-REGN                     
                                                                                
           PERFORM 3100-PROCESS-REASSIGN  THRU 3100-EXIT                        
                                                                                
           PERFORM 4000-START-K034        THRU 4000-EXIT.                       
           PERFORM 4100-READNEXT-K034     THRU 4100-EXIT.                       
                                                                                
           PERFORM UNTIL K034-NUM-NRIC NOT = F022-NUM-NRIC OR                   
                   WS-K034-EOF-SW = 'Y'                                         
               IF F022-NUM-NRIC = K034-NUM-NRIC AND                             
                  F022-NUM-REGN NOT = K034-NUM-REGN                             
                  IF F026-NUM-CANCEL = 'N'                                      
                     IF K034-NUM-STATUS NOT = 'C'                               
                        CONTINUE                                                
                     ELSE                                                       
                        ADD 1 TO WS-CANCEL                                      
                        PERFORM 4100-READNEXT-K034 THRU 4100-EXIT               
                        PERFORM 2000-READ-F022    THRU 2000-EXIT                
                        GO TO 3000-EXIT                                         
                     END-IF                                                     
                  END-IF                                                        
                  MOVE SPACES                TO   BP13K022-REC                  
                  MOVE K034-NUM-REGN         TO   K022-NUM-REGN                 
                                                  WS-PREV-REGN                  
                  IF K034-NUM-REGN(1:1) NOT =  '5' AND '6'                      
                    PERFORM 2600-GET-K022      THRU 2600-EXIT                   
                    IF WS-FOUND = 'Y'                                           
                     IF F026-NUM-POS-OIC = 'N'                                  
                       IF K022-NUM-OIC-APPL NOT = SPACES AND LOW-VALUES         
                         MOVE K022-NUM-OIC-APPL  TO WS-OLD-OIC                  
                         MOVE K024-NUM-OIC   TO K022-NUM-OIC-APPL               
                                                WS-NEW-OIC                      
                         PERFORM 6000-WRITE-K028    THRU 6000-EXIT              
                         PERFORM 5200-REWRITE-K022  THRU 5200-EXIT              
                         ADD 1   TO    WS-OIC-APPL-UPD                          
                         PERFORM 8000-PRINT-RPT-A    THRU 8000-EXIT             
                       ELSE                                                     
                         PERFORM 6000-WRITE-K028    THRU 6000-EXIT              
                         PERFORM 7000-DELETE-K022   THRU 7000-EXIT              
                         PERFORM 5000-WRITE-K022    THRU 5000-EXIT              
                       END-IF                                                   
                     ELSE                                                       
                         PERFORM 6000-WRITE-K028    THRU 6000-EXIT              
                         PERFORM 7000-DELETE-K022   THRU 7000-EXIT              
                         PERFORM 5000-WRITE-K022    THRU 5000-EXIT              
                     END-IF                                                     
                    END-IF                                                      
                  END-IF                                                        
                  PERFORM 4100-READNEXT-K034 THRU 4100-EXIT                     
               ELSE                                                             
                  PERFORM 4100-READNEXT-K034 THRU 4100-EXIT                     
               END-IF                                                           
           END-PERFORM.                                                         
                                                                                
           PERFORM 2000-READ-F022    THRU 2000-EXIT.                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3100-PROCESS-REASSIGN.                                                   
      ****************************************************************          
                                                                                
           PERFORM 2500-READ-K024  THRU 2500-EXIT.                              
                                                                                
           MOVE  0                    TO WS-CNT.                                
           MOVE 'N'                   TO WS-FLAG.                               
                                                                                
           PERFORM UNTIL WS-FLAG = 'Y'                                          
           MOVE 'N'                     TO WS-OIC-FND                           
           PERFORM 4500-READNEXT-K024  THRU 4500-EXIT                           
             UNTIL WS-OIC-FND = 'Y'                                             
                OR WS-K024-EOF-SW = 'Y'                                         
           IF WS-K024-EOF-SW = 'Y'                                              
              ADD 1                    TO WS-CNT                                
              MOVE  LOW-VALUES   TO K024-KEY-FLD                                
              START BP13K024 KEY >  K024-KEY-FLD                                
              MOVE 'N'                      TO WS-OIC-FND                       
              MOVE 'N'                      TO WS-K024-EOF-SW                   
              PERFORM 4500-READNEXT-K024  THRU 4500-EXIT                        
                UNTIL WS-OIC-FND = 'Y'                                          
                   OR WS-K024-EOF-SW = 'Y'                                      
              MOVE  'N'          TO WS-K024-EOF-SW                              
              IF WS-DONE = 'Y'                                                  
                 MOVE  'N'          TO WS-DONE                                  
                 WRITE P13LH33A-REC    FROM WS-BLANK-LINE                       
                 ADD 1                 TO WS-LH33-LINECTR-A                     
              END-IF                                                            
           END-IF                                                               
           IF K024-CDE-TEAM = WS-TEAM1 OR                                       
              K024-CDE-TEAM = WS-TEAM2 OR                                       
              K024-CDE-TEAM = WS-TEAM3 OR                                       
              K024-CDE-TEAM = WS-TEAM4 OR                                       
              K024-CDE-TEAM = WS-TEAM5 OR                                       
              K024-CDE-TEAM = WS-TEAM6 OR                                       
              K024-CDE-TEAM = WS-TEAM7 OR                                       
              K024-CDE-TEAM = WS-TEAM8                                          
                                                                                
              IF K024-CDE-BLOCK NOT = 'Y'                                       
                 IF K024-NUM-RATIO = K024-NUM-CUR-CNT                           
                    SUBTRACT K024-NUM-RATIO FROM K024-NUM-MAX-CNT               
                             GIVING WS-SKIP                                     
                    IF WS-SKIP <= K024-NUM-SKIP                                 
                       MOVE 1                     TO K024-NUM-CUR-CNT           
                       MOVE ZEROES                TO K024-NUM-SKIP              
                       MOVE 'Y'           TO K024-CDE-LAST-REC-BL               
                       PERFORM 5100-REWRITE-K024  THRU 5100-EXIT                
                       MOVE F022-KEY-FLD    TO K022-KEY-FLD                     
                       PERFORM 2580-READ-K022   THRU 2580-EXIT                  
                       PERFORM 6000-WRITE-K028  THRU 6000-EXIT                  
                       IF F026-NUM-POS-OIC = 'Y'                                
                          PERFORM 7000-DELETE-K022 THRU 7000-EXIT               
                          PERFORM 5000-WRITE-K022    THRU 5000-EXIT             
                       ELSE                                                     
                        IF K022-NUM-OIC-APPL = SPACES OR  LOW-VALUES            
                           PERFORM 7000-DELETE-K022 THRU 7000-EXIT              
                           PERFORM 5000-WRITE-K022    THRU 5000-EXIT            
                        ELSE                                                    
                         MOVE K022-NUM-OIC-APPL TO WS-OLD-OIC                   
                         MOVE K024-NUM-OIC   TO K022-NUM-OIC-APPL               
                                                WS-NEW-OIC                      
                         PERFORM 6000-WRITE-K028    THRU 6000-EXIT              
                         PERFORM 5200-REWRITE-K022  THRU 5200-EXIT              
                         ADD 1   TO    WS-OIC-APPL-UPD                          
                         PERFORM 8000-PRINT-RPT-A    THRU 8000-EXIT             
                        END-IF                                                  
                       END-IF                                                   
                       ADD 1                      TO WS-REGN-ALLOC-CTR          
                       MOVE 'Y'                   TO WS-FLAG                    
                       MOVE 'Y'                   TO WS-DONE                    
                    ELSE                                                        
                       ADD  1                     TO K024-NUM-SKIP              
                       PERFORM 5100-REWRITE-K024  THRU 5100-EXIT                
                    END-IF                                                      
                 ELSE                                                           
                    ADD   1                    TO K024-NUM-CUR-CNT              
                    MOVE 'Y'              TO K024-CDE-LAST-REC-BL               
                    PERFORM 5100-REWRITE-K024  THRU 5100-EXIT                   
                    MOVE F022-KEY-FLD    TO K022-KEY-FLD                        
                    PERFORM 2580-READ-K022   THRU 2580-EXIT                     
                    PERFORM 6000-WRITE-K028  THRU 6000-EXIT                     
                    IF F026-NUM-POS-OIC = 'Y'                                   
                        PERFORM 7000-DELETE-K022 THRU 7000-EXIT                 
                        PERFORM 5000-WRITE-K022    THRU 5000-EXIT               
                    ELSE                                                        
                      IF K022-NUM-OIC-APPL = SPACES OR  LOW-VALUES              
                        PERFORM 7000-DELETE-K022 THRU 7000-EXIT                 
                        PERFORM 5000-WRITE-K022    THRU 5000-EXIT               
                      ELSE                                                      
                         MOVE K022-NUM-OIC-APPL  TO WS-OLD-OIC                  
                         MOVE K024-NUM-OIC   TO K022-NUM-OIC-APPL               
                                                WS-NEW-OIC                      
                         PERFORM 6000-WRITE-K028    THRU 6000-EXIT              
                         PERFORM 5200-REWRITE-K022  THRU 5200-EXIT              
                         ADD 1   TO    WS-OIC-APPL-UPD                          
                         PERFORM 8000-PRINT-RPT-A    THRU 8000-EXIT             
                      END-IF                                                    
                    END-IF                                                      
                    ADD 1                      TO WS-REGN-ALLOC-CTR             
                    MOVE 'Y'                   TO WS-FLAG                       
                    MOVE 'Y'                   TO WS-DONE                       
                 END-IF                                                         
              END-IF                                                            
              IF WS-CNT > 1                                                     
                 MOVE 'Y'                         TO WS-FLAG                    
              END-IF                                                            
           END-IF                                                               
           END-PERFORM.                                                         
                                                                                
           IF WS-CNT > 1                                                        
              DISPLAY 'ERROR: ALL OIC''S MARKED AS BLOCK.'                      
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4000-START-K034.                                                         
      ****************************************************************          
                                                                                
           START BP13K034 KEY >= K034-NUM-NRIC                                  
                                                                                
           IF WS-K034-STATUS = 00 OR 02                                         
              CONTINUE                                                          
           ELSE                                                                 
             IF WS-K034-STATUS = 10 OR 23                                       
                MOVE 'Y'    TO WS-K034-EOF-SW                                   
             ELSE                                                               
              DISPLAY 'ERROR WITH START READ OF BP13K034. STATUS IS '           
                       WS-K034-STATUS                                           
              MOVE WS-K034-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
             END-IF                                                             
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4100-READNEXT-K034.                                                      
      ****************************************************************          
                                                                                
            READ BP13K034 NEXT RECORD                                           
               AT END MOVE 'Y' TO WS-K034-EOF-SW                                
               GO TO 4100-EXIT                                                  
            END-READ.                                                           
                                                                                
            ADD 1 TO WS-CNT-READ-K034.                                          
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4500-READNEXT-K024.                                                      
      ****************************************************************          
                                                                                
            READ BP13K024 NEXT RECORD                                           
               AT END MOVE 'Y' TO WS-K024-EOF-SW                                
               GO TO 4500-EXIT                                                  
            END-READ.                                                           
                                                                                
            IF WS-K024-STATUS = 00                                              
               IF K024-CDE-POST-BOOK = F026-NUM-POS-OIC                         
                  MOVE 'Y'                    TO WS-OIC-FND                     
               END-IF                                                           
            ELSE                                                                
            IF WS-K024-STATUS = 46                                              
               MOVE 'Y'                       TO WS-K024-EOF-SW                 
            ELSE                                                                
               DISPLAY 'ERROR READING BP13K024. STATUS IS '                     
                        WS-K024-STATUS                                          
               MOVE WS-K024-STATUS              TO RETURN-CODE                  
               PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                    
            END-IF.                                                             
                                                                                
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5000-WRITE-K022.                                                         
      ****************************************************************          
                                                                                
            MOVE SPACES                    TO BP13K022-REC.                     
            INITIALIZE                        BP13K022-REC.                     
            MOVE WS-BP13F022-REC           TO BP13K022-REC.                     
                                                                                
            MOVE K024-NUM-OIC              TO K022-NUM-OIC                      
                                              WS-NEW-OIC.                       
            MOVE 'BP13CH33'                TO K022-NUM-USERID                   
            MOVE WS-SYSTEM-DATE            TO K022-DTE-UPDATE.                  
                                                                                
            WRITE BP13K022-REC.                                                 
                                                                                
            EVALUATE WS-K022-STATUS                                             
            WHEN 00                                                             
            WHEN 02                                                             
            WHEN 22                                                             
               ADD 1 TO WS-CNT-K022-WRT                                         
            WHEN OTHER                                                          
               DISPLAY 'ERROR WRITING BP13K022. STATUS IS '                     
                        WS-K022-STATUS ' / ' K022-NUM-REGN                      
               MOVE  WS-K022-STATUS          TO RETURN-CODE                     
               PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
            END-EVALUATE.                                                       
                                                                                
            PERFORM 8000-PRINT-RPT-A    THRU 8000-EXIT.                         
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5100-REWRITE-K024.                                                       
      ****************************************************************          
                                                                                
            MOVE 'BP13CH33'            TO K024-NUM-USERID.                      
            MOVE WS-SYSTEM-DATE        TO K024-DTE-UPDATE.                      
                                                                                
            REWRITE BP13K024-REC.                                               
                                                                                
            EVALUATE WS-K024-STATUS                                             
            WHEN 00                                                             
               CONTINUE                                                         
            WHEN OTHER                                                          
               DISPLAY 'ERROR WRITING BP13K024. STATUS IS '                     
                        WS-K024-STATUS                                          
               MOVE WS-K024-STATUS              TO RETURN-CODE                  
               PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5200-REWRITE-K022.                                                       
      ****************************************************************          
                                                                                
            MOVE 'BP13CH33'            TO K022-NUM-USERID.                      
            MOVE WS-SYSTEM-DATE        TO K022-DTE-UPDATE.                      
                                                                                
            REWRITE BP13K022-REC.                                               
                                                                                
            EVALUATE WS-K022-STATUS                                             
            WHEN 00                                                             
            WHEN 02                                                             
               CONTINUE                                                         
            WHEN OTHER                                                          
               DISPLAY 'ERROR WRITING BP13K022. STATUS IS '                     
                        WS-K022-STATUS                                          
               MOVE WS-K022-STATUS              TO RETURN-CODE                  
               PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       6000-WRITE-K028.                                                         
      ****************************************************************          
                                                                                
           MOVE SPACES TO BP13K028-REC                                          
           MOVE K022-NUM-REGN        TO   K028-NUM-REGN                         
           MOVE K022-NUM-OIC         TO   K028-NUM-OIC                          
           MOVE WS-SYSTEM-DATE       TO   K028-DTE-HISTORY                      
           MOVE K022-CDE-ALLO-CAT    TO   K028-CDE-ALLO-CAT                     
           MOVE K022-CDE-BANKLOAN    TO   K028-CDE-BANKLOAN                     
           MOVE K022-CDE-ALERT       TO   K028-CDE-ALERT                        
           MOVE K022-CDE-ALERT-TYPE  TO   K028-CDE-ALERT-TYPE                   
           MOVE K022-NUM-OIC-APPL    TO   K028-NUM-OIC-APPL                     
           MOVE K022-NUM-PRE-OIC-TAG TO   K028-NUM-PRE-OIC-TAG                  
           MOVE K022-NUM-USERID      TO   K028-NUM-USERID                       
           MOVE K022-DTE-UPDATE      TO   K028-DTE-UPDATE                       
                                                                                
           WRITE BP13K028-REC.                                                  
                                                                                
           EVALUATE WS-K028-STATUS                                              
             WHEN 00                                                            
             WHEN 22                                                            
                 ADD 1 TO WS-K028-UPDATED                                       
             WHEN 02                                                            
                 DISPLAY 'RECORD ALREADY EXIST..STAT' WS-K028-STATUS            
      *          PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                     
             WHEN OTHER                                                         
                 DISPLAY 'ERROR WRITING THE RECORD..STAT'                       
                 WS-K028-STATUS                                                 
                 MOVE WS-K028-STATUS           TO RETURN-CODE                   
                 PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       7000-DELETE-K022.                                                        
      ****************************************************************          
                                                                                
           MOVE BP13K022-REC      TO WS-BP13F022-REC.                           
           MOVE K022-NUM-OIC      TO WS-OLD-OIC.                                
                                                                                
           DELETE BP13K022.                                                     
                                                                                
           IF WS-K022-STATUS NOT  = 00 AND 23                                   
              DISPLAY 'ERROR DELETING THE RECORD - K022..STAT'                  
              WS-K022-STATUS '/' K022-NUM-REGN                                  
              MOVE WS-K022-STATUS           TO RETURN-CODE                      
              PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                        
           END-IF.                                                              
                                                                                
           ADD 1 TO WS-REC-DELETED.                                             
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       8000-PRINT-RPT-A.                                                        
      ****************************************************************          
                                                                                
           ADD  1                             TO WS-LH33-SNO-A.                 
           MOVE WS-LH33-SNO-A                 TO LH33-SNO.                      
                                                                                
           PERFORM 8500-MOVE-VALUES     THRU 8500-EXIT.                         
                                                                                
           IF WS-LH33-LINECTR-A >= 55                                           
              ADD                   1   TO WS-LH33-PAGECTR-A                    
              MOVE WS-LH33-PAGECTR-A    TO LH33-PAGE-NO-A                       
              WRITE P13LH33A-REC    FROM LH33-HEADER-1A  AFTER PAGE             
              WRITE P13LH33A-REC    FROM LH33-HEADER-2A                         
              WRITE P13LH33A-REC    FROM LH33-HEADER-03  AFTER 2                
              WRITE P13LH33A-REC    FROM LH33-HEADER-04                         
              WRITE P13LH33A-REC    FROM LH33-DETAIL                            
              MOVE                  6 TO WS-LH33-LINECTR-A                      
           ELSE                                                                 
              ADD                   1 TO WS-LH33-LINECTR-A                      
              WRITE P13LH33A-REC    FROM LH33-DETAIL                            
           END-IF.                                                              
                                                                                
           ADD 1                    TO WS-CNT-WRIT-LH33.                        
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       8500-MOVE-VALUES.                                                        
      ****************************************************************          
                                                                                
           MOVE WS-OLD-OIC                    TO LH33-OLD-OIC.                  
           MOVE WS-PREV-NRIC                  TO LH33-NRIC.                     
           MOVE WS-PREV-REGN                  TO LH33-REGNO.                    
           MOVE WS-NEW-OIC                    TO LH33-OIC.                      
           MOVE K024-NME-OIC                  TO LH33-NME-OIC.                  
                                                                                
           DISPLAY 'FROM OIC - ' WS-OLD-OIC    ' ; '                            
                    'NRIC    - '  WS-PREV-NRIC ' ;'                             
                    'REGN    - '  WS-PREV-REGN  ' ; '                           
                    'NEW OIC - '  WS-NEW-OIC    ' ; '.                          
                                                                                
           STRING WS-OLD-OIC   ';' WS-PREV-NRIC ';'                             
                  WS-PREV-REGN  ';' WS-NEW-OIC   ';'                            
           DELIMITED BY SIZE INTO  BP13RPT1-REC.                                
                                                                                
           WRITE BP13RPT1-REC.                                                  
           ADD 1 TO WS-RPT-WRITE.                                               
                                                                                
       8500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13CH33 CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13F022 RECORDS READ        = ' WS-CNT-READ-F022.          
           DISPLAY 'NO OF CASES UPDATED IN K022  = ' WS-REGN-ALLOC-CTR.         
           DISPLAY 'NO OF CASE READ IN BP13K034  = ' WS-CNT-READ-K034.          
           DISPLAY 'NO OF CASES WRITTEN ON RPT1  = ' WS-RPT-WRITE.              
           DISPLAY 'NO OF CASES WRITTEN ON LH33  = ' WS-CNT-WRIT-LH33.          
           DISPLAY 'NO OF CASES OIC APPL UPDATED = ' WS-OIC-APPL-UPD.           
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF SKIPPED CASES          = ' WS-SKIPPED.                
           DISPLAY 'NO OF CANCELLED CASES        = ' WS-CANCEL.                 
           DISPLAY SPACES.                                                      
           DISPLAY '*==============================================*'.          
                                                                                
           CLOSE BP13F022                                                       
                 BP13K024                                                       
                 BP13F026                                                       
                 BP13K034                                                       
                 BP13K022                                                       
                 BP13K028                                                       
                 P13LH33A                                                       
                 BP13RPT1.                                                      
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K022 CLOSE ERROR, STATUS IS ' WS-K022-STATUS         
              MOVE WS-K022-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           IF WS-K024-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K024 CLOSE ERROR, STATUS IS ' WS-K024-STATUS         
              MOVE WS-K024-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           IF WS-K034-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K034 CLOSE ERROR, STATUS IS ' WS-K034-STATUS         
              MOVE WS-K034-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           IF WS-K028-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K028 CLOSE ERROR, STATUS IS ' WS-K028-STATUS         
              MOVE WS-K028-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
       9999-EXIT.                                                               
           EXIT.                                                                
