       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CL00.                                                 
      *AUTHOR.        BALA AMUTHAN.                                             
      *DATE-WRITTEN.  09/11/2006.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT  (SOC)                                 *           
      * =========================================================== *           
      *                                                             *           
      *  OBJECTIVE  :                                               *           
      *      CREATE DATA FOR PRINTING REPORT FOR COU.               *           
      *                                                             *           
      *  INPUT FILES:  1. BP13F800                                  *           
      *                2. BP13KB20                                  *           
      *                3. BP13KD00                                  *           
      *                4. BP13KD80                                  *           
      *                5. BP13K820                                  *           
      *                6. BP27K025                                  *           
      *                7. BP13KA10                                  *           
      *                8. BP13K205                                  *           
      *                9. BP27K800                                  *           
      *                                                             *           
      *  OUTPUT FILE:  1. BP13LC01                                  *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG REF  BY   ON       DESCRIPTION                          *           
      * -------- ---- -------- -----------                          *           
      * BP132873 BA2  20061109 NEW PROGRAM                          *           
      * BP132873 SSS  20061123 READ BOOKING APPOINTMENT DATE        *           
      * BP133091 RB12 20071017 EXPAND BP13KD00 FR 1200 TO 1600 BYTES*           
      * BP133196 RB12 20080226 REPLACE KD35 WITH KD80               *           
      *                        ADD READ OF F205 TO GET PROJ CDE     *           
      * BP133196 RB12 20080403 READ BP27K800 TO GET DTE APPROVAL    *           
      * BP134442 IMC1 20120209 IGNORE RECORDS WITH NO HLA NUM       *           
      * BP138047 KAC1 20191125 RECOMPILE FOR BP27 COPYBOOK UPDATE   *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13KA10 ASSIGN       TO BP13KA10                             
                           ACCESS       IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS KA10-KEY-FLD                         
                           FILE STATUS  IS WS-BP13KA10-STATUS.                  
                                                                                
           SELECT BP13KD00 ASSIGN       TO BP13KD00                             
                           ACCESS       IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS KD00-KEY-FLD                         
                           FILE STATUS  IS WS-BP13KD00-STATUS.                  
                                                                                
           SELECT BP13KB20 ASSIGN       TO BP13KB20                             
                           ACCESS       IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS KB20-KEY-FLD                         
                           FILE STATUS  IS WS-BP13KB20-STATUS.                  
                                                                                
           SELECT BP13KD80 ASSIGN       TO BP13KD80                             
                           ACCESS       IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS KD80-KEY-FLD                         
                           FILE STATUS  IS WS-BP13KD80-STATUS.                  
                                                                                
           SELECT BP13K820 ASSIGN       TO BP13K820                             
                           ACCESS       IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K820-KEY-FLD                         
                           FILE STATUS  IS WS-BP13K820-STATUS.                  
                                                                                
           SELECT BP27K025 ASSIGN       TO BP27K025                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K025-KEY-FLD                         
                           ALTERNATE RECORD KEY IS K025-NUM-HLA                 
                                           WITH DUPLICATES                      
                           FILE STATUS  IS WS-BP27K025-STATUS.                  
                                                                                
           SELECT BP27K800 ASSIGN       TO BP27K800                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K800-NUM-HLA                         
                           FILE STATUS  IS WS-BP27K800-STATUS.                  
                                                                                
           SELECT BP13F205 ASSIGN       TO BP13F205.                            
                                                                                
           SELECT BP13F800 ASSIGN       TO BP13F800.                            
                                                                                
           SELECT BP13LC01 ASSIGN       TO BP13LC01.                            
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13KA10                                                             
           RECORD CONTAINS 800  CHARACTERS.                                     
                                                                                
       COPY BP13KA10.                                                           
                                                                                
       FD  BP13KD00                                                             
           RECORD CONTAINS 1600 CHARACTERS.                                     
                                                                                
       COPY BP13KD00.                                                           
                                                                                
       FD  BP13KB20                                                             
           RECORD CONTAINS 3300 CHARACTERS.                                     
       COPY BP13KB20.                                                           
                                                                                
       FD  BP13KD80                                                             
           RECORD CONTAINS 3500 CHARACTERS.                                     
       COPY BP13KD80.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BP27K025                                                             
           BLOCK  CONTAINS 0   RECORDS                                          
           RECORD CONTAINS 100 CHARACTERS                                       
           LABEL  RECORDS  ARE STANDARD.                                        
       COPY BP27K025.                                                           
                                                                                
       FD  BP27K800                                                             
           RECORD CONTAINS 1200 CHARACTERS.                                     
       COPY BP27K800.                                                           
                                                                                
       FD  BP13F205                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 80 CHARACTERS                                        
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F205.                                                           
                                                                                
       FD  BP13F800                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F800.                                                           
                                                                                
       FD  BP13LC01                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13LC01.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05 WS-BP13KA10-STATUS        PIC 9(2).                               
           05 WS-BP13KD00-STATUS        PIC 9(2).                               
           05 WS-BP13KB20-STATUS        PIC 9(2).                               
           05 WS-BP13KD80-STATUS        PIC 9(2).                               
           05 WS-BP13K820-STATUS        PIC 9(2).                               
           05 WS-BP27K025-STATUS        PIC 9(2).                               
           05 WS-BP27K800-STATUS        PIC 9(2).                               
                                                                                
       01  WS-SWITCH.                                                           
           05  WS-F800-END           PIC X(1)       VALUE 'N'.                  
               88 CS-F800-END                       VALUE 'Y'.                  
           05  WS-END-K025           PIC X(1)       VALUE 'N'.                  
               88 CS-END-K025                       VALUE 'Y'.                  
                                                                                
       01  WS-COUNT.                                                            
           05  WS-F800-READ          PIC 9(5) COMP  VALUE ZEROS.                
           05  WS-K800-READ          PIC 9(5) COMP  VALUE ZEROS.                
           05  WS-LC01-WRITE         PIC 9(5) COMP  VALUE ZEROS.                
                                                                                
       01  WS-DATE.                                                             
           05 WS-DATE-CHAR           PIC X(8).                                  
           05 WS-DATE-NUM REDEFINES WS-DATE-CHAR                                
                                     PIC 9(8).                                  
                                                                                
       01  WS-IDX                    PIC 9(1)  VALUE ZEROES.                    
       01  WS-NO-DAY                 PIC 9(3)  VALUE ZEROES.                    
       01  WS-COMP-DATE              PIC S9(9) VALUE ZEROES.                    
                                                                                
       01  WS-DTE-CONV.                                                         
           05 WS-DTE-CONV-CHAR       PIC X(8).                                  
           05 WS-DTE-CONV-NUM  REDEFINES WS-DTE-CONV-CHAR                       
                                     PIC 9(8).                                  
                                                                                
       01 WS-DTE-COMPUTED            PIC 9(8)   VALUE ZEROS.                    
                                                                                
      *-------------------------------------------------------*                 
      *    COPY BOOK FOR SUBROUTINE - BP13C913.               *                 
      *-------------------------------------------------------*                 
       COPY P13COMM8.                                                           
                                                                                
      *-------------------------------------------------------*                 
      *    LINKAGE VARIABLES                                  *                 
      *-------------------------------------------------------*                 
       01   WS-LINK-REC.                                                        
            05 WS-LINK-NUM-SCH            PIC X(4).                             
            05 WS-LINK-NUM-ACC            PIC X(4).                             
            05 WS-LINK-NUM-CHK            PIC X(1).                             
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 1000-OPENING-ROUTINE THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13F800   THRU 2000-EXIT.                         
           PERFORM 3000-MAIN-PROCESS    THRU 3000-EXIT                          
                   UNTIL CS-F800-END.                                           
                                                                                
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPENING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           OPEN INPUT BP13F800                                                  
                      BP13F205                                                  
                      BP13KB20                                                  
                      BP13KD00                                                  
                      BP13KD80                                                  
                      BP13K820                                                  
                      BP27K025                                                  
                      BP27K800                                                  
                      BP13KA10                                                  
               OUTPUT BP13LC01.                                                 
                                                                                
           IF WS-BP13KA10-STATUS NOT = 0 AND 97                                 
              DISPLAY 'BP13KA10 OPEN ERROR, STATUS: '                           
                      WS-BP13KA10-STATUS                                        
              PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-BP13KD00-STATUS NOT = 0 AND 97                                 
              DISPLAY 'BP13KD00 OPEN ERROR, STATUS: '                           
                      WS-BP13KD00-STATUS                                        
              PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-BP13KB20-STATUS NOT = 0 AND 97                                 
              DISPLAY 'BP13KB20 OPEN ERROR, STATUS: '                           
                      WS-BP13KB20-STATUS                                        
              PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-BP13KD80-STATUS NOT = 0 AND 97                                 
              DISPLAY 'BP13KD80 OPEN ERROR, STATUS: '                           
                      WS-BP13KD80-STATUS                                        
              PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-BP13K820-STATUS NOT = 0 AND 97                                 
              DISPLAY 'BP13K820 OPEN ERROR, STATUS: '                           
                      WS-BP13K820-STATUS                                        
              PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-BP27K025-STATUS NOT = 0 AND 97                                 
              DISPLAY 'BP27K025 OPEN ERROR, STATUS: '                           
                      WS-BP27K025-STATUS                                        
              PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-BP27K800-STATUS NOT = 0 AND 97                                 
              DISPLAY 'BP27K800 OPEN ERROR, STATUS: '                           
                      WS-BP27K800-STATUS                                        
              PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
                                                                                
           READ BP13F205.                                                       
                                                                                
           DISPLAY 'F205-NUM-NT-ZONE: ' F205-NUM-NT-ZONE.                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F800.                                                      
      *-------------------------------------------------------------            
           READ BP13F800 AT END MOVE 'Y' TO WS-F800-END                         
                                GO TO 2000-EXIT.                                
           ADD 1 TO WS-F800-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-MAIN-PROCESS.                                                       
      *-------------------------------------------------------------            
           IF F800-NUM-DBSS-PRJCT NOT = F205-NUM-NT-ZONE                        
              PERFORM 2000-READ-BP13F800 THRU 2000-EXIT                         
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE ZEROES  TO  WS-IDX.                                             
                                                                                
           MOVE SPACES  TO  BP13LC01-REC BP13COMM8-REC BP13KB20-REC             
                            BP13KD00-REC BP13KD80-REC  BP13K820-REC             
                            BP27K025-REC.                                       
                                                                                
           INITIALIZE   BP13LC01-REC BP13COMM8-REC.                             
                                                                                
           MOVE ZEROES TO LC01-DTE-ACCEPT                                       
                          LC01-DTE-LOAN-APLCTN-SENT                             
                          LC01-DTE-LOAN-APLCTN-APRV                             
                          LC01-DTE-HLE-APPROVAL                                 
                          LC01-DTE-OFFER-LTR-PRT                                
                          LC01-DTE-OFFER-LTR-SENT                               
                          LC01-DTE-OFFER-LTR-DUE3                               
                          LC01-DTE-OFFER-LTR-DUE4                               
                          LC01-DTE-OFFER-LTR-ACCEPT.                            
                                                                                
           IF F800-NUM-HLA NOT EQUAL SPACES AND LOW-VALUES                      
                                                                                
              MOVE F800-NUM-REGN TO KB20-NUM-REGN                               
                                    KD00-NUM-REGN                               
                                    KD80-NUM-REGN                               
                                                                                
              MOVE F800-NUM-HLA  TO K025-NUM-HLA                                
                                                                                
                                                                                
              PERFORM 3100-MOVE-F800-VALUES    THRU 3100-EXIT                   
              PERFORM 3200-READ-BP13KD80       THRU 3200-EXIT                   
              PERFORM 3300-READ-BP13KD00       THRU 3300-EXIT                   
              PERFORM 3400-READ-BP13KB20       THRU 3400-EXIT                   
              PERFORM 3500-START-BP27K025      THRU 3500-EXIT                   
                                                                                
              MOVE SPACES                TO BP13K820-REC                        
              MOVE F800-NUM-REGN         TO K820-NUM-REGN                       
              MOVE F800-NUM-NRIC1        TO K820-NUM-NRIC                       
              PERFORM 3600-READ-BP13K820    THRU 3600-EXIT                      
                                                                                
              IF F800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES AND ZEROS           
                 MOVE SPACES                TO BP13K820-REC                     
                 MOVE F800-NUM-REGN         TO K820-NUM-REGN                    
                 MOVE F800-NUM-NRIC2        TO K820-NUM-NRIC                    
                 PERFORM 3600-READ-BP13K820    THRU 3600-EXIT                   
              END-IF                                                            
                                                                                
              IF F800-NUM-NRIC3 NOT = SPACES AND LOW-VALUES AND ZEROS           
                 MOVE SPACES                TO BP13K820-REC                     
                 MOVE F800-NUM-REGN         TO K820-NUM-REGN                    
                 MOVE F800-NUM-NRIC3        TO K820-NUM-NRIC                    
                 PERFORM 3600-READ-BP13K820    THRU 3600-EXIT                   
              END-IF                                                            
                                                                                
              IF F800-NUM-NRIC4 NOT = SPACES AND LOW-VALUES AND ZEROS           
                 MOVE SPACES                TO BP13K820-REC                     
                 MOVE F800-NUM-REGN         TO K820-NUM-REGN                    
                 MOVE F800-NUM-NRIC4        TO K820-NUM-NRIC                    
                 PERFORM 3600-READ-BP13K820    THRU 3600-EXIT                   
              END-IF                                                            
                                                                                
      *  TO CALL SUBROUTINE BP13C913,GETTING ADDRESS                            
              MOVE F800-NUM-SCH-ACC(1:4)    TO WS-LINK-NUM-SCH                  
              MOVE F800-NUM-SCH-ACC(5:4)    TO WS-LINK-NUM-ACC                  
              MOVE F800-NUM-SCH-ACC(9:1)    TO WS-LINK-NUM-CHK                  
              PERFORM 5000-LINK-BP13C913       THRU 5000-EXIT                   
                                                                                
              PERFORM 7000-READ-BP27K800       THRU 7000-EXIT                   
                                                                                
              PERFORM 6000-WRITE-BP13LC01      THRU 6000-EXIT                   
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F800       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-MOVE-F800-VALUES.                                                   
      *-------------------------------------------------------------            
           MOVE F800-NUM-FLAT-TYPE      TO LC01-NUM-FT.                 00181303
           MOVE F800-NUM-REGN           TO LC01-NUM-REGN.               00181603
           MOVE F800-NUM-SCH-ACC        TO LC01-NUM-HDB-REF.            00181603
           MOVE F800-NUM-HOUSEHOLD      TO LC01-NUM-HHTYPE.                     
           MOVE F800-NUM-ELIG-TAG       TO LC01-NUM-PRE-ELIG-TAG.               
           MOVE F800-NUM-LOAN-TAG       TO LC01-NUM-LOAN-TAG.                   
           MOVE F800-NUM-HLA            TO LC01-NUM-HLE.                00181603
           MOVE F800-DTE-BK-APPT        TO LC01-DTE-BK-APPT.                    
           MOVE F800-NUM-DBSS-PRJCT     TO LC01-NUM-PRJCT               00181303
                                           KA10-NUM-PRJCT.              00181303
           PERFORM 3150-READ-BP13KA10 THRU 3150-EXIT.                   00181303
           MOVE WS-DATE-CHAR            TO LC01-DTE-PRINTED.                    
           MOVE 'BP13LC01'              TO LC01-NUM-PROGRAM.                    
           MOVE '20061120'              TO LC01-DTE-LOAN-APLCTN-SENT.           
                                                                                
      *    05  LC01-DTE-HLE-APPROVAL     PIC X(08).                     00181603
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       3150-READ-BP13KA10.                                                      
      *--------------------------------------------------------------*          
                                                                                
           READ BP13KA10 KEY IS KA10-KEY-FLD.                                   
                                                                                
           IF WS-BP13KA10-STATUS = 00                                           
              MOVE KA10-NME-PRJCT TO LC01-NME-PRJCT                             
           ELSE                                                                 
              IF WS-BP13KA10-STATUS = 23                                        
                 DISPLAY 'RECORD NOT FOUND IN BP13KA10, KEY: '                  
                         KA10-KEY-FLD                                           
                                                                                
              ELSE                                                              
                 DISPLAY 'ERROR WITH READING BP13KA10, STATUS: '                
                          WS-BP13KA10-STATUS                                    
                 PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3150-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       3200-READ-BP13KD80.                                                      
      *--------------------------------------------------------------*          
                                                                                
           READ BP13KD80 KEY IS KD80-KEY-FLD                                    
                                                                                
           IF WS-BP13KD80-STATUS = 00                                           
              PERFORM 3250-MOVE-KD80-VALUES  THRU 3250-EXIT                     
           ELSE                                                                 
              IF WS-BP13KD80-STATUS = 23                                        
                 DISPLAY 'RECORD NOT FOUND IN BP13KD80, KEY: '                  
                         KD80-KEY-FLD                                           
                                                                                
              ELSE                                                              
                 DISPLAY 'ERROR WITH READING BP13KD80, STATUS: '                
                          WS-BP13KD80-STATUS                                    
                 PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3250-MOVE-KD80-VALUES.                                                   
      *-------------------------------------------------------------            
           MOVE KD80-NUM-OFFER-OIC TO                                           
               LC01-NUM-OIC.                                            00181603
                                                                                
       3250-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       3300-READ-BP13KD00.                                                      
      *--------------------------------------------------------------*          
                                                                                
           READ BP13KD00 KEY IS KD00-KEY-FLD.                                   
                                                                                
           IF WS-BP13KD00-STATUS = 00                                           
              PERFORM 3350-MOVE-KD00-VALUES  THRU 3350-EXIT                     
           ELSE                                                                 
              IF WS-BP13KD00-STATUS = 23                                        
                 DISPLAY 'RECORD NOT FOUND IN BP13KD00, KEY: '                  
                         KD00-KEY-FLD                                           
              ELSE                                                              
                 DISPLAY 'ERROR WITH READING BP13KD00, STATUS: '                
                          WS-BP13KD00-STATUS                                    
                 PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3350-MOVE-KD00-VALUES.                                                   
      *-------------------------------------------------------------            
           IF KD00-DTE-HSGNT-SUBMIT NOT = SPACES AND ZEROES AND                 
                                          LOW-VALUES                            
              MOVE 'A' TO LC01-NUM-CPF-TAG                                      
           END-IF.                                                              
                                                                                
           MOVE KD00-DTE-OPTION         TO LC01-DTE-ACCEPT.             00181603
           MOVE KD00-DTE-OFFER-PRINTED  TO                                      
                            LC01-DTE-OFFER-LTR-PRT                      00181603
                            LC01-DTE-OFFER-LTR-SENT.                    00181603
                                                                                
           MOVE KD00-DTE-OFFER-ACCEPT   TO                                      
                            LC01-DTE-OFFER-LTR-ACCEPT.                  00181603
                                                                                
      * FOR OPTION 2                                                            
                                                                                
           IF LC01-DTE-ACCEPT NOT = ZEROES AND LOW-VALUES AND SPACES            
              MOVE LC01-DTE-ACCEPT TO WS-DTE-CONV                               
                                                                                
              COMPUTE WS-NO-DAY =                                               
                         (FUNCTION INTEGER-OF-DATE(WS-DATE-NUM)) -              
                         (FUNCTION INTEGER-OF-DATE(WS-DTE-CONV-NUM))            
                                                                                
              MOVE WS-NO-DAY TO LC01-NUM-DAYS-OVERDUE2                          
           ELSE                                                                 
              MOVE ZEROES TO LC01-NUM-DAYS-OVERDUE2                             
           END-IF.                                                              
                                                                                
      * FOR OPTION 3                                                            
                                                                                
           IF LC01-DTE-OFFER-LTR-SENT NOT =                                     
              ZEROES AND LOW-VALUES AND SPACES                                  
              MOVE LC01-DTE-OFFER-LTR-SENT TO WS-DTE-CONV                       
              COMPUTE WS-DTE-COMPUTED =                                         
                 FUNCTION DATE-OF-INTEGER(                                      
                 FUNCTION INTEGER-OF-DATE(WS-DTE-CONV-NUM) + 10)                
                                                                                
              MOVE WS-DTE-COMPUTED TO LC01-DTE-OFFER-LTR-DUE3                   
                                                                                
              COMPUTE WS-NO-DAY =                                               
                         (FUNCTION INTEGER-OF-DATE(WS-DATE-NUM)) -              
                         (FUNCTION INTEGER-OF-DATE(WS-DTE-CONV-NUM))            
                                                                                
              MOVE WS-NO-DAY TO LC01-NUM-DAYS-OVERDUE3                          
           ELSE                                                                 
              MOVE ZEROES TO LC01-NUM-DAYS-OVERDUE3                             
           END-IF.                                                              
                                                                                
      * FOR OPTION 4                                                            
                                                                                
           IF LC01-DTE-OFFER-LTR-SENT NOT =                                     
              ZEROES AND LOW-VALUES AND SPACES                                  
              MOVE LC01-DTE-OFFER-LTR-SENT TO WS-DTE-CONV                       
              COMPUTE WS-DTE-COMPUTED =                                         
                 FUNCTION DATE-OF-INTEGER(                                      
                 FUNCTION INTEGER-OF-DATE(WS-DTE-CONV-NUM) + 7)                 
                                                                                
              MOVE WS-DTE-COMPUTED TO LC01-DTE-OFFER-LTR-DUE4                   
              COMPUTE WS-NO-DAY =                                               
                         (FUNCTION INTEGER-OF-DATE(WS-DATE-NUM)) -              
                         (FUNCTION INTEGER-OF-DATE(WS-DTE-CONV-NUM))            
                                                                                
              MOVE WS-NO-DAY TO LC01-NUM-DAYS-OVERDUE4                          
              MOVE 'Y'       TO LC01-DTE-OFFER-LTR-TAG                          
           ELSE                                                                 
              MOVE ZEROES TO LC01-NUM-DAYS-OVERDUE4                             
              MOVE 'N'       TO LC01-DTE-OFFER-LTR-TAG                          
           END-IF.                                                              
                                                                                
       3350-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       3400-READ-BP13KB20.                                                      
      *--------------------------------------------------------------*          
                                                                                
           READ BP13KB20 KEY IS KB20-KEY-FLD                                    
                                                                                
           IF WS-BP13KB20-STATUS = 00                                           
              PERFORM 3450-MOVE-KB20-VALUES  THRU 3450-EXIT                     
           ELSE                                                                 
              IF WS-BP13KB20-STATUS = 23                                        
                 DISPLAY 'RECORD NOT FOUND IN BP13KB20, KEY: '                  
                         KB20-KEY-FLD                                           
              ELSE                                                              
                 DISPLAY 'ERROR WITH READING BP13KB20, STATUS: '                
                          WS-BP13KB20-STATUS                                    
                 PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3450-MOVE-KB20-VALUES.                                                   
      *-------------------------------------------------------------            
           MOVE KB20-CDE-SUCC-STATUS   TO  LC01-NUM-SUCC.                       
           MOVE KB20-NUM-APPL-LOAN-REQ TO                               00181603
                                LC01-NUM-APPLY-HDB-LOAN.                00181603
                                                                                
       3450-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       3500-START-BP27K025.                                                     
      *--------------------------------------------------------------*          
      *USING ALTERNATE KEY                                                      
           MOVE 'N'        TO WS-END-K025.                                      
           START BP27K025 KEY IS EQUAL TO K025-NUM-HLA.                         
                                                                                
           IF WS-BP27K025-STATUS = 00 OR 02                                     
              PERFORM 3550-READ-NEXT-BP27K025 THRU 3550-EXIT                    
                      UNTIL CS-END-K025                                         
           ELSE                                                                 
              IF WS-BP27K025-STATUS = 23 OR 10                                  
                 DISPLAY 'RECORD NOT FOUND IN BP27K025, KEY: '                  
                        K025-NUM-HLA                                            
              ELSE                                                              
                DISPLAY 'ERROR WITH START BP27K025, STATUS: '                   
                         WS-BP27K025-STATUS                                     
                PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       3550-READ-NEXT-BP27K025.                                                 
      *--------------------------------------------------------------*          
           READ BP27K025 NEXT                                                   
                   AT END MOVE 'Y' TO  WS-END-K025                              
           GO TO 3550-EXIT.                                                     
                                                                                
           IF K025-NUM-HLA NOT = F800-NUM-HLA                                   
              MOVE 'Y' TO  WS-END-K025                                          
              GO TO 3550-EXIT.                                                  
                                                                                
           IF K025-NUM-OIC-TYPE = 'PE'                                          
              MOVE K025-NUM-OIC  TO LC01-NUM-OIC-PE                             
           ELSE                                                                 
             IF K025-NUM-OIC-TYPE = 'CA'                                        
                MOVE K025-NUM-OIC          TO   LC01-NUM-OIC-CA                 
             END-IF                                                             
           END-IF.                                                              
                                                                                
       3550-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       3600-READ-BP13K820.                                                      
      *--------------------------------------------------------------*          
                                                                                
           READ BP13K820 KEY IS K820-KEY-FLD                                    
                                                                                
           IF WS-BP13K820-STATUS = 00                                           
              PERFORM 3650-MOVE-K820-VALUES  THRU 3650-EXIT                     
           ELSE                                                                 
              IF WS-BP13K820-STATUS = 23                                        
                 DISPLAY 'RECORD NOT FOUND IN BP13K820, KEY: '                  
                         K820-KEY-FLD                                           
              ELSE                                                              
                 DISPLAY 'ERROR WITH READING BP13K820, STATUS: '                
                          WS-BP13K820-STATUS                                    
                 PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3650-MOVE-K820-VALUES.                                                   
      *-------------------------------------------------------------            
           ADD +1             TO WS-IDX.                                        
           MOVE K820-NUM-NRIC TO LC01-NUM-NRIC(WS-IDX).                         
           MOVE K820-NME-OCCP TO LC01-NME-HA(WS-IDX).                           
                                                                                
       3650-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-FIND-DUE-DATE.                                                      
      *-------------------------------------------------------------            
                                                                                
           COMPUTE WS-COMP-DATE =                                               
                   FUNCTION INTEGER-OF-DATE(WS-DTE-CONV-NUM).                   
           ADD +10 TO WS-COMP-DATE.                                             
           COMPUTE WS-DTE-CONV-NUM =                                            
                   FUNCTION DATE-OF-INTEGER(WS-COMP-DATE).                      
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4100-FIND-DUE-DAYS.                                                      
      *-------------------------------------------------------------            
           COMPUTE WS-NO-DAY =                                                  
                      (FUNCTION INTEGER-OF-DATE(WS-DATE-NUM)) -                 
                      (FUNCTION INTEGER-OF-DATE(WS-DTE-CONV-NUM)).              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-LINK-BP13C913.                                                      
      *-------------------------------------------------------------            
           CALL 'BP13C913' USING                                                
                 WS-LINK-REC, BP13COMM8-REC                                     
           END-CALL.                                                            
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE'                       
                         COMM8-CDE-SYSERR                                       
              ELSE                                                              
                 DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR             
              END-IF                                                            
              MOVE SPACES        TO BP13COMM8-REC                               
           ELSE                                                                 
              PERFORM 5100-MOVE-C913-VALUES THRU 5100-EXIT                      
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       5100-MOVE-C913-VALUES.                                                   
      *-------------------------------------------------------------            
           MOVE SPACES                 TO LC01-NUM-BLK                          
                                          LC01-NUM-LEVEL                00181603
                                          LC01-NUM-MAIN-UNIT                    
                                          LC01-NUM-SUB-UNIT             00181603
                                          LC01-NME-STREET.                      
                                                                                
           MOVE COMM8-NUM-BLK          TO LC01-NUM-BLK.                         
           MOVE COMM8-NUM-LEVEL        TO LC01-NUM-LEVEL.               00181603
           MOVE COMM8-NUM-UNIT-MAIN    TO LC01-NUM-MAIN-UNIT.                   
           MOVE COMM8-NUM-UNIT-SUB     TO LC01-NUM-SUB-UNIT.            00181603
           MOVE COMM8-NME-STREET       TO LC01-NME-STREET.                      
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-WRITE-BP13LC01.                                                     
      *-------------------------------------------------------------            
           WRITE   BP13LC01-REC.                                                
           ADD +1                  TO WS-LC01-WRITE.                            
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       7000-READ-BP27K800.                                                      
      *--------------------------------------------------------------*          
                                                                                
           MOVE F800-NUM-HLA TO K800-NUM-HLA OF BP27K800-MASTER.                
                                                                                
           READ BP27K800.                                                       
                                                                                
           IF WS-BP27K800-STATUS = 00                                           
              ADD 1 TO WS-K800-READ                                             
              MOVE K800-DTE-HLA-APPROVE TO LC01-DTE-HLE-APPROVAL                
              MOVE K800-NUM-CASE-STATUS TO LC01-NUM-HLE-STATUS                  
           ELSE                                                                 
              IF WS-BP27K800-STATUS = 23                                        
                 DISPLAY 'RECORD NOT FOUND IN BP27K800, KEY: '                  
                         K800-NUM-HLA OF BP27K800-MASTER                        
              ELSE                                                              
                 DISPLAY 'ERROR WITH READING BP27K800, STATUS: '                
                          WS-BP27K800-STATUS                                    
                 PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY '------- BP13CL00 -------------'.                            
           DISPLAY ' NO OF F800 READ    : ' WS-F800-READ.                       
           DISPLAY ' NO OF K800 READ    : ' WS-K800-READ.                       
           DISPLAY ' NO OF LC01 WRITTEN : ' WS-LC01-WRITE.                      
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13F800                                                       
                 BP13F205                                                       
                 BP13KB20                                                       
                 BP13KA10                                                       
                 BP13KD00                                                       
                 BP13KD80                                                       
                 BP13K820                                                       
                 BP27K025                                                       
                 BP27K800                                                       
                 BP13LC01.                                                      
                                                                                
           IF WS-BP13KA10-STATUS NOT = 0                                        
              DISPLAY 'BP13KA10 CLOSE ERROR, STATUS: '                          
                      WS-BP13KA10-STATUS                                        
           END-IF.                                                              
                                                                                
           IF WS-BP13KD00-STATUS NOT = 0                                        
              DISPLAY 'BP13KD00 CLOSE ERROR, STATUS: '                          
                      WS-BP13KD00-STATUS                                        
           END-IF.                                                              
                                                                                
           IF WS-BP13KB20-STATUS NOT = 0                                        
              DISPLAY 'BP13KB20 CLOSE ERROR, STATUS: '                          
                      WS-BP13KB20-STATUS                                        
           END-IF.                                                              
                                                                                
           IF WS-BP13KD80-STATUS NOT = 0                                        
              DISPLAY 'BP13KD80 CLOSE ERROR, STATUS: '                          
                      WS-BP13KD80-STATUS                                        
           END-IF.                                                              
                                                                                
           IF WS-BP13K820-STATUS NOT = 0                                        
              DISPLAY 'BP13K820 CLOSE ERROR, STATUS: '                          
                      WS-BP13K820-STATUS                                        
           END-IF.                                                              
                                                                                
           IF WS-BP27K025-STATUS NOT = 0                                        
              DISPLAY 'BP13LC01 CLOSE ERROR ON BP27K025 : '                     
                      WS-BP27K025-STATUS                                        
           END-IF.                                                              
                                                                                
           IF WS-BP27K800-STATUS NOT = 0                                        
              DISPLAY 'BP13LC01 CLOSE ERROR ON BP27K800 : '                     
                      WS-BP27K800-STATUS                                        
           END-IF.                                                              
                                                                                
       9000-EXIT.                                                               
           STOP RUN.                                                            
