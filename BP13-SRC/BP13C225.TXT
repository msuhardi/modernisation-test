       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C225.                                                 
      *AUTHOR.        RAYATHATHA ASHOK.                                         
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :                                              *            
      *                                                            *            
      *    1. PRINT CASES THAT HAVE ACCEPTED/REJECTED FLAT FOR     *            
      *       SOC OR WALK-IN FLAT.                                 *            
      *                                                            *            
      *    2. THE INPUT FILE (BP13F310) WAS CREATED THRU ONLINE    *            
      *       ACCEPTANCE/REJECTION PANEL. THE PREVIOUS STEP WILL   *            
      *       SORT THE BP13K310 TO SEQUENTIAL FILE BY REGN-NO.     *            
      *                                                            *            
      *  INPUT FILE :                                              *            
      *      1. BP13F310 (TRANS FILE)                              *            
      *      2. BM06K110                                           *            
      *      3. BP13K820                                           *            
      *      4. BP13K800                                           *            
      *                                                            *            
      *  OUTPUT FILE :                                             *            
      *      1. BP13F220 (RESETTLEMENT FILE)                       *            
      *                                                            *            
      *  LISTING:                                                  *            
      *      1. P13L225A - LIST OF SOC ACCEPTED CASES              *            
      *      2. P13L225B - LIST OF SOC REJECTED CASES              *            
      *      3. P13L225C - LIST OF WIS ACCEPTED CASES              *            
      *      4. P13L225D - LIST OF WIS REJECTED CASES              *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF  BY  DATE      DESCRIPTION                         *            
      * -------- --- --------  -----------                         *            
      * SOC-PH9  GD  15/11/94  CATER FOR ZONE;REFORMATTED REPORT   *            
      * C941228  GD  22/02/95  ADD SUBTOTALS, CAT TO REPORT        *            
      * C950184  LMS 24/11/95  ADD FLAT-STATUS TO REPORT           *            
      * BP130066 RD  09/12/95  ADD APPT-DATE, APPT-TIME AND        *            
      *                        ACC/REJ-TIME                        *            
      * BP130066 LMS 03/01/96  READ ACC/REJ-TIME FROM F310         *            
      * BP130100 LMS 12/06/96  PRINT NIL REPROT                    *            
      * BP130488 SIA 06/05/98  PRINT OCS-RQST.                     *            
      * BP130418 SIA 22/09/98  Y2000 CHANGES.                      *            
      * BP130778 GBR 06/10/99  REMOVE CALLING BP13C904.            *            
      * BP130805 SSS 22/03/00  INCLUDE OCS AMOUNT.                 *            
      * BP130805 SSS 19/04/00  CATER FOR NEW PAGE WHEN NEW RPT ID. *            
      * BP132340 MTD 17/04/03  REMOVE TRANS-CODES 'AA' 'AW'        *            
      *                        'RA' AND 'RW'.                      *            
      * BP132582 LMS 04/11/04  CATER FOR R1, R2 AND R3.            *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F310 ASSIGN TO BP13F310.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE IS RANDOM                                         
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K800-NUM-REGN                                   
                  FILE STATUS IS BP13K800-STATUS.                               
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ACCESS MODE IS RANDOM                                         
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K820-KEY-FLD                                    
                  FILE STATUS IS BP13K820-STATUS.                               
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ACCESS MODE IS RANDOM                                         
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K110-KEY-FLD                                    
                  FILE STATUS IS BM06K110-STATUS.                               
                                                                                
           SELECT BP13F220 ASSIGN TO BP13F220.                                  
                                                                                
           SELECT P13L225A ASSIGN TO P13L225A.                                  
           SELECT P13L225B ASSIGN TO P13L225B.                                  
           SELECT P13L225C ASSIGN TO P13L225C.                                  
           SELECT P13L225D ASSIGN TO P13L225D.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BP13F220                                                             
           RECORDING MODE F                                                     
           BLOCK  CONTAINS 0 CHARACTERS                                         
           RECORD CONTAINS 350 CHARACTERS                                       
           LABEL RECORDS IS STANDARD.                                           
       COPY BP13F220.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BP13F310                                                             
           RECORDING MODE F                                                     
           BLOCK  CONTAINS 0 CHARACTERS                                         
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS IS STANDARD.                                           
       COPY BP13F310.                                                           
                                                                                
       FD  P13L225A                                                             
           RECORDING MODE F                                                     
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  P13L225A-REPORT                PIC X(132).                           
                                                                                
       FD  P13L225B                                                             
           RECORDING MODE F                                                     
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  P13L225B-REPORT                PIC X(132).                           
                                                                                
       FD  P13L225C                                                             
           RECORDING MODE F                                                     
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  P13L225C-REPORT                PIC X(132).                           
                                                                                
       FD  P13L225D                                                             
           RECORDING MODE F                                                     
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  P13L225D-REPORT                PIC X(132).                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-EOF-F310             PIC X     VALUE 'N'.                         
       01  WS-START-FILE           PIC X     VALUE 'Y'.                         
                                                                                
       01  REC-K800-FOUND-FLAG     PIC X     VALUE 'N'.                         
           88  REC-K800-FOUND                VALUE 'Y'.                         
                                                                                
       01  INVALID-REC-FLAG.                                                    
           05  K820-FOUND          PIC X     VALUE 'N'.                         
           05  K110-FOUND          PIC X     VALUE 'N'.                         
           05  ACCEPTED-FLAT-REC   PIC X     VALUE 'N'.                         
           05  WRONG-STATUS-REC    PIC X     VALUE 'N'.                         
           05  NT-RM-FLAG          PIC X     VALUE 'N'.                         
                                                                                
       01  WS-K820-KEY.                                                         
           05  WS-K820-REGN-NO     PIC X(8)   VALUE SPACES.                     
           05  WS-K820-NUM-NRIC    PIC X(9)   VALUE SPACES.                     
                                                                                
       01  WS-DATE                 PIC X(8)   VALUE SPACES.                     
                                                                                
       01  BM06K110-STATUS         PIC 99       VALUE ZEROS.                    
       01  BP13K800-STATUS         PIC 99       VALUE ZEROS.                    
       01  BP13K820-STATUS         PIC 99       VALUE ZEROS.                    
       01  WS-ALLOC-SCHEME         PIC X(3)     VALUE SPACES.                   
       01  WS-ACCEPT-WIS           PIC 9(5)     VALUE ZEROS.                    
       01  WS-ACCEPT               PIC 9(5)     VALUE ZEROS.                    
       01  WS-REJECT-WIS           PIC 9(5)     VALUE ZEROS.                    
       01  WS-REJECT               PIC 9(5)     VALUE ZEROS.                    
       01  WS-CNT                  PIC 9.                                       
       01  WS-NAME                 PIC X(66)    VALUE SPACES.                   
                                                                                
       01  WS-SUBTOTALS.                                                        
           05  WS-ACCEPT-WISA      PIC 9(5)     VALUE ZEROS.                    
           05  WS-ACCEPTA          PIC 9(5)     VALUE ZEROS.                    
           05  WS-REJECT-WISA      PIC 9(5)     VALUE ZEROS.                    
           05  WS-REJECTA          PIC 9(5)     VALUE ZEROS.                    
                                                                                
       01  WS-LISTING-CONTROLS.                                                 
           05  CNT-LINE-A       PIC 9(2)        VALUE 99.                       
           05  CNT-PAGE-A       PIC 9(5)        VALUE ZEROS.                    
           05  CNT-LINE-B       PIC 9(2)        VALUE 99.                       
           05  CNT-PAGE-B       PIC 9(5)        VALUE ZEROS.                    
           05  CNT-LINE-C       PIC 9(2)        VALUE 99.                       
           05  CNT-PAGE-C       PIC 9(5)        VALUE ZEROS.                    
           05  CNT-LINE-D       PIC 9(2)        VALUE 99.                       
           05  CNT-PAGE-D       PIC 9(5)        VALUE ZEROS.                    
           05  CNT-LINE-A1      PIC 9(2)        VALUE 99.                       
           05  CNT-LINE-B1      PIC 9(2)        VALUE 99.                       
           05  CNT-LINE-C1      PIC 9(2)        VALUE 99.                       
           05  CNT-LINE-D1      PIC 9(2)        VALUE 99.                       
                                                                                
       01  WS-NT-ZONE-RM.                                                       
           05  WS-NT            PIC X(3)        VALUE SPACES.                   
           05  WS-RM            PIC X(2)        VALUE SPACES.                   
                                                                                
       01  PRE-NT-ZONE-RM       PIC X(5)        VALUE SPACES.                   
       01  PRE-NT-ZONE-RM-1     PIC X(5)        VALUE SPACES.                   
       01  PRE-NT-ZONE-RM-2     PIC X(5)        VALUE SPACES.                   
       01  PRE-NT-ZONE-RM-3     PIC X(5)        VALUE SPACES.                   
       01  PRE-NT-ZONE-RM-4     PIC X(5)        VALUE SPACES.                   
                                                                                
      **************************************************************            
      *    BP13L225 REPORT COMMON HEADING                          *            
      **************************************************************            
       01  L225-HEAD-01.                                                        
           05  L225-RPT-ID         PIC X(8)      VALUE SPACES.                  
           05  FILLER              PIC X(4)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(35)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                   'SYSTEM OF COMMITMENT'.                      
           05  FILLER              PIC X(20)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  L225-DATE           PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(9)      VALUE '  PAGE : '.             
           05  L225-PAGE           PIC ZZZZ9     VALUE ZEROS.                   
           05  FILLER              PIC X(6)      VALUE SPACES.                  
                                                                                
       01  L225-HEAD-02.                                                        
           05  FILLER              PIC X(50)     VALUE SPACES.                  
           05  FILLER              PIC X(32)     VALUE                          
               'BOOKING SYSTEM (ALLOCATION TEAM)'.                              
           05  FILLER              PIC X(68)     VALUE SPACES.                  
                                                                                
       01  L225-HEAD-04.                                                        
           05  L225-NT-ZONE-TXT    PIC X(14)     VALUE SPACES.                  
           05  L225-CDE-NT-ZONE    PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE SPACES.                  
           05  L225-NT-TXT         PIC X(14)     VALUE SPACES.                  
           05  L225-CDE-NT         PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(12)     VALUE 'FLAT-TYPE : '.          
           05  L225-RECOM-RM-TYPE  PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(91)     VALUE SPACES.                  
                                                                                
       01  L225-HEAD-06.                                                        
           05  L225-HDR-NT-ZONE    PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(5)      VALUE ' CAT '.                 
           05  FILLER              PIC X(6)      VALUE 'ALLOC '.                
           05  FILLER              PIC X(11)     VALUE                          
               'SCH-ACCT-NO'.                                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(5)      VALUE 'BLK'.                   
           05  FILLER              PIC X(11)     VALUE                          
               'STREET NAME'.                                                   
           05  FILLER              PIC X(9)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'FLAT   '.               
           05  FILLER              PIC X(7)      VALUE 'UNIT-NO'.               
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'APPT   '.               
           05  FILLER              PIC X(8)      VALUE 'APPT    '.              
           05  FILLER              PIC X(8)      VALUE 'ACC/REJ'.               
           05  FILLER              PIC X(28)     VALUE                          
               '      OCSD  OCSD   USER ALLO'.                                  
                                                                                
       01  L225-HEAD-06A.                                                       
           05  FILLER              PIC X(20)     VALUE SPACES.                  
           05  FILLER              PIC X(4)      VALUE 'DATE'.                  
           05  FILLER              PIC X(40)     VALUE SPACES.                  
           05  FILLER              PIC X(5)      VALUE 'STAT '.                 
           05  FILLER              PIC X(12)     VALUE SPACES.                  
           05  FILLER              PIC X(4)      VALUE 'DATE'.                  
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(6)      VALUE 'TIME  '.                
           05  FILLER              PIC X(7)      VALUE 'DATE &'.                
           05  FILLER              PIC X(30)     VALUE                          
               'TIME  ST  REQ   AMT   ID   SCH'.                                
                                                                                
       01  L225-HEAD-07.                                                        
           05  L225-NT-REGN-FORMAT.                                             
               10  L225-NT-REGN-NO      PIC X(8).                               
               10  FILLER               PIC X(5).                               
               10  FILLER               PIC X(3)   VALUE SPACES.                
           05  L225-ZONE-REGN-FORMAT REDEFINES L225-NT-REGN-FORMAT.             
               10  L225-QUEUE-NO        PIC X(5).                               
               10  L225-SLASH-1         PIC X.                                  
               10  L225-HH-TYPE         PIC X.                                  
               10  L225-SLASH-2         PIC X.                                  
               10  L225-ZONE-REGN-NO    PIC X(8).                               
           05  FILLER                   PIC X      VALUE SPACE.                 
           05  L225-CAT                 PIC X      VALUE SPACE.                 
           05  FILLER                   PIC X      VALUE SPACES.                
           05  L225-ALLOC-DATE          PIC X(6).                               
           05  FILLER                   PIC X      VALUE SPACES.                
           05  L225-SCHEME              PIC 9(4).                               
           05  L225-ACC                 PIC 9(4).                               
           05  L225-CHK                 PIC 9.                                  
           05  L225-ACC-TYPE            PIC 9(2).                               
           05  FILLER                   PIC X      VALUE SPACES.                
           05  L225-BLK-NO              PIC X(5)   VALUE SPACES.                
           05  FILLER                   PIC X      VALUE SPACES.                
           05  L225-STREET-NAME         PIC X(19)  VALUE SPACES.                
           05  FILLER                   PIC X      VALUE SPACES.                
           05  L225-FLAT-STATUS         PIC X(2)   VALUE SPACES.                
           05  FILLER                   PIC X      VALUE SPACES.                
           05  L225-BUY-BACK            PIC X(2)   VALUE SPACES.                
           05  FILLER                   PIC X(2)   VALUE ' #'.                  
           05  L225-LEVEL-NO            PIC X(2)   VALUE SPACES.                
           05  L225-DASH-4              PIC X      VALUE SPACES.                
           05  L225-MAIN-UNIT           PIC X(4)   VALUE SPACES.                
           05  L225-DASH-5              PIC X      VALUE SPACE.                 
           05  L225-SUB-UNIT            PIC X      VALUE SPACE.                 
           05  FILLER                   PIC X      VALUE SPACES.                
           05  L225-APPT-DATE           PIC X(6)   VALUE SPACES.                
           05  FILLER                   PIC X      VALUE SPACE.                 
           05  L225-APPT-TIME           PIC X(5)   VALUE SPACES.                
           05  FILLER                   PIC X      VALUE SPACE.                 
           05  L225-NEW-ACC-REJ-DATE    PIC X(6)   VALUE SPACES.                
           05  FILLER                   PIC X      VALUE SPACES.                
           05  L225-NEW-ACC-REJ-TIME    PIC X(5)   VALUE SPACES.                
           05  FILLER                   PIC X      VALUE SPACE.                 
           05  L225-NEW-ALLOC-STATUS    PIC X(2)   VALUE SPACES.                
           05  FILLER                   PIC X(3)   VALUE SPACES.                
           05  L225-NUM-OCS-RQST        PIC X      VALUE SPACE.                 
           05  FILLER                   PIC X      VALUE SPACES.                
           05  L225-AMT-OCS-DISCOUNT    PIC ZZZZZ9 VALUE ZEROS.                 
           05  FILLER                   PIC X      VALUE SPACES.                
           05  L225-USERID              PIC X(7)   VALUE SPACES.                
           05  FILLER                   PIC X      VALUE SPACES.                
           05  L225-ALLOCATION-SCHEME   PIC X(3)   VALUE SPACES.                
                                                                                
       01  L225-NIL-ACCEPTANCE         PIC X(132)  VALUE                        
               'THERE WERE NO ACCEPTED CASES'.                                  
                                                                                
       01  L225-NIL-REJECTION         PIC X(132)   VALUE                        
               'THERE WERE NO REJECTED CASES'.                                  
                                                                                
      **************************************************************            
      *    P13L225A REPORT                                         *            
      **************************************************************            
       01  L225A-HEAD-03.                                                       
           05  FILLER              PIC X(35)     VALUE SPACES.                  
           05  FILLER              PIC X(62)     VALUE                          
           'DETAIL LIST OF ACCEPTED CASES (BEFORE SIGNING SALES AGREEMEN        
      -    'T)'.                                                                
           05  FILLER              PIC X(35)     VALUE SPACES.                  
                                                                                
       01  L225A-HEAD-08A.                                                      
           05  FILLER              PIC X(30)     VALUE SPACES.                  
           05  FILLER              PIC X(35)     VALUE                          
               'TOTAL NUMBER OF ACCEPTED CASES FOR '.                           
           05  L225-NTA            PIC X(3).                                    
           05  FILLER              PIC X        VALUE SPACES.                   
           05  L225-FTA            PIC X(2).                                    
           05  FILLER              PIC X(3)     VALUE ' = '.                    
           05  L225-ACCEPTA        PIC ZZ,ZZ9.                                  
                                                                                
       01  L225A-HEAD-08.                                                       
           05  FILLER              PIC X(30)     VALUE SPACES.                  
           05  FILLER              PIC X(29)     VALUE                          
               'TOTAL NO OF ACCEPT CASES = '.                                   
            05  L225-ACCEPT        PIC ZZ,ZZ9.                                  
                                                                                
      **************************************************************            
      *    P13L225B REPORT                                         *            
      **************************************************************            
       01  L225B-HEAD-03.                                                       
           05  FILLER              PIC X(35)     VALUE SPACES.                  
           05  FILLER              PIC X(62)     VALUE                          
           'DETAIL LIST OF REJECTED CASES (BEFORE SIGNING SALES AGREEMEN        
      -    'T)'.                                                                
           05  FILLER              PIC X(35)     VALUE SPACES.                  
                                                                                
       01  L225B-HEAD-08A.                                                      
           05  FILLER              PIC X(30)     VALUE SPACES.                  
           05  FILLER              PIC X(35)     VALUE                          
               'TOTAL NUMBER OF REJECTED CASES FOR '.                           
           05  L225-NTB            PIC X(3).                                    
           05  FILLER              PIC X        VALUE SPACES.                   
           05  L225-FTB            PIC X(2).                                    
           05  FILLER              PIC X(3)     VALUE ' = '.                    
           05  L225-REJECTA       PIC ZZ,ZZ9.                                   
                                                                                
       01  L225B-HEAD-08.                                                       
           05  FILLER              PIC X(30)     VALUE SPACES.                  
           05  FILLER              PIC X(29)     VALUE                          
               'TOTAL NO OF REJECT CASES = '.                                   
            05  L225-REJECT        PIC ZZ,ZZ9.                                  
                                                                                
      **************************************************************            
      *    P13L225C REPORT                                         *            
      **************************************************************            
       01  L225C-HEAD-03.                                                       
           05  FILLER              PIC X(33)     VALUE SPACES.                  
           05  FILLER              PIC X(83)     VALUE                          
           'DETAIL LIST OF WALK IN SELECTION (BEFORE SIGNING SALES AGREE        
      -    'MENT) FOR ACCEPTED CASE'.                                           
           05  FILLER              PIC X(16)     VALUE SPACES.                  
           05  FILLER              PIC X(16)     VALUE SPACES.                  
                                                                                
       01  L225C-HEAD-08A.                                                      
           05  FILLER              PIC X(30)     VALUE SPACES.                  
           05  FILLER              PIC X(43)     VALUE                          
               'TOTAL NUMBER OF ACCEPTED CASES FOR WIS FOR '.                   
           05  L225-NTC            PIC X(3).                                    
           05  FILLER              PIC X        VALUE SPACES.                   
           05  L225-FTC            PIC X(2).                                    
           05  FILLER              PIC X(3)     VALUE ' = '.                    
           05  L225-ACCEPT-WISA    PIC ZZ,ZZ9.                                  
                                                                                
       01  L225C-HEAD-08.                                                       
           05  FILLER              PIC X(30)     VALUE SPACES.                  
           05  FILLER              PIC X(50)     VALUE                          
               'TOTAL NO OF ACCEPT CASES FOR WALK IN SELECTION = '.             
            05  L225-ACCEPT-WIS    PIC ZZ,ZZ9.                                  
                                                                                
      **************************************************************            
      *    P13L225D REPORT                                         *            
      **************************************************************            
       01  L225D-HEAD-03.                                                       
           05  FILLER              PIC X(33)     VALUE SPACES.                  
           05  FILLER              PIC X(83)     VALUE                          
           'DETAIL LIST OF WALK IN SELECTION (BEFORE SIGNING SALES AGREE        
      -    'MENT) FOR REJECTED CASE'.                                           
           05  FILLER              PIC X(16)     VALUE SPACES.                  
                                                                                
       01  L225D-HEAD-08A.                                                      
           05  FILLER              PIC X(30)     VALUE SPACES.                  
           05  FILLER              PIC X(43)     VALUE                          
               'TOTAL NUMBER OF REJECTED CASES FOR WIS FOR '.                   
           05  L225-NTD            PIC X(3).                                    
           05  FILLER              PIC X        VALUE SPACES.                   
           05  L225-FTD            PIC X(2).                                    
           05  FILLER              PIC X(3)     VALUE ' = '.                    
           05  L225-REJECT-WISA   PIC ZZ,ZZ9.                                   
                                                                                
       01  L225D-HEAD-08.                                                       
           05  FILLER              PIC X(30)     VALUE SPACES.                  
           05  FILLER              PIC X(50)     VALUE                          
               'TOTAL NO OF REJECT CASES FOR WALK IN SELECTION = '.             
            05  L225-REJECT-WIS    PIC ZZ,ZZ9.                                  
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 100-OPENING-ROUTINE       THRU 100-EXIT.                     
           PERFORM 150-READ-DATA             THRU 150-EXIT.                     
           PERFORM 200-READ-AND-PROCESS-F310 THRU 200-EXIT                      
                   UNTIL WS-EOF-F310 = 'Y'.                                     
           PERFORM 250-PRINT-NIL-REPORT      THRU 250-EXIT.                     
           PERFORM 1100-CLOSING-ROUTINE      THRU 1100-EXIT.                    
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       100-OPENING-ROUTINE.                                                     
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F310                                                 
                       BM06K110                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                OUTPUT BP13F220                                                 
                       P13L225A                                                 
                       P13L225B                                                 
                       P13L225C                                                 
                       P13L225D.                                                
                                                                                
           IF BM06K110-STATUS NOT = 0 AND 97                                    
              DISPLAY 'OPENING ERROR, K110-STATUS ' BM06K110-STATUS             
              MOVE BM06K110-STATUS TO RETURN-CODE                               
              PERFORM 1100-CLOSING-ROUTINE  THRU 1100-EXIT.                     
                                                                                
           IF BP13K800-STATUS NOT = 0 AND 97                                    
              DISPLAY 'OPENING ERROR, K800-STATUS ' BP13K800-STATUS             
              MOVE BP13K800-STATUS TO RETURN-CODE                               
              PERFORM 1100-CLOSING-ROUTINE  THRU 1100-EXIT.                     
                                                                                
           IF BP13K820-STATUS NOT = 0 AND 97                                    
              DISPLAY 'OPENING ERROR, K820-STATUS ' BP13K820-STATUS             
              MOVE BP13K820-STATUS TO RETURN-CODE                               
              PERFORM 1100-CLOSING-ROUTINE  THRU 1100-EXIT.                     
                                                                                
           MOVE SPACES TO PRE-NT-ZONE-RM                                        
                          PRE-NT-ZONE-RM-1                                      
                          PRE-NT-ZONE-RM-2                                      
                          PRE-NT-ZONE-RM-3                                      
                          PRE-NT-ZONE-RM-4.                                     
                                                                                
           MOVE ZEROS  TO WS-ACCEPT  WS-ACCEPT-WIS                              
                          WS-ACCEPTA WS-ACCEPT-WISA                             
                          WS-REJECT  WS-REJECT-WIS                              
                          WS-REJECTA WS-REJECT-WISA.                            
                                                                                
       100-EXIT.  EXIT.                                                         
                                                                                
      *-------------------------------------------------------------            
       150-READ-DATA.                                                           
      *-------------------------------------------------------------            
           READ BP13F310 AT END                                                 
                MOVE 'Y' TO WS-EOF-F310                                         
                GO TO 150-EXIT.                                                 
                                                                                
           MOVE F310-CDE-NT        TO WS-NT.                                    
           MOVE F310-CDE-FLAT-TYPE TO WS-RM.                                    
                                                                                
           IF F310-CDE-TRANS = 'AI' OR 'AS'                                     
              MOVE PRE-NT-ZONE-RM-1 TO PRE-NT-ZONE-RM                           
              MOVE PRE-NT-ZONE-RM-1 (1:3) TO L225-NTA                           
              MOVE PRE-NT-ZONE-RM-1 (4:2) TO L225-FTA                           
           END-IF.                                                              
                                                                                
           IF F310-CDE-TRANS = 'RI' OR 'RS' OR 'RK' OR 'RL' OR 'RM' OR          
                               'R1' OR 'R2' OR 'R3'                             
              MOVE PRE-NT-ZONE-RM-2 TO PRE-NT-ZONE-RM                           
              MOVE PRE-NT-ZONE-RM-2 (1:3) TO L225-NTB                           
              MOVE PRE-NT-ZONE-RM-2 (4:2) TO L225-FTB                           
           END-IF.                                                              
                                                                                
      **   IF F310-CDE-TRANS = 'AW' OR 'AA'                                     
      **      MOVE PRE-NT-ZONE-RM-3 TO PRE-NT-ZONE-RM                           
      **      MOVE PRE-NT-ZONE-RM-3 (1:3) TO L225-NTC                           
      **      MOVE PRE-NT-ZONE-RM-3 (4:2) TO L225-FTC                           
      **   END-IF.                                                              
                                                                                
      **   IF F310-CDE-TRANS = 'RW' OR 'RA'                                     
      **      MOVE PRE-NT-ZONE-RM-4 TO PRE-NT-ZONE-RM                           
      **      MOVE PRE-NT-ZONE-RM-4 (1:3) TO L225-NTD                           
      **      MOVE PRE-NT-ZONE-RM-4 (4:2) TO L225-FTD                           
      **   END-IF.                                                              
                                                                                
           IF WS-START-FILE = 'Y'                                               
              MOVE WS-NT-ZONE-RM TO PRE-NT-ZONE-RM-1                            
              MOVE WS-NT-ZONE-RM TO PRE-NT-ZONE-RM-2                            
              MOVE WS-NT-ZONE-RM TO PRE-NT-ZONE-RM-3                            
              MOVE WS-NT-ZONE-RM TO PRE-NT-ZONE-RM-4                            
              MOVE WS-NT-ZONE-RM TO PRE-NT-ZONE-RM                              
              MOVE 'N'      TO WS-START-FILE.                                   
                                                                                
           IF WS-NT-ZONE-RM = PRE-NT-ZONE-RM                                    
              MOVE 'N' TO NT-RM-FLAG                                            
           ELSE                                                                 
              MOVE 'Y' TO NT-RM-FLAG                                            
              PERFORM  160-CHECK-NT-ZONE    THRU 160-EXIT                       
           END-IF.                                                              
                                                                                
       150-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       160-CHECK-NT-ZONE.                                                       
      *-------------------------------------------------------------            
           IF F310-CDE-TRANS = 'AI' OR 'AS'                                     
              MOVE WS-NT-ZONE-RM TO PRE-NT-ZONE-RM-1                            
           ELSE                                                                 
           IF F310-CDE-TRANS = 'RI' OR 'RS' OR 'RK' OR 'RL' OR 'RM' OR          
                               'R1' OR 'R2' OR 'R3'                             
              MOVE WS-NT-ZONE-RM TO PRE-NT-ZONE-RM-2                            
      **   ELSE                                                                 
      **   IF F310-CDE-TRANS = 'AW' OR 'AA'                                     
      **      MOVE WS-NT-ZONE-RM TO PRE-NT-ZONE-RM-3                            
      **   ELSE                                                                 
      **   IF F310-CDE-TRANS = 'RW' OR 'RA'                                     
      **      MOVE WS-NT-ZONE-RM TO PRE-NT-ZONE-RM-4                            
           END-IF.                                                              
                                                                                
       160-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       200-READ-AND-PROCESS-F310.                                               
      *-------------------------------------------------------------            
      *                                                                         
      *   1. READ FILE BP13F310.                                                
      *   2. CHECK F310-CDE-TRANS.                                              
      *                                                                         
      *-------------------------------------------------------------            
           MOVE SPACE TO K820-FOUND.                                            
           PERFORM 900-READ-BM06K110 THRU 900-EXIT                              
                                                                                
           IF K110-FOUND = 'Y'                                                  
              PERFORM 300-OUTPUT-RESETTLEMENT-FILE THRU 300-EXIT                
              PERFORM 600-PRINT-LIST               THRU 600-EXIT.               
                                                                                
           PERFORM 150-READ-DATA THRU 150-EXIT.                                 
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       250-PRINT-NIL-REPORT.                                                    
      *-------------------------------------------------------------            
                                                                                
           IF WS-ACCEPT = ZERO                                                  
              MOVE CNT-PAGE-A TO L225-PAGE                                      
              MOVE 'P13L225A' TO L225-RPT-ID                                    
              WRITE P13L225A-REPORT FROM L225-HEAD-01 AFTER PAGE                
              WRITE P13L225A-REPORT FROM L225-HEAD-02                           
              WRITE P13L225A-REPORT FROM L225A-HEAD-03                          
              WRITE P13L225A-REPORT FROM L225-NIL-ACCEPTANCE                    
           END-IF.                                                              
                                                                                
           IF WS-REJECT = ZERO                                                  
              MOVE CNT-PAGE-B TO L225-PAGE                                      
              MOVE 'P13L225B' TO L225-RPT-ID                                    
              WRITE P13L225B-REPORT FROM L225-HEAD-01 AFTER PAGE                
              WRITE P13L225B-REPORT FROM L225-HEAD-02                           
              WRITE P13L225B-REPORT FROM L225B-HEAD-03                          
              WRITE P13L225B-REPORT FROM L225-NIL-REJECTION                     
           END-IF.                                                              
                                                                                
       250-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       300-OUTPUT-RESETTLEMENT-FILE.                                            
      *-------------------------------------------------------------            
      *                                                                         
      *   OUTPUT INFORMATION TO RESETTLEMENT FILE WHERE ALLO-SCHEME             
      *   = 'RH' OR 'RJ' FOR ALL APPLICANT REGISTERED UNDER RESETT.             
      *   BUT MAY BE ALLOCATED UNDER RESETT OR PUBLIC OR OTHERS.                
      *   ONLY CHECK 1ST 2 DIGITS BECAUSE THE LAST BYTE MAY                     
      *   SOMETIMES BE CODED AS '0' OR 'O'.                                     
      *                                                                         
      *-------------------------------------------------------------            
                                                                                
           PERFORM 350-READ-BP13K800 THRU 350-EXIT.                             
                                                                                
           MOVE SPACES TO BP13F220-REC.                                         
                                                                                
           IF F310-CDE-TRANS = 'RI' OR 'RS' OR                                  
                               'RK' OR 'RL' OR 'RM' OR                          
                               'R1' OR 'R2' OR 'R3'                             
              MOVE SPACES               TO F220-NUM-REGN                        
                                           WS-K820-REGN-NO                      
           ELSE                                                                 
              MOVE F310-REGN-NO         TO F220-NUM-REGN                        
                                           WS-K820-REGN-NO                      
           END-IF.                                                              
                                                                                
           IF REC-K800-FOUND AND (WS-ALLOC-SCHEME(1:2) = 'RH' OR 'RJ')          
              MOVE K800-NUM-ALLO-SCHEME TO F220-ALLOC-SCH                       
              MOVE F310-CDE-TRANS       TO F220-ALLOC-STATUS                    
              MOVE F310-DTE-TRANS       TO F220-ACCEPT-REJECT-DATE              
              MOVE F310-SCH-ACC         TO K110-KEY-FLD                         
              PERFORM 311-MOVE-INFO-FOR-RESETTLE THRU 311-EXIT.                 
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       311-MOVE-INFO-FOR-RESETTLE.                                              
      *-------------------------------------------------------------            
                                                                                
           IF BM06K110-STATUS NOT = 0                                           
              GO TO 311-EXIT.                                                   
                                                                                
           MOVE F310-NUM-BLK         TO F220-BLK-NO.                            
           MOVE F310-NME-STREET      TO F220-STREET-NAME.                       
           MOVE F310-NUM-LEVEL       TO F220-LEVEL-NO.                          
           MOVE F310-NUM-MAIN(1:4)   TO F220-MAIN-UNIT.                         
           MOVE F310-NUM-MAIN(5:1)   TO F220-SUB-UNIT.                          
           MOVE '00000000'           TO F220-KEY-ISSUE-DATE.                    
           MOVE F310-CDE-TYP-ACTUSE  TO F220-CDE-TYP-ACTUSE.                    
                                                                                
           MOVE SPACES               TO F220-NME-APPL1                          
                                        F220-NME-APPL2                          
                                        F220-NME-APPL3                          
                                        F220-NME-APPL4.                         
                                                                                
           PERFORM VARYING WS-CNT FROM 1 BY 1 UNTIL WS-CNT > 4                  
              EVALUATE WS-CNT                                                   
              WHEN 1                                                            
                   MOVE K800-NUM-NRIC1 TO WS-K820-NUM-NRIC                      
              WHEN 2                                                            
                   MOVE K800-NUM-NRIC2 TO WS-K820-NUM-NRIC                      
              WHEN 3                                                            
                   MOVE K800-NUM-NRIC3 TO WS-K820-NUM-NRIC                      
              WHEN 4                                                            
                   MOVE K800-NUM-NRIC4 TO WS-K820-NUM-NRIC                      
              END-EVALUATE                                                      
                                                                                
              PERFORM 330-READ-BP13K820 THRU 330-EXIT                           
                                                                                
              IF K820-FOUND = 'Y'                                               
                 MOVE K820-NME-OCCP   TO WS-NAME                                
              ELSE                                                              
                 MOVE SPACES          TO WS-NAME                                
              END-IF                                                            
                                                                                
              EVALUATE WS-CNT                                                   
              WHEN 1                                                            
                   MOVE WS-NAME TO F220-NME-APPL1                               
              WHEN 2                                                            
                   MOVE WS-NAME TO F220-NME-APPL2                               
              WHEN 3                                                            
                   MOVE WS-NAME TO F220-NME-APPL3                               
              WHEN 4                                                            
                   MOVE WS-NAME TO F220-NME-APPL4                               
              END-EVALUATE                                                      
                                                                                
           END-PERFORM.                                                         
                                                                                
           WRITE BP13F220-REC.                                                  
                                                                                
       311-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       330-READ-BP13K820.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE WS-K820-KEY TO K820-KEY-FLD.                                    
                                                                                
           READ BP13K820 INVALID KEY                                            
                DISPLAY 'RECORD NOT FOUND IN BP13K820 ' F310-REGN-NO            
                GO TO 330-EXIT.                                                 
                                                                                
           IF BP13K820-STATUS NOT = 0                                           
              PERFORM 1100-CLOSING-ROUTINE  THRU 1100-EXIT                      
           ELSE                                                                 
              MOVE 'Y' TO K820-FOUND.                                           
                                                                                
       330-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       350-READ-BP13K800.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE 'N' TO REC-K800-FOUND-FLAG.                                     
           MOVE F310-REGN-NO      TO K800-NUM-REGN.                             
                                                                                
           READ BP13K800 INVALID KEY                                            
                DISPLAY 'RECORD NOT FOUND IN BP13K800 ' F310-REGN-NO            
                MOVE SPACES TO WS-ALLOC-SCHEME                                  
                GO TO 350-EXIT.                                                 
                                                                                
           IF BP13K800-STATUS NOT = ZEROES                                      
              DISPLAY 'READ BP13K800 ERROR STATUS ' BP13K800-STATUS             
                      ',  F310-REGN IS ' F310-REGN-NO                           
              MOVE SPACES TO WS-ALLOC-SCHEME                                    
              GO TO 350-EXIT.                                                   
                                                                                
           MOVE K800-NUM-ALLO-SCHEME TO WS-ALLOC-SCHEME.                        
           MOVE 'Y' TO REC-K800-FOUND-FLAG.                                     
                                                                                
           STRING K800-DTE-BK-APPT (7:2)                                        
                  K800-DTE-BK-APPT (5:2)                                        
                  K800-DTE-BK-APPT (3:2)                                        
                  DELIMITED BY SIZE INTO  L225-APPT-DATE.                       
                                                                                
           STRING K800-TME-BK-APPT (1:2) ':'                                    
                  K800-TME-BK-APPT (3:2)                                        
                  DELIMITED BY SIZE INTO  L225-APPT-TIME.                       
                                                                                
           STRING F310-TME-ACCEPT-REJECT (1:2) ':'                              
                  F310-TME-ACCEPT-REJECT (3:2)                                  
                  DELIMITED BY SIZE INTO L225-NEW-ACC-REJ-TIME.                 
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       600-PRINT-LIST.                                                          
      *-------------------------------------------------------------            
      *                                                                         
      *      PRINT DETAIL LIST OF PREPARATION LIST FOR                          
      *      ACCEPTED/REJECTED CASES.                                           
      * CHG ON 10/2/87 - REC WITH TRANS-TY = '000000' COME FROM                 
      * NORMAL ACCEPTANCE, FOR ACCEPTANCE, GET INFO FROM K010-REC               
      * FOR REJECTION, SINCE THE REC IN K010 ALREADY DELETED, SO                
      * GET INFO FROM F310-LOG FILE.                                            
      *                                                                         
      *-------------------------------------------------------------            
           IF F310-NUM-ZONE = SPACES OR LOW-VALUES                              
              MOVE 'NEW TOWN :'            TO L225-NT-ZONE-TXT                  
              MOVE SPACES                  TO L225-NT-TXT                       
              MOVE 'REGN-NO     '          TO L225-HDR-NT-ZONE                  
           ELSE                                                                 
              MOVE 'ZONE     :'            TO L225-NT-ZONE-TXT                  
              MOVE 'NEW TOWN :'            TO L225-NT-TXT                       
              MOVE 'QNO/HT/REGN-NO'        TO L225-HDR-NT-ZONE                  
           END-IF.                                                              
                                                                                
           IF F310-CDE-TRANS = 'AI' OR 'AS'                                     
              ADD 1 TO WS-ACCEPT                                                
                       WS-ACCEPTA                                               
              PERFORM 650-MOVE-PRINTING-INFO THRU 650-EXIT                      
              PERFORM 701-HEADING-A          THRU 701-EXIT.                     
                                                                                
           IF F310-CDE-TRANS = 'RI' OR 'RS' OR 'RK' OR 'RL' OR 'RM' OR          
                               'R1' OR 'R2' OR 'R3'                             
              ADD 1 TO WS-REJECT                                                
                       WS-REJECTA                                               
              PERFORM 650-MOVE-PRINTING-INFO THRU 650-EXIT                      
              PERFORM 702-HEADING-B          THRU 702-EXIT.                     
                                                                                
      **   IF F310-CDE-TRANS = 'AW' OR 'AA'                                     
      **      ADD 1 TO WS-ACCEPT-WIS                                            
      **               WS-ACCEPT-WISA                                           
      **      PERFORM 650-MOVE-PRINTING-INFO THRU 650-EXIT                      
      **      PERFORM 703-HEADING-C          THRU 703-EXIT.                     
                                                                                
      **   IF F310-CDE-TRANS = 'RW' OR 'RA'                                     
      **      ADD 1 TO WS-REJECT-WIS                                            
      **               WS-REJECT-WISA                                           
      **      PERFORM 650-MOVE-PRINTING-INFO THRU 650-EXIT                      
      **      PERFORM 704-HEADING-D          THRU 704-EXIT.                     
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       650-MOVE-PRINTING-INFO.                                                  
      *-------------------------------------------------------------            
                                                                                
           MOVE F310-CDE-FLAT-TYPE   TO L225-RECOM-RM-TYPE.                     
                                                                                
           IF F310-NUM-ZONE = SPACES OR LOW-VALUES                              
              MOVE F310-CDE-NT           TO L225-CDE-NT-ZONE                    
              MOVE F310-REGN-NO          TO L225-NT-REGN-NO                     
           ELSE                                                                 
              MOVE F310-NUM-ZONE         TO L225-CDE-NT-ZONE                    
              MOVE F310-CDE-NT           TO L225-CDE-NT                         
                                                                                
              EVALUATE K800-NUM-HHTY-QUEUE-ALPHA                                
              WHEN 'H'                                                          
                   MOVE 'F' TO L225-HH-TYPE                                     
              WHEN 'G'                                                          
                   MOVE 'U' TO L225-HH-TYPE                                     
              WHEN OTHER                                                        
                   MOVE K800-NUM-HHTY-QUEUE-ALPHA TO L225-HH-TYPE               
              END-EVALUATE                                                      
                                                                                
              MOVE K800-NUM-ZONE-FT-QUEUE      TO L225-QUEUE-NO                 
              MOVE F310-REGN-NO                TO L225-ZONE-REGN-NO             
              MOVE '/'                         TO L225-SLASH-1                  
                                                  L225-SLASH-2                  
           END-IF.                                                              
                                                                                
           STRING K110-DTE-ALLOCN (7:2)                                         
                  K110-DTE-ALLOCN (5:2)                                         
                  K110-DTE-ALLOCN (3:2)                                         
                  DELIMITED BY SIZE INTO L225-ALLOC-DATE.                       
                                                                                
           MOVE K110-SCHEME               TO L225-SCHEME.                       
           MOVE K110-ACC                  TO L225-ACC.                          
           MOVE K110-CHK                  TO L225-CHK.                          
           MOVE K110-ACC-TYPE             TO L225-ACC-TYPE.                     
           MOVE K110-FLAT-STATUS          TO L225-FLAT-STATUS.                  
           MOVE K110-CDE-OCS-TAG          TO L225-NUM-OCS-RQST.                 
           MOVE K110-AMT-OCS-DISCOUNT     TO L225-AMT-OCS-DISCOUNT.             
                                                                                
           IF   K110-FLAT-STATUS = 'RP' AND                                     
                K110-REPOSSESSED-CODE = '40'                                    
                MOVE 'BB'           TO L225-BUY-BACK                            
           ELSE                                                                 
                MOVE SPACES         TO L225-BUY-BACK                            
           END-IF.                                                              
                                                                                
           MOVE F310-NUM-BLK              TO L225-BLK-NO.                       
           MOVE F310-NME-STREET           TO L225-STREET-NAME.                  
           MOVE F310-NUM-LEVEL            TO L225-LEVEL-NO.                     
           MOVE '-'                       TO L225-DASH-4.                       
           MOVE F310-NUM-MAIN (1:4)       TO L225-MAIN-UNIT.                    
           IF F310-NUM-MAIN (5:1) = SPACES                                      
              MOVE SPACE       TO  L225-DASH-5                                  
                                   L225-SUB-UNIT                                
           ELSE                                                                 
              MOVE F310-NUM-MAIN (5:1)       TO L225-SUB-UNIT                   
              MOVE '-'                       TO  L225-DASH-5.                   
                                                                                
           STRING F310-DTE-TRANS(7:2)                                           
                  F310-DTE-TRANS(5:2)                                           
                  F310-DTE-TRANS(3:2)                                           
                  DELIMITED BY SIZE INTO L225-NEW-ACC-REJ-DATE.                 
                                                                                
           MOVE F310-USERID               TO L225-USERID.                       
           MOVE K800-NUM-ALLO-SCHEME      TO L225-ALLOCATION-SCHEME.            
           MOVE K800-NUM-CAT              TO L225-CAT.                          
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO L225-DATE.                             
                                                                                
           MOVE F310-CDE-TRANS            TO L225-NEW-ALLOC-STATUS.             
                                                                                
       650-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       701-HEADING-A.                                                           
      *-------------------------------------------------------------            
                                                                                
           IF CNT-LINE-A > 40                                                   
              IF (NT-RM-FLAG = 'Y') AND (CNT-LINE-A NOT = 99)                   
                 COMPUTE L225-ACCEPTA = WS-ACCEPTA - 1                          
                 WRITE P13L225A-REPORT FROM L225A-HEAD-08A AFTER 2              
                 MOVE  1               TO   WS-ACCEPTA                          
              END-IF                                                            
              ADD 1 TO CNT-PAGE-A                                               
              MOVE CNT-PAGE-A TO L225-PAGE                                      
              MOVE 'P13L225A' TO L225-RPT-ID                                    
              WRITE P13L225A-REPORT FROM L225-HEAD-01  AFTER PAGE               
              WRITE P13L225A-REPORT FROM L225-HEAD-02  AFTER 1                  
              WRITE P13L225A-REPORT FROM L225A-HEAD-03 AFTER 1                  
              WRITE P13L225A-REPORT FROM L225-HEAD-04  AFTER 1                  
              WRITE P13L225A-REPORT FROM L225-HEAD-06  AFTER 2                  
              WRITE P13L225A-REPORT FROM L225-HEAD-06A AFTER 1                  
              MOVE SPACES TO P13L225A-REPORT                                    
              WRITE P13L225A-REPORT                                             
              MOVE SPACES TO NT-RM-FLAG                                         
              MOVE ZEROS TO CNT-LINE-A                                          
                            CNT-LINE-A1.                                        
                                                                                
           IF NT-RM-FLAG = 'Y'                                                  
              MOVE WS-NT-ZONE-RM TO PRE-NT-ZONE-RM-1.                           
                                                                                
           IF (NT-RM-FLAG = 'Y') AND (CNT-LINE-A < 41)                          
              COMPUTE L225-ACCEPTA = WS-ACCEPTA - 1                             
              WRITE P13L225A-REPORT FROM L225A-HEAD-08A AFTER 2                 
              MOVE  1               TO   WS-ACCEPTA                             
              ADD  2 TO CNT-LINE-A                                              
              WRITE P13L225A-REPORT FROM L225-HEAD-04 AFTER 3                   
              WRITE P13L225A-REPORT FROM L225-HEAD-06 AFTER 2                   
              WRITE P13L225A-REPORT FROM L225-HEAD-06A AFTER 1                  
              ADD 7 TO CNT-LINE-A.                                              
                                                                                
           MOVE SPACES TO P13L225A-REPORT.                                      
           ADD 1 TO CNT-LINE-A.                                                 
           WRITE P13L225A-REPORT FROM L225-HEAD-07 AFTER 1.                     
                                                                                
       701-EXIT.  EXIT.                                                         
                                                                                
      *-------------------------------------------------------------            
       702-HEADING-B.                                                           
      *-------------------------------------------------------------            
                                                                                
           IF CNT-LINE-B > 40                                                   
              IF (NT-RM-FLAG = 'Y') AND (CNT-LINE-B NOT = 99)                   
                 COMPUTE L225-REJECTA = WS-REJECTA - 1                          
                 WRITE P13L225B-REPORT FROM L225B-HEAD-08A AFTER 2              
                 MOVE  1               TO   WS-REJECTA                          
              END-IF                                                            
              ADD 1 TO CNT-PAGE-B                                               
              MOVE CNT-PAGE-B TO L225-PAGE                                      
              MOVE 'P13L225B' TO L225-RPT-ID                                    
              WRITE P13L225B-REPORT FROM L225-HEAD-01  AFTER PAGE               
              WRITE P13L225B-REPORT FROM L225-HEAD-02  AFTER 1                  
              WRITE P13L225B-REPORT FROM L225B-HEAD-03 AFTER 1                  
              WRITE P13L225B-REPORT FROM L225-HEAD-04  AFTER 1                  
              WRITE P13L225B-REPORT FROM L225-HEAD-06  AFTER 2                  
              WRITE P13L225B-REPORT FROM L225-HEAD-06A AFTER 1                  
              MOVE SPACES TO NT-RM-FLAG                                         
              MOVE ZEROS TO CNT-LINE-B                                          
                            CNT-LINE-B1.                                        
                                                                                
           IF NT-RM-FLAG = 'Y'                                                  
              MOVE WS-NT-ZONE-RM TO PRE-NT-ZONE-RM-2.                           
                                                                                
           IF (NT-RM-FLAG = 'Y') AND (CNT-LINE-B < 41)                          
              COMPUTE L225-REJECTA = WS-REJECTA - 1                             
              WRITE P13L225B-REPORT FROM L225B-HEAD-08A AFTER 2                 
              MOVE  1               TO   WS-REJECTA                             
              ADD 2 TO CNT-LINE-B                                               
              WRITE P13L225B-REPORT FROM L225-HEAD-04  AFTER 3                  
              WRITE P13L225B-REPORT FROM L225-HEAD-06  AFTER 2                  
              WRITE P13L225B-REPORT FROM L225-HEAD-06A AFTER 1                  
              ADD 7 TO CNT-LINE-B.                                              
                                                                                
           MOVE SPACES TO P13L225B-REPORT.                                      
           ADD 1 TO CNT-LINE-B.                                                 
           WRITE P13L225B-REPORT FROM L225-HEAD-07 AFTER 1.                     
                                                                                
       702-EXIT.  EXIT.                                                         
                                                                                
      *-------------------------------------------------------------            
       703-HEADING-C.                                                           
      *-------------------------------------------------------------            
                                                                                
           IF CNT-LINE-C > 40                                                   
              IF (NT-RM-FLAG = 'Y') AND (CNT-LINE-C NOT = 99)                   
                 COMPUTE L225-ACCEPT-WISA = WS-ACCEPT-WISA - 1                  
                 WRITE P13L225C-REPORT FROM L225C-HEAD-08A AFTER 2              
                 MOVE  1               TO   WS-ACCEPT-WISA                      
              END-IF                                                            
              ADD 1 TO CNT-PAGE-C                                               
              MOVE CNT-PAGE-C TO L225-PAGE                                      
              MOVE 'P13L225C' TO L225-RPT-ID                                    
              WRITE P13L225C-REPORT FROM L225-HEAD-01  AFTER PAGE               
              WRITE P13L225C-REPORT FROM L225-HEAD-02  AFTER 1                  
              WRITE P13L225C-REPORT FROM L225C-HEAD-03 AFTER 1                  
              WRITE P13L225C-REPORT FROM L225-HEAD-04  AFTER 1                  
              WRITE P13L225C-REPORT FROM L225-HEAD-06  AFTER 2                  
              WRITE P13L225C-REPORT FROM L225-HEAD-06A AFTER 1                  
              MOVE SPACES TO NT-RM-FLAG                                         
              MOVE ZEROS TO CNT-LINE-C                                          
                            CNT-LINE-C1.                                        
                                                                                
           IF NT-RM-FLAG = 'Y'                                                  
              MOVE WS-NT-ZONE-RM TO PRE-NT-ZONE-RM-3.                           
                                                                                
           IF (NT-RM-FLAG = 'Y') AND (CNT-LINE-C < 41)                          
              COMPUTE L225-ACCEPT-WISA = WS-ACCEPT-WISA - 1                     
              WRITE P13L225C-REPORT FROM L225C-HEAD-08A AFTER 2                 
              MOVE  1               TO   WS-ACCEPT-WISA                         
              ADD 2 TO CNT-LINE-C                                               
              WRITE P13L225C-REPORT FROM L225-HEAD-04  AFTER 3                  
              WRITE P13L225C-REPORT FROM L225-HEAD-06  AFTER 2                  
              WRITE P13L225C-REPORT FROM L225-HEAD-06A AFTER 1                  
              ADD 7 TO CNT-LINE-C.                                              
                                                                                
           MOVE SPACES TO P13L225C-REPORT.                                      
           ADD 1 TO CNT-LINE-C.                                                 
           WRITE P13L225C-REPORT FROM L225-HEAD-07 AFTER 1.                     
                                                                                
       703-EXIT.  EXIT.                                                         
                                                                                
      *-------------------------------------------------------------            
       704-HEADING-D.                                                           
      *-------------------------------------------------------------            
                                                                                
           IF CNT-LINE-D > 40                                                   
              IF (NT-RM-FLAG = 'Y') AND (CNT-LINE-D NOT = 99)                   
                 COMPUTE L225-REJECT-WISA = WS-REJECT-WISA - 1                  
                 WRITE P13L225D-REPORT FROM L225D-HEAD-08A AFTER 2              
                 MOVE  1               TO   WS-REJECT-WISA                      
              END-IF                                                            
              ADD 1 TO CNT-PAGE-D                                               
              MOVE CNT-PAGE-D TO L225-PAGE                                      
              MOVE 'P13L225D' TO L225-RPT-ID                                    
              WRITE P13L225D-REPORT FROM L225-HEAD-01  AFTER PAGE               
              WRITE P13L225D-REPORT FROM L225-HEAD-02  AFTER 1                  
              WRITE P13L225D-REPORT FROM L225D-HEAD-03 AFTER 1                  
              WRITE P13L225D-REPORT FROM L225-HEAD-04  AFTER 1                  
              WRITE P13L225D-REPORT FROM L225-HEAD-06  AFTER 2                  
              WRITE P13L225D-REPORT FROM L225-HEAD-06A AFTER 1                  
              MOVE SPACES TO NT-RM-FLAG                                         
              MOVE ZEROS TO CNT-LINE-D                                          
                            CNT-LINE-D1.                                        
                                                                                
           IF NT-RM-FLAG = 'Y'                                                  
              MOVE WS-NT-ZONE-RM TO PRE-NT-ZONE-RM-4.                           
                                                                                
           IF (NT-RM-FLAG = 'Y') AND (CNT-LINE-D < 41)                          
              COMPUTE L225-REJECT-WISA = WS-REJECT-WISA - 1                     
              WRITE P13L225D-REPORT FROM L225D-HEAD-08A AFTER 2                 
              MOVE  1               TO   WS-REJECT-WISA                         
              ADD 2 TO CNT-LINE-D                                               
              WRITE P13L225D-REPORT FROM L225-HEAD-04  AFTER 3                  
              WRITE P13L225D-REPORT FROM L225-HEAD-06  AFTER 2                  
              WRITE P13L225D-REPORT FROM L225-HEAD-06A AFTER 1                  
              ADD 7 TO CNT-LINE-D.                                              
                                                                                
           MOVE SPACES TO P13L225D-REPORT.                                      
           ADD 1 TO CNT-LINE-D.                                                 
           WRITE P13L225D-REPORT FROM L225-HEAD-07 AFTER 1.                     
                                                                                
       704-EXIT.  EXIT.                                                         
                                                                                
      *-------------------------------------------------------------            
       900-READ-BM06K110.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE F310-SCH-ACC TO K110-KEY-FLD.                                   
           MOVE 'Y' TO K110-FOUND.                                              
           READ BM06K110 INVALID KEY                                            
                DISPLAY 'READ ERROR, K110-STATUS ' BM06K110-STATUS              
                        ',  K110-KEY-FLD IS ' K110-KEY-FLD                      
                MOVE 'N' TO K110-FOUND.                                         
                                                                                
       900-EXIT.  EXIT.                                                         
                                                                                
      *-------------------------------------------------------------            
       1100-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
                                                                                
           IF CNT-PAGE-A > 0                                                    
              MOVE PRE-NT-ZONE-RM-1(1:3) TO L225-NTA                            
              MOVE PRE-NT-ZONE-RM-1(4:2) TO L225-FTA                            
              MOVE  WS-ACCEPTA      TO L225-ACCEPTA                             
              MOVE  WS-ACCEPT       TO L225-ACCEPT                              
              MOVE  SPACES          TO P13L225A-REPORT                          
              WRITE P13L225A-REPORT FROM L225A-HEAD-08A AFTER 2                 
              WRITE P13L225A-REPORT FROM L225A-HEAD-08 AFTER 3.                 
                                                                                
           IF CNT-PAGE-B > 0                                                    
              MOVE PRE-NT-ZONE-RM-2(1:3) TO L225-NTB                            
              MOVE PRE-NT-ZONE-RM-2(4:2) TO L225-FTB                            
              MOVE  WS-REJECTA      TO L225-REJECTA                             
              MOVE  WS-REJECT       TO L225-REJECT                              
              MOVE  SPACES          TO P13L225B-REPORT                          
              WRITE P13L225B-REPORT FROM L225B-HEAD-08A AFTER 2                 
              WRITE P13L225B-REPORT FROM L225B-HEAD-08 AFTER 3.                 
                                                                                
           IF CNT-PAGE-C > 0                                                    
              MOVE PRE-NT-ZONE-RM-3(1:3) TO L225-NTC                            
              MOVE PRE-NT-ZONE-RM-3(4:2) TO L225-FTC                            
              MOVE  WS-ACCEPT-WISA  TO L225-ACCEPT-WISA                         
              MOVE  WS-ACCEPT-WIS   TO L225-ACCEPT-WIS                          
              MOVE  SPACES          TO P13L225C-REPORT                          
              WRITE P13L225C-REPORT FROM L225C-HEAD-08A AFTER 2                 
              WRITE P13L225C-REPORT FROM L225C-HEAD-08 AFTER 3.                 
                                                                                
           IF CNT-PAGE-D > 0                                                    
              MOVE PRE-NT-ZONE-RM-4(1:3) TO L225-NTD                            
              MOVE PRE-NT-ZONE-RM-4(4:2) TO L225-FTD                            
              MOVE  WS-REJECT-WISA  TO L225-REJECT-WISA                         
              MOVE  WS-REJECT-WIS   TO L225-REJECT-WIS                          
              MOVE  SPACES          TO P13L225D-REPORT                          
              WRITE P13L225D-REPORT FROM L225D-HEAD-08A AFTER 2                 
              WRITE P13L225D-REPORT FROM L225D-HEAD-08 AFTER 3.                 
                                                                                
           CLOSE BP13F310                                                       
                 BP13F220                                                       
                 BM06K110                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 P13L225A                                                       
                 P13L225B                                                       
                 P13L225C                                                       
                 P13L225D.                                                      
                                                                                
           IF BM06K110-STATUS NOT = 0                                           
               MOVE BM06K110-STATUS TO RETURN-CODE                              
               DISPLAY 'CLOSING ERROR, K110-STATUS ' BM06K110-STATUS.           
                                                                                
           IF BP13K800-STATUS NOT = 0                                           
               MOVE BP13K800-STATUS TO RETURN-CODE                              
               DISPLAY 'CLOSING ERROR, K800-STATUS ' BP13K800-STATUS.           
                                                                                
           IF BP13K820-STATUS NOT = 0                                           
               MOVE BP13K820-STATUS TO RETURN-CODE                              
               DISPLAY 'CLOSING ERROR, K820-STATUS ' BP13K820-STATUS.           
                                                                                
           STOP RUN.                                                            
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
