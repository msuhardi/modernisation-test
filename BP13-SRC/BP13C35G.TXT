       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C35G.                                                  
      *DATE-WRITTEN. 29 OCT 2015.                                               
      *AUTHOR.       KRISTINE LANTIN.                                           
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE: 1) To extract units within the blocks updated  *            
      *                by oic for email triggering.                *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP135823 20151029 KVL1  NEW PROGRAM                        *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *-------------------------------------------------------------            
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06F100  ASSIGN        TO BM06F100.                          
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS K110-KEY-FLD                       
                            ALTERNATE KEY IS K110-REGN-NO                       
                                          WITH DUPLICATES                       
                            ALTERNATE KEY IS K110-AIX1                          
                                          WITH DUPLICATES                       
                            FILE STATUS   IS BM06K110-STATUS.                   
                                                                                
           SELECT BP13L35G ASSIGN         TO BP13L35G.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BM06F100                                                             
           RECORD CONTAINS 500  CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       COPY BM06F100.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500  CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD  BP13L35G                                                             
           RECORD CONTAINS 11   CHARACTERS                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  BP13L35G-REC             PIC X(11).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  BM06K110-STATUS     PIC 9(02)  VALUE ZEROES.                         
       01  WS-READ-CNT         PIC 9(06)  VALUE ZEROES.                         
       01  WS-WRITE-CNT        PIC 9(06)  VALUE ZEROES.                         
       01  WS-F100-EOF         PIC X      VALUE 'N'.                            
       01  WS-K110-EOF         PIC X      VALUE 'N'.                            
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-LOGIC.                                                         
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 2000-READ-BM06F100  THRU 2000-EXIT.                          
           IF WS-F100-EOF = 'N'                                                 
              PERFORM 2100-READ-BM06K110  THRU 2100-EXIT                        
              PERFORM 3000-PROCESS-DATA   THRU 3000-EXIT                        
                UNTIL WS-F100-EOF = 'Y'                                         
           ELSE                                                                 
              DISPLAY 'FILE BM06F100 IS EMPTY'                                  
           END-IF                                                               
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BM06F100                                                 
                       BM06K110                                                 
                OUTPUT BP13L35G.                                                
                                                                                
           IF BM06K110-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BM06K110 OPEN ERROR, STATUS : ' BM06K110-STATUS          
              MOVE     BM06K110-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BM06F100.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BM06F100 AT END                                                 
                         MOVE 'Y'  TO WS-F100-EOF                               
                         NOT AT END                                             
                         ADD  1    TO WS-READ-CNT                               
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2100-READ-BM06K110.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                TO K110-REC.                              
           INITIALIZE                    K110-REC.                              
           MOVE 'N'                   TO WS-K110-EOF.                           
                                                                                
           MOVE F100-KEY-FLD          TO K110-AIX1.                             
                                                                                
           START BM06K110 KEY >= K110-AIX1.                                     
                                                                                
           EVALUATE BM06K110-STATUS                                             
              WHEN 00                                                           
                 PERFORM 2200-READNEXT-BM06K110                                 
                    THRU 2200-EXIT                                              
                   UNTIL WS-K110-EOF = 'Y'                                      
              WHEN 10                                                           
              WHEN 23                                                           
              WHEN 46                                                           
                 MOVE 'Y' TO WS-K110-EOF                                        
                 DISPLAY 'BM06K110 REC NOT FND : ' BM06K110-STATUS              
                         ' F100-KEY-FLD = ' F100-KEY-FLD                        
              WHEN OTHER                                                        
                 DISPLAY 'ERROR STARTBROWSE BM06K110. STATUS IS '               
                            BM06K110-STATUS                                     
                 MOVE BM06K110-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2200-READNEXT-BM06K110.                                                  
      *-------------------------------------------------------------            
           READ BM06K110 NEXT RECORD.                                           
                                                                                
           EVALUATE BM06K110-STATUS                                             
              WHEN 00                                                           
              WHEN 02                                                           
                 IF K110-AIX1 = F100-KEY-FLD                                    
                       MOVE  K110-SCH-ACC-NO TO BP13L35G-REC                    
                       WRITE BP13L35G-REC                                       
                       ADD 1 TO WS-WRITE-CNT                                    
                 ELSE                                                           
                       MOVE 'Y' TO   WS-K110-EOF                                
                 END-IF                                                         
              WHEN 10                                                           
              WHEN 23                                                           
              WHEN 46                                                           
                 MOVE 'Y' TO   WS-K110-EOF                                      
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READ NEXT BM06K110. STATUS IS '                 
                            BM06K110-STATUS                                     
                 MOVE BM06K110-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           PERFORM 2000-READ-BM06F100  THRU 2000-EXIT.                          
           IF WS-F100-EOF = 'N'                                                 
              PERFORM 2100-READ-BM06K110  THRU 2100-EXIT                        
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
                                                                                
           DISPLAY '---- BP13C35G CONTROL TOTAL -----'.                         
           DISPLAY ' NO OF REC READ FROM BM06F100  = '                          
                     WS-READ-CNT.                                               
           DISPLAY ' NO OF REC WRITTEN TO BP13L35G = '                          
                     WS-WRITE-CNT.                                              
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE       BM06F100                                                 
                       BM06K110                                                 
                       BP13L35G.                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
