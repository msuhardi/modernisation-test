       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C723.                                                 
       AUTHOR. BALA AMUTHAN.                                                    
       DATE-WRITTEN.  02/04/2007.                                               
                                                                                
      *****************************************************************         
      *                                                               *         
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                         *         
      *                                                               *         
      *    SYSTEM ID : BP13                                           *         
      *                                                               *         
      *    OBJECTIVE  :                                               *         
      *                                                               *         
      *     INPUT FILES:                                              *         
      *           1. BP13F825                                         *         
      *           2. BP13F730                                         *         
      *                                                               *         
      *     OUTPUT FILE:                                              *         
      *           2. P13F825A                                         *         
      *                                                               *         
      *----------------------------------------------------------------         
      * REF NO     DATE    BY   AMENDMENTS/ENHANCEMENTS               *         
      * -------    ----    ---  -----------------------               *         
      * BP13????   020407  BA2  NEW PROG.                             *         
      *****************************************************************         
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F825 ASSIGN TO BP13F825.                                  
                                                                                
           SELECT BP13F730 ASSIGN TO BP13F730.                                  
                                                                                
           SELECT P13F825A ASSIGN TO P13F825A.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F825                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F825.                                                           
                                                                                
       FD  BP13F730                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500  CHARACTERS                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F730.                                                           
                                                                                
       FD  P13F825A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01 P13F825A-REC        PIC X(200).                                       
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-CNT-F825-READ               PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-F730-READ               PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-F825-WRIT               PIC 9(5)    VALUE ZEROES.             
       01  WS-CURRENT-DATE.                                                     
           05 WS-CCYY                     PIC 9999.                             
           05 WS-MM                       PIC 99.                               
           05 WS-DD                       PIC 99.                               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
                                                                                
      ******************************************************************        
      *        MAIN LINE PROCESS                                       *        
      ******************************************************************        
       MAIN-PROCESS.                                                            
           PERFORM 000-OPEN-FILE                THRU 000-EXIT.                  
           PERFORM 100-READ-F825                THRU 100-EXIT.                  
           PERFORM 200-READ-F730                THRU 200-EXIT.                  
           PERFORM 300-PROCESS-SEQUENTIAL-FILES THRU 300-EXIT                   
               UNTIL F825-NUM-REGN = HIGH-VALUES.                               
           PERFORM 1000-CLOSE-ROUTINE           THRU 1000-EXIT.                 
                                                                                
      ***************************************************************           
      *    OPENING FILES SECTION                                    *           
      ***************************************************************           
                                                                                
       000-OPEN-FILE.                                                           
           OPEN INPUT  BP13F825                                                 
                       BP13F730                                                 
                OUTPUT P13F825A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CURRENT-DATE.                  
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ************************************************************              
      *    READ SEQUENTIAL FILE BP13F825                         *              
      ************************************************************              
                                                                                
      ************************************************************              
       100-READ-F825.                                                           
      ************************************************************              
                                                                                
           READ BP13F825                                                        
                AT END MOVE HIGH-VALUE  TO F825-NUM-REGN                        
                GO TO 100-EXIT.                                                 
           ADD  1                      TO WS-CNT-F825-READ.                     
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
      *    READ SEQUENTIAL FILE BP13F730                         *              
      ************************************************************              
                                                                                
      ************************************************************              
       200-READ-F730.                                                           
      ************************************************************              
           READ BP13F730                                                        
                AT END MOVE HIGH-VALUE  TO F730-NUM-REGN                        
                GO TO 200-EXIT.                                                 
           ADD  1                       TO WS-CNT-F730-READ.                    
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ************************************************************              
       300-PROCESS-SEQUENTIAL-FILES.                                            
      ************************************************************              
                                                                                
           IF F825-NUM-REGN = F730-NUM-REGN                                     
              PERFORM 400-PROCESS-WRITE     THRU 400-EXIT                       
                 UNTIL F825-NUM-REGN NOT = F730-NUM-REGN                        
           ELSE                                                                 
             IF F825-NUM-REGN > F730-NUM-REGN                                   
                PERFORM 200-READ-F730       THRU 200-EXIT                       
             ELSE                                                               
                PERFORM 100-READ-F825       THRU 100-EXIT                       
             END-IF                                                             
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
       400-PROCESS-WRITE.                                                       
      ************************************************************              
           MOVE SPACES          TO P13F825A-REC.                                
           MOVE BP13F825-REC    TO P13F825A-REC.                                
           WRITE P13F825A-REC.                                                  
           ADD +1               TO WS-CNT-F825-WRIT.                            
           PERFORM 100-READ-F825       THRU 100-EXIT.                           
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       1000-CLOSE-ROUTINE.                                                      
                                                                                
           DISPLAY '******  BP13C723 *************'.                            
           DISPLAY 'NO OF BP13F825 READ  : '                                    
                                             WS-CNT-F825-READ.                  
           DISPLAY 'NO OF BP13F730 READ  : '                                    
                                             WS-CNT-F730-READ.                  
           DISPLAY 'NO OF P13F825A WRITE : '                                    
                                             WS-CNT-F825-WRIT.                  
                                                                                
           CLOSE BP13F730                                                       
                 BP13F825                                                       
                 P13F825A.                                                      
                                                                                
       1000-EXIT.                                                               
           STOP RUN.                                                            
