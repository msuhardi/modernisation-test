       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CCA6.                                                 
       AUTHOR.        IRVIN CHING.                                              
       DATE-WRITTEN.  24/04/13.                                                 
      *****************************************************************         
      * OBJECTIVE 1) MONTHLY REPORT FOR CASES WITH MONTHLY INSTALMENT           
      *              (MI) ON HOLD DUE TO ZERO OR NEGATIVE LOAN BAL              
      *----------------------------------------------------------------         
      * REF NO   DATE    BY   AMENDMENTS/ENHANCEMENTS                           
      * -------- ----    ---  -----------------------                           
      * BP134707 130424  IMC1 NEW PGM                                           
      * BP134707 130712  IMC1 ADD NEW SUPPRESSION CONDITIONS                    
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13FD00  ASSIGN TO BP13FD00.                                 
                                                                                
           SELECT BP13FD93  ASSIGN TO BP13FD93.                                 
                                                                                
           SELECT BP13LCA6  ASSIGN TO BP13LCA6.                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13FD00                   BLOCK CONTAINS 0 RECORDS                  
                                      RECORD CONTAINS 1600 CHARACTERS           
                                      LABEL RECORD IS STANDARD                  
                                      RECORDING MODE IS F.                      
       COPY BP13FD00.                                                           
                                                                                
       FD  BP13FD93                   BLOCK CONTAINS 0 RECORDS                  
                                      RECORD CONTAINS 50 CHARACTERS             
                                      LABEL RECORD IS STANDARD                  
                                      RECORDING MODE IS F.                      
       COPY BP13FD93.                                                           
                                                                                
       FD  BP13LCA6                   BLOCK CONTAINS 0 RECORDS                  
                                      RECORD CONTAINS 133 CHARACTERS            
                                      LABEL RECORD IS STANDARD                  
                                      RECORDING MODE IS F.                      
       01  BP13LCA6-REC               PIC X(133).                               
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-STATUS.                                                           
           05 KD00-STATUS              PIC 99       VALUE ZEROS.                
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-CURRENT-DATE             PIC X(8).                             
           05 WS-CURRENT-TIME             PIC 9(08) VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-CNT-FD00-RD           PIC 9(6) COMP VALUE 0.                   
           05 WS-CNT-FD93-WRT          PIC 9(6) COMP VALUE 0.                   
           05 WS-CNT-FD93-SNO          PIC 9(4) COMP VALUE 0.                   
                                                                                
       01  WS-SWITCH.                                                           
           05 WS-FD00-END              PIC X(1) VALUE 'N'.                      
                                                                                
      ***************************************************************           
      *    WS-REPORT VARIBLE                                        *           
      ***************************************************************           
                                                                                
       01  LCA6-MAIN-HD.                                                        
           05  FILLER              PIC X(08)  VALUE 'BP13LCA6'.                 
           05  FILLER              PIC X(04)  VALUE SPACES.                     
           05  FILLER              PIC X(07)  VALUE 'HDBCAT3'.                  
           05  FILLER              PIC X(25)  VALUE SPACES.                     
           05  FILLER              PIC X(20) VALUE                              
                                       'S Y S T E M   O F   '.                  
           05  FILLER              PIC X(19) VALUE                              
                                       'C O M M I T M E N T'.                   
           05  FILLER              PIC X(15)  VALUE SPACES.                     
           05  FILLER              PIC X(07)  VALUE 'DATE : '.                  
           05  RPT-HEAD-DATE-YYYY  PIC X(4)   VALUE SPACES.                     
           05  FILLER              PIC X(01)  VALUE '-'.                        
           05  RPT-HEAD-DATE-MM    PIC X(2)   VALUE SPACES.                     
           05  FILLER              PIC X(01)  VALUE '-'.                        
           05  RPT-HEAD-DATE-DD    PIC X(2)   VALUE SPACES.                     
           05  FILLER              PIC X(04)  VALUE SPACES.                     
                                                                                
       01  LCA6-PRINT-REPORT-HD.                                                
           05  FILLER              PIC X(40)         VALUE SPACES.              
           05  FILLER              PIC X(50)         VALUE                      
                      'SUPRESSION OF CPF MONTHLY INSTALMENT'.                   
                                                                                
       01  LCA6-HEADER.                                                         
           05 FILLER                  PIC X(4)     VALUE SPACES.                
           05 FILLER                  PIC X(4)     VALUE 'S/NO'.                
           05 FILLER                  PIC X(5)     VALUE SPACES.                
           05 FILLER                  PIC X(8)     VALUE 'REGN NUM'.            
           05 FILLER                  PIC X(5)     VALUE SPACES.                
           05 FILLER                  PIC X(11)    VALUE 'SCH ACC'.             
           05 FILLER                  PIC X(5)     VALUE SPACES.                
           05 FILLER                  PIC X(12)    VALUE 'AMT LOAN BAL'.        
                                                                                
       01  LCA6-REPORT.                                                         
           05 FILLER                   PIC X(4)     VALUE SPACES.               
           05 LCA6-NUM-SNO             PIC 9(4)     VALUE ZEROES.               
           05 FILLER                   PIC X(5)     VALUE SPACES.               
           05 LCA6-NUM-REGN            PIC X(8)     VALUE SPACES.               
           05 FILLER                   PIC X(5)     VALUE SPACES.               
           05 LCA6-NUM-SCH-ACCNT       PIC X(11)    VALUE SPACES.               
           05 FILLER                   PIC X(5)     VALUE SPACES.               
           05 LCA6-AMT-LOAN-BAL        PIC -9(7).99 VALUE ZEROES.               
                                                                                
       01  LCA6-SPACES.                                                         
           05 FILLER                   PIC X(132)   VALUE SPACES.               
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-START-UP       THRU 1000-EXIT.                          
           PERFORM 3000-READ-BP13FD00  THRU 3000-EXIT.                          
           PERFORM 2000-MAIN-PROCESS   THRU 2000-EXIT                           
             UNTIL WS-FD00-END = 'Y'                                            
           PERFORM 9999-WRAP-UP        THRU 9999-EXIT.                          
                                                                                
                                                                                
      ******************************************************************        
       1000-START-UP.                                                           
      ******************************************************************        
                                                                                
            OPEN INPUT BP13FD00                                                 
                OUTPUT BP13FD93                                                 
                       BP13LCA6.                                                
                                                                                
           INITIALIZE BP13FD93-REC                                              
                      BP13LCA6-REC.                                             
                                                                                
           ACCEPT WS-CURRENT-DATE         FROM DATE YYYYMMDD.                   
           ACCEPT WS-CURRENT-TIME         FROM TIME.                            
                                                                                
           MOVE WS-CURRENT-DATE(1:4)      TO RPT-HEAD-DATE-YYYY.                
           MOVE WS-CURRENT-DATE(5:2)      TO RPT-HEAD-DATE-MM.                  
           MOVE WS-CURRENT-DATE(7:2)      TO RPT-HEAD-DATE-DD.                  
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-MAIN-PROCESS.                                                       
      ******************************************************************        
           IF FD00-AMT-LOAN-BAL NOT NUMERIC                                     
              MOVE ZEROES       TO FD00-AMT-LOAN-BAL                            
           END-IF                                                               
                                                                                
           IF FD00-DTE-INSTAL-EFF NOT NUMERIC                                   
              MOVE ZEROES       TO FD00-DTE-INSTAL-EFF                          
           END-IF                                                               
                                                                                
           IF FD00-DTE-REFINANCE NOT NUMERIC                                    
              MOVE ZEROES       TO FD00-DTE-REFINANCE                           
           END-IF                                                               
                                                                                
           IF FD00-DTE-LOAN-COMM NOT NUMERIC                                    
              MOVE ZEROES       TO FD00-DTE-LOAN-COMM                           
           END-IF                                                               
                                                                                
           IF (FD00-DTE-REFINANCE  NOT = ZEROES) OR                             
              (FD00-DTE-LOAN-COMM      = ZEROES) OR                             
              (FD00-DTE-INSTAL-EFF     = ZEROES) OR                             
              (FD00-AMT-INSTAL-CPF1    = ZEROES AND                             
               FD00-AMT-INSTAL-CPF2    = ZEROES AND                             
               FD00-AMT-INSTAL-CPF3    = ZEROES AND                             
               FD00-AMT-INSTAL-CPF4    = ZEROES)                                
              CONTINUE                                                          
           ELSE                                                                 
              IF FD00-AMT-LOAN-BAL    <= ZEROES                                 
                 PERFORM 5000-WRITE-BP13FD93   THRU 5000-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-BP13FD00       THRU 3000-EXIT.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-READ-BP13FD00.                                                      
      ******************************************************************        
                                                                                
           READ BP13FD00 AT END                                                 
                MOVE 'Y'         TO WS-FD00-END                                 
                GO TO 3000-EXIT.                                                
           ADD 1 TO WS-CNT-FD00-RD.                                             
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-WRITE-BP13FD93.                                                     
      ******************************************************************        
                                                                                
           MOVE FD00-NUM-REGN       TO FD93-NUM-REGN                            
                                       LCA6-NUM-REGN                            
           MOVE FD00-NUM-SCH-ACCNT  TO FD93-NUM-SCH-ACC                         
                                       LCA6-NUM-SCH-ACCNT                       
           MOVE FD00-AMT-LOAN-BAL   TO LCA6-AMT-LOAN-BAL                        
           MOVE WS-CURRENT-DATE     TO FD93-DTE-UPDATE                          
           MOVE WS-CURRENT-TIME     TO FD93-TME-UPDATE                          
                                                                                
           WRITE BP13FD93-REC.                                                  
                                                                                
           IF WS-CNT-FD93-SNO = ZEROES                                          
              WRITE BP13LCA6-REC FROM LCA6-MAIN-HD                              
              WRITE BP13LCA6-REC FROM LCA6-PRINT-REPORT-HD                      
              WRITE BP13LCA6-REC FROM LCA6-SPACES                               
              WRITE BP13LCA6-REC FROM LCA6-HEADER                               
              WRITE BP13LCA6-REC FROM LCA6-SPACES                               
           END-IF.                                                              
                                                                                
           ADD   1                  TO WS-CNT-FD93-SNO.                         
           MOVE WS-CNT-FD93-SNO     TO LCA6-NUM-SNO.                            
                                                                                
           WRITE BP13LCA6-REC FROM LCA6-REPORT.                                 
                                                                                
           INITIALIZE BP13FD93-REC                                              
                      BP13LCA6-REC LCA6-REPORT.                                 
                                                                                
           ADD 1    TO WS-CNT-FD93-WRT.                                         
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-WRAP-UP.                                                            
      ******************************************************************        
           DISPLAY  SPACES.                                                     
           DISPLAY '******* CONTROL   COUNTS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13CCA6'                                      
           DISPLAY  SPACES.                                                     
           DISPLAY 'NO. OF BP13FD00 RECS READ    : ' WS-CNT-FD00-RD.            
           DISPLAY 'NO. OF BP13FD93 RECS READ    : ' WS-CNT-FD93-WRT.           
                                                                                
           CLOSE    BP13FD00                                                    
                    BP13FD93                                                    
                    BP13LCA6.                                                   
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
                                                                                
