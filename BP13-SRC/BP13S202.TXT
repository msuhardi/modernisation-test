  /*---------------------------------------------------------*/                 
  /* THIS PROGRAM PRINTS THE LIST OF UNIT NO'S IN DIFFERENT  */                 
  /* BLOCKS AND STREETS FOR A GIVEN ZONE OR NEW TOWN.        */                 
  /* THE PROGRAM READS A SINGLE RECORD OF F205 AND MATCHES   */                 
  /* THE ZONE AND FLAT TYPE WITH BM06F800 FILE AND PRINTS    */                 
  /* THE REPORT FOR EACH ZONE,FLAT TYPE,STREET AND BLOCK NO. */                 
  /*                                                         */                 
  /*  CHG REQ    DATE    OIC  DESCRIPTION                    */                 
  /* --------- --------  ---  -----------                    */                 
  /* BP130418  14/07/98  LMS  TO CHANGE BM06K210 TO BM06K100.*/                 
  /* BP130641  06/03/99  LMS  TO CATER FOR SA.               */                 
  /* BP130626  19/06/99  JMS  TO GET REVISED ECD FROM        */                 
  /*                          BM06K238.                      */                 
  /* BP130768  07/12/99  GBR  TO PRINT THE LIST OF UNIT NO'S */                 
  /*                          FOR MATURE & NON-MATURE ESTATES*/                 
  /*---------------------------------------------------------*/                 
                                                                                
  PROC FORMAT;                                                                  
  VALUE $MONFMT "01"="JAN"                                                      
                "02"="FEB"                                                      
                "03"="MAR"                                                      
                "04"="APR"                                                      
                "05"="MAY"                                                      
                "06"="JUN"                                                      
                "07"="JUL"                                                      
                "08"="AUG"                                                      
                "09"="SEP"                                                      
                "10"="OCT"                                                      
                "11"="NOV"                                                      
                "12"="DEC";                                                     
                                                                                
  VALUE $RMFMT  "1 "="1-ROOM"                                                   
                "1A"="1RM-SA"                                                   
                "2 "="2-ROOM"                                                   
                "2A"="2RM-SA"                                                   
                "3 "="3-ROOM"                                                   
                "4 "="4-ROOM"                                                   
                "4D"="TYPE-A"                                                   
                "5 "="5-ROOM"                                                   
                "5D"="TYPE-B"                                                   
                "E "="EXEC  "                                                   
                "ED"="TYPE-C";                                                  
                                                                                
    RUN;                                                                        
                                                                                
  DATA BM06F800;                                                                
    INFILE BM06F800 END=EOF;                                                    
      INPUT    @1   SCHACC   $CHAR11. @12 ESTNAME   $CHAR3.                     
               @1   SCH      $CHAR4.                                            
               @15  NEIGH    $CHAR4.  @19  CONTNO   $CHAR4.                     
               @23  BLOCK    $CHAR5.  @510 STREET   $CHAR20.                    
               @501 LEVEL    $CHAR2.  @503 MAINNO   $CHAR4.                     
               @507 SUBNO    $CHAR3.  @544 TYACTUSE $CHAR2.                     
               @540 MODESG   $CHAR2.  @112 SPRICE 8.                            
               @275 AVAILDTE $CHAR8.                                            
               @291 ALLODATE $CHAR8.  @299 ACCDATE  $CHAR8.                     
               @323 KEYDATE  $CHAR8.  @546 NTZONE   $CHAR3.                     
               @558 CDEFLAT  $CHAR1.  @559 ZONE     $CHAR2.;                    
                                                                                
      IF _N_ = 1 THEN                                                           
          DO;                                                                   
               INFILE BP13F205 END=EOF;                                         
               INPUT  @1  NTZ       $CHAR3.                                     
                      @4  FLATTYP   $CHAR2.                                     
                      @6  ALLDATE   $CHAR8.                                     
                     @16  NEWALLO   $CHAR8.                                     
                     @30  ZONESW    $CHAR1.;                                    
                                                                                
           RETAIN  NTZ  FLATTYP ALLDATE NEWALLO ZONESW;                         
                                                                                
          END;                                                                  
                                                                                
      LENGTH K060KEY $5.;                                                       
      LENGTH EST     $7.;                                                       
      LENGTH NTTIT   $27.;                                                      
      LENGTH ZONETIT $27.;                                                      
                                                                                
      CDE = '02';                                                               
      K060KEY = CDE||NTZONE;                                                    
                                                                                
      INFILE BP13K060 VSAM KEY=K060KEY;                                         
      INPUT @;                                                                  
      RC=_IORC_; FBC=_FDBK_;                                                    
      IF RC=0 THEN                                                              
         DO;                                                                    
            INPUT @6   K060DSC $CHAR20.;                                        
                                                                                
            A = LENGTH(K060DSC);                                                
            EST   = ' ESTATE';                                                  
            NTTIT = SUBSTR(K060DSC,1,A)||EST;                                   
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
            _ERROR_ = 0;                                                        
             NTTIT  = ' ';                                                      
         END;                                                                   
                                                                                
      IF ZONE = '3R' OR ZONE = 'RE' OR ZONE = 'ME' OR ZONE = 'ZN' THEN          
         DO;                                                                    
           ZONETIT = ' ';                                                       
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
            LENGTH K060KEY $5.;                                                 
            CDE = '45';                                                         
            K060KEY = CDE||ZONE;                                                
                                                                                
            INFILE BP13K060 VSAM KEY=K060KEY;                                   
            INPUT @;                                                            
            RC=_IORC_; FBC=_FDBK_;                                              
            IF RC=0 THEN                                                        
               DO;                                                              
                  INPUT @6  K060DESC $CHAR20.;                                  
                  ZONETIT = K060DESC;                                           
               END;                                                             
            ELSE                                                                
               DO;                                                              
                  _ERROR_ = 0;                                                  
                  ZONETIT = ' ';                                                
               END;                                                             
         END;                                                                   
                                                                                
      IF ZONESW = 'T' THEN                                                      
         DO;                                                                    
           ZONETIT = NTTIT;                                                     
           NTTIT   = ' ';                                                       
         END;                                                                   
                                                                                
      LENGTH K270KEY $24.;                                                      
      K270KEY = ALLODATE||ESTNAME||NEIGH||CONTNO||BLOCK;                        
                                                                                
      INFILE BP13K270 VSAM KEY=K270KEY;                                         
      INPUT @;                                                                  
      RC=_IORC_; FBC=_FDBK_;                                                    
      IF RC=0 THEN                                                              
         DO;                                                                    
            INPUT @135 MAX_C1_1 3.                                              
                  @138 MAX_C2_1 3.                                              
                  @141 MAX_C3_1 3.                                              
                  @147 MAX_C1_2 3.                                              
                  @150 MAX_C2_2 3.                                              
                  @153 MAX_C3_2 3.                                              
                  @159 MAX_C1_3 3.                                              
                  @162 MAX_C2_3 3.                                              
                  @165 MAX_C3_3 3.                                              
                  @171 MAX_C1_4 3.                                              
                  @174 MAX_C2_4 3.                                              
                  @177 MAX_C3_4 3.                                              
                  @183 MAX_C1_5 3.                                              
                  @186 MAX_C2_5 3.                                              
                  @189 MAX_C3_5 3.                                              
                  @195 MAX_C1_6 3.                                              
                  @198 MAX_C2_6 3.                                              
                  @201 MAX_C3_6 3.                                              
                  @207 MAX_C1_7 3.                                              
                  @210 MAX_C2_7 3.                                              
                  @213 MAX_C3_7 3.                                              
                  @219 MAX_C1_8 3.                                              
                  @222 MAX_C2_8 3.                                              
                  @225 MAX_C3_8 3.                                              
                  @231 MAX_C1_9 3.                                              
                  @234 MAX_C2_9 3.                                              
                  @237 MAX_C3_9 3.;                                             
                                                                                
            IF TYACTUSE = '01' THEN                                             
               DO;                                                              
                 MAXCAT1 = MAX_C1_1;                                            
                 MAXCAT2 = MAX_C2_1;                                            
                 MAXCAT3 = MAX_C3_1;                                            
               END;                                                             
                                                                                
            IF TYACTUSE = '02' THEN                                             
               DO;                                                              
                 MAXCAT1 = MAX_C1_2;                                            
                 MAXCAT2 = MAX_C2_2;                                            
                 MAXCAT3 = MAX_C3_2;                                            
               END;                                                             
                                                                                
            IF TYACTUSE = '03' THEN                                             
               DO;                                                              
                 MAXCAT1 = MAX_C1_3;                                            
                 MAXCAT2 = MAX_C2_3;                                            
                 MAXCAT3 = MAX_C3_3;                                            
               END;                                                             
                                                                                
            IF TYACTUSE = '04' THEN                                             
               DO;                                                              
                 MAXCAT1 = MAX_C1_4 + MAX_C1_5;                                 
                 MAXCAT2 = MAX_C2_4 + MAX_C2_5;                                 
                 MAXCAT3 = MAX_C3_4 + MAX_C3_5;                                 
               END;                                                             
                                                                                
            IF TYACTUSE = '05' THEN                                             
               DO;                                                              
                 MAXCAT1 = MAX_C1_6;                                            
                 MAXCAT2 = MAX_C2_6;                                            
                 MAXCAT3 = MAX_C3_6;                                            
               END;                                                             
                                                                                
            IF TYACTUSE = '06' THEN                                             
               DO;                                                              
                 MAXCAT1 = MAX_C1_7;                                            
                 MAXCAT2 = MAX_C2_7;                                            
                 MAXCAT3 = MAX_C3_7;                                            
               END;                                                             
                                                                                
            IF TYACTUSE = '08' THEN                                             
               DO;                                                              
                 MAXCAT1 = MAX_C1_9;                                            
                 MAXCAT2 = MAX_C2_9;                                            
                 MAXCAT3 = MAX_C3_9;                                            
               END;                                                             
                                                                                
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
            _ERROR_ = 0;                                                        
            MAXCAT1 = 0;                                                        
            MAXCAT2 = 0;                                                        
            MAXCAT3 = 0;                                                        
         END;                                                                   
                                                                                
      IF CDEFLAT = 'N' THEN                                                     
         FLATCODE = '1';                                                        
                                                                                
      IF CDEFLAT = 'B' THEN                                                     
         FLATCODE = '2';                                                        
                                                                                
      IF CDEFLAT = 'R' THEN                                                     
         FLATCODE = '3';                                                        
                                                                                
      IF CDEFLAT = ' ' THEN                                                     
         FLATCODE = '4';                                                        
                                                                                
      LENGTH ROOM $9.;                                                          
             ROOM = TYACTUSE;                                                   
                                                                                
      IF TYACTUSE = '01' THEN                                                   
         DO;                                                                    
           IF MODESG ='80' THEN                                                 
              FLATTYPE = '1A';                                                  
           ELSE                                                                 
              FLATTYPE = '1 ';                                                  
         END;                                                                   
      IF TYACTUSE = '02' THEN                                                   
         DO;                                                                    
           IF MODESG ='80' THEN                                                 
              FLATTYPE = '2A';                                                  
           ELSE                                                                 
              FLATTYPE = '2 ';                                                  
         END;                                                                   
      IF TYACTUSE = '03' THEN FLATTYPE = '3 ';                                  
      IF TYACTUSE = '04' THEN FLATTYPE = '4 ';                                  
      IF TYACTUSE = '05' THEN FLATTYPE = '5 ';                                  
      IF TYACTUSE = '06' THEN FLATTYPE = 'E ';                                  
      IF TYACTUSE = '08' THEN FLATTYPE = 'MG';                                  
                                                                                
                                                                                
      IF (ACCDATE = '00000000' OR ACCDATE = '        ') AND                     
         (KEYDATE = '00000000' OR KEYDATE = '        ') ;                       
                                                                                
      LENGTH K100KEY $16.;                                                      
      K100KEY = ESTNAME||NEIGH||CONTNO||BLOCK;                                  
                                                                                
      INFILE BM06K100 VSAM KEY=K100KEY;                                         
      INPUT @;                                                                  
      RC=_IORC_; FBC=_FDBK_;                                                    
      IF RC=0 THEN                                                              
         DO;                                                                    
            INPUT @409 K_READY $8.                                              
                  @366 CDEBLK  $5.                                              
                  @441 DTE_OCS $CHAR8.                                          
                  @441 OCS_YY  $CHAR4.                                          
                  @445 OCS_MM  $CHAR2.                                          
                  @447 OCS_DD  $CHAR2.;                                         
            OCS_MTH=PUT(OCS_MM,$MONFMT.);                                       
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
            _ERROR_  = 0;                                                       
            CDEBLK  = ' ';                                                      
            K_READY = '00000000';                                               
            DTE_OCS = '00000000';                                               
         END;                                                                   
                                                                                
      LENGTH K500KEY $5.;                                                       
      K500KEY = CDEBLK;                                                         
                                                                                
     INFILE BB14K500 VSAM KEY=K500KEY;                                          
     INPUT @;                                                                   
     RC=_IORC_; FBC=_FDBK_;                                                     
     IF RC=0 THEN                                                               
        DO;                                                                     
           INPUT @48  EST_DTE $CHAR8.                                           
                 @48  EST_YY  $CHAR4.                                           
                 @52  EST_MM  $CHAR2.                                           
                 @54  EST_DD  $CHAR2.                                           
                 @162 LSE_DTE $CHAR8.                                           
                 @162 LSE_YY  $CHAR4.                                           
                 @166 LSE_MM  $CHAR2.                                           
                 @168 LSE_DD  $CHAR2.;                                          
                                                                                
           EST_MTH=PUT(EST_MM,$MONFMT.);                                        
           LSE_MTH=PUT(LSE_MM,$MONFMT.);                                        
                                                                                
        END;                                                                    
     ELSE                                                                       
        DO;                                                                     
           _ERROR_=0;                                                           
           EST_DTE=' ';                                                         
           LSE_DTE=0;                                                           
        END;                                                                    
                                                                                
     IF K_READY = '00000000' THEN                                               
        DO;                                                                     
           LENGTH K238KEY $11.;                                                 
           K238KEY = SCHACC;                                                    
           PUT 'LOOK AT BM06K238 : ' K238KEY;                                   
           INFILE BM06K238 VSAM KEY=K238KEY;                                    
           INPUT @;                                                             
           RC=_IORC_; FBC=_FDBK_;                                               
           IF RC=0 THEN                                                         
              DO;                                                               
                INPUT @28  ESTDTE  $CHAR8.                                      
                      @28  ESTYY  $CHAR4.                                       
                      @32  ESTMM  $CHAR2.                                       
                      @34  ESTDD  $CHAR2.;                                      
                PUT 'DATE ' ESTDTE;                                             
                IF ESTDTE^='00000000' AND ESTDTE^='        ' THEN               
                   DO;                                                          
                     EST_DTE=ESTDTE;                                            
                     EST_DD=ESTDD;                                              
                     EST_MM=ESTMM;                                              
                     EST_YY=ESTYY;                                              
                     EST_MTH=PUT(ESTMM,$MONFMT.);                               
                   END;                                                         
              END;                                                              
            ELSE                                                                
              DO;                                                               
                 _ERROR_=0;                                                     
                 FILE LOG;                                                      
                 PUT 'RECORD NOT FND ' K238KEY;                                 
              END;                                                              
         END;                                                                   
                                                                                
    RUN;                                                                        
                                                                                
  PROC SORT DATA=BM06F800;                                                      
     BY NTZONE FLATTYPE;                                                        
     RUN;                                                                       
                                                                                
  DATA BM6F800;                                                                 
       SET BM06F800;                                                            
                                                                                
       IF ZONESW = 'T' THEN                                                     
          DO;                                                                   
             IF (NTZONE = NTZ) AND (FLATTYPE = FLATTYP);                        
          END;                                                                  
       ELSE                                                                     
       IF ZONESW = 'Z' AND ZONE = '3R' THEN                                     
          DO;                                                                   
             IF (ZONE = NTZ) AND (FLATTYPE = '3 ' OR '4 ');                     
          END;                                                                  
       ELSE                                                                     
          DO;                                                                   
             IF (ZONE = NTZ )  AND (FLATTYPE = FLATTYP);                        
          END;                                                                  
                                                                                
     RUN;                                                                       
                                                                                
  PROC SORT DATA=BM6F800;                                                       
     BY FLATTYPE NTZONE NTTIT BLOCK STREET FLATCODE MAINNO SUBNO LEVEL;         
     RUN;                                                                       
                                                                                
  DATA _NULLS_;                                                                 
     SET BM6F800 END=EOF;                                                       
        BY FLATTYPE NTZONE NTTIT BLOCK STREET FLATCODE MAINNO SUBNO             
           LEVEL;                                                               
                                                                                
     LENGTH REMARKS $60.;                                                       
                                                                                
     IF K_READY ='00000000' OR                                                  
        K_READY =' ' THEN                                                       
        DO;                                                                     
           IF EST_DTE=' ' THEN                                                  
              REMARKS=' ';                                                      
           ELSE                                                                 
              REMARKS='(ESTIMATED BUILDING COMPLETION DATE: '||EST_DD||         
                      ' '||EST_MTH||' '||EST_YY||')';                           
        END;                                                                    
     ELSE                                                                       
        DO;                                                                     
          IF LSE_DTE=. THEN                                                     
             REMARKS=' ';                                                       
          ELSE                                                                  
             DO;                                                                
              CUR_YEAR = (YEAR(TODAY()));                                       
              DATE = PUT(TODAY(),DDMMYY8.);                                     
              T_DATE = PUT(TODAY(),MMDDYY4.);                                   
              CUR_DTE = CUR_YEAR||T_DATE;                                       
              DIFF = LSE_DTE - CUR_DTE;                                         
              IF DIFF < 31 THEN                                                 
                   REMARKS='(KEY AVAILABLE, LEASE COMMENCEMENT DATE: '||        
                             LSE_DD||' '||LSE_MTH||' '||LSE_YY||')';            
                ELSE                                                            
                   REMARKS='KEY AVAILABLE';                                     
             END;                                                               
        END;                                                                    
                                                                                
     LENGTH REMARK1 $60.;                                                       
                                                                                
     IF DTE_OCS ^='00000000' AND                                                
        DTE_OCS ^=' ' THEN                                                      
        DO;                                                                     
           CUR_YR = (YEAR(TODAY()));                                            
           DATE = PUT(TODAY(),DDMMYY8.);                                        
           T_DTE = PUT(TODAY(),MMDDYY4.);                                       
           CUR_DTE1 = CUR_YR||T_DTE;                                            
           DIFF1 = DTE_OCS - CUR_DTE1;                                          
           IF DIFF1 > 0 THEN                                                    
              REMARK1='WITH OCS OPTION (EXPIRY DATE : '||                       
                             OCS_DD||' '||OCS_MTH||' '||OCS_YY||')';            
           ELSE                                                                 
              REMARK1='NO OCS OPTION';                                          
        END;                                                                    
     ELSE                                                                       
     IF (TYACTUSE = '01' OR TYACTUSE = '02') AND MODESG = '80' THEN             
        DO;                                                                     
           REMARK1='';                                                          
        END;                                                                    
     ELSE                                                                       
        DO;                                                                     
           REMARK1='NO OCS OPTION';                                             
        END;                                                                    
                                                                                
     IF SPRICE^=. THEN                                                          
        PRICE=PUT(SPRICE,DOLLAR8.);                                             
                                                                                
     LENGTH HSENO $9.;                                                          
     LENGTH PRPRICE $14.;                                                       
       HSENO='#'||LEVEL||'-'||MAINNO||SUBNO;                                    
       PRPRICE=PRICE;                                                           
                                                                                
     FILE BM06C224 N=PS LINESLEFT=L PS=61 LS=80 HEADER=HH NOTITLES;             
     IF FIRST.NTZONE | FIRST.FLATTYPE THEN                                      
        DO;                                                                     
           PUT _PAGE_;                                                          
           CNT = 20;                                                            
           R1  = 2;                                                             
        END;                                                                    
     ELSE                                                                       
        DO;                                                                     
           IF FIRST.STREET | FIRST.BLOCK | FIRST.FLATTYPE THEN                  
             DO;                                                                
                IF L > 12 THEN                                                  
                   DO;                                                          
                      FORMAT FLATTYPE  RMFMT.;                                  
                      CNT = 20;                                                 
                       PUT @1 //                                                
                           @1 //                                                
                          @1   'APARTMENT BLOCK '  @17 BLOCK                    
                          @23  ': '                @25 STREET/                  
                          @1   REMARK1 $CHAR60. /                               
                          @1   REMARKS $CHAR60. //                              
                          @1   'MAXIMUM UNITS ALLOWED FOR :'                    
                          @28  'MALAY - '          @35 MAXCAT1                  
                          @40  'CHINESE - '        @50 MAXCAT2                  
                          @55  'INDIANS/OTHERS - ' @72 MAXCAT3;                 
                   END;                                                         
                ELSE                                                            
                   DO;                                                          
                       PUT _PAGE_;                                              
                       CNT = 20;                                                
                       R1  = 2;                                                 
                   END;                                                         
             END;                                                               
        END;                                                                    
                                                                                
     IF FIRST.FLATCODE THEN                                                     
        DO;                                                                     
          PUT /;                                                                
          IF FLATCODE = '1' THEN                                                
             PUT @2 'NEW FLAT(S)'/;                                             
          ELSE                                                                  
          IF FLATCODE = '2' THEN                                                
             PUT @2 'BALANCE FLAT(S)'/;                                         
          ELSE                                                                  
          IF FLATCODE = '3' THEN                                                
             PUT @2 'REPURCHASED FLAT(S)'/;                                     
          R1=2;                                                                 
          CNT=0;                                                                
        END;                                                                    
                                                                                
     IF CNT = 20 THEN                                                           
        DO;                                                                     
           CNT = 0;                                                             
           R1=2;                                                                
           PUT /;                                                               
        END;                                                                    
                                                                                
     PUT @R1 HSENO $CHAR9. PRPRICE @;                                           
     CNT+1;                                                                     
     R1+20;                                                                     
     IF R1>79  THEN                                                             
        DO;                                                                     
           PUT;                                                                 
           R1=2;                                                                
        END;                                                                    
                                                                                
      RETURN;                                                                   
                                                                                
   HH:                                                                          
      FORMAT FLATTYPE  RMFMT.;                                                  
      PAGE + 1;                                                                 
      PUT @28  'HOUSING & DEVELOPMENT BOARD'                                    
          @72  'PAGE: '            @78 PAGE;                                    
      IF TYACTUSE = '01' AND MODESG = '80' THEN                                 
         DO;                                                                    
            PUT @20  'SALE OF 35SQM STUDIO APARTMENT'                           
                @51  'IN'          @54 ZONETIT//;                               
         END;                                                                   
      ELSE                                                                      
      IF TYACTUSE = '02' AND MODESG = '80' THEN                                 
         DO;                                                                    
            PUT @20  'SALE OF 45SQM STUDIO APARTMENT'                           
                @51  'IN'          @54 ZONETIT//;                               
         END;                                                                   
      ELSE                                                                      
      IF ZONE = '3R' AND TYACTUSE = '04' THEN                                   
         DO;                                                                    
            PUT @28  'SALE OF'           @36 FLATTYPE                           
                @43  'BUDGET FLATS '//;                                         
         END;                                                                   
      ELSE                                                                      
      IF ZONE = '3R' OR ZONE = 'RE' THEN                                        
         DO;                                                                    
            PUT @28  'SALE OF'           @36 FLATTYPE                           
                @43  'FLATS '//;                                                
         END;                                                                   
      ELSE                                                                      
      IF ZONE = 'ZN' THEN                                                       
         DO;                                                                    
            PUT @28  'SALE OF'           @36 FLATTYPE                           
                @43  'FLATS IN NON-MATURE ESTATES'//;                           
         END;                                                                   
      ELSE                                                                      
      IF ZONE = 'ME' THEN                                                       
         DO;                                                                    
            PUT @28  'SALE OF'           @36 FLATTYPE                           
                @43  'FLATS IN MATURE ESTATES'//;                               
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
            PUT @22  'SALE OF'           @30 FLATTYPE                           
                @37  'FLATS IN'          @46 ZONETIT//;                         
         END;                                                                   
      PUT @1    NTTIT/                                                          
          @1   'APARTMENT BLOCK '  @17 BLOCK                                    
          @23  ': '                @25 STREET/                                  
          @1    REMARK1 $CHAR60. /                                              
          @1    REMARKS $CHAR60. //                                             
          @1   'MAXIMUM UNITS ALLOWED FOR :'                                    
          @28  'MALAY - '          @35 MAXCAT1                                  
          @40  'CHINESE - '        @50 MAXCAT2                                  
          @55  'INDIANS/OTHERS - ' @72 MAXCAT3;                                 
                                                                                
      RETURN;                                                                   
/*                                                                              
