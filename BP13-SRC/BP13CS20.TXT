       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CS20.                                                 
       AUTHOR.        KARL MAGALONA CABUG.                                      
      *DATE-WRITTEN.  28/03/2016.                                               
      *************************************************************             
      *      SYSTEM OF COMMITMENT (BP13)                          *             
      *===========================================================*             
      *                                                           *             
      *  OBJECTIVES: 1. TO SEND OUT EMAIL FOR NOTIFICATION        *             
      *              2. PUT MQ MESSAGE INTO QUEUE WHICH CONTAINS  *             
      *                 CUSTOMER'S DETAILS TO BE SENT BY JAVA     *             
      *                 INTERFACE.                                *             
      *                                                           *             
      *             NUM-APPT-TYPE(IDENTIFIER):                    *             
      *                 GN- ELIGIBLE FOR GRANTS                   *             
      *                 EN- INCOME EXCEEDED                       *             
      *                 DN- DID NOT SUBMIT THE FULL SET OF DOCS   *             
      *                 RN- REQUIRED MORE DOCUMENTS FOR FURTHER   *             
      *                     ASSESSMEN                             *             
      *                                                           *             
      *             NUM-APPT-TYPE - CHANGE 2ND BYTE TO 'Y' ON     *             
      *                             SUCCESSFUL SEND MAIL, WRITE   *             
      *                             TO BP13K848 & UPDATE BP13K800.*             
      *                                                           *             
      *  PROGRAM LOGIC:                                           *             
      *          PROCESS INPUT FILES                              *             
      *          MQCONNECT TO QUEUE MANAGER                       *             
      *          MQOPEN QUEUE                                     *             
      *          MQPUT                                            *             
      *          MQCLOSE QUEUE                                    *             
      *          MQDISCONNECT FROM QUEUE MANAGER                  *             
      *                                                           *             
      *===========================================================*             
      * MODIFICATIONS :                                           *             
      *                                                           *             
      * CHG REQ#    DATE    AUTHOR REMARKS                        *             
      * -------- ---------- ------ ------------------------------ *             
      * BP136668 28/03/2017 KAM4   NEW PROGRAM                    *             
      * BP137063 16/11/2017 KAM4   CATER FOR NUM-FLAT-TYPE AND    *             
      *                                      NUM-ELDERLY-TAG.     *             
      *************************************************************             
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       SPECIAL-NAMES.                                                           
           SYSIN IS PARM-INPUT.                                                 
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F80A ASSIGN       TO BP13F80A.                            
                                                                                
           SELECT BP13K857 ASSIGN       TO BP13K857                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K857-NUM-REGN                        
                           FILE STATUS  IS WS-K857-STATUS.                      
                                                                                
           SELECT BP13K800 ASSIGN       TO BP13K800                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K800-NUM-REGN                        
                           FILE STATUS  IS WS-K800-STATUS.                      
                                                                                
           SELECT BP13K848 ASSIGN       TO BP13K848                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K848-KEY-FLD                         
                           FILE STATUS  IS WS-K848-STATUS.                      
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F80A                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 200 CHARACTERS.                                      
       01  BP13F80A-RECORD.                                                     
           05 F80A-NUM-REGN               PIC X(08).                            
           05 F80A-NUM-APPT-TYPE          PIC X(02).                            
           05 FILLER                      PIC X(190).                           
                                                                                
       FD   BP13K857                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K857.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K848                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K848.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      *-------------------------------------------------------------            
      *    QUEUE MANAGER                                                        
      *    FOR PRODUCTION, QUEUE MANAGER IS 'MQP1'                              
      *    FOR TESTING,    QUEUE MANAGER IS 'MQT1'                              
      *-------------------------------------------------------------            
      *PROD QUEUE MANAGER                                                       
       01  WS-QM-NAME                    PIC X(48) VALUE 'MQP1'.                
      *TEST QUEUE MANAGER                                                       
      *01  WS-QM-NAME                    PIC X(48) VALUE 'MQT1'.                
       01  WS-BP13Q-NAME                 PIC X(48) VALUE SPACES.                
                                                                                
      *-------------------------------------------------------------            
      *    OUTPUT QUEUE NAME TO CRIMSONLOGIC                                    
      *    FOR PROD, QUEUE NAME IS BP13.WIS.MDB.REQUESTQ                        
      *    FOR TESTING, QUEUE NAME IS BP13S.WIS.MDB.REQUESTQ                    
      *-------------------------------------------------------------            
      *PROD QUEUE                                                               
       01  WS-QUEUE-NAME   PIC X(48) VALUE 'BP13.WIS.MDB.REQUESTQ'.             
      **TEST QUEUE                                                              
      *01  WS-QUEUE-NAME   PIC X(48) VALUE 'BP13S.WIS.MDB.REQUESTQ'.            
                                                                                
      *-------------------------------------------------------------            
      *    MESSAGE LENGTH                                                       
      *-------------------------------------------------------------            
      *01  WS-MESSAGE-LENGTH             PIC 9(06) VALUE  51612.                
       01  WS-MESSAGE-LENGTH             PIC 9(06) VALUE  54612.                
                                                                                
      *-------------------------------------------------------------            
      *    MESSAGE                                                              
      *-------------------------------------------------------------            
       01  WS-SEND-MESSAGE.                                                     
           05  WS-MSG-CDE                PIC X(03).                             
           05  WS-FILLER-01              PIC X(01).                             
           05  WS-MSG-APPT-TYP           PIC X(02).                             
           05  WS-FILLER-02              PIC X(01).                             
           05  WS-MSG-CTR                PIC 9(04).                             
           05  WS-FILLER-03              PIC X(01).                             
           05  WS-MSG OCCURS 600 TIMES.                                         
               10  WS-MSG-NUM-REGN       PIC X(08).                             
               10  WS-FILLER-04          PIC X(01).                             
               10  WS-MSG-EMAIL-TYP      PIC X(02).                             
               10  WS-FILLER-05          PIC X(01).                             
               10  WS-MSG-NUM-EMAIL      PIC X(50).                             
               10  WS-FILLER-06          PIC X(01).                             
               10  WS-MSG-AMT-PRE-AHG    PIC 9(05).                             
               10  WS-FILLER-07          PIC X(01).                             
               10  WS-MSG-AMT-PRE-SHG    PIC 9(05).                             
               10  WS-FILLER-08          PIC X(01).                             
               10  WS-MSG-DTE-BALLOT     PIC X(06).                             
               10  WS-FILLER-09          PIC X(01).                             
               10  WS-MSG-ALLO-CAT       PIC X(03).                             
               10  WS-FILLER-10          PIC X(01).                             
               10  WS-NUM-FLAT-TYPE      PIC X(02).                             
               10  WS-FILLER-11          PIC X(01).                             
               10  WS-NUM-ELDERLY-TAG    PIC X(01).                             
               10  WS-FILLER-12          PIC X(01).                             
                                                                                
      *-------------------------------------------------------------            
      *    MQI - TEMPORARY VARIABLES FOR ERROR CODES                            
      *-------------------------------------------------------------            
       01  BP13CS20-REC.                                                        
           05  CS20-REASON               PIC 9(4).                              
           05  CS20-COMPLETION-CODE      PIC 9(4).                              
           05  CS20-ERR-MSG              PIC X(66).                             
           05  FILLER                    PIC X(27).                             
                                                                                
      *-------------------------------------------------------------            
      *    MQI STRUCTURE DECLARATION                                            
      *-------------------------------------------------------------            
       01  MY-MQ-CONSTANTS.                                                     
           COPY CMQV.                                                           
                                                                                
       01  OBJECT-DESCRIPTOR.                                                   
           COPY CMQODV.                                                         
                                                                                
       01  MESSAGE-DESCRIPTOR.                                                  
           COPY CMQMDV.                                                         
                                                                                
      *-------------------------------------------------------------            
      *    PUT MESSAGE OPTIONS                                                  
      *-------------------------------------------------------------            
       01  PMOPTIONS.                                                           
           COPY CMQPMOV.                                                        
                                                                                
      *-------------------------------------------------------------            
      *    VARIABLE DESCRIPTION:                                                
      *    (1) HCONN                                                            
      *        - CONNECTION HANDLE - REPRESENTS THE CONNECTION TO               
      *          THE QUEUE MANAGER. VALUE IS RETURNED BY MQCONN                 
      *          AND SUBSEQUENTLY REFERENCED IN MQOPEN, MQPUT,                  
      *          MQCLOSE AND MQDISC                                             
      *                                                                         
      *    (2) Q-HANDLE                                                         
      *        - ACCESS THAT HAS BEEN ESTABLISHED  TO THE OBJECT                
      *          CONNECTION HANDLE - VALUE IS RETURNED BY MQOPEN                
      *          AND SUBSEQUENTLY REFERENCED IN MQPUT AND MQCLOSE               
      *                                                                         
      *    (3) OBJECT-DESCRIPTOR                                                
      *        - THIS IDENTIFIES THE OBJECT TO BE OPENED                        
      *                                                                         
      *    (4) OPTIONS                                                          
      *        WHEN USED IN MQOPEN                                              
      *        - OPTIONS THAT CONTROL THE ACTION OF MQOPEN                      
      *        - OPTIONS USED IN THIS PROGRAM:                                  
      *          (A) MQOO-OUTPUT - THE QUEUE IS OPENED FOR USE WITH             
      *                            SUBSEQUENT MQPUT CALL                        
      *          (B) MQOO-FAIL-IF-QUIESCING - FORCES MQCALL TO FAIL             
      *                            IF QUEUE MANAGER IS IN QUIESCING             
      *                            STATE (I.E. PREPARING TO SHUTDOWN)           
      *                                                                         
      *        WHEN USED IN MQCLOSE                                             
      *        - OPTIONS THAT CONTROL THE ACTION OF MQCLOSE                     
      *        - OPTIONS USED IN THIS PROGRAM:                                  
      *          (A) MQCO-NONE   - NO OPTIONAL CLOSE PROCESSING                 
      *                            REQUIRED                                     
      *                                                                         
      *    (5) MESSAGE-DESCRIPTOR                                               
      *        - THIS DESCRIBES THE ATTRIBUTES OF THE MESSAGE                   
      *          BEING SENT, AND RECEIVES INFO ABOUT THE MESSAGE                
      *          AFTER THE PUT REQUEST IS COMPLETE                              
      *                                                                         
      *    (6) PMOPTIONS (PUT MESSAGE OPTIONS)                                  
      *        - OPTIONS THAT CONTROL THE ACTION OF MQPUT                       
      *        - OPTION USED IN THIS PROGRAM:                                   
      *          (A) MQMD-EXPIRY - MESSAGE LIFETIME. THE MESSAGE                
      *                            BECOMES ELIGIBLE TO BE DISCARDED             
      *                            IF IT HAS NOT BEEN REMOVED FROM              
      *                            FROM THE QUEUE BEFORE THIS PERIOD            
      *                            OF TIME ELAPSED                              
      *                          - MUST BE SET TO MQEI_UNLIMITED                
      *          (B) MQMD-PERSISTENCE = MQPER-PERSISTENT                        
      *                                                                         
      *    (7) BUFFER-LENGTH                                                    
      *        - LENGTH OF THE MESSAGE IN BUFFER                                
      *                                                                         
      *    (8) BUFFER                                                           
      *        - MESSAGE DATA                                                   
      *-------------------------------------------------------------            
       01  HCONN                         PIC S9(9) BINARY.                      
       01  Q-HANDLE                      PIC S9(9) BINARY.                      
       01  OPTIONS                       PIC S9(9) BINARY.                      
       01  COMPLETION-CODE               PIC S9(9) BINARY.                      
       01  OPEN-CODE                     PIC S9(9) BINARY.                      
       01  CON-REASON                    PIC S9(9) BINARY.                      
       01  REASON                        PIC S9(9) BINARY.                      
       01  MQMSGD                        PIC S9(9) BINARY.                      
       01  BUFFER-LENGTH                 PIC S9(9) BINARY.                      
       01  BUFFER                        PIC X(368012).                         
       01  QM-NAME                       PIC X(48) VALUE SPACES.                
       01  TARGET-QUEUE                  PIC X(48) VALUE SPACES.                
                                                                                
      *-------------------------------------------------------------            
      *  FLAGS / SWITCHES                                                       
      *-------------------------------------------------------------            
       01  WS-OTHER-VARIABLES.                                                  
           05  WS-IDX                    PIC 9(03) VALUE ZEROES.                
           05  WS-K857-STATUS            PIC 9(02) VALUE ZEROES.                
           05  WS-K800-STATUS            PIC 9(02) VALUE ZEROES.                
           05  WS-K848-STATUS            PIC 9(02) VALUE ZEROES.                
           05  WS-F80A-EOF               PIC X(01) VALUE 'N'.                   
           05  WS-K857-FND               PIC X(01) VALUE 'N'.                   
           05  WS-K800-FND               PIC X(01) VALUE 'N'.                   
           05  WS-CURR-DATE              PIC X(08) VALUE SPACES.                
           05  WS-CURR-TIME              PIC X(08) VALUE SPACES.                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-MAIN-PROCESS.                                                       
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 1100-INITIALIZATION     THRU 1100-EXIT.                      
           PERFORM 2000-BP13F80A-READ      THRU 2000-EXIT.                      
                                                                                
           IF WS-F80A-EOF = 'Y'                                                 
              DISPLAY ' *** '                                                   
              DISPLAY ' *** NO RECORDS FOUND IN PRELIM FILE'                    
              DISPLAY ' *** '                                                   
                                                                                
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 2100-INIT-CONNECTION    THRU 2100-EXIT.                      
                                                                                
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-F80A-EOF = 'Y'                                      
                      OR WS-MSG-CTR = 600.                                      
                                                                                
           DISPLAY 'WS-SEND-MESSAGE : ' WS-SEND-MESSAGE.                        
                                                                                
           PERFORM 4000-MQ-PUT-MESSAGE     THRU 4000-EXIT.                      
           DISPLAY ' *** APPOINTMENT NOTIFICATION SENT!'                        
           PERFORM 5000-MQ-CLOSE           THRU 5000-EXIT.                      
           PERFORM 5100-MQ-DISCONNECT      THRU 5100-EXIT.                      
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           DISPLAY ' ***** STARTING PROGRAM BP13CS20 *****'.                    
           DISPLAY ' '.                                                         
                                                                                
           OPEN INPUT BP13F80A                                                  
                      BP13K857                                                  
                  I-O BP13K800                                                  
                      BP13K848.                                                 
                                                                                
           IF WS-K857-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, BP13K857 STATUS = ' WS-K857-STATUS        
              MOVE WS-K857-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, BP13K800 STATUS = ' WS-K800-STATUS        
              MOVE WS-K800-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF WS-K848-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, BP13K848 STATUS = ' WS-K848-STATUS        
              MOVE WS-K848-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO WS-CURR-DATE.                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-INITIALIZATION.                                                     
      *=============================================================            
                                                                                
           MOVE SPACES                    TO WS-SEND-MESSAGE.                   
                                                                                
           MOVE ';'                       TO WS-FILLER-01                       
                                             WS-FILLER-02                       
                                             WS-FILLER-03.                      
                                                                                
           PERFORM VARYING WS-MSG-CTR FROM 1 BY 1                               
             UNTIL WS-MSG-CTR > 600                                             
                   MOVE ';'               TO WS-FILLER-04(WS-MSG-CTR)   TR)     
                                             WS-FILLER-05(WS-MSG-CTR)   TR)     
                                             WS-FILLER-06(WS-MSG-CTR)   TR)     
                                             WS-FILLER-07(WS-MSG-CTR)   TR)     
                                             WS-FILLER-08(WS-MSG-CTR)   TR)     
                                             WS-FILLER-09(WS-MSG-CTR)   TR)     
                                             WS-FILLER-10(WS-MSG-CTR)   TR)     
                                             WS-FILLER-11(WS-MSG-CTR)   TR)     
                                             WS-FILLER-12(WS-MSG-CTR)   TR)     
           END-PERFORM.                                                         
                                                                                
           MOVE 'EML'                     TO WS-MSG-CDE.                        
           MOVE ZEROES                    TO WS-MSG-CTR.                        
                                                                                
           ACCEPT WS-BP13Q-NAME         FROM PARM-INPUT.                        
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13F80A-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13F80A                                                        
              AT END MOVE 'Y'              TO   WS-F80A-EOF.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2100-INIT-CONNECTION.                                                    
      *=============================================================            
                                                                                
           PERFORM 2200-MQ-CONNECT         THRU 2200-EXIT                       
           PERFORM 2300-MQ-OPEN            THRU 2300-EXIT                       
                                                                                
           MOVE SPACES                     TO   BUFFER.                         
           MOVE 'PB'                       TO   WS-MSG-APPT-TYP.                
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       2200-MQ-CONNECT.                                                         
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    SET THE QUEUE MANAGER NAME                                           
      *-------------------------------------------------------------            
           IF WS-BP13Q-NAME NOT = SPACES AND LOW-VALUES                         
              MOVE WS-BP13Q-NAME           TO   QM-NAME                         
           ELSE                                                                 
              MOVE WS-QM-NAME              TO   QM-NAME                         
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    CALL MQCONN TO CONNECT TO QUEUE MANAGER                              
      *-------------------------------------------------------------            
           CALL 'MQCONN' USING QM-NAME, HCONN,                                  
                               COMPLETION-CODE, CON-REASON.                     
                                                                                
           IF COMPLETION-CODE = MQCC-FAILED                                     
              MOVE CON-REASON              TO   CS20-REASON                     
              MOVE COMPLETION-CODE         TO   CS20-COMPLETION-CODE            
              MOVE 'BP13CS20 MQCONN ERROR' TO   CS20-ERR-MSG                    
              PERFORM 9100-EXIT-MQCONN     THRU 9100-EXIT                       
           END-IF.                                                              
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2300-MQ-OPEN.                                                            
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    USE DEFAULT WS-QUEUE-NAME                                            
      *-------------------------------------------------------------            
           MOVE WS-QUEUE-NAME              TO   MQOD-OBJECTNAME.                
           ADD  MQOO-OUTPUT MQOO-FAIL-IF-QUIESCING GIVING OPTIONS.              
                                                                                
      *-------------------------------------------------------------            
      *    CALL MQOPEN TO OPEN CONNECTION                                       
      *-------------------------------------------------------------            
           CALL 'MQOPEN' USING HCONN, OBJECT-DESCRIPTOR,                        
                               OPTIONS, Q-HANDLE,                               
                               OPEN-CODE, REASON.                               
                                                                                
           IF REASON NOT = MQRC-NONE                                            
              MOVE REASON                  TO   CS20-REASON                     
              MOVE OPEN-CODE               TO   CS20-COMPLETION-CODE            
              MOVE 'MQOPEN ENDED WITH REASON CODE'                              
                                           TO   CS20-ERR-MSG                    
              PERFORM 9100-EXIT-MQCONN     THRU 9100-EXIT                       
           END-IF.                                                              
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
           MOVE F80A-NUM-REGN            TO K857-NUM-REGN.                      
           PERFORM 3100-READ-BP13K857  THRU 3100-EXIT.                          
                                                                                
           IF WS-K857-FND = 'Y'                                                 
              ADD 1                      TO WS-MSG-CTR                          
              MOVE F80A-NUM-REGN         TO WS-MSG-NUM-REGN (WS-MSG-CTR)        
              MOVE F80A-NUM-APPT-TYPE(1:1)                                      
                                   TO WS-MSG-EMAIL-TYP(WS-MSG-CTR)              
              MOVE K857-NUM-EMAIL        TO WS-MSG-NUM-EMAIL(WS-MSG-CTR)        
                                                                                
              MOVE F80A-NUM-REGN         TO K800-NUM-REGN                       
              PERFORM 4200-READ-BP13K800 THRU 4200-EXIT                         
                                                                                
              MOVE K800-DTE-BALLOT      TO WS-MSG-DTE-BALLOT(WS-MSG-CTR)        
              MOVE K800-NUM-ALLO-CAT    TO WS-MSG-ALLO-CAT  (WS-MSG-CTR)        
              MOVE K800-NUM-FLAT-TYPE   TO WS-NUM-FLAT-TYPE (WS-MSG-CTR)        
              MOVE K800-NUM-ELDERLY    TO WS-NUM-ELDERLY-TAG(WS-MSG-CTR)        
                                                                                
              IF K800-AMT-PRELIM-AHG NOT NUMERIC                                
                 MOVE ZEROES             TO K800-AMT-PRELIM-AHG                 
              END-IF                                                            
              IF K800-AMT-PRELIM-SHG NOT NUMERIC                                
                 MOVE ZEROES             TO K800-AMT-PRELIM-SHG                 
              END-IF                                                            
                                                                                
              MOVE K800-AMT-PRELIM-AHG TO WS-MSG-AMT-PRE-AHG(WS-MSG-CTR)        
              MOVE K800-AMT-PRELIM-SHG TO WS-MSG-AMT-PRE-SHG(WS-MSG-CTR)        
           ELSE                                                                 
              DISPLAY ' '                                                       
              DISPLAY '**BP13K857 - EMAIL NOTFND, KEY : ' K857-NUM-REGN         
              DISPLAY ' '                                                       
           END-IF.                                                              
                                                                                
           PERFORM 2000-BP13F80A-READ THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3100-READ-BP13K857.                                                      
      *=============================================================            
           READ BP13K857.                                                       
           EVALUATE WS-K857-STATUS                                              
               WHEN 00                                                          
                    MOVE 'Y'                   TO WS-K857-FND                   
               WHEN 23                                                          
                    MOVE 'N'                   TO WS-K857-FND                   
                    MOVE SPACES                TO K857-NUM-EMAIL                
               WHEN OTHER                                                       
                    DISPLAY ' '                                                 
                    DISPLAY '**BP13K857 - READ FILE ERROR, KEY:'                
                                                  K857-NUM-REGN                 
                    DISPLAY ' '                                                 
                    MOVE WS-K857-STATUS        TO RETURN-CODE                   
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4000-MQ-PUT-MESSAGE.                                                     
      *=============================================================            
                                                                                
           MOVE WS-SEND-MESSAGE            TO   BUFFER.                         
                                                                                
      *-------------------------------------------------------------            
      *    SET UP MESSAGE DESCRIPTOR                                            
      *-------------------------------------------------------------            
           MOVE MQEI-UNLIMITED             TO   MQMD-EXPIRY.                    
           MOVE MQPER-PERSISTENT           TO   MQMD-PERSISTENCE.               
           MOVE MQFMT-STRING               TO   MQMD-FORMAT.                    
           MOVE WS-MESSAGE-LENGTH          TO   BUFFER-LENGTH.                  
                                                                                
      *-------------------------------------------------------------            
      *    CALL MQPUT TO PUT MESSAGE TO QUEUE                                   
      *-------------------------------------------------------------            
           CALL 'MQPUT' USING HCONN, Q-HANDLE,                                  
                              MESSAGE-DESCRIPTOR, PMOPTIONS,                    
                              BUFFER-LENGTH, BUFFER,                            
                              COMPLETION-CODE, REASON.                          
                                                                                
           IF REASON NOT = MQRC-NONE                                            
              MOVE REASON                  TO   CS20-REASON                     
              MOVE COMPLETION-CODE         TO   CS20-COMPLETION-CODE            
              MOVE 'BP13CS20 MQPUT ERROR'  TO   CS20-ERR-MSG                    
              PERFORM 9100-EXIT-MQCONN     THRU 9100-EXIT                       
           ELSE                                                                 
              PERFORM 4100-PROC-K800-UPDT  THRU 4100-EXIT                       
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4100-PROC-K800-UPDT.                                                     
      *=============================================================            
           PERFORM VARYING WS-IDX FROM 1 BY 1                                   
             UNTIL WS-IDX > 600                                                 
                OR WS-MSG-NUM-REGN(WS-IDX) = SPACES OR LOW-VALUE                
                                                                                
                MOVE WS-MSG-NUM-REGN(WS-IDX)  TO K800-NUM-REGN                  
                PERFORM 4200-READ-BP13K800  THRU 4200-EXIT                      
                                                                                
                IF WS-K800-FND = 'Y'                                            
                   MOVE 'Y'                  TO K800-PRELIM-OUTCOME-SENT        
                                                                                
                   PERFORM 4300-WRITE-K800-IMAGE THRU 4300-EXIT                 
                   PERFORM 4400-REWRITE-BP13K800 THRU 4400-EXIT                 
                ELSE                                                            
                   DISPLAY ' '                                                  
                   DISPLAY '=>BP13K800 - NOTFND, KEY : ' K800-NUM-REGN          
                   DISPLAY ' '                                                  
                END-IF                                                          
           END-PERFORM.                                                         
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       4200-READ-BP13K800.                                                      
      *=============================================================            
           READ BP13K800.                                                       
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    MOVE 'Y'                      TO WS-K800-FND                
               WHEN 23                                                          
                    MOVE 'N'                      TO WS-K800-FND                
                    MOVE SPACES                   TO BP13K800-MASTER            
                    INITIALIZE                       BP13K800-MASTER            
               WHEN OTHER                                                       
                    DISPLAY ' '                                                 
                    DISPLAY '*BP13K800 - READ ERROR, KEY:' K800-NUM-REGN        
                    DISPLAY ' '                                                 
                    MOVE WS-K857-STATUS           TO RETURN-CODE                
                    PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                  
           END-EVALUATE.                                                        
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       4300-WRITE-K800-IMAGE.                                                   
      *=============================================================            
            MOVE BP13K800-MASTER            TO BP13K848-MASTER.                 
            MOVE 'CS20'                     TO K848-NUM-TRANS-BKIMAGE.          
            MOVE 'BP13CS20'                 TO K848-NUM-USERID.                 
            MOVE K800-NUM-REGN              TO K848-NUM-REGN-BKIMAGE.           
            MOVE WS-CURR-DATE               TO K848-DTE-BKIMAGE.                
            MOVE FUNCTION CURRENT-DATE(9:8) TO K848-TME-BKIMAGE.                
                                                                                
            WRITE BP13K848-MASTER.                                              
                                                                                
            EVALUATE WS-K848-STATUS                                             
                WHEN 00                                                         
                     CONTINUE                                                   
                WHEN OTHER                                                      
                     DISPLAY 'BP13K848 - WRITE ERROR, STATUS : '                
                                               WS-K848-STATUS                   
                    PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                  
            END-EVALUATE.                                                       
       4300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *=============================================================            
       4400-REWRITE-BP13K800.                                                   
      *=============================================================            
           REWRITE BP13K800-MASTER.                                             
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY ' '                                                 
                    DISPLAY '**BP13K007 - REWRITE ERROR, KEY:'                  
                                                  K800-NUM-REGN                 
                    DISPLAY ' '                                                 
                    MOVE WS-K857-STATUS           TO RETURN-CODE                
                    PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                  
           END-EVALUATE.                                                        
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5000-MQ-CLOSE.                                                           
      *=============================================================            
                                                                                
           MOVE MQCO-NONE                  TO   OPTIONS.                        
                                                                                
      *-------------------------------------------------------------            
      *    CALL MQCLOSE TO CLOSE CONNECTION                                     
      *-------------------------------------------------------------            
           CALL 'MQCLOSE' USING HCONN, Q-HANDLE, OPTIONS,                       
                                COMPLETION-CODE, REASON.                        
                                                                                
           IF REASON NOT =  MQRC-NONE                                           
              MOVE REASON                   TO   CS20-REASON                    
              MOVE COMPLETION-CODE          TO   CS20-COMPLETION-CODE           
              MOVE 'BP13CS20 MQCLOSE ERROR' TO   CS20-ERR-MSG                   
              PERFORM 9100-EXIT-MQCONN      THRU 9100-EXIT                      
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5100-MQ-DISCONNECT.                                                      
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    CALL MQDISC TO DISCONNECT FROM QUEUE MANAGER                         
      *-------------------------------------------------------------            
           CALL 'MQDISC' USING HCONN, COMPLETION-CODE, REASON.                  
                                                                                
           IF REASON NOT = MQRC-NONE                                            
              MOVE REASON                  TO   CS20-REASON                     
              MOVE COMPLETION-CODE         TO   CS20-COMPLETION-CODE            
              MOVE 'BP13CS20 MQDISC ERROR' TO   CS20-ERR-MSG                    
              PERFORM 9100-EXIT-MQCONN     THRU 9100-EXIT                       
           END-IF.                                                              
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           CLOSE BP13F80A                                                       
                 BP13K857                                                       
                 BP13K800                                                       
                 BP13K848.                                                      
                                                                                
           IF RETURN-CODE NOT = ZERO                                            
              DISPLAY ' *** RETURN-CODE = ' RETURN-CODE ' ERROR'                
           ELSE                                                                 
              DISPLAY ' '                                                       
              DISPLAY ' *** SUCCESSFUL EXECUTION OF BP13CS20.'                  
              DISPLAY ' *** MESSAGE/S PUT INTO QUEUE SUCCESSFULLY!'             
              DISPLAY ' *** Q MANAGER : ' QM-NAME                               
              DISPLAY ' *** TARGET Q  : ' WS-QUEUE-NAME                         
              DISPLAY ' '                                                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9100-EXIT-MQCONN.                                                        
      *=============================================================            
                                                                                
           DISPLAY ' *** PROGRAM ID:  BP13CS20 '                                
           DISPLAY ' *** Q MANAGER : ' QM-NAME                                  
           DISPLAY ' *** TARGET Q  : ' WS-QUEUE-NAME                            
           DISPLAY ' '                                                          
                                                                                
           IF CS20-COMPLETION-CODE NOT = ZEROES AND LOW-VALUES                  
              DISPLAY ' *** MQ COMPLETION CODE: ' CS20-COMPLETION-CODE          
              DISPLAY ' *** MQ REASON         : ' CS20-REASON                   
              DISPLAY ' *** MQ ERROR MSG      : ' CS20-ERR-MSG                  
              MOVE 99                      TO  RETURN-CODE                      
           END-IF.                                                              
                                                                                
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       9100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
      **********           END OF BP13CS20                **********            
      **************************************************************            
