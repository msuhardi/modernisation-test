       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C258.                                                  
      *AUTHOR.       GD.                                                        
      *DATE-WRITTEN. 09/05/95.                                                  
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *  OBJECTIVE :                                               *            
      *      1.  TO EXTRACT ACCEPTANCE RECORDS FROM BP13K230       *            
      *          AND BP13K240 BY ALLOCATION-DATE, ZONE/NT.         *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REQ  BY   DATE   DESCRIPTION                           *            
      * -------- ---- ------ -----------                           *            
      * C9505D1  LMS  951103 INCLUDE SEPARATE OPTNS IN BP13B279    *            
      * BP130180 SSS  960907 INCLUDE IA COMMENTS                   *            
      * BP130393 SSS  971020 INCLUDE PG ZONE                       *            
      * BP130956 LMS  100101 TO CHANGE LRECL OF K230/K240 FROM 130 *            
      *                      TO 250.                               *            
      * BP132278 JF4  280303 CHGE BP13F230/F240 LRECL TO 450.      *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13K060   ASSIGN       TO BP13K060                           
                             ORGANIZATION IS INDEXED                            
                             ACCESS MODE  IS RANDOM                             
                             RECORD KEY   IS K060-KEY-FLD                       
                             FILE STATUS  IS WS-K060-STATUS.                    
                                                                                
           SELECT BP13F205   ASSIGN       TO BP13F205.                          
           SELECT BP13F230   ASSIGN       TO BP13F230.                          
           SELECT BP13F240   ASSIGN       TO BP13F240.                          
           SELECT P13F230A   ASSIGN       TO P13F230A.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F230                                                            
            RECORD CONTAINS 450 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13K230.                                                           
                                                                                
       FD   BP13F240                                                            
            RECORD CONTAINS 450 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13K240.                                                           
                                                                                
       FD   BP13F205                                                            
            RECORD CONTAINS  80 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F205.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS  25 CHARACTERS.                                     
       COPY BP13K060.                                                           
                                                                                
       FD   P13F230A                                                            
            RECORD CONTAINS 450 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       01   P13F230A-REC           PIC X(450).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-F230-READ            PIC 9(8)  VALUE 0.                           
       01  WS-F240-READ            PIC 9(8)  VALUE 0.                           
       01  WS-F230A-WRITE          PIC 9(8)  VALUE 0.                           
       01  WS-K060-STATUS          PIC 9(2).                                    
                                                                                
       01  WS-CONTROL-FIELDS.                                                   
           05  WS-F230-CTRL-FLDS.                                               
               10  WS-F230-DTE-ALLOC         PIC X(8).                          
               10  WS-F230-NUM-NT-ZONE       PIC X(20).                         
           05  WS-F240-CTRL-FLDS.                                               
               10  WS-F240-DTE-ALLOC         PIC X(8).                          
               10  WS-F240-NUM-NT-ZONE       PIC X(20).                         
           05  WS-F205-CTRL-FLDS.                                               
               10  WS-F205-DTE-ALLOCN        PIC X(8).                          
               10  WS-F205-NUM-NT-ZONE       PIC X(20).                         
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN.                                                                
      *-------------------------------------------------------------            
           PERFORM 100-OPEN-FILES    THRU 100-EXIT.                             
           PERFORM 205-READ-F205-REC THRU 205-EXIT.                             
           PERFORM 200-READ-F230-REC THRU 200-EXIT.                             
           PERFORM 300-PROCESS-F230  THRU 300-EXIT                              
                   UNTIL K230-CDE-INTV = HIGH-VALUE.                            
           PERFORM 400-READ-F240-REC THRU 400-EXIT.                             
           PERFORM 500-PROCESS-F240  THRU 500-EXIT                              
                   UNTIL K240-CDE-INTV = HIGH-VALUE.                            
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F205                                                 
                       BP13F230                                                 
                       BP13F240                                                 
                       BP13K060                                                 
                OUTPUT P13F230A.                                                
                                                                                
           IF (WS-K060-STATUS NOT = ZEROS AND 97)                               
              DISPLAY 'BP13K060 OPEN ERROR, STATUS IS ' WS-K060-STATUS          
              MOVE     WS-K060-STATUS TO RETURN-CODE                            
              GO TO    900-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-F230-REC.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES               TO WS-F230-CTRL-FLDS.                      
                                                                                
           READ BP13F230 AT END                                                 
                MOVE HIGH-VALUE TO K230-CDE-INTV                                
                GO TO 200-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F230-READ.                                               
                                                                                
           MOVE K230-DTE-ALLOC       TO WS-F230-DTE-ALLOC.                      
                                                                                
           IF F205-NUM-NT-ZONE-SW  = 'T'                                        
              MOVE K230-NUM-NEW-TOWN TO WS-F230-NUM-NT-ZONE                     
           ELSE                                                                 
           IF F205-NUM-NT-ZONE-SW  = 'Z'                                        
              MOVE K230-NUM-ZONE     TO WS-F230-NUM-NT-ZONE                     
           ELSE                                                                 
              MOVE SPACES            TO WS-F230-NUM-NT-ZONE                     
           END-IF.                                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       205-READ-F205-REC.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES              TO WS-F205-CTRL-FLDS.                       
                                                                                
           READ BP13F205 AT END                                                 
                GO TO 900-CLOSE-ROUTINE                                         
           END-READ.                                                            
                                                                                
           MOVE F205-DTE-ALLOCN     TO WS-F205-DTE-ALLOCN.                      
                                                                                
      *--------------->  DECODE NEW TOWN / ZONE                                 
                                                                                
           IF F205-NUM-NT-ZONE-SW  = 'T'                                        
              MOVE 02                TO K060-SERIAL-NO                          
              MOVE F205-NUM-NT-ZONE  TO K060-CODE                               
              READ BP13K060 KEY IS K060-KEY-FLD                                 
              EVALUATE WS-K060-STATUS                                           
              WHEN ZEROS                                                        
                MOVE K060-DESC       TO WS-F205-NUM-NT-ZONE                     
              WHEN OTHER                                                        
                DISPLAY 'BP13K060 READ ERROR, STATUS IS ' WS-K060-STATUS        
                DISPLAY 'BP13K060 RECORD KEY IS ' K060-KEY-FLD                  
                GO TO   900-CLOSE-ROUTINE                                       
              END-EVALUATE                                                      
              IF WS-F205-NUM-NT-ZONE = 'KALLANG/WHAMPOA'                        
                 MOVE 'KALLANG/WP/NOVENA' TO WS-F205-NUM-NT-ZONE                
              END-IF                                                            
           ELSE                                                                 
           IF F205-NUM-NT-ZONE-SW  = 'Z'                                        
              PERFORM 210-CONVERT-ZONE THRU 210-EXIT                            
           ELSE                                                                 
              MOVE SPACES            TO WS-F205-NUM-NT-ZONE.                    
                                                                                
       205-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       210-CONVERT-ZONE.                                                        
      *-------------------------------------------------------------            
           EVALUATE F205-NUM-NT-ZONE                                            
             WHEN 'PG'                                                          
                  MOVE 'PUNGGOL    ' TO WS-F205-NUM-NT-ZONE                     
             WHEN 'NN'                                                          
                  MOVE 'NORTH      ' TO WS-F205-NUM-NT-ZONE                     
             WHEN 'SS'                                                          
                  MOVE 'SOUTH      ' TO WS-F205-NUM-NT-ZONE                     
             WHEN 'EE'                                                          
                  MOVE 'EAST       ' TO WS-F205-NUM-NT-ZONE                     
             WHEN 'WW'                                                          
                  MOVE 'WEST       ' TO WS-F205-NUM-NT-ZONE                     
             WHEN 'NE'                                                          
                  MOVE 'NORTH-EAST ' TO WS-F205-NUM-NT-ZONE                     
             WHEN 'NW'                                                          
                  MOVE 'NORTH-WEST ' TO WS-F205-NUM-NT-ZONE                     
             WHEN 'SE'                                                          
                  MOVE 'SOUTH-EAST ' TO WS-F205-NUM-NT-ZONE                     
             WHEN 'SW'                                                          
                  MOVE 'SOUTH-WEST ' TO WS-F205-NUM-NT-ZONE                     
             WHEN OTHER                                                         
                  MOVE SPACES        TO WS-F205-NUM-NT-ZONE                     
           END-EVALUATE.                                                        
                                                                                
       210-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS-F230.                                                        
      *-------------------------------------------------------------            
           IF (WS-F205-CTRL-FLDS = WS-F230-CTRL-FLDS) AND                       
              (K230-CDE-DETAIL   = '02')                                        
               MOVE K230-DETAIL-REC  TO P13F230A-REC                            
               WRITE P13F230A-REC                                               
               ADD  1                TO  WS-F230A-WRITE                         
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-F230-REC THRU 200-EXIT.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       400-READ-F240-REC.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES               TO WS-F240-CTRL-FLDS.                      
           READ BP13F240 AT END                                                 
                MOVE HIGH-VALUE TO K240-CDE-INTV                                
                GO TO 400-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F240-READ.                                               
                                                                                
           MOVE K240-DTE-ALLOC       TO WS-F240-DTE-ALLOC.                      
                                                                                
           IF F205-NUM-NT-ZONE-SW  = 'T'                                        
              MOVE K240-NUM-NEW-TOWN TO WS-F240-NUM-NT-ZONE                     
           ELSE                                                                 
           IF F205-NUM-NT-ZONE-SW  = 'Z'                                        
              MOVE K240-NUM-ZONE     TO WS-F240-NUM-NT-ZONE                     
           ELSE                                                                 
              MOVE SPACES            TO WS-F240-NUM-NT-ZONE                     
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       500-PROCESS-F240.                                                        
      *-------------------------------------------------------------            
           IF (WS-F205-CTRL-FLDS = WS-F240-CTRL-FLDS) AND                       
              (K240-CDE-HEADER   = '01')                                        
               MOVE K240-HEADER-REC  TO P13F230A-REC                            
               WRITE P13F230A-REC                                               
               ADD  1                TO  WS-F230A-WRITE                         
           END-IF.                                                              
                                                                                
           PERFORM 400-READ-F240-REC THRU 400-EXIT.                             
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
      *-------------------------------------------------------------            
           DISPLAY '*-------------- BP13C258 --------------*'.                  
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS READ FROM  F230  : ' WS-F230-READ.            
           DISPLAY 'NO OF RECORDS READ FROM  F240  : ' WS-F240-READ.            
           DISPLAY 'NO OF RECORDS WRITTEN TO F230A : ' WS-F230A-WRITE.          
                                                                                
           CLOSE BP13F230                                                       
                 BP13F240                                                       
                 BP13F205                                                       
                 BP13K060                                                       
                 P13F230A.                                                      
                                                                                
           IF WS-K060-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K060 CLOSE ERROR, STATUS IS ' WS-K060-STATUS         
              MOVE WS-K060-STATUS TO RETURN-CODE                                
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
