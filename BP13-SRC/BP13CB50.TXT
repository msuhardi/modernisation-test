       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB50.                                                 
      *AUTHOR.        EDEN ROSE DINGAL                                          
      *DATE-WRITTEN.  03/01/07.                                                 
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  MATCH F730 W/ K830, IF ADDR TYP NOT H, OUT  *            
      *                IF K830-NUM-ADDR-TYPE NOT = 'H'             *            
      *                   MOVE 'R' TO F730-NUM-PPO                 *            
      *                ADDR-TYPE= N (PRIVATE ADDR)                 *            
      *                                                            *            
      *  INPUT FILE :  1. BP13F730                                 *            
      *                2. BP13K830                                 *            
      *  OUTPUT FILE:  1. P13F730A                                 *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * BP133111 ED1 20080103 NEW PROGRAM                          *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN TO BP13F730.                                  
           SELECT P13F730A ASSIGN TO P13F730A.                                  
                                                                                
           SELECT BP13K830  ASSIGN       TO BP13K830                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K830-KEY-FLD                        
      ***                   ALTERNATE RECORD KEY IS  K830-NUM-REGN              
                            FILE STATUS  IS K830-STATUS.                        
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13K830                                                             
           RECORD CONTAINS 250 CHARACTERS.                                      
       COPY BP13K830.                                                           
                                                                                
       FD  BP13F730                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F730.                                                           
                                                                                
       FD  P13F730A                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       01 P13F730A-REC  PIC X(500).                                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  NO-F730-READ        PIC 9(5)    VALUE ZEROS.                     
           05  NO-F730A-WRITE      PIC 9(5)    VALUE ZEROS.                     
           05  WS-UPDATE           PIC 9(5)    VALUE ZEROS.                     
                                                                                
       01  WS-CUR-DTE               PIC X(8)  VALUE SPACES.                     
       01  WS-CUR-DTE-R REDEFINES WS-CUR-DTE.                                   
           05  WS-CUR-DTE-N         PIC 9(8).                                   
       01  WS-DIFFERENCE            PIC 9(8)  VALUE 0.                          
       01  WS-CURR-DTE-J            PIC 9(8)  VALUE 0.                          
       01  WS-TOM-DTE-J             PIC 9(8)  VALUE 0.                          
       01  K830-STATUS              PIC 99    VALUE 0.                          
       01  WS-EOF                   PIC X       VALUE 'N'.                      
       01  WS-K830-UPD              PIC X       VALUE 'N'.                      
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
           PERFORM 1000-OPENING-ROUTINE  THRU 1000-EXIT.                        
           PERFORM 2000-READ-F730        THRU 2000-EXIT.                        
           PERFORM 4000-PROCESS-FILES    THRU 4000-EXIT                         
                   UNTIL WS-EOF = 'Y'.                                          
           PERFORM 9000-CLOSING-ROUTINE   THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-OPENING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F730                                                 
                       BP13K830                                                 
                OUTPUT P13F730A.                                                
                                                                                
           IF K830-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K830 - OPEN ERROR, STATUS: ' K830-STATUS             
              PERFORM 9000-CLOSING-ROUTINE   THRU 9000-EXIT.                    
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-DTE.                            
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       2000-READ-F730.                                                          
      *-------------------------------------------------------------            
            READ BP13F730 AT END                                                
                 MOVE 'Y' TO WS-EOF                                             
                 GO TO 2000-EXIT.                                               
            ADD 1 TO NO-F730-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4000-PROCESS-FILES.                                                      
      *-------------------------------------------------------------            
           PERFORM 4100-READ-K830         THRU 4100-EXIT.                       
                                                                                
           IF K830-NUM-ADDR-TYPE NOT = 'H'                                      
              MOVE 'R' TO F730-NUM-PPO                                          
              ADD   1  TO WS-UPDATE                                             
           END-IF.                                                              
                                                                                
           WRITE P13F730A-REC FROM BP13F730-REC.                                
           ADD 1 TO NO-F730A-WRITE.                                             
                                                                                
           PERFORM 2000-READ-F730         THRU 2000-EXIT.                       
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4100-READ-K830.                                                          
      *-------------------------------------------------------------            
            INITIALIZE              K830-KEY-FLD.                               
            MOVE F730-NUM-REGN   TO K830-KEY-FLD.                               
                                                                                
            READ BP13K830                                                       
            EVALUATE K830-STATUS                                                
            WHEN 00                                                             
                 CONTINUE                                                       
            WHEN 23                                                             
                 DISPLAY 'BP13K830 - RECORD NOT FOUND, KEY: '                   
                          K830-KEY-FLD                                          
            WHEN OTHER                                                          
                 DISPLAY 'BP13K830 - ERROR READ, STATUS: '                      
                          K830-STATUS                                           
            END-EVALUATE.                                                       
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9000-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           CLOSE BP13K830                                                       
                 BP13F730                                                       
                 P13F730A.                                                      
                                                                                
           IF K830-STATUS NOT = '00'                                            
             DISPLAY 'CLOSING ERROR K830, STATUS ' K830-STATUS                  
           END-IF.                                                              
                                                                                
           DISPLAY '-----------------------------------------'.                 
           DISPLAY '          PROGRAM BP13CB50 '.                               
           DISPLAY '        RUN DATE : ' WS-CUR-DTE.                            
           DISPLAY 'NO. OF F730 REC READ   > ' NO-F730-READ.                    
           DISPLAY 'NO. OF ADDR-TYP = H    > ' WS-UPDATE.                       
           DISPLAY 'NO. OF F730A WRITTEN   > ' NO-F730A-WRITE.                  
           DISPLAY '-----------------------------------------'.                 
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
