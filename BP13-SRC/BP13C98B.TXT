       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C98B.                                                 
      *AUTHOR.        JIJO FRANCIS.                                             
      *DATE-WRITTEN.  26/03/2018.                                               
      *===============================================================*         
      *               SYSTEM OF COMMITMENT (BP13) *                   *         
      *===============================================================*         
      *                                                               *         
      *   OBJECTIVE : FOR NEW APPLN THRU ORSA/INTERNET AF SOC CREATED *         
      *               THIS PROGRAM IS USED TO CREATE THE TRANSACTION  *         
      *               ID (TE)                                         *         
      *1. FILE DETAILS :                                              *         
      *    INPUT   :  BP13F740                                        *         
      *               BP13K820  ( OCCP FILE)                          *         
      *    OUTPUT     BP13F915  ( TRANSACTION FILE)                   *         
      *                                                               *         
      *===============================================================*         
      * CHG-NO    CHG-DATE     BY   DESCRIPTION                       *         
      * ------    --------     --   -----------                       *         
      * BP137228  26/03/2018   JF7  NEW PROGRAM TO CREATE 'TE' RECS   *         
      * BP137326  06/12/2018   FNP1 BLANK OFF OTHER DETAILS WHEN      *         
      *                             CREATING 'TE' FOR F915            *         
      * BP138193  14/02/2020   SMP3 DO NOT TERMINATE ROLE FOR MGPS    *         
      *                             UNLESS APPLICATION IS BOTH UNSUCC *         
      *                             UNDER MGPS AND PA QUEUE           *         
      *****************************************************************         
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
      *----------------------*                                                  
       CONFIGURATION SECTION.                                                   
      *----------------------*                                                  
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *----------------------*                                                  
       INPUT-OUTPUT SECTION.                                                    
      *----------------------*                                                  
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740   ASSIGN TO BP13F740.                                
                                                                                
           SELECT BP13K800   ASSIGN       TO BP13K800                           
                             ORGANIZATION IS INDEXED                            
                             ACCESS MODE  IS RANDOM                             
                             RECORD KEY   IS K800-NUM-REGN                      
                             FILE STATUS  IS WS-K800-STATUS.                    
                                                                                
           SELECT BP13K820   ASSIGN       TO BP13K820                           
                             ORGANIZATION IS INDEXED                            
                             ACCESS MODE  IS DYNAMIC                            
                             RECORD KEY   IS K820-KEY-FLD                       
                             FILE STATUS  IS WS-K820-STATUS.                    
                                                                                
           SELECT BP13F915   ASSIGN TO BP13F915.                                
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
      *-------------*                                                           
       FILE SECTION.                                                            
      *-------------*                                                           
                                                                                
       FD  BP13F740                                                             
           RECORD    CONTAINS 500  CHARACTERS                                   
           BLOCK     CONTAINS   0  RECORDS                                      
           LABEL     RECORDS  ARE  STANDARD                                     
           RECORDING MODE     IS   F.                                           
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BP13F915                                                             
           RECORD    CONTAINS 800 CHARACTERS                                    
           BLOCK     CONTAINS   0 RECORDS                                       
           LABEL     RECORDS  ARE STANDARD                                      
           RECORDING MODE     IS  F.                                            
       COPY BP13F915.                                                           
                                                                                
      *-------------------------*                                               
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
       01  FILE-STATUS.                                                         
           03  WS-K820-STATUS          PIC 9(02)         VALUE ZERO.            
           03  WS-K800-STATUS          PIC 9(02)         VALUE ZEROES.          
                                                                                
       01  WS-SWITCHES.                                                         
           03  F740-EOF-IND            PIC X             VALUE SPACES.          
               88 F740-EOF             VALUE 'Y'.                               
           03  WS-K820-EOF-SW           PIC X(01)  VALUE 'N'.                   
               88  WS-K820-EOF          VALUE 'Y'.                              
           03  WS-K800-SUCC            PIC X(01)         VALUE 'N'.             
                                                                                
       01  WS-ACCUMULATORS.                                                     
           03  WS-NO-READ-F740         PIC 9(10)         VALUE ZERO.            
           03  WS-NO-READ-K800         PIC 9(10)         VALUE ZERO.            
           03  WS-NO-READ-K800-SUCC    PIC 9(10)         VALUE ZERO.            
           03  WS-NO-READ-K820         PIC 9(10)         VALUE ZERO.            
           03  WS-TE-WRT-F915          PIC 9(10)         VALUE ZERO.            
           03  WS-EDT-NO-READ-K800     PIC Z,ZZZ,ZZZ,ZZ9 VALUE ZERO.            
           03  WS-EDT-NO-READ-K800-SUCC PIC Z,ZZZ,ZZZ,ZZ9 VALUE ZERO.           
           03  WS-EDT-NO-READ-K820     PIC Z,ZZZ,ZZZ,ZZ9 VALUE ZERO.            
           03  WS-EDT-NO-READ-F740     PIC Z,ZZZ,ZZZ,ZZ9 VALUE ZERO.            
           03  WS-EDT-TE-WRT-F915      PIC Z,ZZZ,ZZZ,ZZ9 VALUE ZERO.            
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           03  WS-YY                    PIC 9(4).                               
           03  WS-MM                    PIC 9(2).                               
           03  WS-DD                    PIC 9(2).                               
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *--------------------------------------------------------------*          
       000-MAIN-PROCESS.                                                        
      *--------------------------------------------------------------*          
             PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                   
             PERFORM 2000-READ-BP13F740       THRU 2000-EXIT.                   
             PERFORM 3000-PROCESS-ROUTINE     THRU 3000-EXIT                    
               UNTIL F740-EOF.                                                  
             PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                   
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       1000-OPEN-ROUTINE.                                                       
      *--------------------------------------------------------------*          
           OPEN       INPUT   BP13F740                                          
                              BP13K800                                          
                              BP13K820                                          
                      OUTPUT  BP13F915.                                         
                                                                                
           IF WS-K800-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR OPENING BP13K800 FILE, STATUS '                    
                       WS-K800-STATUS                                           
              MOVE WS-K800-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = ZEROS AND '97'                               
              DISPLAY ' ERROR OPENING BP13K820  FILE, STATUS '                  
                                                  WS-K820-STATUS                
              MOVE WS-K820-STATUS            TO   RETURN-CODE                   
              PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)   TO  WS-SYSTEM-DATE.                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       2000-READ-BP13F740.                                                      
      *--------------------------------------------------------------*          
           READ BP13F740 AT END  MOVE 'Y' TO F740-EOF-IND                       
                         GO TO 2000-EXIT.                                       
                                                                                
           ADD 1 TO WS-NO-READ-F740.                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       3000-PROCESS-ROUTINE.                                                    
      *--------------------------------------------------------------*          
           MOVE 'N'                         TO WS-K800-SUCC.                    
                                                                                
           IF (F740-NUM-RANDOM(28:1) = 'M') OR                                  
              (F740-NUM-MGPS = 'P' OR 'C')                                      
              PERFORM 3100-READ-BP13K800    THRU 3100-EXIT                      
           END-IF.                                                              
                                                                                
      *** NOTE: TERMINATE ROLE ONLY WHEN BOTH MGPS AND PA RECORDS ARE           
      ***       UNSUCCESSFUL                                                    
           IF WS-K800-SUCC = 'N'                                                
              PERFORM 4100-START-BP13K820   THRU 4100-EXIT                      
              PERFORM 4300-BROWSE-BP13K820  THRU 4300-EXIT                      
                UNTIL WS-K820-EOF OR                                            
                     (K820-NUM-REGN NOT = F740-NUM-REGN)                        
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F740       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       3100-READ-BP13K800.                                                      
      *--------------------------------------------------------------*          
           MOVE SPACES                      TO BP13K800-MASTER.                 
           INITIALIZE                          BP13K800-MASTER.                 
                                                                                
           MOVE F740-NUM-REGN               TO K800-NUM-REGN.                   
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN ZEROES                                                          
                ADD 1                       TO WS-NO-READ-K800                  
                IF (K800-NUM-QUEUE-MGPS  NOT = SPACES AND LOW-VALUES) OR        
                   (K800-NUM-NT-FT-QUEUE NOT = SPACES AND LOW-VALUES)           
                   MOVE 'Y'                 TO WS-K800-SUCC                     
                   ADD 1                    TO WS-NO-READ-K800-SUCC             
                END-IF                                                          
                                                                                
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FOUND IN BP13K800: ' K800-NUM-REGN          
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING ON BP13K800: ' WS-K800-STATUS            
                MOVE WS-K800-STATUS         TO RETURN-CODE                      
                PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       4100-START-BP13K820.                                                     
      *--------------------------------------------------------------*          
           MOVE 'N'           TO  WS-K820-EOF-SW.                               
           MOVE F740-NUM-REGN TO  K820-NUM-REGN.                                
           MOVE SPACES        TO  K820-NUM-NRIC.                                
                                                                                
           START BP13K820 KEY >= K820-KEY-FLD                                   
           IF WS-K820-STATUS = ZEROS OR 02                                      
              PERFORM 4200-READ-BP13K820   THRU 4200-EXIT                       
           ELSE                                                                 
              DISPLAY 'START BROWSE ERROR, BP13K820 STATUS IS '                 
                      WS-K820-STATUS                                            
              MOVE WS-K820-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       4200-READ-BP13K820.                                                      
      *--------------------------------------------------------------*          
           READ BP13K820 NEXT AT END                                            
                              MOVE 'Y'      TO WS-K820-EOF-SW                   
                              GO TO 4200-EXIT.                                  
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       4300-BROWSE-BP13K820.                                                    
      *--------------------------------------------------------------*          
           IF F740-NUM-REGN  = K820-NUM-REGN                                    
              ADD  1       TO  WS-NO-READ-K820                                  
              PERFORM 4700-TE-WRITE-BP13F915   THRU 4700-EXIT                   
           END-IF.                                                              
                                                                                
           PERFORM 4200-READ-BP13K820  THRU 4200-EXIT.                          
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       4700-TE-WRITE-BP13F915.                                                  
      *--------------------------------------------------------------*          
           INITIALIZE  BP13F915-REC.                                            
           MOVE SPACES                   TO   BP13F915-REC.                     
                                                                                
           MOVE  'TE'                    TO   F915-TXN-TYPE.                    
           MOVE  'P'                     TO   F915-CSTMR-TYPE.                  
           MOVE  'BP13'                  TO   F915-NUM-UPDATE-SOURCE.           
           MOVE  'BP13C98B'              TO   F915-NUM-UPDATE-USERID.           
                                                                                
      **** PERSON PARTICULARS...                                                
                                                                                
           MOVE  K820-NUM-CSTMR-SOURCE   TO   F915-NUM-CSTMR-SOURCE.            
                                                                                
                                                                                
           PERFORM 4800-CONVERT-CUSTOMER-ROLE  THRU 4800-EXIT.                  
           MOVE  K820-NUM-REGN           TO   F915-NUM-BSNS-REF.                
           STRING WS-SYSTEM-DATE(1:4) '-'                                       
                  WS-SYSTEM-DATE(5:2) '-'                                       
                  WS-SYSTEM-DATE(7:2)                                           
                  DELIMITED BY SIZE INTO                                        
                  F915-DTE-ROLE-END.                                            
                                                                                
           STRING WS-SYSTEM-DATE(1:4) '-'                                       
                  WS-SYSTEM-DATE(5:2) '-'                                       
                  WS-SYSTEM-DATE(7:2)                                           
                  DELIMITED BY SIZE INTO                                        
                  F915-DTE-UPDATE-SOURCE.                                       
                                                                                
      ******   WRITING CDB  INFO.  INTO   BP13F915  *******                     
                                                                                
           WRITE BP13F915-REC.                                                  
           ADD 1 TO WS-TE-WRT-F915.                                             
                                                                                
       4700-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       4800-CONVERT-CUSTOMER-ROLE.                                              
      *--------------------------------------------------------------*          
            IF K820-NUM-NRIC        =   F740-NUM-NRIC1                          
               MOVE '011'           TO  F915-NUM-ROLE                           
            ELSE                                                                
              IF ( K820-NUM-NRIC    =   F740-NUM-NRIC2 ) OR                     
                 ( K820-NUM-NRIC    =   F740-NUM-NRIC3 ) OR                     
                 ( K820-NUM-NRIC    =   F740-NUM-NRIC4 )                        
                  MOVE '012'        TO  F915-NUM-ROLE                           
              ELSE                                                              
                  MOVE '013'        TO  F915-NUM-ROLE                           
              END-IF                                                            
            END-IF.                                                             
                                                                                
       4800-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       9000-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------------*          
           CLOSE BP13F740                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13F915.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K800 FILE, STATUS '                    
                       WS-K800-STATUS                                           
              MOVE WS-K800-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY ' ERROR CLOSING BP13K820  FILE, STATUS '                  
              WS-K820-STATUS                                                    
              MOVE WS-K820-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           MOVE  WS-NO-READ-K800       TO   WS-EDT-NO-READ-K800                 
           MOVE  WS-NO-READ-K800-SUCC  TO   WS-EDT-NO-READ-K800-SUCC            
           MOVE  WS-NO-READ-K820       TO   WS-EDT-NO-READ-K820                 
           MOVE  WS-NO-READ-F740       TO   WS-EDT-NO-READ-F740                 
           MOVE  WS-TE-WRT-F915        TO   WS-EDT-TE-WRT-F915                  
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY " BP13C98B  CONTROL LIST  :  ".                              
           DISPLAY "----------------------------".                              
           DISPLAY SPACES.                                                      
           DISPLAY "NO. OF REC READ FROM BP13F740 :" WS-EDT-NO-READ-F740        
           DISPLAY "NO. OF REC READ FROM BP13K800 :" WS-EDT-NO-READ-K800        
           DISPLAY "NO. OF REC READ FROM BP13K820 :" WS-EDT-NO-READ-K820        
           DISPLAY SPACES.                                                      
           DISPLAY "NO. OF REC NOT WRITTEN TO F915 "                            
           DISPLAY " DUE TO SUCC MGPS/NT FT QUEUE :"                            
                         WS-EDT-NO-READ-K800-SUCC                               
           DISPLAY "NO. OF REC WRITE TO  BP13F915 }" WS-EDT-TE-WRT-F915         
           DISPLAY " CDB TABLE   ( 'TE' TRANS )   }"                            
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
