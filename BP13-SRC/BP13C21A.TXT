       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C21A.                                                 
      *AUTHOR.        MTD.                                                      
      *DATE-WRITTEN.  JULY2004.                                                 
      **************************************************************            
      * BP13 - SYSTEM OF COMMITMENT                                *            
      **************************************************************            
      * OBJECTIVE  : TO EXTRACT ACCEPTED FLATS                     *            
      *------------------------------------------------------------*            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      *--------- -------- ----  -----------------------------------*            
      * BP132562 28/07/04 MTD   EXTRACT ACCEPTED FLATS             *            
      **************************************************************            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K230  ASSIGN       TO BP13K230                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K230-KEY-FLD                        
                            ALTERNATE KEY   IS K230-ALT-KEY3                    
                            FILE STATUS  IS WS-K230-STATUS.                     
                                                                                
           SELECT BP13K060  ASSIGN       TO BP13K060                    00470000
                            ORGANIZATION IS INDEXED                     00490000
                            ACCESS MODE  IS RANDOM                      00480000
                            RECORD KEY   IS K060-KEY-FLD                00500000
                            FILE STATUS  IS WS-K060-STATUS.             00510000
                                                                                
           SELECT BP13F208  ASSIGN       TO BP13F208.                   00470000
                                                                                
           SELECT BP13F230  ASSIGN       TO BP13F230.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *------------------------------------------------------------             
       FD  BP13K230                                                             
           RECORD CONTAINS 450 CHARACTERS.                                      
       COPY BP13K230.                                                           
                                                                                
       FD   BP13K060                                                    00640000
            RECORD CONTAINS 25  CHARACTERS.                                     
       COPY BP13K060.                                                   00680000
                                                                                
       FD   BP13F208                                                            
            RECORDING MODE  F                                                   
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13F208.                                                           
                                                                                
       FD   BP13F230                                                            
            RECORDING MODE  F                                                   
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 450 CHARACTERS.                                     
       01  F230-DETAIL-REC               PIC X(450).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-VARIABLES.                                                        
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-DATE            PIC X(08) VALUE SPACES.               
               10  FILLER                 PIC X(13) VALUE SPACES.               
           05  WS-DISP-DATE.                                                    
               10  WS-DISP-DD             PIC X(02) VALUE SPACES.               
               10  FILLER                 PIC X(01) VALUE '/'.                  
               10  WS-DISP-MM             PIC X(02) VALUE SPACES.               
               10  FILLER                 PIC X(01) VALUE '/'.                  
               10  WS-DISP-CCYY           PIC X(04) VALUE SPACES.               
           05  WS-NT-ZONE-DESC            PIC X(20) VALUE SPACES.               
           05  WS-FLAT-TYPE               PIC X(02) VALUE SPACES.               
           05  WS-DTE-ALLOCN              PIC X(08) VALUE SPACES.               
                                                                                
       01  WS-STATUS.                                                           
           05  WS-K230-STATUS             PIC 9(02) VALUE ZEROES.               
           05  WS-K060-STATUS             PIC 9(02) VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F208-READ-CTR           PIC 9(05) VALUE ZEROES.               
           05  WS-ACCEPT-CTR              PIC 9(05) VALUE ZEROES.               
           05  WS-F230-WRT-CTR            PIC 9(05) VALUE ZEROES.               
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-END-OF-F208-SW          PIC X(01) VALUE SPACES.               
               88  C-END-OF-F208          VALUE 'Y'.                            
           05  WS-END-OF-K230-SW          PIC X(01) VALUE SPACES.               
               88  C-END-OF-K230          VALUE 'Y'.                            
           05  WS-NT-ZONE-DESC-FND-SW     PIC X(01) VALUE SPACES.               
               88  C-NT-ZONE-DESC-FND     VALUE 'Y'.                            
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       MAIN-ROUTINE SECTION.                                                    
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-PROCESS-RECORDS    THRU 2000-EXIT                       
             UNTIL C-END-OF-F208.                                               
           PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT.                      
                                                                                
       MAIN-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F208                                                 
                       BP13K230                                                 
                       BP13K060                                                 
                OUTPUT BP13F230.                                                
                                                                                
           IF WS-K230-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR-BP13K230-STATUS ' WS-K230-STATUS              
              MOVE     WS-K230-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K060-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR-BP13K060-STATUS ' WS-K060-STATUS              
              MOVE     WS-K060-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-SYSTEM-DATE.                       
           MOVE WS-SYS-DATE(1:4)       TO WS-DISP-CCYY.                         
           MOVE WS-SYS-DATE(5:2)       TO WS-DISP-MM.                           
           MOVE WS-SYS-DATE(7:2)       TO WS-DISP-DD.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       2000-PROCESS-RECORDS.                                                    
      *-------------------------------------------------------------            
                                                                                
           READ BP13F208 AT END                                                 
                MOVE 'Y'    TO WS-END-OF-F208-SW                                
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F208-READ-CTR.                                           
                                                                                
           MOVE F208-DTE-ALLOCN      TO WS-DTE-ALLOCN.                          
                                                                                
           PERFORM 2100-DECODE-NEW-TOWN            THRU 2100-EXIT.              
           IF NOT C-NT-ZONE-DESC-FND                                            
              DISPLAY 'NEW TOWN/ZONE DESCRIPTION NOT FOUND'                     
              GO TO 2000-EXIT                                                   
           END-IF.                                                              
                                                                                
           PERFORM 2200-DECODE-FLAT-TYPE           THRU 2200-EXIT.              
                                                                                
           MOVE SPACES        TO WS-END-OF-K230-SW.                             
           PERFORM 2300-START-BP13K230             THRU 2300-EXIT.              
           PERFORM 2400-READ-BP13K230              THRU 2400-EXIT               
             UNTIL C-END-OF-K230.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2100-DECODE-NEW-TOWN.                                                    
      *-------------------------------------------------------------            
                                                                                
           MOVE 'N'      TO WS-NT-ZONE-DESC-FND-SW.                             
           MOVE SPACES   TO WS-NT-ZONE-DESC.                                    
                                                                                
           MOVE  F208-NUM-NT-ZONE     TO   K060-CODE.                           
           MOVE '02'                  TO   K060-SERIAL-NO.                      
           PERFORM 2110-READ-BP13K060 THRU 2110-EXIT.                           
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2110-READ-BP13K060.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K060.                                                       
                                                                                
           EVALUATE WS-K060-STATUS                                              
              WHEN 00                                                           
                 MOVE  K060-DESC  TO WS-NT-ZONE-DESC                            
                 MOVE  'Y'        TO WS-NT-ZONE-DESC-FND-SW                     
              WHEN 23                                                           
                 MOVE SPACES      TO WS-NT-ZONE-DESC                            
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BP13K060, STATUS = '                    
                          WS-K060-STATUS                                        
                 DISPLAY 'K060-KEY-FLD = ' K060-KEY-FLD                         
                 MOVE WS-K060-STATUS       TO RETURN-CODE                       
                 PERFORM  9000-CLOSE-FILES            THRU 9000-EXIT            
           END-EVALUATE.                                                        
                                                                                
       2110-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2200-DECODE-FLAT-TYPE.                                                   
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES        TO WS-FLAT-TYPE.                                  
                                                                                
           EVALUATE F208-NUM-FLAT-TYPE                                          
             WHEN 'E '                                                          
                   MOVE '6 '               TO WS-FLAT-TYPE                      
             WHEN OTHER                                                         
                   MOVE F208-NUM-FLAT-TYPE TO WS-FLAT-TYPE                      
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2300-START-BP13K230.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE LOW-VALUES      TO K230-ALT-KEY3.                               
           START BP13K230 KEY NOT LESS THAN K230-ALT-KEY3                       
           IF WS-K230-STATUS = ZEROES                                           
              NEXT SENTENCE                                                     
           ELSE                                                                 
              DISPLAY 'START READ ERROR BP13K230,STATUS ' WS-K230-STATUS        
              MOVE     WS-K230-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       2400-READ-BP13K230.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K230 NEXT RECORD AT END                                     
                MOVE 'Y'   TO  WS-END-OF-K230-SW                                
                GO TO 2400-EXIT.                                                
                                                                                
           IF WS-K230-STATUS = 00 OR 02                                         
              PERFORM 2500-CHECK-NT-ALLOCDTE      THRU 2500-EXIT                
           ELSE                                                                 
              DISPLAY 'READ ERROR BP13K230,STATUS ' WS-K230-STATUS              
              MOVE     WS-K230-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
       2400-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2500-CHECK-NT-ALLOCDTE.                                                  
      *-------------------------------------------------------------            
                                                                                
           IF (WS-NT-ZONE-DESC = K230-NUM-NEW-TOWN) AND                         
              (WS-DTE-ALLOCN = K230-DTE-ALLOC) AND                              
              ((WS-FLAT-TYPE(1:1) =  K230-NUM-FLAT-TYPE(1:1)) OR                
               (WS-FLAT-TYPE(1:1) =  K230-NUM-FLAT-TYPE(2:1)) OR                
               (WS-FLAT-TYPE = 'SE'))                                           
                PERFORM 3000-CHECK-UNIT THRU 3000-EXIT                          
           END-IF.                                                              
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-CHECK-UNIT.                                                         
      *-------------------------------------------------------------            
                                                                                
           IF K230-NUM-ACCEPT = 'A'                                             
              ADD  1  TO WS-ACCEPT-CTR                                          
              PERFORM 3100-WRITE-BP13K230         THRU 3100-EXIT                
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3100-WRITE-BP13K230.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES           TO F230-DETAIL-REC.                            
           INITIALIZE               F230-DETAIL-REC.                            
           MOVE K230-DETAIL-REC  TO F230-DETAIL-REC.                            
           WRITE F230-DETAIL-REC.                                               
                                                                                
           ADD  1     TO WS-F230-WRT-CTR.                                       
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           DISPLAY 'PROGRAM ID   : BP13C21A'.                                   
           DISPLAY 'CURRENT DATE : ' WS-DISP-DATE.                              
           DISPLAY SPACES.                                                      
           DISPLAY '*=============== COUNTERS ===============*'.                
           DISPLAY 'NO. OF F208 RECORD          : ' WS-F208-READ-CTR.           
           DISPLAY 'NO. OF ACCEPTED UNITS       : ' WS-ACCEPT-CTR.              
           DISPLAY 'NO. OF RECORDS WRITTEN TO BP13F230  : '                     
                                                    WS-F230-WRT-CTR.            
                                                                                
           CLOSE BP13F208                                                       
                 BP13K230                                                       
                 BP13K060                                                       
                 BP13F230.                                                      
                                                                                
           IF WS-K230-STATUS NOT = 00                                           
              DISPLAY 'CLOSE ERROR-BP13K230-STATUS ' WS-K230-STATUS             
           END-IF.                                                              
                                                                                
           IF WS-K060-STATUS NOT = 00                                           
              DISPLAY 'CLOSE ERROR-BP13K060-STATUS ' WS-K230-STATUS             
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
