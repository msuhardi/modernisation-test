       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CH61.                                                 
      *AUTHOR.        KSJ3.                                                     
      *DATE-WRITTEN.  16/09/2016.                                               
      * ========================================================== *            
      * OBJECTIVE    :  TO UPDATE BP13F203 BASED ON BP13KC50       *            
      * ========================================================== *            
      * INPUT  FILES :  1.) BP13FC50                               *            
      *                 2.) BP13K800                               *            
      *                 3.) BP13K820                               *            
      * I-O    FILES :  1.) BP13KC50                               *            
      * OUTPUT FILES :  1.) BP13F203                               *            
      * LISTING      :  1.) P13LH61A                               *            
      * ========================================================== *            
      * CHG-NO   BY   DATE       DESCRIPTION                       *            
      * -------- ---  ------     -----------                       *            
      * BP136431 KSJ3 16/09/2016 NEW PROGRAM                       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FC50  ASSIGN       TO BP13FC50.                           
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS WS-K800-STATUS.                     
                                                                                
           SELECT BP13K820  ASSIGN       TO BP13K820                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K820-KEY-FLD                        
                            FILE STATUS  IS WS-K820-STATUS.                     
                                                                                
           SELECT BP13KC50  ASSIGN       TO BP13KC50                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS KC50-KEY-FLD                        
                            FILE STATUS  IS WS-KC50-STATUS.                     
                                                                                
           SELECT BP13F203  ASSIGN       TO BP13F203.                           
                                                                                
           SELECT P13LH61A  ASSIGN       TO P13LH61A.                           
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13FC50                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS  100 CHARACTERS.                                     
           COPY BP13FC50.                                                       
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
           COPY BP13K800.                                                       
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS  400 CHARACTERS.                                     
           COPY BP13K820.                                                       
                                                                                
       FD  BP13KC50                                                             
           RECORD CONTAINS  100 CHARACTERS.                                     
           COPY BP13KC50.                                                       
                                                                                
       FD  BP13F203                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS  200 CHARACTERS.                                     
           COPY BP13F203.                                                       
                                                                                
       FD  P13LH61A                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS  132 CHARACTERS.                                     
       01  P13LH61A-REC                  PIC X(132).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K800-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K820-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-F203-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-KC50-STATUS            PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-FC50-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-FC50-SKIP          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-KC50-UPD           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F203-WRT           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-LH61A-WRITE        PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-LH61A-LINE         PIC 9(2)   VALUE 60.                   
           05  WS-CNT-LH61A-PAGE         PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-LH61A-SNO          PIC 9(5)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-FC50-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K800-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K820-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-F203-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-KC50-FND               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE SPACES.               
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-NUM-DESC               PIC X(60)  VALUE SPACES.               
           05  WS-NME-OCCUP              PIC X(66)  VALUE SPACES.               
                                                                                
      *-------------------------------------------------------------            
      *    REPORT LAYOUT FOR EXCEPTIONS                                         
      *-------------------------------------------------------------            
       01  LH61A-PR-HEAD-01.                                                    
           05  FILLER                    PIC X(14)   VALUE 'BP13LH61A'.         
           05  FILLER                    PIC X(8)    VALUE 'HDB3    '.          
           05  FILLER                    PIC X(25)   VALUE SPACES.              
           05  FILLER                    PIC X(39)   VALUE                      
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                    PIC X(13)   VALUE SPACES.              
           05  FILLER                    PIC X(7)    VALUE 'DATE: '.            
           05  LH61A-DTE-CURRENT         PIC X(10)   VALUE SPACES.              
           05  FILLER                    PIC X(3)    VALUE SPACES.              
           05  FILLER                    PIC X(7)    VALUE 'PAGE: '.            
           05  LH61A-PAGE                PIC ZZZ9.                              
                                                                                
       01  LH61A-PR-HEAD-02.                                                    
           05  FILLER                    PIC X(45)   VALUE SPACES.              
           05  FILLER                    PIC X(41)   VALUE                      
              'EXCEPTION REPORT ON CREATING PCD SMS FILE'.                      
                                                                                
       01  LH61A-PR-HEAD-03.                                                    
           05  FILLER                    PIC X(132)  VALUE                      
              '    S/NO   REGN      ERROR DESCRIPTION'.                         
                                                                                
       01  LH61A-PR-DTL.                                                        
           05  FILLER                    PIC X(4)    VALUE SPACES.              
           05  LH61A-SNO                 PIC ZZZZ9.                             
           05  FILLER                    PIC X(2)    VALUE SPACES.              
           05  LH61A-NUM-REGN            PIC X(8).                              
           05  FILLER                    PIC X(2)    VALUE SPACES.              
           05  LH61A-NUM-DESC            PIC X(60).                             
                                                                                
       01  LH61A-PR-DASH-LINE.                                                  
           05  FILLER                    PIC X(4)    VALUE SPACES.              
           05  FILLER                    PIC X(90)   VALUE ALL '-'.             
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-BP13FC50-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-FC50-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13FC50                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                I-O    BP13KC50                                                 
                OUTPUT BP13F203                                                 
                       P13LH61A.                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K820 ERROR ' WS-K820-STATUS                  
              MOVE     WS-K820-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-KC50-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KC50 ERROR ' WS-KC50-STATUS                  
              MOVE     WS-KC50-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
                                                                                
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8           TO   WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13FC50-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13FC50                                                        
              AT END MOVE 'Y'              TO   WS-FC50-EOF.                    
                                                                                
           IF WS-FC50-EOF NOT = 'Y'                                             
              ADD  1                       TO   WS-CNT-FC50-READ                
              MOVE SPACES                  TO   WS-NUM-DESC                     
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    VALIDATE INPUT                                                       
      *-------------------------------------------------------------            
           IF FC50-NUM-HP = SPACES OR LOW-VALUES OR ZEROES                      
              MOVE 'NO HANDPHONE NUMBER'   TO   WS-NUM-DESC                     
              PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
              GO TO 3000-CONTINUE-READ-NEXT                                     
           END-IF.                                                              
                                                                                
           IF FC50-NUM-HP(1:1) NOT = '3' AND '8' AND '9'                        
              MOVE 'INVALID HANDPHONE NUMBER'                                   
                                           TO   WS-NUM-DESC                     
              PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
              GO TO 3000-CONTINUE-READ-NEXT                                     
           END-IF.                                                              
                                                                                
           IF FC50-DTE-SMS-SENT NOT = SPACES AND LOW-VALUES AND                 
                                      ZEROES                                    
              STRING 'SMS WAS ALREADY SENT ON ' FC50-DTE-SMS-SENT               
                      DELIMITED BY SIZE    INTO WS-NUM-DESC                     
              PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
              GO TO 3000-CONTINUE-READ-NEXT                                     
           END-IF.                                                              
                                                                                
           IF FC50-DTE-APPROVED = SPACES OR LOW-VALUES OR ZEROES                
              MOVE 'SMS IS NOT YET APPROVED'                                    
                                           TO   WS-NUM-DESC                     
              PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
              GO TO 3000-CONTINUE-READ-NEXT                                     
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    GET SCHEME ACCOUNT FROM SOC MASTER                                   
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   BP13K800-MASTER.                
           MOVE FC50-NUM-REGN              TO   K800-NUM-REGN.                  
                                                                                
           PERFORM 3100-BP13K800-READ      THRU 3100-EXIT.                      
                                                                                
           IF WS-K800-FND = 'N'                                                 
              MOVE 'REGN NOT FOUND IN SOC MASTER'                               
                                           TO   WS-NUM-DESC                     
              PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
              GO TO 3000-CONTINUE-READ-NEXT                                     
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    GET OCCUPIER NAME                                                    
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   WS-NME-OCCUP                    
                                                BP13K820-REC.                   
           MOVE FC50-NUM-REGN              TO   K820-NUM-REGN.                  
           MOVE K800-NUM-NRIC1             TO   K820-NUM-NRIC.                  
                                                                                
           PERFORM 3200-BP13K820-READ      THRU 3200-EXIT.                      
                                                                                
           IF WS-K820-FND = 'Y'                                                 
              MOVE K820-NME-OCCP           TO   WS-NME-OCCUP                    
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    CREATE SMS FILE                                                      
      *-------------------------------------------------------------            
           PERFORM 4000-CREATE-SMS         THRU 4000-EXIT.                      
           PERFORM 4100-UPDATE-PCDSMS      THRU 4100-EXIT.                      
                                                                                
       3000-CONTINUE-READ-NEXT.                                                 
                                                                                
           PERFORM 2000-BP13FC50-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3100-BP13K800-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K800-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K800-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K800 ERROR '    WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3200-BP13K820-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K820.                                                       
                                                                                
           EVALUATE WS-K820-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K820-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K820-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K820 ERROR '    WS-K820-STATUS                  
              MOVE     WS-K820-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4000-CREATE-SMS.                                                         
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   BP13F203-REC.                   
           MOVE K800-NUM-ALLO-CAT          TO   F203-NUM-ALLO-MODE.             
           MOVE K800-NUM-NEW-TOWN          TO   F203-NUM-NEW-TOWN.              
           MOVE K800-NUM-FLAT-TYPE         TO   F203-NUM-FLAT-TYPE.             
           MOVE FC50-TYPE-APPMT            TO   F203-TYPE-APPMT.                
           MOVE K800-NUM-CAT               TO   F203-NUM-ETHNIC.                
           MOVE FC50-NUM-REGN              TO   F203-NUM-REGN.                  
           MOVE K800-DTE-BALLOT            TO   F203-DTE-BALLOT.                
           MOVE K800-DTE-BK-APPT           TO   F203-DTE-APPMT.                 
           MOVE K800-NUM-NT-FT-QUEUE       TO   F203-NUM-QUEUE.                 
           MOVE K800-TME-BK-APPT           TO   F203-TME-APPMT.                 
           MOVE K800-NUM-NRIC1             TO   F203-NUM-NRIC.                  
           MOVE WS-NME-OCCUP               TO   F203-NME-NRIC.                  
           MOVE FC50-NUM-HP                TO   F203-NUM-HP.                    
           MOVE 'HA1'                      TO   F203-NUM-APLCNT.                
           MOVE K800-NUM-ZONE              TO   F203-NUM-ZONE.                  
           MOVE 'A'                        TO   F203-NUM-STATUS.                
                                                                                
           PERFORM 4010-BP13F203-WRITE     THRU 4010-EXIT.                      
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4010-BP13F203-WRITE.                                                     
      *=============================================================            
                                                                                
           WRITE BP13F203-REC.                                                  
                                                                                
           EVALUATE WS-F203-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              ADD 1                        TO   WS-CNT-F203-WRT                 
                                                                                
           WHEN 22                                                              
              MOVE 'DUPLICATE RECORD IN SMS FILE'                               
                                           TO   WS-NUM-DESC                     
              PERFORM 8900-PRINT-ERROR     THRU 8900-EXIT                       
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'WRITE BP13F203 ERROR '   WS-F203-STATUS                  
              MOVE     WS-F203-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4100-UPDATE-PCDSMS.                                                      
      *=============================================================            
                                                                                
           MOVE WS-DTE-CURR-8              TO   FC50-DTE-SMS-SENT.              
           MOVE BP13FC50-REC               TO   BP13KC50-REC.                   
                                                                                
           PERFORM 4110-BP13KC50-REWRITE   THRU 4110-EXIT.                      
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4110-BP13KC50-REWRITE.                                                   
      *=============================================================            
                                                                                
           REWRITE BP13KC50-REC.                                                
                                                                                
           EVALUATE WS-KC50-STATUS                                              
           WHEN 00                                                              
              ADD 1                        TO   WS-CNT-KC50-UPD                 
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'REWRITE BP13KC50 ERROR ' WS-KC50-STATUS                  
              MOVE     WS-KC50-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4110-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8900-PRINT-ERROR.                                                        
      *=============================================================            
                                                                                
           IF WS-CNT-LH61A-LINE > 54                                            
              PERFORM 8910-PRINT-ERROR-HDR THRU 8910-EXIT                       
           END-IF.                                                              
                                                                                
           ADD  1                          TO   WS-CNT-LH61A-SNO                
                                                WS-CNT-LH61A-LINE               
                                                WS-CNT-FC50-SKIP.               
                                                                                
           MOVE WS-CNT-LH61A-SNO           TO   LH61A-SNO.                      
           MOVE FC50-NUM-REGN              TO   LH61A-NUM-REGN.                 
           MOVE WS-NUM-DESC                TO   LH61A-NUM-DESC.                 
                                                                                
           WRITE P13LH61A-REC              FROM LH61A-PR-DTL.                   
                                                                                
       8900-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8910-PRINT-ERROR-HDR.                                                    
      *=============================================================            
                                                                                
           ADD 1                           TO   WS-CNT-LH61A-PAGE.              
           MOVE WS-CNT-LH61A-PAGE          TO   LH61A-PAGE.                     
           MOVE WS-DTE-CURR-10             TO   LH61A-DTE-CURRENT.              
                                                                                
           WRITE P13LH61A-REC FROM LH61A-PR-HEAD-01 AFTER PAGE.                 
           WRITE P13LH61A-REC FROM LH61A-PR-HEAD-02.                            
           WRITE P13LH61A-REC FROM LH61A-PR-DASH-LINE AFTER 2.                  
           WRITE P13LH61A-REC FROM LH61A-PR-HEAD-03.                            
           WRITE P13LH61A-REC FROM LH61A-PR-DASH-LINE.                          
                                                                                
           MOVE SPACES                     TO   P13LH61A-REC.                   
           WRITE P13LH61A-REC.                                                  
                                                                                
           MOVE 7                          TO   WS-CNT-LH61A-LINE.              
                                                                                
       8910-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           CLOSE BP13FC50                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13F203                                                       
                 BP13KC50                                                       
                 P13LH61A.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K820 ERROR ' WS-K820-STATUS                  
              MOVE     WS-K820-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-KC50-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13KC50 ERROR ' WS-KC50-STATUS                  
              MOVE     WS-KC50-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13CH61 CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13FC50 READ      : ' WS-CNT-FC50-READ.          
           DISPLAY ' 2. NO OF BP13FC50 SKIPPED   : ' WS-CNT-FC50-SKIP.          
           DISPLAY ' 3. NO OF BP13KC50 UPDATED   : ' WS-CNT-KC50-UPD.           
           DISPLAY ' 4. NO OF BP13F203 WRITTEN   : ' WS-CNT-F203-WRT.           
           DISPLAY ' 5. NO OF P13LH61A WRITTEN   : ' WS-CNT-LH61A-WRITE.        
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
