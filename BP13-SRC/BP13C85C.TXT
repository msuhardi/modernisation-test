       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C85C.                                                  
      *DATE-WRITTEN. 07 NOV 2012.                                               
      *AUTHOR.       ALWYN BENNY.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE: MONTHLY REPORT OF GRANT RECOVERY CASES          *            
      *----------------------------------------------------------  *            
      *CHG REF   DATE       BY   DESCRIPTION                       *            
      *-------- --------   ---- -----------                        *            
      * BP134506 07112012  AB9  NEW PROGRAM                        *            
      * ========================================================== *            
                                                                                
      *-------------------------------------------------------------            
       ENVIRONMENT DIVISION.                                                    
      *-------------------------------------------------------------            
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
      *-------------------------------------------------------------            
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FD05 ASSIGN TO BP13FD05.                                  
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-REGN                      
                            FILE STATUS   IS BP13K800-STATUS.                   
                                                                                
           SELECT BP13K893 ASSIGN         TO BP13K893                           
                           ORGANIZATION   IS INDEXED                            
                           ACCESS MODE    IS DYNAMIC                            
                           RECORD KEY     IS K893-KEY-FLD                       
                           FILE STATUS    IS BP13K893-STATUS.                   
                                                                                
           SELECT BP13K022  ASSIGN       TO BP13K022                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K022-KEY-FLD                        
                            ALTERNATE RECORD KEY IS K022-NUM-REGN               
                            WITH DUPLICATES                                     
                            FILE STATUS  IS BP13K022-STATUS.                    
                                                                                
           SELECT P13F85CA ASSIGN TO P13F85CA.                                  
           SELECT BP13L85C ASSIGN TO BP13L85C.                                  
                                                                                
      *-------------------------------------------------------------            
       DATA DIVISION.                                                           
      *-------------------------------------------------------------            
       FILE SECTION.                                                            
                                                                                
       FD  BP13FD05                                                             
           RECORDING MODE  IS  F.                                               
       COPY BP13FD05.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K893                                                            
            RECORD CONTAINS 2050 CHARACTERS.                                    
       COPY BP13K893.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
       FD  P13F85CA                                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01  F85CA-REC.                                                           
           05 F85CA-SNO                    PIC X(05).                           
           05 FILLER                       PIC X(01).                           
           05 F85CA-REGNO                  PIC X(08).                           
           05 FILLER                       PIC X(01).                           
           05 F85CA-NRIC                   PIC X(09).                           
           05 FILLER                       PIC X(01).                           
           05 F85CA-AHG-AMT                PIC $$$$$99.99DB.                    
           05 FILLER                       PIC X(01).                           
           05 F85CA-SHG-AMT                PIC $$$$$99.99DB.                    
           05 FILLER                       PIC X(03).                           
           05 F85CA-REQ-DTE-CPFB           PIC X(10).                           
           05 FILLER                       PIC X(04).                           
           05 F85CA-OIC-NCK-NAME           PIC X(05).                           
           05 FILLER                       PIC X(128).                          
                                                                                
       FD  BP13L85C                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  PRINT-REC3                     PIC X(132).                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FD05-READ             PIC 9(6)   VALUE ZERO.                      
       01  WS-K800-READ             PIC 9(6)   VALUE ZERO.                      
       01  WS-K800-NOTFD            PIC 9(6)   VALUE ZERO.                      
       01  WS-CNT-F85CA-WRITE       PIC 9(6)   VALUE ZERO.                      
       01  WS-FD05-EOF              PIC X      VALUE 'N'.                       
       01  WS-K893-EOF              PIC X(01)  VALUE SPACES.                    
           88  K893-EOF                        VALUE 'Y'.                       
       01  WS-K800-REC-FND          PIC X      VALUE SPACE.                     
       01  WS-CSM-FOUND             PIC X      VALUE SPACE.                     
       01  WS-CDE-ERROR             PIC X      VALUE SPACE.                     
       01  WS-SOCA-TAG              PIC X      VALUE SPACE.                     
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  BP13K800-STATUS            PIC 9(2)       VALUE ZEROES.          
           05  BP13K893-STATUS            PIC 9(2)       VALUE ZEROES.          
           05  BP13K022-STATUS            PIC 9(2)       VALUE ZEROES.          
                                                                                
       01 WS-PREV-REGN              PIC X(08) VALUE SPACES.                     
       01 WS-PREB-CSM               PIC X(5)   VALUE SPACES.                    
       01 WS-POSB-CSM               PIC X(5)   VALUE SPACES.                    
       01 WS-CSM-NKNME              PIC X(5)   VALUE SPACES.                    
                                                                                
       01  WS-LINE-CNT1                   PIC 99         VALUE 66.              
       01  WS-PAGE-CNT1                   PIC 9(5)       VALUE ZERO.            
       01  WS-LINE-CNT2                   PIC 99         VALUE 66.              
       01  WS-PAGE-CNT2                   PIC 9(5)       VALUE ZERO.            
       01  CNT-F85CA-REC                  PIC 9(4)       VALUE ZERO.            
       01  CNT-KIV-CASES                  PIC 9(4)       VALUE ZERO.            
                                                                                
       01  WS-DTE-CUR.                                                          
           05  WS-CC                      PIC 99.                               
           05  WS-YY                      PIC 99.                               
           05  WS-MM                      PIC 99.                               
           05  WS-DD                      PIC 99.                               
                                                                                
       01  L85CA-PR-HEAD-01.                                                    
           05  L85CA-RPT-HDG       PIC X(8)      VALUE SPACES.                  
           05  FILLER              PIC X(5)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(6)      VALUE 'DATE: '.                
           05  L85CA-DATE          PIC X(10).                                   
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(6)      VALUE 'PAGE :'.                
           05  L85CA-PAGE          PIC ZZ9 .                                    
                                                                                
       01  L85CA-PR-HEAD-02A.                                                   
           05  FILLER              PIC X(40)     VALUE SPACES .                 
           05  FILLER              PIC X(40)     VALUE                          
               ' MONTHLY REPORT FOR CASES WITH REQ DATE'.                       
           05  FILLER              PIC X(29)     VALUE                          
                  '(CPFB) AND NO RECD DATE(CPFB)'.                              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
                                                                                
                                                                                
       01  L85CA-PR-HEAD-02C.                                                   
           05  FILLER              PIC X(40)     VALUE SPACES .                 
           05  FILLER              PIC X(40)     VALUE                          
               ' ERROR LIST OF CASES                   '.                       
           05  FILLER              PIC X(32)     VALUE                          
                  '                                '.                           
           05  FILLER              PIC X(22)     VALUE SPACES.                  
                                                                                
       01  L85CA-PR-HEAD-03A.                                                   
           05  FILLER              PIC X(27)                                    
                  VALUE 'S/NO  REGN_NO     NRIC     '.                          
           05  FILLER              PIC X(38)                                    
                  VALUE 'AHG_GRANT    SHG_GRANT  REQ_DATE(CPFB)'.               
           05  FILLER              PIC X(30)                                    
                  VALUE '  OIC                         '.                       
           05  FILLER              PIC X(17)                                    
                  VALUE '                 '.                                    
           05  FILLER              PIC X(22)     VALUE SPACES.                  
                                                                                
                                                                                
       01  L85CA-PR-HEAD-03C.                                                   
           05  FILLER              PIC X(27)                                    
                  VALUE ' S/NO REGN NO      NRIC    '.                          
           05  FILLER              PIC X(34)                                    
                  VALUE '          AHG GRANT   SHG GRANT   '.                   
           05  FILLER              PIC X(29)                                    
                  VALUE 'REQ DATE(CPFB) OIC         '.                          
           05  FILLER              PIC X(20)                                    
                  VALUE '   REMARK       '.                                     
           05  FILLER              PIC X(40)     VALUE SPACES.                  
                                                                                
       01  L85CA-PR-DETAILS.                                                    
           05  L85CA-SNO           PIC ZZZZ9.                                   
           05  FILLER              PIC X(01)     VALUE SPACES .                 
           05  L85CA-REG-NO        PIC X(08).                                   
           05  FILLER              PIC X(02)     VALUE SPACES .                 
           05  L85CA-NRIC          PIC X(09).                                   
           05  FILLER              PIC X(10)     VALUE SPACES .                 
           05  L85CA-AHG-AMT       PIC $$$$$99.99DB.                            
           05  FILLER              PIC X(02)     VALUE SPACES .                 
           05  L85CA-SHG-AMT       PIC $$$$$99.99DB.                            
           05  FILLER              PIC X(02)     VALUE SPACES .                 
           05  L85CA-REQ-DTE-CPFB  PIC X(10).                                   
           05  FILLER              PIC X(02)     VALUE SPACES .                 
           05  L85CA-OIC           PIC X(05).                                   
           05  FILLER              PIC X(02)     VALUE SPACES .                 
           05  L85CA-REMARK        PIC X(20).                                   
           05  FILLER              PIC X(32)     VALUE SPACES .                 
                                                                                
       01  L85CA-CONTROL-LINE.                                                  
           05  FILLER              PIC X(20)     VALUE  SPACES.                 
           05  CNTL-ITEM           PIC X(30)     VALUE  SPACES.                 
           05  CNTL-CNT            PIC ZZZZ9 .                                  
           05  FILLER              PIC X(58)     VALUE  SPACES.                 
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
                                                                                
      *-------------------------------------------------------------            
       0000-MAIN-LOGIC.                                                         
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-INITIALIZATION THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13FD05  THRU 2000-EXIT.                          
           PERFORM 3000-PROCESS-FD05   THRU 3000-EXIT                           
                   UNTIL WS-FD05-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-INITIALIZATION.                                                     
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13FD05                                                 
                       BP13K800                                                 
                       BP13K893                                                 
                       BP13K022                                                 
                OUTPUT P13F85CA                                                 
                       BP13L85C.                                                
                                                                                
           IF BP13K800-STATUS NOT = ZEROS   AND  97                             
               DISPLAY 'ERROR OPENING - BP13K800 : ' BP13K800-STATUS            
               MOVE BP13K800-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
           IF BP13K893-STATUS NOT = ZEROS   AND  97                             
               DISPLAY 'ERROR OPENING - BP13K893 : ' BP13K893-STATUS            
               MOVE BP13K893-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
           IF BP13K022-STATUS NOT = ZEROS   AND  97                             
               DISPLAY 'ERROR OPENING - BP13K022 : ' BP13K022-STATUS            
               MOVE BP13K022-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DTE-CUR.                            
           STRING WS-DTE-CUR(7:2) '/' WS-DTE-CUR(5:2) '/'                       
                  WS-DTE-CUR(1:4)                                               
              DELIMITED BY SIZE INTO L85CA-DATE.                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13FD05.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13FD05 AT END                                                 
                MOVE 'Y' TO WS-FD05-EOF                                         
                NOT AT END                                                      
                  ADD 1 TO WS-FD05-READ                                         
                  IF WS-FD05-READ = 1                                           
                     MOVE FD05-NUM-REGN              TO  WS-PREV-REGN           
                  END-IF                                                        
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-FD05.                                                       
      *-------------------------------------------------------------            
                                                                                
           PERFORM 3100-READ-BP13K800      THRU 3100-EXIT.                      
           IF WS-K800-REC-FND = 'Y'                                             
              PERFORM 3400-READ-BP13K022   THRU 3400-EXIT                       
              PERFORM 3500-PROCESS         THRU 3500-EXIT UNTIL                 
                      FD05-NUM-REGN NOT = WS-PREV-REGN OR                       
                      WS-FD05-EOF       = 'Y'                                   
              MOVE    FD05-NUM-REGN       TO   WS-PREV-REGN                     
           ELSE                                                                 
               PERFORM 800-ERROR-LIST           THRU  800-EXIT                  
               PERFORM 2000-READ-BP13FD05       THRU  2000-EXIT                 
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
                                                                                
              MOVE 'N'              TO WS-K800-REC-FND.                         
              MOVE SPACES           TO WS-SOCA-TAG.                             
              MOVE FD05-NUM-REGN    TO K800-NUM-REGN.                           
                                                                                
              READ BP13K800.                                                    
                                                                                
              EVALUATE BP13K800-STATUS                                          
              WHEN '00'                                                         
                    MOVE 'Y'      TO    WS-K800-REC-FND                         
                    ADD  1        TO    WS-K800-READ                            
              WHEN  '23'                                                        
                     PERFORM 3150-READ-SOC-HIST   THRU  3150-EXIT               
              WHEN  OTHER                                                       
                     MOVE 'N'     TO     WS-K800-REC-FND                        
                                         WS-K800-REC-FND                        
                     MOVE  BP13K800-STATUS TO RETURN-CODE                       
                     PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                  
              END-EVALUATE.                                                     
                                                                                
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3150-READ-SOC-HIST.                                                      
      *-------------------------------------------------------------            
                                                                                
            MOVE SPACES         TO K893-KEY-FLD.                                
            MOVE K800-NUM-REGN  TO K893-NUM-REGN-HIST                           
            PERFORM 3200-START-BP13K893  THRU 3200-EXIT                         
            IF NOT K893-EOF                                                     
               PERFORM 3300-READ-BP13K893 THRU 3300-EXIT                        
               IF K893-NUM-REGN-HIST = K800-NUM-REGN                            
                  MOVE BP13K893-MASTER(1:2000) TO                               
                                          BP13K800-MASTER                       
                  MOVE 'Y'            TO   WS-K800-REC-FND                      
                  ADD  1              TO   WS-K800-READ                         
                END-IF                                                          
            END-IF.                                                             
                                                                                
                                                                                
       3150-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3200-START-BP13K893.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE 'N'                               TO WS-K893-EOF.               
                                                                                
           START BP13K893 KEY >= K893-KEY-FLD.                                  
                                                                                
           IF BP13K893-STATUS = 00 OR 02                                        
              CONTINUE                                                          
           ELSE                                                                 
              IF BP13K893-STATUS = 23 OR 10                                     
                 MOVE 'Y'                         TO WS-K893-EOF                
              ELSE                                                              
                 DISPLAY 'BP13K893 - ERROR STARTING : ' BP13K893-STATUS         
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3300-READ-BP13K893.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K893 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'    TO WS-K893-EOF                                      
                GO          TO 3300-EXIT                                        
           END-READ.                                                            
                                                                                
           IF BP13K893-STATUS = 00 OR 02                                        
              CONTINUE                                                          
           ELSE                                                                 
              IF BP13K893-STATUS = 23 OR 10                                     
                 MOVE 'Y'                         TO WS-K893-EOF                
              ELSE                                                              
                 DISPLAY 'BP13K893 - ERROR READING NEXT : '                     
                         BP13K893-STATUS                                        
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3400-READ-BP13K022.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE K800-NUM-REGN TO K022-NUM-REGN.                                 
           MOVE SPACES        TO WS-PREB-CSM WS-POSB-CSM WS-CSM-NKNME           
                                             WS-CSM-FOUND.                      
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           IF BP13K022-STATUS = 00 OR 02                                        
              MOVE 'Y'                     TO   WS-CSM-FOUND                    
              PERFORM  3450-GET-CSM        THRU 3450-EXIT                       
           ELSE                                                                 
           IF BP13K022-STATUS = 23                                              
              DISPLAY '> CSM FOR ' K022-NUM-REGN ' NOT FOUND.'                  
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K022, STATUS ' BP13K022-STATUS         
              MOVE BP13K022-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                        
                                                                                
       3400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3450-GET-CSM.                                                            
      *-------------------------------------------------------------            
                                                                                
            IF K022-NUM-OIC NOT = SPACES AND LOW-VALUES                         
               IF K022-NUM-OIC-APPL = SPACES OR LOW-VALUES                      
                  MOVE K022-NUM-OIC        TO WS-PREB-CSM                       
               ELSE                                                             
                  MOVE K022-NUM-OIC-APPL   TO WS-PREB-CSM                       
               END-IF                                                           
            END-IF.                                                             
           IF K022-NUM-OIC-APPL = SPACES OR LOW-VALUES                          
              MOVE SPACES                          TO WS-POSB-CSM               
           ELSE                                                                 
              MOVE K022-NUM-OIC                    TO WS-POSB-CSM               
           END-IF.                                                              
           IF K800-NUM-PROV-TAG = 'Y'                                           
              MOVE SPACES         TO    WS-POSB-CSM                             
              MOVE WS-PREB-CSM    TO    WS-CSM-NKNME                            
           ELSE                                                                 
              IF WS-POSB-CSM NOT = SPACES                                       
                 MOVE WS-POSB-CSM  TO   WS-CSM-NKNME                            
              ELSE                                                              
                 MOVE WS-PREB-CSM  TO   WS-CSM-NKNME                            
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3450-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3500-PROCESS.                                                            
      *-------------------------------------------------------------            
                                                                                
           IF K800-NUM-STATUS    = 'C'                                          
              IF K800-NUM-ALLOC-TAG = 'RG'                                      
                 PERFORM      4000-WRITE-F85CA THRU  4000-EXIT                  
              ELSE                                                              
                   MOVE 'C'     TO     WS-CDE-ERROR                             
                   PERFORM 800-ERROR-LIST           THRU  800-EXIT              
               END-IF                                                           
            ELSE                                                                
                 MOVE 'S'     TO     WS-CDE-ERROR                               
                 PERFORM 800-ERROR-LIST           THRU  800-EXIT                
            END-IF.                                                             
            PERFORM     2000-READ-BP13FD05   THRU  2000-EXIT.                   
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       4000-WRITE-F85CA.                                                        
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                TO  F85CA-REC.                            
           INITIALIZE                     F85CA-REC.                            
           ADD 1                      TO  WS-CNT-F85CA-WRITE.                   
           IF WS-CNT-F85CA-WRITE = 1                                            
              WRITE F85CA-REC  FROM L85CA-PR-HEAD-03A                           
           END-IF.                                                              
                                                                                
           PERFORM  4100-WRITE-F85CA  THRU    4100-EXIT.                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4100-WRITE-F85CA.                                                        
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                TO  F85CA-REC.                            
           INITIALIZE                     F85CA-REC.                            
           IF FD05-AMT-AHG-GRANT IS NOT NUMERIC                                 
              MOVE ZEROS               TO  FD05-AMT-AHG-GRANT                   
           END-IF.                                                              
           IF FD05-AMT-SHG-GRANT IS NOT NUMERIC                                 
              MOVE ZEROS               TO  FD05-AMT-SHG-GRANT                   
           END-IF.                                                              
                                                                                
           ADD 1 TO CNT-F85CA-REC.                                              
           MOVE  CNT-F85CA-REC         TO F85CA-SNO.                            
           MOVE  WS-PREV-REGN          TO F85CA-REGNO.                          
           MOVE  FD05-NUM-NRIC         TO F85CA-NRIC.                           
           MOVE  FD05-AMT-AHG-GRANT    TO F85CA-AHG-AMT.                        
           MOVE  FD05-AMT-SHG-GRANT    TO F85CA-SHG-AMT.                        
           MOVE  WS-CSM-NKNME          TO F85CA-OIC-NCK-NAME.                   
           STRING FD05-DTE-REQ-GRANT-USED-CPFB (7:2) '-'                        
                  FD05-DTE-REQ-GRANT-USED-CPFB (5:2) '-'                        
                  FD05-DTE-REQ-GRANT-USED-CPFB (1:4)                            
                                    DELIMITED BY SIZE INTO                      
                  F85CA-REQ-DTE-CPFB.                                           
                                                                                
           WRITE F85CA-REC.                                                     
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       800-ERROR-LIST.                                                          
      *-------------------------------------------------------------            
                                                                                
           MOVE  SPACES                TO WS-SOCA-TAG.                          
           IF WS-LINE-CNT2 > 46                                                 
               MOVE 1 TO WS-LINE-CNT2                                           
               PERFORM 900-ERROR-HEADING THRU 900-EXIT                          
           ELSE                                                                 
               ADD 1 TO WS-LINE-CNT2.                                           
                                                                                
           IF FD05-AMT-AHG-GRANT IS NOT NUMERIC                                 
              MOVE ZEROS               TO  FD05-AMT-AHG-GRANT                   
           END-IF.                                                              
           IF FD05-AMT-SHG-GRANT IS NOT NUMERIC                                 
              MOVE ZEROS               TO  FD05-AMT-SHG-GRANT                   
           END-IF.                                                              
                                                                                
           ADD 1 TO CNT-KIV-CASES.                                              
           MOVE  CNT-KIV-CASES         TO L85CA-SNO.                            
           MOVE  WS-PREV-REGN          TO L85CA-REG-NO.                         
           MOVE  FD05-NUM-NRIC         TO L85CA-NRIC.                           
           MOVE  FD05-AMT-AHG-GRANT    TO L85CA-AHG-AMT.                        
           MOVE  FD05-AMT-SHG-GRANT    TO L85CA-SHG-AMT.                        
           MOVE  WS-CSM-NKNME          TO L85CA-OIC.                            
           EVALUATE WS-CDE-ERROR                                                
              WHEN 'C'                                                          
                   MOVE 'REJ CODE NOT MATCH'     TO L85CA-REMARK                
              WHEN 'R'                                                          
                   MOVE 'REC NOT IN SOC MAST  '  TO  L85CA-REMARK               
              WHEN 'S'                                                          
                   MOVE 'NOT A CANCELLED CASE'  TO  L85CA-REMARK                
           END-EVALUATE.                                                        
           STRING FD05-DTE-REQ-GRANT-USED-CPFB (7:2) '-'                        
                  FD05-DTE-REQ-GRANT-USED-CPFB (5:2) '-'                        
                  FD05-DTE-REQ-GRANT-USED-CPFB (1:4)                            
                                    DELIMITED BY SIZE INTO                      
                  L85CA-REQ-DTE-CPFB.                                           
                                                                                
           WRITE PRINT-REC3 FROM L85CA-PR-DETAILS.                              
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       900-ERROR-HEADING.                                                       
      *-------------------------------------------------------------            
                                                                                
            ADD 1         TO WS-PAGE-CNT2.                                      
            MOVE WS-PAGE-CNT2 TO L85CA-PAGE.                                    
            MOVE SPACES   TO PRINT-REC3                                         
            WRITE PRINT-REC3 AFTER PAGE.                                        
            MOVE 'BP13L85C'              TO L85CA-RPT-HDG.                      
            WRITE PRINT-REC3 FROM L85CA-PR-HEAD-01 AFTER 2.                     
            WRITE PRINT-REC3 FROM L85CA-PR-HEAD-02C AFTER 3.                    
            WRITE PRINT-REC3 FROM L85CA-PR-HEAD-03C AFTER 3.                    
            MOVE ALL '-'  TO PRINT-REC3.                                        
            WRITE PRINT-REC3 .                                                  
            MOVE SPACES   TO PRINT-REC3.                                        
            WRITE PRINT-REC3 AFTER 1.                                           
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
                                                                                
           DISPLAY '- BP13C85B CONTROL TOTAL --------'.                         
           DISPLAY ' NO OF REC READ FROM BP13FD05  = '                          
                     WS-FD05-READ.                                              
           DISPLAY ' NO OF CASES WRITTEN TO P13F85CA   = '                      
                     WS-CNT-F85CA-WRITE.                                        
           DISPLAY ' NO OF SKIPED CASES            = '                          
                     CNT-KIV-CASES.                                             
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE       BP13FD05                                                 
                       BP13K800                                                 
                       BP13K893                                                 
                       BP13K022                                                 
                       P13F85CA                                                 
                       BP13L85C.                                                
                                                                                
           IF BP13K800-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K800 - ERROR CLOSING : ' BP13K800-STATUS             
              MOVE BP13K800-STATUS              TO RETURN-CODE                  
           END-IF.                                                              
                                                                                
           IF BP13K893-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K893 - ERROR CLOSING : ' BP13K893-STATUS             
              MOVE BP13K893-STATUS             TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           IF BP13K022-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K022 - ERROR CLOSING : ' BP13K022-STATUS             
              MOVE BP13K022-STATUS             TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
