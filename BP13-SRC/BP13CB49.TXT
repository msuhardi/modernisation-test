       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB49.                                                 
      *AUTHOR.        PAULO CAMIA LEGASPI                                       
      *                                                                         
      ************************************************************              
      *                   SOC SYSTEM (SOC)                       *              
      *==========================================================*              
      * DATE WRITTEN      : 15 APR 2011                          *              
      *                                                          *              
      * 1. OBJECTIVE      : TO READ BP13FD15 UNTIL EOF AND       *              
      *                   : MATCH TO AB03K030 BY SCHEME-ACCT-NO. *              
      *                                                          *              
      *==========================================================*              
      *  MODIFICATIONS:                                          *              
      *                                                          *              
      *  CHGE-NO  DATE       OIC  DESCRIPTION                    *              
      *  -------- ---------- ---- ------------------------------ *              
      *  BP134213 14/04/2011 PCL3 NEW PROGRAM.                   *              
      *  BP134372 03/10/2011 ZDD1 ADD NEW FORM FOR REMIDER LETTER*              
      *  BP134862 08/14/2013 PCL3 TO PRINT NEW FIRE INSURANCE LTR*              
      *                           BASED ON AB03 LOAN AMOUNT      *              
      *  BP135262 18/03/2014 PCL4 TO CONVERT BP13LB49 TO EMAIL   *              
      ************************************************************              
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FD15     ASSIGN        TO BP13FD15.                       
                                                                                
                                                                                
           SELECT AB03K030     ASSIGN        TO AB03K030                        
                               ACCESS MODE   IS RANDOM                          
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K030-NUM-HDB-REF                
                               FILE STATUS   IS WS-K030-STATUS.                 
                                                                                
           SELECT BP13KD00     ASSIGN        TO BP13KD00                        
                               ACCESS MODE   IS RANDOM                          
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS KD00-KEY-FLD                    
                               FILE STATUS   IS WS-KD00-STATUS.                 
                                                                                
           SELECT BP13KD03     ASSIGN        TO BP13KD03                        
                               ACCESS MODE   IS RANDOM                          
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS KD03-KEY-FLD                    
                               FILE STATUS   IS WS-KD03-STATUS.                 
                                                                                
           SELECT AB03K700     ASSIGN        TO AB03K700                        
                               ACCESS MODE   IS RANDOM                          
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K700-NUM-HDB-REF                
                               FILE STATUS   IS WS-K700-STATUS.                 
                                                                                
           SELECT BP13LB49     ASSIGN        TO BP13LB49.                       
           SELECT BP13PB49     ASSIGN        TO BP13PB49.                       
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13FD15                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 300 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       COPY BP13FD15.                                                           
                                                                                
       FD  AB03K030                                                             
                               RECORD CONTAINS 800 CHARACTERS.                  
       COPY AB03K030.                                                           
                                                                                
       FD  BP13KD00                                                             
                               RECORD CONTAINS 1600 CHARACTERS.                 
       COPY BP13KD00.                                                           
                                                                                
       FD  BP13KD03                                                             
                               RECORD CONTAINS 1600 CHARACTERS.                 
       COPY BP13KD03.                                                           
                                                                                
       FD  AB03K700                                                             
                               RECORD CONTAINS 500  CHARACTERS.                 
       COPY AB03K700.                                                           
                                                                                
       FD  BP13LB49                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORDING MODE IS V                              
                               LABEL RECORD IS STANDARD.                        
       01  BP13LB49-REC                  PIC X(132).                            
                                                                                
       FD  BP13PB49                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 90  CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       01  BP13PB49-REC                  PIC X(90).                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILE-STATUS.                                                         
           05  WS-K030-STATUS            PIC 9(2)  VALUE ZEROES.                
           05  WS-KD00-STATUS            PIC 9(2)  VALUE ZEROES.                
           05  WS-KD03-STATUS            PIC 9(2)  VALUE ZEROES.                
           05  WS-K700-STATUS            PIC 9(2)  VALUE ZEROES.                
                                                                                
       01  WS-K030-FOUND                 PIC X(1)  VALUE 'N'.                   
       01  WS-KD00-FOUND                 PIC X(1)  VALUE 'N'.                   
       01  WS-HDB-LOAN                   PIC X(1)  VALUE 'N'.                   
       01  WS-AMT-INSURED                PIC ZZZ,ZZ9.                           
       01  WS-AMT-PREMIUM                PIC ZZ9.99.                            
                                                                                
                                                                                
       01  END-OF-FILE-FLAGS.                                                   
           05  WS-FD15-EOF               PIC X     VALUE 'N'.                   
                                                                                
       01  COUNTERS.                                                            
           05  WS-FD15-READ              PIC 9(6)  VALUE ZEROES.                
           05  WS-K030-NFND              PIC 9(6)  VALUE ZEROES.                
           05  WS-K030-FND               PIC 9(6)  VALUE ZEROES.                
           05  WS-CNT-WRITE              PIC 9(6)  VALUE ZEROES.                
           05  WS-CNT-WRT-LTR            PIC 9(6)  VALUE ZEROES.                
           05  WS-CNT-NOLOAN             PIC 9(6)  VALUE ZEROES.                
           05  WS-SNO                    PIC 9(6)  VALUE ZEROES.                
                                                                                
       01  WS-CURRENT-DATE               PIC X(8)  VALUE SPACES.                
       01  WS-NRIC                       PIC X(9)  VALUE SPACES.                
       01  WS-NAME                       PIC X(66) VALUE SPACES.                
                                                                                
       01  WS-ADDRESS.                                                          
           05 P49-BLK                    PIC X(04) VALUE 'BLK '.                
           05 P49-BLK-NO                 PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X     VALUE SPACES.                
           05 P49-FLAT-NO.                                                      
              10 FILLER                  PIC X     VALUE '#'.                   
              10 P49-LVL-NO              PIC X(2)  VALUE SPACES.                
              10 FILLER                  PIC X     VALUE '-'.                   
              10 P49-UNIT-NO             PIC X(4)  VALUE SPACES.                
           05 FILLER                     PIC X(1)  VALUE SPACES.                
           05 P49-STREET                 PIC X(47) VALUE SPACES.                
           05 P49-SINGAPORE              PIC X(09) VALUE 'SINGAPORE'.           
           05 FILLER                     PIC X(1)  VALUE SPACES.                
           05 P49-OPEN                   PIC X(01) VALUE '('.                   
           05 P49-POSTAL-CODE            PIC X(6)  VALUE SPACES.                
           05 P49-CLOSE                  PIC X(01) VALUE ')'.                   
                                                                                
       01  WS-PARSING.                                                          
           05 WS-STRING-TO-PARSE            PIC X(90).                          
           05 WS-PARSED-STRING              PIC X(90).                          
           05 WS-STRING-LENGTH              PIC 9(03).                          
           05 WS-CUR-POS-SP                 PIC 9(03).                          
           05 WS-CUR-POS-PS                 PIC 9(03).                          
           05 WS-SPACE-FLAG                 PIC X    VALUE 'N'.                 
           05 WS-INSERT-SPACE-FLAG          PIC X    VALUE 'N'.                 
                                                                                
      *---------------------------------------------------------------*         
      *       HEADINGS FOR REPORT                                     *         
      *---------------------------------------------------------------*         
                                                                                
       01  LB49-HEADERA.                                                        
           05  LB49-TITLE-HDR      PIC X(8)      VALUE 'BP13LB49'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  LB49-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LB49-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
       01  LB49-HEADERB.                                                        
           05  FILLER              PIC X(45)     VALUE SPACES.                  
           05  FILLER              PIC X(47)     VALUE                          
               'REPORT ON BP13FD15 '.                                           
                                                                                
       01  LB49-HEADERC.                                                        
           05  FILLER                PIC X(050)      VALUE                      
             '   SNO PRJ BLK   UNIT    LVL REGN NO  SCH ACC     '.              
           05  FILLER                PIC X(050)      VALUE                      
             'DTE-IND-TP HDB LOAN  NFND IN SMS'.                                
                                                                                
       01  LB49-DETAIL.                                                         
           03  FILLER                PIC X(01)          VALUE SPACES.           
           03  LB49-SNO              PIC ZZZZ9          VALUE ZEROES.           
           03  FILLER                PIC X(01)          VALUE SPACES.           
           03  LB49-PRJCT            PIC X(03)          VALUE SPACES.           
           03  FILLER                PIC X(01)          VALUE SPACES.           
           03  LB49-BLK              PIC X(05)          VALUE SPACES.           
           03  FILLER                PIC X(01)          VALUE SPACES.           
           03  LB49-UNIT             PIC X(07)          VALUE SPACES.           
           03  FILLER                PIC X(01)          VALUE SPACES.           
           03  LB49-LEVEL            PIC X(03)          VALUE SPACES.           
           03  FILLER                PIC X(01)          VALUE SPACES.           
           03  LB49-REGN-NO          PIC X(08)          VALUE SPACES.           
           03  FILLER                PIC X(01)          VALUE SPACES.           
           03  LB49-SCH-ACC          PIC X(11)          VALUE SPACES.           
           03  FILLER                PIC X(01)          VALUE SPACES.           
           03  LB49-IND-TP           PIC X(10)          VALUE SPACES.           
           03  FILLER                PIC X(04)          VALUE SPACES.           
           03  LB49-HDB-LOAN         PIC X(01)          VALUE SPACES.           
           03  FILLER                PIC X(08)          VALUE SPACES.           
           03  LB49-NFND-SMS         PIC X(01)          VALUE SPACES.           
                                                                                
      *-------------------------------------------------------------            
      *    LETTER LAYOUT                                                        
      *-------------------------------------------------------------            
       01  PB49-LINE01.                                                         
           05 FILLER                     PIC X(72) VALUE SPACES.                
           05 PB49-REGN-NO               PIC X(8)  VALUE SPACES.                
                                                                                
       01  PB49-LINE02.                                                         
           05 FILLER                     PIC X(72) VALUE SPACES.                
           05 PB49-SCH-ACC               PIC X(14) VALUE SPACES.                
                                                                                
       01  PB49-LINE03.                                                         
           05 FILLER                     PIC X(72) VALUE SPACES.                
           05 PB49-DATE                  PIC X(10) VALUE SPACES.                
                                                                                
       01  PB49-LINE04.                                                         
           05 FILLER                     PIC X(11)  VALUE SPACES.               
           05 PB49-NAME1                 PIC X(66)  VALUE SPACES.               
                                                                                
       01  PB49-LINE04B.                                                        
           05 FILLER                     PIC X(11)  VALUE SPACES.               
           05 PB49-NAME2                 PIC X(66)  VALUE SPACES.               
                                                                                
       01  PB49-LINE05.                                                         
           05 FILLER                     PIC X(11)  VALUE SPACES.               
           05 PB49-BLK                   PIC X(10)  VALUE SPACES.               
           05 FILLER                     PIC X(2)   VALUE SPACES.               
           05 PB49-FLAT-NO.                                                     
              15  PB49-CHAR              PIC X      VALUE SPACE.                
              15  PB49-LEVEL             PIC X(2)   VALUE SPACES.               
              15  PB49-DASH              PIC X      VALUE SPACE.                
              15  PB49-HSE-MAIN          PIC X(4)   VALUE SPACES.               
              15  PB49-HSE-SUB           PIC X(3)   VALUE SPACES.               
                                                                                
       01  PB49-LINE05B.                                                        
           05 FILLER                     PIC X(11) VALUE SPACES.                
           05 PB49-STREET                PIC X(47) VALUE SPACES.                
                                                                                
       01  PB49-LINE05C.                                                        
           05  FILLER                   PIC X(11) VALUE SPACES.                 
           05  PB49-ADDR                PIC X(20) VALUE SPACES.                 
                                                                                
       01  PB49-LINE06.                                                         
           05  FILLER                    PIC X(10) VALUE SPACES.                
           05  PB49-BAR-CODE             PIC X(09) VALUE SPACES.                
                                                                                
       01  PB49-LINE07.                                                         
           05  FILLER                    PIC X(10) VALUE SPACES.                
           05  PB49-BOOK-UNIT            PIC X(80) VALUE SPACES.                
                                                                                
       01  PB49-LINE08.                                                         
           05  FILLER                    PIC X(55) VALUE SPACES.                
           05  PB49-SUM-AMT              PIC X(07) VALUE SPACES.                
           05  FILLER                    PIC X(14) VALUE SPACES.                
           05  PB49-PREMIUM              PIC X(06) VALUE SPACES.                
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR AV02C015                                                   
      *-------------------------------------------------------------            
        01  WS-LINK-AV02C015.                                                   
            05  AV02C015-IN-POSTCODE          PIC X(6).                         
            05  AV02C015-OUT-POSTCODE-CHKSUM  PIC X(9).                         
                                                                                
       COPY AV02COMM.                                                           
                                                                                
      *-------------------------------------------------------*                 
      *    COPY BOOK FOR SUBROUTINE - BP13C913.               *                 
      *-------------------------------------------------------*                 
       COPY P13COMM8.                                                           
                                                                                
      *-------------------------------------------------------*                 
      *    COPY BOOK FOR SUBROUTINE - AB20C900.               *                 
      *-------------------------------------------------------*                 
       COPY AB20M97C.                                                           
                                                                                
      *-------------------------------------------------------*                 
      *    LINKAGE VARIABLES                                  *                 
      *-------------------------------------------------------*                 
       01   WS-LINK-REC.                                                        
            05 WS-LINK-NUM-SCH            PIC X(4).                             
            05 WS-LINK-NUM-ACC            PIC X(4).                             
            05 WS-LINK-NUM-CHK            PIC X(1).                             
                                                                                
       PROCEDURE DIVISION.                                                      
      *************************************************************             
       0000-MAIN.                                                               
      *************************************************************             
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 1100-READ-BP13FD15   THRU 1100-EXIT.                         
           PERFORM 1200-WRITE-HEADER    THRU 1200-EXIT.                         
           PERFORM 2000-PROCESS-RECORDS THRU 2000-EXIT                          
             UNTIL WS-FD15-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       1000-OPEN-ROUTINE.                                                       
      *************************************************************             
                                                                                
           OPEN INPUT  BP13FD15                                                 
                       AB03K030                                                 
                       BP13KD00                                                 
                       BP13KD03                                                 
                       AB03K700                                                 
                OUTPUT BP13LB49                                                 
                       BP13PB49.                                                
                                                                                
           IF WS-K030-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'AB03K030 OPEN ERROR ' WS-K030-STATUS                     
              MOVE WS-K030-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-KD00-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13KD00 OPEN ERROR ' WS-KD00-STATUS                     
              MOVE WS-KD00-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-KD03-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13KD03 OPEN ERROR ' WS-KD03-STATUS                     
              MOVE WS-KD03-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K700-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'AB03K700 OPEN ERROR ' WS-K700-STATUS                     
              MOVE WS-K700-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       1100-READ-BP13FD15.                                                      
      *************************************************************             
                                                                                
           READ BP13FD15                                                        
             AT END                                                             
                MOVE 'Y'                 TO WS-FD15-EOF                         
            NOT AT END                                                          
                ADD 1                    TO WS-FD15-READ                        
           END-READ.                                                            
                                                                                
       1100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       1200-WRITE-HEADER.                                                       
      *************************************************************             
                                                                                
           MOVE 1                           TO LB49-PAGE-HDR.                   
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO WS-CURRENT-DATE.                 
                                                                                
           STRING WS-CURRENT-DATE(7:2) '/'                                      
                  WS-CURRENT-DATE(5:2) '/'                                      
                  WS-CURRENT-DATE(1:4)                                          
                  DELIMITED BY SIZE                                             
             INTO LB49-DATE-HDR.                                                
                                                                                
           MOVE LB49-DATE-HDR              TO PB49-DATE.                        
                                                                                
           WRITE BP13LB49-REC            FROM LB49-HEADERA.                     
           WRITE BP13LB49-REC            FROM LB49-HEADERB.                     
           MOVE SPACES                     TO BP13LB49-REC.                     
           WRITE BP13LB49-REC.                                                  
           WRITE BP13LB49-REC            FROM LB49-HEADERC.                     
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       2000-PROCESS-RECORDS.                                                    
      *************************************************************             
                                                                                
           PERFORM 3000-READ-AB03K030           THRU 3000-EXIT.                 
                                                                                
           IF WS-K030-FOUND = 'Y'                                               
              IF K030-DTE-FI-EFF = SPACES OR LOW-VALUES                         
                 IF K030-CDE-ACCT-STAT = 'C'                                    
                   IF K030-AMT-LOAN-GRANTD > 0                                  
                     PERFORM 4000-PRINT-REPORT      THRU 4000-EXIT              
      *              PERFORM 5000-PRINT-LETTER      THRU 5000-EXIT              
                   END-IF                                                       
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 1100-READ-BP13FD15           THRU 1100-EXIT.                 
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3000-READ-AB03K030.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                   TO AB03K030-REC.                       
           INITIALIZE                       AB03K030-REC.                       
                                                                                
           MOVE 'N'                      TO WS-K030-FOUND                       
                                            WS-HDB-LOAN.                        
           MOVE FD15-NUM-SCH-ACC         TO K030-NUM-HDB-REF.                   
                                                                                
           READ AB03K030.                                                       
                                                                                
           EVALUATE WS-K030-STATUS                                              
               WHEN 00                                                          
                    MOVE 'Y'             TO WS-K030-FOUND                       
                                                                                
                    IF K030-AMT-LOAN-BAL  > 0                                   
                       MOVE 'Y'              TO WS-HDB-LOAN                     
                    END-IF                                                      
                                                                                
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1                TO WS-K030-NFND                        
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'AB03K030 READ ERROR ' WS-K030-STATUS               
                    DISPLAY 'K030 KEY: ' K030-NUM-HDB-REF                       
                    MOVE WS-K030-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3100-READ-BP13KD00.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                   TO BP13KD00-REC.                       
           INITIALIZE                       BP13KD00-REC.                       
                                                                                
           MOVE 'N'                      TO WS-KD00-FOUND.                      
                                                                                
           MOVE FD15-NUM-REGN            TO KD00-NUM-REGN.                      
                                                                                
           READ BP13KD00.                                                       
                                                                                
           EVALUATE WS-KD00-STATUS                                              
               WHEN 00                                                          
                    IF KD00-AMT-LOAN-GRANTED NOT NUMERIC                        
                       MOVE ZEROES           TO KD00-AMT-LOAN-GRANTED           
                    END-IF                                                      
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    PERFORM 3200-READ-BP13KD03        THRU 3200-EXIT            
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13KD00 READ ERROR ' WS-KD00-STATUS               
                    DISPLAY 'KD00 KEY: ' KD00-NUM-REGN                          
                    MOVE WS-KD00-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3200-READ-BP13KD03.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                   TO BP13KD03-REC.                       
           INITIALIZE                       BP13KD03-REC.                       
                                                                                
                                                                                
           MOVE FD15-NUM-REGN            TO KD03-NUM-REGN.                      
                                                                                
           READ BP13KD03.                                                       
                                                                                
           EVALUATE WS-KD03-STATUS                                              
               WHEN 00                                                          
                    IF KD03-AMT-LOAN-GRANTED NOT NUMERIC                        
                       MOVE ZEROES           TO KD03-AMT-LOAN-GRANTED           
                    END-IF                                                      
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13KD03 READ ERROR ' WS-KD03-STATUS               
                    DISPLAY 'KD03 KEY: ' KD03-NUM-REGN                          
                    MOVE WS-KD03-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       4000-PRINT-REPORT.                                                       
      *************************************************************             
                                                                                
           MOVE SPACES                   TO LB49-DETAIL                         
                                            BP13LB49-REC.                       
           INITIALIZE                       LB49-DETAIL                         
                                            BP13LB49-REC.                       
                                                                                
           MOVE FD15-NUM-PRJCT           TO LB49-PRJCT.                         
           MOVE FD15-NUM-BLK             TO LB49-BLK.                           
           MOVE FD15-NUM-LEVEL           TO LB49-LEVEL.                         
                                                                                
           STRING FD15-NUM-MAIN-UNIT                                            
                  FD15-NUM-SUB-UNIT                                             
                  DELIMITED BY SIZE INTO LB49-UNIT.                             
                                                                                
           MOVE FD15-NUM-REGN            TO LB49-REGN-NO.                       
           MOVE FD15-NUM-SCH-ACC         TO LB49-SCH-ACC.                       
                                                                                
           IF FD15-DTE-IND-TP NOT = SPACES AND LOW-VALUES AND ZEROES            
              STRING FD15-DTE-IND-TP(7:2) '/'                                   
                     FD15-DTE-IND-TP(5:2) '/'                                   
                     FD15-DTE-IND-TP(1:4)                                       
                     DELIMITED BY SIZE INTO LB49-IND-TP                         
           ELSE                                                                 
              MOVE SPACES                TO LB49-IND-TP                         
           END-IF.                                                              
                                                                                
           PERFORM 3100-READ-BP13KD00  THRU 3100-EXIT.                          
           MOVE WS-HDB-LOAN              TO LB49-HDB-LOAN.                      
                                                                                
           IF K030-CDE-ACCT-STAT  = 'C'                                         
              MOVE 'Y'       TO LB49-NFND-SMS                                   
           END-IF.                                                              
                                                                                
           IF WS-HDB-LOAN = 'Y'                                                 
              ADD 1                      TO WS-SNO                              
              MOVE WS-SNO                TO LB49-SNO                            
              WRITE BP13LB49-REC       FROM LB49-DETAIL                         
              ADD 1                      TO WS-CNT-WRITE                        
           ELSE                                                                 
              ADD 1                      TO WS-CNT-NOLOAN                       
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       5000-PRINT-LETTER.                                                       
      *************************************************************             
                                                                                
           PERFORM 7000-INITIALIZE-LETTER THRU 7000-EXIT                        
           MOVE SPACES                   TO AB03K700-REC.                       
           MOVE K030-NUM-HDB-REF         TO K700-NUM-HDB-REF.                   
                                                                                
           PERFORM 6000-READ-AB03K700    THRU 6000-EXIT.                        
                                                                                
           MOVE K700-NME-LESSEE1         TO PB49-NAME1.                         
           MOVE K700-NME-LESSEE2         TO PB49-NAME2.                         
                                                                                
           MOVE FD15-NUM-REGN      TO PB49-REGN-NO                              
           STRING FD15-NUM-SCH-ACC(1:4) '-' FD15-NUM-SCH-ACC(5:4)               
                '-' FD15-NUM-SCH-ACC(9:2) '-' FD15-NUM-SCH-ACC(11:1)            
           DELIMITED BY SIZE INTO PB49-SCH-ACC.                                 
                                                                                
           PERFORM 8000-GET-NRIC-ADDR   THRU 8000-EXIT.                         
                                                                                
           MOVE COMM8-CDE-TYP-ACTLUSE   TO  BM97-NUM-FLAT-TYPE.                 
           MOVE COMM8-CDE-MODL-DESG     TO  BM97-CDE-MODL-DESG.                 
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CURRENT-DATE.                  
                                                                                
           MOVE WS-CURRENT-DATE         TO  BM97-DTE-COMPARE.                   
                                                                                
           PERFORM 8600-PROCESS-PREMIUM THRU 8600-EXIT.                         
           MOVE WS-AMT-PREMIUM          TO PB49-PREMIUM.                        
           MOVE WS-AMT-INSURED          TO PB49-SUM-AMT.                        
                                                                                
           MOVE SPACES                TO  BP13PB49-REC.                         
           WRITE BP13PB49-REC         AFTER PAGE.                               
                                                                                
           WRITE BP13PB49-REC    FROM  PB49-LINE01  AFTER 1.                    
           WRITE BP13PB49-REC    FROM  PB49-LINE02.                             
           WRITE BP13PB49-REC    FROM  PB49-LINE03.                             
           WRITE BP13PB49-REC    FROM  PB49-LINE04  AFTER 3.                    
           WRITE BP13PB49-REC    FROM  PB49-LINE04B.                            
           WRITE BP13PB49-REC    FROM  PB49-LINE05  AFTER 2                     
           WRITE BP13PB49-REC    FROM  PB49-LINE05B.                            
           WRITE BP13PB49-REC    FROM  PB49-LINE05C.                            
           WRITE BP13PB49-REC    FROM  PB49-LINE06  AFTER 3.                    
           WRITE BP13PB49-REC    FROM  PB49-LINE07  AFTER 7.                    
           WRITE BP13PB49-REC    FROM  PB49-LINE08  AFTER 8.                    
                                                                                
           ADD 1   TO WS-CNT-WRT-LTR.                                           
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       6000-READ-AB03K700.                                                      
      *************************************************************             
                                                                                
           READ AB03K700.                                                       
                                                                                
           EVALUATE WS-K700-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    CONTINUE                                                    
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    DISPLAY 'AB03K700 REC NOT FOUND ' WS-K700-STATUS            
                    DISPLAY 'K700 KEY: ' K700-NUM-HDB-REF                       
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'AB03K700 READ ERROR ' WS-K700-STATUS               
                    DISPLAY 'K700 KEY: ' K700-NUM-HDB-REF                       
                    MOVE WS-K700-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       6100-CALL-AV02C015.                                                      
      *************************************************************             
           CALL 'AV02C015'          USING                                       
                                    AV02C015-IN-POSTCODE                        
                                    AV02C015-OUT-POSTCODE-CHKSUM.               
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       7000-INITIALIZE-LETTER.                                                  
      *************************************************************             
                                                                                
           MOVE SPACES          TO PB49-REGN-NO                                 
                                   PB49-SCH-ACC                                 
                                   PB49-NAME1                                   
                                   PB49-NAME2                                   
                                   PB49-BLK                                     
                                   PB49-CHAR                                    
                                   PB49-LEVEL                                   
                                   PB49-DASH                                    
                                   PB49-HSE-MAIN                                
                                   PB49-HSE-SUB                                 
                                   PB49-STREET                                  
                                   PB49-ADDR                                    
                                   PB49-BAR-CODE                                
                                   PB49-SUM-AMT                                 
                                   PB49-PREMIUM                                 
                                   P49-BLK-NO                                   
                                   P49-LVL-NO                                   
                                   P49-UNIT-NO                                  
                                   P49-STREET.                                  
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       8000-GET-NRIC-ADDR.                                                      
      *************************************************************             
                                                                                
      *  TO CALL SUBROUTINE BP13C913,GETTING ADDRESS                            
           MOVE SPACES                   TO WS-LINK-REC.                        
           MOVE K700-NUM-SCH-ACCNT       TO WS-LINK-REC.                        
           PERFORM 8500-CALL-BP13C913      THRU 8500-EXIT.                      
                                                                                
           MOVE   COMM8-NUM-BLK           TO P49-BLK-NO                         
           MOVE   COMM8-NUM-LEVEL         TO P49-LVL-NO                         
                                             PB49-LEVEL                         
           MOVE   COMM8-NUM-UNIT-MAIN     TO P49-UNIT-NO                        
                                             PB49-HSE-MAIN                      
           MOVE   COMM8-NME-STREET        TO P49-STREET                         
                                             PB49-STREET                        
           MOVE   COMM8-NUM-POSTAL-CODE   TO P49-POSTAL-CODE                    
                                                                                
           STRING 'SINGPORE ' COMM8-NUM-POSTAL-CODE DELIMITED BY SIZE           
                 INTO PB49-ADDR.                                                
           STRING 'BLK ' COMM8-NUM-BLK DELIMITED BY SIZE INTO                   
                  PB49-BLK                                                      
           MOVE '#'                    TO PB49-CHAR.                            
           MOVE '-'                    TO PB49-DASH.                            
                                                                                
           MOVE SPACES                    TO WS-LINK-AV02C015.                  
           MOVE COMM8-NUM-POSTAL-CODE     TO AV02C015-IN-POSTCODE.              
           PERFORM 6100-CALL-AV02C015     THRU 6100-EXIT.                       
           MOVE AV02C015-OUT-POSTCODE-CHKSUM TO PB49-BAR-CODE.                  
                                                                                
                                                                                
           MOVE SPACES                 TO WS-STRING-TO-PARSE.                   
           MOVE WS-ADDRESS             TO WS-STRING-TO-PARSE.                   
           MOVE LENGTH OF WS-ADDRESS                                            
                                       TO WS-STRING-LENGTH.                     
           MOVE 'Y'                    TO WS-INSERT-SPACE-FLAG.                 
           PERFORM 8010-PARSE-ADDRESS  THRU 8010-EXIT.                          
           MOVE     WS-PARSED-STRING   TO PB49-BOOK-UNIT.                       
                                                                                
       8000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       8010-PARSE-ADDRESS.                                                      
      *************************************************************             
           MOVE SPACES                    TO   WS-PARSED-STRING.                
           MOVE 1                         TO   WS-CUR-POS-PS.                   
           ADD  1                         TO   WS-STRING-LENGTH.                
           PERFORM 8020-GET-CHAR          THRU 8020-EXIT                        
              VARYING WS-CUR-POS-SP FROM 1 BY 1                                 
              UNTIL WS-CUR-POS-SP = WS-STRING-LENGTH.                           
                                                                                
                                                                                
       8010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       8020-GET-CHAR.                                                           
      *************************************************************             
           IF WS-STRING-TO-PARSE(WS-CUR-POS-SP:1) NOT = SPACES                  
              IF WS-SPACE-FLAG = 'Y' AND                                        
                 WS-INSERT-SPACE-FLAG = 'Y' AND                                 
                 WS-PARSED-STRING NOT = SPACES                                  
                 MOVE ' ' TO WS-PARSED-STRING(WS-CUR-POS-PS:1)                  
                 ADD 1           TO  WS-CUR-POS-PS                              
              END-IF                                                            
              MOVE WS-STRING-TO-PARSE(WS-CUR-POS-SP:1)                          
                              TO  WS-PARSED-STRING(WS-CUR-POS-PS:1)             
              ADD 1           TO  WS-CUR-POS-PS                                 
              MOVE    'N'      TO WS-SPACE-FLAG                                 
           ELSE                                                                 
              MOVE 'Y'     TO WS-SPACE-FLAG                                     
           END-IF.                                                              
                                                                                
       8020-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       8500-CALL-BP13C913.                                                      
      *************************************************************-            
                                                                                
           CALL 'BP13C913' USING                                                
                 WS-LINK-REC, BP13COMM8-REC                                     
           END-CALL.                                                            
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE'                       
                         COMM8-CDE-SYSERR                                       
              ELSE                                                              
                 DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR             
              END-IF                                                            
              MOVE SPACES        TO BP13COMM8-REC                               
           END-IF.                                                              
                                                                                
       8500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       8600-PROCESS-PREMIUM.                                                    
      *************************************************************             
                                                                                
           INITIALIZE                       BM97-NUM-ERROR                      
                                            BM97-NUM-SYSERR.                    
                                                                                
           CALL 'AB20C900' USING WS-BM97-AREA.                                  
                                                                                
           IF BM97-NUM-ERROR = ZEROS OR SPACES AND                              
              BM97-NUM-SYSERR = ZEROS                                           
              IF   BM97-AMT-INSURED IS NOT NUMERIC                              
                   MOVE  ZERO          TO  BM97-AMT-INSURED                     
              END-IF                                                            
              MOVE BM97-AMT-INSURED    TO  WS-AMT-INSURED                       
              IF   BM97-AMT-PREMIUM IS NOT NUMERIC                              
                   MOVE  ZERO          TO  BM97-AMT-PREMIUM                     
              END-IF                                                            
              MOVE BM97-AMT-PREMIUM    TO  WS-AMT-PREMIUM                       
           ELSE                                                                 
              MOVE BM97-NUM-SYSERR     TO  RETURN-CODE                          
              DISPLAY 'ERROR IN CALLING AB20C900, ' BM97-NUM-ERROR.             
                                                                                
       8600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       9000-CLOSE-ROUTINE.                                                      
      *************************************************************             
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY 'RUN DATE : ' WS-CURRENT-DATE.                               
           DISPLAY '*--------- BP13CB49 CONTROL TOTAL ---------*'.              
           DISPLAY '   '.                                                       
           DISPLAY 'NO. OF READS FROM FD15   : ' WS-FD15-READ.                  
           DISPLAY 'NO. OF HDB-LOAN = N      : ' WS-CNT-NOLOAN.                 
           DISPLAY 'RECS WRITTEN TO BP13LB49 : ' WS-CNT-WRITE.                  
           DISPLAY 'RECS WRITTEN TO BP13PB49 : ' WS-CNT-WRT-LTR.                
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13FD15                                                       
                 AB03K030                                                       
                 BP13KD00                                                       
                 BP13KD03                                                       
                 AB03K700                                                       
                 BP13LB49                                                       
                 BP13PB49.                                                      
                                                                                
           IF WS-K030-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'AB03K030 CLOSE ERROR ' WS-K030-STATUS                    
              MOVE WS-K030-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-KD00-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13KD00 CLOSE ERROR ' WS-KD00-STATUS                    
              MOVE WS-KD00-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-KD03-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13KD03 CLOSE ERROR ' WS-KD03-STATUS                    
              MOVE WS-KD03-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K700-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'AB03K700 CLOSE ERROR ' WS-K700-STATUS                    
              MOVE WS-K700-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
