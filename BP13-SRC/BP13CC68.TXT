      *----------------------------------------------------------------*        
       IDENTIFICATION DIVISION.                                                 
      *----------------------------------------------------------------*        
       PROGRAM-ID.    BP13CC68.                                                 
       AUTHOR.        ARIEL HERNANDEZ.                                          
       DATE-WRITTEN.  22/02/2007.                                               
                                                                                
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                   *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  TO CREATE INTEREST REBATE FOR MORTGAGE LOAN   *        
      *                  (EXISTING LOAN)                               *        
      *                                                                *        
      *   INPUT FILES :  1.  BP13FD69                                  *        
      *                  2.  SY02F001                                  *        
      *                  3.  BP13FD30                                  *        
      *                  4.  BP13KD00                                  *        
      *                                                                *        
      *   OUTPUT FILES:  1.  BP13F125                                  *        
      *                  2.  BP13LC68                                  *        
      *----------------------------------------------------------------*        
      *                                                                *        
      *   CHGE-NO   BY    DATE      DESCRIPTION                        *        
      *   --------  ---  ---------- -----------                        *        
      *   BP132888  BA3  30/01/2007 NEW PROGRAM                        *        
      *   BP133052  BA3  07/06/2007 CHANGE ROUNDING OF AMOUNT REBATE   *        
      *   BP133115  BA3  16/10/2007 ADD NUM-LOAN-SCH                   *        
      *   BP133173  BA3  26/10/2007 UPDATE KD00-AMT-ARR-INTR           *        
      *   BP133091  RB12 16/11/2007 EXPAND KD00 LENGTH FR 1200 TO 1600 *        
      *   BP133209  ED1  31/01/2008 INITIALIZE KD00-AMT-ARR-INTR       *        
      ******************************************************************        
                                                                                
      *----------------------------------------------------------------*        
       ENVIRONMENT DIVISION.                                                    
      *----------------------------------------------------------------*        
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FD69  ASSIGN        TO BP13FD69.                          
           SELECT SY02F001  ASSIGN        TO SY02F001.                          
           SELECT BP13FD30  ASSIGN        TO BP13FD30.                          
                                                                                
           SELECT BP13KD00  ASSIGN        TO BP13KD00                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KD00-KEY-FLD                       
                            FILE STATUS   IS WS-KD00-STATUS.                    
                                                                                
           SELECT BP13F125  ASSIGN        TO BP13F125.                          
           SELECT BP13LC68  ASSIGN        TO BP13LC68.                          
                                                                                
      *----------------------------------------------------------------*        
       DATA DIVISION.                                                           
      *----------------------------------------------------------------*        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13FD69                                                             
           BLOCK  CONTAINS 0   RECORDS                                          
           RECORD CONTAINS 120 CHARACTERS                                       
           LABEL  RECORDS  ARE STANDARD.                                        
                                                                                
       COPY BP13FD69.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13FD30                                                             
           BLOCK  CONTAINS 0    RECORDS                                         
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL  RECORDS  ARE  STANDARD.                                       
                                                                                
       COPY BP13FD30.                                                           
                                                                                
       FD  BP13KD00                                                             
           RECORD CONTAINS 1600 CHARACTERS.                                     
                                                                                
       COPY BP13KD00.                                                           
                                                                                
       FD  BP13F125                                                             
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL  RECORDS  ARE STANDARD.                                        
                                                                                
       COPY BP13F125.                                                           
                                                                                
       FD  BP13LC68                                                             
           BLOCK  CONTAINS    0 RECORDS                                         
           RECORD CONTAINS  132 CHARACTERS                                      
           LABEL  RECORD     IS STANDARD                                        
           RECORDING MODE    IS F.                                              
                                                                                
       01  BP13LC68-REC                 PIC X(132).                             
                                                                                
      *----------------------------------------------------------------*        
       WORKING-STORAGE SECTION.                                                 
      *----------------------------------------------------------------*        
                                                                                
       01  WS-AMT-REBATE                PIC 9(5)V99 VALUE ZEROES.               
       01  WS-AMT-REBATE-6DEC           PIC 9(5)V9(6)     VALUE ZEROES.         
       01  WS-INT-PER-DAY               PIC 9(5)V9(6)     VALUE ZEROES.         
       01  WS-DAY                       PIC 9(03)   VALUE ZEROES.               
                                                                                
       01  WS-FD30-FD                   PIC X(01)   VALUE SPACES.               
       01  WS-KD00-FD                   PIC X(01)   VALUE SPACES.               
       01  WS-EOF-FD30                  PIC X(01)   VALUE SPACES.               
       01  WS-EOF-FD69                  PIC X(01)   VALUE SPACES.               
       01  CNT-FD69-READ                PIC 9(05)   VALUE ZEROES.               
       01  WS-KD00-STATUS               PIC 9(2)    VALUE ZEROES.               
       01  CNT-FD30-READ                PIC 9(8)    VALUE ZEROES.               
       01  CNT-KD00-READ                PIC 9(8)    VALUE ZEROES.               
       01  CNT-FD30-FD69-MATCH          PIC 9(8)    VALUE ZEROES.               
       01  CNT-KD00-NOTFOUND            PIC 9(8)    VALUE ZEROES.               
       01  CNT-KD00-UPDATE              PIC 9(8)    VALUE ZEROES.               
       01  CNT-F125-WRITE               PIC 9(8)    VALUE ZEROES.               
       01  WS-PAGE                      PIC 9(4)    VALUE ZEROES.               
       01  WS-SNO                       PIC 9(4)    VALUE ZEROES.               
       01  WS-LINE                      PIC 9(2)    VALUE 60.                   
       01  WS-PP-REBATE-ALPHA           PIC X(2)    VALUE SPACES.               
       01  REDEFINES WS-PP-REBATE-ALPHA.                                        
           05 WS-PP                     PIC 9(2).                               
       01  WS-TMP-INTRATE               PIC 99V99   VALUE ZEROES.               
       01  WS-INT-FORM REDEFINES WS-TMP-INTRATE.                                
           05 WS-PCT1                   PIC X.                                  
           05 WS-PCT2                   PIC X.                                  
           05 WS-PCT3                   PIC X(2).                               
       01  WS-DTE-PP-CHQ-ALPHA          PIC X(8)    VALUE SPACES.               
       01  REDEFINES WS-DTE-PP-CHQ-ALPHA.                                       
           05  WS-DTE-PP-CHQ-NUM        PIC 9(6).                               
           05  WS-DTE-PP-CHQ-DD         PIC 9(2).                               
                                                                                
      *--------------------------------------------------------------           
      *   REPORT LAYOUT                                                         
      *--------------------------------------------------------------           
       01  LC68-PR-HEAD-01.                                                     
           05  LC68-RPTNAME        PIC X(8)      VALUE 'BP13LC68'.              
           05  FILLER              PIC X(5)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  LC68-DATE           PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  LC68-PRT-PAGE       PIC ZZZZ9     VALUE ZEROES.                  
                                                                                
       01  LC68-PR-HEAD-02.                                                     
           05  FILLER              PIC X(35)     VALUE SPACES .                 
           05  FILLER              PIC X(43)     VALUE                          
               'REPORT ON INTEREST REBATE FOR MORTGAGE LOAN'.                   
           05  FILLER              PIC X(16)     VALUE                          
               ' - EXISTING LOAN'.                                              
                                                                                
       01  LC68-PR-HEAD-03.                                                     
           05  FILLER              PIC X(20)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE                          
               'SNO'.                                                           
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE                          
               'REGN NO'.                                                       
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE                          
               'SCH'.                                                           
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(12)     VALUE                          
               '  LOAN-BAL  '.                                                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE                          
               'IT-REBATE'.                                                     
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE                          
               ' DUE-DATE '.                                                    
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(11)     VALUE                          
               'CHEQUE-SENT'.                                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(12)     VALUE                          
               '  OUTS-LOAN '.                                                  
                                                                                
       01  LC68-PR-HEAD-04.                                                     
           05  FILLER              PIC X(20)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE ALL '-'.                 
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE ALL '-'.                 
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE ALL '-'.                 
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(12)     VALUE ALL '-'.                 
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE ALL '-'.                 
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE ALL '-'.                 
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE ALL '-'.                 
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(12)     VALUE ALL '-'.                 
                                                                                
       01  LC68-PR-DTL.                                                         
           05  FILLER              PIC X(20)     VALUE SPACES.                  
           05  LC68-PRT-SNO        PIC ZZZ9      VALUE ZEROES.                  
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LC68-REGN-NO        PIC X(08)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LC68-SCH            PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LC68-LOAN           PIC Z,ZZZ,ZZ9.99    VALUE ZEROES.            
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LC68-IT-REBATE      PIC ZZ,ZZ9.99 VALUE ZEROES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LC68-DUE-DATE       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LC68-FIN-DATE       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LC68-LOAN-OUTS      PIC Z,ZZZ,ZZ9.99    VALUE ZEROES.            
                                                                                
      *----------------------------------------------------------------*        
       PROCEDURE DIVISION.                                                      
      *----------------------------------------------------------------*        
                                                                                
      *----------------------------------------------------------------*        
       0000-MAIN-ROUTINE.                                                       
      *----------------------------------------------------------------*        
                                                                                
           PERFORM  1000-OPEN-FILES        THRU    1000-EXIT.                   
           PERFORM  2000-READ-FD69         THRU    2000-EXIT.                   
           PERFORM  2500-READ-FD30         THRU    2500-EXIT.                   
           PERFORM  3000-PROCESS           THRU    3000-EXIT                    
                    UNTIL WS-EOF-FD69 = 'Y' AND                                 
                          WS-EOF-FD30 = 'Y'.                                    
           PERFORM  9000-CLOSE-ROUTINE     THRU    9000-EXIT.                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       1000-OPEN-FILES.                                                         
      *----------------------------------------------------------------*        
                                                                                
           OPEN INPUT  BP13FD69                                                 
                       SY02F001                                                 
                       BP13FD30                                                 
                I-O    BP13KD00                                                 
                OUTPUT BP13F125                                                 
                       BP13LC68.                                                
                                                                                
           IF WS-KD00-STATUS NOT = 00 AND 97                                    
               DISPLAY 'OPENING ERROR, BP13KD00-STATUS ' WS-KD00-STATUS         
               MOVE     WS-KD00-STATUS     TO RETURN-CODE                       
               PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 1500-READ-SY02F001   THRU 1500-EXIT.                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       1500-READ-SY02F001.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ SY02F001.                                                       
                                                                                
           STRING F001-DTE-CURRENT(7:2) '/'                                     
                  F001-DTE-CURRENT(5:2) '/'                                     
                  F001-DTE-CURRENT(1:4)                                         
                  DELIMITED BY SIZE INTO LC68-DATE.                             
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       2000-READ-FD69.                                                          
      *----------------------------------------------------------------*        
                                                                                
           READ BP13FD69 AT END MOVE 'Y' TO WS-EOF-FD69                         
                                MOVE HIGH-VALUES TO FD69-NUM-HDB-REF            
                                GO TO 2000-EXIT                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO CNT-FD69-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       2500-READ-FD30.                                                          
      *----------------------------------------------------------------*        
                                                                                
           READ BP13FD30 AT END MOVE 'Y' TO WS-EOF-FD30                         
                                MOVE HIGH-VALUES TO FD30-NUM-SCH-ACC            
                                GO TO 2500-EXIT                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO CNT-FD30-READ.                                              
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3000-PROCESS.                                                            
      *----------------------------------------------------------------*        
                                                                                
           MOVE ZEROES TO       WS-DAY            WS-INT-PER-DAY                
                                WS-AMT-REBATE     WS-AMT-REBATE-6DEC.           
                                                                                
           IF FD69-NUM-HDB-REF = FD30-NUM-SCH-ACC                               
                                                                                
              PERFORM 4000-COMPUTE-REBATE THRU 4000-EXIT                        
              PERFORM 2000-READ-FD69     THRU 2000-EXIT                         
              PERFORM 2500-READ-FD30     THRU 2500-EXIT                         
                                                                                
           ELSE                                                                 
              IF FD69-NUM-HDB-REF < FD30-NUM-SCH-ACC                            
                 PERFORM 2000-READ-FD69 THRU 2000-EXIT                          
                 GO TO 3000-EXIT                                                
              ELSE                                                              
                 PERFORM 2500-READ-FD30 THRU 2500-EXIT                          
                 GO TO 3000-EXIT                                                
           END-IF.                                                              
                                                                                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-COMPUTE-REBATE.                                                     
                                                                                
           MOVE BP13FD30-REC(1966:2) TO WS-PP-REBATE-ALPHA.                     
                                                                                
           IF FD30-DTE-CHEQUE-SENT(WS-PP) = SPACES OR HIGH-VALUES               
              MOVE F001-LST-MTH-DD   TO WS-DAY                                  
           ELSE                                                                 
              MOVE FD30-DTE-CHEQUE-SENT(WS-PP)                                  
                                     TO WS-DTE-PP-CHQ-ALPHA                     
              COMPUTE WS-DAY = WS-DTE-PP-CHQ-DD - 1                             
           END-IF.                                                              
                                                                                
           COMPUTE WS-INT-PER-DAY =                                             
                   FD69-AMT-INST-LOAN / F001-LST-MTH-DD.                        
                                                                                
           COMPUTE WS-AMT-REBATE-6DEC =                                         
                   WS-INT-PER-DAY * WS-DAY.                                     
                                                                                
      ***  ROUND TO TWO DECIMAL PLACES                                          
           COMPUTE WS-AMT-REBATE ROUNDED = WS-AMT-REBATE-6DEC.                  
                                                                                
           ADD 1                       TO CNT-FD30-FD69-MATCH.                  
                                                                                
           IF WS-AMT-REBATE > ZEROS                                             
              PERFORM 4500-UPDATE-REBATE THRU 4500-EXIT                         
           END-IF.                                                              
                                                                                
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4500-UPDATE-REBATE.                                                      
                                                                                
           PERFORM 7000-READ-KD00            THRU 7000-EXIT.                    
           IF WS-KD00-FD = 'Y'                                                  
                                                                                
              IF KD00-AMT-LOAN-BAL IS NOT NUMERIC                               
                 MOVE ZEROES                 TO KD00-AMT-LOAN-BAL               
              END-IF                                                            
              IF KD00-AMT-ARR-INTR IS NOT NUMERIC                               
                 MOVE ZEROES                 TO KD00-AMT-ARR-INTR               
              END-IF                                                            
                                                                                
              COMPUTE KD00-AMT-LOAN-BAL =                                       
                      KD00-AMT-LOAN-BAL - WS-AMT-REBATE                         
                                                                                
              PERFORM 5000-WRITE-F125        THRU 5000-EXIT                     
              PERFORM 6000-PRINT-REPORT      THRU 6000-EXIT                     
                                                                                
              PERFORM 8000-REWRITE-KD00      THRU 8000-EXIT                     
           END-IF.                                                              
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       5000-WRITE-F125.                                                         
      *----------------------------------------------------------------*        
                                                                                
           MOVE SPACES              TO BP13F125-CASHTRAN.                       
           INITIALIZE                  BP13F125-CASHTRAN.                       
                                                                                
           MOVE FD30-NUM-REGN       TO F125-NUM-REGN.                           
           MOVE '68'                TO F125-CDE-TRANS-TYPE.                     
           MOVE 'IT'                TO F125-CDE-PAYMENT-TYPE.                   
           MOVE F001-DTE-CURRENT    TO F125-DTE-POST                            
                                       F125-DTE-TRANS.                          
           MOVE ZEROES              TO F125-COUNT-HISTORY.                      
           MOVE FD69-NUM-HDB-REF    TO F125-NUM-SCH-ACCT.                       
                                                                                
           MOVE FD69-PCT-INT-RATE   TO WS-TMP-INTRATE.                          
           IF  WS-PCT1 = 0                                                      
               STRING 'I'               DELIMITED BY SIZE                       
                      KD00-CDE-LOAN-SCH DELIMITED BY SIZE                       
                      '-'               DELIMITED BY SIZE                       
                      WS-PCT2           DELIMITED BY SIZE                       
                      '.'               DELIMITED BY SIZE                       
                      WS-PCT3           DELIMITED BY SIZE                       
                      INTO              F125-NUM-RECPT-JRNO                     
           ELSE                                                                 
               STRING 'I'               DELIMITED BY SIZE                       
                      KD00-CDE-LOAN-SCH DELIMITED BY SIZE                       
                      '-'               DELIMITED BY SIZE                       
                      WS-PCT1           DELIMITED BY SIZE                       
                      WS-PCT2           DELIMITED BY SIZE                       
                      '.'               DELIMITED BY SIZE                       
                      WS-PCT3           DELIMITED BY SIZE                       
                      INTO              F125-NUM-RECPT-JRNO                     
           END-IF.                                                              
                                                                                
           MOVE WS-AMT-REBATE           TO F125-AMT-RECEIPT                     
                                           F125-AMT-INTEREST.                   
           MOVE FD30-DTE-PP-DUE(WS-PP)  TO F125-DTE-PP-DUE.                     
           MOVE 'C'                     TO F125-CDE-CRDR.                       
           MOVE KD00-CDE-LOAN-SCH       TO F125-NUM-LOAN-SCH.                   
                                                                                
           WRITE BP13F125-CASHTRAN.                                             
           ADD 1 TO CNT-F125-WRITE.                                             
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       6000-PRINT-REPORT.                                                       
      *----------------------------------------------------------------*        
                                                                                
           IF WS-LINE > 50                                                      
              ADD 1 TO WS-PAGE                                                  
              MOVE WS-PAGE TO LC68-PRT-PAGE                                     
              WRITE BP13LC68-REC FROM LC68-PR-HEAD-01  AFTER PAGE               
              WRITE BP13LC68-REC FROM LC68-PR-HEAD-02  AFTER 2                  
              WRITE BP13LC68-REC FROM LC68-PR-HEAD-03  AFTER 2                  
              WRITE BP13LC68-REC FROM LC68-PR-HEAD-04                           
              MOVE SPACES TO BP13LC68-REC                                       
              WRITE BP13LC68-REC                                                
              MOVE  7 TO WS-LINE                                                
           END-IF.                                                              
                                                                                
           ADD 1                             TO WS-SNO                          
                                                WS-LINE.                        
                                                                                
           MOVE SPACES                       TO LC68-PR-DTL.                    
           MOVE WS-SNO                       TO LC68-PRT-SNO.                   
           MOVE FD30-NUM-REGN                TO LC68-REGN-NO.                   
           MOVE WS-PP                        TO LC68-SCH.                       
           MOVE FD69-AMT-LOAN-BAL            TO LC68-LOAN.                      
           MOVE WS-AMT-REBATE                TO LC68-IT-REBATE.                 
           STRING FD30-DTE-PP-DUE(WS-PP)(7:2) '/'                               
                  FD30-DTE-PP-DUE(WS-PP)(5:2) '/'                               
                  FD30-DTE-PP-DUE(WS-PP)(1:4)                                   
                  DELIMITED BY SIZE INTO        LC68-DUE-DATE.                  
           IF FD30-DTE-FIN-PROCESS(WS-PP) NOT = SPACES AND LOW-VALUES           
              STRING FD30-DTE-CHEQUE-SENT(WS-PP)(7:2) '/'                       
                     FD30-DTE-CHEQUE-SENT(WS-PP)(5:2) '/'                       
                     FD30-DTE-CHEQUE-SENT(WS-PP)(1:4)                           
                     DELIMITED BY SIZE INTO        LC68-FIN-DATE                
           END-IF.                                                              
           MOVE KD00-AMT-LOAN-BAL            TO LC68-LOAN-OUTS.                 
                                                                                
           WRITE BP13LC68-REC          FROM LC68-PR-DTL.                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       7000-READ-KD00.                                                          
      *----------------------------------------------------------------*        
                                                                                
           MOVE FD30-NUM-REGN TO KD00-NUM-REGN.                                 
                                                                                
           READ BP13KD00                                                        
           EVALUATE WS-KD00-STATUS                                              
              WHEN 00                                                           
                 MOVE 'Y' TO WS-KD00-FD                                         
                 ADD 1 TO CNT-KD00-READ                                         
              WHEN 23                                                           
                 MOVE 'N' TO WS-KD00-FD                                         
                 ADD 1 TO CNT-KD00-NOTFOUND                                     
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN READING BP13KD00 - STATUS '                  
                             WS-KD00-STATUS                                     
                 MOVE        WS-KD00-STATUS    TO RETURN-CODE                   
                 PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       8000-REWRITE-KD00.                                                       
      *----------------------------------------------------------------*        
                                                                                
      *** COMPUTATION OF ARR-INTR (INTEREST REBATE)                             
           COMPUTE KD00-AMT-ARR-INTR =                                          
                   KD00-AMT-ARR-INTR - WS-AMT-REBATE.                           
                                                                                
           REWRITE BP13KD00-REC.                                                
                                                                                
           EVALUATE WS-KD00-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO CNT-KD00-UPDATE                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN UPDATING BP13KD00 - STATUS '                 
                             WS-KD00-STATUS                                     
                 MOVE        WS-KD00-STATUS    TO RETURN-CODE                   
                 PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       9000-CLOSE-ROUTINE.                                                      
      *----------------------------------------------------------------*        
                                                                                
           CLOSE BP13FD69                                                       
                 SY02F001                                                       
                 BP13FD30                                                       
                 BP13KD00                                                       
                 BP13F125                                                       
                 BP13LC68.                                                      
                                                                                
           IF WS-KD00-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR, BP13KD00-STATUS ' WS-KD00-STATUS.         
              MOVE WS-KD00-STATUS TO RETURN-CODE.                               
                                                                                
           DISPLAY '                                    '.                      
           DISPLAY '***** BP13CC68 CONTROL TOTALS ***** '.                      
           DISPLAY '                                    '.                      
           DISPLAY 'NO OF RECORDS READ FROM  (BP13FD69) = '                     
                    CNT-FD69-READ.                                              
           DISPLAY 'NO OF RECORDS READ FROM  (BP13FD30) = '                     
                    CNT-FD30-READ.                                              
           DISPLAY 'NO OF RECORDS MATCHED               = '                     
                    CNT-FD30-FD69-MATCH.                                        
           DISPLAY 'NO OF RECORDS FOUND FROM (BP13KD00) = '                     
                    CNT-KD00-READ.                                              
           DISPLAY 'NO OF RECORDS NOT FOUND  (BP13KD00) = '                     
                    CNT-KD00-NOTFOUND.                                          
           DISPLAY 'NO OF RECORDS UPDATED    (BP13KD00) = '                     
                    CNT-KD00-UPDATE.                                            
           DISPLAY 'NO OF RECORDS WRITTEN    (BP13F125) = '                     
                    CNT-F125-WRITE.                                             
           DISPLAY 'NO OF RECORDS WRITTEN    (BP13LC68) = '                     
                    WS-SNO.                                                     
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
