      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C49F.                                                 
       AUTHOR.        KARL ANDRUS MAGALONA CABUG.                               
       DATE-WRITTEN.  JANUARY 28, 2020.                                         
      *===============================================================*         
      * OBJECTIVE  :  TO CREATE EMAIL PCD FILE (BP13K493)             *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :  1. P13F49D2 - DA = 'Y' AND LOAN-TAG = B/H       *         
      *               2. BP13K800                                     *         
      *               3. BP13K813                                     *         
      *               4. BM06K110                                     *         
      *               5. BM06K100                                     *         
      *               6. BP13K857                                     *         
      *               7. BP13K022                                     *         
      *               8. BP13K024                                     *         
      *                                                               *         
      * OUTPUT FILE:  1. BP13K493  - BP13.K493.EMAIL.PCD              *         
      *                              FOR SENDING OF EMAIL             *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP138170 29/01/2020 KAC1 NEW PROGRAM                          *         
      * BP139422 29/11/2022 ZAR7 CATER FOR DUP REC FOR K493           *         
      * BP139422 30/11/2022 ZAR7 TO CALL WS-BP13C913                  *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  P13F49D2  ASSIGN        TO P13F49D2.                         
                                                                                
           SELECT  BP13K800  ASSIGN        TO BP13K800                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K800-NUM-REGN                     
                             FILE STATUS   IS WS-K800-STATUS.                   
                                                                                
           SELECT  BP13K813  ASSIGN        TO BP13K813                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K813-KEY-FLD                      
                             FILE STATUS   IS WS-K813-STATUS.                   
                                                                                
           SELECT  BP13K493  ASSIGN        TO BP13K493                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K493-NUM-REGN                     
                             FILE STATUS   IS WS-K493-STATUS.                   
                                                                                
           SELECT BM06K110   ASSIGN        TO BM06K110                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K110-KEY-FLD                      
                             FILE STATUS   IS WS-K110-STATUS.                   
                                                                                
           SELECT BM06K100   ASSIGN        TO BM06K100                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K100-KEY-FLD                      
                             FILE STATUS   IS WS-K100-STATUS.                   
                                                                                
           SELECT BP13K857   ASSIGN        TO BP13K857                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K857-NUM-REGN                     
                             FILE STATUS   IS WS-K857-STATUS.                   
                                                                                
           SELECT BP13K022   ASSIGN        TO BP13K022                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K022-KEY-FLD                      
                             ALTERNATE RECORD KEY IS K022-NUM-REGN              
                             WITH DUPLICATES                                    
                             FILE STATUS   IS WS-K022-STATUS.                   
                                                                                
           SELECT BP13K024   ASSIGN        TO BP13K024                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K024-KEY-FLD                      
                             FILE STATUS   IS WS-K024-STATUS.                   
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   P13F49D2                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 50 CHARACTERS                                       
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       01   P13F49D2-RECORD.                                                    
            05 P13F49D2-NUM-REGN           PIC X(08).                           
            05 P13F49D2-LOAN-TAG           PIC X(01).                           
            05 P13F49D2-DTE-REGN           PIC X(08).                           
            05 FILLER                      PIC X(33).                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K100.                                                           
                                                                                
       FD   BP13K857                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K857.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K024                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K024.                                                           
                                                                                
       FD   BP13K493                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K493.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
                                                                                
       01  WS-SQL-CODE                   PIC 9(4).                              
       01  WS-BP13C913                   PIC X(08) VALUE 'BP13C913'.            
       01  WS-VARIABLES.                                                        
           05  WS-K800-STATUS          PIC X(2)  VALUE ZEROES.                  
           05  WS-K813-STATUS          PIC X(2)  VALUE ZEROES.                  
           05  WS-K110-STATUS          PIC X(2)  VALUE ZEROES.                  
           05  WS-K100-STATUS          PIC X(2)  VALUE ZEROES.                  
           05  WS-K857-STATUS          PIC X(2)  VALUE ZEROES.                  
           05  WS-K022-STATUS          PIC X(2)  VALUE ZEROES.                  
           05  WS-K024-STATUS          PIC X(2)  VALUE ZEROES.                  
           05  WS-K493-STATUS          PIC X(2)  VALUE ZEROES.                  
           05  WS-EOF-F49D2            PIC X(1)  VALUE 'N'.                     
           05  WS-K800-FND             PIC X(1)  VALUE 'N'.                     
           05  WS-K813-FND             PIC X(1)  VALUE 'N'.                     
           05  WS-K110-FND             PIC X(1)  VALUE 'N'.                     
           05  WS-K100-FND             PIC X(1)  VALUE 'N'.                     
           05  WS-K857-FND             PIC X(1)  VALUE 'N'.                     
           05  WS-K493-FND             PIC X(1)  VALUE 'N'.                     
           05  WS-NUM-AHG              PIC X(1)  VALUE SPACES.                  
           05  WS-NUM-EHG              PIC X(1)  VALUE SPACES.                  
           05  WS-NUM-AHG-SHG          PIC X(1)  VALUE SPACES.                  
           05  WS-DATE                 PIC X(8)  VALUE SPACES.                  
           05  WS-MONTH                PIC X(3)  VALUE SPACES.                  
           05  WS-QUARTER              PIC X(11) VALUE SPACES.                  
           05  WS-DTE-PCD-M            PIC X(18) VALUE SPACES.                  
           05  WS-DTE-PCD-Q            PIC X(18) VALUE SPACES.                  
           05  WS-YEAR                 PIC 9(4).                                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F49D2-READ           PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-K493             PIC 9(7)  VALUE ZEROES.                  
           05  WS-K800-FND-CTR         PIC 9(7)  VALUE ZEROES.                  
           05  WS-K813-FND-CTR         PIC 9(7)  VALUE ZEROES.                  
           05  WS-K800-NTFND-CTR       PIC 9(7)  VALUE ZEROES.                  
           05  WS-K813-NTFND-CTR       PIC 9(7)  VALUE ZEROES.                  
           05  WS-K110-NTFND-CTR       PIC 9(7)  VALUE ZEROES.                  
           05  WS-K100-NTFND-CTR       PIC 9(7)  VALUE ZEROES.                  
           05  WS-K022-NTFND-CTR       PIC 9(7)  VALUE ZEROES.                  
           05  WS-K024-NTFND-CTR       PIC 9(7)  VALUE ZEROES.                  
           05  WS-K857-NTFND-CTR       PIC 9(7)  VALUE ZEROES.                  
           05  WS-K493-CTR             PIC 9(7)  VALUE ZEROES.                  
                                                                                
       01  WS-MONTHS-IN-DIGITS-AND-WORDS.                                       
           05 FILLER                   PIC X(06) VALUE '01 JAN'.                
           05 FILLER                   PIC X(06) VALUE '02 FEB'.                
           05 FILLER                   PIC X(06) VALUE '03 MAR'.                
           05 FILLER                   PIC X(06) VALUE '04 APR'.                
           05 FILLER                   PIC X(06) VALUE '05 MAY'.                
           05 FILLER                   PIC X(06) VALUE '06 JUN'.                
           05 FILLER                   PIC X(06) VALUE '07 JUL'.                
           05 FILLER                   PIC X(06) VALUE '08 AUG'.                
           05 FILLER                   PIC X(06) VALUE '09 SEP'.                
           05 FILLER                   PIC X(06) VALUE '10 OCT'.                
           05 FILLER                   PIC X(06) VALUE '11 NOV'.                
           05 FILLER                   PIC X(06) VALUE '12 DEC'.                
       01  FILLER  REDEFINES  WS-MONTHS-IN-DIGITS-AND-WORDS.                    
           05 WS-MONTH-TABLE               OCCURS 12                            
                                           INDEXED BY WS-MTH-IND.               
              10 WS-MONTH-IN-DIGITS    PIC X(02).                               
              10 FILLER                PIC X(01).                               
              10 WS-MONTH-IN-WORDS     PIC X(03).                               
      *=======================================================*                 
      *    LINKAGE VARIABLES                                  *                 
      *=======================================================*                 
       01  WS-LINK-REC.                                                         
            05 WS-LINK-NUM-SCH            PIC X(4).                             
            05 WS-LINK-NUM-ACC            PIC X(4).                             
            05 WS-LINK-NUM-CHK            PIC X(1).                             
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-P13F49D2      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F49D2 = 'Y'.                                          
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  P13F49D2                                                 
                       BP13K800                                                 
                       BP13K813                                                 
                       BM06K110                                                 
                       BM06K100                                                 
                       BP13K857                                                 
                       BP13K022                                                 
                       BP13K024                                                 
               I-O     BP13K493.                                                
                                                                                
            IF WS-K800-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPEN FAILED FOR BP13K800,STATUS ' WS-K800-STATUS        
               MOVE WS-K800-STATUS              TO RETURN-CODE                  
               PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                    
            END-IF.                                                             
                                                                                
            IF WS-K813-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPEN FAILED FOR BP13K813,STATUS ' WS-K813-STATUS        
               MOVE WS-K813-STATUS              TO RETURN-CODE                  
               PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                    
            END-IF.                                                             
                                                                                
            IF WS-K110-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPEN FAILED FOR BM06K110,STATUS ' WS-K110-STATUS        
               MOVE WS-K110-STATUS              TO RETURN-CODE                  
               PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                    
            END-IF.                                                             
                                                                                
            IF WS-K100-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPEN FAILED FOR BM06K100,STATUS ' WS-K100-STATUS        
               MOVE WS-K100-STATUS              TO RETURN-CODE                  
               PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                    
            END-IF.                                                             
                                                                                
            IF WS-K857-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPEN FAILED FOR BP13K857,STATUS ' WS-K857-STATUS        
               MOVE WS-K857-STATUS              TO RETURN-CODE                  
               PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                    
            END-IF.                                                             
                                                                                
            IF WS-K022-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPEN FAILED FOR BP13K022,STATUS ' WS-K022-STATUS        
               MOVE WS-K022-STATUS              TO RETURN-CODE                  
               PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                    
            END-IF.                                                             
                                                                                
            IF WS-K024-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPEN FAILED FOR BP13K024,STATUS ' WS-K024-STATUS        
               MOVE WS-K024-STATUS              TO RETURN-CODE                  
               PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                    
            END-IF.                                                             
                                                                                
            IF WS-K493-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPEN FAILED FOR BP13K493,STATUS ' WS-K493-STATUS        
               MOVE WS-K493-STATUS              TO RETURN-CODE                  
               PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                    
            END-IF.                                                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-P13F49D2.                                                      
      ******************************************************************        
           READ P13F49D2           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F49D2                            
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO WS-F49D2-READ.                            
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
           MOVE SPACES                             TO WS-NUM-AHG                
                                                      WS-NUM-AHG-SHG            
                                                      WS-NUM-EHG.               
           MOVE 'N'                                TO WS-K800-FND.              
           MOVE P13F49D2-NUM-REGN                  TO K800-NUM-REGN.            
           PERFORM 3100-READ-BP13K800            THRU 3100-EXIT.                
                                                                                
           IF WS-K800-FND = 'Y'                                                 
              MOVE 'N'                             TO WS-K813-FND               
              MOVE SPACES                          TO BP13K813-REC              
              INITIALIZE                              BP13K813-REC              
                                                                                
              DISPLAY 'K800-NUM-BTO-ZONE : ' K800-NUM-BTO-ZONE                  
              DISPLAY 'NUM-BTO-FLAT-TYPE : ' K800-NUM-BTO-FLAT-TYPE             
              MOVE K800-NUM-BTO-ZONE               TO K813-NUM-ZONE             
              MOVE K800-NUM-BTO-FLAT-TYPE          TO K813-NUM-FLAT-TYPE        
              MOVE K800-DTE-BALLOT                 TO K813-DTE-BALLOT           
                                                                                
              PERFORM 3500-READ-BP13K813         THRU 3500-EXIT                 
              IF WS-K813-FND = 'Y'                                              
                IF K813-NUM-MATURE-EST-TAG = 'Y'                                
                   IF K800-DTE-REGN < 20190911                                  
                      MOVE 'Y'                     TO WS-NUM-AHG                
                   ELSE                                                         
                     IF K800-DTE-REGN >= 20190911                               
                        MOVE 'Y'                   TO WS-NUM-EHG                
                     END-IF                                                     
                   END-IF                                                       
                ELSE                                                            
                  IF K800-NUM-ROOM-TYPE = '5' OR 'E'                            
                     IF K800-DTE-REGN < 20190911                                
                        MOVE 'Y'                   TO WS-NUM-AHG                
                     ELSE                                                       
                       IF K800-DTE-REGN >= 20190911                             
                          MOVE 'Y'                 TO WS-NUM-EHG                
                       END-IF                                                   
                     END-IF                                                     
                  ELSE                                                          
                    IF K800-DTE-REGN < 20190911                                 
                       MOVE 'Y'                    TO WS-NUM-AHG-SHG            
                    ELSE                                                        
                      IF K800-DTE-REGN >= 20190911                              
                         MOVE 'Y'                  TO WS-NUM-EHG                
                      END-IF                                                    
                    END-IF                                                      
                  END-IF                                                        
                END-IF                                                          
                                                                                
                IF (WS-NUM-AHG NOT = SPACES AND LOW-VALUES) OR                  
                   (WS-NUM-EHG NOT = SPACES AND LOW-VALUES) OR                  
                   (WS-NUM-AHG-SHG NOT = SPACES AND LOW-VALUES)                 
                    PERFORM 4000-PREP-BP13K493   THRU 4000-EXIT                 
                END-IF                                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-P13F49D2            THRU 2000-EXIT.                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3100-READ-BP13K800.                                                      
      ******************************************************************        
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    MOVE 'Y'                    TO WS-K800-FND                  
               WHEN 23                                                          
                    ADD 1                       TO WS-K800-NTFND-CTR            
                    DISPLAY 'BP13K800 - REC NOTFND, REGN: '                     
                             K800-NUM-REGN                                      
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K800 : ' WS-K800-STATUS          
                            ' REGN = ' K800-NUM-REGN                            
                    PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                   
           END-EVALUATE.                                                        
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3500-READ-BP13K813.                                                      
      ******************************************************************        
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
               WHEN 00                                                          
                    MOVE 'Y'                    TO WS-K813-FND                  
               WHEN 23                                                          
                    ADD 1                       TO WS-K813-NTFND-CTR            
                    DISPLAY 'BP13K813 - REC NOT FOUND'                          
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K813 : ' WS-K813-STATUS          
                            ' REGN = ' K813-KEY-FLD                             
                    PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-PREP-BP13K493.                                                      
      ******************************************************************        
           MOVE SPACES                        TO BP13K493-REC                   
                                                 WS-DTE-PCD-M                   
                                                 WS-DTE-PCD-Q.                  
           MOVE K800-NUM-REGN                 TO K493-NUM-REGN.                 
                                                                                
           MOVE K800-NUM-SCH-ACC              TO K110-SCH-ACC-NO.               
           PERFORM 4100-READ-BM06K110       THRU 4100-EXIT.                     
                                                                                
           MOVE K110-AIX1                     TO K100-KEY-FLD.                  
           PERFORM 4200-READ-BM06K100       THRU 4200-EXIT.                     
           IF K100-NUM-PACD = 'M'                                               
              PERFORM 4300-MONTHLY-PACD     THRU 4300-EXIT                      
                                                                                
              MOVE WS-DTE-PCD-M               TO K493-DTE-PCD                   
           ELSE                                                                 
             IF K100-NUM-PACD = 'Q'                                             
                PERFORM 4400-QUARTERLY-PACD THRU 4400-EXIT                      
                                                                                
                MOVE WS-DTE-PCD-Q             TO K493-DTE-PCD                   
             END-IF                                                             
           END-IF.                                                              
                                                                                
           MOVE P13F49D2-NUM-REGN             TO K857-NUM-REGN.                 
           PERFORM 4500-READ-BP13K857       THRU 4500-EXIT.                     
                                                                                
           MOVE K857-NUM-EMAIL                TO K493-NUM-EMAIL.                
                                                                                
           PERFORM 4600-GET-BLK-ADDR        THRU 4600-EXIT.                     
                                                                                
           MOVE COMM8-NUM-BLK                 TO K493-NUM-BLK.                  
           MOVE COMM8-NUM-LEVEL               TO K493-NUM-LEVEL.                
           MOVE COMM8-NUM-UNIT-MAIN           TO K493-NUM-MAIN-UNIT.            
           MOVE COMM8-NME-STREET              TO K493-NUM-STREET.               
           MOVE COMM8-NUM-POSTAL-CODE         TO K493-NUM-POSTAL.               
                                                                                
           PERFORM 4700-GET-OIC-NAME        THRU 4700-EXIT.                     
           MOVE K024-NME-OIC                  TO K493-NME-OIC.                  
                                                                                
           MOVE WS-NUM-AHG                    TO K493-NUM-AHG.                  
           MOVE WS-NUM-AHG-SHG                TO K493-NUM-AHG-SHG.              
           MOVE WS-NUM-EHG                    TO K493-NUM-EHG.                  
           MOVE FUNCTION CURRENT-DATE(1:8)    TO K493-DTE-UPDATE.               
                                                                                
           PERFORM 4110-READ-BP13K493  THRU 4110-EXIT.                          
           IF WS-K493-FND = 'N'                                                 
             PERFORM 5000-WRITE-BP1K493       THRU 5000-EXIT                    
           ELSE                                                                 
             PERFORM 5010-REWRITE-BP1K493     THRU 5010-EXIT                    
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4100-READ-BM06K110.                                                      
      ******************************************************************        
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-STATUS                                              
               WHEN 00                                                          
                    MOVE 'Y'                    TO WS-K110-FND                  
               WHEN 23                                                          
                    ADD 1                       TO WS-K110-NTFND-CTR            
                    MOVE SPACES                 TO K110-REC                     
                    DISPLAY 'BM06K110 - REC NOTFND, KEY : '                     
                             K110-SCH-ACC-NO                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BM06K110 : ' WS-K110-STATUS          
                            ' REGN = '                                          
                    PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                   
           END-EVALUATE.                                                        
                                                                                
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4110-READ-BP13K493.                                                      
      ******************************************************************        
           MOVE  K800-NUM-REGN         TO K493-NUM-REGN                         
           READ BP13K493.                                                       
                                                                                
           EVALUATE WS-K493-STATUS                                              
               WHEN 00                                                          
                    MOVE 'Y'                    TO WS-K493-FND                  
               WHEN 23                                                          
                    MOVE 'N'                    TO WS-K493-FND                  
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K493 : ' WS-K493-STATUS          
                            ' REGN = '                                          
                    PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                   
           END-EVALUATE.                                                        
                                                                                
                                                                                
       4110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4200-READ-BM06K100.                                                      
      ******************************************************************        
           READ BM06K100.                                                       
                                                                                
           EVALUATE WS-K100-STATUS                                              
               WHEN 00                                                          
                    MOVE 'Y'                    TO WS-K100-FND                  
               WHEN 23                                                          
                    ADD 1                       TO WS-K100-NTFND-CTR            
                    MOVE SPACES                 TO K100-REC                     
                    DISPLAY 'BM06K100 - REC NOTFND, KEY : '                     
                             K100-KEY-FLD                                       
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BM06K100 : ' WS-K100-STATUS          
                            ' REGN = '                                          
                    PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                   
           END-EVALUATE.                                                        
                                                                                
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4300-MONTHLY-PACD.                                                       
      ******************************************************************        
           SET WS-MTH-IND TO 1.                                                 
                                                                                
           SEARCH WS-MONTH-TABLE                                                
               AT END                                                           
               MOVE '   ' TO WS-MONTH                                           
            WHEN WS-MONTH-IN-DIGITS(WS-MTH-IND) = K100-DTE-PACD-QTR(5:2)        
               MOVE WS-MONTH-IN-WORDS(WS-MTH-IND) TO WS-MONTH                   
           END-SEARCH.                                                          
                                                                                
           MOVE K100-DTE-PACD-QTR(1:4)       TO WS-YEAR.                        
                                                                                
           STRING WS-MONTH ' ' WS-YEAR DELIMITED BY SIZE                        
                  INTO WS-DTE-PCD-M                                             
           END-STRING.                                                          
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4400-QUARTERLY-PACD.                                                     
      ******************************************************************        
           IF K100-DTE-PACD-QTR(5:2) = '01' OR '02' OR '03'                     
              MOVE '1ST QUARTER' TO WS-QUARTER                                  
           ELSE                                                                 
              IF K100-DTE-PACD-QTR(5:2) = '04' OR '05' OR '06'                  
                 MOVE '2ND QUARTER' TO WS-QUARTER                               
              ELSE                                                              
                 IF K100-DTE-PACD-QTR(5:2) = '07' OR '08' OR '09'               
                    MOVE '3RD QUARTER' TO WS-QUARTER                            
                 ELSE                                                           
                    MOVE '4TH QUARTER' TO WS-QUARTER                            
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE K100-DTE-PACD-QTR(1:4)       TO WS-YEAR.                        
                                                                                
           STRING WS-QUARTER ' ' WS-YEAR DELIMITED BY SIZE                      
                  INTO WS-DTE-PCD-Q                                             
           END-STRING.                                                          
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       4500-READ-BP13K857.                                                      
      ******************************************************************        
           READ BP13K857.                                                       
                                                                                
           EVALUATE WS-K857-STATUS                                              
               WHEN 00                                                          
                    MOVE 'Y'                    TO WS-K857-FND                  
               WHEN 23                                                          
                    ADD 1                       TO WS-K857-NTFND-CTR            
                    MOVE SPACES                 TO K857-NUM-EMAIL               
               WHEN OTHER                                                       
                   DISPLAY 'BP13K857 - READ ERR, STAT: ' WS-K857-STATUS         
                           ' REGN = ' K857-NUM-REGN                             
                   PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                     
           END-EVALUATE.                                                        
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4600-GET-BLK-ADDR.                                                       
      ******************************************************************        
           MOVE SPACES                          TO BP13COMM8-REC.               
           MOVE K800-NUM-SCH                    TO WS-LINK-NUM-SCH.             
           MOVE K800-NUM-ACC                    TO WS-LINK-NUM-ACC.             
           MOVE K800-NUM-ACC-CK                 TO WS-LINK-NUM-CHK.             
                                                                                
           CALL WS-BP13C913 USING                                               
                 WS-LINK-REC, BP13COMM8-REC                                     
           END-CALL.                                                            
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              MOVE COMM8-CDE-SYSERR             TO WS-SQL-CODE                  
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE' WS-SQL-CODE           
              ELSE                                                              
                 DISPLAY 'ERROR READING PBF TABLE' WS-SQL-CODE                  
              END-IF                                                            
              MOVE SPACES                       TO BP13COMM8-REC                
           END-IF.                                                              
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4700-GET-OIC-NAME.                                                       
      ******************************************************************        
           MOVE P13F49D2-NUM-REGN               TO K022-NUM-REGN.               
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           IF WS-K022-STATUS = 00                                               
              MOVE K022-NUM-OIC                 TO K024-NUM-OIC                 
              PERFORM 4800-READ-BP13K024      THRU 4800-EXIT                    
           ELSE                                                                 
             IF WS-K022-STATUS = 23                                             
                ADD 1                           TO WS-K022-NTFND-CTR            
                MOVE SPACES                     TO K024-NME-OIC                 
                DISPLAY '> CSM FOR ' K022-NUM-REGN ' NOT FOUND.'                
             ELSE                                                               
                DISPLAY 'BP13K022 - READ ERROR, STAT: ' WS-K022-STATUS          
                MOVE WS-K022-STATUS             TO RETURN-CODE                  
                PERFORM 9900-HANDLE-ABEND     THRU 9900-EXIT                    
             END-IF                                                             
           END-IF.                                                              
       4700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4800-READ-BP13K024.                                                      
      ******************************************************************        
           DISPLAY '@4800-READ-BP13K024.'                                       
           READ BP13K024 KEY IS K024-KEY-FLD.                                   
                                                                                
           IF WS-K024-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
             IF WS-K024-STATUS = 23                                             
                ADD 1                             TO WS-K024-NTFND-CTR          
                MOVE SPACES                       TO K024-NME-OIC               
                DISPLAY '> CSM DETAIL OF ' K024-NUM-OIC ' FOR '                 
                         K022-NUM-REGN ' NOT FOUND.'                            
             ELSE                                                               
                DISPLAY 'BP13K024 - READ ERROR, STAT: ' WS-K024-STATUS          
                MOVE WS-K024-STATUS               TO RETURN-CODE                
                PERFORM 9900-HANDLE-ABEND       THRU 9900-EXIT                  
             END-IF                                                             
           END-IF.                                                              
       4800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-WRITE-BP1K493.                                                      
      ******************************************************************        
           DISPLAY '@5000-WRITE-BP1K493.'                                       
           DISPLAY 'BP13K493-REC {' BP13K493-REC '}'                            
                                                                                
           WRITE BP13K493-REC.                                                  
                                                                                
           EVALUATE WS-K493-STATUS                                              
               WHEN 00                                                          
                    ADD 1                       TO WS-CNT-K493                  
               WHEN OTHER                                                       
                 DISPLAY 'BP13K493 - READ ERROR, STAT: ' WS-K493-STATUS         
                 MOVE WS-K493-STATUS            TO RETURN-CODE                  
                 PERFORM 9900-HANDLE-ABEND    THRU 9900-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5010-REWRITE-BP1K493.                                                    
      ******************************************************************        
           DISPLAY '@5010-REWRITE-BP1K493.'                                     
           DISPLAY 'BP13K493-REC {' BP13K493-REC '}'                            
                                                                                
           REWRITE BP13K493-REC.                                                
                                                                                
           EVALUATE WS-K493-STATUS                                              
               WHEN 00                                                          
                    ADD 1                       TO WS-CNT-K493                  
               WHEN OTHER                                                       
                 DISPLAY 'BP13K493 - REWRITE ER, STAT: ' WS-K493-STATUS         
                 MOVE WS-K493-STATUS            TO RETURN-CODE                  
                 PERFORM 9900-HANDLE-ABEND    THRU 9900-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       5010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9900-HANDLE-ABEND.                                                       
      ******************************************************************        
                                                                                
           DISPLAY '************* CONTROL ABEND TOTALS  *************'.         
           DISPLAY 'PROGRAM-ID : BP13C49F      STAT: ABEND'.                    
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF INPUT(F49D2) RECORDS READ.... : '                 
                    WS-F49D2-READ.                                              
           DISPLAY '(2) NO OF BP13K493 RECORDS WRITTEN..... : '                 
                    WS-CNT-K493                                                 
                                                                                
           CLOSE    P13F49D2                                                    
                    BP13K800                                                    
                    BP13K813                                                    
                    BM06K110                                                    
                    BM06K100                                                    
                    BP13K857                                                    
                    BP13K022                                                    
                    BP13K024                                                    
                    BP13K493.                                                   
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE FAILED FOR BP13K800,STAT: ' WS-K800-STATUS         
              MOVE WS-K800-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
           STOP RUN.                                                            
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE FAILED FOR BP13K813,STAT: ' WS-K813-STATUS         
              MOVE WS-K813-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
           STOP RUN.                                                            
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE FAILED FOR BM06K110,STAT: ' WS-K110-STATUS         
              MOVE WS-K110-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
           STOP RUN.                                                            
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE FAILED FOR BM06K100,STAT: ' WS-K100-STATUS         
              MOVE WS-K100-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
           STOP RUN.                                                            
                                                                                
           IF WS-K857-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE FAILED FOR BP13K857,STAT: ' WS-K857-STATUS         
              MOVE WS-K857-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
           STOP RUN.                                                            
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE FAILED FOR BP13K022,STAT: ' WS-K022-STATUS         
              MOVE WS-K022-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
           STOP RUN.                                                            
                                                                                
           IF WS-K024-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE FAILED FOR BP13K024,STAT: ' WS-K024-STATUS         
              MOVE WS-K024-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
           STOP RUN.                                                            
                                                                                
           IF WS-K493-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE FAILED FOR BP13K493,STAT: ' WS-K493-STATUS         
              MOVE WS-K493-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
           STOP RUN.                                                            
                                                                                
       9900-EXIT.                                                               
           EXIT.                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C49F'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF INPUT(F49D2) RECORDS READ.... : '                 
                    WS-F49D2-READ.                                              
           DISPLAY '(2) NO OF BP13K493 RECORDS WRITTEN..... : '                 
                    WS-CNT-K493                                                 
                                                                                
           CLOSE    P13F49D2                                                    
                    BP13K800                                                    
                    BP13K813                                                    
                    BM06K110                                                    
                    BM06K100                                                    
                    BP13K857                                                    
                    BP13K022                                                    
                    BP13K024                                                    
                    BP13K493.                                                   
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE FAILED FOR BP13K800,STAT: ' WS-K800-STATUS         
              MOVE WS-K800-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
           STOP RUN.                                                            
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE FAILED FOR BP13K813,STAT: ' WS-K813-STATUS         
              MOVE WS-K813-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
           STOP RUN.                                                            
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE FAILED FOR BM06K110,STAT: ' WS-K110-STATUS         
              MOVE WS-K110-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
           STOP RUN.                                                            
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE FAILED FOR BM06K100,STAT: ' WS-K100-STATUS         
              MOVE WS-K100-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
           STOP RUN.                                                            
                                                                                
           IF WS-K857-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE FAILED FOR BP13K857,STAT: ' WS-K857-STATUS         
              MOVE WS-K857-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
           STOP RUN.                                                            
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE FAILED FOR BP13K022,STAT: ' WS-K022-STATUS         
              MOVE WS-K022-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
           STOP RUN.                                                            
                                                                                
           IF WS-K024-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE FAILED FOR BP13K024,STAT: ' WS-K024-STATUS         
              MOVE WS-K024-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
           STOP RUN.                                                            
                                                                                
           IF WS-K493-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE FAILED FOR BP13K493,STAT: ' WS-K493-STATUS         
              MOVE WS-K493-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
