       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C872                                                     
      *AUTHOR. LSB.                                                             
      *DATE-WRITTEN : 31.08.94                                                  
      *----------------------------------------------------------------*        
      *                                                                *        
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                            *        
      *                                                                *        
      *  SYSTEM ID   : BP13                                            *        
      *                                                                *        
      *  OBJECTIVE   : COMPUTE NUMBERS OF MONTHS RESIDE BASED ON       *        
      *                THE DATE IT RUNS.                               *        
      *                                                                *        
      *  REMARKS     : SIMILAR LOGIC AS PROGRAM BP13C805               *        
      *                                                                *        
      *----------------------------------------------------------------*        
      *  CHGE-ID OIC DATE    DESCRIPTION                               *        
      *  ------- --- ----    -----------                               *        
      * BP130146 YCH 960909 - NEW PGM                                  *        
      * BP130261 YCH 970404      BREAKDOWN OF RFS APPLNS               *        
      * BP130630 NOR 990114  CHGE F715 F716 EXPAND TO 350              *        
      *----------------------------------------------------------------*        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F716 ASSIGN TO BP13F716.                                  
           SELECT P13F716A ASSIGN TO P13F716A.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F716                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 350 CHARACTERS               
                                   RECORDING MODE IS F                          
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F716.                                                           
      *                                                                         
       FD  P13F716A                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 350 CHARACTERS               
                                   RECORDING MODE IS F                          
                                   LABEL RECORDS ARE STANDARD.                  
                                                                                
       01  P13F716A-REC                       PIC X(350).                       
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-F716-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-F716A-WRITE          PIC 9(8)    VALUE ZERO.                      
       01  WS-ZERO-MTH             PIC 9(8)    VALUE ZERO.                      
       01  WS-LATEST-MTH-RESIDE    PIC 9(4)    VALUE ZERO.                      
                                                                                
                                                                                
       77  WS-EOF-F716             PIC X(3).                                    
           88  EOF-OF-F716                     VALUE 'YES'.                     
                                                                                
       01  WS-DTE-REGN.                                                         
           05 WS-REGN-CC           PIC 9(2).                                    
           05 WS-REGN-YY           PIC 9(2).                                    
           05 WS-REGN-MM           PIC 9(2).                                    
           05 WS-REGN-DD           PIC 9(2).                                    
                                                                                
       01  WS-CUR-DATE.                                                         
           05 WS-YY                PIC 9999.                                    
           05 WS-MM                PIC 99.                                      
           05 WS-DD                PIC 99.                                      
                                                                                
       01  WS-EDIT-DATE.                                                        
           05 WS-EDIT-DD           PIC X(02).                                   
           05 FILLER               PIC X(01) VALUE '/'.                         
           05 WS-EDIT-MM           PIC X(02).                                   
           05 FILLER               PIC X(01) VALUE '/'.                         
           05 WS-EDIT-YY           PIC X(04).                                   
                                                                                
       01  WS-MOVE-DATE.                                                        
           05 WS-MOVE-YY           PIC 9999.                                    
           05 WS-MOVE-MM           PIC 99.                                      
           05 WS-MOVE-DD           PIC 99.                                      
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       000-MAIN-LOGIC.                                                          
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 300-READ-F716 THRU 300-EXIT.                                 
           PERFORM 400-PROCESS   THRU 400-EXIT                                  
                   UNTIL EOF-OF-F716.                                           
           PERFORM 900-FINALIZATION THRU 900-EXIT.                              
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-INITIALIZATION.                                                      
           OPEN INPUT  BP13F716                                                 
                OUTPUT P13F716A.                                                
           MOVE 'NO' TO WS-EOF-F716.                                            
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-CUR-DATE.                    
           MOVE WS-DD      TO WS-EDIT-DD                                        
           MOVE WS-MM      TO WS-EDIT-MM                                        
           MOVE WS-YY      TO WS-EDIT-YY.                                       
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-READ-F716.                                                           
           READ BP13F716 AT END MOVE 'YES' TO WS-EOF-F716                       
               GO TO 300-EXIT.                                                  
                                                                                
           ADD  1                  TO WS-F716-READ.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *  CALCULATE MTH RESIDE FOR NUM-RDB-DP-OM =                     *         
      *  'D' -> EXISTING FLAT BOUGHT DIRECTLY FROM BOARD              *         
      *  'M' -> EXISTING FLAT BOUGHT FROM OM (FND IN SMS AS OM)       *         
      *  'S' -> EXISTING FLAT OF FIRST TIMER BOUGHT FROM OM           *         
      *  'Z' -> OCCUPIER OF EXISTING FLAT                             *         
      *****************************************************************         
                                                                                
       400-PROCESS.                                                             
                                                                                
            IF F716-NUM-RDB-DP-OM = 'D' OR 'M' OR 'S' OR 'Z'                    
               PERFORM 450-CALCULATE-RESIDE THRU 450-EXIT                       
            ELSE                                                                
               WRITE P13F716A-REC     FROM BP13F716-REC                         
               ADD  1                   TO WS-F716A-WRITE                       
            END-IF.                                                             
                                                                                
            PERFORM 300-READ-F716 THRU 300-EXIT.                                
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------*                                                     
       450-CALCULATE-RESIDE.                                                    
      *-------------------*                                                     
                                                                                
           IF F716-DTE-MOVING-IN NUMERIC                                        
              MOVE F716-DTE-MOVING-IN   TO WS-MOVE-DATE                         
           ELSE                                                                 
              IF F716-DTE-ACCEPTED NUMERIC                                      
                 MOVE F716-DTE-ACCEPTED TO WS-MOVE-DATE                         
              ELSE                                                              
                 MOVE ZEROES            TO WS-MOVE-DATE                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-MOVE-DATE > ZEROS                                              
              COMPUTE  WS-LATEST-MTH-RESIDE =                                   
                  ( (WS-YY * 12) + WS-MM) -                                     
                  ( (WS-MOVE-YY * 12) + WS-MOVE-MM)                             
              MOVE WS-LATEST-MTH-RESIDE TO F716-NUM-MTH-RESIDE                  
           ELSE                                                                 
              MOVE ZEROES               TO F716-NUM-MTH-RESIDE                  
              ADD 1 TO WS-ZERO-MTH                                              
           END-IF.                                                              
                                                                                
           WRITE P13F716A-REC     FROM BP13F716-REC.                            
           ADD  1                   TO WS-F716A-WRITE.                          
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       900-FINALIZATION.                                                        
             DISPLAY '*--------BP13C872  -----------*'.                         
             DISPLAY '* COMPUTE FIVE YEARS OCCUPATION PERIOD'.                  
             DISPLAY ' BP13F716-READ      ' WS-F716-READ.                       
             DISPLAY ' BP13F716-WRITE     ' WS-F716A-WRITE.                     
             DISPLAY '    '.                                                    
           CLOSE      BP13F716                                                  
                      P13F716A.                                                 
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
