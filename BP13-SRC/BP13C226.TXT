      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C226.                                                 
      *AUTHOR.        LKH.                                                      
      *DATE-WRITTEN.  11.1.91.                                                  
                                                                                
      * ======================================================== *              
      * SYSTEM OF COMMITMENT  (BP13)                             *              
      * ======================================================== *              
      *    OBJECTIVE  :                                          *              
      *        1.  PROGRAM TO EXTRACT DATA TO PRINT APPENDIX B   *              
      *            CONSISTING OF ALL UNITS AVAILABLE FOR         *              
      *            SELECTION, DISCARD UNITS THAT ARE SET ASIDE   *              
      *            FOR SAF AND MHA OR FREEZE FROM ALLOCATIONS.   *              
      *                                                          *              
      *     INPUT FILES:                                         *              
      *           1. BP13F205  - CONTROL RECORDS FILE            *              
      *           2. BP13K270  - BOOKING PROGRAM                 *              
      *           3. BM06K110  - FLAT INVENTORY MASTER           *              
      *                                                          *              
      *     OUTPUT FILES:                                        *              
      *           1. BM06F800  - EXTRACT RECORD FOR PRINTING     *              
      *                          SELLING PRICE                   *              
      *                                                          *              
      *  NOTE : THIS PROGRAM NEED TO RUN BEFORE BM06696A         *              
      * -------------------------------------------------------- *              
      * CHG NO   DATE     BY   DESCRIPTION                       *              
      * -------  -------- ---  -----------                       *              
      * C901227  22/01/91 LKH  TO EXTRACT DATA FOR PRINTING      *              
      *                        SELLING PRICE                     *              
      * C910576  21/06/91 LSB  TO EXCLUDE RP FLATS               *              
      * SOC-PH8  31/04/94 JEP  CHANGE BM06CTL TO BP13F205        *              
      * SOC-PH9  22/10/94 LMS  TO MOVE ZONE CODE TO BM06F800     *              
      * SOC-PH9  24/11/94 LMS  TO MOVE FLAT STATUS TO BM06F800   *              
      * C941092  13/02/95 YPH  TO MOVE DTE-BALLOT TO BM06F800    *              
      * C941228  30/03/95 LMS  TO REMOVE THE CHECKING OF K110-   *              
      *                        FLAT-STATUS IN DETERMINING THE    *              
      *                        FLAT-STATUS TO BE PASSED TO       *              
      *                        BM06F800                          *              
      * C950184  21/06/95 LMS  TO ADD IN THE CONTROL OF BY-      *              
      *                        PASSING SD FLATS.                 *              
      * BP130100 27/02/96 LMS  TO ADD NEW ALLOC-TAG 'W' & 'F'.   *              
      * SOC 8G   21/11/95 SSS  GET PROPERTY DATA FROM PIDB.      *              
      * BP130369 12/08/97 LMS  TO INCLUDE NEW ALLOCN-TAG 'J'.    *              
      * BP130369 12/08/97 SGK  TO REMOVE      ALLOCN-TAG 'F'.    *              
      * BP130369 14/08/97 LMS  TO CHANGE ALLOCN-TAG 'J' TO 'T'.  *              
      * BP130427 03/01/98 SSS  INCLUDE ALLOCN-TAG 'A, B, F, D'   *              
      * BP130418 24/02/98 SIA  CHGE BM06K211 TO BM06K110 (Y2K)   *              
      *          19/09/98 LSB  IF REGN=##, MOVE SPACES F800-REGN *              
      * BP130641 01/03/99 SRA  TO ADD FLATTYPE '1A' AND '2A'     *              
      * BP130768 07/12/99 GBR  TO WRITE THE UNIT TO BM06F800 WHEN*              
      *                        ALLOCTN-TAG 'F' FOR MATURE & NON- *              
      *                        MATURE ESTATES.                   *              
      * BP130883 04/08/00 RAS  TO CHANGE BP13C904 TO BP13C913    *              
      * BP130877 02/11/00 LMS  TO READ NT-ZONE-SW.               *              
      * BP130877 23/11/00 AKT  TO MOVE VALUE TO CDE-SELTY        *              
      * BP131049 13/07/01 LMS  TO CATER FOR COF SELECTION.       *              
      * BP131059 15/08/01 LMS  TO CATER FOR BTO SELECTION.       *              
      * BP132075 16/01/02 LMS  TO CATER FOR RFS SELECTION.       *              
      * BP132075 17/01/02 JAM1 TO ACCUMULATE UNIT OFFERS AND     *              
      *                        UPDATE TO BP13K270.               *              
      * BP132119 15/03/02 LMS1 TO CHECK TAG FOR ALL UNITS        *              
      * BP132278 15/02/03 JF4  CALL DJ02C906 TO RETRIEVE UPG-STAT*              
      *                        CHG BM06F800 LRECL FROM 600 TO 800*              
      * BP132477 19/01/04 JF4  CATER FOR 'WIP' SELECTION TYPE    *              
      * BP132452 04/03/04 MTD  REMOVE INTV TAG                   *              
      * BP132500 13/03/04 JF4  MOVE CDE-APPL-ZONE TO F800-CDE-ZONE              
      *                        FOR SELECTION-TYPE 'SER'.         *              
      * BP132560 10/07/04 MTD  MOVE COMM8-QTY-AREA-ORGNL TO      *              
      *                        F800-QTY-AREA-ORGNL               *              
      * BP132541 16/10/04 LMS  EXPANSION OF ALLOCN-TAG TO X(3).  *              
      * BP132785 24/11/05 LMS  TO CATER FOR COMBINE SA QUOTA.    *              
      * BP132846 20/04/06 LMS  TO MATCH SELECTION-TYPE AND ALLOCN*              
      *                        QUARTER.                          *              
      * BP133026 14/03/07 RB12 CATER FOR FLAT TYPES 2,1A & 2A    *              
      *                        FOR ZONE 3R                       *              
      * BP133065 26/05/07 LMS  TO REMOVE MOVING OF ZONE TO F800. *              
      * BP133430 10/09/08 LMS1 NEW SELECTION TYPE WIH            *              
      * BP133447 16/10/08 LMS1 NEW SELECTION TYPE NPL            *              
      * BP133718 24/09/09 RB12 NEW SELECTION TYPE SBF            *              
      * BP133736 13/10/09 LMS1 MOVE CDE-APPL-ZONE TO F800-CDE-ZONE              
      *                        FOR SELECTION-TYPE 'NPL'.         *              
      * BP133724 19/10/09 ESA1 ADD CALLING OF BP13C916 TO RETRIEVE              
      *                        NME-LOCAL-PRCNCT                  *              
      * BP133851 02/03/10 ESA1 TO CATER FOR PAIR UNIT AND ADD    *              
      *                        READING OF BP13KQ30               *              
      * BP134043 03/11/10 ESA1 TO CATER FOR PUNGGOL SBF 2010     *              
      * BP134731 09/11/12 PCL4 TO CATER FOR MODL-DESIGN 89       *              
      * BP134997 28/08/13 ESA1 TO CATER FOR BONUS FLATS          *              
      * BP135135 15/11/13 ESA1 TO CATER FOR BONUS FLATS(2-ROOM)  *              
      * BP135281 30/05/14 ESA1 TO CATER FOR 3GEN & NO 3GEN UNITS *              
      * BP135838 01/07/15 ESA1 TO CATER FOR MODL-DESIGN 92 & 95  *              
      * BP136002 09/11/15 ESA1 TO CATER FOR 2-ROOM FLEXI         *              
      * BP136066 03/02/16 ESA1 TO CATER FOR DESIGN-OFFER FOR     *              
      *                        '3M' FLAT-TYPE                    *              
      * BP136991 19/09/17 ESA1 TO CATER FOR MODL-DESIGN = '98'   *              
      * BP138485 27/10/20 EL27 INCLUDE '85' FOR '3M' FLAT-TYPE   *              
      * BP138613 03/03/21 EL27 ADD CCA FLAT MODEL DESIGN 74      *              
      * BP139531 10/03/23 MRR5 CATER FOR 3-ROOM FSH              *              
      * BP139531 20/03/23 MRR5 UPDATE CALLING ON BP13C913, BP13C916             
      * BP139708 20/03/23 MRR5 CATER FOR SERS                    *              
      * BP139898 02/02/24 MRR5 INCLUDE MODL DESG 93 FOR 3M FLAT TYPE            
      * ======================================================== *              
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13K270 ASSIGN TO BP13K270                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS DYNAMIC                                         
                RECORD KEY   IS K270-KEY-FLD                                    
                FILE STATUS  IS K270-STATUS.                                    
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                ORGANIZATION  IS INDEXED                                        
                ACCESS MODE   IS DYNAMIC                                        
                RECORD KEY    IS K110-KEY-FLD                                   
                ALTERNATE KEY IS K110-AIX1                                      
                FILE STATUS   IS K110-STATUS.                                   
                                                                                
           SELECT BM06F110 ASSIGN TO BM06F110                                   
                ORGANIZATION  IS INDEXED                                        
                ACCESS MODE   IS RANDOM                                         
                RECORD KEY    IS F110-KEY-FLD                                   
                FILE STATUS   IS F110-STATUS.                                   
                                                                                
           SELECT BM06K100 ASSIGN TO BM06K100                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K100-KEY-FLD                                    
                FILE STATUS  IS K100-STATUS.                                    
                                                                                
           SELECT BP13KQ30 ASSIGN TO BP13KQ30                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS KQ30-KEY-FLD                                    
                ALTERNATE RECORD KEY IS KQ30-ALT-KEY1                           
                FILE STATUS  IS KQ30-STATUS.                                    
                                                                                
           SELECT BP13F205 ASSIGN TO BP13F205.                                  
           SELECT BM06F800 ASSIGN TO BM06F800.                                  
           SELECT BP13F270 ASSIGN TO BP13F270.                                  
                                                                                
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
       FILE SECTION.                                                            
                                                                                
       FD   BP13K270                 RECORD CONTAINS 250 CHARACTERS.            
       COPY BP13K270.                                                           
                                                                                
       FD   BM06K110                 RECORD CONTAINS 500 CHARACTERS.            
       COPY BM06K110.                                                           
                                                                                
       FD   BM06F110                 RECORD CONTAINS 500 CHARACTERS.            
       COPY BM06F110.                                                           
                                                                                
       FD   BM06K100                 RECORD CONTAINS 500 CHARACTERS.            
       COPY BM06K100.                                                           
                                                                                
       FD   BP13KQ30                 RECORD CONTAINS 100 CHARACTERS.            
       COPY BP13KQ30.                                                           
                                                                                
       FD   BP13F205                                                            
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS  ARE STANDARD                                        
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 80  CHARACTERS.                                     
       COPY BP13F205.                                                           
                                                                                
       FD  BM06F800                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 800 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BM06F800.                                                           
                                                                                
       FD   BP13F270                                                            
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS  ARE STANDARD                                        
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13F270.                                                           
                                                                                
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
       01  FILES-STS.                                                           
           05  K110-STATUS         PIC 99      VALUE ZEROS.                     
           05  K270-STATUS         PIC 99      VALUE ZEROS.                     
           05  K100-STATUS         PIC 99      VALUE ZEROS.                     
           05  KQ30-STATUS         PIC 99      VALUE ZEROS.                     
           05  F110-STATUS         PIC 99      VALUE ZEROS.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ-CNT-CTL     PIC 9(5)    VALUE ZEROS.                     
           05  WS-READ-CNT-AS      PIC 9(5)    VALUE ZEROS.                     
           05  WS-READ-CNT-HOLD    PIC 9(5)    VALUE ZEROS.                     
           05  WS-READ-CNT-RP      PIC 9(5)    VALUE ZEROS.                     
           05  WS-READ-CNT-SD      PIC 9(5)    VALUE ZEROS.                     
           05  WS-READ-CNT-K270    PIC 9(5)    VALUE ZEROS.                     
           05  WS-READ-CNT-K110    PIC 9(5)    VALUE ZEROS.                     
           05  WS-READ-CNT-K100    PIC 9(5)    VALUE ZEROS.                     
           05  WS-READ-CNT-KQ30    PIC 9(5)    VALUE ZEROS.                     
           05  WS-SKIP-RECORD      PIC 9(5)    VALUE ZEROS.                     
           05  WS-WRITE-CNT-F800   PIC 9(5)    VALUE ZEROS.                     
           05  WS-F270-WRITE-CTR   PIC 9(5)    VALUE ZEROS.                     
           05  WS-NUM-ALLOC.                                                    
               10  WS-NUM-ALLOC1   PIC X(2)    VALUE SPACES.                    
               10  WS-NUM-ALLOC2   PIC X(2)    VALUE SPACES.                    
               10  WS-NUM-ALLOC3   PIC X(2)    VALUE SPACES.                    
               10  WS-NUM-ALLOC4   PIC X(2)    VALUE SPACES.                    
               10  WS-NUM-ALLOC5   PIC X(2)    VALUE SPACES.                    
               10  WS-NUM-ALLOC6   PIC X(2)    VALUE SPACES.                    
               10  WS-NUM-ALLOC7   PIC X(2)    VALUE SPACES.                    
               10  WS-NUM-ALLOC8   PIC X(2)    VALUE SPACES.                    
               10  WS-NUM-ALLOC9   PIC X(2)    VALUE SPACES.                    
                                                                                
           05  WS-OFFER-COUNT-ARRAY.                                            
               10  WS-OFFER-COUNT-TABLE OCCURS 9 TIMES.                         
                   15  WS-OFFER-COUNT                PIC 9(3).                  
                                                                                
       01  WS-K110-FLAT-TYPE        PIC X(2)    VALUE SPACES.                   
       01  WS-NUM-FLAT-TYPE         PIC X(2)    VALUE SPACES.                   
       01  WS-NUM-FLAT-STATUS       PIC X(1)    VALUE SPACES.                   
       01  WS-NUM-UPG-STATUS        PIC X(3)    VALUE SPACES.                   
       01  WS-F800-NME-LOCAL-PRCNCT PIC X(40)   VALUE SPACES.                   
       01  WS-PAIRED-FLAG-SA        PIC X(1)    VALUE 'N'.                      
       01  WS-BP13C913              PIC X(8)    VALUE 'BP13C913'.               
       01  WS-BP13C916              PIC X(8)    VALUE 'BP13C916'.               
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-EOF-CTL           PIC X       VALUE SPACES.                   
           05  WS-EOF-K110          PIC X       VALUE SPACES.                   
           05  WS-EOF-K270          PIC X       VALUE SPACES.                   
           05  WS-UPDATE-FLAG       PIC X       VALUE SPACES.                   
           05  WS-K100-FND          PIC X       VALUE SPACES.                   
                                                                                
       01  WS-K270-KEY-FLD.                                                     
           05  WS-K270-ALLOCN-DATE PIC 9(8).                                    
           05  WS-K270-ALLOCN-INFO PIC X(16).                                   
                                                                                
       01  WS-K110-AIX1.                                                        
           05  WS-K110-ESTATE      PIC X(3).                                    
           05  WS-K110-NEIGH       PIC X(4).                                    
           05  WS-K110-CONTRACT-NO PIC X(4).                                    
           05  WS-K110-BLK-NO      PIC X(5).                                    
                                                                                
      *---------------------------------------------------------------*         
      *        LINKAGE FOR BP13C913                                   *         
      *---------------------------------------------------------------*         
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH   PIC X(4).                                  
               10  WS-LINK-NUM-ACC   PIC X(5).                                  
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *-----------------------------------------------------------*             
      *      LINK-AREA TO CALL SUB-PROGRAM (DJ02C906)             *             
      *-----------------------------------------------------------*             
       01  LINK-AREA.                                                           
           05  LINK-SQL-RETCODE             PIC S9(4).                          
           05  LINK-NUM-SCHM                PIC X(4).                           
           05  LINK-NUM-ACCNT               PIC X(7).                           
           05  LINK-NUM-BNDRY-STATUS        PIC X(3).                           
                                                                                
       01  WS-SQL-RETCODE                   PIC -9(3).                          
                                                                                
      *---------------------------------------------------------------*         
      *        LINKAGE FOR BP13C916                                   *         
      *---------------------------------------------------------------*         
       01  WS-LINK-REC-C916.                                                    
           05  WS-LINK-BLDNG-GL             PIC X(5).                           
                                                                                
       01  WS-BP13C916-REC.                                                     
           05  C916-NME-LOCAL-PRCNCT        PIC X(40).                          
           05  C916-CDE-SYSERR              PIC S9(3).                          
           05  C916-CDE-ERROR               PIC X(5).                           
                                                                                
      *-----------------------*                                                 
       PROCEDURE DIVISION.                                                      
      *-----------------------*                                                 
      *-----------------------------------------------------------              
       000-MAIN-ROUTINE.                                                        
      *-----------------------------------------------------------              
           PERFORM 100-OPEN-FILES  THRU 100-EXIT.                               
           PERFORM 200-READ-CTL    THRU 200-EXIT.                               
           PERFORM 300-PROCESS-REC THRU 300-EXIT                                
                   UNTIL WS-EOF-CTL = 'Y'.                                      
           PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       100-OPEN-FILES.                                                          
      *-----------------------------------------------------------              
           OPEN INPUT  BP13F205                                                 
                       BM06K110                                                 
                       BP13K270                                                 
                       BM06K100                                                 
                       BP13KQ30                                                 
                       BM06F110                                                 
                OUTPUT BM06F800                                                 
                       BP13F270.                                                
                                                                                
           IF K110-STATUS NOT = ZEROS AND 97                                    
              DISPLAY 'OPEN ERROR,BM06K110-STATUS ' K110-STATUS                 
              MOVE K110-STATUS TO RETURN-CODE                                   
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT.                          
                                                                                
           IF F110-STATUS NOT = ZEROS AND 97                                    
              DISPLAY 'OPEN ERROR,BM06K110-STATUS ' F110-STATUS                 
              MOVE F110-STATUS TO RETURN-CODE                                   
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT.                          
                                                                                
           IF K100-STATUS NOT = ZEROS AND 97                                    
              DISPLAY 'OPEN ERROR, BM06K100 STATUS ' K100-STATUS                
              MOVE K100-STATUS TO RETURN-CODE                                   
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT.                          
                                                                                
           IF KQ30-STATUS NOT = ZEROS AND 97                                    
              DISPLAY 'OPEN ERROR, BP13KQ30 STATUS ' KQ30-STATUS                
              MOVE KQ30-STATUS TO RETURN-CODE                                   
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT.                          
                                                                                
           IF K270-STATUS NOT = ZEROS AND 97                                    
              DISPLAY 'OPEN ERROR,BP13K270-STATUS ' K270-STATUS                 
              MOVE K270-STATUS TO RETURN-CODE                                   
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT.                          
                                                                                
           MOVE ZEROS TO WS-COUNTERS.                                           
           MOVE 'N'   TO WS-EOF-CTL.                                            
           MOVE 'N'   TO WS-EOF-K110.                                           
           MOVE 'N'   TO WS-EOF-K270.                                           
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       200-READ-CTL.                                                            
      *-----------------------------------------------------------              
           READ BP13F205 AT END                                                 
                MOVE 'Y' TO WS-EOF-CTL                                          
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1 TO WS-READ-CNT-CTL.                                            
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       300-PROCESS-REC.                                                         
      *-----------------------------------------------------------              
           MOVE 'N' TO WS-EOF-K270.                                             
           PERFORM 400-START-K270 THRU 400-EXIT.                                
           PERFORM 410-READ-K270  THRU 410-EXIT UNTIL                           
                   WS-EOF-K270 = 'Y'.                                           
                                                                                
           IF WS-EOF-K270 = 'Y'                                                 
              PERFORM 200-READ-CTL THRU 200-EXIT                                
              GO TO 300-EXIT.                                                   
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       400-START-K270.                                                          
      *-----------------------------------------------------------              
           MOVE LOW-VALUE       TO K270-KEY-FLD.                                
           MOVE F205-DTE-ALLOCN TO WS-K270-ALLOCN-DATE.                         
           MOVE WS-K270-KEY-FLD TO K270-KEY-FLD.                                
                                                                                
           START BP13K270 KEY NOT < K270-KEY-FLD INVALID                        
                 DISPLAY 'START ERROR, BP13K270-STATUS ' K270-STATUS            
                 DISPLAY 'K270-KEY-FLD ' K270-KEY-FLD                           
                 MOVE 'Y' TO WS-EOF-K270.                                       
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------              
       410-READ-K270.                                                           
      *-----------------------------------------------------------              
           READ BP13K270 NEXT AT END                                            
                MOVE 'Y' TO WS-EOF-K270                                         
                GO TO 410-EXIT.                                                 
                                                                                
           IF K270-ALLOCN-DATE = F205-DTE-ALLOCN                                
              CONTINUE                                                          
           ELSE                                                                 
              MOVE 'Y' TO WS-EOF-K270                                           
              GO TO 410-EXIT.                                                   
                                                                                
           IF (F205-NUM-NT-ZONE-SW = 'T') AND                                   
              (F205-NUM-NT-ZONE    = K270-CDE-NT)                               
               CONTINUE                                                         
           ELSE                                                                 
              IF (F205-NUM-NT-ZONE-SW = 'Z')                                    
                 IF (F205-NUM-NT-ZONE = K270-CDE-APPL-ZONE) OR                  
                    (F205-NUM-NT-ZONE (1:1) = K270-NUM-SITE)                    
                     CONTINUE                                                   
                 ELSE                                                           
                    GO TO 410-EXIT                                              
                 END-IF                                                         
              ELSE                                                              
                 GO TO 410-EXIT                                                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
           ADD 1 TO WS-READ-CNT-K270.                                           
                                                                                
           IF K270-NUM-ALLOC(1) = ZEROES                                        
              MOVE '01'             TO WS-NUM-ALLOC1.                           
                                                                                
           IF K270-NUM-ALLOC(2) = ZEROES                                        
              MOVE '02'             TO WS-NUM-ALLOC2.                           
                                                                                
           IF K270-NUM-ALLOC(3) = ZEROES                                        
              MOVE '03'             TO WS-NUM-ALLOC3.                           
                                                                                
           IF K270-NUM-ALLOC(4) = ZEROES                                        
              MOVE '04'             TO WS-NUM-ALLOC4.                           
                                                                                
           IF K270-NUM-ALLOC(5) = ZEROES                                        
              MOVE '4S'             TO WS-NUM-ALLOC5.                           
                                                                                
           IF K270-NUM-ALLOC(6) = ZEROES                                        
              MOVE '05'             TO WS-NUM-ALLOC6.                           
                                                                                
           IF K270-NUM-ALLOC(7) = ZEROES                                        
              MOVE '06'             TO WS-NUM-ALLOC7.                           
                                                                                
           IF K270-NUM-ALLOC(8) = ZEROES                                        
              MOVE '07'             TO WS-NUM-ALLOC8.                           
                                                                                
           IF K270-NUM-ALLOC(9) = ZEROES                                        
              MOVE '08'             TO WS-NUM-ALLOC9.                           
                                                                                
           PERFORM 420-DECODE-FLAT-TYPE THRU 420-EXIT.                          
           PERFORM 430-PROCESS-K110     THRU 430-EXIT.                          
                                                                                
           MOVE SPACES              TO WS-NUM-ALLOC.                            
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       420-DECODE-FLAT-TYPE.                                                    
      *-----------------------------------------------------------              
           EVALUATE F205-NUM-FLAT-TYPE                                          
              WHEN '1 '                                                         
              WHEN '1F'                                                         
                MOVE '01' TO WS-NUM-FLAT-TYPE                                   
              WHEN '1A'                                                         
                MOVE '1A' TO WS-NUM-FLAT-TYPE                                   
              WHEN '2 '                                                         
              WHEN '2F'                                                         
                MOVE '02' TO WS-NUM-FLAT-TYPE                                   
              WHEN '2A'                                                         
                MOVE '2A' TO WS-NUM-FLAT-TYPE                                   
              WHEN '3 '                                                         
              WHEN '3M'                                                         
              WHEN '3N'                                                         
                MOVE '03' TO WS-NUM-FLAT-TYPE                                   
              WHEN '4 '                                                         
              WHEN '4D'                                                         
              WHEN '4P'                                                         
                MOVE '04' TO WS-NUM-FLAT-TYPE                                   
              WHEN '5 '                                                         
              WHEN '5D'                                                         
              WHEN '5P'                                                         
                MOVE '05' TO WS-NUM-FLAT-TYPE                                   
              WHEN 'E '                                                         
              WHEN 'ED'                                                         
                MOVE '06' TO WS-NUM-FLAT-TYPE                                   
              WHEN 'H '                                                         
                MOVE '07' TO WS-NUM-FLAT-TYPE                                   
              WHEN 'MG'                                                         
                MOVE '08' TO WS-NUM-FLAT-TYPE                                   
           END-EVALUATE.                                                        
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       425-DECODE-K110-FLAT-TYPE.                                               
      *-----------------------------------------------------------              
           IF COMM8-CDE-TYP-ACTLUSE = '04' AND                                  
              COMM8-CDE-MODL-DESG = '06'                                        
              MOVE '4S'                  TO WS-K110-FLAT-TYPE                   
           ELSE                                                                 
              IF F205-NUM-FLAT-TYPE = '2F'                                      
                 MOVE '02'                  TO WS-K110-FLAT-TYPE                
              ELSE                                                              
                 MOVE COMM8-CDE-TYP-ACTLUSE TO WS-K110-FLAT-TYPE                
              END-IF                                                            
           END-IF.                                                              
                                                                                
       425-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       430-PROCESS-K110.                                                        
      *-----------------------------------------------------------              
           MOVE 'N' TO WS-EOF-K110, WS-UPDATE-FLAG.                             
           INITIALIZE WS-OFFER-COUNT-ARRAY.                                     
           MOVE K270-ESTATE         TO WS-K110-ESTATE.                          
           MOVE K270-NEIGHBOURHOOD  TO WS-K110-NEIGH.                           
           MOVE K270-CONTRACT-NO    TO WS-K110-CONTRACT-NO.                     
           MOVE K270-NUM-BLK        TO WS-K110-BLK-NO.                          
           MOVE WS-K110-AIX1        TO K110-AIX1.                               
           PERFORM 431-START-K110 THRU 431-EXIT.                                
           PERFORM 432-READ-K110  THRU 432-EXIT UNTIL                           
                   WS-EOF-K110 = 'Y'.                                           
                                                                                
           IF WS-UPDATE-FLAG = 'Y'                                              
              MOVE K270-REC  TO F270-REC                                        
              MOVE WS-OFFER-COUNT-ARRAY TO F270-NUM-UNIT-OFFER                  
              WRITE F270-REC                                                    
              ADD 1 TO WS-F270-WRITE-CTR                                        
           END-IF.                                                              
                                                                                
       430-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       431-START-K110.                                                          
      *-----------------------------------------------------------              
           MOVE 'N' TO WS-EOF-K110.                                             
                                                                                
           START BM06K110 KEY = K110-AIX1 INVALID                               
                 DISPLAY 'START ERROR, BM06K110-STATUS ' K110-STATUS            
                 DISPLAY 'K110-AIX1 ' K110-AIX1                                 
                 MOVE 'Y' TO WS-EOF-K110.                                       
                                                                                
       431-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *                                                                         
      * IF K270-NUM-ALLOC = ZEROS, DO NOT OUTPUT REC FOR THE FLAT TYPE          
      *                                                                         
      *-----------------------------------------------------------              
       432-READ-K110.                                                           
      *-----------------------------------------------------------              
           READ BM06K110 NEXT AT END                                            
                MOVE 'Y' TO WS-EOF-K110                                         
                GO TO 432-EXIT.                                                 
                                                                                
           IF K110-AIX1 = WS-K110-AIX1                                          
              CONTINUE                                                          
           ELSE                                                                 
              MOVE 'Y' TO WS-EOF-K110                                           
              GO TO 432-EXIT.                                                   
                                                                                
           IF (K110-DTE-ALLOCN = K270-ALLOCN-DATE) AND                          
              (K110-NUM-SELECTION = F205-NUM-SELECTION) AND                     
              (K110-NUM-ALLOCN-QTR = F205-DTE-BALLOT)                           
              CONTINUE                                                          
           ELSE                                                                 
              GO TO 432-EXIT                                                    
           END-IF.                                                              
                                                                                
           IF K110-FLAT-STATUS = 'SD'                                           
              ADD 1 TO WS-READ-CNT-SD                                           
              GO TO 432-EXIT                                                    
           END-IF.                                                              
                                                                                
           MOVE 'N'                     TO WS-PAIRED-FLAG-SA.                   
                                                                                
           IF F205-NUM-FLAT-TYPE = '4P' OR '5P'                                 
              PERFORM 700-READ-BP13KQ30 THRU 700-EXIT                           
           ELSE                                                                 
              PERFORM 435-PROCESS-DATA  THRU 435-EXIT                           
           END-IF.                                                              
                                                                                
       432-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------              
       435-PROCESS-DATA.                                                        
      *-----------------------------------------------------------              
           MOVE SPACES               TO BP13COMM8-REC.                          
           INITIALIZE                   BP13COMM8-REC.                          
                                                                                
           MOVE K110-SCH-ACC-NO      TO WS-LINK-REC.                            
                                                                                
           CALL WS-BP13C913 USING WS-LINK-REC,                                  
                                  BP13COMM8-REC.                                
                                                                                
                                                                                
           IF WS-PAIRED-FLAG-SA = 'Y'                                           
              CONTINUE                                                          
           ELSE                                                                 
              IF (WS-NUM-FLAT-TYPE = '1A' OR '2A')                              
                 IF (K270-NUM-COMBINE-SA = 'Y') AND                             
                    (COMM8-CDE-TYP-ACTLUSE = '01' OR '02') AND                  
                    (COMM8-CDE-MODL-DESG = '80' OR '89' OR '92')                
                    CONTINUE                                                    
                 ELSE                                                           
                    IF ((WS-NUM-FLAT-TYPE = '1A') AND                           
                       (COMM8-CDE-TYP-ACTLUSE = '01') AND                       
                       (COMM8-CDE-MODL-DESG = '80' OR '89' OR '92')) OR         
                       ((WS-NUM-FLAT-TYPE = '2A') AND                           
                       (COMM8-CDE-TYP-ACTLUSE = '02') AND                       
                       (COMM8-CDE-MODL-DESG = '80' OR '89' OR '92'))            
                        CONTINUE                                                
                    ELSE                                                        
                        GO TO 432-EXIT                                          
                    END-IF                                                      
                 END-IF                                                         
              ELSE                                                              
                 IF F205-NUM-FLAT-TYPE = '3M'                                   
                    IF ((COMM8-CDE-TYP-ACTLUSE = WS-NUM-FLAT-TYPE) AND          
                       ((COMM8-CDE-MODL-DESG = '81' OR '82' OR '83' OR          
                       '84' OR '85' OR '86' OR '87' OR '88' OR '93') OR         
                         K110-NUM-DESIGN-OFFER = 'P'))                          
                       CONTINUE                                                 
                    ELSE                                                        
                       GO TO 432-EXIT                                           
                    END-IF                                                      
                 ELSE                                                           
                    IF F205-NUM-FLAT-TYPE = '3N'                                
                       IF (COMM8-CDE-TYP-ACTLUSE = WS-NUM-FLAT-TYPE) AND        
                          (COMM8-CDE-MODL-DESG NOT = '34' AND '70' AND          
                           '71' AND '72' AND '73' AND '81' AND '82' AND         
                           '83' AND '84' AND '85' AND '86' AND '87' AND         
                           '88')                                                
                           IF K110-NUM-DESIGN-OFFER NOT = 'P'                   
                              CONTINUE                                          
                           ELSE                                                 
                              GO TO 432-EXIT                                    
                           END-IF                                               
                       ELSE                                                     
                           GO TO 432-EXIT                                       
                       END-IF                                                   
                    ELSE                                                        
                       IF F205-NUM-FLAT-TYPE = '2F'                             
                          IF K110-NUM-DESIGN-OFFER = 'F' AND                    
                             COMM8-CDE-TYP-ACTLUSE NOT = '03'                   
                             CONTINUE                                           
                          ELSE                                                  
                             GO TO 432-EXIT                                     
                          END-IF                                                
                       ELSE                                                     
                          IF F205-NUM-GEN3 = 'Y'                                
                             IF (COMM8-CDE-TYP-ACTLUSE =                        
                                 WS-NUM-FLAT-TYPE) AND                          
                                (COMM8-CDE-MODL-DESG = '90' OR                  
                                                       '91' OR '95')            
                                CONTINUE                                        
                             ELSE                                               
                                GO TO 432-EXIT                                  
                             END-IF                                             
                          ELSE                                                  
                             IF F205-NUM-GEN3 = 'N'                             
                                IF (COMM8-CDE-TYP-ACTLUSE =                     
                                    WS-NUM-FLAT-TYPE)  AND                      
                                   (COMM8-CDE-MODL-DESG NOT = '90' AND          
                                                         '91' AND '95')         
                                   CONTINUE                                     
                                ELSE                                            
                                   GO TO 432-EXIT                               
                                END-IF                                          
                             ELSE                                               
                                IF WS-NUM-FLAT-TYPE = '06'                      
                                   IF COMM8-CDE-TYP-ACTLUSE = '06' OR           
                                                              '08'              
                                      CONTINUE                                  
                                   ELSE                                         
                                      GO TO 432-EXIT                            
                                   END-IF                                       
                                ELSE                                            
                                   IF (COMM8-CDE-TYP-ACTLUSE =                  
                                       WS-NUM-FLAT-TYPE)  AND                   
                                      (COMM8-CDE-MODL-DESG NOT = '80'           
                                                     AND '89' AND '92')         
                                      CONTINUE                                  
                                   ELSE                                         
                                      GO TO 432-EXIT                            
                                   END-IF                                       
                                END-IF                                          
                             END-IF                                             
                          END-IF                                                
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
      **---> GET UPGRADING STATUS                                               
           MOVE SPACES      TO LINK-AREA.                                       
           INITIALIZE          LINK-AREA.                                       
           MOVE K110-SCH-ACC-NO(1:4) TO LINK-NUM-SCHM.                          
           MOVE K110-SCH-ACC-NO(5:7) TO LINK-NUM-ACCNT.                         
                                                                                
           MOVE SPACES        TO WS-NUM-UPG-STATUS.                             
           CALL 'DJ02C906' USING LINK-AREA.                                     
                                                                                
           IF LINK-SQL-RETCODE = 0                                              
              MOVE LINK-NUM-BNDRY-STATUS  TO WS-NUM-UPG-STATUS                  
           ELSE                                                                 
              IF LINK-SQL-RETCODE = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PIDB : '                         
                                                K110-SCH-ACC-NO(1:11)           
              ELSE                                                              
                 MOVE LINK-SQL-RETCODE TO WS-SQL-RETCODE                        
                 DISPLAY 'RETCODE : ' WS-SQL-RETCODE                            
                 DISPLAY 'ERROR IN CALLING DJ02C906'.                           
                                                                                
           IF (K110-DTE-ACCEPTANCE = ZEROS OR SPACES OR LOW-VALUES) AND         
              (K110-DTE-KEY-ISSUED  = ZEROS OR SPACES OR LOW-VALUES)            
               CONTINUE                                                         
           ELSE                                                                 
              ADD 1 TO WS-READ-CNT-AS                                           
              GO TO 432-EXIT.                                                   
                                                                                
           IF K110-ALLOCN-TAG NOT = SPACES AND LOW-VALUES AND                   
                                    'BFS'                                       
              ADD 1 TO WS-READ-CNT-HOLD                                         
              GO TO 432-EXIT.                                                   
                                                                                
           IF K110-FLAT-STATUS = 'RP' OR 'RR'                                   
              ADD   1  TO WS-READ-CNT-RP.                                       
                                                                                
           ADD 1 TO WS-READ-CNT-K110.                                           
                                                                                
      *>>> ACCUMULATE UNIT OFFERS <<<<<<<                                       
                                                                                
           PERFORM 450-ACCUM-OFFER THRU 450-EXIT.                               
                                                                                
           PERFORM 425-DECODE-K110-FLAT-TYPE THRU 425-EXIT.                     
                                                                                
           IF (COMM8-CDE-TYP-ACTLUSE = '02' AND                                 
              (COMM8-CDE-MODL-DESG = '96' OR '97' OR '98'))                     
              IF K270-NUM-ALLOC(2) = ZEROES AND K270-NUM-ALLOC(1) > 0           
                 MOVE SPACES             TO WS-NUM-ALLOC2                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-K110-FLAT-TYPE = WS-NUM-ALLOC1 OR WS-NUM-ALLOC2 OR             
                                  WS-NUM-ALLOC3 OR WS-NUM-ALLOC4 OR             
                                  WS-NUM-ALLOC5 OR WS-NUM-ALLOC6 OR             
                                  WS-NUM-ALLOC7 OR WS-NUM-ALLOC8 OR             
                                  WS-NUM-ALLOC9                                 
              ADD 1   TO WS-SKIP-RECORD                                         
              GO TO 432-EXIT.                                                   
                                                                                
           PERFORM 440-BM06K100-READ   THRU 440-EXIT.                           
                                                                                
           IF WS-K100-FND = 'Y'                                                 
              MOVE K100-CDE-BLK TO WS-LINK-BLDNG-GL                             
              DISPLAY 'BLDNG : ' WS-LINK-BLDNG-GL                               
                                                                                
              CALL WS-BP13C916 USING WS-LINK-REC-C916 WS-BP13C916-REC           
              MOVE C916-NME-LOCAL-PRCNCT TO WS-F800-NME-LOCAL-PRCNCT            
              DISPLAY 'PRCNCT: ' C916-NME-LOCAL-PRCNCT                          
           END-IF.                                                              
                                                                                
           PERFORM 600-WRITE-F800 THRU 600-EXIT.                                
                                                                                
       435-EXIT.                                                                
           EXIT.                                                                
                                                                                
       440-BM06K100-READ.                                                       
      *-----------------------------------------------------------              
           MOVE 'N'                  TO WS-K100-FND.                            
           MOVE LOW-VALUES           TO K100-KEY-FLD.                           
           MOVE K110-AIX1            TO K100-KEY-FLD.                           
                                                                                
           READ BM06K100.                                                       
           EVALUATE K100-STATUS                                                 
           WHEN 00                                                              
              ADD  1       TO   WS-READ-CNT-K100                                
              MOVE  'Y '   TO   WS-K100-FND                                     
           WHEN 23                                                              
              MOVE  'N'    TO   WS-K100-FND                                     
              MOVE SPACES  TO   WS-F800-NME-LOCAL-PRCNCT                        
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BM06K100 : ' K100-STATUS                   
              MOVE K100-STATUS            TO  RETURN-CODE                       
              PERFORM 999-CLOSE-ROUTINE   THRU 999-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       440-EXIT.                                                                
           EXIT.                                                                
                                                                                
       450-ACCUM-OFFER.                                                         
      *-----------------------------------------------------------              
           MOVE 'Y' TO WS-UPDATE-FLAG.                                          
           EVALUATE TRUE                                                        
              WHEN K110-NUM-DESIGN-OFFER = 'F' AND                              
                   COMM8-CDE-MODL-DESG  NOT = '74'                              
                   ADD 1 TO WS-OFFER-COUNT(02)                                  
                                                                                
              WHEN COMM8-CDE-TYP-ACTLUSE = '01'                                 
                   ADD 1 TO WS-OFFER-COUNT(01)                                  
                                                                                
              WHEN COMM8-CDE-TYP-ACTLUSE = '02'                                 
                   ADD 1 TO WS-OFFER-COUNT(02)                                  
                                                                                
              WHEN COMM8-CDE-TYP-ACTLUSE = '03'                                 
                   ADD 1 TO WS-OFFER-COUNT(03)                                  
                                                                                
              WHEN COMM8-CDE-TYP-ACTLUSE = '04' AND                             
                   COMM8-CDE-MODL-DESG  NOT = '06'                              
                   ADD 1 TO WS-OFFER-COUNT(04)                                  
                                                                                
              WHEN COMM8-CDE-TYP-ACTLUSE = '04' AND                             
                   COMM8-CDE-MODL-DESG   = '06'                                 
                   ADD 1 TO WS-OFFER-COUNT(05)                                  
                                                                                
              WHEN COMM8-CDE-TYP-ACTLUSE = '05'                                 
                   ADD 1 TO WS-OFFER-COUNT(06)                                  
                                                                                
              WHEN COMM8-CDE-TYP-ACTLUSE = '06'                                 
                   ADD 1 TO WS-OFFER-COUNT(07)                                  
                                                                                
              WHEN COMM8-CDE-TYP-ACTLUSE = '07'                                 
                   ADD 1 TO WS-OFFER-COUNT(08)                                  
                                                                                
              WHEN COMM8-CDE-TYP-ACTLUSE = '08'                                 
                   ADD 1 TO WS-OFFER-COUNT(09)                                  
           END-EVALUATE.                                                        
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------              
       600-WRITE-F800.                                                          
      *-----------------------------------------------------------              
           MOVE SPACES                 TO F800-REC.                             
           INITIALIZE F800-REC.                                                 
                                                                                
           IF K110-REGN-NO(1:2) = '##'                                          
               MOVE SPACES TO K110-REGN-NO                                      
           END-IF.                                                              
                                                                                
           MOVE K110-REC               TO F800-REC (1:500).                     
                                                                                
           MOVE COMM8-NME-STREET       TO F800-STREET-NAME.                     
           MOVE COMM8-NUM-POSTAL-CODE  TO F800-POSTAL-CODE.                     
           MOVE COMM8-NUM-LEVEL        TO F800-LEVEL-NO.                        
           MOVE COMM8-NUM-UNIT-MAIN    TO F800-MAIN-UNIT.                       
           MOVE COMM8-NUM-UNIT-SUB     TO F800-SUB-UNIT.                        
           MOVE COMM8-CDE-BUC          TO F800-CDE-BUC.                         
           MOVE COMM8-QTY-AREA-TOTAL   TO F800-QTY-AREA-TOTAL.                  
           MOVE COMM8-QTY-AREA-ORGNL   TO F800-QTY-AREA-ORGNL.                  
           MOVE K270-CDE-NT            TO F800-CDE-NT.                          
           IF K270-CDE-SELTY = 'BTO' OR 'WIS' OR 'WIT' OR 'WIH' OR              
                               'WIP' OR 'WIA' OR 'WIB' OR 'WIC' OR              
                               'BE ' OR 'SBF'                                   
              MOVE SPACES             TO F800-CDE-ZONE                          
           ELSE                                                                 
              MOVE K270-CDE-APPL-ZONE TO F800-CDE-ZONE                          
           END-IF.                                                              
           MOVE K270-CDE-FLAT-STATUS   TO F800-CDE-FLAT-STATUS.                 
           MOVE K270-CDE-SELTY         TO F800-CDE-SELTY.                       
           MOVE K270-NUM-SITE          TO F800-NUM-SITE.                        
           IF COMM8-CDE-TYP-ACTLUSE = '02'                                      
              MOVE K270-NUM-BONUS      TO F800-NUM-BONUS                        
           ELSE                                                                 
              MOVE 'N'                 TO F800-NUM-BONUS                        
           END-IF.                                                              
           IF (COMM8-CDE-TYP-ACTLUSE = '01' OR '02') AND                        
              (COMM8-CDE-MODL-DESG = '80' OR '89' OR '92')                      
               MOVE K270-NUM-COMBINE-SA TO F800-NUM-COMBINE-SA                  
           END-IF.                                                              
           MOVE F205-DTE-BALLOT        TO F800-DTE-BALLOT.                      
           MOVE WS-NUM-UPG-STATUS      TO F800-NUM-UPG-STATUS.                  
                                                                                
           EVALUATE F800-CDE-TYP-ACTUSE                                         
              WHEN '01'                                                         
                IF F800-CDE-MODL-DESG = '80' OR '89' OR '92'                    
                   MOVE '1A' TO F800-NUM-FT                                     
                ELSE                                                            
                   MOVE '01' TO F800-NUM-FT                                     
                END-IF                                                          
              WHEN '02'                                                         
                IF F800-CDE-MODL-DESG = '80' OR '89' OR '92'                    
                   MOVE '2A' TO F800-NUM-FT                                     
                ELSE                                                            
                   MOVE '02' TO F800-NUM-FT                                     
                END-IF                                                          
              WHEN '03'                                                         
                MOVE '03' TO F800-NUM-FT                                        
              WHEN '04'                                                         
                MOVE '04' TO F800-NUM-FT                                        
              WHEN '05'                                                         
                MOVE '05' TO F800-NUM-FT                                        
              WHEN '06'                                                         
                MOVE '06' TO F800-NUM-FT                                        
           END-EVALUATE.                                                        
                                                                                
      ** MOVE 02 TO FLAT TYPE WHEN DESG OFFER = F AND MODL DESG NOT 74          
      ** DO NOT OVERWRITE WITH 02 IF 3-ROOM FSH CASE AND SERS CASE              
                                                                                
           IF K110-NUM-DESIGN-OFFER = 'F' AND                                   
              F800-CDE-MODL-DESG NOT = '74' AND                                 
              F800-CDE-TYP-ACTUSE NOT = '03' AND                                
              F800-CDE-SELTY NOT = 'SER'                                        
              MOVE '02'                    TO F800-CDE-TYP-ACTUSE               
                                              F800-NUM-FT                       
           END-IF.                                                              
                                                                                
           MOVE WS-F800-NME-LOCAL-PRCNCT   TO F800-NME-LOCAL-PRCNCT.            
                                                                                
           WRITE F800-REC.                                                      
           ADD 1 TO WS-WRITE-CNT-F800.                                          
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       700-READ-BP13KQ30.                                                       
      *-----------------------------------------------------------              
           MOVE SPACES                   TO KQ30-ALT-KEY1.                      
           MOVE K110-SCH-ACC-NO          TO KQ30-ALT-KEY1.                      
                                                                                
           READ BP13KQ30 KEY IS KQ30-ALT-KEY1.                                  
                                                                                
           EVALUATE KQ30-STATUS                                                 
           WHEN 00                                                              
              ADD  1       TO   WS-READ-CNT-KQ30                                
              PERFORM 435-PROCESS-DATA  THRU 435-EXIT                           
              MOVE KQ30-KEY-FLD          TO F110-KEY-FLD                        
              PERFORM 750-READ-BM06F110  THRU 750-EXIT                          
           WHEN 23                                                              
              CONTINUE                                                          
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13KQ30 : ' KQ30-STATUS                   
              MOVE KQ30-STATUS            TO  RETURN-CODE                       
              PERFORM 999-CLOSE-ROUTINE   THRU 999-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       750-READ-BM06F110.                                                       
      *-------------------------------------------------------------            
           READ BM06F110.                                                       
                                                                                
           EVALUATE F110-STATUS                                                 
             WHEN 00                                                            
                MOVE 'Y'                     TO WS-PAIRED-FLAG-SA               
                MOVE F110-REC                TO K110-REC                        
                PERFORM 435-PROCESS-DATA     THRU 435-EXIT                      
             WHEN 23                                                            
                DISPLAY 'PAIR SCH-ACC-NO REC NOT FOUND IN BM06K110'             
                      ', K110-SCH-ACC-NO : ' F110-SCH-ACC-NO                    
                CONTINUE                                                        
             WHEN OTHER                                                         
                DISPLAY 'BM06K110 READ ERROR,STATUS : ' K110-STATUS             
                      ', K110 KEY FLD : ' K110-KEY-FLD                          
                MOVE K110-STATUS       TO RETURN-CODE                           
                PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       750-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       999-CLOSE-ROUTINE.                                                       
      *-----------------------------------------------------------              
           DISPLAY '*----------  BP13C226 SUMMARY LIST  ----------*'.           
           DISPLAY ' NO OF BP13F205 READ    = ' WS-READ-CNT-CTL.                
           DISPLAY ' NO OF BP13K270 READ    = ' WS-READ-CNT-K270.               
           DISPLAY ' NO OF BM06K110 READ    = ' WS-READ-CNT-K110.               
           DISPLAY ' NO OF BM06K100 READ    = ' WS-READ-CNT-K100.               
           DISPLAY ' NO OF BM06F800 WRITE   = ' WS-WRITE-CNT-F800.              
           DISPLAY ' REC NOT FOR ALLOCN     = ' WS-SKIP-RECORD.                 
           DISPLAY ' NO OF RECORD AS        = ' WS-READ-CNT-AS.                 
           DISPLAY ' NO OF RECORD SD        = ' WS-READ-CNT-SD.                 
           DISPLAY ' REC FREEZE FR ALLOCN   = ' WS-READ-CNT-HOLD.               
           DISPLAY ' NO OF RECORD RP        = ' WS-READ-CNT-RP.                 
           DISPLAY ' NO OF F270 REC WRITTEN = ' WS-F270-WRITE-CTR.              
                                                                                
           CLOSE BP13F205                                                       
                 BM06F800                                                       
                 BP13K270                                                       
                 BM06K110                                                       
                 BM06K100                                                       
                 BP13KQ30                                                       
                 BM06F110                                                       
                 BP13F270.                                                      
                                                                                
           IF K110-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, BM06K110-STATUS ' K110-STATUS.              
           IF F110-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, BM06K110-STATUS ' F110-STATUS.              
           IF F110-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, BM06K110-STATUS ' F110-STATUS.              
           IF K270-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, BP13K270-STATUS ' K270-STATUS.              
           IF K100-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, K100-STATUS ' K100-STATUS.                  
           IF KQ30-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, KQ30-STATUS ' KQ30-STATUS.                  
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
