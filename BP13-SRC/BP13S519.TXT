  /*===============================================================*/           
  /*  OBJECTIVES : TO TAB THE RESULT OF BALLOTING ON THE BOOKING   */           
  /*               EXERCISE UNDER GRO PRIORITY SCHEME BY RES/      */           
  /*               NON-RES, ESTATE, FLAT TYPE                      */           
  /*                                                               */           
  /*---------------------------------------------------------------*/           
  /*  CHG-NO   BY  DATE   DESCRIPTION                              */           
  /*--------  --- ------  -----------                              */           
  /*          GD  030795                                           */           
  /*===============================================================*/           
       /***********************************************************/            
       /* IMPORTANT NOTE : IF GRORANK IS SPACES, HIGH-VALUES ARE  */            
       /*    MOVED TO IT IN ORDER TO PLACE IT AT THE LAST COLUMN  */            
       /*    FOR EACH OF THE SUCC/UNSUCC COLUMN. OTHERWISE, THE   */            
       /*    GRORANK = ' '   COLUMN (WITH THE LOWEST PRIORITY)    */            
       /*    WILL BE PRINTED AHEAD OF THE OTHER PRIORITIES.  -GD  */            
       /***********************************************************/            
  PROC FORMAT;                                                                  
   VALUE $GRANK   "Ÿ"="NON-HIGHEST PRIORITY"  /* CONTAINS HIGH-VALUES */        
                  "1"="RANK 1ST (HIGHEST PRIORITY)"                             
                  "2"="RANK 2ND"                                                
                  "3"="RANK 3RD";                                               
                                                                                
   %GLOBAL DATE QTR YR;                                                         
   DATA BP13F520;                                                               
        INFILE BP13F520 END=EOF;                                                
        INPUT                                                                   
              @187 NT1      $CHAR3.                                             
              @211 FT       $CHAR2.                                             
              @213 ALL_SCH  $CHAR3.                                             
              @226 SUCC_NT  $CHAR3.                                             
              @163 GRORANK  $CHAR1.                                             
              @253 HA1_RES  $CHAR1.                                             
              @254 HA2_RES  $CHAR1.                                             
              @121 HA3_RES  $CHAR1.                                             
              @122 HA4_RES  $CHAR1.                                             
              ;                                                                 
                                                                                
       IF _N_=1 THEN                                                            
          DO;                                                                   
             CUR_DTE=PUT(TODAY(),DDMMYY8.);                                     
             CALL SYMPUT('DATE',CUR_DTE);                                       
          END;                                                                  
                                                                                
       IF SUCC_NT NOT > ' ' THEN                                                
          SUCC = 'UNSUCCESSFUL';                                                
       ELSE                                                                     
          SUCC = 'SUCCESSFUL';                                                  
                                                                                
       IF GRORANK = ' ' THEN GRORANK = 'Ÿ'; /*  CONTAINS HIGH-VALUES */         
       RES_SCH = '            ';                                                
       IF ((HA1_RES=" " OR HA1_RES="T") AND                                     
           (HA2_RES=" " OR HA2_RES="T") AND                                     
           (HA3_RES=" " OR HA3_RES="T") AND                                     
           (HA4_RES=" " OR HA4_RES="T")) THEN                                   
           DO;                                                                  
              RES_SCH = 'NON-RESIDENT';   /* NON-RESIDENT */                    
           END;                                                                 
       ELSE                                                                     
           DO;                                                                  
              RES_SCH = 'RESIDENT';                                             
           END;                                                                 
                                                                                
   RUN;                                                                         
                                                                                
   PROC SORT DATA=BP13F520;                                                     
     BY ALL_SCH;                                                                
   RUN;                                                                         
                                                                                
   DATA BP13PRIO;                                                               
        INFILE PRIOFMT;                                                         
        INPUT @2 ALL_SCH $CHAR3. @9 PRIO_SCH $CHAR3.;                           
        IF PRIO_SCH = 'CCC';    /* SELECT ONLY GRO SCHEMES */                   
   RUN;                                                                         
                                                                                
   PROC SORT; BY ALL_SCH;                                                       
   RUN;                                                                         
                                                                                
   DATA F520SCH;                                                                
   MERGE BP13F520(IN=M) BP13PRIO(IN=T); BY ALL_SCH;                             
         IF M AND T;                                                            
   RUN;                                                                         
                                                                                
   OPTIONS MISSING=0 NODATE;                                                    
   PROC PRINTTO UNIT=18 NEW;                                                    
   PROC TABULATE DATA=F520SCH MISSING FORMAT=10.;                               
                                                                                
   TITLE1 "BP13L519 HDB3                           SYSTEM OF COMMITMENT         
                                         PAGE : ";                              
   TITLE2 "                                          SUMMARY REPORT SUCCESSFUL/U
NSUCCESSFUL                                     DATE : &DATE" ;                 
   TITLE3 "BY RESIDENT/NON-RESIDENT/FLAT TYPE UNDER GRO SCHEME           ";     
                                                                                
   CLASS  RES_SCH NT1 FT SUCC GRORANK;                                          
                                                                                
   FORMAT GRORANK $GRANK.;                                                      
                                                                                
   KEYLABEL ALL=""                                                              
            PCTN="RATE (%)"                                                     
            N="";                                                               
   LABEL RES_SCH ="SCHEME : ";                                                  
                                                                                
   TABLE RES_SCH,                                                               
       NT1=''*FT='' ALL='TOTAL',                                                
       (GRORANK=''*(SUCC=''*N=''                                                
       SUCC=''*PCTN<SUCC GRORANK ALL>*F=10.2))                                  
       N=''*ALL='TOTAL';                                                        
   RUN;                                                                         
                                                                                
