       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB25.                                                 
      *AUTHOR.        LLOYD CAPARRO.                                            
      *                                                                         
      ************************************************************              
      *                   SOC SYSTEM (SOC)                       *              
      *==========================================================*              
      * DATE WRITTEN      : 20 FEB 2005                          *              
      *                                                          *              
      * 1. OBJECTIVE      : CREATE 2 FILES FROM BP13F593 WITH    *              
      *                   : FOUND AND NOT FOUND CASES IN         *              
      *                   : BB24K020 FILE                        *              
      *                                                          *              
      * 2. INPUT          : BP13F593 - BP13.F593.C995.NOTFOUND   *              
      *                   : BB24K020 - BB24.K020.OCCUPIER        *              
      *                                                          *              
      * 3. OUTPUT         : P13F593F - BP13.F593.CB25.FOUND      *              
      *                   : P13F593N - BP13.F593.CB25.NOTFOUND   *              
      *==========================================================*              
      *  MODIFICATIONS:                                          *              
      *                                                          *              
      *  CHGE-NO  DATE       OIC  DESCRIPTION                    *              
      *  -------- ---------- ---- ------------------------------ *              
      *  BP132667 21/02/2005 CG7  NEW PROGRAM.                   *              
      *                                                          *              
      ************************************************************              
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F593     ASSIGN        TO BP13F593.                       
                                                                                
           SELECT BB24K020     ASSIGN        TO BB24K020                        
                               ACCESS MODE   IS DYNAMIC                         
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K020-KEY-FLD                    
                               ALTERNATE RECORD KEY IS K020-NUM-UIN             
                                                WITH DUPLICATES                 
                               FILE STATUS   IS WS-K020-STATUS.                 
                                                                                
           SELECT BB24K010     ASSIGN BB24K010                                  
                               ORGANIZATION IS INDEXED                          
                               ACCESS MODE IS RANDOM                            
                               RECORD KEY IS K010-KEY-FLD                       
                               FILE STATUS IS WS-K010-STATUS.                   
                                                                                
           SELECT P13F593F     ASSIGN        TO P13F593F.                       
                                                                                
           SELECT P13F593N     ASSIGN        TO P13F593N.                       
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F593                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 2000 CHARACTERS                  
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       COPY BP13F593.                                                           
                                                                                
       FD  BB24K020                                                             
                               RECORD CONTAINS 200 CHARACTERS.                  
       COPY BB24K020.                                                           
                                                                                
       FD   BB24K010           RECORD CONTAINS 5000 CHARACTERS.                 
       COPY BB24K010.                                                           
                                                                                
       FD  P13F593F                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 2000 CHARACTERS                  
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       01  P13F593F-REC                  PIC X(2000).                           
                                                                                
       FD  P13F593N                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 2000 CHARACTERS                  
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       01  P13F593N-REC                  PIC X(2000).                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILE-STATUS.                                                         
           05  WS-K020-STATUS            PIC 9(2)  VALUE ZEROES.                
           05  WS-K010-STATUS            PIC 9(2)  VALUE ZEROES.                
                                                                                
       01  END-OF-FILE-FLAGS.                                                   
           05  WS-F593-EOF               PIC X     VALUE 'N'.                   
           05  WS-K020-EOF               PIC X     VALUE 'N'.                   
           05  WS-K010-FOUND-SW      PIC X(01)  VALUE 'N'.                      
               88  WS-K010-FOUND     VALUE 'Y'.                                 
                                                                                
       01  COUNTERS.                                                            
           05  WS-F593-READ-CNT          PIC 9(6)  VALUE ZEROES.                
           05  WS-595F-WRITE-CNT         PIC 9(6)  VALUE ZEROES.                
           05  WS-595N-WRITE-CNT         PIC 9(6)  VALUE ZEROES.                
           05  WS-NRIC-NOTFND            PIC 9(6)  VALUE ZEROES.                
           05  WS-NRIC-LIST-CNT          PIC 9(1)  VALUE ZEROES.                
           05  WS-CNT                    PIC 9(1)  VALUE ZERO.                  
           05  WS-K010-READ              PIC 9(05)  VALUE 0.                    
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURRENT-DATE1          PIC X(8) VALUE SPACES.                 
           05  WS-CURRENT-TIME           PIC X(9) VALUE SPACES.                 
               05  WS-TIME REDEFINES WS-CURRENT-TIME.                           
                   10  WS-HHMMSSMS       PIC X(8).                              
                   10  WS-GM             PIC X(1).                              
           05  FILLER                    PIC X(4) VALUE SPACES.                 
                                                                                
       01  TEMPORARY-VARIABLES.                                                 
           05  WS-NRIC OCCURS 2 TIMES.                                          
               10 WS-NRIC-LIST           PIC X(9)  VALUE SPACES.                
           05  WS-RUN-DATE               PIC X(10) VALUE SPACES.                
           05  WS-NRIC-FOUND             PIC X(1)  VALUE 'N'.                   
           05  WS-K020-REC-PROCESS       PIC X(1)  VALUE 'N'.                   
           05  WS-K020-NRIC-PREV         PIC X(9)  VALUE SPACES.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *************************************************************             
       0000-MAIN.                                                               
      *************************************************************             
           PERFORM 1000-OPEN-ROUTINE THRU 1000-EXIT.                            
           PERFORM 2000-PROCESS-RECORDS THRU 2000-EXIT                          
             UNTIL WS-F593-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       1000-OPEN-ROUTINE.                                                       
      *************************************************************             
           OPEN INPUT  BP13F593                                                 
                       BB24K020                                                 
                       BB24K010                                                 
                OUTPUT P13F593F                                                 
                       P13F593N.                                                
                                                                                
           IF WS-K020-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BB24K020 OPEN ERROR ' WS-K020-STATUS                     
              MOVE WS-K020-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K010-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BB24K010 OPEN ERROR ' WS-K010-STATUS                     
              MOVE WS-K010-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE    TO WS-CURRENT-DATE.                    
                                                                                
           STRING WS-CURRENT-DATE1(7:2) '/'                                     
                  WS-CURRENT-DATE1(5:2) '/'                                     
                  WS-CURRENT-DATE1(1:4)                                         
                  DELIMITED BY SIZE                                             
             INTO WS-RUN-DATE                                                   
                                                                                
           PERFORM 1100-READ-BP13F593 THRU 1100-EXIT.                           
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       1100-READ-BP13F593.                                                      
      *************************************************************             
           READ BP13F593                                                        
             AT END                                                             
                MOVE 'Y'                 TO WS-F593-EOF                         
            NOT AT END                                                          
                ADD 1                    TO WS-F593-READ-CNT                    
           END-READ.                                                            
                                                                                
       1100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       2000-PROCESS-RECORDS.                                                    
      *************************************************************             
           MOVE SPACES                   TO WS-NRIC-LIST(1)                     
                                            WS-NRIC-LIST(2).                    
                                                                                
           MOVE F593-NUM-NRIC            TO WS-NRIC-LIST(1).                    
           MOVE 1                        TO WS-NRIC-LIST-CNT.                   
                                                                                
           MOVE 'N'                      TO WS-NRIC-FOUND.                      
                                                                                
           PERFORM VARYING WS-CNT FROM 1 BY 1                                   
             UNTIL WS-CNT > WS-NRIC-LIST-CNT                                    
                OR WS-NRIC-FOUND = 'Y'                                          
                   MOVE SPACES           TO P13F593F-REC                        
                                            P13F593N-REC                        
                                                                                
                   PERFORM 3000-START-BB24K020 THRU 3000-EXIT                   
           END-PERFORM.                                                         
                                                                                
           IF WS-NRIC-FOUND = 'Y'                                               
              PERFORM 3200-READ-BB24K010 THRU 3200-EXIT                         
              MOVE BP13F593-REC          TO P13F593F-REC                        
              IF K010-CDE-SCH-ALLOC-BYR = '6 ' OR '6J' OR '8C' OR               
                                          '8D' OR '8F'                          
                MOVE K010-CDE-SCH-ALLOC-BYR TO P13F593F-REC(46:3)               
                MOVE 'E'                    TO P13F593F-REC(49:1)               
                WRITE P13F593F-REC                                              
                ADD 1             TO WS-595F-WRITE-CNT                          
              ELSE                                                              
                MOVE BP13F593-REC           TO P13F593N-REC                     
                WRITE P13F593N-REC                                              
                ADD 1                       TO WS-595N-WRITE-CNT                
              END-IF                                                            
           ELSE                                                                 
              MOVE BP13F593-REC          TO P13F593N-REC                        
                                                                                
              WRITE P13F593N-REC                                                
                                                                                
              ADD 1                      TO WS-595N-WRITE-CNT                   
           END-IF.                                                              
                                                                                
           PERFORM 1100-READ-BP13F593 THRU 1100-EXIT.                           
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3000-START-BB24K020.                                                     
      *************************************************************             
           MOVE SPACES                   TO BB24K020-REC.                       
           INITIALIZE                       BB24K020-REC.                       
                                                                                
           MOVE WS-NRIC-LIST(WS-CNT)     TO K020-NUM-UIN                        
                                            WS-K020-NRIC-PREV.                  
           MOVE 'N'                      TO WS-K020-EOF                         
                                            WS-K020-REC-PROCESS.                
                                                                                
           START BB24K020 KEY >= K020-NUM-UIN.                                  
                                                                                
           EVALUATE WS-K020-STATUS                                              
               WHEN 00                                                          
                    PERFORM 3100-READNEXT-BB24K020 THRU 3100-EXIT               
                      UNTIL K020-NUM-UIN NOT = WS-K020-NRIC-PREV                
                         OR WS-K020-EOF = 'Y'                                   
                         OR WS-NRIC-FOUND = 'Y'                                 
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1 TO WS-NRIC-NOTFND                                     
               WHEN OTHER                                                       
                    DISPLAY 'BP13K020 START ERROR ' WS-K020-STATUS              
                    DISPLAY 'K020 KEY: ' K020-KEY-FLD                           
                    MOVE WS-K020-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3100-READNEXT-BB24K020.                                                  
      *************************************************************             
           READ BB24K020 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'                 TO WS-K020-EOF                         
           END-READ.                                                            
                                                                                
           EVALUATE WS-K020-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    IF K020-NUM-UIN = WS-K020-NRIC-PREV                         
                       MOVE 'Y'          TO WS-NRIC-FOUND                       
                                            WS-K020-REC-PROCESS                 
                    ELSE                                                        
                       IF WS-K020-REC-PROCESS NOT = 'Y'                         
                          ADD 1 TO WS-NRIC-NOTFND                               
                       END-IF                                                   
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                     ADD 1 TO WS-NRIC-NOTFND                                    
               WHEN OTHER                                                       
                    DISPLAY 'BP13K020 READ NEXT ERROR ' WS-K020-STATUS          
                    DISPLAY 'K020 KEY: ' K020-KEY-FLD                           
                    MOVE WS-K020-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      **************************                                                
       3200-READ-BB24K010.                                                      
      **************************                                                
           MOVE K020-NUM-CASE              TO  K010-NUM-CASE.                   
           MOVE 'N'                        TO  WS-K010-FOUND-SW.                
                                                                                
           READ BB24K010.                                                       
           IF WS-K010-STATUS = 00                                               
              MOVE 'Y'                     TO  WS-K010-FOUND-SW                 
              ADD  1                       TO  WS-K010-READ                     
           ELSE                                                                 
           IF WS-K010-STATUS = 23                                               
              DISPLAY 'NUM CASE ' K020-NUM-CASE                                 
                      'IS NOT FOUND IN BB24K010!'                               
              MOVE 'N'                     TO  WS-K010-FOUND-SW                 
           ELSE                                                                 
              DISPLAY 'ERROR READING BB24K010! STATUS : '                       
                      WS-K010-STATUS                                            
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       9000-CLOSE-ROUTINE.                                                      
      *************************************************************             
           DISPLAY '   '.                                                       
           DISPLAY 'RUN DATE : ' WS-RUN-DATE.                                   
           DISPLAY '*--------- BP13CB25 CONTROL TOTAL ---------*'.              
           DISPLAY '   '.                                                       
           DISPLAY 'NO. OF READS FROM F593   : ' WS-F593-READ-CNT.              
           DISPLAY 'NO. OF NOTFND IN K020    : ' WS-NRIC-NOTFND.                
           DISPLAY 'NO. OF FND FROM K010     : ' WS-K010-READ.                  
           DISPLAY 'RECS WRITTEN TO P13F593F : ' WS-595F-WRITE-CNT.             
           DISPLAY 'RECS WRITTEN TO P13F593N : ' WS-595N-WRITE-CNT.             
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13F593                                                       
                 BB24K020                                                       
                 BB24K010                                                       
                 P13F593F                                                       
                 P13F593N.                                                      
                                                                                
           IF WS-K020-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BB24K020 CLOSE ERROR ' WS-K020-STATUS                    
              MOVE WS-K020-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
