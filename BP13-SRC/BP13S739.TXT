  /*===============================================================*/           
  /*   OBJECTIVES : TO PRINT A REPORT ON SUCCESSFUL  APPLICANTS    */           
  /*                BY ZONE/SCHEME/FLAT_TYPE/PIGEONHOLE TIME/      */           
  /*                   HHOLD TYPE  FOR PURE MT/TCP CASES ONLY.     */           
  /*---------------------------------------------------------------*/           
  /*  REMARKS : COMBINE REPORTS BP13L739 BP13L744 BP13L750         */           
  /*                            BP13L530 BP13L522 BP13L751         */           
  /*---------------------------------------------------------------*/           
  /* CHG-NO   BY   DATE   DESCRIPTION                              */           
  /* -------  ---  ------ -----------                              */           
  /* BP130150 RHB 270996 NEW PROGRAM.                              */           
  /*                                                               */           
  /*===============================================================*/           
                                                                                
  PROC FORMAT;                                                                  
   VALUE $HH_CDE  "3"="3RM-UPG  "                                               
                  "G"="UPGRADER"                                                
                  "H"="FIRST TIMER"                                             
                  "T"="FIRST TIMER TENANT";                                     
                                                                                
   VALUE $RM_TYP  "3"="3-RM"                                                    
                  "4"="4-RM"                                                    
                  "5"="5-RM"                                                    
                  "6"="EXEC";                                                   
                                                                                
   VALUE $SUC_FMT  " "="SUCC"                                                   
                   "U"="UNSUCC";                                                
                                                                                
   %GLOBAL DATE;                                                                
   OPTIONS NODATE;                                                              
                                                                                
   DATA BP13F520;                                                               
      INFILE BP13F520 END=EOF;                                                  
      INPUT @78  REQ_ZN   $CHAR2.                                               
            @187 SEL_NT   $CHAR3.  @211 FLT_TYP $CHAR1.                         
            @80  COHORT   $CHAR2.  @116 CDE_HH  $CHAR1.                         
            @213 ALC_SCH  $CHAR3.  @225 CAT     $CHAR1.                         
            @165 PIGEN_TM $CHAR2.                                               
            @226 SUCC_NT  $CHAR3.                                               
            @229 UNSUCC   $CHAR1.  @114 WAIT_TM $CHAR2.;                        
                                                                                
       IF _N_=1 THEN                                                            
          DO;                                                                   
             CUR_DTE=PUT(TODAY(),DDMMYY8.);                                     
             CALL SYMPUT('DATE',CUR_DTE);                                       
          END;                                                                  
                                                                                
       IF FLT_TYP="E" THEN FLT_TYP="6";                                         
                                                                                
       IF REQ_ZN=" " THEN                                                       
          CODE_NT=SEL_NT;                                                       
       ELSE                                                                     
          CODE_NT=REQ_ZN;                                                       
                                                                                
       IF SUCC_NT=" " THEN                                                      
          UNSUCC="U";                                                           
       ELSE                                                                     
          UNSUCC=" ";                                                           
                                                                                
   RUN;                                                                         
                                                                                
   PROC SORT; BY ALC_SCH;                                                       
   RUN;                                                                         
                                                                                
   DATA BP13PRIO;                                                               
        INFILE PRIOFMT;                                                         
        INPUT @2 ALC_SCH $CHAR3. @9 PRIO_SCH $CHAR3.;                           
   RUN;                                                                         
                                                                                
   PROC SORT; BY ALC_SCH;                                                       
   RUN;                                                                         
                                                                                
   DATA BP13SCH;                                                                
   MERGE BP13F520(IN=M) BP13PRIO(IN=T); BY ALC_SCH;                             
         IF M;                                                                  
   RUN;                                                                         
                                                                                
   PROC SORT ;                                                                  
   BY DESCENDING PIGEN_TM;                                                      
   RUN;                                                                         
                                                                                
 /*PROC SORT ;                                                                  
   BY CODE_NT PRIO_SCH FLT_TYP DESCENDING PIGEN_TM;                             
   RUN; */                                                                      
                                                                                
   /* REPORT 1 ON SUMMARY */                                                    
                                                                                
   OPTIONS MISSING=0;                                                           
   PROC PRINTTO UNIT=18 NEW;                                                    
   PROC TABULATE DATA=BP13SCH ORDER=DATA MISSING FORMAT=6.;                     
                                                                                
TITLE1 "P13L739A   HDB3              S Y S T E M   O F   C O M M I T M E N T    
                           DATE : &DATE  PAGE  :   ";                           
TITLE2 "                                                                        
                                                   ";                           
TITLE3 "PURE JB QUOTA                    REPORT ON SUCC/UNSUCC APPLICATIONS     
                                                   ";                           
TITLE4 "                        BY ZONE/FLAT TYPE/HH-TYPE/SCHEME/PIGEONHOLE TIME
                                                   ";                           
                                                                                
   CLASS  CODE_NT PIGEN_TM CAT FLT_TYP PRIO_SCH CDE_HH UNSUCC;                  
                                                                                
   FORMAT CDE_HH  $HH_CDE.                                                      
          UNSUCC  $SUC_FMT.                                                     
          FLT_TYP $RM_TYP.;                                                     
                                                                                
   KEYLABEL SUM  ="UNIT "                                                       
            ALL  ="TOTAL";                                                      
                                                                                
   LABEL CODE_NT  ="ZONE :"                                                     
         PIGEN_TM  ="PIGEONHOLE TIME"                                           
         PRIO_SCH ="SCHEME : "                                                  
         FLT_TYP  ="FLAT TYPE";                                                 
                                                                                
   TABLE (CODE_NT*PRIO_SCH*FLT_TYP="FLAT TYPE : "),                             
         (PIGEN_TM="PIGEONHOLE TIME " ALL="TOTAL"),                             
         (CDE_HH=" "ALL="TOTAL")*                                               
         (N="APPN " UNSUCC=" "*N=" " UNSUCC=" "*PCTN                            
         <UNSUCC ALL>*F=4.)/RTS=7                                               
         BOX="HHTYPE/RESULT";                                                   
                                                                                
   RUN;                                                                         
                                                                                
   /* REPORT 2 ON SUMMARY */                                                    
                                                                                
   PROC TABULATE DATA=BP13SCH ORDER=DATA MISSING FORMAT=6.;                     
                                                                                
TITLE1 "P13L739B   HDB3              S Y S T E M   O F   C O M M I T M E N T    
                           DATE : &DATE  PAGE  :   ";                           
TITLE2 "                                                                        
                                                   ";                           
TITLE3 " PURE JB QUOTA               SUMMARY REPORT ON SUCC/UNSUCC APPLICATIONS 
                                                   ";                           
TITLE4 "                                      BY PRIORITY & PA (ALL ZONES)      
                                                   ";                           
                                                                                
   CLASS  CODE_NT PIGEN_TM CAT FLT_TYP PRIO_SCH CDE_HH UNSUCC;                  
                                                                                
   FORMAT CDE_HH  $HH_CDE.                                                      
          UNSUCC  $SUC_FMT.                                                     
          FLT_TYP $RM_TYP.;                                                     
                                                                                
   KEYLABEL SUM  ="UNIT "                                                       
            ALL  ="TOTAL";                                                      
                                                                                
   LABEL CODE_NT  ="ZONE :"                                                     
         PIGEN_TM  ="PEGIONHOLE TIME"                                           
         FLT_TYP  ="FLAT TYPE";                                                 
                                                                                
   TABLE (PRIO_SCH="SCHEME : "*FLT_TYP="FLAT TYPE"),                            
         (PIGEN_TM="PEGIONHOLE TIME " ALL="TOTAL"),                             
         (CDE_HH=" "ALL="TOTAL")*                                               
         (N="APPN " UNSUCC=" "*N=" " UNSUCC=" "*PCTN                            
         <UNSUCC ALL>*F=4.)/RTS=7                                               
         BOX="HOUSEHOLD TYPE/RESULT";                                           
                                                                                
   RUN;                                                                         
