       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13CF36.                                                  
      *AUTHOR.       ZDD1.                                                      
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *  OBJECTIVE:                                                *            
      *  TO UPDATE BP13K712                                        *            
      *                                                            *            
      *  I    :  BP13F712                                          *            
      *  I/O  :  BP13K712                                          *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP134301  ZDD1  21072011  NEW PROGRAM                      *            
      * BP135458  SMR2  04082014  EXPAND BP13K712 TO 350           *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F712 ASSIGN TO BP13F712.                                  
                                                                                
           SELECT BP13K712 ASSIGN TO BP13K712                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K712-KEY-FLD                               
                  FILE STATUS     IS WS-K712-STATUS.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F712   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 350 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13F712.                                                           
                                                                                
       FD  BP13K712   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 350 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13K712.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-SWITCHES.                                                         
           05  WS-K712-STATUS            PIC 99    VALUE ZEROS.                 
           05  WS-F712-EOF               PIC X     VALUE 'N'.                   
           05  WS-F712-READ              PIC 9(6)  VALUE ZEROS.                 
           05  WS-K712-READ              PIC 9(6)  VALUE ZEROS.                 
           05  WS-K712-REWRITE           PIC 9(6)  VALUE ZEROS.                 
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-LOGIC.                                                          
            PERFORM 100-OPEN-FILES      THRU 100-EXIT.                          
            PERFORM 200-READ-BP13F712   THRU 200-EXIT.                          
            PERFORM 500-PROCESS-K712    THRU 500-EXIT                           
                UNTIL WS-F712-EOF = 'Y'                                         
            PERFORM 999-CLOSE-FILES     THRU 999-EXIT.                          
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F712                                                 
                I-O    BP13K712.                                                
                                                                                
           IF WS-K712-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, K712-STATUS ' WS-K712-STATUS              
              MOVE WS-K712-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-BP13F712.                                                       
      *-------------------------------------------------------------            
           READ BP13F712 AT END                                                 
           MOVE 'Y'                      TO WS-F712-EOF                         
           GO TO 200-EXIT.                                                      
                                                                                
           ADD 1                         TO WS-F712-READ.                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       500-PROCESS-K712.                                                        
      *-------------------------------------------------------------            
             MOVE F712-KEY-FLD  TO K712-KEY-FLD                                 
             READ BP13K712                                                      
                                                                                
             IF WS-K712-STATUS = 00                                             
                ADD 1 TO  WS-K712-READ                                          
                PERFORM 600-REWRITE-BP13K712 THRU 600-EXIT                      
             ELSE                                                               
                IF WS-K712-STATUS = 23                                          
                   DISPLAY 'RECORD NOT FND IN BP13K712 ' WS-K712-STATUS         
                ELSE                                                            
                   DISPLAY 'ERROR READ BP13K712,STATUS ' WS-K712-STATUS         
                   MOVE WS-K712-STATUS     TO RETURN-CODE                       
                   GO TO 999-CLOSE-FILES.                                       
                                                                                
           PERFORM 200-READ-BP13F712 THRU 200-EXIT.                             
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       600-REWRITE-BP13K712.                                                    
      *-------------------------------------------------------------            
                                                                                
             MOVE   K712-NUM-FLAT-NH     TO K712-NUM-FLAT-UG                    
                                                                                
                                                                                
             REWRITE BP13K712-REC                                               
                                                                                
              IF WS-K712-STATUS = 00                                            
                 ADD 1                 TO WS-K712-REWRITE                       
              ELSE                                                              
                 DISPLAY 'ERROR REWRITING BP13K712 STATUS '                     
                                                 WS-K712-STATUS                 
                 MOVE WS-K712-STATUS      TO RETURN-CODE                        
                 PERFORM 999-CLOSE-FILES  THRU 999-EXIT                         
              END-IF.                                                           
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
       999-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
           DISPLAY '* ------------ BP13CF36 ------------- * '.                  
           DISPLAY 'NO OF REC READ    BP13F712 = ' WS-F712-READ.                
           DISPLAY 'NO OF REC READ    BP13K712 = ' WS-K712-READ.                
           DISPLAY 'NO OF REC REWRITE BP13K712 = ' WS-K712-REWRITE.             
                                                                                
           CLOSE BP13F712                                                       
                 BP13K712.                                                      
                                                                                
           IF WS-K712-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K712, STATUS ' WS-K712-STATUS          
              MOVE WS-K712-STATUS        TO RETURN-CODE.                        
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
