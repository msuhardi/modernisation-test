       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C182.                                                 
       AUTHOR.        NOR.                                                      
      *DATE-WRITTEN.  16/10/1997.                                               
                                                                                
      *****************************************************************         
      * SYSTEM OF COMMITMENT (SOC)                                    *         
      *===============================================================*         
      *                                                               *         
      *  OBJECTIVES : 1. PROGRAM TO MATCH SCH-ACC-NO OF TRANSFEROR    *         
      *                  TO READ TRANSFER HISTORY TO DIVORCE CODE     *         
      *                  TO BE FOUND IN SELLER RECORD.                *         
      *                                                               *         
      *===============================================================*         
      * FILES DESCRIPTION :-                                          *         
      *                ACCESS      REC                                *         
      * DD NAME  I/O/E  MODE       LEN  KEY          REMARKS          *         
      * -------  ----- -------     ---  ---          --------         *         
      * BP13F558 I      SEQUENTIAL                                    *         
      * BB13K500 I      DYNAMIC                                       *         
      * P13F558O O      SEQUENTIAL                                    *         
      *===============================================================*         
      * MODIFICATIONS :                                               *         
      * CHGE REQ  DATE      BY     REMARKS                            *         
      * --------- ----      ---    -------                            *         
      * BP130542  19081998  NOR - NEW PGM                             *         
      * BP130897  01082000  AKT - EXPAND F558 RECL FROM 400 TO 600    *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BB13K500 ASSIGN TO BB13K500                                   
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE IS DYNAMIC                               
                           RECORD KEY IS K500-KEY-FLD                           
                           FILE STATUS IS BB13K500-STATUS.                      
                                                                                
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
                                                                                
           SELECT P13F558O ASSIGN TO P13F558O.                                  
                                                                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BB13K500     RECORD CONTAINS 1000 CHARACTERS.                        
       COPY BB13K500.                                                           
                                                                                
       FD  BP13F558     BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 600 CHARACTERS                          
                        LABEL RECORD IS STANDARD                                
                        RECORDING MODE IS F.                                    
       COPY BP13F558.                                                           
                                                                                
       FD  P13F558O     BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 600 CHARACTERS                          
                        LABEL RECORD IS OMITTED                                 
                        RECORDING MODE IS F.                                    
       01  P13F558O-REC.                                                        
           05  FILLER           PIC X(600).                                     
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FILE-FLAGS.                                                       
           05  BB13K500-STATUS     PIC 9(2)      VALUE ZEROS.                   
           05  WS-F558-EOF         PIC X         VALUE 'N'.                     
           05  WS-K500-EOF         PIC X         VALUE 'N'.                     
           05  WS-BUYER-FD         PIC X         VALUE SPACES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F558-CNT-READ    PIC 9(8)      VALUE ZEROS.                   
           05  WS-K500-CNT-READ    PIC 9(8)      VALUE ZEROS.                   
           05  WS-F558O-CNT-WRT    PIC 9(8)      VALUE ZEROS.                   
           05  WS-DIVORCE          PIC 9(8)      VALUE ZEROS.                   
           05  WS-TRF-LESSEE       PIC X(1)      VALUE SPACES.                  
                                                                                
       01  WS-DTE-CC               PIC X(2).                                    
       01  WS-DTE-CURRENT.                                                      
           05  WS-DTE-DD           PIC X(2).                                    
           05  WS-DTE-MM           PIC X(2).                                    
           05  WS-DTE-YY           PIC X(2).                                    
                                                                                
       01  WS-TME-SYSTEM           PIC X(8).                                    
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 1010-READ-BP13F558-REC   THRU 1010-EXIT.                     
           PERFORM 2000-MAIN-ROUTINE        THRU 2000-EXIT                      
             UNTIL WS-F558-EOF = 'Y'.                                           
                                                                                
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN INPUT  BB13K500                                                 
                       BP13F558                                                 
                OUTPUT P13F558O.                                                
                                                                                
                                                                                
           IF BB13K500-STATUS NOT = ZEROES AND 97                               
              MOVE BB13K500-STATUS TO RETURN-CODE                               
              DISPLAY 'OPEN BB13K500 FAIL STATUS=' BB13K500-STATUS              
              GO TO 9000-CLOSE-FILES.                                           
                                                                                
           ACCEPT WS-DTE-CURRENT FROM DATE.                                     
           ACCEPT WS-TME-SYSTEM  FROM TIME.                                     
                                                                                
           IF WS-DTE-YY < '90'                                                  
              MOVE '20'      TO WS-DTE-CC                                       
           ELSE                                                                 
              MOVE '19'      TO WS-DTE-CC.                                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1010-READ-BP13F558-REC.                                                  
      *****************************************************************         
           READ BP13F558 AT END MOVE 'Y'        TO WS-F558-EOF                  
                                MOVE HIGH-VALUE TO F558-NUM-NRIC                
                GO TO 1010-EXIT.                                                
           ADD 1 TO WS-F558-CNT-READ.                                           
       1010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1020-READ-BB13K500-REC.                                                  
      *****************************************************************         
           READ BB13K500 NEXT AT END MOVE HIGH-VALUES TO K500-KEY-FLD.          
                                                                                
       1020-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-MAIN-ROUTINE.                                                       
      *****************************************************************         
                                                                                
      *----------------------------------------------------*                    
      * COMMENTS :  DO THE MATCHING.                       *                    
      *----------------------------------------------------*                    
                                                                                
                                                                                
           IF F558-NUM-HDB-REF-TR = SPACES OR LOW-VALUES                        
              PERFORM 2020-WRITE-F558-REC           THRU 2020-EXIT              
              PERFORM 1010-READ-BP13F558-REC        THRU 1010-EXIT              
              GO TO 2000-EXIT.                                                  
                                                                                
           MOVE SPACES TO  WS-BUYER-FD.                                         
           MOVE SPACES                  TO K500-KEY-FLD.                        
           MOVE F558-NUM-HDB-REF-TR      TO K500-KEY-FLD                        
           START BB13K500 KEY >= K500-KEY-FLD.                                  
           IF BB13K500-STATUS = ZEROES                                          
              PERFORM 1020-READ-BB13K500-REC  THRU 1020-EXIT                    
              PERFORM 2005-CHECK-STATUS-REC   THRU 2005-EXIT                    
                UNTIL K500-KEY-FLD(1:9) NOT = F558-NUM-HDB-REF-TR(1:9)          
                 OR   K500-KEY-FLD = HIGH-VALUE                                 
                 OR   WS-BUYER-FD = 'Y'                                         
           ELSE                                                                 
           IF BB13K500-STATUS = 23                                              
              DISPLAY 'K500-KEY-FLD : ' K500-KEY-FLD                            
           END-IF.                                                              
                                                                                
           PERFORM 2020-WRITE-F558-REC        THRU 2020-EXIT.                   
                                                                                
           PERFORM 1010-READ-BP13F558-REC     THRU 1010-EXIT.                   
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2005-CHECK-STATUS-REC.                                                   
      *****************************************************************         
                                                                                
           IF K500-CDE-REC = 'B'                                                
                                                                                
              IF F558-NUM-HDB-REF-TR (1:9) = K500-NUM-HDB-REF (1:9)             
                 AND                                                            
                 F558-NUM-HDB-REF-TR (10:1) <  K500-NUM-HDB-REF (10:1)          
                   MOVE 'Y' TO WS-BUYER-FD                                      
                   IF K500-CDE-DIVORCE = 'Y'                                    
                      ADD 1 TO WS-DIVORCE                                       
                      MOVE 'Y' TO F558-NUM-DIVORCE-TR                           
                   END-IF                                                       
                END-IF                                                          
           END-IF.                                                              
                                                                                
           PERFORM 1020-READ-BB13K500-REC        THRU 1020-EXIT.                
                                                                                
       2005-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
       2020-WRITE-F558-REC.                                                     
      *****************************************************************         
                                                                                
           MOVE SPACES                     TO P13F558O-REC.                     
           WRITE P13F558O-REC FROM BP13F558-REC.                                
           ADD 1                      TO WS-F558O-CNT-WRT.                      
       2020-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           DISPLAY 'PROGRAM = BP13C182 MATCH TRANSFER HISTORY'.                 
           DISPLAY 'TOTAL F558 REC READ     = ' WS-F558-CNT-READ.               
           DISPLAY 'TOTAL F558 REC WRITTEN  = ' WS-F558O-CNT-WRT.               
           DISPLAY 'REC FD WITH DIVORCE     = ' WS-DIVORCE.                     
                                                                                
           CLOSE      BB13K500                                                  
                      BP13F558                                                  
                      P13F558O.                                                 
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
