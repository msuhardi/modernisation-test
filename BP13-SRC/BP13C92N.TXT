      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13C92N.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  31 AUG 2015.                                              
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : TO UPDATE NUM-PRINT-TAG IF RECORD IS FOUND  *           
      *                 IN BP13INP                                  *           
      *   INPUT FILES :                                             *           
      *   1.  BP13INP                                               *           
      *                                                             *           
      *   OUTPUT FILES:                                             *           
      *   1.  BP13F200                                              *           
      *   2.  BP13K200                                              *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP135941 31/08/2015 FNP1 NEW PROGRAM                        *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13INP ASSIGN  TO BP13INP.                                   
                                                                                
           SELECT BP13F200 ASSIGN TO BP13F200.                                  
                                                                                
           SELECT BP13K200 ASSIGN TO BP13K200                                   
                  ACCESS MODE   IS DYNAMIC                                      
                  ORGANIZATION  IS INDEXED                                      
                  RECORD KEY    IS K200-KEY-FLD                                 
                  ALTERNATE KEY IS K200-ALT-KEY                                 
                  FILE STATUS   IS BP13K200-STATUS.                             
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13INP                                                             
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  10 CHARACTERS                                      
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       01   BP13INP-REC.                                                        
            05 INP-REGN         PIC X(8).                                       
            05 FILLER           PIC X(2).                                       
                                                                                
       FD   BP13K200                                                            
            RECORD CONTAINS 300  CHARACTERS.                                    
       COPY BP13K200.                                                           
                                                                                
       FD  BP13F200                                                             
           RECORDING  MODE  IS F                                                
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 300 CHARACTERS                                       
           LABEL  RECORDS  ARE  STANDARD.                                       
       COPY BP13F200.                                                           
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  WS-CURR-DATE           PIC X(08) VALUE ZEROES.                       
       01  WS-INP-READ            PIC 9(08) VALUE ZEROES.                       
       01  WS-K200-FOUND          PIC 9(08) VALUE ZEROES.                       
       01  WS-K200-NFND           PIC 9(08) VALUE ZEROES.                       
       01  WS-K200-UPDATED        PIC 9(08) VALUE ZEROES.                       
       01  WS-F200-UPDATED        PIC 9(08) VALUE ZEROES.                       
       01  WS-INP-EOF             PIC X     VALUE SPACES.                       
       01  WS-BP13F200-EOF        PIC X     VALUE SPACES.                       
       01  BP13K200-STATUS        PIC 9(02) VALUE ZEROES.                       
                                                                                
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-INP      THRU 2000-EXIT.                           
           PERFORM 2500-READ-BP13F200 THRU 2500-EXIT.                           
           PERFORM 3000-PROCESS-REC   THRU 3000-EXIT                            
                   UNTIL WS-INP-EOF = 'Y'.                                      
           PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13INP                                                  
                I-O    BP13F200                                                 
                       BP13K200.                                                
                                                                                
           IF BP13K200-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K200 : ' BP13K200-STATUS             
              MOVE BP13K200-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
                                                                                
           ACCEPT WS-CURR-DATE FROM DATE YYYYMMDD.                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-INP.                                                           
      *---------------*                                                         
                                                                                
           READ BP13INP AT END                                                  
                MOVE HIGH-VALUES TO BP13INP-REC                                 
                MOVE 'Y' TO WS-INP-EOF                                          
                MOVE 'Y' TO WS-BP13F200-EOF                                     
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-INP-READ.                                                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2500-READ-BP13F200.                                                      
      *-------------------------------------------------------------            
           READ BP13F200                                                        
             AT END                                                             
                MOVE 'Y' TO WS-BP13F200-EOF                                     
                GO TO 2500-EXIT                                                 
           END-READ.                                                            
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
           IF INP-REGN = F200-NUM-REGN                                          
              PERFORM 4100-REWRITE-BP13F200  THRU 4100-EXIT                     
              PERFORM 3500-READ-BP13K200     THRU 3500-EXIT                     
              PERFORM 2000-READ-INP          THRU 2000-EXIT                     
           ELSE                                                                 
              PERFORM 2500-READ-BP13F200     THRU 2500-EXIT                     
           END-IF.                                                              
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3500-READ-BP13K200.                                                      
      *-----------------*                                                       
                                                                                
           MOVE INP-REGN             TO K200-NUM-REGN.                          
           READ BP13K200         KEY IS K200-ALT-KEY.                           
                                                                                
           EVALUATE BP13K200-STATUS                                             
             WHEN 00                                                            
             WHEN 02                                                            
                ADD 1 TO WS-K200-FOUND                                          
                MOVE 'T' TO K200-NUM-PRINT-TAG                                  
                PERFORM 4000-REWRITE-BP13K200  THRU 4000-EXIT                   
             WHEN 23                                                            
                DISPLAY 'REC NOT FOUND IN BP13K200'                             
                      ', K200-NUM-REGN: ' K200-NUM-REGN                         
                ADD 1 TO WS-K200-NFND                                           
             WHEN OTHER                                                         
                DISPLAY 'BP13K200 READ ERROR,STATUS : ' BP13K200-STATUS         
                MOVE BP13K200-STATUS       TO RETURN-CODE                       
                PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       4000-REWRITE-BP13K200.                                                   
      *---------------*                                                         
           REWRITE BP13K200-REC.                                                
                                                                                
           EVALUATE BP13K200-STATUS                                             
             WHEN 00                                                            
                ADD 1 TO WS-K200-UPDATED                                        
             WHEN OTHER                                                         
                DISPLAY 'BP13K200 REWRITE ERROR : ' BP13K200-STATUS             
                MOVE BP13K200-STATUS       TO RETURN-CODE                       
                PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4100-REWRITE-BP13F200.                                                   
      *-------------------------------------------------------------            
                                                                                
           MOVE 'T' TO F200-NUM-PRINT-TAG                                       
                                                                                
           REWRITE BP13F200-REC.                                                
                                                                                
           ADD 1 TO WS-F200-UPDATED.                                            
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13C92N  *************'.                           
           DISPLAY 'RECS READ FROM  BP13INP    : ' WS-INP-READ.                 
           DISPLAY 'RECS FOUND   IN BP13K200    : ' WS-K200-FOUND.              
           DISPLAY 'RECS NOT FND IN BP13K200    : ' WS-K200-NFND.               
           DISPLAY 'RECS UPDATED IN BP13K200    : ' WS-K200-UPDATED.            
           DISPLAY 'RECS UPDATED IN BP13F200    : ' WS-F200-UPDATED.            
                                                                                
           CLOSE BP13INP                                                        
                 BP13F200                                                       
                 BP13K200.                                                      
                                                                                
           IF BP13K200-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K200 : ' BP13K200-STATUS             
              MOVE BP13K200-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
