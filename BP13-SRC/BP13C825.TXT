       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C825                                                  
       AUTHOR.        RACHEL G. MENDOZA.                                        
      *DATE-WRITTEN.  13 JAN 1995.                                              
      ************************************************************              
      *                                                          *              
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                    *              
      *                                                          *              
      *    SYSTEM ID : TO PRINT REPORT TO LIST SUCCESSFUL        *              
      *                APPLICATIONS WHICH WERE UPDATED WITHIN    *              
      *                3 MONTHS PRIOR TO PIGEONHOLING.           *              
      *                                                          *              
      *    OBJECTIVE :                                           *              
      *                                                          *              
      *     INPUT FILES:                                         *              
      *      1. BP13K800                                         *              
      *      2. BP13F810                                         *              
      *                                                          *              
      *     OUTPUT FILES :                                       *              
      *      1. BP13L825                                         *              
      *                                                          *              
      * AMENDMENTS:                                              *              
      * REF      DATE      BY    DESCRIPTIONS                    *              
      * -------  -------   ----  ------------                    *              
      * C506G3   5/7/95    MS1   CHECKING FROM BP13F520          *              
      *BP130418  27/07/98  SIA   CATER FOR YR2000                *              
      *BP130652  20/02/99  NOR   EXPAND F520 RECL FROM 300 TO 500*              
      *BP137918  09/07/19  KV6   TO CATER FOR REMOVAL OF DTE-DEF-*              
      *                          ER-EXPIRY AND NUM-DEFER-ZONE-FT *              
      ************************************************************              
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F810 ASSIGN TO BP13F810.                                  
                                                                                
           SELECT BP13F520 ASSIGN TO BP13F520.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS BP13K800-STATUS.                           
                                                                                
           SELECT BP13L825 ASSIGN TO BP13L825.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F810                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13F810.                                                           
                                                                                
       FD   BP13F520                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F520.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13L825                                                            
            BLOCK     CONTAINS 0   RECORDS                                      
            RECORD    CONTAINS 132 CHARACTERS                                   
            LABEL     RECORD   IS  OMITTED                                      
            RECORDING MODE     IS  F.                                           
       01  BP13L825-REC                  PIC X(132).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-ACCESS-STATUS.                                               
           05  BP13F810-STATUS           PIC 9(2)  VALUE ZEROES.                
           05  BP13F520-STATUS           PIC 9(2)  VALUE ZEROES.                
           05  BP13K800-STATUS           PIC 9(2)  VALUE ZEROES.                
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F810-EOF               PIC X     VALUE 'N'.                   
               88  F810-EOF                        VALUE 'Y'.                   
           05  WS-F520-EOF               PIC X     VALUE 'N'.                   
               88  F520-EOF                        VALUE 'Y'.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-LINE                   PIC 9(2)  VALUE 70.                    
           05  WS-PAGE                   PIC 9(4)  VALUE ZEROES.                
           05  WS-F810-READ              PIC 9(6)  VALUE ZEROES.                
           05  WS-F520-READ              PIC 9(6)  VALUE ZEROES.                
           05  WS-K800-READ              PIC 9(6)  VALUE ZEROES.                
           05  WS-MATCH-CNT              PIC 9(6)  VALUE ZEROES.                
           05  WS-NUM-PRINT              PIC 9(6)  VALUE ZEROES.                
           05  WS-PRINT-TAG              PIC X     VALUE SPACES.                
           05  WS-COMP-MM                PIC 9(2)  VALUE ZEROS.                 
           05  WS-NUM-MONTHS             PIC 9(5)  VALUE ZEROS.                 
           05  WS-DATE-UPDATE-YYMM       PIC 9(5)  VALUE ZEROS.                 
           05  WS-SYS-YYMM               PIC 9(5)  VALUE ZEROS.                 
                                                                                
       01  WS-FORMAT-DATE.                                                      
           05  WS-PRT-CC                 PIC X(2)  VALUE SPACES.                
           05  WS-PRT-YY                 PIC X(2)  VALUE SPACES.                
           05  WS-PRT-MM                 PIC X(2)  VALUE SPACES.                
           05  WS-PRT-DD                 PIC X(2)  VALUE SPACES.                
                                                                                
       01  WS-PRINT-DATE.                                                       
           05  WS-PRT-DD                 PIC X(2)  VALUE SPACES.                
           05  FILLER                    PIC X     VALUE '/'.                   
           05  WS-PRT-MM                 PIC X(2)  VALUE SPACES.                
           05  FILLER                    PIC X     VALUE '/'.                   
           05  WS-PRT-CC                 PIC X(2)  VALUE SPACES.                
           05  WS-PRT-YY                 PIC X(2)  VALUE SPACES.                
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-YY                     PIC X(4)  VALUE SPACES.                
           05  WS-MM                     PIC X(2)  VALUE SPACES.                
           05  WS-DD                     PIC X(2)  VALUE SPACES.                
                                                                                
       01  WS-EDIT-DATE.                                                        
           05  WS-DD                     PIC X(2)  VALUE SPACES.                
           05  FILLER                    PIC X     VALUE '/'.                   
           05  WS-MM                     PIC X(2)  VALUE SPACES.                
           05  FILLER                    PIC X     VALUE '/'.                   
           05  WS-YY                     PIC X(4)  VALUE SPACES.                
                                                                                
       01  WS-DATE-UPDATE.                                                      
           05  WS-DATE-UPDATE-YEAR       PIC X(4)  VALUE SPACES.                
           05  WS-DATE-UPDATE-YY-RED REDEFINES WS-DATE-UPDATE-YEAR.             
               10 WS-DATE-UPDATE-YY      PIC 9(4).                              
           05  WS-DATE-UPDATE-MTH        PIC X(2)  VALUE SPACES.                
           05  WS-DATE-UPDATE-MM-RED REDEFINES WS-DATE-UPDATE-MTH.              
               10 WS-DATE-UPDATE-MM      PIC 9(2).                              
           05  WS-DATE-UPDATE-DD         PIC X(2)  VALUE SPACES.                
                                                                                
       01  WS-SYS-DATE.                                                         
           05 WS-SYS-YY                  PIC 9(4)  VALUE ZEROS.                 
           05 WS-SYS-MM                  PIC 9(2)  VALUE ZEROS.                 
           05 WS-SYS-DD                  PIC 9(2)  VALUE ZEROS.                 
                                                                                
      **************** REPORT FORMAT *******************                        
                                                                                
       01  L825-HEAD1.                                                          
           03  FILLER              PIC X(14)   VALUE 'BP13L825'.                
           03  FILLER              PIC X(8)    VALUE 'HDB3'.                    
           03  FILLER              PIC X(18)   VALUE SPACES.                    
           03  FILLER              PIC X(39)   VALUE                            
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           03  FILLER              PIC X(31)   VALUE SPACES.                    
           03  FILLER              PIC X(8)    VALUE 'DATE : '.                 
           03  HEAD1-DATE          PIC X(10)   VALUE SPACES.                    
                                                                                
       01  L825-HEAD2.                                                          
           03  FILLER              PIC X(15) VALUE SPACES.                      
           03  FILLER              PIC X(89) VALUE                              
               'LIST OF SUCCESSFUL APPLICATIONS WHICH WERE UPDATED WITHI        
      -        'N 3 MONTHS PRIOR TO PIGEON-HOLING'.                             
           03  FILLER              PIC X(8)    VALUE SPACES.                    
           03  FILLER              PIC X(8)    VALUE 'PAGE : '.                 
           03  HEAD1-PAGE          PIC ZZZZ9.                                   
                                                                                
       01  L825-HEAD3.                                                          
           03  FILLER              PIC X(46) VALUE SPACES.                      
           03  FILLER              PIC X(52) VALUE                              
               ' (FOR INTERNAL AUDIT USED) '.                                   
           03  FILLER              PIC X(14) VALUE SPACES.                      
           03  FILLER              PIC X(20) VALUE 'USER :KUEK AH TEE'.         
                                                                                
       01  L825-HEAD4.                                                          
           03  FILLER              PIC X(52) VALUE SPACES.                      
           03  FILLER              PIC X(50) VALUE SPACES.                      
           03  FILLER              PIC X(13) VALUE 'BEFORE UPDATE'.             
                                                                                
       01  L825-HEAD5.                                                          
           03  FILLER              PIC X(52) VALUE SPACES.                      
           03  FILLER              PIC X(50) VALUE SPACES.                      
           03  FILLER              PIC X(13) VALUE 'AFTER  UPDATE'.             
                                                                                
       01  L825-HEAD6.                                                          
           05  FILLER              PIC X(04)   VALUE SPACES.                    
           05  FILLER              PIC X(03)   VALUE 'S/N'.                     
           05  FILLER              PIC X(04)   VALUE SPACES.                    
           05  FILLER              PIC X(07)   VALUE 'REGN-NO'.                 
           05  FILLER              PIC X(05)   VALUE SPACES.                    
           05  FILLER              PIC X(11)   VALUE 'CUR-REGN-NO'.             
           05  FILLER              PIC X(05)   VALUE SPACES.                    
           05  FILLER              PIC X(03)   VALUE 'UIN'.                     
           05  FILLER              PIC X(07)   VALUE SPACES.                    
           05  FILLER              PIC X(07)   VALUE 'ZONE/NT'.                 
           05  FILLER              PIC X(03)   VALUE SPACES.                    
           05  FILLER              PIC X(02)   VALUE 'FT'.                      
           05  FILLER              PIC X(03)   VALUE SPACES.                    
           05  FILLER              PIC X(04)   VALUE 'ASCH'.                    
           05  FILLER              PIC X(03)   VALUE SPACES.                    
           05  FILLER              PIC X(07)   VALUE 'JB REGN'.                 
           05  FILLER              PIC X(05)   VALUE SPACES.                    
           05  FILLER              PIC X(07)   VALUE 'UP-DATE'.                 
           05  FILLER              PIC X(05)   VALUE SPACES.                    
           05  FILLER              PIC X(04)   VALUE 'WAIT'.                    
           05  FILLER              PIC X(04)   VALUE SPACES.                    
           05  FILLER              PIC X(05)   VALUE 'DEFER'.                   
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(03)   VALUE 'CAT'.                     
           05  FILLER              PIC X(03)   VALUE SPACES.                    
           05  FILLER              PIC X(02)   VALUE 'HH'.                      
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(04)   VALUE '5-YR'.                    
           05  FILLER              PIC X(02)   VALUE SPACES.                    
           05  FILLER              PIC X(03)   VALUE 'DTE'.                     
                                                                                
       01  L825-HEAD7.                                                          
           05  FILLER              PIC X(74)   VALUE SPACES.                    
           05  FILLER              PIC X(02)   VALUE 'NO'.                      
           05  FILLER              PIC X(07)   VALUE SPACES.                    
           05  FILLER              PIC X(07)   VALUE 'USER-ID'.                 
           05  FILLER              PIC X(05)   VALUE SPACES.                    
           05  FILLER              PIC X(04)   VALUE 'TIME'.                    
           05  FILLER              PIC X(04)   VALUE SPACES.                    
           05  FILLER              PIC X(03)   VALUE 'MTH'.                     
           05  FILLER              PIC X(20)   VALUE SPACES.                    
           05  FILLER              PIC X(06)   VALUE 'BALLOT'.                  
                                                                                
       01  L825-DETAIL1.                                                        
           05  FILLER              PIC X(04)   VALUE SPACES.                    
           03  L825-SERIAL         PIC ZZ9.                                     
           03  FILLER              PIC X(04) VALUE SPACES.                      
           03  L825-REGN           PIC X(08).                                   
           03  FILLER              PIC X(04) VALUE SPACES.                      
           03  L825-CUR-REGN       PIC X(08).                                   
           03  FILLER              PIC X(05) VALUE SPACES.                      
           03  L825-NRIC           PIC X(09).                                   
           03  FILLER              PIC X(04) VALUE SPACES.                      
           03  L825-ZONE           PIC X(02).                                   
           03  FILLER              PIC X(02) VALUE ' /'.                        
           03  L825-NT             PIC X(03).                                   
           03  FILLER              PIC X(03) VALUE SPACES.                      
           03  L825-FT             PIC X(02).                                   
           03  FILLER              PIC X(03) VALUE SPACES.                      
           03  L825-ASCH           PIC X(03).                                   
           03  FILLER              PIC X(04) VALUE SPACES.                      
           03  L825-JB-REGN        PIC X(08).                                   
           03  FILLER              PIC X(04) VALUE SPACES.                      
           03  L825-UPD-DATE       PIC X(10).                                   
           03  FILLER              PIC X(02) VALUE SPACES.                      
           03  L825-WAIT-TIME      PIC ZZ9.                                     
           03  FILLER              PIC X(04) VALUE SPACES.                      
           03  L825-DEFER-MTH      PIC ZZ9.                                     
           03  FILLER              PIC X(06) VALUE SPACES.                      
           03  L825-CAT            PIC X(01).                                   
           03  FILLER              PIC X(04) VALUE SPACES.                      
           03  L825-HH             PIC X(02).                                   
           03  FILLER              PIC X(04) VALUE SPACES.                      
           03  L825-5-YR           PIC X(01).                                   
           03  FILLER              PIC X(03) VALUE SPACES.                      
           03  L825-DTE-BALLOT     PIC X(06).                                   
                                                                                
       01  L825-DETAIL2.                                                        
           03  FILLER              PIC X(23) VALUE SPACES.                      
           03  L825-CUR-REGN2      PIC X(08).                                   
           03  FILLER              PIC X(18) VALUE SPACES.                      
           03  L825-ZONE2          PIC X(02).                                   
           03  FILLER              PIC X(02) VALUE ' /'.                        
           03  L825-NT2            PIC X(03).                                   
           03  FILLER              PIC X(03) VALUE SPACES.                      
           03  L825-FT2            PIC X(02).                                   
           03  FILLER              PIC X(03) VALUE SPACES.                      
           03  L825-ASCH2          PIC X(03).                                   
           03  FILLER              PIC X(04) VALUE SPACES.                      
           03  L825-JB-REGN2       PIC X(08).                                   
           03  FILLER              PIC X(04) VALUE SPACES.                      
           03  L825-USERID         PIC X(07).                                   
           03  FILLER              PIC X(05) VALUE SPACES.                      
           03  L825-WAIT-TIME2     PIC ZZ9.                                     
           03  FILLER              PIC X(04) VALUE SPACES.                      
           03  L825-DEFER-MTH2     PIC ZZ9.                                     
           03  FILLER              PIC X(06) VALUE SPACES.                      
           03  L825-CAT2           PIC X(01).                                   
           03  FILLER              PIC X(04) VALUE SPACES.                      
           03  L825-HH2            PIC X(02).                                   
           03  FILLER              PIC X(04) VALUE SPACES.                      
           03  L825-5-YR2          PIC X(01).                                   
           03  FILLER              PIC X(03) VALUE SPACES.                      
           03  L825-DTE-BALLOT2    PIC X(06).                                   
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       000-MAIN-ROUTINE.                                                        
                                                                                
           PERFORM 100-OPEN-FILES                                               
              THRU 100-EXIT.                                                    
           PERFORM 200-READ-BP13F810                                            
              THRU 200-EXIT.                                                    
           PERFORM 210-READ-BP13F520                                            
              THRU 210-EXIT.                                                    
           PERFORM 300-PROCESS-AUDIT-RPT                                        
              THRU 300-EXIT                                                     
             UNTIL F810-EOF AND  F520-EOF.                                      
           PERFORM 900-CLOSE-ROUTINE                                            
              THRU 900-EXIT.                                                    
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-FILES.                                                          
                                                                                
           OPEN  INPUT BP13F810                                                 
                       BP13F520                                                 
                       BP13K800                                                 
                OUTPUT BP13L825.                                                
                                                                                
           IF BP13F810-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPEN ERROR FOR BP13F810, STATUS '                        
                                      BP13F810-STATUS                           
              MOVE BP13F810-STATUS TO RETURN-CODE                               
              PERFORM 900-CLOSE-ROUTINE                                         
                 THRU 900-EXIT.                                                 
                                                                                
           IF BP13F520-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPEN ERROR FOR BP13F520, STATUS '                        
                                      BP13F520-STATUS                           
              MOVE BP13F520-STATUS TO RETURN-CODE                               
              PERFORM 900-CLOSE-ROUTINE                                         
                 THRU 900-EXIT.                                                 
                                                                                
           IF BP13K800-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPEN ERROR FOR BP13K800, STATUS '                        
                                             BP13K800-STATUS                    
              MOVE BP13K800-STATUS TO RETURN-CODE                               
              PERFORM 900-CLOSE-ROUTINE                                         
                 THRU 900-EXIT.                                                 
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYS-DATE.                           
           COMPUTE WS-SYS-YYMM  = (WS-SYS-YY * 12) + WS-SYS-MM.                 
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-BP13F810.                                                       
                                                                                
           READ BP13F810 NEXT  AT END                                           
                MOVE 'Y'            TO   WS-F810-EOF                            
                MOVE HIGH-VALUES    TO   F810-NUM-REGN                          
                GO   TO  200-EXIT.                                              
                                                                                
           ADD   1   TO  WS-F810-READ.                                          
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       210-READ-BP13F520.                                                       
                                                                                
           READ BP13F520 NEXT  AT END                                           
                MOVE 'Y' TO WS-F520-EOF                                         
                MOVE HIGH-VALUES    TO   F520-NUM-REGN                          
                GO   TO  210-EXIT.                                              
                                                                                
           ADD   1   TO  WS-F520-READ.                                          
                                                                                
       210-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS-AUDIT-RPT.                                                   
                                                                                
           IF F520-NUM-REGN = F810-NUM-REGN                                     
              ADD 1 TO WS-MATCH-CNT                                             
              PERFORM 320-PROCESS-REPORT THRU 320-EXIT UNTIL                    
                      F810-NUM-REGN > F520-NUM-REGN                             
                      OR F810-EOF                                               
              PERFORM 210-READ-BP13F520   THRU  210-EXIT                        
                                                                                
           ELSE                                                                 
           IF F520-NUM-REGN < F810-NUM-REGN                                     
              PERFORM 210-READ-BP13F520   THRU  210-EXIT                        
           ELSE                                                                 
              PERFORM 200-READ-BP13F810   THRU  200-EXIT.                       
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       320-PROCESS-REPORT.                                                      
                                                                                
           PERFORM 350-DTE-UPDATE-CHECK   THRU  350-EXIT.                       
           IF WS-PRINT-TAG = 'Y'                                                
              PERFORM 420-READ-BP13K800   THRU  420-EXIT                        
              IF BP13K800-STATUS = 00                                           
                    PERFORM 430-MOVE-BP13K800-DATA THRU 430-EXIT                
                    PERFORM 400-PRINT-REPORT       THRU 400-EXIT                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-BP13F810   THRU  200-EXIT.                          
                                                                                
       320-EXIT.                                                                
           EXIT.                                                                
                                                                                
       350-DTE-UPDATE-CHECK.                                                    
                                                                                
           MOVE SPACES          TO WS-PRINT-TAG.                                
           INITIALIZE              WS-DATE-UPDATE.                              
           IF F810-DTE-UPDATE IS NOT NUMERIC                                    
              MOVE ZEROS TO F810-DTE-UPDATE.                                    
           MOVE F810-DTE-UPDATE TO WS-DATE-UPDATE.                              
           COMPUTE WS-DATE-UPDATE-YYMM = (WS-DATE-UPDATE-YY * 12) +             
                                          WS-DATE-UPDATE-MM.                    
           COMPUTE WS-NUM-MONTHS = WS-SYS-YYMM -                                
                                   WS-DATE-UPDATE-YYMM.                         
                                                                                
           IF WS-NUM-MONTHS < 4                                                 
              MOVE 'Y' TO WS-PRINT-TAG                                          
           END-IF.                                                              
                                                                                
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       400-PRINT-REPORT.                                                        
                                                                                
           IF WS-LINE > 55                                                      
              PERFORM 500-PRINT-HEAD                                            
                 THRU 500-EXIT.                                                 
                                                                                
           ADD 1                          TO   WS-NUM-PRINT.                    
           MOVE WS-NUM-PRINT              TO   L825-SERIAL.                     
                                                                                
           MOVE F810-NUM-REGN             TO   L825-REGN.                       
           MOVE F810-NUM-CUR-REGN         TO   L825-CUR-REGN.                   
           MOVE F810-NUM-NRIC1            TO   L825-NRIC.                       
           MOVE F810-NUM-ZONE             TO   L825-ZONE.                       
           MOVE F810-NUM-NEW-TOWN         TO   L825-NT.                         
           MOVE F810-NUM-FLAT-TYPE        TO   L825-FT.                         
           MOVE F810-NUM-ALLO-SCHEME      TO   L825-ASCH.                       
           MOVE F810-NUM-JT-BALLOT-REGN-1 TO   L825-JB-REGN.                    
           MOVE F810-DTE-UPDATE           TO   WS-FORMAT-DATE.                  
           MOVE CORR WS-FORMAT-DATE       TO   WS-PRINT-DATE.                   
           MOVE WS-PRINT-DATE             TO   L825-UPD-DATE.                   
           MOVE F810-NUM-WAIT-TIME        TO   L825-WAIT-TIME.                  
      *    MOVE F810-NUM-DEFER-ZONE-FT    TO   L825-DEFER-MTH.                  
           MOVE F810-NUM-CAT              TO   L825-CAT.                        
           MOVE F810-NUM-HOUSEHOLD        TO   L825-HH.                         
           MOVE F810-NUM-5YR-OCCUPN       TO   L825-5-YR.                       
           MOVE F810-DTE-BALLOT           TO   L825-DTE-BALLOT.                 
           WRITE BP13L825-REC             FROM L825-DETAIL1 AFTER 2.            
           WRITE BP13L825-REC             FROM L825-DETAIL2 AFTER 1.            
           ADD 3                          TO   WS-LINE.                         
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       420-READ-BP13K800.                                                       
                                                                                
           MOVE F810-NUM-REGN TO K800-NUM-REGN                                  
           READ BP13K800                                                        
           IF BP13K800-STATUS = 00                                              
               ADD     1 TO WS-K800-READ                                        
           ELSE                                                                 
              IF BP13K800-STATUS = 23                                           
                 MOVE SPACES TO  BP13L825-REC                                   
                 DISPLAY 'RECORD NOT FOUND IN BP13K800, STATUS '                
                          BP13K800-STATUS ' ' K800-NUM-REGN                     
              ELSE                                                              
                 DISPLAY 'ERROR READING  BP13K800, STATUS '                     
                                         BP13K800-STATUS                        
                 MOVE    BP13K800-STATUS TO RETURN-CODE                         
                 PERFORM 900-CLOSE-ROUTINE                                      
                    THRU 900-EXIT                                               
              END-IF                                                            
           END-IF.                                                              
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
       430-MOVE-BP13K800-DATA.                                                  
                                                                                
           MOVE K800-NUM-CUR-REGN         TO   L825-CUR-REGN2.                  
           MOVE K800-NUM-ZONE             TO   L825-ZONE2.                      
           MOVE K800-NUM-NEW-TOWN         TO   L825-NT2.                        
           MOVE K800-NUM-FLAT-TYPE        TO   L825-FT2.                        
           MOVE K800-NUM-ALLO-SCHEME      TO   L825-ASCH2.                      
           MOVE K800-NUM-JT-BALLOT-REGN   TO   L825-JB-REGN2.                   
           MOVE F810-NUM-USERID           TO   L825-USERID.                     
           MOVE K800-NUM-WAIT-TIME        TO   L825-WAIT-TIME2.                 
      *    MOVE K800-NUM-DEFER-ZONE-FT    TO   L825-DEFER-MTH2.                 
           MOVE K800-NUM-CAT              TO   L825-CAT2.                       
           MOVE K800-NUM-HOUSEHOLD        TO   L825-HH2.                        
           MOVE K800-NUM-5YR-OCCUPN       TO   L825-5-YR2.                      
           MOVE K800-DTE-BALLOT           TO   L825-DTE-BALLOT2.                
                                                                                
       430-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-PRINT-HEAD.                                                          
                                                                                
           ADD  1                        TO   WS-PAGE.                          
           MOVE WS-PAGE                  TO   HEAD1-PAGE.                       
           MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE.                       
           MOVE CORR WS-CURRENT-DATE     TO   WS-EDIT-DATE.                     
                                                                                
           MOVE WS-EDIT-DATE             TO   HEAD1-DATE.                       
                                                                                
           WRITE BP13L825-REC     FROM L825-HEAD1 AFTER PAGE.                   
           WRITE BP13L825-REC     FROM L825-HEAD2 AFTER 1.                      
           WRITE BP13L825-REC     FROM L825-HEAD3 AFTER 1.                      
           WRITE BP13L825-REC     FROM L825-HEAD4 AFTER 2.                      
           WRITE BP13L825-REC     FROM L825-HEAD5 AFTER 1.                      
           WRITE BP13L825-REC     FROM L825-HEAD6 AFTER 2.                      
           WRITE BP13L825-REC     FROM L825-HEAD7 AFTER 1.                      
           MOVE  SPACES           TO   BP13L825-REC.                            
           WRITE BP13L825-REC.                                                  
           MOVE 10                TO   WS-LINE.                                 
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
                                                                                
           DISPLAY '************** BP13C825 ***************'.                   
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS READ FROM BP13F810 : ' WS-F810-READ.          
           DISPLAY 'NO OF RECORDS READ FROM BP13F520 : ' WS-F520-READ.          
           DISPLAY 'NO OF RECORDS READ FROM BP13K800 : ' WS-K800-READ.          
           DISPLAY 'NO OF RECORDS MATCH              : ' WS-MATCH-CNT.          
           DISPLAY 'NO OF RECORDS PRINTED,  BP13L825 : ' WS-NUM-PRINT.          
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13K800                                                       
                 BP13F520                                                       
                 BP13F810                                                       
                 BP13L825.                                                      
                                                                                
           IF BP13F810-STATUS NOT = 00                                          
              MOVE BP13F810-STATUS  TO  RETURN-CODE                             
              DISPLAY '***  CLOSING ERROR, BP13F810-STATUS '                    
              BP13F810-STATUS.                                                  
                                                                                
           IF BP13F520-STATUS NOT = 00                                          
              MOVE BP13F520-STATUS  TO  RETURN-CODE                             
              DISPLAY '***  CLOSING ERROR, BP13F520-STATUS '                    
              BP13F520-STATUS.                                                  
                                                                                
           IF BP13K800-STATUS NOT = 00                                          
              MOVE BP13K800-STATUS  TO  RETURN-CODE                             
              DISPLAY '***  CLOSING ERROR, BP13K800-STATUS '                    
              BP13K800-STATUS.                                                  
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
