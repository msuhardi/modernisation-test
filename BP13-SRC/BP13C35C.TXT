       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C35C.                                                 
      *AUTHOR.        ALWYN BENNY.                                              
      *DATE-WRITTEN.  24/08/2012.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *  OBJECTIVE :                                               *            
      *                                                            *            
      *    TO SEND EMAIL TO CSM WHEN COURT ORDER WITH NO CPF REFUND*            
      *    IS UPDATED IN SOC MASTER FILE.                          *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE(YMD)  BY    DESCRIPTION                      *            
      * -------- ---------- ----- -------------------------------- *            
      * BP134561 2012-08-24 AB9   NEW PROGRAM                      *            
      * BP135147 2013-11-26 PCL4  TO CHANGE THE EMAIL RECIPIENT    *            
      *                           FROM SC34 TO TH1.                *            
      * BP135537 2015-03-04 RJB1  CHANGE THE EMAIL RECIPIENT FROM  *            
      *                           NICKNAME INTO COMPLETE NAME      *            
      * BP136082 2016-01-13 PCL4  TO UPDATE EMAIL RECIPIENTS       *            
      * BP136477 2016-10-18 RJB1  TO REMOVE LIM KIM HUAT           *            
      * BP138202 2020-03-10 KV5   REPLACE MAIL FROM MOHAMED ALI BEE*            
      *                           KATHIJAH TO HENG SC SUZANNE(HDB) *            
      * BP139030 2022-01-07 ZAR7  REPLACE CHUA YEE MIEN TO LIM KOK CHUN         
      * BP139276 2024-04-05 KAC1  REPLACE LIM KOK CHUN BY          *            
      *                                   KATHIJAH_BEE_ALI_MOHAMED *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT P13F35B  ASSIGN        TO P13F35B.                            
           SELECT BP13MAIL ASSIGN        TO BP13MAIL.                           
           SELECT P13MAILA ASSIGN        TO P13MAILA.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
                                                                                
       FD  P13F35B                                                              
           RECORD CONTAINS 80 CHARACTERS                                        
           RECORDING  MODE IS F.                                                
       01  P13F35B-REC.                                                         
           05 FILLER                   PIC X(01).                               
           05 F35B-REGN                PIC X(08).                               
           05 FILLER                   PIC X(04).                               
           05 F35B-SCH-ACC             PIC X(11).                               
           05 FILLER                   PIC X(04).                               
           05 F35B-LOAN-TAG            PIC X(01).                               
           05 FILLER                   PIC X(03).                               
           05 F35B-OIC-NCK-NAME        PIC X(05).                               
           05 FILLER                   PIC X(02).                               
           05 F35B-CORDER-STATUS       PIC X(10).                               
           05 FILLER                   PIC X(02).                               
           05 F35B-DTE-APPROVAL        PIC X(10).                               
           05 FILLER                   PIC X(19).                               
                                                                                
       FD  BP13MAIL                                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           RECORDING  MODE IS F.                                                
       01  BP13MAIL-REC    PIC X(80).                                           
                                                                                
       FD  P13MAILA                                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           RECORDING  MODE IS F.                                                
       01  P13MAILA-REC    PIC X(80).                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-VARIABLES.                                                        
           05  CNT-F35B-READ        PIC 9(05)  VALUE ZEROES.                    
           05  WS-I                 PIC 9(02)  VALUE ZEROES.                    
           05  WS-CNT               PIC 9(02)  VALUE ZEROES.                    
           05  WS-F35B-EOF          PIC X(01)  VALUE SPACES.                    
               88  F35B-EOF                    VALUE 'Y'.                       
           05  WS-SYS-DATE          PIC X(8)   VALUE SPACES.                    
           05  WS-UPD-HDR-WRITE     PIC X(1)   VALUE SPACES.                    
           05  WS-SNO               PIC 9(4)   VALUE ZEROS.                     
           05  WS-PREV-CSM          PIC X(05)  VALUE SPACES.                    
                                                                                
      *-------------------------------------------------------------            
      * LAYOUT BP13MAIL  -  DATASET FOR EMAIL                                   
      *-------------------------------------------------------------            
       01  MAIL-HDR-HELO.                                                       
           05  FILLER PIC X(80)  VALUE 'HELO SGPHDB1'.                          
                                                                                
       01  MAIL-HDR-MAIL-FROM.                                                  
           05  FILLER PIC X(80)  VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.              
      * ACJOB HEADER                                                            
      *    05  FILLER PIC X(80)  VALUE 'MAIL FROM:<ACJOB@SGPHDB1>'.             
                                                                                
       01  MAIL-DTL1.                                                           
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  MAIL-RCPT       PIC X(71)  VALUE spaces.                         
                                                                                
       01  MAIL-DTL1B.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  RCPT-ID         PIC X(24)  VALUE                                 
                                          'KATHIJAH_BEE_ALI_MOHAMED'.           
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
                                                                                
       01  MAIL-DTL1C.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
      *    05  RCPT-ID PIC X(24)  VALUE 'KATHIJAH_BEE_ALI_MOHAMED'.             
           05  RCPT-ID PIC X(15)  VALUE 'SUZANNE_SC_HENG'.                      
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
                                                                                
       01  MAIL-HDR-DATA.                                                       
           05  FILLER PIC X(80) VALUE 'DATA'.                                   
                                                                                
       01  MAIL-HDR-FROM.                                                       
           05  FILLER PIC X(80) VALUE 'FROM: SOC System - Email Alert'.         
                                                                                
       01  MAIL-TO.                                                             
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  MAIL-RCPT1      PIC X(76)  VALUE spaces.                         
                                                                                
       01  MAIL-TO2.                                                            
           05  FILLER          PIC X(04)  VALUE 'TO:<'.                         
           05  TO-MAIL-ID      PIC X(24)  VALUE                                 
                                          'KATHIJAH_BEE_ALI_MOHAMED'.           
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
           05  FILLER          PIC X(40)  VALUE SPACES.                         
                                                                                
       01  MAIL-TO3.                                                            
           05  FILLER          PIC X(04)  VALUE 'TO:<'.                         
      *    05  TO-MAIL-ID PIC X(24)  VALUE 'KATHIJAH_BEE_ALI_MOHAMED'.          
           05  TO-MAIL-ID PIC X(15)  VALUE 'SUZANNE_SC_HENG'.                   
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
       01  MAIL-UPD-SUBJECT.                                                    
           05  FILLER PIC X(80)  VALUE 'SUBJECT : Court order with no cp        
      -        'f refund cases - Approved.'.                                    
                                                                                
                                                                                
       01  MAIL-DTL-HDR-UPD.                                                    
           05 FILLER                   PIC X(51)  VALUE                         
              ' SNO   Regn No   Sch Account    Loan  OIC   Status '.            
           05 FILLER                   PIC X(29)  VALUE                         
              ' Date of Approval'.                                              
                                                                                
       01  MAIL-DTL-FIL-UPD.                                                    
           05 FILLER                   PIC X(51)  VALUE                         
              ' ===== =======   ===========    ==== ===== ========'.            
           05 FILLER                   PIC X(35)  VALUE                         
              ' ================'.                                              
                                                                                
       01  MAIL-DTL-CASE.                                                       
           05 MAIL-SNO                 PIC ZZZZ9   VALUE ZEROS.                 
           05 FILLER                   PIC X(01)  VALUE SPACES.                 
           05 MAIL-REGN                PIC X(08)  VALUE SPACES.                 
           05 FILLER                   PIC X(02)  VALUE SPACES.                 
           05 MAIL-SCH-ACC             PIC X(14)  VALUE SPACES.                 
           05 FILLER                   PIC X(03)  VALUE SPACES.                 
           05 MAIL-LOAN-TAG            PIC X(01)  VALUE SPACES.                 
           05 FILLER                   PIC X(03)  VALUE SPACES.                 
           05 MAIL-OIC-NCK-NAME        PIC X(05)  VALUE SPACES.                 
           05 FILLER                   PIC X(02)  VALUE SPACES.                 
           05 MAIL-CORDER-STATUS       PIC X(10)  VALUE SPACES.                 
           05 FILLER                   PIC X(02)  VALUE SPACES.                 
           05 MAIL-DTE-APPROVAL        PIC X(10)  VALUE SPACES.                 
           05 FILLER                   PIC X(14)  VALUE SPACES.                 
                                                                                
                                                                                
       01  MAIL-END.                                                            
           05 FILLER                   PIC X      VALUE '.'.                    
                                                                                
       01  MAIL-SPACES.                                                         
           05 FILLER                   PIC X(80)  VALUE SPACES.                 
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
            PERFORM 1000-OPEN-ROUTINE         THRU 1000-EXIT.                   
            PERFORM 2000-READ-P13F35B         THRU 2000-EXIT.                   
            PERFORM 3000-PROCESS-DETAIL       THRU 3000-EXIT                    
                              UNTIL F35B-EOF.                                   
            PERFORM 9000-CLOSE-ROUTINE        THRU  9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  P13F35B                                                  
                OUTPUT BP13MAIL                                                 
                OUTPUT P13MAILA.                                                
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       2000-READ-P13F35B.                                                       
      *-------------------------------------------------------------            
                                                                                
           READ P13F35B                                                         
             AT END                                                             
                MOVE 'Y'   TO WS-F35B-EOF                                       
                GO         TO 2000-EXIT                                         
           END-READ.                                                            
           ADD             1 TO     CNT-F35B-READ.                              
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DETAIL.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES           TO         BP13MAIL-REC.                       
           INITIALIZE                       BP13MAIL-REC.                       
           IF F35B-OIC-NCK-NAME NOT =  WS-PREV-CSM                              
              MOVE 1                  TO WS-SNO                                 
              MOVE F35B-OIC-NCK-NAME  TO WS-PREV-CSM                            
              MOVE WS-SNO TO MAIL-SNO                                           
              IF CNT-F35B-READ NOT = 1                                          
                 WRITE  BP13MAIL-REC FROM    MAIL-END                           
              END-IF                                                            
              MOVE SPACES    TO MAIL-RCPT MAIL-RCPT1                            
              STRING  F35B-OIC-NCK-NAME '@HDB.GOV.SG>'                          
              DELIMITED BY SPACE INTO MAIL-RCPT                                 
              STRING  F35B-OIC-NCK-NAME '@HDB.GOV.SG>'                          
              DELIMITED BY SPACE INTO MAIL-RCPT1                                
              PERFORM 3500-PRINT-MAIL-HDR    THRU 3500-EXIT                     
           ELSE                                                                 
              ADD 1 TO WS-SNO                                                   
              MOVE WS-SNO TO MAIL-SNO                                           
           END-IF.                                                              
           IF CNT-F35B-READ = 1                                                 
              PERFORM 3600-PRINT-MAIL-HDR    THRU 3600-EXIT                     
           END-IF.                                                              
           MOVE   F35B-REGN            TO   MAIL-REGN.                          
           STRING F35B-SCH-ACC(1:4) '-' F35B-SCH-ACC(5:4) '-'                   
                  F35B-SCH-ACC(9:1) '-' F35B-SCH-ACC(10:2)                      
                  DELIMITED BY SIZE INTO    MAIL-SCH-ACC.                       
           MOVE   F35B-LOAN-TAG        TO   MAIL-LOAN-TAG.                      
           MOVE   F35B-OIC-NCK-NAME    TO   MAIL-OIC-NCK-NAME.                  
           MOVE   F35B-CORDER-STATUS   TO   MAIL-CORDER-STATUS.                 
           STRING F35B-DTE-APPROVAL(7:2) '-' F35B-DTE-APPROVAL(5:2)'-'          
                  F35B-DTE-APPROVAL(1:4)                                        
                  DELIMITED BY SIZE INTO    MAIL-DTE-APPROVAL.                  
           WRITE BP13MAIL-REC        FROM   MAIL-DTL-CASE.                      
           MOVE  CNT-F35B-READ         TO   MAIL-SNO.                           
           WRITE P13MAILA-REC        FROM   MAIL-DTL-CASE.                      
           MOVE SPACES                 TO   BP13MAIL-REC.                       
           INITIALIZE                       BP13MAIL-REC.                       
           WRITE BP13MAIL-REC        FROM   MAIL-SPACES.                        
                                                                                
           PERFORM 2000-READ-P13F35B        THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3500-PRINT-MAIL-HDR.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES       TO      BP13MAIL-REC.                              
           INITIALIZE                BP13MAIL-REC.                              
                                                                                
           WRITE BP13MAIL-REC FROM MAIL-HDR-HELO.                               
           WRITE BP13MAIL-REC FROM MAIL-HDR-MAIL-FROM.                          
           WRITE BP13MAIL-REC FROM MAIL-DTL1.                                   
           WRITE BP13MAIL-REC FROM MAIL-HDR-DATA.                               
           WRITE BP13MAIL-REC FROM MAIL-HDR-FROM.                               
           WRITE BP13MAIL-REC FROM MAIL-TO.                                     
           WRITE BP13MAIL-REC FROM MAIL-UPD-SUBJECT.                            
           WRITE BP13MAIL-REC FROM MAIL-SPACES.                                 
           WRITE BP13MAIL-REC FROM MAIL-DTL-HDR-UPD.                            
           WRITE BP13MAIL-REC FROM MAIL-DTL-FIL-UPD.                            
           WRITE BP13MAIL-REC FROM MAIL-SPACES.                                 
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3600-PRINT-MAIL-HDR.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES       TO P13MAILA-REC.                                   
           INITIALIZE           P13MAILA-REC.                                   
                                                                                
           WRITE P13MAILA-REC FROM MAIL-HDR-HELO.                               
           WRITE P13MAILA-REC FROM MAIL-HDR-MAIL-FROM.                          
           WRITE P13MAILA-REC FROM MAIL-DTL1B.                                  
           WRITE P13MAILA-REC FROM MAIL-DTL1C.                                  
           WRITE P13MAILA-REC FROM MAIL-HDR-DATA.                               
           WRITE P13MAILA-REC FROM MAIL-HDR-FROM.                               
           WRITE P13MAILA-REC FROM MAIL-TO2.                                    
           WRITE P13MAILA-REC FROM MAIL-TO3.                                    
           WRITE P13MAILA-REC FROM MAIL-UPD-SUBJECT.                            
           WRITE P13MAILA-REC FROM MAIL-SPACES.                                 
           WRITE P13MAILA-REC FROM MAIL-DTL-HDR-UPD.                            
           WRITE P13MAILA-REC FROM MAIL-DTL-FIL-UPD.                            
           WRITE P13MAILA-REC FROM MAIL-SPACES.                                 
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY ' - BP13C35C CONTROL TOTALS -----'.                          
           DISPLAY ' '.                                                         
           DISPLAY ' NO. OF P13F35B RECS READ    = ' CNT-F35B-READ.             
           DISPLAY ' NO. OF RECORDS  WRITTEN     = ' WS-CNT.                    
           DISPLAY ' '.                                                         
                                                                                
            CLOSE P13F35B                                                       
                  BP13MAIL                                                      
                  P13MAILA.                                                     
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
