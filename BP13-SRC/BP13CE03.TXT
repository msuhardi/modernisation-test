       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CE03.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  23/06/09.                                                 
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  GET AGE OF HA1                              *            
      *                MATCH BY NRIC                               *            
      *                                                            *            
      *  INPUT FILES:  1. BP13F825                                 *            
      *                2. BP13F730                                 *            
      *                                                            *            
      *  OUTPUT FILE:  1. P13F730A                                 *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG NO.  BY.  DATE.    DETAILS.                            *            
      * ======== ==== ======== =================================== *            
      * BP133641 JB8  20090623 NEW PROGRAM                         *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F825 ASSIGN       TO BP13F825.                            
           SELECT BP13F730 ASSIGN       TO BP13F730.                            
           SELECT P13F730A ASSIGN       TO P13F730A.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F730                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F730.                                                           
                                                                                
       FD  BP13F825                                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F825.                                                           
                                                                                
       FD  P13F730A                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       01  P13F730A-REC            PIC X(500).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  NO-F825-READ        PIC 9(6)    VALUE ZEROS.                     
           05  NO-F730-READ        PIC 9(6)    VALUE ZEROS.                     
           05  NO-F730A-WRITE      PIC 9(6)    VALUE ZEROS.                     
           05  WS-MATCHED          PIC 9(6)    VALUE ZEROS.                     
                                                                                
       01  WS-CUR-DTE               PIC X(8)  VALUE SPACES.                     
       01  WS-CUR-DTE-R REDEFINES WS-CUR-DTE.                                   
           05  WS-CUR-DTE-N         PIC 9(8).                                   
       01  WS-PREV-REGN             PIC X(08) VALUE SPACES.                     
       01  WS-DTE-REQUEST.                                                      
           05  WS-REQUEST-CYM       PIC 9(6)  VALUE ZEROES.                     
           05  WS-REQUEST-DD        PIC 9(2)  VALUE ZEROES.                     
       01  WS-DTE-BIRTH.                                                        
           05  WS-BIRTH-CYM         PIC 9(6)  VALUE ZEROES.                     
           05  WS-BIRTH-DD          PIC 9(2)  VALUE ZEROES.                     
       01  WS-AGE                   PIC 9(6)  VALUE ZEROES.                     
       01  WS-NUM-AGE REDEFINES WS-AGE.                                         
           05 WS-APPLN-AGE          PIC 9(4).                                   
           05 WS-NUM-REMNG          PIC 9(2).                                   
       01  WS-MS                    PIC X(1).                                   
       01  WS-EOF                   PIC X     VALUE 'N'.                        
                                                                                
       01 WS-F825-REC.                                                          
          05 FILLER             PIC X(152).                                     
          05 WS-F825-FILE-STAT  PIC X(02).                                      
          05 FILLER             PIC X(196).                                     
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
           PERFORM 1000-OPENING-ROUTINE  THRU 1000-EXIT.                        
           PERFORM 2000-READ-F825        THRU 2000-EXIT.                        
           PERFORM 2100-READ-F730        THRU 2100-EXIT.                        
           PERFORM 4000-PROCESS-FILES    THRU 4000-EXIT                         
             UNTIL F825-NUM-NRIC = HIGH-VALUES                                  
               AND F730-NUM-NRIC1 = HIGH-VALUES.                                
           PERFORM 9000-CLOSING-ROUTINE   THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-OPENING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F825                                                 
                       BP13F730                                                 
                OUTPUT P13F730A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-DTE.                            
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       2000-READ-F825.                                                          
      *-------------------------------------------------------------            
            READ BP13F825 AT END                                                
                 MOVE HIGH-VALUES TO F825-NUM-NRIC                              
                 GO TO 2000-EXIT.                                               
                                                                                
            ADD  1            TO NO-F825-READ.                                  
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       2100-READ-F730.                                                          
      *-------------------------------------------------------------            
            READ BP13F730 AT END                                                
                 MOVE HIGH-VALUES TO F730-NUM-NRIC1                             
                 GO TO 2100-EXIT.                                               
                                                                                
            ADD  1            TO NO-F730-READ.                                  
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4000-PROCESS-FILES.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE ZEROES                        TO WS-AGE                         
                                                 WS-DTE-BIRTH                   
                                                 WS-DTE-REQUEST.                
                                                                                
           IF F825-NUM-NRIC = F730-NUM-NRIC1                                    
              PERFORM 4100-COMPUTE-HH-SIZE   THRU 4100-EXIT                     
                UNTIL F730-NUM-NRIC1 NOT = F825-NUM-NRIC                        
                   OR F730-NUM-NRIC1 = HIGH-VALUES                              
              PERFORM 2000-READ-F825         THRU 2000-EXIT                     
           ELSE                                                                 
              IF F825-NUM-NRIC > F730-NUM-NRIC1                                 
                 PERFORM 5000-WRITE-P13F730A THRU 5000-EXIT                     
                 PERFORM 2100-READ-F730      THRU 2100-EXIT                     
              ELSE                                                              
                 PERFORM 2000-READ-F825      THRU 2000-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4100-COMPUTE-HH-SIZE.                                                    
      *-------------------------------------------------------------            
                                                                                
           IF F825-DTE-BIRTH NOT NUMERIC                                        
              MOVE ZEROES                     TO F825-DTE-BIRTH                 
           END-IF                                                               
                                                                                
           MOVE F825-DTE-BIRTH                TO WS-DTE-BIRTH                   
           MOVE F730-DTE-REQUEST              TO WS-DTE-REQUEST                 
                                                                                
           COMPUTE WS-AGE = WS-REQUEST-CYM - WS-BIRTH-CYM                       
                                                                                
           PERFORM 5000-WRITE-P13F730A      THRU 5000-EXIT.                     
           ADD 1                              TO WS-MATCHED.                    
                                                                                
           PERFORM 2100-READ-F730           THRU 2100-EXIT.                     
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       5000-WRITE-P13F730A.                                                     
      ***************************************************************           
                                                                                
           MOVE WS-APPLN-AGE                  TO BP13F730-REC(468:4).           
                                                                                
           MOVE BP13F730-REC                  TO P13F730A-REC.                  
                                                                                
           WRITE P13F730A-REC                                                   
           ADD  1                             TO NO-F730A-WRITE.                
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           CLOSE BP13F825                                                       
                 BP13F730                                                       
                 P13F730A.                                                      
                                                                                
           DISPLAY '-----------------------------------------'.                 
           DISPLAY '          PROGRAM BP13CE03 '.                               
           DISPLAY '        RUN DATE : ' WS-CUR-DTE.                            
           DISPLAY 'NO. OF F825 REC READ        > ' NO-F825-READ.               
           DISPLAY 'NO. OF F730 REC READ        > ' NO-F730-READ.               
           DISPLAY 'NO. OF F730A WRITTEN        > ' NO-F730A-WRITE.             
           DISPLAY 'NO. OF MATCHED CASES        > ' WS-MATCHED.                 
           DISPLAY '-----------------------------------------'.                 
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
