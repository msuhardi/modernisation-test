       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C533.                                                  
      *DATE-WRITTEN. 26/12/2002.                                                
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *  OBJECTIVE:  TO DETERMINE SERS PREMIUM TAG AND UPDATE      *            
      *              IT TO K740                                    *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG-NO   CHG-DATE  BY   DESCRIPTION                        *            
      * ------   --------  --   -----------                        *            
      * BP132500 19042004 YCH2  UPD PREM CDE TO BP13K740           *            
      * BP132618 06112004 YCH2  UPD PREM CDE ONLY IF BP13K740 DO   *            
      *                         NOT HAVE ANY INFO                  *            
      * BP134838 12032013 YCH2) NEW PREMIUM CODES ADDED            *            
      * BP134843 12032013     )                                    *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
                                                                                
           SELECT BP13K740 ASSIGN TO BP13K740                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K740-KEY-FLD                               
                  ALTERNATE KEY   IS K740-NUM-REGN                              
                  FILE STATUS     IS BP13K740-STATUS.                           
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F558                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 600 CHARACTERS               
                                   RECORDING MODE IS F                          
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F558.                                                           
                                                                                
       FD   BP13K740                                                            
                                   RECORD CONTAINS 500 CHARACTERS.              
       COPY BP13K740.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  BP13K740-STATUS             PIC 9(2)    VALUE ZEROES.                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F558-READ            PIC 9(8)    VALUE ZEROES.                
           05  WS-RSL-PREM             PIC 9(8)    VALUE ZEROES.                
           05  WS-CNT-PREM             PIC 9(8)    VALUE ZEROES.                
           05  WS-K740-REWRITE         PIC 9(8)    VALUE ZEROES.                
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-RSL-STATUS-CDE       PIC X(3)    VALUE SPACES.                
           05  WS-NUM-REGN             PIC X(8)    VALUE SPACES.                
           05  WS-CUR-DATE             PIC X(8)    VALUE SPACES.                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-LOGIC.                                                         
           PERFORM 1000-INITIALIZATION  THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13F558   THRU 2000-EXIT.                         
           PERFORM 3000-PROCESS-RECORD  THRU 3000-EXIT                          
             UNTIL F558-NUM-REGN = HIGH-VALUES.                                 
           PERFORM 9999-CLOSE-FILES     THRU 9999-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-INITIALIZATION.                                                     
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F558                                                 
               I-O     BP13K740.                                                
                                                                                
           IF BP13K740-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K740 - OPEN FILE ERROR, ' BP13K740-STATUS            
              MOVE BP13K740-STATUS     TO RETURN-CODE                           
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE    TO WS-CUR-DATE.                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-BP13F558.                                                      
      *-------------------------------------------------------------            
           READ BP13F558 AT END                                                 
               MOVE HIGH-VALUES TO F558-NUM-REGN                                
               GO TO 2000-EXIT.                                                 
                                                                                
           ADD 1 TO WS-F558-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-PROCESS-RECORD.                                                     
      *-------------------------------------------------------------            
           MOVE SPACES TO WS-RSL-STATUS-CDE.                                    
                                                                                
           MOVE F558-NUM-REGN TO WS-NUM-REGN.                                   
                                                                                
           PERFORM 3100-PROCESS-BP13F558  THRU 3100-EXIT                        
                   UNTIL F558-NUM-REGN NOT = WS-NUM-REGN                        
                     OR  F558-NUM-REGN = HIGH-VALUES.                           
                                                                                
           IF WS-RSL-STATUS-CDE = SPACES                                        
              GO TO 3000-EXIT                                                   
           END-IF                                                               
                                                                                
           ADD 1 TO WS-CNT-PREM.                                                
                                                                                
           MOVE WS-NUM-REGN TO K740-NUM-REGN.                                   
           READ BP13K740 KEY IS K740-NUM-REGN.                                  
                                                                                
           EVALUATE BP13K740-STATUS                                             
              WHEN  00                                                          
                    IF K740-NUM-PREM-CDE = SPACES OR LOW-VALUES                 
                       PERFORM 3200-UPDATE-K740 THRU 3200-EXIT                  
                    END-IF                                                      
              WHEN  23                                                          
                    DISPLAY 'RECORD NOT FOUND IN K740 ' WS-NUM-REGN             
              WHEN  OTHER                                                       
                    DISPLAY 'READ  K740 ERROR ' BP13K740-STATUS                 
                    DISPLAY 'KEY IS ' WS-NUM-REGN                               
                    PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3100-PROCESS-BP13F558.                                                   
      *-------------------------------------------------------------            
                                                                                
           IF F558-CDE-FILE-STATUS-RSL = 'SN ' OR 'BSN' OR                      
                       'CN ' OR 'BCN' OR '2N ' OR 'B2N' OR                      
                       'ESN' OR 'ELP' OR 'ON ' OR 'BON' OR                      
                       'AN ' OR 'BAN' OR 'VSN'                                  
              ADD 1 TO WS-RSL-PREM                                              
              MOVE F558-CDE-FILE-STATUS-RSL TO WS-RSL-STATUS-CDE                
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F558 THRU 2000-EXIT.                           
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3200-UPDATE-K740.                                                        
      *-------------------------------------------------------------            
                                                                                
           MOVE WS-CUR-DATE           TO K740-DTE-UPDATE.                       
           MOVE 'P13C522'             TO K740-NUM-USER-ID.                      
                                                                                
           MOVE WS-RSL-STATUS-CDE  TO K740-NUM-PREM-CDE.                        
                                                                                
           REWRITE BP13K740-REC.                                                
                                                                                
           EVALUATE BP13K740-STATUS                                             
             WHEN   00                                                          
                    ADD 1 TO WS-K740-REWRITE                                    
             WHEN   OTHER                                                       
                    DISPLAY ' ERROR UPDATING BP13K740 REC '                     
                         ' STATUS ' BP13K740-STATUS                             
                    DISPLAY ' KEY IS ' WS-NUM-REGN                              
                    PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9999-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           DISPLAY '* ------------ BP13C533 ------------- * '.                  
           DISPLAY 'NO OF REC READ BP13F558     = ' WS-F558-READ.               
           DISPLAY 'NO OF RESALE PREM CASES     = ' WS-CNT-PREM.                
           DISPLAY 'NO OF RESALE PREM RECORDS   = ' WS-RSL-PREM.                
           DISPLAY 'NO OF K740 UPDATED          = ' WS-K740-REWRITE.            
                                                                                
           CLOSE BP13F558                                                       
                 BP13K740.                                                      
                                                                                
           IF BP13K740-STATUS NOT = 00                                          
              DISPLAY 'BP13K740 - CLOSE FILE ERROR, ' BP13K740-STATUS           
              MOVE BP13K740-STATUS TO RETURN-CODE                               
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
