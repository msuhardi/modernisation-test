      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CC05.                                                 
       AUTHOR.        ABH.                                                      
       DATE-WRITTEN.  20/04/06.                                                 
      *****************************************************************         
      * OBJECTIVE 1. TO POST PAYMENTS AND ADJUSTMENTS TO PROGRESS               
      *              PAYMENT MASTER BP13KD00 FOR INSTALMENT                     
      *           2. RAISE INTEREST AND ADD TO LOAN-BALANCE                     
      *           3. UPDATE POSTING DATE FILE BP13KD65                          
      *                                                                         
      * SUBROUTINE     - NIL                                                    
      *                                                                         
      * IDENTICAL PGM  - AB03C105                                               
      * IDENTICAL FILE - AB03K090 = BP13KD65                                    
      *                - AB03F001 = BP13F125                                    
      *                                                                         
      *----------------------------------------------------------------         
      * REF NO    DATE      BY   AMENDMENTS/ENHANCEMENTS                        
      * -------   --------  ---  ------------------------                       
      * BP132772  20042006  BA3  NEW PROGRAM                                    
      * BP132772  25082006  BA3  COMPUTE MTH-ARREARS                            
      * BP132772  12102006  BA3  ADD SCH-ACC TO BP13K140                        
      * BP132888  31102006  BA3  SHIFT REPORT HEADING DOWN                      
      * BP132888  19032007  BA3  MOVE ZEROES TO KD00-NUM-MTH-INSTAL             
      *                          IF KD00-AMT-ARR-INSTAL EQUAL TO ZEROES         
      * BP133095  22082007  BA3  ROUNDED COMPUTATION FOR NUM-MTH-INSTAL         
      * BP133173  18102007  BA3  INCLUDE COMPUTATION OF INTEREST INSTAL         
      *                          AND TOTAL INTEREST TODATE. ADDED F125A         
      * BP133175  16112007  BA3  POPULATE CODE LOAN SCHEME AND CODE CRDR        
      *                          INTO BP13F125 FOR PAYMENT TYPE EQUAL TO        
      *                          'PP'                                           
      * BP133091  16112007  RB12 EXPAND KD00 LENGTH FR 1200 TO 1600             
      * BP133091  15122007  LMS1 EXPAND KD99 LENGTH FR 1250 TO 1650             
      * BP133535  10022009  JB8  READ/UPDATE KD03 IF KD00 NOT FND               
      *                          ONLY IF PAYMENT-TYPE = 'PP '                   
      * BP133662  05112009  JB8  TO CATER FOR 'X' ACCOUNT                       
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F125  ASSIGN  TO   BP13F125.                             
           SELECT  SY02F001  ASSIGN  TO   SY02F001.                             
                                                                                
           SELECT  BP13KD00  ASSIGN  TO   BP13KD00                              
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS KD00-NUM-REGN                      
                             FILE STATUS  IS KD00-STATUS                        
                             ACCESS MODE  IS DYNAMIC.                           
                                                                                
           SELECT  BP13KD03  ASSIGN  TO   BP13KD03                              
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS KD03-NUM-REGN                      
                             FILE STATUS  IS KD03-STATUS                        
                             ACCESS MODE  IS DYNAMIC.                           
                                                                                
           SELECT  BP13KD65  ASSIGN  TO   BP13KD65                              
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS KD65-CDE-FILEID                    
                             FILE STATUS  IS KD65-STATUS                        
                             ACCESS MODE  IS RANDOM.                            
                                                                                
           SELECT  BP13K140  ASSIGN  TO   BP13K140                              
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS K140-KEY-FLD                       
                             FILE STATUS  IS K140-STATUS                        
                             ACCESS MODE  IS RANDOM.                            
                                                                                
           SELECT  BP13KD99  ASSIGN  TO   BP13KD99                              
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS KD99-KEY-FLD                       
                             FILE STATUS  IS KD99-STATUS                        
                             ACCESS MODE  IS DYNAMIC.                           
                                                                                
           SELECT  BP13LC05  ASSIGN  TO   BP13LC05.                             
                                                                                
           SELECT  P13F125A  ASSIGN  TO   P13F125A.                             
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F125                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 150 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F125.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13KD00                   RECORD CONTAINS 1600 CHARACTERS.         
       COPY BP13KD00.                                                           
                                                                                
       FD   BP13KD03                   RECORD CONTAINS 1600 CHARACTERS.         
       COPY BP13KD03.                                                           
                                                                                
       FD   BP13KD99                   RECORD CONTAINS 1650 CHARACTERS.         
       COPY BP13KD99.                                                           
                                                                                
       FD   BP13KD65                   RECORD CONTAINS 200 CHARACTERS.          
       COPY BP13KD65.                                                           
                                                                                
       FD  BP13K140                    RECORD CONTAINS 150 CHARACTERS.          
       COPY BP13K140.                                                           
                                                                                
       FD  BP13LC05                    BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 132 CHARACTERS           
                                       LABEL RECORDS ARE OMITTED                
                                       RECORDING MODE IS F.                     
       01  PRINT-REC                   PIC X(132).                              
                                                                                
       FD  P13F125A                    BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 150 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       01  OUT-F125A                   PIC X(150).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  RETURN-CODES.                                                        
           05 KD00-STATUS              PIC 9(2)  VALUE 0.                       
           05 KD03-STATUS              PIC 9(2)  VALUE 0.                       
           05 KD65-STATUS              PIC 9(2)  VALUE 0.                       
           05 K140-STATUS              PIC 9(2)  VALUE 0.                       
           05 KD99-STATUS              PIC 9(2)  VALUE 0.                       
                                                                                
       01  WS-FLAG-UPDATE-KD65         PIC X     VALUE 'N'.                     
       01  WS-KD00-FOUND               PIC X     VALUE 'N'.                     
       01  WS-KD03-FND                 PIC X.                                   
           88  KD03-FND                VALUE 'Y'.                               
                                                                                
       01  WS-COUNTERS                           VALUE ZEROES.                  
           05  WS-KD00-FND-CNT         PIC 9(7).                                
           05  WS-KD00-NOT-FND-CNT     PIC 9(7).                                
           05  WS-CNT-KD00-WRT         PIC 9(7).                                
           05  WS-CNT-KD03-WRT         PIC 9(7).                                
           05  WS-CNT-KD99-WRT         PIC 9(7).                                
           05  WS-CNT-KD65-WRT         PIC 9(7).                                
           05  WS-CNT-K140-WRT         PIC 9(7).                                
           05  WS-CNT-F125             PIC 9(7).                                
           05  WS-CNT-F125-ZERO        PIC 9(7).                                
           05  WS-WRITE-F125A-CNT      PIC 9(7).                                
           05  WS-PAGE-CNT             PIC 9(5).                                
           05  WS-LINE-CNT             PIC 9(2).                                
           05  CNT-KIV-CASES           PIC 9(5).                                
           05  WS-CNT-HIST             PIC 9(2).                                
                                                                                
       01  WS-ACCUMULATORS.                                                     
           05  WS-AMT-LOAN             PIC S9(12)V99 VALUE +0.                  
                                                                                
       01  WS-AMOUT-VARS.                                                       
           05  WS-AMT-INTEREST         PIC S9(07)V99 VALUE +0.                  
                                                                                
       01  WS-CURR-DATE.                                                        
           05  WS-CURR-CCYY            PIC X(4)  VALUE SPACES.                  
           05  WS-CURR-MM              PIC X(2)  VALUE SPACES.                  
           05  WS-CURR-DD              PIC X(2)  VALUE SPACES.                  
                                                                                
       01  WS-DTE-GO-POSTNG-CYM        PIC X(6)  VALUE SPACES.                  
                                                                                
       01  WS-DTE-CPF-POSTNG-CYM       PIC X(6)  VALUE SPACES.                  
                                                                                
       01  WS-DTE-1ST-POSTNG                     VALUE SPACES.                  
           05  WS-DTE-1ST-POSTNG-CYM   PIC X(6).                                
           05  WS-DTE-1ST-POSTNG-DD    PIC X(2).                                
                                                                                
       01  WS-DTE-NW-POSTNG                      VALUE SPACES.                  
           05  WS-DTE-NW-POSTNG-CYM    PIC X(6).                                
           05  WS-DTE-NW-POSTNG-DD     PIC X(2).                                
                                                                                
       01  WS-DTE-NCN-WKLY-POSTNG                VALUE SPACES.                  
           05  WS-DTE-NCN-WKLY-POSTNG-CYM        PIC X(6).                      
           05  WS-DTE-NCN-WKLY-POSTNG-DD         PIC X(2).                      
                                                                                
       01  WS-DTE-VR-POSTNG                      VALUE SPACES.                  
           05  WS-DTE-VR-POSTNG-CYM    PIC X(6).                                
           05  WS-DTE-VR-POSTNG-DD     PIC X(2).                                
                                                                                
       01  WS-DTE-IT-POSTNG                      VALUE SPACES.                  
           05  WS-DTE-IT-POSTNG-CYM    PIC X(6).                                
           05  WS-DTE-IT-POSTNG-DD     PIC X(2).                                
                                                                                
       01  LC05-PR-HEAD-01.                                                     
           05  FILLER                  PIC X(8)  VALUE 'BP13LC05'.              
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(8)  VALUE 'HDBCAT 3'.              
           05  FILLER                  PIC X(24) VALUE SPACES.                  
           05  FILLER                  PIC X(20) VALUE                          
                                       'S Y S T E M   O F   '.                  
           05  FILLER                  PIC X(19) VALUE                          
                                       'C O M M I T M E N T'.                   
           05  FILLER                  PIC X(15) VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'DATE :'.                
           05  LC05-DATE               PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'PAGE : '.               
           05  LC05-PAGENO             PIC ZZZZ9 VALUE ZEROES.                  
                                                                                
       01  LC05-PR-HEAD-02.                                                     
           05  FILLER                  PIC X(26) VALUE SPACES.                  
           05  FILLER                  PIC X(51) VALUE                          
               ' ERROR REPORT FOR TRANSACITON CODE NOT EQUAL TO C O'.           
           05  FILLER                  PIC X(15) VALUE                          
               'N BP13KD00 FOR '.                                               
           05  LC05-FORDATE            PIC X(08) VALUE SPACES.                  
                                                                                
       01  LC05-PR-HEAD-03.                                                     
           05  FILLER                  PIC X(6)  VALUE 'S/NO'.                  
           05  FILLER                  PIC X(6)  VALUE SPACES.                  
           05  FILLER                  PIC X(8)  VALUE 'REG NO '.               
           05  FILLER                  PIC X(6)  VALUE SPACES.                  
           05  FILLER                  PIC X(8)  VALUE 'PAY TYPE'.              
           05  FILLER                  PIC X(2)  VALUE SPACES.                  
           05  FILLER                  PIC X(9)  VALUE 'NUM-NRIC '.             
           05  FILLER                  PIC X(2)  VALUE SPACES.                  
           05  FILLER                  PIC X(10) VALUE 'TRANS TYPE'.            
           05  FILLER                  PIC X(2)  VALUE SPACES.                  
           05  FILLER                  PIC X(14) VALUE                          
                                                 'RECEIPT NO    '.              
           05  FILLER                  PIC X(2)  VALUE SPACES.                  
           05  FILLER                  PIC X(10) VALUE '  AMOUNT  '.            
           05  FILLER                  PIC X(2)  VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'CDE ERR'.               
                                                                                
       01  LC05-PR-DETAILS.                                                     
           05  LC05-SNO                PIC ZZZZ9 VALUE ZEROES.                  
           05  FILLER                  PIC X(6)  VALUE SPACES.                  
           05  LC05-ORIG-REG-NO        PIC X(8)  VALUE SPACES.                  
           05  FILLER                  PIC X(9)  VALUE SPACES.                  
           05  LC05-PAYMENT-TYPE       PIC X(3)  VALUE SPACES.                  
           05  FILLER                  PIC X(5)  VALUE SPACES.                  
           05  LC05-NUM-NRIC           PIC X(9)  VALUE SPACES.                  
           05  FILLER                  PIC X(6)  VALUE SPACES.                  
           05  LC05-TRANS-TYPE         PIC X(6)  VALUE SPACES.                  
           05  FILLER                  PIC X(2)  VALUE SPACES.                  
           05  LC05-NUM-RECEIPT        PIC X(14) VALUE SPACES.                  
           05  FILLER                  PIC X(2)  VALUE SPACES.                  
           05  LC05-AMT                PIC $$$$$$9.99DB                         
                                                 VALUE ZEROES.                  
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  LC05-CDE-ERR            PIC X(1)  VALUE SPACES.                  
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN-PARA.                                                          
      ******************************************************************        
                                                                                
           PERFORM 1000-START-UP           THRU  1000-EXIT.                     
                                                                                
           PERFORM 2000-READ-F125          THRU  2000-EXIT.                     
                                                                                
           PERFORM 4000-PROCESS-RECORD     THRU  4000-EXIT                      
                   UNTIL F125-KEY-FLD      =     HIGH-VALUES.                   
                                                                                
           PERFORM 9999-WRAP-UP.                                                
                                                                                
       0000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1000-START-UP.                                                           
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F125                                                 
                       SY02F001                                                 
                I-O    BP13KD00                                                 
                       BP13KD03                                                 
                       BP13KD65                                                 
                       BP13K140                                                 
                       BP13KD99                                                 
                OUTPUT BP13LC05                                                 
                       P13F125A.                                                
                                                                                
           IF  KD00-STATUS NOT = 0 AND 97                                       
               DISPLAY 'OPEN FAILED FOR BP13KD00 ' KD00-STATUS                  
               MOVE KD00-STATUS TO RETURN-CODE                                  
               GO       TO 9999-WRAP-UP.                                        
                                                                                
           IF  KD03-STATUS NOT = 0 AND 97                                       
               DISPLAY 'OPEN FAILED FOR BP13KD03 ' KD03-STATUS                  
               MOVE KD03-STATUS TO RETURN-CODE                                  
               GO       TO 9999-WRAP-UP.                                        
                                                                                
           IF  KD65-STATUS NOT = 0 AND 97                                       
               DISPLAY 'OPEN FAILED FOR BP13KD65 ' KD65-STATUS                  
               MOVE KD65-STATUS TO RETURN-CODE                                  
               GO       TO 9999-WRAP-UP.                                        
                                                                                
           IF  K140-STATUS NOT = 0 AND 97                                       
               DISPLAY 'OPEN FAILED FOR BP13K140 ' K140-STATUS                  
               MOVE K140-STATUS TO RETURN-CODE                                  
               GO       TO 9999-WRAP-UP.                                        
                                                                                
           IF  KD99-STATUS NOT = 0 AND 97                                       
               DISPLAY 'OPEN FAILED FOR BP13KD99 ' KD99-STATUS                  
               MOVE KD99-STATUS TO RETURN-CODE                                  
               GO       TO 9999-WRAP-UP.                                        
                                                                                
                                                                                
           MOVE 60                    TO   WS-LINE-CNT.                         
           PERFORM 1500-READ-SY02F001 THRU 1500-EXIT.                           
           STRING WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CCYY                    
                  DELIMITED BY SIZE INTO LC05-DATE.                             
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
        1500-READ-SY02F001.                                                     
      ******************************************************************        
                                                                                
            READ SY02F001.                                                      
            MOVE F001-DTE-CURRENT      TO WS-CURR-DATE.                         
                                                                                
            STRING F001-TXT-CURRENT-MTH(1:3) ' '                                
                   F001-DTE-CURRENT(1:4)                                        
                   DELIMITED BY SIZE INTO LC05-FORDATE.                         
                                                                                
        1500-EXIT.                                                              
            EXIT.                                                               
                                                                                
      ******************************************************************        
       2000-READ-F125.                                                          
      ******************************************************************        
                                                                                
           READ BP13F125           AT   END                                     
                MOVE HIGH-VALUES   TO   F125-KEY-FLD                            
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO   WS-CNT-F125.                            
                                                                                
       2000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       4000-PROCESS-RECORD.                                                     
      ******************************************************************        
                                                                                
           PERFORM  4100-CHK-TT-FOR-DTE-PSTNG THRU 4100-EXIT.                   
                                                                                
           PERFORM  4500-READ-KD00         THRU 4500-EXIT.                      
           IF WS-KD00-FOUND = 'Y'                                               
              ADD 1 TO WS-KD00-FND-CNT                                          
              PERFORM 5000-PROCESS-POSTING THRU 5000-EXIT                       
           ELSE                                                                 
              ADD 1 TO WS-KD00-NOT-FND-CNT                                      
           END-IF.                                                              
                                                                                
           PERFORM 7500-WRITE-F125A        THRU  7500-EXIT.                     
           PERFORM 2000-READ-F125          THRU  2000-EXIT.                     
                                                                                
       4000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       4100-CHK-TT-FOR-DTE-PSTNG.                                               
      ******************************************************************        
      *  POSTING DATE FOR "GO"="64","IT"="62","NM" AND "SD"="12,13,14".         
      *--------------------------------------------*                            
      * TRANS-TYPE = 11           - DTE-FIRST                                   
      * TRANS-TYPE = 12           - DTE-NCN                                     
      * TRANS-TYPE = 13,14,15,16  - DTE-VR                                      
      * TRANS-TYPE = 62           - INTEREST                                    
      * TRANS-TYPE = 64           - GIRO                                        
                                                                                
           IF F125-CDE-TRANS-TYPE = '64'                                        
              AND  WS-DTE-GO-POSTNG-CYM     =   SPACES                          
                  MOVE F125-DTE-TRANS      TO  WS-DTE-GO-POSTNG-CYM.            
                                                                                
      *? MONTHLY/WKLY POSTING FOR NCN/CPF                                       
      *   IF F125-CDE-TRANS-TYPE = '12' OR '13' OR '14'                         
      *    AND  WS-DTE-CPF-POSTNG-CYM    =   SPACES                             
      *         MOVE F125-DTE-TRANS      TO  WS-DTE-CPF-POSTNG-CYM.             
                                                                                
           IF F125-CDE-TRANS-TYPE = '11'                                        
              IF F125-DTE-TRANS           >  WS-DTE-1ST-POSTNG                  
                 MOVE F125-DTE-TRANS      TO WS-DTE-1ST-POSTNG.                 
                                                                                
           IF F125-CDE-TRANS-TYPE = '12'                                        
              IF F125-DTE-TRANS           >  WS-DTE-NCN-WKLY-POSTNG             
                 MOVE F125-DTE-TRANS      TO WS-DTE-NCN-WKLY-POSTNG.            
                                                                                
           IF F125-CDE-TRANS-TYPE = '13' OR '14' OR '15' OR '16'                
              AND F125-DTE-TRANS          >  WS-DTE-VR-POSTNG                   
                 MOVE F125-DTE-TRANS      TO WS-DTE-VR-POSTNG.                  
                                                                                
           IF F125-CDE-TRANS-TYPE = '62'                                        
                 MOVE F125-DTE-TRANS      TO WS-DTE-IT-POSTNG.                  
                                                                                
       4100-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       4500-READ-KD00.                                                          
      ******************************************************************        
                                                                                
           MOVE F125-NUM-REGN     TO KD00-NUM-REGN.                             
           MOVE 'N'               TO WS-KD00-FOUND                              
                                     WS-KD03-FND.                               
                                                                                
           READ BP13KD00.                                                       
           EVALUATE KD00-STATUS                                                 
              WHEN 00                                                           
                 MOVE 'Y' TO WS-KD00-FOUND                                      
              WHEN 23                                                           
                 PERFORM 4501-READ-KD03    THRU 4501-EXIT                       
                 IF KD03-FND                                                    
                    MOVE BP13KD03-REC       TO BP13KD00-REC                     
                    MOVE 'Y'                TO WS-KD00-FOUND                    
                 END-IF                                                         
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN READING BP13KD00 - STATUS '                  
                             KD00-STATUS                                        
                 MOVE        KD00-STATUS    TO RETURN-CODE                      
                 GO       TO 9999-WRAP-UP                                       
           END-EVALUATE.                                                        
                                                                                
       4500-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       4501-READ-KD03.                                                          
      ******************************************************************        
                                                                                
           MOVE F125-NUM-REGN     TO KD03-NUM-REGN.                             
                                                                                
           READ BP13KD03.                                                       
           EVALUATE KD03-STATUS                                                 
              WHEN 00                                                           
                 MOVE 'Y' TO WS-KD03-FND                                        
              WHEN 23                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN READING BP13KD03 - STATUS '                  
                             KD03-STATUS                                        
                 MOVE        KD03-STATUS    TO RETURN-CODE                      
                 GO       TO 9999-WRAP-UP                                       
           END-EVALUATE.                                                        
                                                                                
       4501-EXIT.  EXIT.                                                        
                                                                                
      *                                                                         
      * NO POSTING, BYPASS IF :                                                 
      * CDE-PAYMENT-TYPE = 'INS' AND CDE-TRANS-TYPE NOT = '65',                 
      *                                                                         
                                                                                
      ******************************************************************        
       5000-PROCESS-POSTING.                                                    
      ******************************************************************        
           IF F125-AMT-RECEIPT IS NOT NUMERIC                                   
              MOVE 0 TO F125-AMT-RECEIPT.                                       
           IF F125-AMT-INTEREST IS NOT NUMERIC                                  
              MOVE 0 TO F125-AMT-INTEREST.                                      
           IF KD00-AMT-PP-PD-DEVLP-TOT IS NOT NUMERIC                           
              MOVE 0 TO KD00-AMT-PP-PD-DEVLP-TOT.                               
           IF KD00-AMT-PP-LUMPSUM-PAID IS NOT NUMERIC                           
              MOVE 0 TO KD00-AMT-PP-LUMPSUM-PAID.                               
           IF KD00-AMT-LOAN-BAL IS NOT NUMERIC                                  
              MOVE 0 TO KD00-AMT-LOAN-BAL.                                      
           IF KD00-AMT-TOTL-PYMT-TODTE IS NOT NUMERIC                           
              MOVE 0 TO KD00-AMT-TOTL-PYMT-TODTE.                               
           IF KD00-AMT-ARR-INSTAL IS NOT NUMERIC                                
              MOVE 0 TO KD00-AMT-ARR-INSTAL.                                    
                                                                                
           IF F125-CDE-PAYMENT-TYPE  =   'PP '                                  
              PERFORM 5100-PROCESS-PROGRESS-PAYMENT THRU 5100-EXIT              
           ELSE                                                                 
           IF F125-CDE-PAYMENT-TYPE  =   'INS'                                  
              PERFORM 5200-PROCESS-INSTALMENT THRU 5200-EXIT                    
           ELSE                                                                 
           IF F125-CDE-PAYMENT-TYPE  =   'IT '                                  
              PERFORM 5300-PROCESS-INTEREST THRU 5300-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 6000-WRITE-LOG-BP13KD99  THRU 6000-EXIT.                     
           PERFORM 6100-UPDATE-BP13KD00     THRU 6100-EXIT.                     
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       5100-PROCESS-PROGRESS-PAYMENT.                                           
      ******************************************************************        
                                                                                
           MOVE KD00-CDE-LOAN-SCH             TO F125-NUM-LOAN-SCH.             
           MOVE 'C'                           TO F125-CDE-CRDR.                 
                                                                                
           IF F125-CDE-TRANS-TYPE  =   '63'                                     
              COMPUTE KD00-AMT-PP-PD-DEVLP-TOT =                                
                      KD00-AMT-PP-PD-DEVLP-TOT + F125-AMT-RECEIPT               
              MOVE F125-DTE-TRANS TO KD00-DTE-PP-PAID-DEVLP                     
           ELSE                                                                 
              COMPUTE KD00-AMT-PP-LUMPSUM-PAID =                                
                      KD00-AMT-PP-LUMPSUM-PAID + F125-AMT-RECEIPT               
           END-IF.                                                              
                                                                                
       5100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       5200-PROCESS-INSTALMENT.                                                 
      ******************************************************************        
           IF KD00-AMT-ARR-INTR IS NOT NUMERIC                                  
              MOVE ZEROES TO KD00-AMT-ARR-INTR                                  
           END-IF.                                                              
           IF KD00-AMT-TOT-INTR-TODTE IS NOT NUMERIC                            
              MOVE ZEROES TO KD00-AMT-TOT-INTR-TODTE                            
           END-IF.                                                              
                                                                                
           IF KD00-CDE-ACCT-STAT   =     'C'                                    
              IF F125-CDE-TRANS-TYPE  = '11' OR '12' OR '13' OR '14' OR         
                                        '15' OR '16' OR '64'                    
                 COMPUTE KD00-AMT-LOAN-BAL =   KD00-AMT-LOAN-BAL                
                                           -   F125-AMT-RECEIPT                 
                 COMPUTE KD00-AMT-TOTL-PYMT-TODTE                               
                                           =   KD00-AMT-TOTL-PYMT-TODTE         
                                           +   F125-AMT-RECEIPT                 
                 COMPUTE KD00-AMT-LOAN-PAID                                     
                                           =   KD00-AMT-LOAN-PAID               
                                           +   F125-AMT-RECEIPT                 
                 COMPUTE KD00-AMT-ARR-INSTAL                                    
                                           =   KD00-AMT-ARR-INSTAL              
                                           -   F125-AMT-RECEIPT                 
                 MOVE WS-CURR-DATE(1:6) TO KD00-DTE-LATEST-PYMT-LOAN            
                                                                                
      *** COMPUTATION OF ARR-INTR (PAYMENT)                                     
                 MOVE 'C'                    TO F125-CDE-CRDR                   
                 IF F125-AMT-RECEIPT > KD00-AMT-ARR-INTR                        
                    MOVE KD00-AMT-ARR-INTR   TO F125-AMT-INTEREST               
                    MOVE KD00-AMT-ARR-INTR   TO WS-AMT-INTEREST                 
                    MOVE ZEROES              TO KD00-AMT-ARR-INTR               
                 ELSE                                                           
                    COMPUTE KD00-AMT-ARR-INTR =                                 
                            KD00-AMT-ARR-INTR - F125-AMT-RECEIPT                
                    MOVE F125-AMT-RECEIPT      TO F125-AMT-INTEREST             
                    MOVE F125-AMT-RECEIPT      TO WS-AMT-INTEREST               
                 END-IF                                                         
                                                                                
                 COMPUTE KD00-AMT-TOT-INTR-TODTE =                              
                         KD00-AMT-TOT-INTR-TODTE + WS-AMT-INTEREST              
                                                                                
                 IF KD00-AMT-ARR-INSTAL > 0                                     
                   COMPUTE KD00-NUM-MTH-INSTAL ROUNDED =                        
                           KD00-AMT-ARR-INSTAL / KD00-AMT-INSTAL                
                 ELSE                                                           
                   MOVE ZEROES          TO KD00-NUM-MTH-INSTAL                  
                 END-IF                                                         
              ELSE                                                              
                 PERFORM 7000-WRITE-KIV-FILE   THRU 7000-EXIT                   
                 PERFORM 8000-ERROR-LIST       THRU 8000-EXIT                   
              END-IF.                                                           
                                                                                
       5200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       5300-PROCESS-INTEREST.                                                   
      ******************************************************************        
                                                                                
           IF KD00-AMT-ARR-INTR IS NOT NUMERIC                                  
              MOVE ZEROES           TO KD00-AMT-ARR-INTR                        
           END-IF.                                                              
                                                                                
           IF KD00-CDE-ACCT-STAT = 'C' OR 'X'                                   
              IF F125-CDE-TRANS-TYPE  = '62'                                    
                 COMPUTE KD00-AMT-LOAN-BAL =   KD00-AMT-LOAN-BAL                
                                           +   F125-AMT-RECEIPT                 
                                                                                
      *** COMPUTATION OF ARR-INTR (RAISE INTEREST)                              
                 MOVE 'D'                    TO F125-CDE-CRDR                   
                 COMPUTE KD00-AMT-ARR-INTR =                                    
                         KD00-AMT-ARR-INTR + F125-AMT-RECEIPT                   
                                                                                
              ELSE                                                              
                 PERFORM 7000-WRITE-KIV-FILE   THRU 7000-EXIT                   
                 PERFORM 8000-ERROR-LIST       THRU 8000-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       6000-WRITE-LOG-BP13KD99.                                                 
      ******************************************************************        
                                                                                
           MOVE BP13KD00-REC               TO BP13KD99-REC.                     
           MOVE KD00-NUM-REGN              TO KD99-NUM-REGN-LOG.                
           MOVE WS-CURR-DATE               TO KD99-DTE-UPDATE-LOG.              
           MOVE FUNCTION CURRENT-DATE(9:8) TO KD99-TME-UPDATE-LOG.              
                                                                                
           WRITE BP13KD99-REC.                                                  
           EVALUATE KD99-STATUS                                                 
              WHEN 00                                                           
              WHEN 22                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN WRITING  BP13KD99 - STATUS '                 
                             KD99-STATUS                                        
                 MOVE        KD99-STATUS    TO RETURN-CODE                      
                 GO TO 9999-WRAP-UP                                             
           END-EVALUATE.                                                        
                                                                                
           ADD 1 TO WS-CNT-KD99-WRT.                                            
                                                                                
       6000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       6100-UPDATE-BP13KD00.                                                    
      ******************************************************************        
                                                                                
           IF KD03-FND                                                          
              MOVE BP13KD00-REC TO BP13KD03-REC                                 
              REWRITE BP13KD03-REC                                              
              IF KD03-STATUS NOT = ZERO                                         
                 DISPLAY 'BP13KD03 - REWRITE FAILED ' KD03-STATUS               
                         ',' KD03-NUM-REGN                                      
                 MOVE KD03-STATUS TO RETURN-CODE                                
                 GO TO 9999-WRAP-UP                                             
              END-IF                                                            
              ADD 1 TO WS-CNT-KD03-WRT                                          
           ELSE                                                                 
              REWRITE BP13KD00-REC                                              
              IF KD00-STATUS NOT = ZERO                                         
                 DISPLAY 'BP13KD00 - REWRITE FAILED ' KD00-STATUS               
                         ',' KD00-NUM-REGN                                      
                 MOVE KD00-STATUS TO RETURN-CODE                                
                 GO TO 9999-WRAP-UP                                             
              END-IF                                                            
              ADD 1 TO WS-CNT-KD00-WRT                                          
           END-IF.                                                              
                                                                                
       6100-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       7000-WRITE-KIV-FILE.                                                     
      ******************************************************************        
           MOVE SPACES                 TO  BP13K140-KIVTRAN.                    
           ADD 1                       TO  WS-CNT-HIST.                         
           MOVE F125-NUM-REGN          TO  K140-NUM-REGN.                       
           MOVE F125-NUM-SCH-ACCT      TO  K140-NUM-SCH-ACCT.                   
           MOVE F125-CDE-TRANS-TYPE    TO  K140-CDE-TRANS-TYPE.                 
           MOVE WS-CURR-DATE           TO  K140-DTE-POST.                       
           MOVE 'S'                    TO  K140-COUNT-HIST-CHAR.                
           MOVE WS-CNT-HIST            TO  K140-COUNT-HIST-NUM.                 
           MOVE F125-NUM-RECPT-JRNO    TO  K140-NUM-RECPT-JRNO.                 
           MOVE F125-AMT-RECEIPT       TO  K140-AMT-RECEIPT.                    
           MOVE F125-DTE-TRANS         TO  K140-DTE-TRANS.                      
           MOVE 'T'                    TO  K140-CDE-ERROR.                      
           MOVE F125-CDE-PAYMENT-TYPE  TO  K140-CDE-PAYMENT-TYPE.               
           MOVE F125-CDE-APPLT-CPF     TO  K140-CDE-APPLT-CPF.                  
           MOVE F125-CDE-CRDR          TO  K140-CDE-CRDR.                       
           WRITE BP13K140-KIVTRAN.                                              
           IF K140-STATUS > 0 AND NOT = 22 AND NOT = '02'                       
               DISPLAY ' WRITE ERROR AT BP13K140 '                              
               DISPLAY ' BP13K140-STATUS IS ' K140-STATUS                       
               DISPLAY ' REGISTRATION NO IS : ' F125-NUM-REGN                   
               MOVE K140-STATUS TO RETURN-CODE                                  
               GO TO 9999-WRAP-UP                                               
           ELSE                                                                 
               IF K140-STATUS  = 22                                             
                  ADD 1 TO  WS-CNT-HIST                                         
                  MOVE WS-CNT-HIST TO  K140-COUNT-HIST-NUM                      
                  WRITE BP13K140-KIVTRAN                                        
                  DISPLAY ' DUPLICATE RECORDS AT BP13K140 '                     
                  DISPLAY ' REGISTRATION NO IS : '                              
                             F125-NUM-REGN.                                     
           ADD 1 TO WS-CNT-K140-WRT.                                            
                                                                                
       7000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       7500-WRITE-F125A.                                                        
      ******************************************************************        
           MOVE SPACES             TO OUT-F125A.                                
           MOVE BP13F125-CASHTRAN  TO OUT-F125A.                                
           WRITE OUT-F125A.                                                     
           ADD 1                   TO WS-WRITE-F125A-CNT.                       
                                                                                
       7500-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       8000-ERROR-LIST.                                                         
      ******************************************************************        
           IF WS-LINE-CNT > 50                                                  
               PERFORM 8100-ERROR-HEADING THRU 8100-EXIT                        
           END-IF.                                                              
      *    ADD   F125-AMT-RECEIPT      TO WS-AMT-KIV-TOTAL.                     
           ADD   1                     TO CNT-KIV-CASES                         
                                          WS-LINE-CNT.                          
           MOVE  CNT-KIV-CASES         TO LC05-SNO.                             
           MOVE  F125-NUM-REGN         TO LC05-ORIG-REG-NO.                     
           MOVE  F125-AMT-RECEIPT      TO LC05-AMT.                             
           MOVE  F125-CDE-PAYMENT-TYPE TO LC05-PAYMENT-TYPE.                    
           MOVE  F125-CDE-TRANS-TYPE   TO LC05-TRANS-TYPE.                      
           MOVE  F125-NUM-NRIC         TO LC05-NUM-NRIC.                        
           MOVE  F125-CDE-ERROR        TO LC05-CDE-ERR.                         
           MOVE  F125-NUM-RECPT-JRNO   TO LC05-NUM-RECEIPT.                     
           WRITE PRINT-REC FROM LC05-PR-DETAILS.                                
           MOVE  SPACES                TO LC05-PR-DETAILS.                      
                                                                                
       8000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       8100-ERROR-HEADING.                                                      
      ******************************************************************        
           ADD 1               TO WS-PAGE-CNT.                                  
           MOVE WS-PAGE-CNT    TO LC05-PAGENO.                                  
           MOVE SPACES         TO PRINT-REC                                     
           WRITE PRINT-REC FROM LC05-PR-HEAD-01 AFTER PAGE.                     
           WRITE PRINT-REC FROM LC05-PR-HEAD-02 AFTER 3.                        
           WRITE PRINT-REC FROM LC05-PR-HEAD-03 AFTER 2.                        
           MOVE SPACES TO PRINT-REC.                                            
           WRITE PRINT-REC AFTER 2.                                             
           MOVE 8 TO WS-LINE-CNT.                                               
                                                                                
       8100-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       9000-REWRITE-BP13KD65.                                                   
      ******************************************************************        
                                                                                
           MOVE WS-CURR-DATE TO KD65-DTE-LATEST-PAYMT-UPDATE.                   
                                                                                
           IF  WS-DTE-1ST-POSTNG    NOT  =   SPACES                             
           AND WS-DTE-1ST-POSTNG         >   KD65-DTE-FIRST-POSTNG              
               MOVE WS-DTE-1ST-POSTNG    TO  KD65-DTE-FIRST-POSTNG              
               MOVE 'Y'                  TO  WS-FLAG-UPDATE-KD65.               
                                                                                
           IF  WS-DTE-GO-POSTNG-CYM NOT  =   SPACES                             
           AND WS-DTE-GO-POSTNG-CYM      >   KD65-DTE-GIRO-POSTNG               
               MOVE WS-DTE-GO-POSTNG-CYM TO  KD65-DTE-GIRO-POSTNG               
               MOVE 'Y'                  TO  WS-FLAG-UPDATE-KD65.               
                                                                                
      *?   IF  WS-DTE-CPF-POSTNG-CYM NOT =   SPACES                             
      *    AND WS-DTE-CPF-POSTNG-CYM     >   KD65-DTE-NCN-MTHLY-POSTNG          
      *        MOVE WS-DTE-CPF-POSTNG-CYM TO KD65-DTE-NCN-MTHLY-POSTNG          
      *        MOVE 'Y'                  TO  WS-FLAG-UPDATE-KD65.               
                                                                                
           IF  WS-DTE-NCN-WKLY-POSTNG    NOT  =   SPACES                        
           AND WS-DTE-NCN-WKLY-POSTNG    >   KD65-DTE-NCN-WKLY-POSTNG           
               MOVE WS-DTE-NCN-WKLY-POSTNG                                      
                                         TO  KD65-DTE-NCN-WKLY-POSTNG           
               MOVE 'Y'                  TO  WS-FLAG-UPDATE-KD65.               
                                                                                
           IF  WS-DTE-VR-POSTNG     NOT  =   SPACES                             
           AND WS-DTE-VR-POSTNG          >   KD65-DTE-VR-POSTNG                 
               MOVE WS-DTE-VR-POSTNG     TO  KD65-DTE-VR-POSTNG                 
               MOVE 'Y'                  TO  WS-FLAG-UPDATE-KD65.               
                                                                                
           IF  WS-DTE-IT-POSTNG     NOT  =   SPACES                             
           AND WS-DTE-IT-POSTNG          >   KD65-DTE-INT-POSTNG                
               MOVE WS-DTE-IT-POSTNG     TO  KD65-DTE-INT-POSTNG                
               MOVE 'Y'                  TO  WS-FLAG-UPDATE-KD65.               
                                                                                
           IF  WS-FLAG-UPDATE-KD65       =   'Y'                                
               REWRITE BP13KD65-REC                                             
               IF KD65-STATUS NOT        =   0                                  
                  MOVE KD65-STATUS TO  RETURN-CODE                              
                  DISPLAY 'BP13KD65 - REWRITE FAILED'                           
               END-IF                                                           
               ADD 1 TO WS-CNT-KD65-WRT                                         
           END-IF.                                                              
                                                                                
       9000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       9999-WRAP-UP.                                                            
      ******************************************************************        
                                                                                
           MOVE SPACES                 TO BP13KD65-REC                          
           INITIALIZE                     BP13KD65-REC.                         
           MOVE 'KD65'                 TO KD65-CDE-FILEID.                      
           READ BP13KD65.                                                       
           IF KD65-STATUS = 00 OR 02                                            
              PERFORM 9000-REWRITE-BP13KD65 THRU 9000-EXIT                      
           ELSE                                                                 
             IF KD65-STATUS = 23                                                
                DISPLAY 'BP13KD65-RECORD NOT FOUND.'                            
              ELSE                                                              
                 DISPLAY ' BP13KD65-READ ERROR, STATUS: '                       
                         KD65-STATUS                                            
                 MOVE KD65-STATUS TO RETURN-CODE                                
             END-IF                                                             
           END-IF.                                                              
                                                                                
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13CC05'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY 'NO OF BP13F125 RECORDS READ..........: '                    
                    WS-CNT-F125.                                                
           DISPLAY '   -  BP13F125 RECS W/ ZERO AMT......: '                    
                    WS-CNT-F125-ZERO.                                           
           DISPLAY 'NO OF BP13KD00 RECORDS FOUND.........: '                    
                    WS-KD00-FND-CNT.                                            
           DISPLAY 'NO OF BP13KD00 RECORDS NOT FOUND.....: '                    
                    WS-KD00-NOT-FND-CNT.                                        
           DISPLAY 'NO OF BP13KD00 RECORDS UPDATED.......: '                    
                    WS-CNT-KD00-WRT.                                            
           DISPLAY 'NO OF BP13KD03 RECORDS UPDATED.......: '                    
                    WS-CNT-KD03-WRT.                                            
           DISPLAY 'NO OF BP13KD65 RECORDS UPDATED.......: '                    
                    WS-CNT-KD65-WRT.                                            
           DISPLAY 'NO OF BP13K140 RECORDS WRITTEN.......: '                    
                    WS-CNT-K140-WRT.                                            
           DISPLAY 'NO OF BP13LC05 RECORDS WRITTEN.......: '                    
                    CNT-KIV-CASES.                                              
           DISPLAY 'NO OF BP13KD99 RECORDS UPDATED.......: '                    
                    WS-CNT-KD99-WRT.                                            
           DISPLAY 'NO OF P13F125A RECORDS WRITTEN.......: '                    
                    WS-WRITE-F125A-CNT.                                         
                                                                                
           CLOSE BP13F125                                                       
                 SY02F001                                                       
                 BP13KD00                                                       
                 BP13KD03                                                       
                 BP13KD65                                                       
                 BP13K140                                                       
                 BP13KD99                                                       
                 BP13LC05                                                       
                 P13F125A.                                                      
                                                                                
           IF  KD00-STATUS NOT = 0                                              
               DISPLAY 'CLOSE FAILED FOR BP13KD00 ' KD00-STATUS                 
               MOVE KD00-STATUS TO RETURN-CODE.                                 
                                                                                
           IF  KD03-STATUS NOT = 0                                              
               DISPLAY 'CLOSE FAILED FOR BP13KD03 ' KD03-STATUS                 
               MOVE KD03-STATUS TO RETURN-CODE.                                 
                                                                                
           IF  KD65-STATUS NOT = 0                                              
               DISPLAY 'CLOSE FAILED FOR BP13KD65 ' KD65-STATUS                 
               MOVE KD65-STATUS TO RETURN-CODE.                                 
                                                                                
           IF  K140-STATUS NOT = 0                                              
               DISPLAY 'CLOSE FAILED FOR BP13K140 ' K140-STATUS                 
               MOVE K140-STATUS TO RETURN-CODE.                                 
                                                                                
           IF  KD99-STATUS NOT = 0                                              
               DISPLAY 'CLOSE FAILED FOR BP13KD99 ' KD99-STATUS                 
               MOVE KD99-STATUS TO RETURN-CODE.                                 
                                                                                
           STOP RUN.                                                            
