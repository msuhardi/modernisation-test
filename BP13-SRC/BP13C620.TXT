       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C620.                                                  
      *DATE-WRITTEN. 23 AUG 2016.                                               
      *AUTHOR.       KRISTINE LANTIN.                                           
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE: 1) TO GENERATE STAMP FEE REFUND FILE TO BE SENT*            
      *                TO IRAS.                                    *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP136262 20160823 KVL1  NEW PROGRAM                        *            
      * BP136536 20161208 KVL1  FOR NO CASES TO SEND TO IRAS TO    *            
      *                         CREATE HEADER AND TRAILER WITH 0   *            
      *                         DETAILS                            *            
      * BP138916 20230426 ZDD1  FOR NON NUMEROC AMT-REFUND-CASH    *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *-------------------------------------------------------------            
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F62A  ASSIGN        TO BP13F62A.                          
                                                                                
           SELECT BP13F62H  ASSIGN        TO BP13F62H.                          
                                                                                
           SELECT BP13K62E  ASSIGN        TO BP13K62E                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS K62E-KEY-FLD                       
                            FILE STATUS   IS BP13K62E-STATUS.                   
                                                                                
           SELECT BP13K62A  ASSIGN        TO BP13K62A                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS K62A-KEY-FLD                       
                            FILE STATUS   IS BP13K62A-STATUS.                   
                                                                                
           SELECT BP13F620  ASSIGN        TO BP13F620.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F62A                                                             
           RECORD CONTAINS 1200 CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       COPY BP13F62A.                                                           
                                                                                
       FD  BP13F62H                                                             
           RECORD CONTAINS 50   CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       01  BP13F62H-REC.                                                        
           05 F62H-LABEL        PIC X(07).                                      
           05 F62H-COUNT        PIC 9(06).                                      
           05 FILLER            PIC X(37).                                      
                                                                                
       FD  BP13K62E                                                             
           RECORD CONTAINS 1200 CHARACTERS.                                     
       COPY BP13K62E.                                                           
                                                                                
       FD  BP13K62A                                                             
           RECORD CONTAINS 1200 CHARACTERS.                                     
       COPY BP13K62A.                                                           
                                                                                
       FD  BP13F620                                                             
           RECORD CONTAINS 415  CHARACTERS                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F620.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  BP13K62E-STATUS     PIC 9(02)  VALUE ZEROES.                         
       01  BP13K62A-STATUS     PIC 9(02)  VALUE ZEROES.                         
       01  WS-READ-F62A-CNT    PIC 9(06)  VALUE ZEROES.                         
       01  WS-READ-K62E-CNT    PIC 9(06)  VALUE ZEROES.                         
       01  WS-WRITE-CNT        PIC 9(06)  VALUE ZEROES.                         
       01  WS-COUNT            PIC 9(06)  VALUE ZEROES.                         
       01  WS-TOTAL-STAMP-DUTY PIC 9(13)V99 COMP-3                              
                                          VALUE ZEROES.                         
       01  WS-TOTAL-CPF-REFUND PIC 9(13)V99 COMP-3                              
                                          VALUE ZEROES.                         
       01  WS-STAMP-DUTY       PIC 9(13)V99 COMP-3                              
                                          VALUE ZEROES.                         
       01  WS-CPF-REFUND       PIC 9(13)V99 COMP-3                              
                                          VALUE ZEROES.                         
       01  WS-BP13F62A-EOF     PIC X      VALUE 'N'.                            
       01  WS-BP13K62E-EOF     PIC X      VALUE 'N'.                            
       01  WS-DOC-TYPE         PIC X(05)  VALUE 'ALEO1'.                        
       01  WS-CURR-DATE.                                                        
           05 WS-CURRENT-DATE  PIC 9(08)  VALUE ZEROES.                         
           05 WS-CURRENT-TIME  PIC 9(08)  VALUE ZEROES.                         
           05 WS-DIFF-FR-GMT   PIC S9(04) VALUE ZEROES.                         
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-LOGIC.                                                         
      *-------------------------------------------------------------            
                                                                                
           MOVE FUNCTION CURRENT-DATE       TO   WS-CURR-DATE.                  
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F62A       THRU 2000-EXIT.                     
           IF WS-BP13F62A-EOF = 'Y'                                             
              DISPLAY 'FILE BP13F62A IS  EMPTY'                                 
              MOVE 0                        TO   WS-COUNT                       
              PERFORM 3100-CREATE-HEADER    THRU 3100-EXIT                      
              MOVE SPACES                   TO   BP13F620-TRAILER               
              INITIALIZE                         BP13F620-TRAILER               
              MOVE ZEROES                   TO   WS-WRITE-CNT                   
                                                 WS-TOTAL-STAMP-DUTY            
                                                 WS-TOTAL-CPF-REFUND            
              PERFORM 3400-CREATE-TRAILER   THRU 3400-EXIT                      
           ELSE                                                                 
              PERFORM 2100-READ-BP13F62H    THRU 2100-EXIT                      
              MOVE SPACES                   TO   BP13F620-HEADER                
              INITIALIZE                         BP13F620-HEADER                
              PERFORM 3100-CREATE-HEADER    THRU 3100-EXIT                      
              PERFORM 3000-PROCESS-DATA     THRU 3000-EXIT                      
                UNTIL WS-BP13F62A-EOF = 'Y'                                     
              MOVE SPACES                   TO   BP13F620-TRAILER               
              INITIALIZE                         BP13F620-TRAILER               
              PERFORM 3400-CREATE-TRAILER   THRU 3400-EXIT                      
           END-IF                                                               
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F62A                                                 
                       BP13F62H                                                 
                       BP13K62E                                                 
                I-O    BP13K62A                                                 
                OUTPUT BP13F620.                                                
                                                                                
           IF BP13K62E-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K62E OPEN ERROR, STATUS : ' BP13K62E-STATUS          
              MOVE     BP13K62E-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BP13K62A-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K62A OPEN ERROR, STATUS : ' BP13K62A-STATUS          
              MOVE     BP13K62A-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F62A.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F62A AT END                                                 
                         MOVE 'Y'  TO WS-BP13F62A-EOF                           
                         NOT AT END                                             
                         ADD  1    TO WS-READ-F62A-CNT                          
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2100-READ-BP13F62H.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F62H NOT AT END                                             
                         MOVE F62H-COUNT TO WS-COUNT                            
           END-READ.                                                            
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2200-STARTBR-BP13K62E.                                                   
      *-------------------------------------------------------------            
                                                                                
           START BP13K62E KEY >= K62E-KEY-FLD.                                  
                                                                                
           EVALUATE BP13K62E-STATUS                                             
              WHEN 00                                                           
                 PERFORM 2210-READNXT-BP13K62E THRU 2210-EXIT                   
                   UNTIL WS-BP13K62E-EOF = 'Y'                                  
              WHEN 10                                                           
              WHEN 23                                                           
                 MOVE 'Y'                      TO WS-BP13K62E-EOF               
              WHEN OTHER                                                        
                 DISPLAY 'ERROR STR BRW BP13K62E. STATUS IS '                   
                          BP13K62E-STATUS                                       
                 MOVE BP13K62E-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2210-READNXT-BP13K62E.                                                   
      *-------------------------------------------------------------            
                                                                                
           READ BP13K62E NEXT RECORD.                                           
                                                                                
           EVALUATE BP13K62E-STATUS                                             
            WHEN 00                                                             
                 IF K62E-NUM-REGN          = F62A-NUM-REGN                      
                    IF K62E-NUM-DOC-DTLS   = F62A-STAMP-DOC-TYPE                
                       ADD 1                      TO   WS-READ-K62E-CNT         
                       MOVE SPACES                TO   BP13F620-PARTY           
                       PERFORM 3300-CREATE-PARTY  THRU 3300-EXIT                
                    END-IF                                                      
                 ELSE                                                           
                    MOVE 'Y'                      TO WS-BP13K62E-EOF            
                 END-IF                                                         
            WHEN 10                                                             
            WHEN 23                                                             
                 MOVE 'Y'                         TO WS-BP13K62E-EOF            
            WHEN OTHER                                                          
                 DISPLAY 'ERROR READNXT BP13K62E. STATUS IS '                   
                            BP13K62E-STATUS                                     
                 MOVE BP13K62E-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       2210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2300-READ-BP13K62A.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K62A.                                                       
                                                                                
           EVALUATE BP13K62A-STATUS                                             
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN 10                                                           
              WHEN 23                                                           
                 DISPLAY 'RECORD NOT FOUND IN BP13K62A. KEY IS '                
                          F62A-KEY-FLD                                          
                 MOVE BP13K62A-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BP13K62A. STATUS IS '                   
                          BP13K62A-STATUS                                       
                 MOVE BP13K62A-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2400-REWRITE-BP13K62A.                                                   
      *-------------------------------------------------------------            
                                                                                
           REWRITE BP13K62A-REC.                                                
                                                                                
           EVALUATE BP13K62A-STATUS                                             
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR REWRITING BP13K62A FOR KEY '                    
                          F62A-KEY-FLD                                          
                 MOVE BP13K62A-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       2400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                   TO   BP13F620-DETAIL.                  
           INITIALIZE                         BP13F620-DETAIL.                  
           PERFORM 3200-CREATE-DETAIL    THRU 3200-EXIT.                        
      *    MOVE SPACES                   TO   BP13F620-REC.                     
      *    INITIALIZE                         BP13F620-REC.                     
           MOVE 'N'                      TO   WS-BP13K62E-EOF.                  
           MOVE SPACES                   TO   K62E-KEY-FLD.                     
           MOVE F62A-NUM-REGN            TO   K62E-NUM-REGN.                    
           PERFORM 2200-STARTBR-BP13K62E THRU 2200-EXIT                         
             UNTIL WS-BP13K62E-EOF = 'Y'.                                       
           MOVE SPACES                   TO   K62A-KEY-FLD.                     
           MOVE F62A-KEY-FLD             TO   K62A-KEY-FLD.                     
           PERFORM 2300-READ-BP13K62A    THRU 2300-EXIT.                        
           MOVE WS-CURRENT-DATE          TO   K62A-DTE-SENT-TO-IRAS.            
           PERFORM 2400-REWRITE-BP13K62A THRU 2400-EXIT.                        
           PERFORM 2000-READ-BP13F62A    THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-CREATE-HEADER.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE 'HEAD'                 TO F620-TABLE-ID-HEADER.                 
           MOVE WS-CURRENT-DATE        TO F620-DTE-FILE-SENT.                   
           MOVE WS-COUNT               TO F620-NUM-DOCS.                        
           WRITE BP13F620-HEADER.                                               
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3200-CREATE-DETAIL.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE 'DETAIL'               TO F620-TABLE-ID-DETAIL                  
           MOVE F62A-NUM-IRAS-DOC-REF-K625                                      
                                       TO F620-NUM-IRAS-DOC-REF-K625.           
           MOVE F62A-NUM-HDB-REF       TO F620-NUM-HDB-REF.                     
           MOVE F62A-NUM-REGN          TO F620-NUM-REGN.                        
           MOVE F62A-DTE-DOC-PREPARE-K625                                       
                                       TO F620-DTE-DOC-PREPARE-K625.            
           MOVE F62A-DTE-CANCELLATION  TO F620-DTE-CANCELLATION.                
           MOVE F62A-AMT-STAMP-DUTY    TO F620-AMT-STAMP-DUTY                   
                                          WS-STAMP-DUTY.                        
           MOVE F62A-AMT-TOTAL-REFUND-CPF                                       
                                       TO F620-AMT-TOTAL-REFUND-CPF             
                                          WS-CPF-REFUND.                        
           MOVE F62A-IND-DECLARATN     TO F620-IND-DECLARATN.                   
           MOVE F62A-IND-REFUND-TYPE   TO F620-IND-REFUND-TYPE.                 
           MOVE F62A-IND-CHEQUE        TO F620-IND-CHEQUE.                      
           WRITE BP13F620-DETAIL.                                               
           ADD 1                       TO WS-WRITE-CNT.                         
           COMPUTE WS-TOTAL-STAMP-DUTY =  WS-TOTAL-STAMP-DUTY +                 
                                          WS-STAMP-DUTY.                        
           COMPUTE WS-TOTAL-CPF-REFUND =  WS-TOTAL-CPF-REFUND +                 
                                          WS-CPF-REFUND.                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3300-CREATE-PARTY.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE 'PARTY'                TO F620-TABLE-ID-PARTY.                  
           MOVE K62E-PARTY-NAME        TO F620-PARTY-NAME.                      
           MOVE K62E-PARTY-TYPE        TO F620-PARTY-TYPE.                      
           MOVE K62E-PARTY-ENTITY-TYPE TO F620-PARTY-ENTITY-TYPE.               
           MOVE K62E-PARTY-ENTITY-ID   TO F620-PARTY-ENTITY-ID.                 
           MOVE K62E-PARTY-ADDR-TYPE   TO F620-PARTY-ADDR-TYPE.                 
           MOVE K62E-STREET-NME        TO F620-STREET-NME.                      
           MOVE K62E-NUM-BLK           TO F620-NUM-BLK                          
           MOVE K62E-NUM-LVL           TO F620-NUM-LVL                          
           MOVE K62E-NUM-UNIT          TO F620-NUM-UNIT                         
           MOVE K62E-BLDG-NME          TO F620-BLDG-NME                         
           MOVE K62E-CDE-POSTAL        TO F620-CDE-POSTAL                       
           MOVE K62E-ADDR-LINE1        TO F620-ADDR-LINE1                       
           MOVE K62E-ADDR-LINE2        TO F620-ADDR-LINE2                       
           MOVE K62E-ADDR-LINE3        TO F620-ADDR-LINE3                       
           MOVE K62E-ADDR-LINE4        TO F620-ADDR-LINE4                       
           MOVE K62E-IND-DECEASED      TO F620-IND-DECEASED                     
           MOVE K62E-AMT-REFUND-CPF    TO F620-AMT-REFUND-CPF                   
           IF   K62E-AMT-REFUND-CASH IS NOT NUMERIC                             
           MOVE ZEROES                 TO F620-AMT-REFUND-CASH                  
           ELSE                                                                 
           MOVE K62E-AMT-REFUND-CASH   TO F620-AMT-REFUND-CASH                  
           END-IF                                                               
           WRITE BP13F620-PARTY.                                                
           ADD 1                       TO WS-WRITE-CNT.                         
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3400-CREATE-TRAILER.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE 'TRAIL'             TO F620-TABLE-ID-TRAILER.                   
           MOVE WS-WRITE-CNT        TO F620-NUM-TOTAL-ROWS.                     
           MOVE WS-TOTAL-STAMP-DUTY TO F620-AMT-GRAND-TOT-STAMP-DUTY.           
           MOVE WS-TOTAL-CPF-REFUND TO F620-AMT-GRAND-TOT-REFUND-CPF.           
           WRITE BP13F620-TRAILER.                                              
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '---- BP13CZZZ CONTROL TOTAL -----'.                         
           DISPLAY ' NO OF REC READ FROM BP13F62A   = '                         
                     WS-READ-F62A-CNT.                                          
           DISPLAY ' NO OF REC READ FROM BP13K62E   = '                         
                     WS-READ-K62E-CNT.                                          
           DISPLAY ' NO OF ROWS WRITTEN TO BP13F620 = '                         
                     WS-WRITE-CNT.                                              
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE       BP13F62A                                                 
                       BP13F62H                                                 
                       BP13K62A                                                 
                       BP13K62E                                                 
                       BP13F620.                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
