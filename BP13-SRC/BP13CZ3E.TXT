       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CZ3E.                                                 
       AUTHOR.        GURU..                                                    
      *DATE-WRITTEN.  13/05/2019.                                               
                                                                                
      *===============================================================*         
      *===         HDB SYSTEM OF COMMITMENT (BP13)                 ===*         
      *===============================================================*         
      *                                                               *         
      * OBJECTIVES : CANCELLATION OF OTHER APPICATIONS FOR BOOKED FLAT*         
      *              CASES.                                           *         
      *                                                               *         
      *===============================================================*         
      * MODIFICATIONS :                                               *         
      *===============================================================*         
      * CHGE-REQ    DATE    BY    REMARKS                             *         
      * --------------------------------------------------------------*         
      * BP137718  13052019  KR13  NEW PROGRAM.                        *         
      * BP137918  06082019  KR13  HANDLE DUP WHEN WRITING K840.       *         
      * BP138716  26042021  FP6   CREATE BP13F915 FOR CANCELLATION    *         
      * BP138837  03082021  SMP3  RECTIFY THE PROBLEM DURING UPDATE   *         
      *                           OF BOOK STATUS = 'NE'               *         
      *****************************************************************         
                                                                                
      *--------------------*                                                    
       ENVIRONMENT DIVISION.                                                    
      *--------------------*                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F310  ASSIGN       TO BP13F310.                           
           SELECT BP13F915  ASSIGN       TO BP13F915.                           
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS BP13K800-STATUS.                    
                                                                                
           SELECT BP13K820  ASSIGN       TO BP13K820                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K820-KEY-FLD                        
                    ALTERNATE RECORD KEY IS K820-NUM-NRIC                       
                                            WITH DUPLICATES                     
                            FILE STATUS  IS BP13K820-STATUS.                    
                                                                                
           SELECT BP13K840  ASSIGN       TO BP13K840                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS RANDOM                              
                            RECORD KEY   IS K840-KEY-FLD                        
                            FILE STATUS  IS BP13K840-STATUS.                    
                                                                                
           SELECT BP13K848  ASSIGN       TO BP13K848                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K848-KEY-FLD                        
                            FILE STATUS  IS BP13K848-STATUS.                    
                                                                                
           SELECT BP13K595  ASSIGN       TO BP13K595                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K595-KEY-FLD                        
                            FILE STATUS  IS BP13K595-STATUS.                    
                                                                                
      *--------------*                                                          
       DATA DIVISION.                                                           
      *--------------*                                                          
       FILE SECTION.                                                            
                                                                                
       FD   BP13F310.                                                           
       COPY BP13F310.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K840                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K840.                                                           
                                                                                
       FD   BP13K848                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K848.                                                           
                                                                                
       FD   BP13K595                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K595.                                                           
                                                                                
       FD  BP13F915                                                             
           RECORD CONTAINS 800 CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORD IS STANDARD                                             
           RECORDING MODE IS F.                                                 
       COPY BP13F915.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
                                                                                
       01 WS-FILE-STATUS.                                                       
          05 BP13K800-STATUS               PIC 9(02) VALUE ZEROES.              
          05 BP13K820-STATUS               PIC 9(02) VALUE ZEROES.              
          05 BP13K840-STATUS               PIC 9(02) VALUE ZEROES.              
          05 BP13K848-STATUS               PIC 9(02) VALUE ZEROES.              
          05 BP13K595-STATUS               PIC 9(02) VALUE ZEROES.              
                                                                                
       01 WS-REC-CNTR.                                                          
          05 WS-F310-READ                  PIC 9(08) VALUE ZERO.                
          05 WS-K800-READ                  PIC 9(08) VALUE ZERO.                
          05 WS-K820-READ                  PIC 9(08) VALUE ZERO.                
          05 WS-K840-READ                  PIC 9(08) VALUE ZERO.                
          05 WS-K595-READ                  PIC 9(08) VALUE ZERO.                
          05 WS-K800-UPDT                  PIC 9(08) VALUE ZERO.                
          05 WS-K840-WRITE                 PIC 9(08) VALUE ZERO.                
          05 WS-K840-UPDT                  PIC 9(08) VALUE ZERO.                
          05 WS-K595-UPDT                  PIC 9(08) VALUE ZERO.                
          05 WS-CTR                        PIC 9(02) VALUE ZERO.                
          05 WS-CNT                        PIC 9(02) VALUE ZERO.                
          05 WS-F915-WRT                   PIC 9(6)  VALUE ZEROS.               
          05 WS-K820-FND                   PIC 9(06) VALUE ZEROES.              
          05 WS-K820-NOTFND                PIC 9(06) VALUE ZEROES.              
                                                                                
                                                                                
       01 WS-TAG.                                                               
          05 WS-K800-FOUND                 PIC X(01) VALUE 'N'.                 
          05 WS-K800-UPDTE                 PIC X(01) VALUE 'N'.                 
          05 WS-K820-FOUND                 PIC X(01) VALUE 'N'.                 
          05 WS-K840-FOUND                 PIC X(01) VALUE 'N'.                 
          05 WS-K595-FOUND                 PIC X(01) VALUE 'N'.                 
          05 WS-F310-EOF                   PIC X(01) VALUE 'N'.                 
          05 WS-K800-EOF                   PIC X(01) VALUE 'N'.                 
          05 WS-K820-EOF                   PIC X(01) VALUE 'N'.                 
          05 WS-K840-EOF                   PIC X(01) VALUE 'N'.                 
          05 WS-K595-EOF                   PIC X(01) VALUE 'N'.                 
          05 WS-FOUND                      PIC X(01) VALUE 'N'.                 
          05 WS-K820-NTFND                 PIC X(01) VALUE 'N'.                 
          05 WS-REC-PROCESS                PIC X(01) VALUE 'N'.                 
                                                                                
       01  REGN-TABLE.                                                          
           05  REGN-LOOP OCCURS 40 TIMES INDEXED BY WS-REG-PTR.                 
               10  WS-REGN-L               PIC X(8).                            
                                                                                
       01  WS-DATE-TIME.                                                        
           05  WS-CURR-DATE                PIC X(8) VALUE SPACES.               
           05  WS-CURR-TIME                PIC X(4) VALUE SPACES.               
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-DATE                 PIC X(08) VALUE SPACES.              
           05  FILLER                      PIC X(13) VALUE SPACES.              
                                                                                
       01  WS-SYSTEM-TIME.                                                      
           05 WS-HH                        PIC 9(2).                            
           05 WS-MM                        PIC 9(2).                            
           05 WS-SS                        PIC 9(2).                            
           05 WS-MI                        PIC 9(2).                            
                                                                                
       01 WS-VARIABLES.                                                         
           05  WS-REGN                     PIC X(08) VALUE SPACES.              
           05  WS-NUM-NRIC                 PIC X(09) VALUE SPACES.              
           05  WS-NRIC1                    PIC X(09) VALUE SPACES.              
           05  WS-NRIC2                    PIC X(09) VALUE SPACES.              
           05  WS-NRIC3                    PIC X(09) VALUE SPACES.              
           05  WS-NRIC4                    PIC X(09) VALUE SPACES.              
           05  WS-FOUND-SW                 PIC X(01) VALUE 'N'.                 
                                                                                
      *==================*                                                      
       PROCEDURE DIVISION.                                                      
      *==================*                                                      
                                                                                
      *----------------------------------------------------------------*        
       0000-MAIN.                                                               
      *----------------------------------------------------------------*        
                                                                                
           PERFORM 1000-INITIALIZE          THRU 1000-EXIT.                     
                                                                                
           PERFORM 2000-READ-BP13F310       THRU 2000-EXIT.                     
                                                                                
           PERFORM 3000-PROCESS-PARA        THRU 3000-EXIT                      
             UNTIL WS-F310-EOF = 'Y'.                                           
                                                                                
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       1000-INITIALIZE.                                                         
      *----------------------------------------------------------------*        
                                                                                
           OPEN INPUT  BP13F310                                                 
                       BP13K820                                                 
                  I-O  BP13K800                                                 
                       BP13K840                                                 
                       BP13K848                                                 
                       BP13K595                                                 
                OUTPUT BP13F915.                                                
                                                                                
           IF BP13K800-STATUS NOT = 00                                          
              DISPLAY 'OPEN ERROR OCCURED FOR BP13K800'                         
              DISPLAY 'FILE STATUS :' BP13K800-STATUS                           
              MOVE BP13K800-STATUS          TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF                                                               
                                                                                
           IF BP13K820-STATUS NOT = 00                                          
              DISPLAY 'OPEN ERROR OCCURED FOR BP13K820'                         
              DISPLAY 'FILE STATUS :' BP13K820-STATUS                           
              MOVE BP13K820-STATUS          TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF                                                               
                                                                                
           IF BP13K840-STATUS NOT = 00                                          
              DISPLAY 'OPEN ERROR OCCURED FOR BP13K840'                         
              DISPLAY 'FILE STATUS :' BP13K840-STATUS                           
              MOVE BP13K840-STATUS          TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF                                                               
                                                                                
           IF BP13K848-STATUS NOT = 00                                          
              DISPLAY 'OPEN ERROR OCCURED FOR BP13K848'                         
              DISPLAY 'FILE STATUS :' BP13K848-STATUS                           
              MOVE BP13K848-STATUS          TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF                                                               
                                                                                
           IF BP13K595-STATUS NOT = 00                                          
              DISPLAY 'OPEN ERROR OCCURED FOR BP13K595'                         
              DISPLAY 'FILE STATUS :' BP13K595-STATUS                           
              MOVE BP13K595-STATUS          TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF                                                               
                                                                                
           MOVE FUNCTION CURRENT-DATE       TO WS-SYSTEM-DATE.                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       2000-READ-BP13F310.                                                      
      *----------------------------------------------------------------*        
                                                                                
           READ BP13F310                                                        
           AT END                                                               
              MOVE 'Y'                      TO WS-F310-EOF                      
              GO TO 2000-EXIT                                                   
           END-READ                                                             
                                                                                
           ADD 1                            TO WS-F310-READ.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3000-PROCESS-PARA.                                                       
      *----------------------------------------------------------------*        
                                                                                
           MOVE F310-REGN-NO                TO WS-REGN                          
           MOVE SPACES                      TO REGN-TABLE.                      
           MOVE ZEROES                      TO WS-CNT.                          
                                                                                
           PERFORM 3100-READ-K800           THRU 3100-EXIT.                     
                                                                                
           IF K800-NUM-SCH-ACC NOT = SPACES                                     
                                                                                
              MOVE K800-NUM-NRIC1           TO WS-NRIC1                         
              MOVE K800-NUM-NRIC2           TO WS-NRIC2                         
              MOVE K800-NUM-NRIC3           TO WS-NRIC3                         
              MOVE K800-NUM-NRIC4           TO WS-NRIC4                         
                                                                                
              IF WS-NRIC1 NOT = SPACES AND LOW-VALUES                           
                 MOVE WS-NRIC1              TO WS-NUM-NRIC                      
                 MOVE 'N'                   TO WS-K820-EOF                      
                 PERFORM 3200-START-K820    THRU 3200-EXIT                      
                   UNTIL WS-K820-EOF = 'Y'                                      
              END-IF                                                            
                                                                                
              IF WS-NRIC2 NOT = SPACES AND LOW-VALUES                           
                 MOVE WS-NRIC2              TO WS-NUM-NRIC                      
                 MOVE 'N'                   TO WS-K820-EOF                      
                 PERFORM 3200-START-K820    THRU 3200-EXIT                      
                   UNTIL WS-K820-EOF = 'Y'                                      
              END-IF                                                            
                                                                                
              IF WS-NRIC3 NOT = SPACES AND LOW-VALUES                           
                 MOVE WS-NRIC3              TO WS-NUM-NRIC                      
                 MOVE 'N'                   TO WS-K820-EOF                      
                 PERFORM 3200-START-K820    THRU 3200-EXIT                      
                   UNTIL WS-K820-EOF = 'Y'                                      
              END-IF                                                            
                                                                                
              IF WS-NRIC4 NOT = SPACES AND LOW-VALUES                           
                 MOVE WS-NRIC4              TO WS-NUM-NRIC                      
                 MOVE 'N'                   TO WS-K820-EOF                      
                 PERFORM 3200-START-K820    THRU 3200-EXIT                      
                   UNTIL WS-K820-EOF = 'Y'                                      
              END-IF                                                            
           END-IF                                                               
                                                                                
           PERFORM 2000-READ-BP13F310       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3100-READ-K800.                                                          
      *----------------------------------------------------------------*        
                                                                                
           INITIALIZE  BP13K800-MASTER                                          
                                                                                
           MOVE 'N'                         TO WS-K800-FOUND                    
                                               WS-K800-EOF.                     
           MOVE WS-REGN                     TO K800-NUM-REGN                    
                                                                                
           READ BP13K800                                                        
                                                                                
           EVALUATE BP13K800-STATUS                                             
               WHEN 00                                                          
                    MOVE 'Y'                TO WS-K800-FOUND                    
               WHEN 23                                                          
                    DISPLAY 'BP13K800 REC NOT FOUND, KEY : '                    
                                                          K800-NUM-REGN         
               WHEN OTHER                                                       
                    DISPLAY 'ERROR STARTING BP13K800!'                          
                        ' / KEY : ' K800-NUM-REGN                               
                        ' / FILE STATUS :' BP13K800-STATUS                      
                    PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3200-START-K820.                                                         
      *----------------------------------------------------------------*        
                                                                                
           MOVE SPACES                      TO BP13K820-REC.                    
           INITIALIZE BP13K820-REC.                                             
                                                                                
           MOVE WS-NUM-NRIC                 TO K820-NUM-NRIC.                   
                                                                                
           START BP13K820 KEY >= K820-NUM-NRIC.                                 
                                                                                
           EVALUATE BP13K820-STATUS                                             
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 3250-NEXT-K820  THRU 3250-EXIT                      
                      UNTIL K820-NUM-NRIC NOT = WS-NUM-NRIC                     
                         OR WS-K820-EOF   = 'Y'                                 
               WHEN 23                                                          
                    MOVE 'Y'                TO WS-K820-EOF                      
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K820!'                           
                    DISPLAY 'K820-NUM-REGN :' K820-NUM-REGN                     
                            'FILE STATUS : ' BP13K820-STATUS                    
                    PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3250-NEXT-K820.                                                          
      *----------------------------------------------------------------*        
                                                                                
           READ BP13K820 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'                    TO WS-K820-EOF                      
           END-READ.                                                            
                                                                                
           EVALUATE BP13K820-STATUS                                             
               WHEN 00                                                          
               WHEN 02                                                          
                    IF K820-NUM-NRIC = WS-NUM-NRIC                              
                       ADD 1                TO WS-K820-READ WS-CNT              
                       PERFORM 3500-PROCESS-UPDATE THRU 3500-EXIT               
                       MOVE 'Y'             TO WS-K820-FOUND                    
                    ELSE                                                        
                       MOVE 'Y'             TO WS-K820-EOF                      
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE 'Y'                TO WS-K820-EOF                      
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K820!'                           
                     ', K820-NUM-REGN :' K820-NUM-REGN                          
                         'FILE STATUS : ' BP13K820-STATUS                       
                    PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3250-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3500-PROCESS-UPDATE.                                                     
      *----------------------------------------------------------------*        
                                                                                
           SET WS-REG-PTR TO 1                                                  
           MOVE 'N'                        TO WS-FOUND-SW                       
                                                                                
           IF WS-CNT NOT = 1                                                    
              SEARCH REGN-LOOP                                                  
                  AT END MOVE 'N'           TO WS-FOUND-SW                      
                WHEN WS-REGN-L (WS-REG-PTR) = K820-NUM-REGN                     
                         MOVE 'Y'           TO WS-FOUND-SW                      
              END-SEARCH                                                        
           END-IF                                                               
                                                                                
           IF WS-FOUND-SW = 'N'                                                 
                                                                                
              MOVE K820-NUM-REGN            TO WS-REGN WS-REGN-L(WS-CNT)        
                                                                                
              PERFORM 3510-READ-UPDT-K800   THRU 3510-EXIT                      
                                                                                
              IF WS-K800-UPDTE = 'Y'                                            
                PERFORM 3520-READ-UPDT-K840 THRU 3520-EXIT                      
                                                                                
                PERFORM 3530-READ-UPDT-K595 THRU 3530-EXIT                      
                                                                                
                PERFORM 4000-PROCESS-CDB    THRU 4000-EXIT                      
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3510-READ-UPDT-K800.                                                     
      *----------------------------------------------------------------*        
                                                                                
           MOVE 'N'                         TO WS-K800-UPDTE                    
                                                                                
           PERFORM 3100-READ-K800           THRU 3100-EXIT                      
                                                                                
           IF WS-K800-FOUND = 'Y'                                               
           AND K800-NUM-STATUS NOT = 'C'                                        
           AND K800-NUM-SCH-ACC = SPACES                                        
              ADD 1 TO WS-K800-READ                                             
              IF K800-NUM-ALLO-CAT = 'BTO' OR 'SBF' OR 'ROF' OR 'OBF'           
                                                                                
                 PERFORM 3515-WRITE-K800-IMAGE THRU 3515-EXIT                   
                                                                                
                 MOVE WS-SYS-DATE           TO K800-DTE-CANCEL                  
                                               K800-DTE-UPDATE                  
                 MOVE 'D7'                  TO K800-NUM-CANCEL-REASON           
                 MOVE 'NE'                  TO K800-NUM-ALLOC-TAG               
                 MOVE 'C'                   TO K800-NUM-STATUS                  
                 MOVE 'Y'                   TO WS-K800-UPDTE                    
                                                                                
                 REWRITE BP13K800-MASTER                                        
                                                                                
                 EVALUATE BP13K800-STATUS                                       
                     WHEN 00                                                    
                          ADD 1 TO WS-K800-UPDT                                 
                                                                                
                     WHEN OTHER                                                 
                          DISPLAY 'ERROR STARTING BP13K800!'                    
                              ' / KEY : ' K800-NUM-REGN                         
                              ' / FILE STATUS :' BP13K800-STATUS                
                          PERFORM 9000-CLOSE-FILES THRU 9000-EXIT               
                 END-EVALUATE                                                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3510-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       3515-WRITE-K800-IMAGE.                                                   
      *--------------------------------------------------------------*          
                                                                                
           MOVE BP13K800-MASTER             TO BP13K848-MASTER.                 
           MOVE 'CZ3E'                      TO K848-NUM-TRANS-BKIMAGE.          
                                                                                
           MOVE 'P13CZ3E'                   TO K848-NUM-USERID.                 
           MOVE WS-SYSTEM-DATE              TO K848-DTE-BKIMAGE.                
                                                                                
           MOVE FUNCTION CURRENT-DATE(9:8)  TO WS-SYSTEM-TIME.                  
                                                                                
           MOVE WS-SYSTEM-TIME              TO K848-TME-BKIMAGE.                
           MOVE K848-NUM-REGN               TO K848-NUM-REGN-BKIMAGE.           
                                                                                
           WRITE BP13K848-MASTER.                                               
                                                                                
           EVALUATE BP13K848-STATUS                                             
               WHEN 00                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR WRITING BP13K848. STATUS IS '                
                            BP13K848-STATUS                                     
           END-EVALUATE.                                                        
                                                                                
       3515-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3520-READ-UPDT-K840.                                                     
      *----------------------------------------------------------------*        
                                                                                
           MOVE WS-REGN                     TO K840-NUM-REGN                    
                                                                                
           READ BP13K840                                                        
                                                                                
           EVALUATE BP13K840-STATUS                                             
               WHEN 00                                                          
                    ADD 1 TO WS-K840-READ                                       
                    IF K840-DTE-CANCEL = SPACES OR ZEROES OR LOW-VALUES         
                       MOVE WS-SYS-DATE     TO K840-DTE-CANCEL                  
                                               K840-DTE-UPDATE                  
                       MOVE 'D7'            TO K840-NUM-CANCEL                  
                       MOVE 'P13CZ3E'       TO K840-NUM-USERID                  
                                                                                
                       REWRITE BP13K840-REC                                     
                                                                                
                       EVALUATE BP13K840-STATUS                                 
                           WHEN 00                                              
                                ADD 1  TO WS-K840-UPDT                          
                           WHEN OTHER                                           
                                DISPLAY 'ERROR REWRITING BP13K840!'             
                                    ' / KEY : ' K840-NUM-REGN                   
                                    ' / FILE STATUS :' BP13K840-STATUS          
                       END-EVALUATE                                             
                    END-IF                                                      
               WHEN 23                                                          
                    PERFORM 3525-WRITE-K840 THRU 3525-EXIT                      
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K840!'                           
                          ' / KEY : ' K840-NUM-REGN                             
                          ' / FILE STATUS :' BP13K840-STATUS                    
           END-EVALUATE.                                                        
                                                                                
       3520-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3525-WRITE-K840.                                                         
      *----------------------------------------------------------------*        
                                                                                
           MOVE SPACES                      TO BP13K840-REC.                    
           INITIALIZE BP13K840-REC.                                             
                                                                                
           MOVE WS-REGN                     TO K840-NUM-REGN                    
           MOVE WS-SYS-DATE                 TO K840-DTE-CANCEL                  
                                               K840-DTE-UPDATE                  
           MOVE 'D7'                        TO K840-NUM-CANCEL                  
           MOVE 'P13CZ3E'                   TO K840-NUM-USERID                  
           MOVE K800-DTE-REGN               TO K840-DTE-REGN.                   
           MOVE K800-NUM-ZONE               TO K840-NUM-ZONE.                   
           MOVE K800-NUM-FLAT-TYPE          TO K840-NUM-FLAT-TYPE.              
           MOVE K800-NUM-NEW-TOWN           TO K840-NUM-NEW-TOWN.               
           MOVE K800-NUM-IDP-SCHEME         TO K840-NUM-IDP-SCH.                
           MOVE K800-NUM-ALLO-SCHEME        TO K840-NUM-ALLOC-SCH.              
           MOVE K800-NUM-SCH-ACC            TO K840-NUM-SCH-ACC.                
                                                                                
           MOVE K800-NUM-NRIC1              TO K840-NUM-NRIC1                   
           MOVE K820-NME-OCCP               TO K840-NME-APPL1.                  
                                                                                
           IF K800-NUM-NRIC2 NOT = SPACES                                       
              MOVE K800-NUM-NRIC2           TO K840-NUM-NRIC2                   
           ELSE                                                                 
              MOVE '#'                      TO K840-NUM-NRIC2(1:1)              
              MOVE K800-NUM-REGN            TO K840-NUM-NRIC2(2:8).             
                                                                                
           IF K800-NUM-NRIC3 NOT = SPACES                                       
              MOVE K800-NUM-NRIC3           TO K840-NUM-NRIC3                   
           ELSE                                                                 
              MOVE '#'                      TO K840-NUM-NRIC3(1:1)              
              MOVE K800-NUM-REGN            TO K840-NUM-NRIC3(2:8).             
                                                                                
           IF K800-NUM-NRIC4 NOT = SPACES                                       
              MOVE K800-NUM-NRIC4           TO K840-NUM-NRIC4                   
           ELSE                                                                 
              MOVE '#'                      TO K840-NUM-NRIC4(1:1)              
              MOVE K800-NUM-REGN            TO K840-NUM-NRIC4(2:8).             
                                                                                
           WRITE BP13K840-REC.                                                  
                                                                                
           IF BP13K840-STATUS = 00                                              
              ADD 1                         TO WS-K840-WRITE                    
           ELSE                                                                 
              DISPLAY ' ERROR WRITING TO BP13K840,'                             
                      ' FOUND DUPLICATE REC, STATUS ' BP13K840-STATUS           
                      ' ACCESS KEY : ' K840-KEY-FLD                             
           END-IF.                                                              
                                                                                
       3525-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3530-READ-UPDT-K595.                                                     
      *----------------------------------------------------------------*        
                                                                                
           MOVE SPACES                      TO BP13K595-REC.                    
           INITIALIZE BP13K595-REC.                                             
                                                                                
           MOVE 'N'                         TO WS-K595-FOUND                    
                                               WS-K595-EOF.                     
                                                                                
           MOVE SPACES                      TO K595-KEY-FLD.                    
           MOVE K820-NUM-REGN               TO K595-NUM-REGN-REF.               
                                                                                
           START BP13K595 KEY >= K595-KEY-FLD                                   
                                                                                
           EVALUATE BP13K595-STATUS                                             
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 3531-READ-K595  THRU 3531-EXIT                      
                      UNTIL WS-K595-EOF = 'Y'                                   
               WHEN 23                                                          
                    DISPLAY 'BP13K595 REC NOT FOUND, KEY : '                    
                                                           K595-KEY-FLD         
               WHEN OTHER                                                       
                    DISPLAY 'ERROR LOCATING KEY IN BP13K595, '                  
                  'KEY : ' K595-KEY-FLD ',FILE STATUS :' BP13K595-STATUS        
                    MOVE BP13K595-STATUS    TO RETURN-CODE                      
                    PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       3530-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3531-READ-K595.                                                          
      *----------------------------------------------------------------*        
                                                                                
           READ BP13K595 NEXT                                                   
                                                                                
           EVALUATE BP13K595-STATUS                                             
               WHEN 00                                                          
               WHEN 02                                                          
                    IF K595-NUM-REGN-REF = K820-NUM-REGN                        
                       ADD 1 TO WS-K595-READ                                    
                       IF  K595-NUM-BOOK-STATUS = SPACES                        
                       AND K595-NUM-RANDOM  NOT = SPACES                        
                           MOVE 'NE'        TO K595-NUM-BOOK-STATUS             
                                                                                
                           REWRITE BP13K595-REC                                 
                                                                                
                           EVALUATE BP13K595-STATUS                             
                               WHEN 00                                          
                                    ADD 1 TO WS-K595-UPDT                       
                               WHEN OTHER                                       
                                    DISPLAY 'ERROR UPDATING BP13K595!'          
                                     ' , KEY : ' K595-KEY-FLD                   
                                     ' , FILE STATUS :' BP13K595-STATUS         
                                    MOVE BP13K595-STATUS  TO RETURN-CODE        
                           END-EVALUATE                                         
                       END-IF                                                   
                    ELSE                                                        
                       MOVE 'Y'             TO WS-K595-EOF                      
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE 'Y'                TO WS-K595-EOF                      
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K595!'                           
                       ' / K595-KEY-FLD :' K595-KEY-FLD                         
                       ' / FILE STATUS : ' BP13K595-STATUS                      
                    PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       3531-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       4000-PROCESS-CDB.                                                        
      *************************************************************             
           MOVE SPACES                   TO BP13K820-REC.                       
           INITIALIZE                       BP13K820-REC.                       
                                                                                
           MOVE 'N'                      TO WS-K820-EOF                         
                                            WS-K820-NTFND                       
                                            WS-REC-PROCESS.                     
                                                                                
           MOVE K800-NUM-REGN            TO K820-NUM-REGN.                      
                                                                                
           START BP13K820 KEY >= K820-KEY-FLD.                                  
                                                                                
           EVALUATE BP13K820-STATUS                                             
               WHEN 00                                                          
                    PERFORM 4100-READNEXT-BP13K820 THRU 4100-EXIT               
                      UNTIL K820-NUM-REGN NOT = K800-NUM-REGN                   
                         OR WS-K820-EOF = 'Y'                                   
                         OR WS-K820-NTFND = 'Y'                                 
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1                TO WS-K820-NOTFND                      
               WHEN OTHER                                                       
                    DISPLAY 'BP13K820 START BROWSE ERROR : '                    
                            BP13K820-STATUS                                     
                    MOVE BP13K820-STATUS  TO RETURN-CODE                        
                                                                                
                    PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       4100-READNEXT-BP13K820.                                                  
      *************************************************************             
           READ BP13K820 NEXT RECORD.                                           
                                                                                
           EVALUATE BP13K820-STATUS                                             
               WHEN 00                                                          
                    IF K820-NUM-REGN = K800-NUM-REGN                            
                       ADD 1             TO WS-K820-FND                         
                       MOVE 'Y'          TO WS-REC-PROCESS                      
                                                                                
                       PERFORM 4300-WRITE-BP13F915 THRU 4300-EXIT               
                    ELSE                                                        
                       IF WS-REC-PROCESS = 'N'                                  
                          ADD 1          TO WS-K820-NOTFND                      
                       END-IF                                                   
                                                                                
                       MOVE 'Y'          TO WS-K820-NTFND                       
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1                TO WS-K820-NOTFND                      
                    MOVE 'Y'             TO WS-K820-NTFND                       
               WHEN OTHER                                                       
                    DISPLAY 'BP13K820 READ NEXT ERROR : '                       
                        BP13K820-STATUS                                         
                    MOVE BP13K820-STATUS  TO RETURN-CODE                        
                                                                                
                    PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       4300-WRITE-BP13F915.                                                     
      *************************************************************             
           IF K820-NUM-CSTMR-SOURCE NOT = SPACES AND LOW-VALUES                 
              MOVE SPACES                   TO BP13F915-REC                     
              INITIALIZE                       BP13F915-REC                     
                                                                                
              MOVE  'TE'                    TO F915-TXN-TYPE                    
              MOVE  'P'                     TO F915-CSTMR-TYPE                  
              MOVE  'BP13'                  TO F915-NUM-UPDATE-SOURCE           
              MOVE  'BP13CZ3E'              TO F915-NUM-UPDATE-USERID           
              MOVE  K820-NUM-CSTMR-SOURCE   TO F915-NUM-CSTMR-SOURCE            
                                                                                
              IF K820-NUM-NRIC = K800-NUM-NRIC1                                 
                 MOVE '011'                 TO F915-NUM-ROLE                    
              ELSE                                                              
                 IF (K820-NUM-NRIC = K800-NUM-NRIC2) OR                         
                    (K820-NUM-NRIC = K800-NUM-NRIC3) OR                         
                    (K820-NUM-NRIC = K800-NUM-NRIC4)                            
                     MOVE '012'             TO F915-NUM-ROLE                    
                 ELSE                                                           
                     MOVE '013'             TO F915-NUM-ROLE                    
                 END-IF                                                         
              END-IF                                                            
                                                                                
              MOVE  K820-NUM-REGN           TO F915-NUM-BSNS-REF                
                                                                                
              STRING K800-DTE-CANCEL(1:4) '-'                                   
                     K800-DTE-CANCEL(5:2) '-'                                   
                     K800-DTE-CANCEL(7:2)                                       
                     DELIMITED BY SIZE                                          
                INTO F915-DTE-ROLE-END                                          
                                                                                
              STRING WS-SYS-DATE(1:4) '-'                                       
                     WS-SYS-DATE(5:2) '-'                                       
                     WS-SYS-DATE(7:2)                                           
                     DELIMITED BY SIZE                                          
                INTO F915-DTE-UPDATE-SOURCE                                     
                                                                                
              WRITE BP13F915-REC                                                
                                                                                
              ADD 1                         TO WS-F915-WRT                      
                                                                                
           END-IF.                                                              
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       9000-CLOSE-FILES.                                                        
      *----------------------------------------------------------------*        
                                                                                
           CLOSE BP13F310                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K840                                                       
                 BP13K848                                                       
                 BP13K595                                                       
                 BP13F915.                                                      
                                                                                
           IF BP13K800-STATUS NOT = 00                                          
              DISPLAY 'ERROR CLOSING FILE : BP13K800!'                          
              DISPLAY 'FILE-STATUS :' BP13K800-STATUS                           
           END-IF                                                               
                                                                                
           IF BP13K820-STATUS NOT = 00                                          
              DISPLAY 'ERROR CLOSING FILE : BP13K820!'                          
              DISPLAY 'FILE-STATUS :' BP13K820-STATUS                           
           END-IF                                                               
                                                                                
           IF BP13K840-STATUS NOT = 00                                          
              DISPLAY 'ERROR CLOSING FILE : BP13K840!'                          
              DISPLAY 'FILE-STATUS :' BP13K840-STATUS                           
           END-IF                                                               
                                                                                
           IF BP13K848-STATUS NOT = 00                                          
              DISPLAY 'ERROR CLOSING FILE : BP13K848!'                          
              DISPLAY 'FILE-STATUS :' BP13K848-STATUS                           
           END-IF                                                               
                                                                                
           IF BP13K595-STATUS NOT = 00                                          
              DISPLAY 'ERROR CLOSING FILE : BP13K595!'                          
              DISPLAY 'FILE-STATUS :' BP13K595-STATUS                           
           END-IF                                                               
                                                                                
           PERFORM 9100-DISPLAY-STATISTICS  THRU 9100-EXIT                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       9100-DISPLAY-STATISTICS.                                                 
      *----------------------------------------------------------------*        
           DISPLAY '*================================================*'         
           DISPLAY '*== PROGRAM STATISTICS - BP13CZ3E    ============*'         
           DISPLAY '*================================================*'         
           DISPLAY '*NUMBER OF F310 RECS READ    :' WS-F310-READ                
           DISPLAY '*NUMBER OF K800 RECS READ    :' WS-K800-READ                
           DISPLAY '*NUMBER OF K820 RECS READ    :' WS-K820-READ                
           DISPLAY '*NUMBER OF K840 RECS READ    :' WS-K840-READ                
           DISPLAY '*NUMBER OF K595 RECS READ    :' WS-K595-READ                
           DISPLAY '*NUMBER OF K800 RECS UPDATED :' WS-K800-UPDT                
           DISPLAY '*NUMBER OF K840 RECS UPDATED :' WS-K840-UPDT                
           DISPLAY '*NUMBER OF K840 RECS WRITTEN :' WS-K840-WRITE               
           DISPLAY '*NUMBER OF K595 RECS UPDATED :' WS-K595-UPDT                
           DISPLAY '*NUMBER OF F915 RECS WRITTEN :' WS-F915-WRT                 
           DISPLAY '*================================================*'         
           .                                                                    
       9100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********  E N D   O F   P R O G R A M   B P 1 3 C Z 3 E *********        
