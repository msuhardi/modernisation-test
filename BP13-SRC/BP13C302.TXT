      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID. BP13C302.                                                    
       AUTHOR. ACK.                                                             
      * ======================================================== *              
      *               SYSTEM OF COMMITMENT  (BP13)               *              
      * ======================================================== *              
      *    OBJECTIVE :                                           *              
      *      1.  THIS IS A PROGRAM TO UPDATE SOC MASTER &        *              
      *          OCCUPIER MASTER FROM AGREEMENT ORDER FILE       *              
      *          FOR CASES UPDATED AFTER SA.                     *              
      *      2.  CREATE RECORDS FOR REFUND CASES INTO            *              
      *          IDP-EX-APPLICANT OR SOC-EX-APPLICANT FILE.      *              
      *      3.  OUTPUT HISTORY REC                              *              
      *                                                          *              
      *    INPUT FILES :                                         *              
      *      1.  SORTED AGREEMENT ORDER FILE (BP13F300)          *              
      *          FOR CASES WHOSE TRANS TYPE = 'AA'.              *              
      *      2.  SOC MASTER (BP13K800)                           *              
      *      3.  OCCUPIER FILE (BP13K820)                        *              
      *                                                          *              
      *    OUTPUT FILES :                                        *              
      *      1.  SOC EX-APPLICANT FILE (BP13K150)                *              
      *      2.  KIV FILE (TRANS FORMAT UNSUCCCESSFUL) BP13K800  *              
      *      3.  HISTORY UPDATE FILE (BP13K840)                  *              
      *      4.  SUCCESSFUL CASES (BP13OUT)                      *              
      *                                                          *              
      * ======================================================== *              
      * CHG-NO   BY        DATE    DESCRIPTION                   *              
      * -------- --------- ------  -----------                   *              
      *          FCGALINDO 901102  REVISION ON PROCESSING OF     *              
      *                            CPF AMOUNT                    *              
      *          LMS       940104  TO WRITE EXAPPL NAME TO       *              
      *                            EXAPPL FILE(BP13K150)         *              
      * BP130020 SSS       951010  REPORITION PRT-ID ROW 1 COL 1 *              
      * BP130418 SGK       980602  TO CATER FOR Y2K.             *              
      * BP130850 AS1       010914  CREATE CDB REC TO BP13F915    *              
      * BP130850 AS1       011213  ADD REGN-NO TO BP13F915       *              
      * BP135602 JGO1      150109  ADD THE K300 UPD AMOUNTS TO   *              
      *                            K800 INSTEAD OF MOVE          *              
      * ======================================================== *              
                                                                                
      *--------------------*                                                    
       ENVIRONMENT DIVISION.                                                    
      *--------------------*                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K800-NUM-REGN                                   
                  ACCESS MODE IS RANDOM                                         
                  FILE STATUS IS WS-K800-FILE-STATUS.                           
                                                                                
           SELECT BP13K150 ASSIGN TO BP13K150                                   
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K150-KEY-FLD                                    
                  ACCESS MODE IS RANDOM                                         
                  FILE STATUS IS WS-K150-FILE-STATUS.                           
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE IS RANDOM                                         
                  RECORD KEY IS K820-KEY-FLD                                    
                  FILE STATUS IS WS-K820-FILE-STATUS.                           
                                                                                
           SELECT BP13F300 ASSIGN TO BP13F300                                   
                  ACCESS MODE IS SEQUENTIAL.                                    
                                                                                
           SELECT BP13OUT  ASSIGN TO BP13OUT                                    
                  ACCESS MODE IS SEQUENTIAL.                                    
                                                                                
           SELECT BP13KIV  ASSIGN TO BP13KIV                                    
                  ACCESS MODE IS SEQUENTIAL.                                    
                                                                                
           SELECT BP13F915 ASSIGN TO BP13F915.                                  
                                                                                
           SELECT BP13L302 ASSIGN TO BP13L302.                                  
           EJECT                                                                
                                                                                
      *-------------*                                                           
       DATA DIVISION.                                                           
      *-------------*                                                           
                                                                                
       FILE SECTION.                                                            
       FD  BP13K800                                                             
                                   BLOCK CONTAINS 0 RECORDS                     
                                   RECORD CONTAINS 2000 CHARACTERS              
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K150                                                             
                                   BLOCK CONTAINS 0 RECORDS                     
                                   RECORD CONTAINS 150 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13K150.                                                           
                                                                                
       FD  BP13K820                                                             
                                   BLOCK CONTAINS 0 RECORDS                     
                                   RECORD CONTAINS 400 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13K820.                                                           
                                                                                
       FD  BP13F300                                                             
                                   BLOCK CONTAINS 0 RECORDS                     
                                   RECORD CONTAINS 2000 CHARACTERS              
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F300.                                                           
                                                                                
       FD  BP13OUT                                                              
                                   BLOCK CONTAINS 0 RECORDS                     
                                   RECORD CONTAINS 2000 CHARACTERS              
                                   LABEL RECORDS ARE STANDARD.                  
       01  BP13OUT-REC             PIC X(2000).                                 
                                                                                
       FD  BP13KIV                                                              
                                   BLOCK CONTAINS 0 RECORDS                     
                                   RECORD CONTAINS 2000 CHARACTERS              
                                   LABEL RECORDS ARE STANDARD.                  
       01  BP13KIV-REC             PIC X(2000).                                 
                                                                                
       FD  BP13F915                                                             
                                   BLOCK CONTAINS 0 RECORDS                     
                                   RECORD CONTAINS 800 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F915.                                                           
                                                                                
       FD  BP13L302                                                             
                                   LABEL RECORDS ARE OMITTED.                   
       01  BP13L302-REC                PIC X(133).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
       77  WS-K800-FILE-STATUS     PIC 99       VALUE ZERO.                     
       77  WS-K820-FILE-STATUS     PIC 99       VALUE ZERO.                     
       77  WS-K150-FILE-STATUS     PIC 99       VALUE ZERO.                     
       77  WS-F300-INPUT           PIC 9(7)     VALUE ZERO.                     
       77  WS-K150-OUTPUT          PIC 9(7)     VALUE ZERO.                     
       77  WS-K150-REWRITE         PIC 9(7)     VALUE ZERO.                     
       77  WS-K150-WRITE           PIC 9(7)     VALUE ZERO.                     
       77  WS-K820-OUTPUT          PIC 9(7)     VALUE ZERO.                     
       77  WS-K820-REWRITE         PIC 9(7)     VALUE ZERO.                     
       77  WS-K800-OUTPUT          PIC 9(7)     VALUE ZERO.                     
       77  WS-F915-WRITE           PIC 9(7)     VALUE ZERO.                     
       77  WS-OUT-OUTPUT           PIC 9(7)     VALUE ZERO.                     
       77  WS-KIV-OUTPUT           PIC 9(7)     VALUE ZERO.                     
       77  WS-F300-NOTFND          PIC 9(7)     VALUE ZERO.                     
       77  WS-AMT-CASH             PIC S9(7)V99 VALUE ZERO.                     
       77  WS-AMT-CPF1             PIC S9(7)V99 VALUE ZERO.                     
       77  WS-AMT-CPF2             PIC S9(7)V99 VALUE ZERO.                     
       77  WS-CPF1-REQ             PIC S9(7)V99 VALUE ZERO.                     
       77  WS-CPF2-REQ             PIC S9(7)V99 VALUE ZERO.                     
       77  WS-CPF1-PAID            PIC S9(7)V99 VALUE ZERO.                     
       77  WS-CPF2-PAID            PIC S9(7)V99 VALUE ZERO.                     
       77  WS-CD-CPF1-REQ          PIC S9(7)V99 VALUE ZERO.                     
       77  WS-CD-CPF2-REQ          PIC S9(7)V99 VALUE ZERO.                     
       77  WS-DEBTOR               PIC S9(7)V99 VALUE ZERO.                     
       77  WS-AMT                  PIC S9(7)V99 VALUE ZERO.                     
       77  WS-LINE                 PIC 9(2)     VALUE 60.                       
       77  WS-PAGE                 PIC 9(3)     VALUE ZEROES.                   
       77  WS-EOF-F300             PIC X(3)     VALUE 'NO '.                    
           88  EOF-OF-F300                      VALUE 'YES'.                    
                                                                                
       01  WS-STORE-FIELDS.                                                     
           05 WS-CD                PIC S9(7)V99 VALUE +0.                       
           05 WS-CD-CASH           PIC S9(7)V99 VALUE +0.                       
           05 WS-CD-CPF1           PIC S9(7)V99 VALUE +0.                       
           05 WS-CD-CPF2           PIC S9(7)V99 VALUE +0.                       
           05 WS-AMT-REFUND        PIC S9(7)V99 VALUE +0.                       
                                                                                
      *----------------------------------------*                                
      * STORE FIELDS FOR COMPUTE-REFUND-AMOUNT *                                
      *----------------------------------------*                                
       01  WS-STORE-FOR-CRA.                                                    
           05 WS-NAME              PIC X(66)    VALUE SPACES.                   
           05 WS-NUM-CPF           PIC X(9)     VALUE SPACES.                   
           05 WS-NUM-NRIC          PIC X(9)     VALUE SPACES.                   
           05 WS-CNT               PIC 9.                                       
           05 WS-K800-AMT-CPF      PIC S9(7)V99.                                
           05 WS-F300-AMT-CPF      PIC S9(7)V99.                                
                                                                                
       01  WS-DATE.                                                             
           05 WS-CCYY              PIC X(4)    VALUE SPACES.                    
           05 WS-MM                PIC X(2)    VALUE SPACES.                    
           05 WS-DD                PIC X(2)    VALUE SPACES.                    
                                                                                
       01  WS-EDITED-DATE.                                                      
           05 WS-EDITED-DD         PIC X(2)    VALUE SPACES.                    
           05 FILLER               PIC X       VALUE '/'.                       
           05 WS-EDITED-MM         PIC X(2)    VALUE SPACES.                    
           05 FILLER               PIC X       VALUE '/'.                       
           05 WS-EDITED-CCYY       PIC X(4)    VALUE SPACES.                    
                                                                                
       01  WS-CURR-DATE.                                                        
              10 WS-CURR-CCYY      PIC X(4)    VALUE SPACES.                    
              10 WS-CURR-MM        PIC X(2)    VALUE SPACES.                    
              10 WS-CURR-DD        PIC X(2)    VALUE SPACES.                    
                                                                                
       01  PRINT-ERR1.                                                          
           05 FILLER               PIC X(10)   VALUE 'BP13-L302'.               
           05 FILLER               PIC X(11)   VALUE SPACES.                    
           05 FILLER               PIC X(79)   VALUE                            
              'ERROR LIST FOR UPDATING SOC MASTER & OCCUPIER MASTER'.           
           05 FILLER               PIC X(7)    VALUE 'PAGE : '.                 
           05 PRINT-PAGE           PIC ZZZ9.                                    
           05 FILLER               PIC X(2)    VALUE SPACES.                    
           05 FILLER               PIC X(7)    VALUE 'DATE : '.                 
           05 PRINT-DATE1          PIC X(10)   VALUE SPACES.                    
                                                                                
       01  PRINT-ERR2.                                                          
           05 FILLER               PIC X(2)    VALUE SPACES.                    
           05 FILLER               PIC X(7)    VALUE 'REGN NO'.                 
           05 FILLER               PIC X(5)    VALUE SPACES.                    
           05 FILLER               PIC X(10)   VALUE 'REMARKS'.                 
                                                                                
       01  PRINT-ERR3.                                                          
           05 FILLER               PIC X(2)    VALUE SPACES.                    
           05 PRT-NUM-REGN         PIC X(8)    VALUE SPACES.                    
           05 FILLER               PIC X(4)    VALUE SPACES.                    
           05 PRT-REMARKS          PIC X(60)   VALUE SPACES.                    
                                                                                
       01  PRINT-HDG1.                                                          
           05 FILLER               PIC X(8)    VALUE 'BP13L302'.                
           05 FILLER               PIC X(13)   VALUE SPACES.                    
           05 FILLER               PIC X(79)   VALUE                            
              'CONTROL LIST FOR UPDATING SOC MASTER & OCCUPIER MASTER'.         
           05 FILLER               PIC X(13)   VALUE SPACES.                    
           05 FILLER               PIC X(7)    VALUE 'DATE : '.                 
           05 PRINT-DATE           PIC X(10)   VALUE SPACES.                    
                                                                                
       01  PRINT-REC.                                                           
           05 FILLER               PIC X(10)   VALUE SPACES.                    
           05 PRINT-DESC           PIC X(30)   VALUE SPACES.                    
           05 PRINT-TOTAL          PIC ZZZZZZZ9.                                
                                                                                
      *------------------*                                                      
       PROCEDURE DIVISION.                                                      
      *------------------*                                                      
                                                                                
      ******************************************************************        
       0000-INITIALIZATION.                                                     
      ******************************************************************        
                                                                                
           OPEN I-O    BP13K800                                                 
                       BP13K820                                                 
                       BP13K150                                                 
                INPUT  BP13F300                                                 
                OUTPUT BP13OUT                                                  
                       BP13KIV                                                  
                       BP13F915                                                 
                       BP13L302.                                                
                                                                                
           IF WS-K800-FILE-STATUS NOT = ZEROES AND 97                           
              DISPLAY 'BP13K800 - OPEN ERROR ' WS-K800-FILE-STATUS              
              MOVE WS-K800-FILE-STATUS TO RETURN-CODE                           
              PERFORM 9999-FINALIZATION.                                        
                                                                                
           IF WS-K820-FILE-STATUS NOT = ZEROES AND 97                           
              DISPLAY 'BP13K820 - OPEN ERROR ' WS-K820-FILE-STATUS              
              MOVE WS-K820-FILE-STATUS TO RETURN-CODE                           
              PERFORM 9999-FINALIZATION.                                        
                                                                                
           IF WS-K150-FILE-STATUS NOT = ZEROES AND 97                           
              DISPLAY 'BP13K150 - OPEN ERROR ' WS-K150-FILE-STATUS              
              MOVE WS-K150-FILE-STATUS TO RETURN-CODE                           
              PERFORM 9999-FINALIZATION.                                        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO   WS-DATE.                       
           MOVE WS-CCYY        TO WS-EDITED-CCYY.                               
           MOVE WS-MM          TO WS-EDITED-MM.                                 
           MOVE WS-DD          TO WS-EDITED-DD.                                 
           MOVE WS-EDITED-CCYY TO WS-CURR-CCYY.                                 
           MOVE WS-EDITED-MM   TO WS-CURR-MM.                                   
           MOVE WS-EDITED-DD   TO WS-CURR-DD.                                   
           MOVE WS-EDITED-DATE TO PRINT-DATE.                                   
           MOVE WS-EDITED-DATE TO PRINT-DATE1.                                  
                                                                                
      ******************************************************************        
       0020-MAIN-PROCESS.                                                       
      ******************************************************************        
                                                                                
           PERFORM 0100-READ-BP13F300 THRU 0100-EXIT.                           
           PERFORM 0200-UPDATE-SOC    THRU 0200-EXIT                            
                   UNTIL EOF-OF-F300.                                           
           PERFORM 0300-PRINT-CONTROL THRU 0300-EXIT.                           
           PERFORM 9999-FINALIZATION.                                           
                                                                                
      ******************************************************************        
       0100-READ-BP13F300.                                                      
      ******************************************************************        
                                                                                
           READ BP13F300 AT END                                                 
                MOVE 'YES' TO WS-EOF-F300                                       
                GO TO 0100-EXIT.                                                
                                                                                
           ADD 1 TO WS-F300-INPUT.                                              
                                                                                
       0100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       0200-UPDATE-SOC.                                                         
      ******************************************************************        
                                                                                
      *===========================================================*             
      * 0200-UPDATE-SOC.              |                           *             
      *-----------------------------------------------------------*             
      *   2000-UPD-AMOUNT-INFO.       |                           *             
      *        2010-READ-BP13K820.    |                           *             
      *        2020-READ-K150.        |                           *             
      *   2100-UPD-LESSEE-INFO.       | UPDATE NRIC AND CPF NO    *             
      *   2200-WRITE-TO-FILE.         | WRITE AND REWRITE RECORDS *             
      *===========================================================*             
           MOVE F300-CUR-REG TO K800-NUM-REGN.                                  
           READ BP13K800 INVALID KEY                                            
                MOVE F300-CUR-REG                TO PRT-NUM-REGN                
                MOVE 'RECORD NOT IN SOC MASTER ' TO PRT-REMARKS                 
                PERFORM 2300-PRINT-ERR THRU 2300-EXIT                           
                WRITE BP13KIV-REC FROM BP13F300-REC                             
                ADD 1 TO WS-KIV-OUTPUT                                          
                PERFORM 0100-READ-BP13F300 THRU 0100-EXIT                       
                GO TO 0200-EXIT.                                                
                                                                                
           IF WS-K800-FILE-STATUS = ZEROES                                      
              PERFORM 2000-UPD-AMOUNT-INFO THRU 2000-EXIT                       
              PERFORM 2100-UPD-LESSEE-INFO THRU 2100-EXIT                       
              PERFORM 2200-WRITE-TO-FILE   THRU 2200-EXIT                       
              PERFORM 0100-READ-BP13F300   THRU 0100-EXIT                       
           ELSE                                                                 
              DISPLAY 'ERROR IN READING BP13K800' WS-K800-FILE-STATUS           
              GO TO 9999-FINALIZATION.                                          
                                                                                
       0200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       0300-PRINT-CONTROL.                                                      
      ******************************************************************        
                                                                                
           WRITE BP13L302-REC FROM PRINT-HDG1 AFTER PAGE.                       
                                                                                
           MOVE SPACES TO BP13L302-REC.                                         
           WRITE BP13L302-REC AFTER 2.                                          
                                                                                
           MOVE 'TOTAL RECORDS PROCESSED   ' TO PRINT-DESC.                     
           WRITE BP13L302-REC FROM PRINT-REC AFTER 2.                           
                                                                                
           MOVE WS-F300-INPUT  TO PRINT-TOTAL.                                  
           MOVE '      BP13F300 READ          : ' TO PRINT-DESC.                
           WRITE BP13L302-REC FROM PRINT-REC AFTER 2.                           
                                                                                
           MOVE WS-K800-OUTPUT TO PRINT-TOTAL.                                  
           MOVE '      UPDATE TO BP13K800     : ' TO PRINT-DESC.                
           WRITE BP13L302-REC FROM PRINT-REC AFTER 2.                           
                                                                                
           MOVE WS-KIV-OUTPUT  TO PRINT-TOTAL.                                  
           MOVE '      WRITE  TO BP13KIV      : ' TO PRINT-DESC.                
           WRITE BP13L302-REC FROM PRINT-REC AFTER 2.                           
                                                                                
           MOVE SPACES TO BP13L302-REC.                                         
           WRITE BP13L302-REC AFTER 2.                                          
                                                                                
           MOVE SPACES TO PRINT-REC.                                            
           MOVE 'SUB CONTROL OF RECORDS         ' TO PRINT-DESC.                
           WRITE BP13L302-REC FROM PRINT-REC AFTER 2.                           
                                                                                
           MOVE WS-K820-OUTPUT TO PRINT-TOTAL.                                  
           MOVE '      WRITE   TO BP13K820    : ' TO PRINT-DESC.                
           WRITE BP13L302-REC FROM PRINT-REC AFTER 2.                           
                                                                                
           MOVE WS-K820-REWRITE TO PRINT-TOTAL.                                 
           MOVE '      REWRITE TO BP13K820    : ' TO PRINT-DESC.                
           WRITE BP13L302-REC FROM PRINT-REC AFTER 2.                           
                                                                                
           MOVE WS-K150-WRITE  TO PRINT-TOTAL.                                  
           MOVE '      WRITE   TO BP13K150    : ' TO PRINT-DESC.                
           WRITE BP13L302-REC FROM PRINT-REC AFTER 2.                           
                                                                                
           MOVE WS-K150-REWRITE TO PRINT-TOTAL.                                 
           MOVE '      REWRITE TO BP13K150    : ' TO PRINT-DESC.                
           WRITE BP13L302-REC FROM PRINT-REC AFTER 2.                           
                                                                                
           MOVE WS-K150-OUTPUT TO PRINT-TOTAL.                                  
           MOVE '      I/O     TO BP13K150    : ' TO PRINT-DESC.                
           WRITE BP13L302-REC FROM PRINT-REC AFTER 2.                           
                                                                                
           MOVE WS-F915-WRITE  TO PRINT-TOTAL.                                  
           MOVE '      WRITE   TO BP13F915    : ' TO PRINT-DESC.                
           WRITE BP13L302-REC FROM PRINT-REC AFTER 2.                           
                                                                                
       0300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-UPD-AMOUNT-INFO.                                                    
      ******************************************************************        
                                                                                
      *    IF (F300-CPF1 = F300-PREV-CPF2 OR                                    
      *        F300-NRIC1 = F300-PREV-NRIC2 ) AND                               
      *       (F300-CPF2 = F300-PREV-CPF1 OR                                    
      *        F300-NRIC2 = F300-PREV-NRIC1 )                                   
      *        MOVE K800-AMT-CPF1-PAID   TO WS-CPF1-PAID                        
      *        MOVE K800-AMT-CPF2-PAID   TO WS-CPF2-PAID                        
      *        MOVE K800-AMT-CD-CPF1     TO WS-CD-CPF1-REQ                      
      *        MOVE K800-AMT-CD-CPF2     TO WS-CD-CPF2-REQ                      
      *        MOVE DPM-AMT-SOC-CPF1-REQ TO WS-CPF1-REQ                         
      *        MOVE DPM-AMT-SOC-CPF2-REQ TO WS-CPF2-REQ                         
      *        MOVE WS-CPF1-PAID         TO K800-AMT-CPF2-PAID                  
      *        MOVE WS-CPF2-PAID         TO K800-AMT-CPF1-PAID                  
      *        MOVE WS-CD-CPF1-REQ       TO K800-AMT-CD-CPF2                    
      *        MOVE WS-CD-CPF2-REQ       TO K800-AMT-CD-CPF1                    
      *        MOVE WS-CPF1-REQ          TO DPM-AMT-SOC-CPF2-REQ                
      *        MOVE WS-CPF2-REQ          TO DPM-AMT-SOC-CPF1-REQ.               
                                                                                
           PERFORM 2001-COMPUTE-REFUND-AMOUNT  THRU 2001-EXIT.                  
                                                                                
           IF F300-AMT-CD-REQ > K800-AMT-CD                                     
              COMPUTE WS-CD = F300-AMT-CD-REQ - K800-AMT-CD                     
              MOVE F300-AMT-CD-REQ TO K800-AMT-CD                               
              ADD WS-CD TO K800-AMT-SOC-DEBTOR                                  
              MOVE +0 TO WS-CD.                                                 
                                                                                
      * ADD UPD FIELDS INSTEAD OF MOVE                                          
           IF F300-AMT-CD-CASH-UPD IS NUMERIC                                   
              COMPUTE K800-AMT-CD-CASH = K800-AMT-CD-CASH +                     
                                         F300-AMT-CD-CASH-UPD.                  
                                                                                
           IF F300-AMT-CD-CPF1-UPD IS NUMERIC                                   
              COMPUTE K800-AMT-CD-CPF1 = K800-AMT-CD-CPF1 +                     
                                         F300-AMT-CD-CPF1-UPD.                  
                                                                                
           IF F300-AMT-CD-CPF2-UPD IS NUMERIC                                   
              COMPUTE K800-AMT-CD-CPF2 = K800-AMT-CD-CPF2 +                     
                                         F300-AMT-CD-CPF2-UPD.                  
                                                                                
           IF F300-AMT-CD-CPF3-UPD IS NUMERIC                                   
              COMPUTE K800-AMT-CD-CPF3 = K800-AMT-CD-CPF3 +                     
                                         F300-AMT-CD-CPF3-UPD.                  
                                                                                
           IF F300-AMT-CD-CPF4-UPD IS NUMERIC                                   
              COMPUTE K800-AMT-CD-CPF4 = K800-AMT-CD-CPF4 +                     
                                         F300-AMT-CD-CPF4-UPD.                  
                                                                                
           IF F300-CDE-LESSEE-CHG1 = 'D' OR 'O'                                 
              MOVE 'SOC'               TO  K150-CDE-PAYMENT-TYPE                
              MOVE F300-CUR-REG        TO  K150-NUM-REGN                        
              MOVE F300-PREV-CPF1      TO  K150-NUM-CPF                         
              MOVE FUNCTION CURRENT-DATE(1:8)                                   
                                       TO  K150-DTE-ADDED                       
              MOVE F300-SCH-ACC        TO  K150-NUM-SCH-ACCT                    
              MOVE F300-CUR-REG        TO  K820-NUM-REGN                        
              MOVE F300-PREV-NRIC1     TO  K820-NUM-NRIC                        
              PERFORM 2010-READ-BP13K820 THRU 2010-EXIT                         
              MOVE K800-AMT-CPF1-PAID  TO  WS-AMT-REFUND                        
              MOVE K820-NME-OCCP       TO  K150-NME-NAME                        
              PERFORM 2020-READ-K150 THRU 2020-EXIT.                            
                                                                                
      *-------------------------------------------*                             
      *  PREVIOUSLY ONLY ONE APPLICANT            *                             
      *-------------------------------------------*                             
           IF F300-CDE-LESSEE-CHG2 = 'D' OR 'O'                                 
              IF F300-PREV-NRIC2 = SPACES                                       
                 MOVE 'N' TO F300-CDE-LESSEE-CHG2                               
              ELSE                                                              
                 NEXT SENTENCE                                                  
           ELSE                                                                 
               NEXT SENTENCE.                                                   
                                                                                
           IF F300-CDE-LESSEE-CHG2 = 'D' OR 'O'                                 
              MOVE 'SOC'             TO  K150-CDE-PAYMENT-TYPE                  
              MOVE F300-CUR-REG      TO  K150-NUM-REGN                          
              MOVE F300-PREV-CPF2    TO  K150-NUM-CPF                           
              MOVE FUNCTION CURRENT-DATE(1:8)                                   
                                     TO  K150-DTE-ADDED                         
              MOVE F300-SCH-ACC      TO  K150-NUM-SCH-ACCT                      
              MOVE F300-CUR-REG      TO  K820-NUM-REGN                          
              MOVE F300-PREV-NRIC2   TO  K820-NUM-NRIC                          
              PERFORM 2010-READ-BP13K820 THRU 2010-EXIT                         
              MOVE K800-AMT-CPF2-PAID TO WS-AMT-REFUND                          
              MOVE K820-NME-OCCP     TO  K150-NME-NAME                          
              PERFORM 2020-READ-K150 THRU 2020-EXIT.                            
                                                                                
      *-------------------------------------------*                             
      *  PREVIOUSLY ONLY TWO APPLICANT            *                             
      *-------------------------------------------*                             
           IF  (F300-CDE-LESSEE-CHG3 = 'D' OR 'O')                              
           AND (F300-PREV-NRIC3      = SPACES)                                  
               MOVE 'N' TO F300-CDE-LESSEE-CHG3.                                
                                                                                
           IF F300-CDE-LESSEE-CHG3 = 'D' OR 'O'                                 
              MOVE 'SOC'             TO  K150-CDE-PAYMENT-TYPE                  
              MOVE F300-CUR-REG      TO  K150-NUM-REGN                          
              MOVE F300-PREV-CPF3    TO  K150-NUM-CPF                           
              MOVE FUNCTION CURRENT-DATE(1:8)                                   
                                     TO  K150-DTE-ADDED                         
              MOVE F300-SCH-ACC      TO  K150-NUM-SCH-ACCT                      
              MOVE F300-CUR-REG      TO  K820-NUM-REGN                          
              MOVE F300-PREV-NRIC3   TO  K820-NUM-NRIC                          
              PERFORM 2010-READ-BP13K820 THRU 2010-EXIT                         
              MOVE K800-AMT-CPF3-PAID TO WS-AMT-REFUND                          
              MOVE K820-NME-OCCP     TO  K150-NME-NAME                          
              PERFORM 2020-READ-K150 THRU 2020-EXIT.                            
                                                                                
      *-------------------------------------------*                             
      *  PREVIOUSLY ONLY THREE APPLICANT          *                             
      *-------------------------------------------*                             
           IF  (F300-CDE-LESSEE-CHG4 = 'D' OR 'O')                              
           AND (F300-PREV-NRIC4 = SPACES)                                       
               MOVE 'N' TO F300-CDE-LESSEE-CHG4.                                
                                                                                
           IF F300-CDE-LESSEE-CHG4 = 'D' OR 'O'                                 
              MOVE 'SOC'                TO  K150-CDE-PAYMENT-TYPE               
              MOVE F300-CUR-REG         TO  K150-NUM-REGN                       
              MOVE F300-PREV-CPF4       TO  K150-NUM-CPF                        
              MOVE FUNCTION CURRENT-DATE(1:8)                                   
                                        TO  K150-DTE-ADDED                      
              MOVE F300-SCH-ACC         TO  K150-NUM-SCH-ACCT                   
              MOVE F300-CUR-REG         TO  K820-NUM-REGN                       
              MOVE F300-PREV-NRIC4      TO  K820-NUM-NRIC                       
              PERFORM 2010-READ-BP13K820 THRU 2010-EXIT                         
              MOVE K800-AMT-CPF4-PAID   TO  WS-AMT-REFUND                       
              MOVE K820-NME-OCCP        TO  K150-NME-NAME                       
              PERFORM 2020-READ-K150   THRU 2020-EXIT.                          
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2001-COMPUTE-REFUND-AMOUNT.                                              
      ******************************************************************        
                                                                                
           MOVE K800-NUM-REGN TO K820-NUM-REGN.                                 
                                                                                
           PERFORM VARYING WS-CNT FROM 1 BY 1 UNTIL WS-CNT > 4                  
              MOVE SPACES  TO  BP13K150-REC                                     
              MOVE ZEROES  TO  K150-AMT-REFUND                                  
                                                                                
              EVALUATE WS-CNT                                                   
                WHEN 1                                                          
                     MOVE K800-NUM-NRIC1       TO K820-NUM-NRIC                 
                     MOVE F300-AMT-CD-CPF1-UPD TO WS-F300-AMT-CPF               
                     MOVE K800-AMT-CD-CPF1     TO WS-K800-AMT-CPF               
      *              MOVE F300-NAME1           TO WS-NAME                       
                WHEN 2                                                          
                     MOVE K800-NUM-NRIC2       TO K820-NUM-NRIC                 
                     MOVE F300-AMT-CD-CPF2-UPD TO WS-F300-AMT-CPF               
                     MOVE K800-AMT-CD-CPF2     TO WS-K800-AMT-CPF               
      *              MOVE F300-NAME2           TO WS-NAME                       
                WHEN 3                                                          
                     MOVE K800-NUM-NRIC3       TO K820-NUM-NRIC                 
                     MOVE F300-AMT-CD-CPF3-UPD TO WS-F300-AMT-CPF               
                     MOVE K800-AMT-CD-CPF3     TO WS-K800-AMT-CPF               
      *              MOVE F300-NAME3           TO WS-NAME                       
                WHEN 4                                                          
                     MOVE K800-NUM-NRIC4       TO K820-NUM-NRIC                 
                     MOVE F300-AMT-CD-CPF4-UPD TO WS-F300-AMT-CPF               
                     MOVE K800-AMT-CD-CPF4     TO WS-K800-AMT-CPF               
      *              MOVE F300-NAME4           TO WS-NAME                       
              END-EVALUATE                                                      
                                                                                
              PERFORM 2010-READ-BP13K820 THRU 2010-EXIT                         
                                                                                
              IF K820-NUM-CPF NOT  = SPACES AND LOW-VALUES                      
                 MOVE K820-NUM-CPF TO WS-NUM-CPF                                
              ELSE                                                              
                 MOVE SPACES TO WS-NUM-CPF                                      
              END-IF                                                            
                                                                                
              MOVE K820-NME-OCCP TO WS-NAME                                     
                                                                                
              IF WS-F300-AMT-CPF       < WS-K800-AMT-CPF                        
                 COMPUTE WS-AMT-REFUND = WS-K800-AMT-CPF                        
                                       - WS-F300-AMT-CPF                        
                 MOVE WS-NUM-CPF         TO  K150-NUM-CPF                       
                 MOVE WS-NAME            TO  K150-NME-NAME                      
                 PERFORM 2002-WRITE-TO-K150  THRU  2002-EXIT                    
              END-IF                                                            
                                                                                
           END-PERFORM.                                                         
                                                                                
       2001-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2002-WRITE-TO-K150.                                                      
      ******************************************************************        
                                                                                
           MOVE F300-CUR-REG   TO  K150-NUM-REGN.                               
                                                                                
           READ BP13K150 INVALID KEY                                            
                MOVE F300-SCH-ACC    TO  K150-NUM-SCH-ACCT                      
                MOVE WS-AMT-REFUND   TO  K150-AMT-REFUND                        
                MOVE 'SOC'           TO  K150-CDE-PAYMENT-TYPE                  
                MOVE FUNCTION CURRENT-DATE(1:8)                                 
                                     TO  K150-DTE-ADDED                         
                PERFORM 2004-WRITE-K150-REC   THRU 2004-EXIT                    
                GO TO 2002-EXIT.                                                
                                                                                
           IF WS-K150-FILE-STATUS   = ZEROES                                    
              COMPUTE WS-AMT-REFUND = WS-AMT-REFUND + K150-AMT-REFUND           
              MOVE WS-AMT-REFUND      TO K150-AMT-REFUND                        
              MOVE FUNCTION CURRENT-DATE(1:8)                                   
                                      TO  K150-DTE-ADDED                        
              MOVE 'SOC'              TO  K150-CDE-PAYMENT-TYPE                 
              PERFORM 2003-REWRITE-K150-REC    THRU 2003-EXIT                   
           ELSE                                                                 
              DISPLAY 'ERROR IN READING BP13K150 ' WS-K150-FILE-STATUS          
              GO TO 9999-FINALIZATION.                                          
                                                                                
       2002-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2003-REWRITE-K150-REC.                                                   
      ******************************************************************        
                                                                                
           REWRITE BP13K150-REC.                                                
                                                                                
           IF WS-K150-FILE-STATUS = ZEROES                                      
              ADD 1       TO WS-K150-REWRITE                                    
              ADD 1       TO WS-K150-OUTPUT                                     
              MOVE ZEROES TO WS-AMT-REFUND                                      
           ELSE                                                                 
              DISPLAY 'ERROR IN UPDATING BP13K150 ' WS-K150-FILE-STATUS         
              DISPLAY 'BP13K150 KEY FLD  2003    ' K150-KEY-FLD                 
              GO TO 9999-FINALIZATION.                                          
                                                                                
       2003-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2004-WRITE-K150-REC.                                                     
      ******************************************************************        
                                                                                
           WRITE BP13K150-REC.                                                  
                                                                                
           IF WS-K150-FILE-STATUS = ZEROES                                      
              ADD 1       TO WS-K150-WRITE                                      
              ADD 1       TO WS-K150-OUTPUT                                     
              MOVE ZEROES TO WS-AMT-REFUND                                      
           ELSE                                                                 
              DISPLAY 'ERROR IN UPDATING BP13K150 ' WS-K150-FILE-STATUS         
              DISPLAY 'BP13K150 KEY FLD  2004    ' K150-KEY-FLD                 
              GO TO 9999-FINALIZATION.                                          
                                                                                
       2004-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2010-READ-BP13K820.                                                      
      ******************************************************************        
                                                                                
           READ BP13K820 INVALID KEY                                            
                GO TO 2010-EXIT.                                                
                                                                                
           IF WS-K820-FILE-STATUS = ZEROES                                      
              NEXT SENTENCE                                                     
           ELSE                                                                 
              DISPLAY 'ERROR IN READING BP13K820 ' WS-K820-FILE-STATUS          
              GO TO 9999-FINALIZATION.                                          
                                                                                
       2010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2020-READ-K150.                                                          
      ******************************************************************        
                                                                                
           READ BP13K150 INVALID KEY                                            
                PERFORM 2122-CHECK-SOC-AMT THRU 2122-EXIT                       
                GO TO 2020-EXIT.                                                
                                                                                
           IF WS-K150-FILE-STATUS = 00                                          
              PERFORM 2121-CHECK-LESSEE THRU 2121-EXIT                          
           ELSE                                                                 
              DISPLAY 'ERROR IN READIN BP13K150 ' WS-K150-FILE-STATUS           
              GO TO 9999-FINALIZATION.                                          
                                                                                
       2020-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2121-CHECK-LESSEE.                                                       
      ******************************************************************        
                                                                                
              COMPUTE WS-AMT-REFUND = K800-AMT-CPF1-PAID +                      
                                      K150-AMT-REFUND                           
              MOVE WS-AMT-REFUND  TO  K150-AMT-REFUND                           
              PERFORM 2003-REWRITE-K150-REC THRU 2003-EXIT                      
              MOVE ZEROES TO WS-AMT-REFUND.                                     
                                                                                
       2121-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2122-CHECK-SOC-AMT.                                                      
      ******************************************************************        
                                                                                
              MOVE WS-AMT-REFUND TO K150-AMT-REFUND                             
              DISPLAY '2122 LESSEE  ' K150-KEY-FLD                              
              PERFORM 2004-WRITE-K150-REC THRU 2004-EXIT                        
              MOVE 0 TO WS-AMT-REFUND.                                          
                                                                                
       2122-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2100-UPD-LESSEE-INFO.                                                    
      ******************************************************************        
                                                                                
           IF F300-CDE-SUB-TRANSFER NOT = K800-NUM-SUB-TRANSFER                 
              IF F300-CDE-SUB-TRANSFER = 'Y' OR 'N'                             
                 MOVE F300-CDE-SUB-TRANSFER TO K800-NUM-SUB-TRANSFER            
              ELSE                                                              
                 NEXT SENTENCE.                                                 
                                                                                
           IF F300-NRIC1 NOT  =  K800-NUM-NRIC1                                 
              MOVE F300-NRIC1 TO K800-NUM-NRIC1.                                
                                                                                
           IF F300-NRIC2 NOT  =  K800-NUM-NRIC2                                 
              MOVE F300-NRIC2 TO K800-NUM-NRIC2.                                
                                                                                
           IF F300-NRIC3 NOT  =  K800-NUM-NRIC3                                 
              MOVE F300-NRIC3 TO K800-NUM-NRIC3.                                
                                                                                
           IF F300-NRIC4 NOT  =  K800-NUM-NRIC4                                 
              MOVE F300-NRIC4 TO K800-NUM-NRIC4.                                
                                                                                
           IF F300-IDP-SCH NOT    =  K800-NUM-IDP-SCHEME                        
              MOVE F300-IDP-SCH   TO K800-NUM-IDP-SCHEME.                       
                                                                                
           IF F300-ELIG-SCH NOT   =  K800-NUM-ELIG-SCHEME                       
              MOVE F300-ELIG-SCH  TO K800-NUM-ELIG-SCHEME.                      
                                                                                
           IF F300-ALLOC-SCH NOT  =  K800-NUM-ALLO-SCHEME                       
              MOVE F300-ALLOC-SCH TO K800-NUM-ALLO-SCHEME.                      
                                                                                
           IF F300-PR-TAG1 NOT  =  K800-NUM-APPL1-PR                            
              MOVE F300-PR-TAG1 TO K800-NUM-APPL1-PR.                           
                                                                                
           IF F300-PR-TAG2 NOT  =  K800-NUM-APPL2-PR                            
              MOVE F300-PR-TAG2 TO K800-NUM-APPL2-PR.                           
                                                                                
           IF F300-PR-TAG3 NOT  =  K800-NUM-APPL3-PR                            
              MOVE F300-PR-TAG3 TO K800-NUM-APPL3-PR.                           
                                                                                
           IF F300-PR-TAG4 NOT  =  K800-NUM-APPL4-PR                            
              MOVE F300-PR-TAG4 TO K800-NUM-APPL4-PR.                           
                                                                                
           IF F300-CPF4 NOT = SPACES AND LOW-VALUE                              
              MOVE 4 TO K800-NUM-JT-APPL                                        
           ELSE                                                                 
              IF F300-CPF3 NOT = SPACES AND LOW-VALUE                           
                 MOVE 3 TO K800-NUM-JT-APPL                                     
              ELSE                                                              
                 IF F300-CPF2 NOT = SPACES AND LOW-VALUE                        
                    MOVE 2 TO K800-NUM-JT-APPL                                  
                 ELSE                                                           
                    MOVE 1 TO K800-NUM-JT-APPL.                                 
                                                                                
           MOVE F300-CUR-REG     TO K800-NUM-REGN                               
                                                                                
           PERFORM VARYING WS-CNT FROM 1 BY 1 UNTIL WS-CNT > 4                  
              EVALUATE WS-CNT                                                   
                 WHEN 1                                                         
                      MOVE F300-NRIC1               TO WS-NUM-NRIC              
                      MOVE F300-NAME1               TO WS-NAME                  
                 WHEN 2                                                         
                      MOVE F300-NRIC2               TO WS-NUM-NRIC              
                      MOVE F300-NAME2               TO WS-NAME                  
                 WHEN 3                                                         
                      MOVE F300-NRIC3               TO WS-NUM-NRIC              
                      MOVE F300-NAME3               TO WS-NAME                  
                 WHEN 4                                                         
                      MOVE F300-NRIC4               TO WS-NUM-NRIC              
                      MOVE F300-NAME4               TO WS-NAME                  
              END-EVALUATE                                                      
                                                                                
              IF WS-NUM-NRIC = LOW-VALUES OR SPACES                             
                 GO TO 2100-EXIT                                                
              ELSE                                                              
                 MOVE WS-NUM-NRIC                   TO K820-NUM-NRIC            
                 PERFORM 2010-READ-BP13K820 THRU 2010-EXIT                      
              END-IF                                                            
                                                                                
                                                                                
              IF WS-K820-FILE-STATUS NOT = ZEROES                               
                 MOVE SPACES                        TO BP13K820-REC             
                 MOVE F300-CUR-REG                  TO PRT-NUM-REGN             
                 MOVE 'NO OCCUPIER INFO IN BP13K820'                            
                                                    TO PRT-REMARKS              
                 PERFORM 2300-PRINT-ERR THRU 2300-EXIT                          
                 MOVE F300-CUR-REG                  TO K820-NUM-REGN            
                 MOVE WS-NUM-NRIC                   TO K820-NUM-NRIC            
                 MOVE WS-NAME                       TO K820-NME-OCCP            
                 PERFORM 2110-WRITE-BP13K820 THRU 2110-EXIT                     
              ELSE                                                              
                 IF K820-NME-OCCP NOT = WS-NAME                                 
                    MOVE WS-NAME TO K820-NME-OCCP                               
                    PERFORM 3000-UPD-BP13F915-PSN THRU 3000-EXIT                
                    PERFORM 2120-REWRITE-BP13K820 THRU 2120-EXIT                
                 END-IF                                                         
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2110-WRITE-BP13K820.                                                     
      ******************************************************************        
                                                                                
           WRITE BP13K820-REC.                                                  
                                                                                
           IF WS-K820-FILE-STATUS NOT = ZEROES AND 02                           
              DISPLAY 'ERROR IN WRITING BP13K820 ' WS-K820-FILE-STATUS          
              GO TO 9999-FINALIZATION.                                          
                                                                                
           ADD 1 TO WS-K820-OUTPUT.                                             
                                                                                
       2110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2120-REWRITE-BP13K820.                                                   
      ******************************************************************        
                                                                                
           REWRITE BP13K820-REC.                                                
                                                                                
           IF WS-K820-FILE-STATUS NOT = ZEROES                                  
              DISPLAY 'ERROR IN UPDATING BP13K820 ' WS-K820-FILE-STATUS         
              GO TO 9999-FINALIZATION.                                          
                                                                                
           ADD 1 TO WS-K820-REWRITE.                                            
                                                                                
       2120-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2200-WRITE-TO-FILE.                                                      
      ******************************************************************        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO K800-DTE-UPDATE.                  
                                                                                
           REWRITE BP13K800-MASTER.                                             
                                                                                
           IF WS-K800-FILE-STATUS NOT = ZEROES                                  
              DISPLAY 'ERROR IN UPDATING BP13K800 ' WS-K800-FILE-STATUS         
              GO TO 9999-FINALIZATION.                                          
                                                                                
           ADD 1 TO WS-K800-OUTPUT.                                             
                                                                                
           WRITE BP13OUT-REC FROM BP13F300-REC.                                 
                                                                                
           ADD 1 TO WS-OUT-OUTPUT.                                              
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2300-PRINT-ERR.                                                          
      ******************************************************************        
                                                                                
           IF WS-LINE > 55                                                      
              ADD 1 TO WS-PAGE                                                  
              MOVE WS-PAGE TO PRINT-PAGE                                        
              WRITE BP13L302-REC FROM PRINT-ERR1 AFTER 1                        
              WRITE BP13L302-REC FROM PRINT-ERR2 AFTER 3                        
              MOVE 0 TO WS-LINE                                                 
              MOVE SPACES TO BP13L302-REC                                       
              WRITE BP13L302-REC AFTER 2.                                       
           WRITE BP13L302-REC FROM PRINT-ERR3 AFTER 2.                          
           ADD 1 TO WS-LINE.                                                    
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-UPD-BP13F915-PSN.                                                   
      ******************************************************************        
                                                                                
           INITIALIZE  BP13F915-REC.                                            
           MOVE SPACES                TO   BP13F915-REC.                        
                                                                                
           MOVE  'UA'                 TO   F915-TXN-TYPE.                       
           MOVE  'P'                  TO   F915-CSTMR-TYPE.                     
           MOVE  'BP13'               TO   F915-NUM-UPDATE-SOURCE.              
           MOVE  'BP13C302'           TO   F915-NUM-UPDATE-USERID.              
                                                                                
           MOVE   K820-NUM-CSTMR-SOURCE                                         
                                      TO   F915-NUM-CSTMR-SOURCE.               
                                                                                
           MOVE   K820-NUM-REGN       TO   F915-NUM-REGN.                       
           MOVE   K820-NUM-NRIC       TO   F915-NUM-NRIC.                       
                                                                                
           MOVE  K820-NME-OCCP        TO   F915-NUM-NEW-NME-PERSON.             
                                                                                
           PERFORM 3100-WRITE-BP13F915     THRU  3100-EXIT.                     
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3100-WRITE-BP13F915.                                                     
      ******************************************************************        
                                                                                
           WRITE BP13F915-REC.                                                  
           ADD  1   TO  WS-F915-WRITE.                                          
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       9999-FINALIZATION.                                                       
      ******************************************************************        
                                                                                
           CLOSE      BP13F300                                                  
                      BP13K800                                                  
                      BP13K820                                                  
                      BP13K150                                                  
                      BP13OUT                                                   
                      BP13KIV                                                   
                      BP13F915                                                  
                      BP13L302.                                                 
                                                                                
           IF WS-K800-FILE-STATUS NOT = ZEROES                                  
              DISPLAY 'BP13K800 - CLOSE ERROR ' WS-K800-FILE-STATUS             
              MOVE WS-K820-FILE-STATUS TO RETURN-CODE.                          
                                                                                
           IF WS-K820-FILE-STATUS NOT = ZEROES                                  
              DISPLAY 'BP13K820 - CLOSE ERROR ' WS-K820-FILE-STATUS             
              MOVE WS-K820-FILE-STATUS TO RETURN-CODE.                          
                                                                                
           IF WS-K150-FILE-STATUS NOT = ZEROES                                  
              DISPLAY 'BP13K150 - CLOSE ERROR ' WS-K150-FILE-STATUS             
              MOVE WS-K150-FILE-STATUS TO RETURN-CODE.                          
                                                                                
           STOP RUN.                                                            
