       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C107.                                                 
       AUTHOR.        LJL.                                                      
       DATE-WRITTEN.  13/10/89.                                                 
      *DATE-COMPILED.                                                           
      *REMARKS.                                                                 
      ************************************************************              
      *                                                          *              
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                    *              
      *                                                          *              
      *    SYSTEM ID   : BP13                                    *              
      *                                                          *              
      *    OBJECTIVE   :                                         *              
      *                  THIS PROGRAMME WILL TRANSFER THE        *              
      *                  TRANSACTION (FILE BP13F125)  TO         *              
      *                  VSAM BALANCING FILE (BP13K130)          *              
      *                                                          *              
      *     INPUT FILE :                                         *              
      *                  1. BP13F125 (DAILY KIV TRANS)           *              
      *                                                          *              
      *     OUTPUT FILE:                                         *              
      *                  1. BP13K130 (SOCHIST   FILE)            *              
      *                                                          *              
      *                                                          *              
      *  CHG-NO   BY   ON        DESCRIPTION                     *              
      *  -------  ---  --------  ------------------------------  *              
      *  SOC-PH8  JEP  09/07/93  CHANGE OF SOME DATA ITEM NAMES  *              
      *  BP132888 SD10 29/08/06  PASS DATA TO NUM-PP-SCHEDULE    *              
      *           BA3  12/10/06  PASS DATA TO DTE-PP-DUE         *              
      *                                                          *              
      ************************************************************              
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K130 ASSIGN       TO BP13K130                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K130-KEY-FLD                         
                           FILE STATUS  IS BP13K130-STATUS.                     
                                                                                
           SELECT BP13F125 ASSIGN       TO BP13F125.                            
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F125                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13F125.                                                           
                                                                                
       FD  BP13K130                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13K130.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  BP13K130-STATUS                PIC X(2).                             
       01  WS-BP13K130-WRITE              PIC 9(5)       VALUE ZEROS.           
       01  WS-BP13F125-READ               PIC 9(5)       VALUE ZEROS.           
                                                                                
       01  WS-F125-KEY-FLD.                                                     
           05  WS-F125-NUM-ORIG-REGN      PIC X(8)       VALUE SPACES.          
           05  WS-F125-DTE-POST           PIC X(8)       VALUE SPACES.          
           05  WS-F125-COUNT-HISTORY      PIC 9(3)       VALUE ZEROS.           
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F125-EOF                PIC X          VALUE 'N'.             
                                                                                
       01  WS-WRITE-K130-STATUS           PIC X          VALUE SPACES.          
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
                                                                                
      ******************************************************************        
      *        MAIN PROCESS                                            *        
      ******************************************************************        
       000-MAIN.                                                                
                                                                                
           PERFORM 100-OPEN-FILE           THRU 100-EXIT.                       
           PERFORM 200-READ-DATA-BP13F125  THRU 200-EXIT.                       
           PERFORM 250-PROCESS-BP13F125    THRU 250-EXIT                        
                       UNTIL WS-F125-EOF = 'Y'.                                 
           PERFORM 900-CLOSE-ROUTINE       THRU 900-EXIT.                       
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    OPENING FILES SECTION                                      *         
      ******************************************************************        
       100-OPEN-FILE.                                                           
                                                                                
           OPEN INPUT  BP13F125                                                 
                I-O    BP13K130.                                                
                                                                                
           IF BP13K130-STATUS NOT = '00' AND                                    
              BP13K130-STATUS NOT = '97'                                        
              DISPLAY 'OPENING ERROR - BP13K130-STATUS : '                      
                   BP13K130-STATUS                                              
              MOVE BP13K130-STATUS      TO   RETURN-CODE                        
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                          
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    200-READ-DATA-BP13F125                                      *        
      ******************************************************************        
       200-READ-DATA-BP13F125.                                                  
                                                                                
           READ BP13F125    AT END                                              
                MOVE 'Y' TO WS-F125-EOF                                         
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1 TO WS-BP13F125-READ.                                           
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       250-PROCESS-BP13F125.                                                    
                                                                                
           PERFORM 300-MAIN-ROUTINE       THRU 300-EXIT.                        
           PERFORM 200-READ-DATA-BP13F125 THRU 200-EXIT.                        
                                                                                
       250-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    300-MAIN-ROUTINE.                                           *        
      ******************************************************************        
       300-MAIN-ROUTINE.                                                        
                                                                                
           IF F125-COUNT-HISTORY = ZEROES                                       
              ADD  1  TO F125-COUNT-HISTORY.                                    
                                                                                
           MOVE F125-NUM-REGN       TO WS-F125-NUM-ORIG-REGN.                   
           MOVE F125-DTE-POST       TO WS-F125-DTE-POST.                        
           MOVE F125-COUNT-HISTORY  TO WS-F125-COUNT-HISTORY.                   
                                                                                
           PERFORM 320-CREATE-BP13K130-REC  THRU  320-EXIT.                     
                                                                                
           WRITE BP13K130-CDHIST.                                               
                                                                                
           IF BP13K130-STATUS = '00'                                            
              ADD 1 TO WS-BP13K130-WRITE                                        
           ELSE                                                                 
              IF BP13K130-STATUS = '22'                                         
                 MOVE 'N' TO WS-WRITE-K130-STATUS                               
                 PERFORM 400-READ-BP13K130 THRU 400-EXIT UNTIL                  
                             WS-WRITE-K130-STATUS = 'Y'                         
              ELSE                                                              
                 MOVE BP13K130-STATUS   TO RETURN-CODE                          
                 DISPLAY 'READING ERROR IN 300 - BP13K130-STATUS : '            
                          BP13K130-STATUS                                       
                 PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                       
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       320-CREATE-BP13K130-REC.                                                 
                                                                                
           MOVE  SPACES                TO    BP13K130-CDHIST.                   
           MOVE  F125-CDE-TRANS-TYPE   TO    K130-TRANS-TYPE.                   
           MOVE  F125-NUM-REGN         TO    K130-NUM-ORIG-REGN.                
           MOVE  F125-DTE-POST         TO    K130-DTE-POST.                     
           MOVE  WS-F125-COUNT-HISTORY TO    K130-COUNT-HISTORY.                
           MOVE  F125-NUM-NRIC         TO    K130-NUM-NRIC.                     
           MOVE  F125-NME-NAME         TO    K130-NME-NAME.                     
           MOVE  F125-NUM-RECPT-JRNO   TO    K130-NUM-RECPT-JRNO.               
           MOVE  F125-AMT-RECEIPT      TO    K130-AMT-CASH.                     
           MOVE  F125-CDE-CRDR         TO    K130-AMT-TYP.                      
           MOVE  F125-DTE-TRANS        TO    K130-DTE-TRANS.                    
           MOVE  F125-CDE-PAYMENT-TYPE TO    K130-PAYMENT-TYPE.                 
           MOVE  F125-NUM-SCH-ACCT     TO    K130-NUM-SCH-ACCT.                 
           MOVE  F125-CDE-ERROR        TO    K130-CDE-ERROR.                    
           MOVE  F125-CDE-VALID        TO    K130-CDE-VALID.                    
           MOVE  F125-CDE-CANCEL       TO    K130-CDE-CANCEL.                   
           MOVE  F125-CDE-CPF-APPL     TO    K130-NUM-CPF-APPL.                 
           MOVE  F125-NUM-PP-SCHEDULE  TO    K130-NUM-PP-SCHEDULE.              
           MOVE  F125-DTE-PP-DUE       TO    K130-DTE-PP-DUE.                   
                                                                                
       320-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       400-READ-BP13K130.                                                       
                                                                                
           ADD 1                 TO  WS-F125-COUNT-HISTORY                      
           MOVE WS-F125-KEY-FLD  TO  K130-KEY-FLD.                              
                                                                                
           READ BP13K130.                                                       
                                                                                
           IF BP13K130-STATUS = '00'                                            
              NEXT SENTENCE                                                     
           ELSE                                                                 
              IF  BP13K130-STATUS = '23'                                        
                  MOVE 'Y'  TO WS-WRITE-K130-STATUS                             
                  PERFORM 320-CREATE-BP13K130-REC  THRU  320-EXIT               
                  PERFORM 500-WRITE-BP13K130       THRU  500-EXIT.              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       500-WRITE-BP13K130.                                                      
                                                                                
           WRITE BP13K130-CDHIST.                                               
                                                                                
           IF BP13K130-STATUS = '00'                                            
              ADD 1 TO WS-BP13K130-WRITE                                        
           ELSE                                                                 
              MOVE BP13K130-STATUS   TO RETURN-CODE                             
              DISPLAY 'READING ERROR IN 500 - BP13K130-STATUS : '               
                       BP13K130-STATUS                                          
              PERFORM 900-CLOSE-ROUTINE  THRU 900-EXIT.                         
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ***************************************************************           
      *     CLOSE ALL FILES AND STOP RUN.                           *           
      ***************************************************************           
       900-CLOSE-ROUTINE.                                                       
           DISPLAY 'CONTROL COUNT FOR BP13C107'.                                
           DISPLAY 'NO OF RECORDS PROCESSED (BP13F125) = '                      
                    WS-BP13F125-READ.                                           
           DISPLAY 'NO OF RECORDS WRITTEN   (BP13K130) = '                      
                    WS-BP13K130-WRITE.                                          
                                                                                
           CLOSE BP13F125                                                       
                 BP13K130.                                                      
                                                                                
           IF BP13K130-STATUS NOT = '00'                                        
              DISPLAY 'CLOSING ERROR-BP13K130 ' BP13K130-STATUS                 
              MOVE BP13K130-STATUS TO RETURN-CODE.                              
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
