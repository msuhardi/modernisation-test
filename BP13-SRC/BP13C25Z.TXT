       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C25Z.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  06 MAY 2016.                                              
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVES: TO UPDATE POSTAL CODE IN BP13K016              *            
      *                                                            *            
      *  INPUT FILE : BM06F120                                     *            
      *               BP13K060                                     *            
      * OUTPUT FILE : BP13L25Z                                     *            
      * ---------------------------------------------------------- *            
      * CHG REF   BY    DATE      DETAILS                          *            
      * --------  ====  ========= ================================ *            
      * BP136205  ESA1  20160506  NEW PGM                          *            
      * BP137928  EL27  20190913  UPDATE INCOME CEILING            *            
      * BP138040  SSR1  20191203  INCLUDED DTE-BALLOT IN K016 KEY  *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BM06F120  ASSIGN        TO BM06F120.                          
                                                                                
           SELECT BP13K060  ASSIGN        TO BP13K060                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K060-KEY-FLD                       
                            FILE STATUS   IS WS-K060-STATUS.                    
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K110-KEY-FLD                       
                            FILE STATUS   IS BM06K110-STATUS.                   
                                                                                
           SELECT BP13K016  ASSIGN        TO BP13K016                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K016-KEY-FLD                       
                            FILE STATUS   IS WS-K016-STATUS.                    
                                                                                
           SELECT BP13L25Z  ASSIGN        TO BP13L25Z.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BM06F120                                                            
            RECORD CONTAINS 700 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BM06F120.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BM06K110.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS 25 CHARACTERS.                                      
       COPY BP13K060.                                                           
                                                                                
       FD   BP13K016                                                            
            RECORD CONTAINS 50 CHARACTERS.                                      
       COPY BP13K016.                                                           
                                                                                
       FD   BP13L25Z                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   BP13L25Z-REC.                                                       
            05 L25Z-SLNO         PIC Z(4)9.                                     
            05 FIL01             PIC X(01).                                     
            05 L25Z-BLK          PIC X(05).                                     
            05 FIL02             PIC X(01).                                     
            05 L25Z-STREET-NAME  PIC X(32).                                     
            05 FIL03             PIC X(01).                                     
            05 L25Z-POSTAL-CODE  PIC X(06).                                     
            05 FIL04             PIC X(01).                                     
            05 L25Z-BALLOT-DTE   PIC X(06).                                     
            05 FIL05             PIC X(01).                                     
            05 FILLER            PIC X(141).                                    
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05 WS-K060-STATUS             PIC 9(02)    VALUE ZEROES.             
           05 WS-K016-STATUS             PIC 9(02)    VALUE ZEROES.             
           05 BM06K110-STATUS            PIC 9(02)     VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F120-READ               PIC 9(08)    VALUE ZEROES.             
           05 WS-K110-READ               PIC 9(08)    VALUE ZEROES.             
           05 WS-K110-NOTFD              PIC 9(08)    VALUE ZEROES.             
           05 WS-L25Z-WRITE              PIC 9(08)    VALUE ZEROES.             
           05 WS-K016-WRITE              PIC 9(08)    VALUE ZEROES.             
           05 WS-SNO                     PIC 9(08)    VALUE ZEROES.             
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-F120-END                PIC X        VALUE 'N'.                
           05 WS-DATE                    PIC X(08)    VALUE SPACES.             
                                                                                
       01  WS-F120-KEY-FLD.                                                     
           05 WS-F120-KEY.                                                      
              10 WS-F120-NUM-NT          PIC X(3).                              
              10 WS-F120-CDE-FLAT-TYPE   PIC X(2).                              
      *    05 WS-F120-DTE-BALLOT         PIC X(6).                              
           05 WS-F120-NUM-POSTAL         PIC X(6).                              
           05 WS-F120-BLK-NO             PIC X(5).                              
           05 WS-F120-STREET-NAME        PIC X(32).                             
                                                                                
       01  WS-PREV-KEY-FLD.                                                     
           05 WS-PREV-KEY.                                                      
              10 WS-PREV-NUM-NT          PIC X(3).                              
              10 WS-PREV-CDE-FLAT-TYPE   PIC X(2).                              
      *    05 WS-PREV-DTE-BALLOT         PIC X(6).                              
           05 WS-PREV-NUM-POSTAL         PIC X(6).                              
           05 WS-PREV-BLK-NO             PIC X(5).                              
           05 WS-PREV-STREET-NAME        PIC X(32).                             
                                                                                
       01 WS-REPORT-VAR.                                                        
          05 L25Z-HEADER1.                                                      
             10 FILLER            PIC X(08) VALUE 'BP13L25Z'.                   
             10 FILLER            PIC X(05) VALUE SPACES.                       
             10 FILLER            PIC X(08) VALUE 'HDBCAT 3'.                   
             10 FILLER            PIC X(29) VALUE SPACES.                       
             10 FILLER            PIC X(20) VALUE                               
                'SYSTEM OF COMMITMENT'.                                         
             10 FILLER            PIC X(23) VALUE SPACES.                       
             10 FILLER            PIC X(07) VALUE 'DATE : '.                    
             10 L25Z-DATE         PIC X(12) VALUE SPACES.                       
             10 FILLER            PIC X(88) VALUE SPACES.                       
          05 L25Z-HEADER2.                                                      
             10 FILLER            PIC X(45)  VALUE SPACES.                      
             10 FILLER            PIC X(40)  VALUE                              
                'REPORT ON UPDATE OF POSTAL CODES FOR SBF'.                     
             10 L25Z-MATURE-TXT   PIC X(25) VALUE SPACES.                       
             10 FILLER            PIC X(90) VALUE SPACES.                       
          05 L25Z-HEADER2A.                                                     
             10 L25Z-NEW-TOWN     PIC X(20) VALUE SPACES.                       
             10 FILLER            PIC X(05) VALUE SPACES.                       
             10 L25Z-FT-DESC      PIC X(40) VALUE SPACES.                       
             10 FILLER            PIC X(135) VALUE SPACES.                      
          05 L25Z-HEADER3.                                                      
             10 FILLER            PIC X(05)  VALUE 'S/NO '.                     
             10 FILLER            PIC X(01)  VALUE ';'.                         
             10 FILLER            PIC X(05)  VALUE 'BLOCK'.                     
             10 FILLER            PIC X(01)  VALUE ';'.                         
             10 FILLER            PIC X(32)  VALUE 'STREET NAME'.               
             10 FILLER            PIC X(01)  VALUE ';'.                         
             10 FILLER            PIC X(06)  VALUE 'POSTAL'.                    
             10 FILLER            PIC X(01)  VALUE ';'.                         
             10 FILLER            PIC X(06)  VALUE 'BALLOT'.                    
             10 FILLER            PIC X(01)  VALUE ';'.                         
             10 FILLER            PIC X(129) VALUE SPACES.                      
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
                                                                                
           PERFORM 2000-READ-BM06F120  THRU 2000-EXIT.                          
                                                                                
           PERFORM 3000-PROCESS-DATA   THRU 3000-EXIT                           
              UNTIL WS-F120-END = 'Y'.                                          
                                                                                
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BM06F120                                                 
                       BM06K110                                                 
                       BP13K060                                                 
                I-O    BP13K016                                                 
                OUTPUT BP13L25Z.                                                
                                                                                
           IF WS-K060-STATUS NOT =  00 AND 97                                   
              DISPLAY 'ERROR OPENING - BP13K060 : ' WS-K060-STATUS              
              MOVE WS-K060-STATUS                TO RETURN-CODE                 
              PERFORM  9000-CLOSE-ROUTINE      THRU 9000-EXIT                   
           END-IF.                                                              
                                                                                
           IF BM06K110-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BM06K110 : ' BM06K110-STATUS             
              MOVE     BM06K110-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE      THRU 9000-EXIT                   
           END-IF.                                                              
                                                                                
           IF WS-K016-STATUS NOT =  00 AND 97                                   
              DISPLAY 'ERROR OPENING - BP13K016 : ' WS-K016-STATUS              
              MOVE WS-K016-STATUS                TO RETURN-CODE                 
              PERFORM  9000-CLOSE-ROUTINE      THRU 9000-EXIT                   
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BM06F120.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BM06F120                                                        
             AT END                                                             
                MOVE 'Y'            TO WS-F120-END                              
                GO TO 9000-CLOSE-ROUTINE                                        
           END-READ.                                                            
                                                                                
           ADD 1                    TO WS-F120-READ.                            
                                                                                
           IF WS-F120-READ = 1                                                  
              MOVE F120-NUM-NT         TO WS-PREV-NUM-NT                        
      *       MOVE F120-NUM-ALLOCN-QTR TO WS-PREV-DTE-BALLOT                    
              MOVE F120-POSTAL-CODE    TO WS-PREV-NUM-POSTAL                    
              MOVE F120-CDE-TYP-ACTUSE TO WS-PREV-CDE-FLAT-TYPE                 
              MOVE F120-BLK-NO         TO WS-PREV-BLK-NO                        
              MOVE F120-STREET-NAME    TO WS-PREV-STREET-NAME                   
              MOVE SPACES              TO BP13L25Z-REC                          
              STRING WS-DATE(7:2) '/'  WS-DATE(5:2) '/'                         
                     WS-DATE(1:4) DELIMITED BY SIZE                             
                     INTO L25Z-DATE                                             
              END-STRING                                                        
      ************************************************************              
      * USE F120-CDE-CLS-ACTUSE AS A MATURE-TAG FIELD IN BP13C25Y               
      ************************************************************              
              IF F120-CDE-CLS-ACTUSE = 'N'                                      
                 MOVE ' (NON-MATURE ESTATES)  '  TO L25Z-MATURE-TXT             
              ELSE                                                              
                 MOVE ' (MATURE ESTATES)      '  TO L25Z-MATURE-TXT             
              END-IF                                                            
              WRITE BP13L25Z-REC FROM L25Z-HEADER1                              
              WRITE BP13L25Z-REC FROM L25Z-HEADER2                              
           END-IF.                                                              
                                                                                
           MOVE F120-NUM-NT         TO WS-F120-NUM-NT.                          
      *    MOVE F120-NUM-ALLOCN-QTR TO WS-F120-DTE-BALLOT.                      
           MOVE F120-POSTAL-CODE    TO WS-F120-NUM-POSTAL.                      
           MOVE F120-CDE-TYP-ACTUSE TO WS-F120-CDE-FLAT-TYPE.                   
           MOVE F120-BLK-NO         TO WS-F120-BLK-NO.                          
           MOVE F120-STREET-NAME    TO WS-F120-STREET-NAME.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
           PERFORM 3500-READ-BM06K110     THRU 3500-EXIT.                       
           PERFORM 4000-WRITE-BP13K106    THRU 4000-EXIT.                       
                                                                                
           MOVE F120-NUM-NT         TO WS-PREV-NUM-NT.                          
      *    MOVE F120-NUM-ALLOCN-QTR TO WS-PREV-DTE-BALLOT.                      
           MOVE F120-POSTAL-CODE    TO WS-PREV-NUM-POSTAL.                      
           MOVE F120-CDE-TYP-ACTUSE TO WS-PREV-CDE-FLAT-TYPE.                   
           MOVE F120-BLK-NO         TO WS-PREV-BLK-NO.                          
           MOVE F120-STREET-NAME    TO WS-PREV-STREET-NAME.                     
                                                                                
           PERFORM 2000-READ-BM06F120          THRU 2000-EXIT.                  
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3500-READ-BM06K110.                                                      
      *-------------------------------------------------------------            
           MOVE F120-KEY-FLD      TO K110-KEY-FLD.                              
                                                                                
           READ BM06K110.                                                       
           EVALUATE BM06K110-STATUS                                             
             WHEN 00                                                            
                ADD             1 TO WS-K110-READ                               
             WHEN 23                                                            
                ADD             1 TO WS-K110-NOTFD                              
             WHEN OTHER                                                         
                DISPLAY 'BM06K110 READ ERROR,STATUS : ' BM06K110-STATUS         
                      ', K110 KEY FLD : ' K110-KEY-FLD                          
                MOVE BM06K110-STATUS       TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
      *--------------------------------------------------------------*          
       4000-WRITE-BP13K106.                                                     
      *--------------------------------------------------------------*          
           MOVE SPACES                  TO BP13K016-REC.                        
           MOVE F120-NUM-NT             TO K016-NUM-NT                          
           MOVE K110-NUM-ALLOCN-QTR     TO K016-DTE-BALLOT.                     
           MOVE F120-POSTAL-CODE        TO K016-NUM-POSTAL.                     
           IF F120-CDE-TYP-ACTUSE = '5G'                                        
              MOVE '5 '                 TO K016-CDE-FLAT-TYPE                   
           ELSE                                                                 
              MOVE F120-CDE-TYP-ACTUSE  TO K016-CDE-FLAT-TYPE                   
           END-IF.                                                              
           MOVE F120-BLK-NO             TO K016-NUM-BLK.                        
           MOVE FUNCTION CURRENT-DATE(1:8) TO K016-DTE-UPDATE.                  
           MOVE 'BP13C25Z'                 TO K016-NUM-USERID.                  
                                                                                
           WRITE BP13K016-REC.                                                  
                                                                                
           EVALUATE WS-K016-STATUS                                              
              WHEN 00                                                           
              WHEN 22                                                           
                   ADD 1    TO WS-K016-WRITE WS-SNO                             
                   IF WS-F120-KEY NOT = WS-PREV-KEY OR                          
                      WS-K016-WRITE = 1                                         
                      MOVE SPACES      TO BP13L25Z-REC                          
                      IF F120-NUM-NT = 'JE'                                     
                         MOVE 'JURONG EAST/WEST'                                
                                          TO L25Z-NEW-TOWN                      
                      ELSE                                                      
                         PERFORM 5000-READ-BP13K060 THRU 5000-EXIT              
                         MOVE K060-DESC   TO L25Z-NEW-TOWN                      
                      END-IF                                                    
                      PERFORM 6000-DECODE-FT     THRU 6000-EXIT                 
                      WRITE BP13L25Z-REC FROM L25Z-HEADER2A                     
                      WRITE BP13L25Z-REC FROM L25Z-HEADER3                      
                      MOVE 1                TO WS-SNO                           
                   END-IF                                                       
                   MOVE SPACES              TO BP13L25Z-REC                     
                   MOVE WS-SNO              TO L25Z-SLNO                        
                   MOVE ';'                 TO FIL01  FIL02                     
                                               FIL03  FIL04  FIL05              
                   MOVE F120-BLK-NO         TO L25Z-BLK                         
                   MOVE F120-POSTAL-CODE    TO L25Z-POSTAL-CODE                 
                   MOVE F120-STREET-NAME    TO L25Z-STREET-NAME                 
                   MOVE K110-NUM-ALLOCN-QTR TO L25Z-BALLOT-DTE                  
                   IF L25Z-BALLOT-DTE = LOW-VALUES                              
                      MOVE SPACES TO L25Z-BALLOT-DTE                            
                   END-IF                                                       
                   WRITE BP13L25Z-REC                                           
                   ADD   1    TO  WS-L25Z-WRITE                                 
              WHEN OTHER                                                        
                   DISPLAY 'ERROR WRITING BP13K016. STATUS IS '                 
                           WS-K016-STATUS                                       
                   PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       5000-READ-BP13K060.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES       TO K060-KEY-FLD.                                   
           MOVE 02           TO K060-SERIAL-NO.                                 
           MOVE F120-NUM-NT  TO K060-CODE.                                      
                                                                                
           READ BP13K060.                                                       
                                                                                
           EVALUATE WS-K060-STATUS                                              
              WHEN 00                                                           
                   CONTINUE                                                     
              WHEN 23                                                           
                   MOVE SPACES TO BP13K060-REC                                  
              WHEN OTHER                                                        
                   DISPLAY 'ERROR WITH READING BP13K060, STATUS : '             
                           WS-K060-STATUS ', KEY: ' K060-KEY-FLD                
                   GO TO 9000-CLOSE-ROUTINE                                     
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       6000-DECODE-FT.                                                          
      *-------------------------------------------------------------            
           MOVE SPACES        TO L25Z-FT-DESC.                                  
                                                                                
           EVALUATE F120-CDE-TYP-ACTUSE                                         
             WHEN '2F'                                                          
                  MOVE '2-ROOM FLEXI FLAT'                                      
                              TO L25Z-FT-DESC                                   
             WHEN '3 '                                                          
      ************************************************************              
      * USE F120-CDE-CLS-ACTUSE AS A MATURE-TAG FIELD IN BP13C25Y               
      ************************************************************              
                  IF F120-CDE-CLS-ACTUSE = 'N'                                  
                     MOVE '3-ROOM FLAT (INCOME CEILING $7,000)'                 
                              TO L25Z-FT-DESC                                   
                  ELSE                                                          
                     MOVE '3-ROOM FLAT'                                         
                              TO L25Z-FT-DESC                                   
                  END-IF                                                        
             WHEN '3P'                                                          
                  MOVE '3-ROOM FLAT (INCOME CEILING $14,000)'                   
                              TO L25Z-FT-DESC                                   
             WHEN '4 '                                                          
                  MOVE '4-ROOM FLAT'                                            
                              TO L25Z-FT-DESC                                   
             WHEN '5 '                                                          
                  MOVE '5-ROOM FLAT'                                            
                              TO L25Z-FT-DESC                                   
             WHEN OTHER                                                         
                  IF F120-CDE-TYP-ACTUSE = '5G'                                 
                      MOVE '5-ROOM/3GEN FLAT'                                   
                              TO L25Z-FT-DESC                                   
                  ELSE                                                          
                     MOVE SPACES TO L25Z-FT-DESC                                
                  END-IF                                                        
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' BP13L25Z CONTROL TOTAL '.                                  
           DISPLAY '------------------------------------------'.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECS READ IN BM06F120    : ' WS-F120-READ.            
           DISPLAY 'NO OF RECS WRITTEN TO BP13K016 : ' WS-K016-WRITE.           
           DISPLAY 'NO OF RECS WRITTEN TO BP13L25Z : ' WS-L25Z-WRITE.           
                                                                                
           CLOSE BM06F120                                                       
                 BP13K060                                                       
                 BP13K016                                                       
                 BP13L25Z.                                                      
                                                                                
           IF WS-K060-STATUS NOT =  00 AND 97                                   
              DISPLAY 'ERROR OPENING - BP13K060 : ' WS-K060-STATUS              
              MOVE WS-K060-STATUS                TO RETURN-CODE                 
           END-IF.                                                              
                                                                                
           IF WS-K016-STATUS NOT =  00 AND 97                                   
              DISPLAY 'ERROR OPENING - BP13K016 : ' WS-K016-STATUS              
              MOVE WS-K016-STATUS                TO RETURN-CODE                 
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
