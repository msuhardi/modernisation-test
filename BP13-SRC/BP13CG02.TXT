      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13CG02.                                                 
      *AUTHOR.        ZARITA DANE SARMIENTO.                                    
      *DATE-WRITTEN.  JUNE 6, 2013.                                             
      *===============================================================*         
      *   OBJECTIVE:  PRINT FROM FB00                                 *         
      *===============================================================*         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *                  1. BP13FB00                                  *         
      *                  2. BP13KB02                                  *         
      *                                                               *         
      *     OUTPUT FILE: 1. P13LRPT1   -                              *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP13XXXX 06/06/2013 ZDD1 NEW PROGRAM                          *         
      * BP138103 04/12/2019 PP11 ADD BP13K800 AND NEW FIELDS          *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13FB00  ASSIGN        TO BP13FB00.                         
           SELECT  BP13FB02  ASSIGN        TO BP13FB02.                         
           SELECT  P13LRPT1  ASSIGN        TO P13LRPT1.                         
                                                                                
           SELECT SY08K010 ASSIGN    TO SY08K010                                
                           ACCESS MODE   IS RANDOM                              
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K010-NUM-USERID                     
                           FILE STATUS   IS WS-K010-STATUS.                     
                                                                                
           SELECT BP13K800   ASSIGN        TO BP13K800                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K800-NUM-REGN                     
                             FILE STATUS   IS WS-K800-STATUS.                   
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13FB00                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13FB00.                                                           
                                                                                
       FD   BP13FB02                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13KB02.                                                           
                                                                                
       FD   SY08K010                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY SY08K010.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   P13LRPT1                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 250 CHARACTERS                                      
            LABEL RECORD IS OMITTED                                             
            RECORDING MODE IS F.                                                
       01   P13LRPT1-REC               PIC X(250).                              
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-FB00             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-FB02             PIC X(1)  VALUE 'N'.                     
           05  WS-K010-STATUS          PIC X(2)  VALUE SPACES.                  
           05  WS-K800-STATUS          PIC X(2)  VALUE SPACES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-FB00-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-FB02-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-FB00-NFND            PIC 9(7)  VALUE ZEROES.                  
           05  WS-MATCH                PIC 9(7)  VALUE ZEROES.                  
           05  WS-SNO                  PIC 9(5)  VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(2)  VALUE 51.                      
           05  WS-PAGE-CTR             PIC 9(4)  VALUE ZEROES.                  
           05  WS-CNT-LRPT-WRITE       PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-WRITE            PIC 9(1)  VALUE 2.                       
           05  WS-MAX-LINE             PIC 9(2)  VALUE 55.                      
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-SYSTEM-DATE           PIC X(8)  VALUE SPACES.                  
           05 WS-SNO-ZZZZ9             PIC ZZZZ9 VALUE ZEROES.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-CCYY                 PIC 9(04) VALUE ZEROES.                  
           05  WS-MM                   PIC 9(02) VALUE ZEROES.                  
           05  WS-DD                   PIC 9(02) VALUE ZEROES.                  
                                                                                
                                                                                
       01  LRPT-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'P13LRPT1'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  LRPT-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LRPT-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
       01  LRPT-HEADER02.                                                       
           05  FILLER              PIC X(45)     VALUE SPACES.                  
           05  FILLER              PIC X(52)     VALUE                          
               'ADHOC REPORT USING BP13FB00 FOR BTO/SBF'.                       
                                                                                
       01  LRPT-HEADER03.                                                       
           05  FILLER              PIC X(05)    VALUE SPACES.                   
           05  FILLER              PIC X(05)    VALUE '  S/N'.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'REGN NO '.              
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE 'SALE MODE'.             
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'BALLOT QTR'.            
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(06)     VALUE 'OIC   '.                
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'DTE ROUTE '.            
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(64)     VALUE 'REASON'.                
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE 'QUEUE'.                 
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'RESULT TAG'.            
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'BK APPMT'.              
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'BK TIME'.               
                                                                                
       01  LRPT-DETAIL01.                                                       
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LRPT-SNO            PIC ZZZ9.                                    
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LRPT-REGNO          PIC X(08)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LRPT-MODE           PIC X(09)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LRPT-BQTR           PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LRPT-OIC            PIC X(06)     VALUE SPACES.                  
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  LRPT-DTE-RT         PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  LRPT-REASON         PIC X(64)     VALUE SPACES.                  
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  LRPT-QUEUE          PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LRPT-RESULT-TAG     PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  LRPT-BK-APPMT       PIC X(08)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LRPT-BK-TIME        PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
                                                                                
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13FB00      THRU 2000-EXIT.                      
           PERFORM 2100-READ-BP13FB02      THRU 2100-EXIT.                      
                                                                                
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-FB00 = 'Y' OR WS-EOF-FB02 = 'Y'.                      
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13FB00                                                 
                       BP13FB02                                                 
                       BP13K800                                                 
                       SY08K010                                                 
              OUTPUT   P13LRPT1.                                                
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR, SY08K010-STATUS ' WS-K010-STATUS             
              MOVE     WS-K010-STATUS TO RETURN-CODE                            
              PERFORM  9999-CLOSE-ROUTINE     THRU 9999-EXIT                    
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR, BP13K800-STATUS ' WS-K800-STATUS             
              MOVE     WS-K800-STATUS TO RETURN-CODE                            
              PERFORM  9999-CLOSE-ROUTINE     THRU 9999-EXIT                    
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE.                        
                                                                                
           STRING WS-SYSTEM-DATE(7:2) '/'                                       
                  WS-SYSTEM-DATE(5:2) '/'                                       
                  WS-SYSTEM-DATE(1:4)                                           
                  DELIMITED BY SIZE INTO LRPT-DATE-HDR.                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13FB00.                                                      
      ******************************************************************        
                                                                                
           READ BP13FB00           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-FB00                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO   WS-FB00-READ.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2100-READ-BP13FB02.                                                      
      ******************************************************************        
                                                                                
           READ BP13FB02           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-FB02                             
                GO                 TO   2100-EXIT.                              
                                                                                
           ADD  1                  TO   WS-FB02-READ.                           
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           IF FB00-NUM-REGN  = KB02-NUM-REGN                                    
              PERFORM 6000-PRINT-REPORT   THRU 6000-EXIT                        
              PERFORM 2000-READ-BP13FB00  THRU 2000-EXIT                        
              PERFORM 2100-READ-BP13FB02  THRU 2100-EXIT                        
              ADD 1 TO WS-MATCH                                                 
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF FB00-NUM-REGN  > KB02-NUM-REGN                                    
              PERFORM 2100-READ-BP13FB02  THRU 2100-EXIT                        
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF FB00-NUM-REGN  < KB02-NUM-REGN                                    
              PERFORM 2000-READ-BP13FB00  THRU 2000-EXIT                        
              ADD 1 TO WS-FB00-NFND                                             
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-READ-SY08K010.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES           TO SY08K010-USER-REC.                          
           INITIALIZE               SY08K010-USER-REC.                          
                                                                                
           STRING 'EA' KB02-NUM-ACK-USERID(1:5) DELIMITED BY SIZE               
           INTO K010-NUM-USERID.                                                
                                                                                
           READ SY08K010.                                                       
                                                                                
           EVALUATE WS-K010-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                   CONTINUE                                                     
              WHEN 23                                                           
                   MOVE SPACES   TO SY08K010-USER-REC                           
                   INITIALIZE       SY08K010-USER-REC                           
              WHEN OTHER                                                        
                DISPLAY 'READ ERROR, SY08K010-STATUS ' WS-K010-STATUS           
                MOVE     WS-K010-STATUS TO RETURN-CODE                          
                PERFORM  9999-CLOSE-ROUTINE     THRU 9999-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       4100-READ-BP13K800.                                                      
      *********************                                                     
           INITIALIZE BP13K800-MASTER.                                          
                                                                                
           MOVE FB00-NUM-REGN   TO K800-NUM-REGN.                               
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = ZEROS OR 02                                      
              IF K800-NUM-NT-FT-QUEUE(1:1) NOT NUMERIC                          
                 MOVE ZEROES  TO K800-NUM-NT-FT-QUEUE(1:1)                      
              END-IF                                                            
              IF K800-NUM-NT-FT-QUEUE(2:1) NOT NUMERIC                          
                 MOVE ZEROES  TO K800-NUM-NT-FT-QUEUE(2:1)                      
              END-IF                                                            
              IF K800-NUM-NT-FT-QUEUE(3:1) NOT NUMERIC                          
                 MOVE ZEROES  TO K800-NUM-NT-FT-QUEUE(3:1)                      
              END-IF                                                            
              IF K800-NUM-NT-FT-QUEUE(4:1) NOT NUMERIC                          
                 MOVE ZEROES  TO K800-NUM-NT-FT-QUEUE(4:1)                      
              END-IF                                                            
              IF K800-NUM-NT-FT-QUEUE(5:1) NOT NUMERIC                          
                 MOVE ZEROES  TO K800-NUM-NT-FT-QUEUE(5:1)                      
              END-IF                                                            
           ELSE                                                                 
              IF WS-K800-STATUS = 23                                            
                 MOVE ZEROES TO K800-NUM-NT-FT-QUEUE                            
              ELSE                                                              
                 DISPLAY 'READ ERROR, BP13K800 STATUS = '                       
                 WS-K800-STATUS                                                 
                 MOVE WS-K800-STATUS TO RETURN-CODE                             
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       6000-PRINT-REPORT.                                                       
      ******************************************************************        
                                                                                
           IF WS-LINE-CTR > 50                                                  
              PERFORM 7000-PRINT-HEADER            THRU 7000-EXIT               
           END-IF.                                                              
                                                                                
           PERFORM 4000-READ-SY08K010    THRU 4000-EXIT.                        
           PERFORM 4100-READ-BP13K800    THRU 4100-EXIT.                        
                                                                                
           ADD 1                     TO WS-SNO.                                 
           MOVE WS-SNO               TO LRPT-SNO.                               
                                                                                
           MOVE FB00-NUM-REGN           TO LRPT-REGNO.                          
           MOVE FB00-NUM-ALLO-CAT       TO LRPT-MODE.                           
           MOVE FB00-DTE-REQUEST(1:6)   TO LRPT-BQTR.                           
           MOVE K010-NUM-LOTUS-NCKNM    TO LRPT-OIC.                            
           STRING KB02-DTE-ROUTE(7:2) '/'                                       
                  KB02-DTE-ROUTE(5:2) '/'                                       
                  KB02-DTE-ROUTE(1:4)                                           
                  DELIMITED BY SIZE INTO LRPT-DTE-RT.                           
           MOVE KB02-TXT-ROUTE-REASON   TO LRPT-REASON.                         
           MOVE K800-NUM-NT-FT-QUEUE    TO LRPT-QUEUE.                          
           MOVE K800-NUM-BALLOT-STATUS  TO LRPT-RESULT-TAG.                     
           MOVE K800-DTE-BK-APPT        TO LRPT-BK-APPMT.                       
           MOVE K800-TME-BK-APPT        TO LRPT-BK-TIME.                        
                                                                                
           WRITE P13LRPT1-REC  FROM LRPT-DETAIL01.                              
                                                                                
           ADD 1                                  TO WS-LINE-CTR.               
           ADD 1                                  TO WS-CNT-LRPT-WRITE.         
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7000-PRINT-HEADER.                                                       
      ******************************************************************        
                                                                                
           ADD 1                               TO WS-PAGE-CTR.                  
           MOVE ZEROES                         TO LRPT-PAGE-HDR.                
           MOVE WS-PAGE-CTR                    TO LRPT-PAGE-HDR.                
                                                                                
           WRITE P13LRPT1-REC    FROM LRPT-HEADER01 AFTER PAGE.                 
           WRITE P13LRPT1-REC    FROM LRPT-HEADER02.                            
           WRITE P13LRPT1-REC    FROM LRPT-HEADER03 AFTER 2.                    
                                                                                
           INITIALIZE WS-LINE-CTR.                                              
           MOVE 5  TO WS-LINE-CTR.                                              
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13RPT1'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13FB00 RECORDS READ............. : '            
                    WS-FB00-READ.                                               
           DISPLAY '(2) NO OF BP13FB02 RECORDS NOT FOUND........ : '            
                    WS-FB02-READ.                                               
           DISPLAY '(3) NO OF MATCH CASES             .......... : '            
                    WS-MATCH.                                                   
           DISPLAY '(3) NO OF NOT FOUND FB00 IN FB02  .......... : '            
                    WS-FB00-NFND.                                               
           DISPLAY '(3) NO OF P13LRPT1 RECORDS WRITTEN.......... : '            
                    WS-CNT-LRPT-WRITE.                                          
                                                                                
           CLOSE    BP13FB00                                                    
                    BP13FB02                                                    
                    BP13K800                                                    
                    SY08K010                                                    
                    P13LRPT1.                                                   
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
