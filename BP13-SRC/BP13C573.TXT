       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C573.                                                  
      *AUTHOR.       YPH.                                                       
      *DATE-WRITTEN. 29.11.94.                                                  
      ************************************************************              
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                      *              
      *  SYSTEM ID   : BP13                                      *              
      *  OBJECTIVE   : USE THE CUR-REGN-NO IN BP13F586 TO READ   *              
      *                BM06K110 AND BP13K800                     *              
      *                IF REC IN K110 SHOW ACCEPTANCE, THEN TAG  *              
      *                BP13K520 AS 'SS'                          *              
      *                AND THE RECS WILL BE SHOWN IN BP13K800 AS *              
      *                AS, AW, AI.                               *              
      *                IF REC IN BP13K800 SHOW REJECTION WITHIN  *              
      *                THE SAME MONTH, THEN TAG AS 'RA', 'RS',   *              
      *                'RI' OR 'RW'.                             *              
      *                                                          *              
      * CHG NO  BY  ON     DESCRIPTION                           *              
      * ------  --  --     ---------------                       *              
      *N940014  YPH 291194 SOC PH9                               *              
      *BP130418 MPA 170498 TO CATER FOR Y2000.                   *              
      *BP130652 NOR 220299 EXPAND K520 RECL FROM 300 TO 500      *              
      ************************************************************              
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F586 ASSIGN TO BP13F586.                                  
                                                                                
           SELECT BP13K520 ASSIGN TO BP13K520                                   
                  ACCESS MODE   IS DYNAMIC                                      
                  ORGANIZATION  IS INDEXED                                      
                  RECORD KEY    IS K520-KEY-NEW                                 
                  FILE STATUS   IS K520-STATUS.                                 
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ACCESS MODE IS RANDOM                                         
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K110-KEY-FLD                                    
                  ALTERNATE RECORD KEY IS K110-REGN-NO                          
                  FILE STATUS IS K110-STATUS.                                   
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE IS RANDOM                                         
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K800-NUM-REGN                                   
                  FILE STATUS IS K800-STATUS.                                   
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F586                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 30  CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F586.                                                           
                                                                                
       FD  BP13K520                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13K520.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILE-STS.                                                            
           05  K110-STATUS         PIC 9(2)    VALUE ZEROS.                     
           05  K520-STATUS         PIC 9(2)    VALUE ZEROS.                     
           05  K800-STATUS         PIC 9(2)    VALUE ZEROS.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ-CNT-F586    PIC 9(8)    VALUE ZEROS.                     
           05  WS-READ-CNT-K110    PIC 9(8)    VALUE ZEROS.                     
           05  WS-READ-CNT-K520    PIC 9(8)    VALUE ZEROS.                     
           05  WS-READ-CNT-K800    PIC 9(8)    VALUE ZEROS.                     
           05  WS-WRITE-CNT-K520   PIC 9(8)    VALUE ZEROS.                     
                                                                                
       01  INELIGIBLE-CDE          PIC X       VALUE SPACE.                     
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-K110-FOUND       PIC X(1)    VALUE SPACES.                    
           05  WS-K520-FOUND       PIC X(1)    VALUE SPACES.                    
           05  WS-K800-FOUND       PIC X(1)    VALUE SPACES.                    
                                                                                
       01  WS-EOF-F586             PIC X(1).                                    
           88  EOF-OF-F586                     VALUE 'Y'.                       
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
       10-MAIN-LOGIC.                                                           
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F586      THRU 200-EXIT.                            
           PERFORM 300-MATCH-ROUTINE  THRU 300-EXIT                             
                   UNTIL EOF-OF-F586.                                           
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       100-INITIALIZATION.                                                      
           OPEN INPUT  BP13F586                                                 
                       BM06K110                                                 
                       BP13K800                                                 
                I-O    BP13K520.                                                
                                                                                
           IF K520-STATUS NOT = ZEROS AND 97                                    
              DISPLAY 'OPENING ERROR, BP13K520-STATUS ' K520-STATUS             
              MOVE K520-STATUS TO RETURN-CODE                                   
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                          
                                                                                
           IF K110-STATUS NOT = ZEROS AND 97                                    
              DISPLAY 'OPENING ERROR, BM06K110-STATUS ' K110-STATUS             
              MOVE K110-STATUS TO RETURN-CODE                                   
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                          
                                                                                
           IF K800-STATUS NOT = ZEROS AND 97                                    
              DISPLAY 'OPENING ERROR, BP13K800-STATUS ' K800-STATUS             
              MOVE K800-STATUS TO RETURN-CODE                                   
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                          
                                                                                
           MOVE ZEROS TO WS-COUNTERS.                                           
           MOVE 'N' TO WS-EOF-F586.                                             
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-F586.                                                           
           READ BP13F586                                                        
               AT END MOVE 'Y' TO WS-EOF-F586                                   
               GO TO 200-EXIT.                                                  
                                                                                
           ADD 1 TO WS-READ-CNT-F586.                                           
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      * MATCH THE REGN NO IN BP13K800 WITH BM06K110 AND UPDATE REC    *         
      * IN BP13K520.                                                  *         
      *****************************************************************         
       300-MATCH-ROUTINE.                                                       
           PERFORM 500-READ-K520 THRU 500-EXIT.                                 
           PERFORM 600-READ-K800 THRU 600-EXIT.                                 
           IF WS-K800-FOUND = 'Y'                                               
               IF K800-NUM-STATUS NOT = 'C'                                     
                                                                                
                  IF K800-DTE-BALLOT NOT = SPACES AND LOW-VALUES                
                     MOVE 'SL'   TO K520-CDE-ELIG-TAG                           
                  END-IF                                                        
                                                                                
                  PERFORM 400-READ-K110       THRU 400-EXIT                     
                                                                                
                  IF WS-K110-FOUND = 'Y'                                        
                     PERFORM 520-MOVE-FIELDS  THRU 520-EXIT                     
                  END-IF                                                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF K520-CDE-ELIG-TAG NOT = SPACES AND LOW-VALUES                     
              PERFORM 550-REWRITE-K520    THRU 550-EXIT.                        
                                                                                
       300-READNEXT.                                                            
           PERFORM 200-READ-F586          THRU 200-EXIT.                        
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-READ-K110.                                                           
           MOVE 'Y' TO WS-K110-FOUND.                                           
           MOVE F586-NUM-REGN       TO K110-REGN-NO.                            
                                                                                
           READ BM06K110 KEY IS K110-REGN-NO.                                   
           IF K110-STATUS = ZEROS                                               
              NEXT SENTENCE                                                     
           ELSE                                                                 
           IF K110-STATUS = 23                                                  
              DISPLAY 'RECORD NOT FOUND IN BM06K110-STATUS ' K110-STATUS        
              DISPLAY 'K110-REGN-NO = ' K110-REGN-NO                            
              MOVE 'N' TO WS-K110-FOUND                                         
              GO TO 400-EXIT                                                    
           ELSE                                                                 
              DISPLAY 'READ ERROR, BM06K110-STATUS ' K110-STATUS                
              DISPLAY 'K110-REGN-NO = ' K110-REGN-NO                            
              MOVE 'N' TO WS-K110-FOUND                                         
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                          
                                                                                
           ADD 1 TO WS-READ-CNT-K110.                                           
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-READ-K520.                                                           
           MOVE 'Y'                TO WS-K520-FOUND.                            
           MOVE F586-NUM-CUR-REGN  TO K520-KEY-NEW.                             
                                                                                
           READ BP13K520.                                                       
           IF K520-STATUS = ZEROS                                               
              NEXT SENTENCE                                                     
           ELSE                                                                 
           IF K520-STATUS = 23                                                  
              DISPLAY 'RECORD NOT FOUND IN BP13K520 ' K520-KEY-NEW              
              MOVE 'N' TO WS-K520-FOUND                                         
              GO TO 500-EXIT                                                    
           ELSE                                                                 
              DISPLAY 'READ ERROR, BP13K520-STATUS ' K520-STATUS                
              DISPLAY 'K520-KEY-NEW ' K520-KEY-NEW                              
              MOVE K520-STATUS TO RETURN-CODE                                   
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                          
                                                                                
           ADD 1 TO WS-READ-CNT-K520.                                           
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       520-MOVE-FIELDS.                                                         
           IF K110-DTE-KEY-ISSUED = ZEROS OR SPACES OR LOW-VALUES               
              IF K110-DTE-ACCEPTANCE = ZEROS OR SPACES OR LOW-VALUES            
                 NEXT SENTENCE                                                  
              ELSE                                                              
                 IF K520-CDE-ELIG-TAG = 'SL'                                    
                    MOVE SPACES TO K520-CDE-ELIG-TAG                            
                 END-IF                                                         
                                                                                
                 IF F586-NUM-NRIC = K520-NUM-NRIC1                              
                    MOVE 'S' TO K520-CDE-ELIG-TAG (1:1)                         
                 ELSE                                                           
                    MOVE 'S' TO K520-CDE-ELIG-TAG (2:1)                         
                 END-IF                                                         
                                                                                
              END-IF                                                            
           ELSE                                                                 
              MOVE SPACES TO  K520-NUM-EXIST-REGN                               
              MOVE SPACES TO  K520-CDE-ELIG-TAG.                                
                                                                                
       520-EXIT.                                                                
           EXIT.                                                                
                                                                                
       550-REWRITE-K520.                                                        
           IF WS-K520-FOUND = 'N'                                               
              GO TO 550-EXIT.                                                   
                                                                                
           REWRITE BP13K520-REC.                                                
           IF K520-STATUS = ZEROS                                               
              NEXT SENTENCE                                                     
           ELSE                                                                 
           IF K520-STATUS = 23                                                  
              DISPLAY 'RECORD NOT FOUND IN BP13K520 ' K520-KEY-NEW              
              GO TO 500-EXIT                                                    
           ELSE                                                                 
              DISPLAY 'REWRITE ERROR, BP13K520-STATUS ' K520-STATUS             
              DISPLAY 'K520-KEY-NEW ' K520-KEY-NEW                              
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                          
                                                                                
           ADD 1 TO WS-WRITE-CNT-K520.                                          
                                                                                
       550-EXIT.                                                                
           EXIT.                                                                
                                                                                
       600-READ-K800.                                                           
           MOVE 'Y' TO WS-K800-FOUND.                                           
                                                                                
           MOVE F586-NUM-REGN       TO K800-NUM-REGN.                           
           READ BP13K800 KEY IS K800-NUM-REGN.                                  
           IF K800-STATUS = ZEROS                                               
              NEXT SENTENCE                                                     
           ELSE                                                                 
           IF K800-STATUS = 23                                                  
              DISPLAY 'RECORD NOT FOUND IN BP13K800 ' K800-NUM-REGN     O       
              MOVE 'N' TO WS-K800-FOUND                                         
              GO TO 600-EXIT                                                    
           ELSE                                                                 
              DISPLAY 'READ ERROR, BP13K800-STATUS ' K800-STATUS                
              DISPLAY 'K800-NUM-REGN = ' K800-NUM-REGN                          
              MOVE 'N' TO WS-K800-FOUND                                         
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                          
                                                                                
           ADD 1 TO WS-READ-CNT-K800.                                           
                                                                                
           IF K800-NUM-STATUS = 'C'                                             
              MOVE SPACES TO K520-NUM-EXIST-REGN                                
           ELSE                                                                 
              MOVE F586-NUM-REGN TO K520-NUM-EXIST-REGN.                        
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
           DISPLAY '  '.                                                        
           DISPLAY '***** SUMMARY REPORT FOR BP13C573 *****'.                   
           DISPLAY 'TOTAL NO OF REC READ FROM BP13F586  = '                     
                   WS-READ-CNT-F586.                                            
           DISPLAY 'TOTAL NO OF REC READ FROM BP13K520  = '                     
                   WS-READ-CNT-K520.                                            
           DISPLAY 'TOTAL NO OF REC READ FROM BM06K110  = '                     
                   WS-READ-CNT-K110.                                            
           DISPLAY 'TOTAL NO OF REC READ FROM BP13K800  = '                     
                   WS-READ-CNT-K800.                                            
           DISPLAY 'TOTAL NO OF REC REWRITE TO BP13K520 = '                     
                   WS-WRITE-CNT-K520.                                           
                                                                                
           CLOSE      BP13F586                                                  
                      BP13K520                                                  
                      BP13K800                                                  
                      BM06K110.                                                 
                                                                                
           IF K520-STATUS NOT = ZEROS AND 97                                    
              DISPLAY 'CLOSE ERROR, BP13K520-STATUS ' K520-STATUS.              
                                                                                
           IF K110-STATUS NOT = ZEROS AND 97                                    
              DISPLAY 'CLOSE ERROR, BM06K110-STATUS ' K110-STATUS.              
                                                                                
           IF K800-STATUS NOT = ZEROS AND 97                                    
              DISPLAY 'CLOSE ERROR, BP13K800-STATUS ' K800-STATUS.              
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
