       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD73.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  27/01/2011.                                               
      ******************************************************************        
      *                SYSTEM OF COMMITMENT (BP13)                     *        
      ******************************************************************        
      * TO PRINT REPORT FOR BUY 1ST 2ND CONC LOAN CASES                *        
      * -------------------------------------------------------------- *        
      * INPUT : BP13F310 - CDE-TRANS = 'U2'                            *        
      *         BP13KD55                                               *        
      *         SY08K010                                               *        
      * OUTPUT: BP13LD73                                               *        
      * ---------------------------------                              *        
      * CR NO.    BY   DATE      DESCRIPTION                           *        
      * --------  ---- --------  ------------------------------------- *        
      * BP13      JB8  27/01/11  NEW PROGRAM                           *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F310      ASSIGN TO BP13F310.                             
           SELECT BP13KD55       ASSIGN TO BP13KD55                             
                            ACCESS MODE IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                             RECORD KEY IS KD55-NUM-REGN                        
                            FILE STATUS IS BP13KD55-STATUS.                     
           SELECT SY08K010       ASSIGN TO SY08K010                             
                            ACCESS MODE IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                             RECORD KEY IS K010-NUM-USERID                      
                            FILE STATUS IS SY08K010-STATUS.                     
           SELECT BP13LD73       ASSIGN TO BP13LD73.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F310                                                            
            RECORD CONTAINS 150  CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F310.                                                           
                                                                                
       FD   BP13KD55                                                            
            RECORD CONTAINS 1500 CHARACTERS.                                    
       COPY BP13KD55.                                                           
                                                                                
       FD   SY08K010                                                            
            RECORD CONTAINS 100  CHARACTERS.                                    
       COPY SY08K010.                                                           
                                                                                
       FD   BP13LD73                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   BP13LD73-REC   PIC X(132).                                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-VARIABLES.                                                        
           05  WS-F310-EOF                  PIC X(01)  VALUE SPACES.            
               88  F310-EOF                            VALUE 'Y'.               
           05  NO-F310-READ                 PIC 9(07)  VALUE ZEROS.             
           05  BP13KD55-STATUS              PIC 9(02)  VALUE ZEROES.            
           05  SY08K010-STATUS              PIC 9(02)  VALUE ZEROES.            
           05  WS-SYS-DATE                  PIC X(08)  VALUE SPACES.            
                                                                                
       01  WS-REPORT-VARIABLES.                                                 
           05  NO-REC-PAGE                  PIC 9(02)  VALUE 55.                
           05  NO-SNO                       PIC 9(02)  VALUE ZEROES.            
           05  NO-PAGE                      PIC 9(02)  VALUE ZEROES.            
                                                                                
       01  WS-REPORT-DETAIL.                                                    
           05  LD73-HEADER-01.                                                  
               10  LD73-REPORT-ID           PIC X(14) VALUE 'BP13LD73'.         
               10  FILLER                   PIC X(8)  VALUE 'HDB3'.             
               10  FILLER                   PIC X(24) VALUE SPACES.             
               10  FILLER                   PIC X(39) VALUE                     
                   'S Y S T E M   O F   C O M M I T M E N T'.                   
               10  FILLER                   PIC X(18) VALUE SPACES.             
               10  FILLER                   PIC X(06) VALUE 'DATE: '.           
               10  LD73-CURR-DATE           PIC X(10) VALUE SPACES.             
               10  FILLER                   PIC X(07) VALUE ' PAGE: '.          
               10  LD73-PAGE-NO             PIC ZZZZ9 VALUE ZEROES.             
                                                                                
           05  LD73-HEADER-02.                                                  
               10  FILLER                   PIC X(38) VALUE SPACES.             
               10  FILLER                   PIC X(46) VALUE                     
                   'REPORT FOR DBSS 2ND CONCESSIONARY LOAN CASES ('.            
               10  LD73-BUY-SELL-TAG        PIC X(08) VALUE SPACES.             
               10  FILLER                   PIC X(39) VALUE ')'.                
                                                                                
           05  LD73-DETAIL-01.                                                  
               10  FILLER                   PIC X(132) VALUE                    
                   ' S/N    REGN NO   HDB REF NO      CASH PROCEED  NRIC        
      -            '1      NRIC2      NRIC3      NRIC4      OIC'.               
                                                                                
           05  LD73-DETAIL-02.                                                  
               10  FILLER                   PIC X(132) VALUE                    
                   ' -----  --------  --------------  ------------  ----        
      -            '-----  ---------  ---------  ---------  ------------        
      -            '--------'.                                                  
                                                                                
           05  LD73-DETAIL-03.                                                  
               10  FILLER                   PIC X(01) VALUE SPACES.             
               10  LD73-SNO                 PIC ZZZZ9 VALUE ZEROES.             
               10  FILLER                   PIC X(02) VALUE SPACES.             
               10  LD73-REGN-NO             PIC X(08) VALUE SPACES.             
               10  FILLER                   PIC X(02) VALUE SPACES.             
               10  LD73-HDB-REF-NO          PIC X(14) VALUE SPACES.             
               10  FILLER                   PIC X(05) VALUE SPACES.             
               10  LD73-CASH-PROCEED        PIC Z,ZZZ,ZZ9 VALUE ZEROES.         
               10  FILLER                   PIC X(02) VALUE SPACES.             
               10  LD73-NRIC1               PIC X(09) VALUE SPACES.             
               10  FILLER                   PIC X(02) VALUE SPACES.             
               10  LD73-NRIC2               PIC X(09) VALUE SPACES.             
               10  FILLER                   PIC X(02) VALUE SPACES.             
               10  LD73-NRIC3               PIC X(09) VALUE SPACES.             
               10  FILLER                   PIC X(02) VALUE SPACES.             
               10  LD73-NRIC4               PIC X(09) VALUE SPACES.             
               10  FILLER                   PIC X(02) VALUE SPACES.             
               10  LD73-OIC                 PIC X(20) VALUE SPACES.             
                                                                                
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
       0000-MAIN.                                                               
      *****************************************************************         
           PERFORM 8000-OPEN-FILES              THRU 8000-EXIT.                 
                                                                                
           PERFORM UNTIL F310-EOF                                               
              PERFORM 8001-READ-BP13F310        THRU 8001-EXIT                  
              IF NOT F310-EOF                                                   
                 PERFORM 1000-PROCESS-RECORDS   THRU 1000-EXIT                  
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           PERFORM 9000-CLOSE-FILES             THRU 9000-EXIT.                 
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1000-PROCESS-RECORDS.                                                    
      *****************************************************************         
                                                                                
           MOVE F310-REGN-NO                   TO KD55-NUM-REGN                 
           PERFORM 8002-READ-BP13KD55        THRU 8002-EXIT.                    
                                                                                
           MOVE F310-USERID                    TO K010-NUM-USERID               
           PERFORM 8003-READ-SY08K010        THRU 8003-EXIT.                    
                                                                                
           ADD 1                               TO NO-REC-PAGE                   
           IF NO-REC-PAGE > 55                                                  
              PERFORM 2000-PRINT-HEADER      THRU 2000-EXIT                     
           END-IF.                                                              
                                                                                
           ADD 1                               TO NO-SNO                        
           MOVE NO-SNO                         TO LD73-SNO                      
           MOVE F310-REGN-NO                   TO LD73-REGN-NO                  
           STRING KD55-NUM-SCH-ACC(1:4) '-'                                     
                  KD55-NUM-SCH-ACC(5:4) '-'                                     
                  KD55-NUM-SCH-ACC(9:1) '-'                                     
                  KD55-NUM-SCH-ACC(10:2)                                        
                  DELIMITED BY SIZE INTO LD73-HDB-REF-NO.                       
           MOVE KD55-AMT-PROCEED-HDB-KEPT      TO LD73-CASH-PROCEED             
           MOVE KD55-NUM-HA-UIN(1)             TO LD73-NRIC1                    
           MOVE KD55-NUM-HA-UIN(2)             TO LD73-NRIC2                    
           MOVE KD55-NUM-HA-UIN(3)             TO LD73-NRIC3                    
           MOVE KD55-NUM-HA-UIN(4)             TO LD73-NRIC4                    
           MOVE K010-NME-USER                  TO LD73-OIC.                     
           WRITE BP13LD73-REC FROM LD73-DETAIL-03.                              
                                                                                
           ADD 1                               TO NO-REC-PAGE.                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-PRINT-HEADER.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                         TO BP13LD73-REC.                 
           WRITE BP13LD73-REC AFTER PAGE.                                       
                                                                                
           MOVE 'BP13LD73'                     TO LD73-REPORT-ID.               
           STRING WS-SYS-DATE(7:2) '/'                                          
                  WS-SYS-DATE(5:2) '/'                                          
                  WS-SYS-DATE(1:4) DELIMITED BY SIZE INTO LD73-CURR-DATE        
           ADD 1                               TO NO-PAGE                       
           MOVE NO-PAGE                        TO LD73-PAGE-NO.                 
           WRITE BP13LD73-REC FROM LD73-HEADER-01 AFTER 3.                      
                                                                                
           MOVE 'BUY 1ST '                     TO LD73-BUY-SELL-TAG             
           WRITE BP13LD73-REC FROM LD73-HEADER-02.                              
                                                                                
           WRITE BP13LD73-REC FROM LD73-DETAIL-01 AFTER 2.                      
           WRITE BP13LD73-REC FROM LD73-DETAIL-02.                              
                                                                                
           MOVE 7                              TO NO-REC-PAGE.                  
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT BP13F310                                                  
                      BP13KD55                                                  
                      SY08K010                                                  
               OUTPUT BP13LD73.                                                 
                                                                                
           IF BP13KD55-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13KD55 - OPEN ERROR (' BP13KD55-STATUS ')'             
              MOVE BP13KD55-STATUS            TO RETURN-CODE                    
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           IF SY08K010-STATUS NOT = 00 AND 97                                   
              DISPLAY 'SY08K010 - OPEN ERROR (' SY08K010-STATUS ')'             
              MOVE SY08K010-STATUS            TO RETURN-CODE                    
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)    TO WS-SYS-DATE.                   
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8001-READ-BP13F310.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F310                                                        
             AT END                                                             
                MOVE 'Y'             TO WS-F310-EOF                             
                GO                   TO 8001-EXIT.                              
                                                                                
           ADD  1                    TO NO-F310-READ.                           
                                                                                
       8001-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8002-READ-BP13KD55.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13KD55.                                                       
                                                                                
           IF BP13KD55-STATUS = 00                                              
              CONTINUE                                                          
           ELSE                                                                 
              IF BP13KD55-STATUS = 23                                           
                 DISPLAY 'BP13KD55 - RECORD NOT FND (' KD55-NUM-REGN ')'        
                 GO TO 9000-CLOSE-FILES                                         
              ELSE                                                              
                 DISPLAY 'BP13KD55 - ERROR READ (' BP13KD55-STATUS ')'          
                 MOVE BP13KD55-STATUS             TO RETURN-CODE                
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8002-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8003-READ-SY08K010.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ SY08K010.                                                       
                                                                                
           IF SY08K010-STATUS = 00                                              
              CONTINUE                                                          
           ELSE                                                                 
              IF SY08K010-STATUS = 23                                           
                 DISPLAY 'SY08K010 - RECORD NOT FND ('                          
                         K010-NUM-USERID ')'                                    
                 GO TO 9000-CLOSE-FILES                                         
              ELSE                                                              
                 DISPLAY 'SY08K010 - ERROR READ (' SY08K010-STATUS ')'          
                 MOVE SY08K010-STATUS             TO RETURN-CODE                
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8003-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           DISPLAY SPACES.                                                      
           DISPLAY '***************************************'.                   
           DISPLAY 'PROGRAM ID     :  BP13CD73'.                                
           DISPLAY '***************************************'.                   
           DISPLAY 'NO-BP13F310-READ       : ' NO-F310-READ.                    
                                                                                
           CLOSE BP13F310                                                       
                 BP13KD55                                                       
                 SY08K010                                                       
                 BP13LD73.                                                      
                                                                                
           IF BP13KD55-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13KD55 - OPEN ERROR (' BP13KD55-STATUS ')'             
              MOVE BP13KD55-STATUS            TO RETURN-CODE                    
           END-IF.                                                              
                                                                                
           IF SY08K010-STATUS NOT = 00 AND 97                                   
              DISPLAY 'SY08K010 - OPEN ERROR (' SY08K010-STATUS ')'             
              MOVE SY08K010-STATUS            TO RETURN-CODE                    
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
