       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C21W.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  20/08/2010.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVES: TO MATCH BP13F800 AND BP13FH17 AND CHECK IF    *            
      *             BP13F800 QUEUE NO. IS IN THE RANGE OF BP13FH17 *            
      *             QUEUE NO.                                                   
      * ---------------------------------------------------------- *            
      * CHG REF   BY    DATE      DETAILS                          *            
      * -------   ====  ========= ================================ *            
      * BP134006  ESA1  20082010  NEW PROGRAM                      *            
      * BP134226  ESA1  22072011  CATER FOR 2-BYTES QUEUE PREFIX   *            
      * BP134681  ESA1  02102012  CATER FOR 3-BYTES ZONE           *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
      *-------------------------------------------------------------            
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT BP13FH17  ASSIGN        TO BP13FH17.                          
           SELECT BP13F800  ASSIGN        TO BP13F800.                          
           SELECT P13F800B  ASSIGN        TO P13F800B.                          
                                                                                
      *-------------------------------------------------------------            
       DATA DIVISION.                                                           
      *-------------------------------------------------------------            
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F800                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD   BP13FH17                                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13FH17.                                                           
                                                                                
       FD   P13F800B                                                            
            RECORDING MODE IS F.                                                
       01 P13F800B-REC                 PIC X(2000).                             
                                                                                
      *-------------------------------------------------------------            
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01 WS-VARIABLES.                                                         
          05 WS-CURRENT-DATE      PIC X(8)  VALUE SPACES.                       
                                                                                
       01 WS-COUNTERS.                                                          
          05 WS-FH17-READ         PIC 9(08) VALUE ZEROES.                       
          05 WS-F800-READ         PIC 9(08) VALUE ZEROES.                       
          05 WS-MATCHED           PIC 9(08) VALUE ZEROES.                       
          05 WS-FH17-NOTFD        PIC 9(08) VALUE ZEROES.                       
          05 WS-F800-NOTFD        PIC 9(08) VALUE ZEROES.                       
          05 WS-F800-WRITE        PIC 9(08) VALUE ZEROES.                       
                                                                                
       01 WS-SWITCHES.                                                          
          05 WS-FH17-EOF                 PIC X(1) VALUE 'N'.                    
          05 WS-F800-EOF                 PIC X(1) VALUE 'N'.                    
                                                                                
       01 WS-FH17-KEY.                                                          
          10 WS-FH17-MAIN-KEY.                                                  
             15 WS-FH17-NUM-SELECTION         PIC X(3).                         
             15 WS-FH17-DTE-BALLOT            PIC X(6).                         
             15 WS-FH17-NUM-NT                PIC X(3).                         
                                                                                
       01 WS-FH17-QUEUE.                                                        
          10 WS-QUEUE-START                   PIC X(5).                         
          10 WS-QUEUE-END                     PIC X(5).                         
                                                                                
       01 WS-F800-KEY.                                                          
          10 WS-F800-MAIN-KEY.                                                  
             15 WS-F800-ALLO-CAT              PIC X(3).                         
             15 WS-F800-DTE-BALLOT            PIC X(6).                         
             15 WS-F800-NUM-BTO-ZONE          PIC X(3).                         
          10 WS-F800-QUEUE                    PIC X(5).                         
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13F800  THRU 2000-EXIT.                          
           PERFORM 3000-READ-BP13FH17  THRU 3000-EXIT.                          
           PERFORM 4000-PROCESS-DATA   THRU 4000-EXIT                           
              UNTIL WS-F800-EOF = 'Y'                                           
               AND  WS-FH17-EOF = 'Y'.                                          
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13FH17                                                 
                       BP13F800                                                 
               OUTPUT  P13F800B.                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F800.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES               TO WS-F800-KEY.                            
                                                                                
           READ BP13F800 AT END                                                 
             MOVE 'Y'                TO WS-F800-EOF                             
             MOVE HIGH-VALUES        TO WS-F800-KEY                             
             GO TO 2000-EXIT.                                                   
                                                                                
           ADD 1                     TO WS-F800-READ.                           
                                                                                
           MOVE F800-NUM-ALLO-CAT    TO WS-F800-ALLO-CAT.                       
           MOVE F800-DTE-BALLOT      TO WS-F800-DTE-BALLOT.                     
           MOVE F800-NUM-BTO-ZONE    TO WS-F800-NUM-BTO-ZONE.                   
                                                                                
           MOVE ZEROS                TO F800-NUM-NT-FT-QUEUE(1:1).              
           IF F800-NUM-NT-FT-QUEUE(2:1) NOT NUMERIC                             
              MOVE ZEROS             TO F800-NUM-NT-FT-QUEUE(2:1)               
           END-IF.                                                              
           MOVE F800-NUM-NT-FT-QUEUE TO WS-F800-QUEUE.                          
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-READ-BP13FH17.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13FH17 AT END                                                 
             MOVE 'Y'                TO WS-FH17-EOF                             
             MOVE HIGH-VALUES        TO WS-FH17-KEY                             
             GO TO 3000-EXIT.                                                   
                                                                                
           ADD 1                     TO WS-FH17-READ.                           
                                                                                
           MOVE FH17-NUM-SELECTION   TO WS-FH17-NUM-SELECTION.                  
           MOVE FH17-DTE-BALLOT      TO WS-FH17-DTE-BALLOT.                     
           MOVE FH17-NUM-NEW-TOWN    TO WS-FH17-NUM-NT.                         
                                                                                
           MOVE FH17-NUM-QUEUE-START TO WS-QUEUE-START                          
           MOVE FH17-NUM-QUEUE-END   TO WS-QUEUE-END.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF WS-F800-MAIN-KEY = WS-FH17-MAIN-KEY                               
              ADD 1                                   TO WS-MATCHED             
              PERFORM 5000-CHECK-QUEUE-NO           THRU 5000-EXIT              
              PERFORM 2000-READ-BP13F800            THRU 2000-EXIT              
           ELSE                                                                 
           IF WS-F800-MAIN-KEY <  WS-FH17-MAIN-KEY                              
              ADD 1                              TO WS-FH17-NOTFD               
              PERFORM 2000-READ-BP13F800       THRU 2000-EXIT                   
           ELSE                                                                 
              ADD 1                              TO WS-F800-NOTFD               
              PERFORM 3000-READ-BP13FH17       THRU 3000-EXIT                   
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-CHECK-QUEUE-NO.                                                     
      *-------------------------------------------------------------            
                                                                                
           IF WS-F800-QUEUE >= WS-QUEUE-START AND                               
              WS-F800-QUEUE <= WS-QUEUE-END                                     
              PERFORM 6000-WRITE-P13F800B             THRU 6000-EXIT            
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-WRITE-P13F800B.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                              TO P13F800B-REC.            
           INITIALIZE                                  P13F800B-REC.            
                                                                                
           WRITE P13F800B-REC                     FROM BP13F800-MASTER.         
           ADD 1                                    TO WS-F800-WRITE.           
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' BP13C21W CONTROL TOTAL '.                                  
           DISPLAY '------------------------------------------'.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECS READ FROM BP13F800: ' WS-F800-READ.              
           DISPLAY 'NO OF RECS READ FROM BP13FH17: ' WS-FH17-READ.              
           DISPLAY 'NO OF RECS NOT FOUND BP13F800: ' WS-F800-NOTFD.             
           DISPLAY 'NO OF RECS NOT FOUND BP13FH17: ' WS-FH17-NOTFD.             
           DISPLAY 'NO OF RECS WRITTEN   BP13F800: ' WS-F800-WRITE.             
                                                                                
           CLOSE BP13F800                                                       
                 BP13FH17                                                       
                 P13F800B.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
