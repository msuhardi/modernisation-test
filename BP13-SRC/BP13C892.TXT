       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C892.                                                 
      *AUTHOR.        EVM.                                                      
      *DATE-WRITTEN.  12/07/96.                                                 
      *DATE-COMPILED.                                                           
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *    1.   THIS IS A PROGRAM TO REFORMAT AND WRITE RECORDS*                
      *         TO BP13F214 FOR QUEUE MANAGEMENT SYSTEM TO     *                
      *         DISPLAY REGN-NO AND QUEUE POSITION ON THE      *                
      *         ELECTRONIC BOARD.                              *                
      *    2.   SOC ONLY OUTPUT APPOINTMENT FOR THE NEXT WEEK. *                
      *         THE PROGRAM WILL BE RUN ON FRIDAY.             *                
      *                                                        *                
      *    INPUT FILES :                                       *                
      *    1.   BP13F800 (SOC  MASTER)                         *                
      *                                                        *                
      *    OUTPUT FILES :                                      *                
      *    1.   BP13F214                                       *                
      *                                                        *                
      * CHG-NO.  BY  ON       DESCRIPTION                      *                
      * -------- --- -------- -----------                      *                
      * BP130142 EVM 19960905 NEW PROGRAM                      *                
      * BP130142 RHB 19961112 CHECK IF DATES ARE WITHIN NEXT   *                
      *                       WEEK'S DATES                     *                
      * BP130231 RHB 19970121 TO CATER FOR QUEUE MANAGEMENT    *                
      *                       SYSTEM FOR SATP CASES.           *                
      * BP130418 SGK 19980901 TO CATER FOR Y2K.                *                
      * BP130909 SRN 20000819 TO OUTPOT ZN/FT FROM OFA ZN/FT   *                
      * BP130937 LSB 20000819 TAKE OUT OFA ZN/FT               *                
      * BP132018 LMS 20011006 TO CATER FOR BTO RESERVED LIST.  *                
      * BP134681 ESA120121002 TO CATER FOR 3-BYTES ZONE        *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800   ASSIGN TO BP13F800.                                
                                                                                
           SELECT BP13F214   ASSIGN TO BP13F214.                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F800                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F800.                                                           
                                                                                
       FD   BP13F214                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 50 CHARACTERS                                       
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F214.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-F800-READ                PIC 9(8)    VALUE ZEROES.                
       01  WS-F214-WRITE               PIC 9(8)    VALUE ZEROES.                
                                                                                
       01  WS-WRITE-REC                PIC X       VALUE 'N'.                   
       01  WS-F800-EOF                 PIC X(1).                                
           88  F800-EOF                            VALUE 'Y'.                   
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURR-YYYY            PIC 9(4).                                
           05  WS-CURR-MM              PIC 99.                                  
           05  WS-CURR-DD              PIC 99.                                  
                                                                                
       01  WS-UPDATE-DATE.                                                      
           05  WS-UPD-YYYY             PIC 9(4).                                
           05  WS-UPD-MM               PIC 99.                                  
           05  WS-UPD-DD               PIC 99.                                  
                                                                                
       01  WS-START-DATE.                                                       
           05  WS-STR-CY               PIC 9999.                                
           05  WS-STR-MM               PIC 99.                                  
           05  WS-STR-DD               PIC 99.                                  
                                                                                
       01  WS-NEXT-WEEK-DATE.                                                   
           05  WS-NWK-CY               PIC 9999.                                
           05  WS-NWK-MM               PIC 99.                                  
           05  WS-NWK-DD               PIC 99.                                  
                                                                                
       01  WS-CURR-MM-DAYS             PIC 99.                                  
       01  WS-NUM-QUOTIENT             PIC 99.                                  
       01  WS-NUM-REMAINDER            PIC 99.                                  
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      ******************************************************************        
       MAIN-ROUTINE SECTION.                                                    
      ******************************************************************        
                                                                                
           PERFORM 10-OPEN-FILES    THRU 10-EXIT.                               
           PERFORM 20-READ-BP13F800 THRU 20-EXIT.                               
           PERFORM 30-PROCESS-F800  THRU 30-EXIT                                
                                    UNTIL F800-EOF.                             
                                                                                
           DISPLAY 'PROGRAM : BP13C892    '.                                    
           DISPLAY 'DATE    : ' WS-UPDATE-DATE.                                 
           DISPLAY '*=========== COUNTERS ===========*'                         
           DISPLAY '1) NO. OF REC READ IN APPLN MASTER FILE : '                 
                                          WS-F800-READ.                         
           DISPLAY '2) NO. OF REC WRITTEN IN BP13F214 FILE  : '                 
                                          WS-F214-WRITE.                        
                                                                                
           PERFORM 99-CLOSE-RTN THRU 99-EXIT.                                   
                                                                                
       MAIN-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       10-OPEN-FILES.                                                           
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F800                                                 
                OUTPUT BP13F214.                                                
                                                                                
           MOVE   FUNCTION CURRENT-DATE(1:8)                                    
                                       TO WS-CURRENT-DATE.                      
                                                                                
           MOVE   WS-CURR-YYYY         TO WS-UPD-YYYY.                          
           MOVE   WS-CURR-MM           TO WS-UPD-MM.                            
           MOVE   WS-CURR-DD           TO WS-UPD-DD.                            
                                                                                
           MOVE SPACES                 TO WS-F800-EOF.                          
                                                                                
           MOVE WS-UPDATE-DATE         TO WS-NEXT-WEEK-DATE.                    
           MOVE WS-UPDATE-DATE         TO WS-START-DATE.                        
           PERFORM 200-COMPUTE-NEXT-WEEK-DATE THRU 200-EXIT.                    
                                                                                
       10-EXIT.                                                                 
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       20-READ-BP13F800.                                                        
      ******************************************************************        
                                                                                
           READ BP13F800 AT END                                                 
                MOVE 'Y'        TO WS-F800-EOF                                  
                GO TO 20-EXIT.                                                  
                                                                                
           ADD 1 TO WS-F800-READ.                                               
                                                                                
       20-EXIT.                                                                 
           EXIT.                                                                
                                                                                
      ******************************************************************        
       30-PROCESS-F800.                                                         
      ******************************************************************        
           MOVE SPACES                TO BP13F214-REC.                          
           INITIALIZE BP13F214-REC.                                             
           MOVE 'N' TO WS-WRITE-REC.                                            
                                                                                
           MOVE F800-NUM-REGN         TO F214-NUM-REGN.                         
           IF F800-NUM-BTO-ZONE = '000' OR                                      
              ((F800-NUM-ZONE = 'WW' OR 'NE' OR 'NN' OR 'PG') AND               
               (F800-DTE-BALLOT = '200201'))                                    
                MOVE '4 ' TO F214-NUM-FLAT-TYPE                                 
           ELSE                                                                 
              MOVE F800-NUM-FLAT-TYPE TO F214-NUM-FLAT-TYPE                     
           END-IF.                                                              
                                                                                
           IF F800-NUM-NEW-TOWN = SPACES                                        
              IF F800-NUM-BTO-ZONE = '000'                                      
                 MOVE 'RL '         TO F214-NUM-NT-ZONE                         
              ELSE                                                              
                 MOVE F800-NUM-BTO-ZONE TO F214-NUM-NT-ZONE                     
              END-IF                                                            
           ELSE                                                                 
              MOVE F800-NUM-NEW-TOWN  TO F214-NUM-NT-ZONE.                      
                                                                                
           IF F800-DTE-SO-APPT NOT = SPACES AND ZEROS                           
              IF F800-DTE-SO-APPT >= WS-START-DATE AND                          
                 F800-DTE-SO-APPT <= WS-NEXT-WEEK-DATE                          
                 MOVE 'Y' TO WS-WRITE-REC                                       
                 IF F800-DTE-SALE-AGMT = SPACES OR LOW-VALUES OR ZEROES         
                    MOVE 'SP'                  TO F214-NUM-APPMT                
                 ELSE                                                           
                    MOVE 'TP'                  TO F214-NUM-APPMT                
                 END-IF                                                         
                                                                                
                 MOVE F800-DTE-SO-APPT      TO F214-DTE-APPMT                   
                 MOVE F800-TME-SO-APPT      TO F214-TME-APPMT                   
              END-IF                                                            
           ELSE                                                                 
              IF F800-DTE-AO-APPT NOT = SPACES AND ZEROS                        
                 IF F800-DTE-AO-APPT >= WS-START-DATE AND                       
                    F800-DTE-AO-APPT <= WS-NEXT-WEEK-DATE                       
                    MOVE 'Y' TO WS-WRITE-REC                                    
                    MOVE 'SA'               TO F214-NUM-APPMT                   
                    MOVE F800-DTE-AO-APPT   TO F214-DTE-APPMT                   
                    MOVE F800-TME-AO-APPT   TO F214-TME-APPMT                   
                 END-IF                                                         
              ELSE                                                              
                 IF F800-DTE-BK-APPT >= WS-START-DATE  AND                      
                    F800-DTE-BK-APPT <= WS-NEXT-WEEK-DATE                       
                    MOVE 'Y' TO WS-WRITE-REC                                    
                    MOVE 'BK'               TO F214-NUM-APPMT                   
                    MOVE F800-DTE-BK-APPT   TO F214-DTE-APPMT                   
                    MOVE F800-TME-BK-APPT   TO F214-TME-APPMT                   
                    IF F800-NUM-BTO-ZONE = '000'                                
                       MOVE F800-NUM-SLIST-PA-ZNFT(2:4) TO                      
                            F214-NUM-QUEUE-POS                                  
                    ELSE                                                        
                       MOVE F800-NUM-ZONE-FT-QUEUE(2:4) TO                      
                            F214-NUM-QUEUE-POS                                  
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE 'P13C892'                TO F214-NUM-USERID.                    
           MOVE WS-UPDATE-DATE           TO F214-DTE-UPDATE.                    
                                                                                
           IF WS-WRITE-REC = 'Y'                                                
              WRITE BP13F214-REC                                                
              ADD 1 TO WS-F214-WRITE.                                           
                                                                                
           PERFORM 20-READ-BP13F800   THRU 20-EXIT.                             
                                                                                
       30-EXIT.                                                                 
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       99-CLOSE-RTN.                                                            
      ******************************************************************        
                                                                                
           CLOSE BP13F800                                                       
                 BP13F214.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       99-EXIT.                                                                 
           EXIT.                                                                
                                                                                
      ******************************************************************        
       200-COMPUTE-NEXT-WEEK-DATE.                                              
      ******************************************************************        
                                                                                
      *---> GET NO. OF DAYS OF THE CURRENT MONTH.                               
                                                                                
            IF WS-CURR-MM = 1 OR 3 OR 5 OR 7 OR 8 OR 10 OR 12                   
               MOVE 31 TO WS-CURR-MM-DAYS                                       
            ELSE                                                                
               IF WS-CURR-MM = 4 OR 6 OR 9 OR 11                                
                  MOVE 30 TO WS-CURR-MM-DAYS                                    
               ELSE                                                             
      *--->       CHECK IF LEAP YEAR                                            
                  DIVIDE WS-NWK-CY BY 4 GIVING WS-NUM-QUOTIENT                  
                                        REMAINDER WS-NUM-REMAINDER              
                  IF WS-NUM-REMAINDER = ZEROES                                  
                     MOVE 29 TO WS-CURR-MM-DAYS                                 
                  ELSE                                                          
                     MOVE 28 TO WS-CURR-MM-DAYS                                 
                  END-IF                                                        
               END-IF                                                           
            END-IF.                                                             
                                                                                
      *---> COMPUTE START DAY                                                   
                                                                                
            COMPUTE WS-STR-DD = WS-CURR-DD + 1.                                 
            IF WS-STR-DD > WS-CURR-MM-DAYS                                      
               COMPUTE WS-STR-DD = WS-STR-DD - WS-CURR-MM-DAYS                  
               ADD 1 TO WS-STR-MM                                               
               IF WS-STR-MM > 12                                                
                  MOVE 01 TO WS-STR-MM                                          
                  ADD 1 TO WS-STR-CY                                            
               END-IF                                                           
            END-IF.                                                             
                                                                                
      *---> COMPUTE NEXT WEEK'S DAY                                             
                                                                                
            COMPUTE WS-NWK-DD = WS-CURR-DD + 8.                                 
            IF WS-NWK-DD > WS-CURR-MM-DAYS                                      
               COMPUTE WS-NWK-DD = WS-NWK-DD - WS-CURR-MM-DAYS                  
               ADD 1 TO WS-NWK-MM                                               
               IF WS-NWK-MM > 12                                                
                  MOVE 01 TO WS-NWK-MM                                          
                  ADD 1 TO WS-NWK-CY                                            
               END-IF                                                           
            END-IF.                                                             
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
