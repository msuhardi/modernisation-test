      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.   BP13C02Q.                                                  
      *AUTHOR.       MSD.                                                       
      *DATE-WRITTEN. 08/01/08.                                                  
                                                                                
      **************************************************************            
      * SYSTEM NAME :                                              *            
      *============================================================*            
      * OBJECTIVE :  TO GENERATE A REPORT ON INELIGIBLE REQUEST    *            
      *              FOR THIRD CHILD PRIORITY (TCP) SCHEME         *            
      *============================================================*            
      * FILE DESCRIPTION :                                         *            
      * DD NAME   I/O/E  MODE        LEN   REMARKS                 *            
      * --------  -----  ----------  ---   ---------------------   *            
      * BP13F730    I    SEQUENTIAL        REGISTRATION NUMBERS    *            
      * BP13K825    I    VSAM DATASET      DATA                    *            
      * BP13K060    I    VSAM DATASET      CODES                   *            
      * BP13L02Q    O    SEQUENTIAL  136   REPORT FILE             *            
      **************************************************************            
      *   MODIFICATION LOG                                         *            
      *   ----------------                                         *            
      *   MODIFY DATE       CHGE REQ  DESCRIPTION OF CHANGE        *            
      *   ------ ---------- --------  ----------------------       *            
      *   IL5    17042009   BP133533  TO CHANGE REPORT LENGTH FROM *            
      *                               136 TO 132                   *            
      *   ZDD1   21042009   BP133533  TO CATER THE END OF FILE     *            
      *                               STATUS FOR BP13K825          *            
      *   ZDD1   10122010   BP134067  TO CATER FOR NEW FIELD       *            
      **************************************************************            
                                                                                
      ***********************                                                   
       ENVIRONMENT DIVISION.                                                    
      ***********************                                                   
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730  ASSIGN TO BP13F730                                  
                           FILE STATUS IS WS-F730-STATUS.                       
                                                                                
           SELECT BP13K825  ASSIGN        TO BP13K825                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K825-KEY-FLD                       
                            FILE STATUS   IS WS-K825-STATUS.                    
                                                                                
           SELECT BP13K060  ASSIGN        TO BP13K060                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K060-KEY-FLD                       
                            FILE STATUS   IS WS-K060-STATUS.                    
                                                                                
           SELECT BP13KB78  ASSIGN        TO BP13KB78                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KB78-KEY-FLD                       
                            FILE STATUS   IS WS-KB78-STATUS.                    
                                                                                
           SELECT BP13L02Q  ASSIGN TO BP13L02Q.                                 
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F730         RECORD CONTAINS 500 CHARACTERS                      
                            RECORDING MODE IS F.                                
       COPY BP13F730.                                                           
                                                                                
       FD BP13K825.                                                             
       COPY BP13K825.                                                           
                                                                                
       FD BP13K060.                                                             
       COPY BP13K060.                                                           
                                                                                
       FD BP13KB78.                                                             
       COPY BP13KB78.                                                           
                                                                                
       FD  BP13L02Q         RECORD CONTAINS 132 CHARACTERS                      
                            LABEL RECORDS ARE OMITTED                           
                            RECORDING MODE IS F.                                
                                                                                
       01 BP13L02Q-REC       PIC X(132).                                        
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01 FILE-STATUS.                                                          
          03 WS-K825-STATUS       PIC 9(2)     VALUE ZEROES.                    
          03 WS-K060-STATUS       PIC 9(2)     VALUE ZEROES.                    
          03 WS-F730-STATUS       PIC 9(2)     VALUE ZEROES.                    
          03 WS-KB78-STATUS       PIC 9(2)     VALUE ZEROES.                    
                                                                                
       01 FLAGS.                                                                
          03 WS-F730-EOF          PIC X(1)     VALUE 'N'.                       
          03 WS-K825-EOF          PIC X(1)     VALUE 'N'.                       
          03 WS-K060-NOTFD        PIC X(1)     VALUE 'N'.                       
          03 WS-K825-NOTFD        PIC X(1)     VALUE 'N'.                       
          03 WS-FOUND-KB78        PIC X(1)     VALUE 'N'.                       
          03 WS-PRINT-NOT         PIC X(1)     VALUE 'N'.                       
                                                                                
       01 COUNTERS.                                                             
          03 WS-SNO               PIC 9(2)     VALUE ZEROES.                    
          03 WS-CHILD-CTR         PIC 9(2)     VALUE ZEROES.                    
          03 WS-CTR               PIC 9(3)     VALUE ZEROES.                    
          03 WS-COUNTER           PIC 9(6)     VALUE ZEROES.                    
          03 WS-PAGE-CNTR         PIC 9(6)     VALUE ZEROES.                    
          03 WS-LINE-CNTR         PIC 9(2)     VALUE ZEROES.                    
          03 WS-F730-READ         PIC 9(6)     VALUE ZEROES.                    
          03 WS-K825-READ         PIC 9(6)     VALUE ZEROES.                    
          03 WS-K060-READ         PIC 9(6)     VALUE ZEROES.                    
          03 WS-KB78-READ         PIC 9(6)     VALUE ZEROES.                    
          03 WS-NOTFD-CNT-K825    PIC 9(6)     VALUE ZEROES.                    
          03 WS-NOTFD-CNT-K060    PIC 9(6)     VALUE ZEROES.                    
          03 WS-NOTFD-CNT-KB78    PIC 9(6)     VALUE ZEROES.                    
          03 WS-EXCEPTION         PIC 9(6)     VALUE ZEROES.                    
                                                                                
       01 ACCUMULATORS.                                                         
          03 WS-BP13L02Q-WRITE     PIC 9(6)     VALUE ZEROES.                   
                                                                                
       01 VARIABLES.                                                            
          05 WS-REGN-PREV         PIC X(8).                                     
          05 WS-K825-DETAILS         OCCURS 10 TIMES.                           
             10 WS-NRIC              PIC X(9).                                  
      *      10 WS-NAME              PIC X(46).                                 
             10 WS-NAME              PIC X(35).                                 
             10 WS-PHONE             PIC X(8).                                  
             10 WS-REL-CODE          PIC X(2).                                  
             10 WS-STAT-CODE         PIC X(1).                                  
             10 WS-DOB               PIC X(8).                                  
             10 WS-CITI-CODE         PIC X(2).                                  
             10 WS-REL-DESC          PIC X(12).                                 
             10 WS-STAT-DESC         PIC X(9).                                  
             10 WS-CITI-DESC         PIC X(20).                                 
                                                                                
       01 DATES.                                                                
          03 WS-CURRENT-DATE.                                                   
             05 WS-CURR-CC        PIC 9(2).                                     
             05 WS-CURR-YY        PIC 9(2).                                     
             05 WS-CURR-MM        PIC 9(2).                                     
             05 WS-CURR-DD        PIC 9(2).                                     
                                                                                
       01 BLANK-LINE.                                                           
          03 FILLER               PIC X(132)   VALUE SPACES.                    
                                                                                
       01 HDNG-1.                                                               
           05  FILLER              PIC X(08)     VALUE 'BP13L02Q'.              
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(25)     VALUE SPACES.                  
           05  FILLER              PIC X(41)     VALUE                          
               'S Y S T E M    O F    C O M M I T M E N T'.                     
          03 FILLER               PIC X(12)     VALUE SPACES.                   
          03 FILLER               PIC X(7)     VALUE 'DATE : '.                 
          03 HDNG-CURR-DD         PIC X(2)     VALUE SPACES.                    
          03 FILLER               PIC X(1)     VALUE '/'.                       
          03 HDNG-CURR-MM         PIC X(2)     VALUE SPACES.                    
          03 FILLER               PIC X(1)     VALUE '/'.                       
          03 HDNG-CURR-CC         PIC X(2)     VALUE SPACES.                    
          03 HDNG-CURR-YY         PIC X(2)     VALUE SPACES.                    
          03 FILLER               PIC X(2)     VALUE SPACES.                    
          03 FILLER               PIC X(07)     VALUE 'PAGE : '.                
          03 HDNG-PAGE-CNTR       PIC Z,ZZ9    VALUE SPACES.                    
                                                                                
       01 HDNG-2.                                                               
          03 FILLER               PIC X(14)    VALUE 'PROJECT CODE :'.          
          03 FILLER               PIC X(01)    VALUE SPACES.                    
          03 L02Q-PROJ-NAME       PIC X(25)    VALUE SPACES.                    
          03 FILLER               PIC X(50)    VALUE                            
             'DAILY REPORT ON INELIGIBLE REQUEST FOR THIRD CHILD'.              
                                                                                
       01 HDNG-3.                                                               
          03 FILLER               PIC X(56)    VALUE SPACES.                    
          03 FILLER               PIC X(21)    VALUE                            
                   'PRIORITY (TCP) SCHEME'.                                     
          03 FILLER               PIC X(55)     VALUE SPACES.                   
                                                                                
       01 HDNG-4.                                                               
          03 FILLER               PIC X(05)    VALUE 'S/NO.'.                   
          03 FILLER               PIC X(01)    VALUE SPACES.                    
          03 FILLER               PIC X(08)    VALUE 'REGN NO.'.                
          03 FILLER               PIC X(2)     VALUE SPACES.                    
          03 FILLER               PIC X(17)    VALUE                            
                   'NAME OF APPL/OCCU'.                                         
          03 FILLER               PIC X(20)    VALUE SPACES.                    
          03 FILLER               PIC X(08)    VALUE 'PHONE NO'.                
          03 FILLER               PIC X(04)    VALUE SPACES.                    
          03 FILLER               PIC X(07)    VALUE 'NRIC NO'.                 
          03 FILLER               PIC X(05)    VALUE SPACES.                    
          03 FILLER               PIC X(12)    VALUE 'RELATIONSHIP'.            
          03 FILLER               PIC X(2)     VALUE SPACES.                    
          03 FILLER               PIC X(09)    VALUE 'M. STATUS'.               
          03 FILLER               PIC X(2)     VALUE SPACES.                    
          03 FILLER               PIC X(3)     VALUE 'DOB'.                     
          03 FILLER               PIC X(9)     VALUE SPACES.                    
          03 FILLER               PIC X(11)    VALUE 'CITIZENSHIP'.             
                                                                                
       01 HDNG-5.                                                               
          03 FILLER               PIC X(132)   VALUE ALL '-'.                   
                                                                                
       01 HDNG-6.                                                               
          03 FILLER               PIC X(06)    VALUE SPACES.                    
          03 FILLER               PIC X(10)    VALUE 'CORR ADD: '.              
                                                                                
       01 DETL-1.                                                               
          03 DETL-SNO             PIC 9(2)     VALUE ZEROS.                     
          03 FILLER               PIC X(4)     VALUE SPACES.                    
          03 DETL-REGN            PIC X(08)    VALUE SPACES.                    
          03 FILLER               PIC X(02)    VALUE SPACES.                    
      *   03 DETL-NAME            PIC X(46)    VALUE SPACES.                    
          03 DETL-NAME            PIC X(35)    VALUE SPACES.                    
          03 FILLER               PIC X(02)    VALUE SPACES.                    
          03 DETL-PHONE           PIC X(09)    VALUE SPACES.                    
          03 FILLER               PIC X(3)     VALUE SPACES.                    
          03 DETL-NRIC            PIC X(09)    VALUE SPACES.                    
          03 FILLER               PIC X(3)     VALUE SPACES.                    
          03 DETL-REL             PIC X(12)    VALUE SPACES.                    
          03 FILLER               PIC X(2)     VALUE SPACES.                    
          03 DETL-STAT            PIC X(09)    VALUE SPACES.                    
          03 FILLER               PIC X(2)     VALUE SPACES.                    
          03 DETL-DOB-DD          PIC X(2)     VALUE SPACES.                    
          03 FILLER-1             PIC X(1)     VALUE '/'.                       
          03 DETL-DOB-MM          PIC X(2)     VALUE SPACES.                    
          03 FILLER-2             PIC X(1)     VALUE '/'.                       
          03 DETL-DOB-CC          PIC X(2)     VALUE SPACES.                    
          03 DETL-DOB-YY          PIC X(2)     VALUE SPACES.                    
          03 FILLER               PIC X(2)     VALUE SPACES.                    
          03 DETL-CTZNSHP         PIC X(20)    VALUE SPACES.                    
                                                                                
       01 DETL-2.                                                               
          03 FILLER               PIC X(6)     VALUE SPACES.                    
          03 DETL-ADDR-EMAIL      PIC X(110)   VALUE SPACES.                    
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *****************************************************************         
       MAIN.                                                                    
      *****************************************************************         
                                                                                
            PERFORM 0000-OPENING       THRU 0000-EXIT.                          
            PERFORM 1000-READ-BP13F730 THRU 1000-EXIT.                          
            PERFORM 5000-PRINT-HEADING THRU 5000-EXIT.                          
            PERFORM 2000-PROCESS-DATA  THRU 2000-EXIT                           
                        UNTIL WS-F730-EOF = 'Y'.                                
            PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                          
                                                                                
      *****************************************************************         
       0000-OPENING.                                                            
      *****************************************************************         
                                                                                
            OPEN INPUT  BP13F730                                                
                        BP13K825                                                
                        BP13K060                                                
                        BP13KB78                                                
                 OUTPUT BP13L02Q.                                               
                                                                                
            IF WS-K825-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING BP13K825 ERROR ' WS-K825-STATUS                 
               MOVE     WS-K825-STATUS      TO RETURN-CODE                      
               PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                      
            END-IF.                                                             
                                                                                
            IF WS-K060-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING BP13K060 ERROR ' WS-K060-STATUS                 
               MOVE     WS-K060-STATUS      TO RETURN-CODE                      
               PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                      
            END-IF.                                                             
                                                                                
            IF WS-KB78-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING BP13KB78 ERROR ' WS-KB78-STATUS                 
               MOVE     WS-KB78-STATUS      TO RETURN-CODE                      
               PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                      
            END-IF.                                                             
                                                                                
            MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE.                      
            MOVE WS-CURR-DD TO HDNG-CURR-DD.                                    
            MOVE WS-CURR-MM TO HDNG-CURR-MM.                                    
            MOVE WS-CURR-CC TO HDNG-CURR-CC.                                    
            MOVE WS-CURR-YY TO HDNG-CURR-YY.                                    
                                                                                
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       1000-READ-BP13F730.                                                      
      *****************************************************************         
                                                                                
            READ BP13F730                                                       
               AT END MOVE 'Y' TO WS-F730-EOF                                   
               GO TO 1000-EXIT.                                                 
                                                                                
            ADD 1      TO WS-F730-READ.                                         
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       2000-PROCESS-DATA.                                                       
      *****************************************************************         
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1                                   
             UNTIL WS-CTR > 10                                                  
                MOVE SPACES TO WS-K825-DETAILS (WS-CTR)                         
           END-PERFORM.                                                         
                                                                                
           MOVE SPACES TO K825-KEY-FLD.                                         
           MOVE F730-NUM-REGN TO K825-NUM-REGN.                                 
                                                                                
           START BP13K825 KEY IS >= K825-KEY-FLD END-START.                     
           MOVE 0 TO WS-CTR.                                                    
           MOVE 0 TO WS-CHILD-CTR.                                              
           MOVE 'N' TO WS-PRINT-NOT.                                            
           MOVE 'N' TO WS-K825-EOF.                                             
                                                                                
           PERFORM 3000-READ-K825 THRU 3000-EXIT.                               
                                                                                
           PERFORM 3500-PROCESS-K825 THRU 3500-EXIT                             
              UNTIL F730-NUM-REGN NOT EQUAL K825-NUM-REGN                       
              OR WS-K825-EOF = 'Y'                                              
              OR WS-PRINT-NOT = 'Y'.                                            
                                                                                
                                                                                
           IF WS-PRINT-NOT = 'N'                                                
              MOVE 1 TO WS-CTR                                                  
              PERFORM 5100-PRINT-DETAILS THRU 5100-EXIT                         
                 UNTIL WS-CTR > 10 OR WS-NAME(WS-CTR) = SPACES                  
              MOVE SPACES              TO BP13KB78-REC                          
                                          DETL-2                                
              MOVE F730-NUM-REGN       TO KB78-NUM-REGN                         
              PERFORM 4600-READ-KB78   THRU 4600-EXIT                           
              IF WS-FOUND-KB78 = 'Y'                                            
                WRITE BP13L02Q-REC    FROM HDNG-6                               
                WRITE BP13L02Q-REC    FROM DETL-2                               
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
           PERFORM 1000-READ-BP13F730 THRU 1000-EXIT.                           
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       3000-READ-K825.                                                          
      *****************************************************************         
                                                                                
            ADD 1 TO WS-CTR.                                                    
                                                                                
            READ BP13K825 NEXT RECORD.                                          
                                                                                
            IF WS-K825-STATUS  =  00                                            
               ADD 1 TO WS-K825-READ                                            
               IF F730-NUM-REGN = K825-NUM-REGN                                 
                  MOVE K825-NUM-NRIC TO WS-NRIC(WS-CTR)                         
                  MOVE K825-NME-OCCP TO WS-NAME(WS-CTR)                         
                  MOVE K825-NUM-PHONE TO WS-PHONE(WS-CTR)                       
                  MOVE K825-NUM-RELATIONSHIP TO WS-REL-CODE(WS-CTR)             
                  MOVE K825-NUM-MARITAL-STATUS TO WS-STAT-CODE(WS-CTR)          
                  MOVE K825-DTE-BIRTH TO WS-DOB(WS-CTR)                         
                  MOVE K825-NUM-CITIZENSHIP TO WS-CITI-CODE(WS-CTR)             
               END-IF                                                           
            ELSE                                                                
               IF WS-K825-STATUS  =  10                                         
                  MOVE 'Y'  TO WS-K825-EOF                                      
               ELSE                                                             
                 IF WS-K825-STATUS  =  23                                       
                    ADD 1 TO WS-NOTFD-CNT-K825                                  
                    MOVE  'Y'  TO WS-K825-NOTFD                                 
                    DISPLAY 'REC NOT FND IN K825 REGN : ' K825-NUM-REGN         
                 ELSE                                                           
                   DISPLAY 'ERROR READING BP13K825   : ' WS-K825-STATUS         
                   MOVE     WS-K825-STATUS      TO RETURN-CODE                  
                   PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                  
                 END-IF                                                         
               END-IF                                                           
            END-IF.                                                             
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       3500-PROCESS-K825.                                                       
      *****************************************************************         
                                                                                
           PERFORM 4000-GET-DETAILS THRU 4000-EXIT.                             
                                                                                
           PERFORM 3000-READ-K825 THRU 3000-EXIT.                               
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       4000-GET-DETAILS.                                                        
      *****************************************************************         
                                                                                
            IF K825-NUM-CITIZENSHIP = SPACES                                    
               CONTINUE                                                         
            ELSE                                                                
               MOVE SPACES TO K060-KEY-FLD                                      
               MOVE '25' TO K060-SERIAL-NO                                      
               MOVE WS-CITI-CODE(WS-CTR) TO K060-CODE                           
               PERFORM 4500-READ-K060 THRU 4500-EXIT                            
               MOVE K060-DESC TO WS-CITI-DESC(WS-CTR)                           
            END-IF.                                                             
                                                                                
            IF K825-NUM-RELATIONSHIP = SPACES                                   
               CONTINUE                                                         
            ELSE                                                                
               MOVE SPACES TO K060-KEY-FLD                                      
               MOVE '35' TO K060-SERIAL-NO                                      
               MOVE WS-REL-CODE(WS-CTR) TO K060-CODE                            
               PERFORM 4500-READ-K060 THRU 4500-EXIT                            
               MOVE K060-DESC TO WS-REL-DESC(WS-CTR)                            
               IF WS-REL-DESC(WS-CTR) = 'CHILD'                                 
                  ADD 1 TO WS-CHILD-CTR                                         
                  IF WS-CHILD-CTR >= 3                                          
                     PERFORM 4100-VALIDATE-CHILD THRU 4100-EXIT                 
                  END-IF                                                        
               END-IF                                                           
            END-IF.                                                             
                                                                                
            IF K825-NUM-MARITAL-STATUS = SPACES                                 
               CONTINUE                                                         
            ELSE                                                                
               MOVE SPACES TO K060-KEY-FLD                                      
               MOVE '21' TO K060-SERIAL-NO                                      
               MOVE WS-STAT-CODE(WS-CTR) TO K060-CODE                           
               PERFORM 4500-READ-K060 THRU 4500-EXIT                            
               MOVE K060-DESC TO WS-STAT-DESC(WS-CTR)                           
            END-IF.                                                             
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       4100-VALIDATE-CHILD.                                                     
      *****************************************************************         
                                                                                
           IF WS-CHILD-CTR = 3                                                  
           AND WS-DOB(WS-CTR) >= '19870101'                                     
           AND WS-CITI-DESC(WS-CTR) = 'SINGAPORE CITIZEN'                       
              ADD 1 TO WS-EXCEPTION                                             
              MOVE 'Y' TO WS-PRINT-NOT                                          
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       4500-READ-K060.                                                          
      *****************************************************************         
            READ BP13K060 KEY IS K060-KEY-FLD.                                  
                                                                                
            IF WS-K060-STATUS  =  00                                            
               ADD 1 TO WS-K060-READ                                            
            ELSE                                                                
               IF WS-K060-STATUS  =  23                                         
                  ADD 1 TO WS-NOTFD-CNT-K060                                    
                  MOVE  'Y'  TO WS-K060-NOTFD                                   
                  DISPLAY 'REC NOT FND IN K060 SN   : ' K060-SERIAL-NO          
               ELSE                                                             
                  DISPLAY 'ERROR READING BP13K060   : ' WS-K060-STATUS          
                  MOVE     WS-K060-STATUS      TO RETURN-CODE                   
                  PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                   
               END-IF                                                           
            END-IF.                                                             
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       4600-READ-KB78.                                                          
      *****************************************************************         
            READ BP13KB78.                                                      
                                                                                
            IF WS-KB78-STATUS  =  00                                            
               ADD 1 TO WS-KB78-READ                                            
               MOVE 'Y'   TO WS-FOUND-KB78                                      
               STRING KB78-NUM-BLK ' #' KB78-NUM-LEVEL '-'                      
                    KB78-NUM-MAIN-UNIT ' ' KB78-NUM-SUB-UNIT ' '                
                     KB78-NUM-CORRESP-STREET ' ' KB78-CDE-CORRESP-POST          
                      ' ' KB78-NUM-EMAIL DELIMITED BY SIZE INTO                 
                      DETL-ADDR-EMAIL                                           
            ELSE                                                                
               IF WS-KB78-STATUS  =  23                                         
                  MOVE 'N'   TO WS-FOUND-KB78                                   
                  ADD 1 TO WS-NOTFD-CNT-KB78                                    
                  DISPLAY 'REC NOT FND IN KB78 SN   : ' KB78-KEY-FLD            
               ELSE                                                             
                  DISPLAY 'ERROR READING BP13KB78   : ' WS-KB78-STATUS          
                  MOVE     WS-KB78-STATUS      TO RETURN-CODE                   
                  PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                   
               END-IF                                                           
            END-IF.                                                             
                                                                                
       4600-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       5000-PRINT-HEADING.                                                      
      *****************************************************************         
            EVALUATE F730-CDE-NT1                                               
                WHEN 'BD '                                                      
                   MOVE 'BELVIA             ' TO L02Q-PROJ-NAME                 
                WHEN 'HG'                                                       
                   MOVE 'PARKLAND RESIDENCES' TO L02Q-PROJ-NAME                 
                WHEN 'JW'                                                       
                   MOVE 'LAKE VISTA         ' TO L02Q-PROJ-NAME                 
                WHEN 'CL'                                                       
                   MOVE 'TRIVELIS           ' TO L02Q-PROJ-NAME                 
                WHEN 'PRC'                                                      
                   MOVE 'PASIR RIS ONE      ' TO L02Q-PROJ-NAME                 
                WHEN OTHER                                                      
                   MOVE SPACES         TO L02Q-PROJ-NAME                        
            END-EVALUATE.                                                       
                                                                                
            ADD 1                TO WS-PAGE-CNTR.                               
            MOVE WS-PAGE-CNTR    TO HDNG-PAGE-CNTR.                             
                                                                                
            WRITE BP13L02Q-REC    FROM BLANK-LINE AFTER PAGE.                   
            WRITE BP13L02Q-REC    FROM HDNG-1.                                  
            WRITE BP13L02Q-REC    FROM HDNG-2.                                  
            WRITE BP13L02Q-REC    FROM HDNG-3.                                  
            WRITE BP13L02Q-REC    FROM BLANK-LINE.                              
            WRITE BP13L02Q-REC    FROM HDNG-4.                                  
            WRITE BP13L02Q-REC    FROM HDNG-5.                                  
            WRITE BP13L02Q-REC    FROM BLANK-LINE.                              
            MOVE 8               TO WS-LINE-CNTR.                               
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       5100-PRINT-DETAILS.                                                      
      *****************************************************************         
                                                                                
            IF WS-LINE-CNTR > 55                                                
               MOVE 0  TO WS-LINE-CNTR                                          
               PERFORM 5000-PRINT-HEADING THRU 5000-EXIT.                       
                                                                                
            IF F730-NUM-REGN NOT EQUAL WS-REGN-PREV                             
               ADD 1 TO WS-SNO                                                  
               MOVE WS-SNO TO DETL-SNO                                          
               MOVE F730-NUM-REGN TO WS-REGN-PREV                               
                                     DETL-REGN                                  
            END-IF                                                              
                                                                                
            MOVE WS-NAME(WS-CTR)          TO DETL-NAME                          
            MOVE WS-PHONE(WS-CTR)         TO DETL-PHONE                         
            MOVE WS-NRIC(WS-CTR)          TO DETL-NRIC                          
            MOVE WS-REL-DESC(WS-CTR)      TO DETL-REL                           
            MOVE WS-STAT-DESC(WS-CTR)     TO DETL-STAT                          
            MOVE WS-DOB(WS-CTR)(7:2)      TO DETL-DOB-DD                        
            MOVE '/'                      TO FILLER-1                           
            MOVE WS-DOB(WS-CTR)(5:2)      TO DETL-DOB-MM                        
            MOVE '/'                      TO FILLER-2                           
            MOVE WS-DOB(WS-CTR)(1:2)      TO DETL-DOB-CC                        
            MOVE WS-DOB(WS-CTR)(3:2)      TO DETL-DOB-YY                        
            MOVE WS-CITI-DESC(WS-CTR)     TO DETL-CTZNSHP                       
                                                                                
            WRITE BP13L02Q-REC             FROM DETL-1.                         
            ADD 1 TO WS-LINE-CNTR                                               
                     WS-CTR                                                     
                     WS-BP13L02Q-WRITE.                                         
                                                                                
            MOVE SPACES TO DETL-1.                                              
                                                                                
       5100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       9000-CLOSE-ROUTINE.                                                      
      *****************************************************************         
                                                                                
            CLOSE BP13F730                                                      
                  BP13K825                                                      
                  BP13K060                                                      
                  BP13KB78                                                      
                  BP13L02Q.                                                     
                                                                                
            IF WS-K825-STATUS NOT = 00                                          
               DISPLAY 'ERROR CLOSING BP13K825 ... ' WS-K825-STATUS             
               MOVE WS-K825-STATUS TO RETURN-CODE                               
            END-IF.                                                             
                                                                                
            IF WS-K060-STATUS NOT = 00                                          
               DISPLAY 'ERROR CLOSING BP13K060 ... ' WS-K060-STATUS             
               MOVE WS-K060-STATUS TO RETURN-CODE                               
            END-IF.                                                             
                                                                                
            IF WS-KB78-STATUS NOT = 00                                          
               DISPLAY 'ERROR CLOSING BP13KB78 ... ' WS-KB78-STATUS             
               MOVE WS-KB78-STATUS TO RETURN-CODE                               
            END-IF.                                                             
                                                                                
            DISPLAY '                          '.                               
            DISPLAY '  PROGRAM : BP13C02Q      '.                               
            DISPLAY 'BP13F730 RECORDS READ   : ' WS-F730-READ.                  
            DISPLAY 'BP13K825 RECORDS READ   : ' WS-K825-READ.                  
            DISPLAY 'BP13K060 RECORDS READ   : ' WS-K060-READ.                  
            DISPLAY 'BP13KB78 RECORDS READ   : ' WS-KB78-READ.                  
            DISPLAY 'BP13L02Q RECORDS WRITTEN: ' WS-BP13L02Q-WRITE.             
            DISPLAY 'NUMBER OF EXCEPTIONS    : ' WS-EXCEPTION.                  
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *======================  END OF PROGRAM  ======================*          
