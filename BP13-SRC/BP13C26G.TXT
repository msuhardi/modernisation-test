       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C26G.                                                 
      *AUTHOR.        RACHELLE SAN BUENAVENTURA.                                
      *DATE-WRITTEN.  MAY 4 2007.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE: SUBROUTINE TO PRINT SALE OF FLATS               *            
      *                                                            *            
      * INPUT FILES: BP13F999                                      *            
      *              BP13K813                                      *            
      *              BM06K110                                       *           
      *              BM06K100                                       *           
      *                                                            *            
      * OUTPUT FILE: BP13L26G                                      *            
      *----------------------------------------------------------- *            
      * CHG REF  DATE     BY   DESCRIPTION                         *            
      * -------- -------- ---- ---------------------------------   *            
      * BP133056 20070504 RB12 NEW PROGRAM                         *            
      * BP133390 20080731 RB12 EXPAND F999 FROM 200 TO 300 AND     *            
      *                        PRINT ECD                           *            
      * BP133610 20090511 ESA1 TO REPLACE BP13K767 W/ BP13K816     *            
      * BP133736 20091102 RB12 REVISE LOGIC FOR CHANGE IN NT       *            
      * BP133772 20091113 ESA1 CATER FOR NPL W/ MONTH & YR ON TITLE*            
      * BP133772 20100114 ESA1 TO ADD SPACES BETWEEN REMARKS       *            
      * BP133896 20100507 ESA1 TO CATER FOR CHANGE IN NT           *            
      * BP133921 20100726 ESA1 TO CATER FOR 1STD & 4STD FOR STUDIO *            
      * BP134043 20101028 ESA1 TO REPLACE ECD WITH PCD FOR SBF     *            
      * BP134044 20101210 ESA1 TO CORRECT CONDITION FOR PCD        *            
      * BP134044 20110107 ESA1 TO CHANGE HEADER FOR GRO FLATLIST   *            
      * BP134208 20110412 ESA1 TO USE BP13UD FORM                  *            
      * BP134353 20110909 ESA1 TO CHANGE THE FONT FOR STUDIO       *            
      * BP134471 20120313 ESA1 TO CATER FOR MAX SPR QUOTA          *            
      * BP134513 20120414 ESA1 TO CATER FOR MGPS                    *           
      * BP134634 20120808 ESA1 TO REMOVE NEIGH&CNTRCT IN PRINTING   *           
      * BP134681 20120717 ESA1 TO REPLACE BP13K816 W/ BP13K813      *           
      * BP134909 20130520 ESA1 TO CHANGE LCD FOR SA COMPLETED       *           
      *                        TO CATER FOR SH FLAT DESCRIPTION     *           
      *                        TO CATER FOR PCD FOR NPL UNIT        *           
      * BP134959 20130610 ESA1 TO CATER FOR LBS                     *           
      * BP135151 20131209 ESA1 TO CATER FOR PRINTING OF POC         *           
      * BP135190 20131204 SMR2 EXPAND BP13K813 TO 1000              *           
      * BP135331 20140611 ESA1 TO CORRECT PRINTING OF FLAT CODE     *           
      * BP135198 20150227 ESA1 TO CORRECT PRINTING OF DJDE          *           
      * BP135555 20150320 ESA1 TO REPLACE BP13C910 W/ BP13C91J      *           
      * BP135799 20150609 ESA1 TO REMOVE DISPLAY OF MODEL-DESIGN    *           
      * BP138637 20211012 EL27 PRINT DPD AS EDPD IF SBF             *           
      * BP139089 20220310 EL27 PRINT DPD AS EDPD IF OBF             *           
      * BP139436 20230125 MRR5 CALL NEW SUBR BP13CRG3 TO READ THE   *           
      *                        BLDG_FTR TABLE FOR CAR-LITE TEXT     *           
      *                        CHECK MODL DESGN 75/76 FOR PLH TEXT  *           
      * BP139436 20230306 MRR5 FIX REMARKS ISSUE                    *           
      * BP139685 MRR5 20230929 CATER FOR SER 50YR                   *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F999  ASSIGN TO BP13F999.                                 
                                                                                
           SELECT BM06K100  ASSIGN TO BM06K100                                  
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS RANDOM                              
                            RECORD KEY   IS K100-KEY-FLD                        
                            FILE STATUS  IS WS-K100-STATUS.                     
                                                                                
           SELECT BM06K110  ASSIGN       TO BM06K110                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS RANDOM                              
                            RECORD KEY   IS K110-KEY-FLD                        
                            FILE STATUS  IS WS-K110-STATUS.                     
                                                                                
           SELECT BP13L26G  ASSIGN TO BP13L26G.                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
      *--------------------------------------------------------------           
       FILE SECTION.                                                            
      *--------------------------------------------------------------           
                                                                                
       FD  BP13F999                                                             
           RECORD CONTAINS 300 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       COPY BP13F999.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K100.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD  BP13L26G                                                             
           RECORD CONTAINS 100 CHARACTERS                                       
           LABEL RECORDS OMITTED                                                
           RECORDING MODE IS F.                                                 
       01  L26G-PRTREC                    PIC X(100).                           
                                                                                
      *--------------------------------------------------------------           
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------           
       01  WS-WORK-VAR.                                                         
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-DATE            PIC X(08) VALUE SPACES.               
               10  FILLER                 PIC X(13) VALUE SPACES.               
           05  WS-DISP-DATE               PIC X(10) VALUE SPACES.               
           05  WS-NUM-MONTH               PIC X(02) VALUE SPACES.               
           05  WS-TXT-QTR                 PIC X(07) VALUE SPACES.               
           05  WS-DTE-BALLOT              PIC X(10) VALUE SPACES.               
           05  WS-BLOCK-INFO              PIC X(30) OCCURS 8.                   
           05  WS-REMARKS                 PIC X(30) OCCURS 10.                  
           05  WS-PREV-CDE-NT             PIC X(03) VALUE SPACES.               
           05  WS-PREV-CNTRCT-BLOCK.                                            
               10  WS-PREV-NEIGHBOURHOOD  PIC X(04) VALUE SPACES.               
               10  WS-PREV-CONTRACT-NO    PIC X(04) VALUE SPACES.               
               10  WS-PREV-BLOCK-NO       PIC X(05) VALUE SPACES.               
           05  WS-NME-ESTATE              PIC X(20) VALUE SPACES.               
           05  WS-NUM-EST                 PIC X(03) VALUE SPACES.               
           05  WS-TMP-CNTRCT-TYP          PIC X(30) VALUE SPACES.               
           05  WS-SUB                     PIC 9(02) VALUE ZEROES.               
           05  WS-MAX-REM                 PIC 9(02) VALUE ZEROES.               
           05  WS-CTR                     PIC 9(03) VALUE ZEROES.               
           05  WS-NAME-MONTH              PIC X(03) VALUE SPACES.               
           05  WS-RETURN-CODE             PIC 9(02) VALUE ZEROES.               
           05  WS-NUM-MAX-CAT.                                                  
               10  WS-NUM-MAX-CAT1        PIC ZZ9.                              
               10  WS-NUM-MAX-CAT2        PIC ZZ9.                              
               10  WS-NUM-MAX-CAT3        PIC ZZ9.                              
               10  WS-NUM-MAX-SPR         PIC ZZ9.                              
           05  WS-PREV-FLAT-TYPE          PIC X(03) VALUE SPACES.               
           05  WS-SELTYPE                 PIC X(40) VALUE SPACES.               
           05  WS-CAR-LITE-TAG            PIC X(01) VALUE SPACES.               
                                                                                
       01  WS-PARSING.                                                          
           05 WS-STRING-TO-PARSE            PIC X(80).                          
           05 WS-PARSED-STRING              PIC X(80).                          
           05 WS-STRING-LENGTH              PIC 9(03).                          
           05 WS-CUR-POS-SP                 PIC 9(03).                          
           05 WS-CUR-POS-PS                 PIC 9(03).                          
           05 WS-SPACE-FLAG                 PIC X    VALUE 'N'.                 
           05 WS-INSERT-SPACE-FLAG          PIC X    VALUE 'N'.                 
                                                                                
       01  WS-TABLES.                                                           
           05  WS-MONTH-TABLE.                                                  
               10  FILLER                 PIC X(05) VALUE '01JAN'.              
               10  FILLER                 PIC X(05) VALUE '02FEB'.              
               10  FILLER                 PIC X(05) VALUE '03MAR'.              
               10  FILLER                 PIC X(05) VALUE '04APR'.              
               10  FILLER                 PIC X(05) VALUE '05MAY'.              
               10  FILLER                 PIC X(05) VALUE '06JUN'.              
               10  FILLER                 PIC X(05) VALUE '07JUL'.              
               10  FILLER                 PIC X(05) VALUE '08AUG'.              
               10  FILLER                 PIC X(05) VALUE '09SEP'.              
               10  FILLER                 PIC X(05) VALUE '10OCT'.              
               10  FILLER                 PIC X(05) VALUE '11NOV'.              
               10  FILLER                 PIC X(05) VALUE '12DEC'.              
           05  FILLER  REDEFINES WS-MONTH-TABLE.                                
               10  WS-MONTH-TBL  OCCURS 12 INDEXED BY WS-MNT-NDX.               
                   15  WS-MONTH-NUM       PIC X(02).                            
                   15  WS-MONTH-NAME      PIC X(03).                            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K853-STATUS             PIC 9(02) VALUE ZEROES.               
           05  WS-K820-STATUS             PIC 9(02) VALUE ZEROES.               
           05  WS-K010-STATUS             PIC 9(02) VALUE ZEROES.               
           05  WS-K110-STATUS             PIC 9(02) VALUE ZEROES.               
           05  WS-K100-STATUS             PIC 9(02) VALUE ZEROES.               
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-END-OF-F999-SW         PIC X(01) VALUE SPACES.                
               88  C-END-OF-F999         VALUE 'Y'.                             
           05  WS-BLKINFO-PRINTED-SW     PIC X(01) VALUE SPACES.                
               88  C-BLKINFO-PRINTED     VALUE 'Y'.                             
           05  WS-REMARKS-PRINTED-SW     PIC X(01) VALUE SPACES.                
               88  C-REMARKS-PRINTED     VALUE 'Y'.                             
           05  WS-FIRST-REC-READ-SW      PIC X(01) VALUE SPACES.                
               88  C-FIRST-REC-READ      VALUE 'Y'.                             
           05  WS-CHANGE-BLK-SW          PIC X(01) VALUE SPACES.                
               88  C-CHANGE-BLK          VALUE 'Y'.                             
           05  WS-K110-FND               PIC X(01)  VALUE 'N'.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F999-READ-CTR          PIC 9(06) VALUE ZEROES.                
           05  WS-LINE-CTR               PIC 9(02) VALUE ZEROES.                
           05  WS-LINE-MAX               PIC 9(02) VALUE 45.                    
           05  WS-LINE-LEFT              PIC 9(02) VALUE ZEROES.                
           05  WS-LINE-CTR-A             PIC 9(02) VALUE ZEROES.                
           05  WS-PAGE-CTR               PIC 9(03) VALUE ZEROES.                
           05  WS-PRT-CTR                PIC 9(06) VALUE ZEROES.                
                                                                                
      *-------------------------------                                          
      *  FOR BP13C91J                *                                          
      *-------------------------------                                          
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-ZONE        PIC X(03).                               
           05  WS-LINK-NUM-FT          PIC X(02).                               
           05  WS-LINK-DTE-BALLOT      PIC X(06).                               
       01  WS-BP13K813-REC             PIC X(1000).                             
       01  WS-ZONE-FND-FLAG            PIC X(01).                               
                                                                                
      *-------------------------------                                          
      *  FOR BP13CRG3                *                                          
      *-------------------------------                                          
       01  WS-CRG3-REC.                                                         
           05  WS-CRG3-BLDG-GL        PIC X(05).                                
           05  WS-CRG3-BLDGFTR-DTLS.                                            
               10 WS-CRG3-DTLS        OCCURS 100 TIMES.                         
                  15  WS-CRG3-TYPE-FTR     PIC X(15).                           
                  15  WS-CRG3-NUM-FTR      PIC X(8).                            
                  15  WS-CRG3-NUM-SQNC     PIC X(2).                            
                  15  WS-CRG3-QTY-FTR      PIC S9(4) USAGE COMP.                
                  15  WS-CRG3-QTY-MSRMNT   PIC S9(6)V9(2) USAGE COMP-3.         
                  15  WS-CRG3-DTE-LASTUPD  PIC X(26).                           
           05  WS-CRG3-CDE-SYSERR     PIC S9(3).                                
           05  WS-CRG3-CDE-ERROR      PIC X(04).                                
           05  WS-CRG3-ERR-MSG        PIC X(50).                                
                                                                                
       COPY BP13K813.                                                           
                                                                                
      *-------------------------------                                          
      *  REPORT LAYOUT               *                                          
      *-------------------------------                                          
       01  L26G-HEADER.                                                         
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  FILLER                  PIC X(32) VALUE                          
               'SALE OF STUDIO APARTMENT (SA)   '.                              
                                                                                
       01  L26G-HEADER-GRO.                                                     
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  FILLER                  PIC X(80) VALUE                          
               'SALE OF STUDIO APARTMENT (SA) UNDER THE GRASSROOTS ORGAN        
      -        'ISATION SCHEME'.                                                
                                                                                
       01  L26G-HEADER-MGP.                                                     
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  FILLER                  PIC X(80) VALUE                          
               'SALE OF STUDIO APARTMENT (SA) UNDER THE MULTI-GENERATION        
      -        ' PRIORITY SCHEME'.                                              
                                                                                
       01  L26G-HEADER-LBS.                                                     
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  FILLER                  PIC X(40) VALUE                          
               'SALE OF STUDIO APARTMENT (SA) UNDER THE '.                      
           05  L26G-LEASE-BAL          PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(20) VALUE                          
               ' YEARS LEASE BALANCE'.                                          
                                                                                
       01  L26G-HEADER-SA.                                                      
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  FILLER                  PIC X(32) VALUE                          
               'SALE OF STUDIO APARTMENT (SA) - '.                              
           05  L26G-ALLOC-DATE         PIC X(08)  VALUE SPACES.                 
           05  FILLER                  PIC X(109) VALUE SPACES.                 
                                                                                
       01  L26G-SUBHEADER.                                                      
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  L26G-NME-ESTATE         PIC X(62) VALUE SPACES.                  
           05  FILLER                  PIC X(07) VALUE '  PAGE:'.               
           05  L26G-PAGE               PIC ZZZ.                                 
                                                                                
       01  L26G-DETAIL-REC.                                                     
           05  FILLER                  PIC X(06) VALUE SPACES.                  
           05  L26G-BLK-DETAILS        PIC X(30) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  L26G-NUM-UNIT           PIC X(08) VALUE SPACES.                  
           05  L26G-NUM-FLAT-CODE      PIC X(04) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  L26G-FLOOR-AREA         PIC ZZZ.                                 
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  L26G-FLEXI-CDE          PIC X(03) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  L26G-IN-FLOOR-AREA      PIC ZZZ.                                 
           05  FILLER                  PIC X(04) VALUE SPACES.                  
           05  L26G-SELLING-PRICE      PIC Z,ZZZ,ZZZ.                           
                                                                                
       01  L26G-LINE.                                                           
           05  FILLER                  PIC X(04) VALUE SPACES.                  
           05  FILLER                  PIC X(72) VALUE ALL '_'.                 
                                                                                
      *-------------------------------                                          
      *  DJDE FOR FORM TYPE = 'D'    *                                          
      *-------------------------------                                          
       01  DJDE-FORM-BP13UD              PIC X(132)  VALUE                      
           '$DJDE$ JDE=13C26D,FORMS=BP13UI,COPIES=3,FONT=(R8TICP,R8TICP,        
      -    'RB12BP,R6BOBP,RM4SCP,R7TIBK),END;'.                                 
                                                                                
       01  DJDE-FORM-BP13UU              PIC X(132)  VALUE                      
           '$DJDE$ JDE=13C26D,FORMS=BP13UU,COPIES=3,FONT=(R8TICP,R8TICP,        
      -    'RB12BP,R6BOBP,RM4SCP,R7TIBK),END;'.                                 
                                                                                
       01  DJDE-FORM-LINE2               PIC X(132)  VALUE                      
           '$DJDE$ JDL=BP13,END;'.                                              
                                                                                
      *-------------------------------                                          
      *  DJDE FOR LAST PAGE          *                                          
      *-------------------------------                                          
       01  DJDE-LAST-PAGE                PIC X(80)  VALUE                       
           '$DJDE$ FORMS=BP13UD,END;'.                                          
                                                                                
      *-----------------------------------------------------------              
       LINKAGE SECTION.                                                         
      *-----------------------------------------------------------              
       01  WS-C260-PARAMETERS.                                                  
           05  WS-C260-FORM-TYPE         PIC X(01).                             
           05  WS-C260-FLAT-TYPE         PIC X(02).                             
           05  WS-C260-NUM-GRO           PIC X(01).                             
           05  WS-C260-RETURN-CODE       PIC 9(04).                             
                                                                                
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION USING WS-C260-PARAMETERS.                             
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-INITIALIZATION    THRU 1000-EXIT.                       
                                                                                
           WRITE L26G-PRTREC FROM DJDE-FORM-BP13UD AFTER PAGE.                  
      *    WRITE L26G-PRTREC FROM DJDE-FORM-LINE2.                              
                                                                                
           PERFORM 3000-PROCESS-RECORDS   THRU 3000-EXIT                        
             UNTIL C-END-OF-F999.                                               
                                                                                
           IF NOT C-BLKINFO-PRINTED OR                                          
              NOT C-REMARKS-PRINTED                                             
              PERFORM 3330-PRINT-REMAINING-INFO    THRU 3330-EXIT               
           END-IF.                                                              
                                                                                
      **-- PRINT LAST PAGE (FOR BP13R6)                                         
      *    MOVE SPACES        TO L26G-PRTREC.                                   
      *    WRITE L26G-PRTREC     FROM DJDE-LAST-PAGE  AFTER PAGE.               
      *    MOVE SPACES        TO L26G-PRTREC.                                   
      *    WRITE L26G-PRTREC     AFTER 1.                                       
                                                                                
           MOVE ZEROES        TO WS-RETURN-CODE.                                
                                                                                
           PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           GOBACK.                                                              
                                                                                
      *-------------------------------------------------------------            
       1000-INITIALIZATION.                                                     
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT    BP13F999                                               
                         BM06K110                                               
                         BM06K100                                               
                OUTPUT   BP13L26G.                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 OPEN ERROR, STATUS ' WS-K110-STATUS             
              MOVE WS-K110-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT                   
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K100 OPEN ERROR, STATUS ' WS-K100-STATUS             
              MOVE WS-K100-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT                   
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-SYSTEM-DATE.                       
           STRING WS-SYS-DATE(7:2) '/'                                          
                  WS-SYS-DATE(5:2) '/'                                          
                  WS-SYS-DATE(1:4)                                              
                  DELIMITED BY SPACES INTO WS-DISP-DATE.                        
                                                                                
           CALL 'O13K813A'         USING WS-RETURN-CODE.                        
                                                                                
           IF WS-RETURN-CODE NOT = ZEROS AND 97                                 
              DISPLAY 'OPEN ERROR, BP13K813 STATUS ' WS-RETURN-CODE             
              PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT                   
           END-IF.                                                              
                                                                                
           MOVE 'Y'       TO WS-FIRST-REC-READ-SW.                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-RECORDS.                                                    
      *-------------------------------------------------------------            
                                                                                
           READ BP13F999 AT END                                                 
                MOVE 'Y'     TO WS-END-OF-F999-SW                               
                GO TO 3000-EXIT.                                                
                                                                                
           ADD 1   TO WS-F999-READ-CTR.                                         
                                                                                
           PERFORM 3100-PRINT-DETAILS                THRU 3100-EXIT.            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-PRINT-DETAILS.                                                      
      *-------------------------------------------------------------            
                                                                                
           IF (F999-NEIGHBOURHOOD = WS-PREV-NEIGHBOURHOOD) AND                  
              (F999-CONTRACT-NO = WS-PREV-CONTRACT-NO) AND                      
              (F999-BLOCK-NO = WS-PREV-BLOCK-NO)                                
              PERFORM 3300-WRITE-DETAILS              THRU 3300-EXIT            
           ELSE                                                                 
              MOVE 'Y'     TO WS-CHANGE-BLK-SW                                  
              IF NOT C-BLKINFO-PRINTED OR                                       
                 NOT C-REMARKS-PRINTED                                          
                 PERFORM 3330-PRINT-REMAINING-INFO    THRU 3330-EXIT            
              END-IF                                                            
              PERFORM 3200-GET-CNTRCT-BLOCK-INFO      THRU 3200-EXIT            
              PERFORM 3300-WRITE-DETAILS              THRU 3300-EXIT            
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3200-GET-CNTRCT-BLOCK-INFO.                                              
      *-------------------------------------------------------------            
                                                                                
           MOVE F999-NEIGHBOURHOOD    TO WS-PREV-NEIGHBOURHOOD.                 
           MOVE F999-CONTRACT-NO      TO WS-PREV-CONTRACT-NO.                   
           MOVE F999-BLOCK-NO         TO WS-PREV-BLOCK-NO.                      
                                                                                
           PERFORM 3210-GET-BLK-DETAILS            THRU 3210-EXIT.              
           PERFORM 3220-GET-REMARKS                THRU 3220-EXIT.              
           MOVE ZEROES   TO WS-SUB.                                             
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3210-GET-BLK-DETAILS.                                                    
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES     TO WS-BLKINFO-PRINTED-SW.                            
                                                                                
           PERFORM VARYING WS-SUB FROM 1 BY 1 UNTIL WS-SUB > 8                  
              MOVE SPACES         TO WS-BLOCK-INFO (WS-SUB)                     
           END-PERFORM.                                                         
                                                                                
           MOVE ZEROES      TO WS-SUB.                                          
                                                                                
           IF F999-NUM-ZONE = SPACES OR LOW-VALUES                              
              MOVE F999-NME-ESTATE        TO WS-NME-ESTATE                      
           ELSE                                                                 
              PERFORM 3212-GET-ESTATE-NAME       THRU 3212-EXIT                 
           END-IF.                                                              
                                                                                
           IF F999-NUM-SELTY = 'BTO'                                            
              IF WS-NME-ESTATE(1:9) = 'TOA PAYOH' AND                           
                 WS-NUM-EST = '749'                                             
                 MOVE 'TOA PAYOH(BIDADARI)'  TO WS-NME-ESTATE                   
           END-IF.                                                              
                                                                                
           PERFORM 3213-CHANGE-CNTRCT-TYP    THRU 3213-EXIT.                    
      *    MOVE WS-TMP-CNTRCT-TYP             TO WS-BLOCK-INFO (1).             
           MOVE SPACES                        TO WS-BLOCK-INFO (1).             
                                                                                
      *    MOVE   WS-NME-ESTATE               TO WS-BLOCK-INFO (2).             
                                                                                
           MOVE F999-NME-LOCAL-PRCNCT         TO WS-BLOCK-INFO (2).             
                                                                                
           STRING 'BLK ' F999-BLOCK-NO                                          
                   DELIMITED BY SIZE        INTO WS-BLOCK-INFO (3).             
                                                                                
           MOVE F999-NME-STREET               TO WS-BLOCK-INFO (4).             
                                                                                
           MOVE SPACES                        TO WS-BLOCK-INFO (5).             
                                                                                
           MOVE 'MAXIMUM UNITS ALLOWED FOR: '    TO WS-BLOCK-INFO (6).          
                                                                                
           MOVE F999-NUM-MAX-CAT1   TO WS-NUM-MAX-CAT1.                         
           MOVE F999-NUM-MAX-CAT2   TO WS-NUM-MAX-CAT2.                         
           MOVE F999-NUM-MAX-CAT3   TO WS-NUM-MAX-CAT3.                         
           STRING 'M - '   WS-NUM-MAX-CAT1 ', '                                 
                  'C - '   WS-NUM-MAX-CAT2 ', '                                 
                  'I/O - ' WS-NUM-MAX-CAT3                                      
                  DELIMITED BY SIZE         INTO WS-BLOCK-INFO (7).             
                                                                                
           IF F999-NUM-SELTY = 'SER'                                            
              MOVE F999-NUM-MAX-SPR    TO WS-NUM-MAX-SPR                        
              STRING 'SPR - '   WS-NUM-MAX-SPR                                  
                    DELIMITED BY SIZE  INTO WS-BLOCK-INFO (8)                   
           ELSE                                                                 
              MOVE  SPACES             TO  WS-BLOCK-INFO (8)                    
           END-IF.                                                              
                                                                                
       3210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3212-GET-ESTATE-NAME.                                                    
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES               TO WS-LINK-REC.                            
           IF F999-NUM-SELTY = 'BTO'                                            
              MOVE F999-NUM-ZONE     TO WS-LINK-NUM-ZONE                        
           ELSE                                                                 
              MOVE F999-CDE-NT       TO WS-LINK-NUM-ZONE                        
           END-IF.                                                              
           MOVE F999-NUM-FLAT-TYPE   TO WS-LINK-NUM-FT.                         
           MOVE F999-DTE-BALLOT      TO WS-LINK-DTE-BALLOT.                     
           CALL 'BP13C91J' USING  WS-LINK-REC,                                  
                                  WS-BP13K813-REC,                              
                                  WS-ZONE-FND-FLAG.                             
           IF WS-ZONE-FND-FLAG = 'Y'                                            
              MOVE WS-BP13K813-REC   TO BP13K813-REC                            
              MOVE K813-NME-SITE     TO WS-NME-ESTATE                           
              MOVE K813-NUM-EST      TO WS-NUM-EST                              
           ELSE                                                                 
              MOVE SPACES            TO WS-NME-ESTATE                           
                                        WS-NUM-EST                              
           END-IF.                                                              
                                                                                
       3212-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3213-CHANGE-CNTRCT-TYP.                                                  
      *--------------------------------*                                        
                                                                                
           MOVE SPACES               TO WS-TMP-CNTRCT-TYP.                      
                                                                                
           IF F999-NUM-FLAT-TYPE = '3 '                                         
              EVALUATE F999-CDE-MODL-DESG                                       
                 WHEN '1IMP'                                                    
                    MOVE 'IMPROVED'         TO WS-TMP-CNTRCT-TYP                
                 WHEN '1PA '                                                    
                    MOVE 'PREMIUM APARTMENTS' TO WS-TMP-CNTRCT-TYP              
                 WHEN '2DBP'                                                    
                    MOVE 'DESIGN & BUILD/DESIGN PLUS'                           
                                            TO WS-TMP-CNTRCT-TYP                
                 WHEN '2MOA'                                                    
                    MOVE 'MODEL A'          TO WS-TMP-CNTRCT-TYP                
                 WHEN '3NG'                                                     
                    MOVE 'NEW GENERATION'   TO WS-TMP-CNTRCT-TYP                
                 WHEN '1STD'                                                    
                 WHEN '3STD'                                                    
                 WHEN '4STD'                                                    
                    MOVE 'STANDARD'         TO WS-TMP-CNTRCT-TYP                
                 WHEN '4SIM'                                                    
                    MOVE 'SIMPLIFIED'       TO WS-TMP-CNTRCT-TYP                
              END-EVALUATE                                                      
           ELSE                                                                 
              EVALUATE F999-NUM-CNTRCT-TYP                                      
                WHEN '2DBP'                                                     
                   MOVE 'DESIGN & BUILD/DESIGN PLUS'                            
                                             TO WS-TMP-CNTRCT-TYP               
                WHEN '1PA '                                                     
                   MOVE 'PREMIUM APARTMENTS' TO WS-TMP-CNTRCT-TYP               
                WHEN '1STD'                                                     
                WHEN '3STD'                                                     
                WHEN '4STD'                                                     
                   MOVE 'STANDARD'           TO WS-TMP-CNTRCT-TYP               
                WHEN '3STB'                                                     
                   MOVE 'STANDARD (BUDGET)'  TO WS-TMP-CNTRCT-TYP               
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
           IF F999-NUM-MODL-DESG = '28'                                         
              MOVE 'TERRACE'       TO WS-TMP-CNTRCT-TYP                         
           END-IF.                                                              
                                                                                
           IF F999-NUM-MODL-DESG = '85'                                         
              MOVE 'DBSS'          TO WS-TMP-CNTRCT-TYP                         
           END-IF.                                                              
                                                                                
       3213-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3220-GET-REMARKS.                                                        
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES     TO WS-REMARKS-PRINTED-SW.                            
                                                                                
           PERFORM VARYING WS-SUB FROM 1 BY 1 UNTIL WS-SUB > 10                 
              MOVE SPACES       TO WS-REMARKS (WS-SUB)                          
           END-PERFORM.                                                         
                                                                                
           MOVE ZEROES          TO WS-SUB.                                      
                                                                                
           IF F999-CDE-KEY-AVAIL = 'Y'                                          
              ADD  1  TO WS-SUB                                                 
              MOVE 'COMPLETED FLAT'    TO WS-REMARKS (WS-SUB)                   
           END-IF.                                                              
                                                                                
           IF F999-NUM-SELTY = 'SBF' OR 'NPL'                                   
               IF F999-DTE-PCD NOT = SPACES AND LOW-VALUES                      
                  PERFORM 3223-MOVE-PCD-QTR              THRU 3223-EXIT         
               END-IF                                                           
           ELSE                                                                 
               IF F999-DTE-ECD NOT = SPACES AND LOW-VALUES                      
                  PERFORM 3221-MOVE-ECD-QTR              THRU 3221-EXIT         
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF F999-DTE-DLVRY-POSSESSION NOT = SPACES AND LOW-VALUES             
              PERFORM 3222-MOVE-DLVRY-POSSESSION     THRU 3222-EXIT             
           END-IF.                                                              
                                                                                
           IF (F999-CDE-OCS-TAG = 'Y') AND                                      
              (F999-NUM-FLAT-TYPE NOT = '1A' AND '2A') AND                      
              (F999-DTE-EXPIRY NOT = SPACES AND LOW-VALUES AND ZEROES)          
              ADD 2 TO WS-SUB                                                   
              MOVE 'WITH OCS OPTION'     TO WS-REMARKS(WS-SUB)                  
           END-IF.                                                              
                                                                                
           IF ((F999-CDE-KEY-AVAIL = 'Y') AND                                   
               (F999-NUM-FLAT-TYPE = '1A' OR '2A'))                             
              ADD 2 TO WS-SUB                                                   
              STRING 'LCD: '                                                    
                      'FROM KEYS COLLECTION DATE'                               
                      DELIMITED BY SIZE    INTO WS-REMARKS(WS-SUB)              
           ELSE                                                                 
              IF F999-DTE-LEASE-COMM NOT = SPACES AND LOW-VALUES                
                 ADD 2 TO WS-SUB                                                
                 MOVE F999-DTE-LEASE-COMM(5:2) TO WS-NUM-MONTH                  
                 PERFORM 3400-GET-MONTH-NAME       THRU 3400-EXIT               
                 STRING 'LCD: '                                                 
                      F999-DTE-LEASE-COMM(7:2) ' '                              
                      WS-NAME-MONTH ' '                                         
                      F999-DTE-LEASE-COMM(1:4)                                  
                      DELIMITED BY SIZE    INTO WS-REMARKS(WS-SUB)              
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F999-NUM-MODL-DESG = '75' OR '76'                                 
              ADD 2    TO WS-SUB                                                
              MOVE 'PRIME LOCATION PUBLIC HOUSING' TO WS-REMARKS(WS-SUB)        
           END-IF.                                                              
                                                                                
           PERFORM 322A-GET-BLDG-FTR THRU 322A-EXIT.                            
           IF WS-CAR-LITE-TAG = 'Y'                                             
              ADD 2    TO WS-SUB                                                
              MOVE 'CAR-LITE PRECINCT' TO WS-REMARKS(WS-SUB)                    
           END-IF.                                                              
                                                                                
      *** MOVE THE COUNTER OF THE LAST REMARKS LINE TO WS-MAX-REM               
                                                                                
           MOVE WS-SUB TO WS-MAX-REM.                                           
                                                                                
       3220-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       322A-GET-BLDG-FTR.                                                       
      ******************************************************************        
           MOVE SPACES                 TO WS-CRG3-REC.                          
           INITIALIZE                     WS-CRG3-REC.                          
           MOVE SPACES                 TO WS-CAR-LITE-TAG.                      
                                                                                
           PERFORM 322B-GET-BLDG-GL    THRU 322B-EXIT.                          
           MOVE K100-CDE-BLK           TO WS-CRG3-BLDG-GL.                      
                                                                                
           CALL 'BP13CRG3' USING WS-CRG3-REC.                                   
                                                                                
           IF (WS-CRG3-CDE-SYSERR = 0)                                          
              PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 100 OR          
                                                 WS-CAR-LITE-TAG = 'Y'          
                 IF (WS-CRG3-TYPE-FTR(WS-CTR) = 'CAR-LITE'                      
                                             OR 'CAR LITE')                     
                    AND (WS-CRG3-NUM-FTR (WS-CTR) = 'Y')                        
                    MOVE 'Y' TO WS-CAR-LITE-TAG                                 
                 END-IF                                                         
              END-PERFORM                                                       
           ELSE                                                                 
              DISPLAY 'ERROR CALLING BP13CRG3 '                                 
              DISPLAY 'SQL CODE = ' WS-CRG3-CDE-SYSERR                          
              MOVE SPACES                TO WS-CAR-LITE-TAG                     
           END-IF.                                                              
                                                                                
       322A-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       322B-GET-BLDG-GL.                                                        
      *-----------------------------------------------------------              
            MOVE SPACES           TO K110-SCH-ACC-NO.                           
            MOVE F999-NUM-HDB-REF TO K110-SCH-ACC-NO.                           
            READ BM06K110.                                                      
                                                                                
            EVALUATE WS-K110-STATUS                                             
               WHEN 00                                                          
                    PERFORM 322C-READ-BM06K100 THRU 322C-EXIT                   
               WHEN 23                                                          
                    MOVE SPACES TO K100-CDE-BLK                                 
               WHEN OTHER                                                       
                    DISPLAY 'BM06K110 READ ERROR : ' WS-K110-STATUS             
                    DISPLAY 'BM06K110 KEY-FLD    : ' K110-SCH-ACC-NO            
                    MOVE WS-K110-STATUS          TO   RETURN-CODE               
                    PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT               
            END-EVALUATE.                                                       
                                                                                
       322B-EXIT.                                                               
           EXIT.                                                                
                                                                                
       322C-READ-BM06K100.                                                      
      *-----------------------------------------------------------              
           MOVE LOW-VALUES           TO K100-KEY-FLD.                           
           MOVE K110-ESTATE          TO K100-ESTATE.                            
           MOVE K110-NEIGHBOURHOOD   TO K100-NEIGHBOURHOOD.                     
           MOVE K110-CONTRACT-NO     TO K100-CONTRACT-NO.                       
           MOVE K110-BLK-NO          TO K100-BLK-NO.                            
                                                                                
           READ BM06K100.                                                       
           EVALUATE WS-K100-STATUS                                              
           WHEN 00                                                              
              CONTINUE                                                          
           WHEN 23                                                              
              MOVE SPACES                TO K100-CDE-BLK                        
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BM06K100 : ' WS-K100-STATUS                
              MOVE WS-K100-STATUS             TO  RETURN-CODE                   
              PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       322C-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3221-MOVE-ECD-QTR.                                                       
      *-------------------------------------------------------------            
                                                                                
           ADD 2 TO WS-SUB.                                                     
                                                                                
           EVALUATE F999-DTE-ECD(5:2)                                           
              WHEN '01'                                                         
              WHEN '02'                                                         
              WHEN '03'                                                         
                 MOVE '1ST QTR' TO WS-TXT-QTR                                   
              WHEN '04'                                                         
              WHEN '05'                                                         
              WHEN '06'                                                         
                 MOVE '2ND QTR' TO WS-TXT-QTR                                   
              WHEN '07'                                                         
              WHEN '08'                                                         
              WHEN '09'                                                         
                 MOVE '3RD QTR' TO WS-TXT-QTR                                   
              WHEN '10'                                                         
              WHEN '11'                                                         
              WHEN '12'                                                         
                 MOVE '4TH QTR' TO WS-TXT-QTR                                   
           END-EVALUATE.                                                        
                                                                                
           STRING 'ECD: '                                                       
                   WS-TXT-QTR ' '                                               
                   F999-DTE-ECD(1:4)                                            
                   DELIMITED BY SIZE   INTO WS-REMARKS(WS-SUB).                 
                                                                                
       3221-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3222-MOVE-DLVRY-POSSESSION.                                              
      *-------------------------------------------------------------            
                                                                                
           ADD 2 TO WS-SUB.                                                     
           MOVE F999-DTE-DLVRY-POSSESSION(5:2)  TO WS-NUM-MONTH.                
           PERFORM 3400-GET-MONTH-NAME       THRU 3400-EXIT.                    
                                                                                
           IF F999-NUM-BTO-NEW-CONTRACT = 'Y' OR                                
              F999-NUM-SELTY = 'SBF' OR 'OBF' OR 'SER'                          
              STRING 'EDPD: '                                                   
                     F999-DTE-DLVRY-POSSESSION(7:2) ' '                         
                     WS-NAME-MONTH ' '                                          
                     F999-DTE-DLVRY-POSSESSION(1:4)                             
                     DELIMITED BY SIZE   INTO WS-REMARKS(WS-SUB)                
           ELSE                                                                 
              STRING 'DPD: '                                                    
                     F999-DTE-DLVRY-POSSESSION(7:2) ' '                         
                     WS-NAME-MONTH ' '                                          
                     F999-DTE-DLVRY-POSSESSION(1:4)                             
                     DELIMITED BY SIZE   INTO WS-REMARKS(WS-SUB)                
           END-IF.                                                              
                                                                                
       3222-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3223-MOVE-PCD-QTR.                                                       
      *-------------------------------------------------------------            
                                                                                
           ADD 2 TO WS-SUB.                                                     
                                                                                
           EVALUATE F999-DTE-PCD(5:2)                                           
              WHEN '01'                                                         
              WHEN '02'                                                         
              WHEN '03'                                                         
                 MOVE '1ST QTR' TO WS-TXT-QTR                                   
              WHEN '04'                                                         
              WHEN '05'                                                         
              WHEN '06'                                                         
                 MOVE '2ND QTR' TO WS-TXT-QTR                                   
              WHEN '07'                                                         
              WHEN '08'                                                         
              WHEN '09'                                                         
                 MOVE '3RD QTR' TO WS-TXT-QTR                                   
              WHEN '10'                                                         
              WHEN '11'                                                         
              WHEN '12'                                                         
                 MOVE '4TH QTR' TO WS-TXT-QTR                                   
           END-EVALUATE.                                                        
                                                                                
           STRING 'PCD: '                                                       
                   WS-TXT-QTR ' '                                               
                   F999-DTE-PCD(1:4)                                            
                   DELIMITED BY SIZE   INTO WS-REMARKS(WS-SUB).                 
                                                                                
       3223-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3300-WRITE-DETAILS.                                                      
      *-------------------------------------------------------------            
                                                                                
           IF C-FIRST-REC-READ                                                  
              PERFORM 4100-PRINT-PAGE-BREAK         THRU 4100-EXIT              
              PERFORM 3310-PRINT-FIRST-RECORD       THRU 3310-EXIT              
              MOVE F999-CDE-NT TO WS-PREV-CDE-NT                                
              GO TO 3300-EXIT                                                   
           ELSE                                                                 
              IF F999-CDE-NT NOT = WS-PREV-CDE-NT                               
                 PERFORM 4100-PRINT-PAGE-BREAK   THRU 4100-EXIT                 
                 MOVE F999-CDE-NT TO WS-PREV-CDE-NT                             
              ELSE                                                              
                 IF C-CHANGE-BLK                                                
                    ADD  1        TO WS-LINE-CTR                                
                    COMPUTE WS-LINE-LEFT = WS-LINE-MAX - WS-LINE-CTR            
                    IF WS-LINE-LEFT > 14                                        
                       WRITE L26G-PRTREC FROM L26G-LINE AFTER 1                 
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF C-CHANGE-BLK                                                      
              MOVE SPACES   TO WS-CHANGE-BLK-SW                                 
              MOVE ZEROES   TO WS-SUB                                           
              IF WS-LINE-CTR < 45                                               
                 MOVE ZEROES              TO WS-LINE-CTR-A                      
                 IF F999-NUM-FLAT-TYPE NOT = WS-PREV-FLAT-TYPE                  
                    MOVE F999-NUM-FLAT-TYPE  TO WS-PREV-FLAT-TYPE               
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           ADD 1   TO WS-LINE-CTR.                                              
           PERFORM 4000-CHECK-LINE-CTR           THRU 4000-EXIT.                
                                                                                
           IF NOT C-BLKINFO-PRINTED                                             
              PERFORM 3340-PRINT-BLOCK-INFO      THRU 3340-EXIT                 
           END-IF.                                                              
                                                                                
           IF C-BLKINFO-PRINTED AND                                             
              NOT C-REMARKS-PRINTED                                             
              PERFORM 3350-PRINT-REMARKS         THRU 3350-EXIT                 
           END-IF.                                                              
                                                                                
           IF WS-LINE-CTR-A = 5 OR                                              
              F999-NUM-FLAT-TYPE NOT = WS-PREV-FLAT-TYPE                        
              PERFORM 3360-PRINT-SPACES          THRU 3360-EXIT                 
           END-IF.                                                              
                                                                                
           PERFORM 3320-MOVE-OTHER-DETAIL        THRU 3320-EXIT.                
           WRITE L26G-PRTREC        FROM L26G-DETAIL-REC AFTER 1.               
           ADD   1  TO WS-PRT-CTR                                               
                       WS-LINE-CTR-A.                                           
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3310-PRINT-FIRST-RECORD.                                                 
      *-------------------------------------------------------------            
                                                                                
           MOVE 1        TO WS-SUB.                                             
           MOVE WS-BLOCK-INFO (WS-SUB)       TO L26G-BLK-DETAILS.               
           PERFORM 3320-MOVE-OTHER-DETAIL       THRU 3320-EXIT.                 
           WRITE L26G-PRTREC       FROM L26G-DETAIL-REC AFTER 1.                
           ADD  1        TO WS-PRT-CTR.                                         
                                                                                
           ADD  1        TO WS-LINE-CTR                                         
                            WS-LINE-CTR-A.                                      
                                                                                
           MOVE SPACES   TO WS-FIRST-REC-READ-SW                                
                            WS-CHANGE-BLK-SW.                                   
                                                                                
           MOVE F999-NUM-FLAT-TYPE     TO WS-PREV-FLAT-TYPE.                    
                                                                                
       3310-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3320-MOVE-OTHER-DETAIL.                                                  
      *-------------------------------------------------------------            
                                                                                
           STRING ' ' F999-LEVEL-NO '-'                                         
                  F999-MAIN-UNIT                                                
                  DELIMITED BY SIZE       INTO L26G-NUM-UNIT.                   
                                                                                
           EVALUATE F999-NUM-FLAT-CODE                                          
           WHEN 'SF'                                                            
              MOVE '*'                 TO L26G-NUM-FLAT-CODE                    
           WHEN 'SH'                                                            
              MOVE '(H)'               TO L26G-NUM-FLAT-CODE                    
           WHEN SPACES                                                          
              MOVE SPACES              TO L26G-NUM-FLAT-CODE                    
           WHEN OTHER                                                           
              STRING '(' F999-NUM-FLAT-CODE ')'                                 
                   DELIMITED BY SIZE INTO L26G-NUM-FLAT-CODE                    
           END-EVALUATE.                                                        
                                                                                
      *    MOVE F999-NUM-FLAT-TYPE          TO L26G-FLAT-TYPE                   
           MOVE F999-QTY-AREA-TOTAL         TO L26G-FLOOR-AREA.                 
           IF F999-CDE-FLEXI-TAG = 'L'                                          
              STRING  '(' F999-CDE-FLEXI-TAG ')'                                
                DELIMITED BY SIZE INTO L26G-FLEXI-CDE                           
           ELSE                                                                 
              MOVE SPACES           TO L26G-FLEXI-CDE                           
           END-IF.                                                              
           MOVE F999-QTY-AREA-ORGNL         TO L26G-IN-FLOOR-AREA.              
           MOVE F999-AMT-SELLING-PRICE      TO L26G-SELLING-PRICE.              
                                                                                
       3320-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3330-PRINT-REMAINING-INFO.                                               
      *-------------------------------------------------------------            
                                                                                
           IF C-FIRST-REC-READ                                                  
              GO TO 3330-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE SPACES           TO L26G-DETAIL-REC.                            
                                                                                
           IF NOT C-BLKINFO-PRINTED                                             
              PERFORM 3340-PRINT-BLOCK-INFO      THRU 3340-EXIT                 
                UNTIL C-BLKINFO-PRINTED                                         
           END-IF.                                                              
                                                                                
           IF C-BLKINFO-PRINTED AND                                             
              NOT C-REMARKS-PRINTED                                             
              PERFORM 3350-PRINT-REMARKS         THRU 3350-EXIT                 
                UNTIL C-REMARKS-PRINTED                                         
           END-IF.                                                              
                                                                                
       3330-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3340-PRINT-BLOCK-INFO.                                                   
      *-------------------------------------------------------------            
                                                                                
           COMPUTE WS-LINE-LEFT = WS-LINE-MAX - WS-LINE-CTR                     
           IF WS-LINE-CTR >= 45                                                 
              PERFORM 4100-PRINT-PAGE-BREAK          THRU 4100-EXIT             
           END-IF.                                                              
                                                                                
           ADD  1   TO WS-SUB.                                                  
                                                                                
           IF WS-SUB = 1                                                        
              IF WS-LINE-LEFT < 14                                              
                 PERFORM 4100-PRINT-PAGE-BREAK          THRU 4100-EXIT          
                 ADD  1   TO WS-SUB                                             
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-SUB > 8                                                        
              MOVE 'Y'      TO WS-BLKINFO-PRINTED-SW                            
              MOVE ZEROES   TO WS-SUB                                           
              GO TO 3340-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE WS-BLOCK-INFO (WS-SUB)     TO L26G-BLK-DETAILS.                 
                                                                                
           IF C-CHANGE-BLK OR                                                   
              C-END-OF-F999                                                     
              ADD  1   TO WS-LINE-CTR                                           
              PERFORM 4000-CHECK-LINE-CTR      THRU 4000-EXIT                   
              WRITE L26G-PRTREC        FROM L26G-DETAIL-REC AFTER 1             
           END-IF.                                                              
                                                                                
       3340-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3350-PRINT-REMARKS.                                                      
      *-------------------------------------------------------------            
                                                                                
           ADD  1  TO WS-SUB.                                                   
                                                                                
           IF (WS-SUB > WS-MAX-REM)                                             
              MOVE SPACES         TO L26G-BLK-DETAILS                           
              MOVE 'Y'            TO WS-REMARKS-PRINTED-SW                      
              MOVE ZEROES         TO WS-SUB                                     
              GO TO 3350-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE WS-REMARKS (WS-SUB)        TO L26G-BLK-DETAILS.                 
                                                                                
           IF C-CHANGE-BLK OR                                                   
              C-END-OF-F999                                                     
              ADD  1  TO WS-LINE-CTR                                            
              PERFORM 4000-CHECK-LINE-CTR           THRU 4000-EXIT              
              WRITE L26G-PRTREC        FROM L26G-DETAIL-REC AFTER 1             
           END-IF.                                                              
                                                                                
       3350-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3360-PRINT-SPACES.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE ZEROES        TO WS-LINE-CTR-A.                                 
                                                                                
           MOVE SPACES        TO L26G-NUM-UNIT                                  
                                 L26G-NUM-FLAT-CODE.                            
                                                                                
           MOVE ZEROES        TO L26G-FLOOR-AREA                                
                                 L26G-IN-FLOOR-AREA                             
                                 L26G-SELLING-PRICE.                            
                                                                                
           WRITE L26G-PRTREC     FROM L26G-DETAIL-REC AFTER 1                   
                                                                                
           ADD 1   TO WS-LINE-CTR                                               
           PERFORM 4000-CHECK-LINE-CTR        THRU 4000-EXIT                    
           MOVE SPACES        TO L26G-BLK-DETAILS                               
                                                                                
           IF F999-NUM-FLAT-TYPE NOT = WS-PREV-FLAT-TYPE                        
              MOVE F999-NUM-FLAT-TYPE     TO WS-PREV-FLAT-TYPE                  
           END-IF.                                                              
                                                                                
           IF NOT C-BLKINFO-PRINTED                                             
              PERFORM 3340-PRINT-BLOCK-INFO      THRU 3340-EXIT                 
           END-IF.                                                              
                                                                                
           IF C-BLKINFO-PRINTED AND                                             
              NOT C-REMARKS-PRINTED                                             
              PERFORM 3350-PRINT-REMARKS         THRU 3350-EXIT                 
           END-IF.                                                              
                                                                                
       3360-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3400-GET-MONTH-NAME.                                                     
      *-------------------------------------------------------------            
                                                                                
           SET WS-MNT-NDX TO 1.                                                 
           SEARCH WS-MONTH-TBL                                                  
              AT END                                                            
                 MOVE SPACES     TO WS-NAME-MONTH                               
              WHEN WS-NUM-MONTH = WS-MONTH-NUM (WS-MNT-NDX)                     
                 MOVE WS-MONTH-NAME (WS-MNT-NDX)   TO WS-NAME-MONTH             
           END-SEARCH.                                                          
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-CHECK-LINE-CTR.                                                     
      *-------------------------------------------------------------            
                                                                                
           IF WS-LINE-CTR > 45  OR                                              
              WS-LINE-CTR = 0                                                   
              PERFORM 4100-PRINT-PAGE-BREAK       THRU 4100-EXIT                
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4100-PRINT-PAGE-BREAK.                                                   
      *-------------------------------------------------------------            
                                                                                
           IF F999-NUM-SELTY = 'NPL'                                            
              MOVE F999-DTE-BALLOT (5:2)       TO WS-NUM-MONTH                  
              PERFORM 3400-GET-MONTH-NAME     THRU 3400-EXIT                    
              STRING WS-MONTH-NAME (WS-MNT-NDX) ' '                             
                     F999-DTE-BALLOT (1:4)                                      
                     DELIMITED BY SIZE INTO L26G-ALLOC-DATE                     
              END-STRING                                                        
              WRITE L26G-PRTREC        FROM L26G-HEADER-SA AFTER PAGE           
           ELSE                                                                 
             MOVE F999-DTE-BALLOT (5:2)       TO WS-NUM-MONTH                   
             PERFORM 3400-GET-MONTH-NAME     THRU 3400-EXIT                     
             STRING WS-MONTH-NAME (WS-MNT-NDX) ' '                              
                     F999-DTE-BALLOT (1:4)                                      
                 DELIMITED BY SIZE INTO WS-DTE-BALLOT                           
             END-STRING                                                         
             EVALUATE F999-NUM-SELTY                                            
                WHEN 'BTO'                                                      
                   STRING 'BUILD-TO-ORDER ' WS-DTE-BALLOT                       
                          DELIMITED BY SIZE INTO WS-SELTYPE                     
                   END-STRING                                                   
                WHEN 'SBF'                                                      
                   STRING 'SALE OF BALANCE FLATS ' WS-DTE-BALLOT                
                          DELIMITED BY SIZE INTO WS-SELTYPE                     
                   END-STRING                                                   
                WHEN 'SER'                                                      
                   STRING 'SELECTIVE EN BLOC REDEVELOPMENT SCHEME '             
                          DELIMITED BY SIZE INTO WS-SELTYPE                     
                   END-STRING                                                   
                WHEN 'REN'                                                      
                   STRING 'RELOCATION EXERCISE '                                
                          DELIMITED BY SIZE INTO WS-SELTYPE                     
                   END-STRING                                                   
             END-EVALUATE                                                       
             IF F999-NUM-LEASE-BALANCE > ZEROES                                 
                MOVE F999-NUM-LEASE-BALANCE TO  L26G-LEASE-BAL                  
                WRITE L26G-PRTREC    FROM L26G-HEADER-LBS AFTER PAGE            
             ELSE                                                               
                IF F999-NUM-GRO = 'Y'                                           
                   WRITE L26G-PRTREC    FROM L26G-HEADER-GRO AFTER PAGE         
                ELSE                                                            
                   IF F999-NUM-GRO = 'P' OR 'C'                                 
                      WRITE L26G-PRTREC FROM L26G-HEADER-MGP AFTER PAGE         
                   ELSE                                                         
                      WRITE L26G-PRTREC    FROM L26G-HEADER AFTER PAGE          
                   END-IF                                                       
               END-IF                                                           
             END-IF                                                             
           END-IF.                                                              
                                                                                
           IF F999-NUM-GRO = 'P'                                                
              STRING WS-SELTYPE  ' - '  WS-NME-ESTATE                           
                    ' (PARENT APPLICATION)'                                     
                     DELIMITED BY SIZE INTO WS-STRING-TO-PARSE                  
              END-STRING                                                        
              MOVE LENGTH OF WS-STRING-TO-PARSE                                 
                                        TO WS-STRING-LENGTH                     
              MOVE 'Y'                     TO WS-INSERT-SPACE-FLAG              
              PERFORM 4200-PARSE-STRING    THRU 4200-EXIT                       
              MOVE WS-PARSED-STRING        TO  L26G-NME-ESTATE                  
           ELSE                                                                 
              IF F999-NUM-GRO = 'C'                                             
                 STRING WS-SELTYPE  ' - '  WS-NME-ESTATE                        
                        ' (MARRIED CHILD APPLICATION)'                          
                     DELIMITED BY SIZE INTO WS-STRING-TO-PARSE                  
                 END-STRING                                                     
                 MOVE LENGTH OF WS-STRING-TO-PARSE                              
                                        TO WS-STRING-LENGTH                     
                 MOVE 'Y'               TO WS-INSERT-SPACE-FLAG                 
                 PERFORM 4200-PARSE-STRING    THRU 4200-EXIT                    
                 MOVE WS-PARSED-STRING        TO  L26G-NME-ESTATE               
              ELSE                                                              
                 IF F999-NUM-SELTY = 'NPL'                                      
                    MOVE WS-NME-ESTATE   TO L26G-NME-ESTATE                     
                 ELSE                                                           
                    STRING WS-SELTYPE  ' - '  WS-NME-ESTATE                     
                        DELIMITED BY SIZE INTO WS-STRING-TO-PARSE               
                    END-STRING                                                  
                    MOVE LENGTH OF WS-STRING-TO-PARSE                           
                                        TO WS-STRING-LENGTH                     
                    MOVE 'Y'               TO WS-INSERT-SPACE-FLAG              
                    PERFORM 4200-PARSE-STRING    THRU 4200-EXIT                 
                    MOVE WS-PARSED-STRING        TO  L26G-NME-ESTATE            
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
           ADD  1             TO WS-PAGE-CTR.                                   
           MOVE WS-PAGE-CTR   TO L26G-PAGE.                                     
                                                                                
           IF L26G-NME-ESTATE = SPACES                                          
              MOVE F999-NME-ESTATE    TO L26G-NME-ESTATE                        
           END-IF.                                                              
           WRITE L26G-PRTREC     FROM L26G-SUBHEADER.                           
           MOVE SPACES        TO L26G-PRTREC.                                   
           WRITE L26G-PRTREC     AFTER 5.                                       
           MOVE 5             TO WS-LINE-CTR.                                   
           MOVE ZEROES        TO WS-LINE-CTR-A.                                 
                                                                                
           IF F999-NUM-FLAT-TYPE NOT = WS-PREV-FLAT-TYPE                        
              MOVE F999-NUM-FLAT-TYPE     TO WS-PREV-FLAT-TYPE                  
           END-IF.                                                              
                                                                                
           IF (F999-NEIGHBOURHOOD = WS-PREV-NEIGHBOURHOOD) AND                  
              (F999-CONTRACT-NO = WS-PREV-CONTRACT-NO) AND                      
              (F999-BLOCK-NO = WS-PREV-BLOCK-NO)                                
              MOVE SPACES     TO WS-BLKINFO-PRINTED-SW                          
                                 WS-REMARKS-PRINTED-SW                          
              MOVE ZEROES     TO WS-SUB                                         
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------           
       4200-PARSE-STRING.                                                       
      *--------------------------------------------------------------           
           MOVE SPACES                    TO   WS-PARSED-STRING.                
           MOVE 1                         TO   WS-CUR-POS-PS.                   
           ADD  1                         TO   WS-STRING-LENGTH.                
           PERFORM 4300-GET-CHAR          THRU 4300-EXIT                        
              VARYING WS-CUR-POS-SP FROM 1 BY 1                                 
              UNTIL WS-CUR-POS-SP = WS-STRING-LENGTH.                           
                                                                                
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------          
       4300-GET-CHAR.                                                           
      *---------------------------------------------------------------          
           IF WS-STRING-TO-PARSE(WS-CUR-POS-SP:1) NOT = SPACES                  
              IF WS-SPACE-FLAG = 'Y' AND                                        
                 WS-INSERT-SPACE-FLAG = 'Y' AND                                 
                 WS-PARSED-STRING NOT = SPACES                                  
                 MOVE ' ' TO WS-PARSED-STRING(WS-CUR-POS-PS:1)                  
                 ADD 1           TO  WS-CUR-POS-PS                              
              END-IF                                                            
              MOVE WS-STRING-TO-PARSE(WS-CUR-POS-SP:1)                          
                              TO  WS-PARSED-STRING(WS-CUR-POS-PS:1)             
              ADD 1           TO  WS-CUR-POS-PS                                 
              MOVE    'N'      TO WS-SPACE-FLAG                                 
           ELSE                                                                 
              MOVE 'Y'     TO WS-SPACE-FLAG                                     
           END-IF.                                                              
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '--- BP13C26G CONTROL TOTAL ---- '                           
                   'ON ' WS-DISP-DATE.                                          
           DISPLAY SPACES.                                                      
           DISPLAY '*=============== COUNTERS ===============*'.                
           DISPLAY ' BP13F999 RECORDS READ : ' WS-F999-READ-CTR.                
           DISPLAY ' NO. OF RECORDS PRINTED: ' WS-PRT-CTR.                      
                                                                                
           CALL 'C13K813A'         USING WS-RETURN-CODE.                        
                                                                                
           IF WS-RETURN-CODE NOT = ZEROS AND 97                                 
              DISPLAY 'CLOSE ERROR, BP13K813 STATUS ' WS-RETURN-CODE            
           END-IF.                                                              
                                                                                
           MOVE WS-RETURN-CODE         TO WS-C260-RETURN-CODE.                  
                                                                                
           CLOSE BP13F999                                                       
                 BM06K110                                                       
                 BM06K100                                                       
                 BP13L26G.                                                      
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR, WS-K110-STATUS ' WS-K110-STATUS             
              MOVE WS-K110-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR, WS-K100-STATUS ' WS-K100-STATUS             
              MOVE WS-K100-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
