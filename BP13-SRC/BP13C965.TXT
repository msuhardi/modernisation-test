       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C965.                                                 
      *AUTHOR.        SATHAPPAN.                                                
      *DATE-WRITTEN.  08/02/2002.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *   OBJECTIVE   :  PRINT THE REPORT FOR CONFLICT THE DATA    *            
      *                  BETWEEN MHA  AND  SOC.                    *            
      *                                                            *            
      *   INPUT  FILES:  1.BE01F713 - SOC CDB TRANSACTION FILE     *            
      *                  2.BP13F820 - SOC OCCUPIER FILE            *            
      *   OUTPUT FILES:  1.BP13FOUT - OUTPUT DETAILS FOR REPORT    *            
      *                                                            *            
      * CHG REF  DATE      OIC   REMARKS                           *            
      * -------- --------  ----  --------------------------------- *            
      * BP130850 20011218  AS1   NEW PROGRAM                       *            
      * BP132313 20030325  CKK   TO BYPASS CHANGES IN MARITAL STATUS            
      * BP136627 20170203  KSJ3  TO ADD OIC AND PRINT DETAILS IN   *            
      *                          FILE. REPORT TO BE RUN IN C966    *            
      * BP137269 20190701  KV6   TO FURTHER FILTER OUT CASES & WILL*            
      *                          ONLY REPORT CASES WITH MISMATCH IN*            
      *                          PR-STATUS,MARITAL STATUS,SEX,ID NO*            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT BE01F713  ASSIGN       TO BE01F713.                           
                                                                                
           SELECT BP13F820  ASSIGN       TO BP13F820.                           
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS WS-K800-STATUS.                     
                                                                                
           SELECT BP13K060  ASSIGN       TO BP13K060                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K060-KEY-FLD                        
                            FILE STATUS  IS WS-K060-STATUS.                     
                                                                                
           SELECT BP13K022  ASSIGN        TO BP13K022                           
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K022-KEY-FLD                       
                            ALTERNATE KEY IS K022-NUM-REGN                      
                            ACCESS MODE   IS RANDOM                             
                            FILE STATUS   IS WS-K022-STATUS.                    
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K110-KEY-FLD                       
                            ACCESS MODE   IS RANDOM                             
                            FILE STATUS   IS WS-K110-STATUS.                    
                                                                                
           SELECT BP13FOUT  ASSIGN       TO BP13FOUT.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD   BE01F713           BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 560 CHARACTERS                   
                               LABEL RECORD IS STANDARD                         
                               RECORDING MODE  IS  F.                           
       COPY BE01F713.                                                           
                                                                                
       FD   BP13F820           BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 400 CHARACTERS                   
                               LABEL RECORD IS STANDARD                         
                               RECORDING MODE  IS  F.                           
       COPY BP13F820.                                                           
                                                                                
       FD   BP13K800           RECORD CONTAINS 2000 CHARACTERS.                 
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K060           RECORD CONTAINS 25 CHARACTERS.                   
       COPY BP13K060.                                                           
                                                                                
       FD   BM06K110           RECORD CONTAINS 500 CHARACTERS.                  
       COPY BM06K110.                                                           
                                                                                
       FD   BP13K022.                                                           
       COPY BP13K022.                                                           
                                                                                
       FD   BP13FOUT           BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 230 CHARACTERS                   
                               LABEL RECORD IS STANDARD                         
                               RECORDING MODE  IS  F.                           
       01  BP13FOUT-REC.                                                        
           05  FOUT-REGN-NO            PIC X(08).                               
           05  FOUT-CSTMR-ID           PIC X(09).                               
           05  FOUT-OIC                PIC X(05).                               
           05  FOUT-AST-NRIC           PIC X(01).                               
           05  FOUT-BEFORE-NRIC        PIC X(09).                               
           05  FOUT-AST-PRST           PIC X(01).                               
           05  FOUT-BEFORE-PRST        PIC X(03).                               
           05  FOUT-AST-NTNLTY         PIC X(01).                               
           05  FOUT-BEFORE-NTNLTY      PIC X(04).                               
           05  FOUT-AST-SEX            PIC X(01).                               
           05  FOUT-BEFORE-SEX         PIC X(03).                               
           05  FOUT-AST-MRTL           PIC X(01).                               
           05  FOUT-BEFORE-MRTL        PIC X(04).                               
           05  FOUT-AST-NAME           PIC X(01).                               
           05  FOUT-BEFORE-NAME        PIC X(64).                               
           05  FOUT-AFTER-NRIC         PIC X(09).                               
           05  FOUT-AFTER-PRST         PIC X(03).                               
           05  FOUT-AFTER-NTNLTY       PIC X(04).                               
           05  FOUT-AFTER-SEX          PIC X(03).                               
           05  FOUT-AFTER-MRTL         PIC X(04).                               
           05  FOUT-AFTER-NAME         PIC X(64).                               
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F713-READ             PIC 9(4) VALUE ZEROES.                  
           05  WS-F820-READ             PIC 9(4) VALUE ZEROES.                  
           05  WS-F820A-WRT             PIC 9(4) VALUE ZEROES.                  
           05  WS-NO-WRT-FOUT-REC       PIC 9(4) VALUE ZEROES.                  
                                                                                
       01  WS-TEMP.                                                             
           05 WS-K022-FND              PIC X(1)  VALUE SPACE.                   
           05 WS-PREV-REGN             PIC X(8)  VALUE SPACES.                  
           05 WS-EOF-F713              PIC X(11) VALUE SPACES.                  
           05 WS-EOF-F820              PIC X(11) VALUE SPACES.                  
           05 WS-CURRENT-DATE          PIC X(08) VALUE SPACES.                  
           05 WS-F820-PR-STATUS        PIC X(03) VALUE SPACES.                  
           05 WS-F820-MRTL-STATUS      PIC X(01) VALUE SPACES.                  
           05 WS-F820-SEX              PIC X(03) VALUE SPACES.                  
           05 WS-CHANGE-BTH            PIC X(01) VALUE 'N'.                     
           05 WS-CHG-TAG               PIC X(01) VALUE 'N'.                     
           05 WS-VALID                 PIC X(01) VALUE 'N'.                     
                                                                                
       01  WS-ERROR-REPORT.                                                     
           05 WS-LINE-CTR              PIC 9(6)  VALUE 65.                      
           05 WS-PAGE-CTR              PIC 9(5)  VALUE ZEROES.                  
           05 WS-SEQN-CTR              PIC 9(6)  VALUE ZEROES.                  
                                                                                
       01  WS-STATUS.                                                           
           05 WS-K800-STATUS           PIC 9(2)  VALUE ZEROES.                  
           05 WS-K060-STATUS           PIC 9(2)  VALUE ZEROES.                  
           05 WS-K022-STATUS           PIC 9(2)  VALUE ZEROES.                  
           05 WS-K110-STATUS           PIC 9(2)  VALUE ZEROES.                  
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *-------------------------------------------------------------            
       0000-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-BE01F713 THRU 2000-EXIT.                           
           PERFORM 3000-READ-BP13F820 THRU 3000-EXIT.                           
           PERFORM 4000-MAIN-PROCESS  THRU 4000-EXIT                            
                       UNTIL F713-NUM-CSTMR-ID       = HIGH-VALUE AND           
                             F820-NUM-CSTMR-SOURCE   = HIGH-VALUE               
           PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BE01F713                                                 
                       BP13F820                                                 
                       BP13K800                                                 
                       BP13K060                                                 
                       BP13K022                                                 
                       BM06K110                                                 
                OUTPUT BP13FOUT.                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY "WS-K800-STATUS    : " WS-K800-STATUS                     
              MOVE     WS-K800-STATUS    TO  RETURN-CODE                        
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K060-STATUS NOT = 00 AND 97                                    
              DISPLAY "WS-K060-STATUS    : " WS-K060-STATUS                     
              MOVE     WS-K060-STATUS    TO  RETURN-CODE                        
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY "WS-K022-STATUS    : " WS-K022-STATUS                     
              MOVE     WS-K022-STATUS    TO  RETURN-CODE                        
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BE01F713.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BE01F713 AT END MOVE HIGH-VALUE TO F713-NUM-CSTMR-ID            
                            GO TO 2000-EXIT.                                    
                                                                                
           ADD  1                         TO WS-F713-READ.                      
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3000-READ-BP13F820.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F820 AT END MOVE HIGH-VALUE TO F820-NUM-CSTMR-SOURCE        
                            GO TO 3000-EXIT.                                    
                                                                                
           ADD  1                         TO WS-F820-READ.                      
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------*                                                    
       3100-READ-BP13K800.                                                      
      *--------------------*                                                    
                                                                                
            READ BP13K800.                                                      
                                                                                
            EVALUATE WS-K800-STATUS                                             
                WHEN 00                                                         
                     CONTINUE                                                   
                WHEN 23                                                         
                     DISPLAY 'NOT FND IN BP13K800 : ' WS-K800-STATUS            
                             '  K800-NUM-REGN : ' K800-NUM-REGN                 
                WHEN OTHER                                                      
                     DISPLAY 'ERROR IN BP13K800 : ' WS-K800-STATUS              
                             '  K800-NUM-REGN : ' K800-NUM-REGN                 
                     MOVE     WS-K800-STATUS     TO  RETURN-CODE                
                     PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                 
            END-EVALUATE.                                                       
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------*                                                       
       4000-MAIN-PROCESS.                                                       
      *-----------------*                                                       
                                                                                
           MOVE 'N'                           TO   WS-VALID                     
           IF F713-NUM-CSTMR-ID = F820-NUM-CSTMR-SOURCE                         
              PERFORM 4100-VALIDATE           THRU 4100-EXIT                    
              IF WS-VALID = 'Y'                                                 
                 PERFORM 5000-CHECK-CHANGES   THRU 5000-EXIT                    
              END-IF                                                            
              PERFORM 2000-READ-BE01F713      THRU 2000-EXIT                    
              PERFORM 3000-READ-BP13F820      THRU 3000-EXIT                    
           ELSE                                                                 
             IF F713-NUM-CSTMR-ID < F820-NUM-CSTMR-SOURCE                       
                PERFORM  2000-READ-BE01F713   THRU 2000-EXIT                    
             ELSE                                                               
               IF F713-NUM-CSTMR-ID > F820-NUM-CSTMR-SOURCE                     
                  PERFORM  3000-READ-BP13F820 THRU 3000-EXIT                    
               END-IF                                                           
             END-IF                                                             
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------*                                                      
       4100-VALIDATE.                                                           
      *------------------*                                                      
                                                                                
                                                                                
           MOVE SPACES                     TO   K800-NUM-REGN.                  
           MOVE F820-NUM-REGN              TO   K800-NUM-REGN.                  
           PERFORM 3100-READ-BP13K800      THRU 3100-EXIT.                      
           MOVE SPACES                     TO   K110-SCH-ACC-NO.                
           MOVE K800-NUM-SCH-ACC           TO   K110-SCH-ACC-NO.                
           PERFORM 4200-READ-BM06K110      THRU 4200-EXIT.                      
           IF K110-DTE-ACCEPTANCE NOT = SPACES AND LOW-VALUES AND ZEROES        
              IF K110-DTE-KEY-ISSUED  = SPACES OR  LOW-VALUES OR  ZEROES        
                 MOVE 'Y'                  TO   WS-VALID                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------*                                                    
       4200-READ-BM06K110.                                                      
      *--------------------*                                                    
                                                                                
            READ BM06K110.                                                      
                                                                                
            EVALUATE WS-K110-STATUS                                             
                WHEN 00                                                         
                     CONTINUE                                                   
                WHEN 23                                                         
                     DISPLAY 'NOT FND IN BM06K110 : ' WS-K110-STATUS            
                             '  K110-SCH-ACC-NO: ' K110-SCH-ACC-NO              
                WHEN OTHER                                                      
                     DISPLAY 'ERROR IN BM06K110 : ' WS-K110-STATUS              
                             '  K110-SCH-ACC-NO: ' K110-SCH-ACC-NO              
                     MOVE     WS-K110-STATUS     TO  RETURN-CODE                
                     PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                 
            END-EVALUATE.                                                       
                                                                                
       4200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------*                                                      
       5000-CHECK-CHANGES.                                                      
      *------------------*                                                      
                                                                                
            MOVE  SPACES                TO   BP13FOUT-REC.                      
            MOVE   'N'                  TO   WS-CHG-TAG.                        
                                                                                
            IF F713-CHG-PR-STATUS    = 'Y'  OR                                  
               F713-CHG-MRTL-STATUS  = 'Y'  OR                                  
               F713-CHG-SEX          = 'Y'  OR                                  
               F713-CHG-IDNTY-NO     = 'Y'                                      
                                                                                
               IF F713-CHG-PR-STATUS   =  'Y'                                   
                  PERFORM 5400-CONVERT-PRSTATUS-TO-SOC THRU 5400-EXIT           
                  IF F713-NEW-PR-STATUS > SPACES OR LOW-VALUES                  
                     IF WS-F820-PR-STATUS NOT = F713-NEW-PR-STATUS              
                       MOVE WS-F820-PR-STATUS    TO FOUT-BEFORE-PRST            
                       MOVE F713-NEW-PR-STATUS   TO FOUT-AFTER-PRST             
                       MOVE     '*'              TO FOUT-AST-PRST               
                       MOVE     'Y'              TO WS-CHG-TAG                  
                     END-IF                                                     
                  END-IF                                                        
               END-IF                                                           
                                                                                
               IF F713-CHG-MRTL-STATUS =  'Y'                                   
                  PERFORM 5100-CONVERT-MARITAL-CODE THRU 5100-EXIT              
                  IF F713-NEW-MRTL-STATUS > SPACES OR LOW-VALUES                
                    IF WS-F820-MRTL-STATUS NOT = F713-NEW-MRTL-STATUS           
                      MOVE F713-NEW-MRTL-STATUS    TO FOUT-AFTER-MRTL           
                      MOVE WS-F820-MRTL-STATUS     TO FOUT-BEFORE-MRTL          
                      MOVE      '*'                TO FOUT-AST-MRTL             
                      MOVE      'Y'                TO WS-CHG-TAG                
                    END-IF                                                      
                  END-IF                                                        
               END-IF                                                           
                                                                                
               IF F713-CHG-SEX =  'Y'                                           
                 PERFORM 5200-CONVERT-SEX-CODE  THRU  5200-EXIT                 
                 IF F713-NEW-SEX > SPACES OR LOW-VALUES                         
                    IF WS-F820-SEX NOT = F713-NEW-SEX                           
                      MOVE WS-F820-SEX          TO FOUT-BEFORE-SEX              
                      MOVE F713-NEW-SEX         TO FOUT-AFTER-SEX               
                      MOVE      '*'             TO FOUT-AST-SEX                 
                      MOVE      'Y'             TO WS-CHG-TAG                   
                    END-IF                                                      
                 END-IF                                                         
               END-IF                                                           
                                                                                
               IF  F713-CHG-IDNTY-NO = 'Y'                                      
                   IF F713-NEW-IDNTY-NO > SPACES OR LOW-VALUES                  
                      IF F713-NEW-IDNTY-NO NOT = F820-NUM-NRIC                  
                         MOVE F713-NEW-IDNTY-NO   TO FOUT-AFTER-NRIC            
                         MOVE F820-NME-OCCP       TO FOUT-BEFORE-NAME           
                         MOVE F713-NEW-NME-PERSON TO FOUT-AFTER-NAME            
                         MOVE      '*'            TO FOUT-AST-NRIC              
                         MOVE      'Y'            TO WS-CHG-TAG                 
                      END-IF                                                    
                   END-IF                                                       
               END-IF                                                           
                                                                                
               IF WS-CHG-TAG = 'Y'                                              
                  MOVE    F820-NUM-REGN           TO  FOUT-REGN-NO              
                  MOVE    F820-NUM-NRIC           TO  FOUT-BEFORE-NRIC          
                  MOVE    F820-NUM-CSTMR-SOURCE   TO  FOUT-CSTMR-ID             
                  PERFORM 8000-PRINT-ERROR-REPORT THRU  8000-EXIT               
              END-IF                                                            
            END-IF.                                                             
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------*                                               
       5100-CONVERT-MARITAL-CODE.                                               
      *-------------------------*                                               
                                                                                
            MOVE    SPACES                  TO    K060-KEY-FLD.                 
            MOVE    44                      TO    K060-SERIAL-NO.               
            MOVE    F820-NUM-MARITAL-STATUS TO    K060-CODE.                    
            PERFORM 5500-CHECK-BP13K060     THRU  5500-EXIT.                    
                                                                                
            IF WS-K060-STATUS = 00                                              
               MOVE  K060-DESC1(1:1)     TO WS-F820-MRTL-STATUS                 
            END-IF.                                                             
                                                                                
       5100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------------*                                                   
       5200-CONVERT-SEX-CODE.                                                   
      *---------------------*                                                   
                                                                                
            MOVE    SPACES               TO    K060-KEY-FLD.                    
            MOVE    43                   TO    K060-SERIAL-NO.                  
            MOVE    F820-NUM-SEX         TO    K060-CODE.                       
            PERFORM 5500-CHECK-BP13K060  THRU  5500-EXIT.                       
                                                                                
            IF WS-K060-STATUS = 00                                              
               MOVE    K060-DESC1        TO    WS-F820-SEX                      
            END-IF.                                                             
                                                                                
       5200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------------*                                            
       5400-CONVERT-PRSTATUS-TO-SOC.                                            
      *----------------------------*                                            
                                                                                
            IF F820-NUM-CITIZENSHIP = 'OC' OR 'ID' OR 'TH' OR 'IN' OR           
                                      'SL' OR 'BD' OR 'PH' OR 'TN' OR           
                                      'PS' OR 'MC' OR 'MP' OR 'SB' OR           
                                      'SR' OR '00' OR '01' OR '02' OR           
                                      '03' OR '04' OR '05' OR '06' OR           
                                      '07' OR '21' OR '30' OR '40' OR           
                                      '60' OR '80'                              
               MOVE        'A'       TO  WS-F820-PR-STATUS                      
           ELSE                                                                 
             IF F820-NUM-CITIZENSHIP = 'SC' OR '10'                             
                MOVE       'C'       TO  WS-F820-PR-STATUS                      
             ELSE                                                               
               IF F820-NUM-CITIZENSHIP = 'SP' OR '20'                           
                  MOVE     'P'       TO  WS-F820-PR-STATUS                      
               ELSE                                                             
                 IF F820-NUM-CITIZENSHIP = 'NI' OR '99'                         
                    MOVE   'U'       TO  WS-F820-PR-STATUS                      
                 ELSE                                                           
                    MOVE   'U'       TO  WS-F820-PR-STATUS                      
                 END-IF                                                         
               END-IF                                                           
             END-IF                                                             
           END-IF.                                                              
                                                                                
       5400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------*                                                    
       5500-CHECK-BP13K060.                                                     
      *--------------------*                                                    
                                                                                
            READ BP13K060.                                                      
                                                                                
            EVALUATE WS-K060-STATUS                                             
                WHEN 00                                                         
                     CONTINUE                                                   
                WHEN 23                                                         
                     DISPLAY 'NOT FND IN BP13K060 : ' WS-K060-STATUS            
                             '  K060-KEY-FLD  : ' K060-KEY-FLD                  
                             '  F820-KEY-FLD  : ' F820-KEY-FLD                  
                WHEN OTHER                                                      
                     DISPLAY 'ERROR IN BP13K060 : ' WS-K060-STATUS              
                             '  K060-KEY-FLD  :  ' K060-KEY-FLD                 
                     DISPLAY 'F820-KEY-FLD    :  ' F820-KEY-FLD                 
                     MOVE     WS-K060-STATUS     TO  RETURN-CODE                
                     PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                 
            END-EVALUATE.                                                       
                                                                                
       5500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------------*                                                 
       8000-PRINT-ERROR-REPORT.                                                 
      *-----------------------*                                                 
                                                                                
           MOVE SPACES                     TO   K022-KEY-FLD.                   
           MOVE FOUT-REGN-NO               TO   K022-NUM-REGN.                  
           PERFORM 8100-READ-BP13K022      THRU 8100-EXIT.                      
                                                                                
           IF WS-K022-FND = 'Y'                                                 
              IF K800-NUM-PROV-TAG = SPACE                                      
                 MOVE K022-NUM-OIC         TO   FOUT-OIC                        
              ELSE                                                              
                 IF K022-NUM-OIC-APPL NOT = SPACES                              
                    MOVE K022-NUM-OIC-APPL TO   FOUT-OIC                        
                 ELSE                                                           
                    MOVE K022-NUM-OIC      TO   FOUT-OIC                        
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           WRITE BP13FOUT-REC.                                                  
           ADD 1                           TO   WS-NO-WRT-FOUT-REC.             
                                                                                
       8000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------*                                                      
       8100-READ-BP13K022.                                                      
      *------------------*                                                      
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K022-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              MOVE 'Y'                     TO  WS-K022-FND                      
                                                                                
           WHEN 23                                                              
              MOVE 'N'                     TO  WS-K022-FND                      
              MOVE SPACES                  TO  K022-NUM-OIC                     
              DISPLAY 'NOT FND IN BP13K022 : ' WS-K060-STATUS                   
                      '  K022-NUM-REGN : ' K022-NUM-REGN                        
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR IN BP13K022   : ' WS-K022-STATUS                   
                      '  K022-NUM-REGN : ' K022-NUM-REGN                        
              MOVE     WS-K022-STATUS      TO  RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       8100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13C965 CONTROL '.                                         
           DISPLAY '-----------------------------------'.                       
           DISPLAY 'TOTAL F713  REC READ      = ' WS-F713-READ.                 
           DISPLAY 'TOTAL F820  REC READ      = ' WS-F820-READ.                 
      *    DISPLAY 'TOTAL F820A REC WRITTEN   = ' WS-F820A-WRT.                 
           DISPLAY 'TOTAL FOUTA REC WRITTEN   = ' WS-NO-WRT-FOUT-REC            
                                                                                
           CLOSE      BE01F713                                                  
                      BP13F820                                                  
                      BP13K800                                                  
                      BP13K060                                                  
                      BP13K022                                                  
                      BM06K110                                                  
                      BP13FOUT.                                                 
                                                                                
           IF WS-K060-STATUS NOT = 00 AND 97                                    
              DISPLAY "WS-K060-STATUS    : " WS-K060-STATUS                     
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY "WS-K022-STATUS    : " WS-K022-STATUS                     
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY "WS-K110-STATUS    : " WS-K110-STATUS                     
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
