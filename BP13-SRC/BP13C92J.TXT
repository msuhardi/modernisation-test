      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13C92J.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  07 MAY 2015.                                              
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : TO CREATE BP13FW3C BASED ON BP13F249        *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BP13F249                                              *           
      *                                                             *           
      *   OUTPUT FILES:                                             *           
      *   1.  BP13FW3C                                              *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP136299 07/05/2015 FNP1 NEW PROGRAM                        *           
      * BP136299 24/05/2017 SMR2 CATER FOR TOTAL BOOK               *           
      * BP136832 06/06/2017 SMR2 TOTAL BOOK = TOTAL - AVAIL TOTAL   *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F249 ASSIGN TO BP13F249.                                  
                                                                                
           SELECT BP13FW3C ASSIGN TO BP13FW3C.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F249                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  450 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F249.                                                           
                                                                                
                                                                                
       FD   BP13FW3C                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  500 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13FW3C.                                                           
                                                                                
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  WS-CURR-DATE           PIC X(08) VALUE ZEROES.                       
       01  WS-F249-READ           PIC 9(08) VALUE ZEROES.                       
       01  WS-FW3C-WRITE          PIC 9(08) VALUE ZEROES.                       
       01  WS-F249-EOF            PIC X     VALUE SPACES.                       
       01  BP13F249-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13FW3C-STATUS        PIC 9(02) VALUE ZEROES.                       
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NUM-FT          PIC X(02) VALUE SPACES.                       
           05  WS-TOTAL-BOOK      PIC 9(04) VALUE ZEROES.                       
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-F249    THRU 2000-EXIT.                            
           PERFORM 3000-PROCESS-REC  THRU 3000-EXIT                             
                   UNTIL WS-F249-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13F249                                                 
                OUTPUT BP13FW3C.                                                
                                                                                
                                                                                
           ACCEPT WS-CURR-DATE FROM DATE YYYYMMDD.                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-F249.                                                          
      *---------------*                                                         
                                                                                
           READ BP13F249 AT END                                                 
                MOVE HIGH-VALUES TO BP13F249-REC                                
                MOVE 'Y' TO WS-F249-EOF                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F249-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           MOVE SPACES          TO WS-NUM-FT.                                   
           MOVE ZEROES          TO WS-TOTAL-BOOK.                               
                                                                                
           IF F249-NUM-FLAT-TYPE(1:2) = 'SA'                                    
              MOVE '1A'          TO WS-NUM-FT                                   
           ELSE                                                                 
              IF F249-NUM-FLAT-TYPE(1:2) = 'EX'                                 
                 MOVE 'E '       TO WS-NUM-FT                                   
           ELSE                                                                 
              IF F249-NUM-FLAT-TYPE(2:1) = '-'                                  
                 MOVE F249-NUM-FLAT-TYPE(1:1) TO WS-NUM-FT                      
           ELSE                                                                 
              MOVE F249-NUM-FLAT-TYPE(1:2) TO WS-NUM-FT                         
           END-IF.                                                              
                                                                                
           COMPUTE WS-TOTAL-BOOK = F249-NUM-TOTAL -                             
                                   F249-NUM-AVAIL-TOTAL.                        
                                                                                
           PERFORM 4000-MOVE-F249-DETAILS  THRU 4000-EXIT.                      
           PERFORM 2000-READ-F249          THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       4000-MOVE-F249-DETAILS.                                                  
      *---------------*                                                         
           MOVE SPACES   TO  BP13FW3C-REC.                                      
           INITIALIZE        BP13FW3C-REC.                                      
                                                                                
           MOVE F249-NUM-NEW-TOWN      TO FW3C-NUM-NEW-TOWN.                    
           MOVE F249-NUM-FLAT-TYPE     TO FW3C-NUM-FLAT-TYPE.                   
           MOVE F249-NUM-SELECTION     TO FW3C-NUM-SELECTION.                   
           MOVE F249-DTE-BALLOT        TO FW3C-DTE-BALLOT.                      
           MOVE F249-NUM-BONUS         TO FW3C-NUM-BONUS.                       
           MOVE F249-NUM-TOTAL         TO FW3C-NUM-TOTAL-FLAT.                  
           MOVE F249-NUM-AVAIL-CAT1    TO FW3C-NUM-AVAIL-CAT1.                  
           MOVE F249-NUM-AVAIL-CAT2    TO FW3C-NUM-AVAIL-CAT2.                  
           MOVE F249-NUM-AVAIL-CAT3    TO FW3C-NUM-AVAIL-CAT3.                  
           MOVE F249-NUM-AVAIL-TOTAL   TO FW3C-NUM-AVAIL-TOTAL.                 
           MOVE F249-NUM-LIMIT         TO FW3C-NUM-LIMIT.                       
           MOVE F249-NUM-ZONE          TO FW3C-NUM-ZONE.                        
           MOVE F249-NUM-AVAIL-SPR     TO FW3C-NUM-AVAIL-SPR.                   
           MOVE F249-SPR-AVAIL         TO FW3C-SPR-AVAIL.                       
           MOVE F249-NUM-UNIT-SPR      TO FW3C-NUM-UNIT-SPR.                    
           MOVE F249-NUM-BTO-ZONE      TO FW3C-NUM-BTO-ZONE.                    
           MOVE F249-NUM-TOTAL-GEN3    TO FW3C-NUM-TOTAL-GEN3.                  
           MOVE F249-NUM-UNIT-CAT1-NOGEN3 TO FW3C-NUM-UNIT-CAT1-NOGEN3.         
           MOVE F249-NUM-UNIT-CAT2-NOGEN3 TO FW3C-NUM-UNIT-CAT2-NOGEN3.         
           MOVE F249-NUM-UNIT-CAT3-NOGEN3 TO FW3C-NUM-UNIT-CAT3-NOGEN3.         
           MOVE F249-NME-PROJECT       TO FW3C-NME-PROJECT.                     
                                                                                
           MOVE F249-CDE-NT          TO FW3C-NUM-NT                             
                                        FW3C-NUM-NT-CHAR3.                      
           MOVE WS-NUM-FT            TO FW3C-NUM-FT                             
                                        FW3C-NUM-FT-CHAR2.                      
           IF F249-NUM-SELECTION = 'BTO'                                        
              MOVE F249-NUM-BTO-ZONE    TO FW3C-NUM-NT                          
              MOVE '00'                 TO FW3C-NUM-FT                          
           END-IF.                                                              
                                                                                
           MOVE WS-TOTAL-BOOK           TO FW3C-NUM-TOTAL-BOOK.                 
                                                                                
           PERFORM 4500-WRITE-BP13FW3C  THRU 4500-EXIT.                         
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       4500-WRITE-BP13FW3C.                                                     
      *---------------*                                                         
                                                                                
                                                                                
           WRITE BP13FW3C-REC.                                                  
                                                                                
           ADD   1  TO WS-FW3C-WRITE.                                           
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13C92J  *************'.                           
           DISPLAY 'RECS READ FROM  BP13F249    : ' WS-F249-READ.               
           DISPLAY '                              '.                            
           DISPLAY 'RECS WRITTEN IN  BP13FW3C   : ' WS-FW3C-WRITE.              
                                                                                
           CLOSE BP13F249                                                       
                 BP13FW3C.                                                      
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
