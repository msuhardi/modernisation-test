      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
                                                                                
       PROGRAM-ID.    BP13C246.                                                 
       AUTHOR.        ROSALYN M. LUMANGLAS.                                     
       DATE-WRITTEN.  01/09/93.                                                 
                                                                                
      *===============================================================*         
      *                                                               *         
      *    SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                 *         
      *                                                               *         
      *    SYSTEM ID   :  BP13                                        *         
      *                                                               *         
      *    OBJECTIVE   :  THIS IS THE PROGRAM TO PRINT THE CHECK      *         
      *                   LIST OF APPOINTMENT LETTERS PRINTED.        *         
      *                                                               *         
      *    INPUT FILES :  1. BP13F205 - CONTORL RECORDS FILE          *         
      *                   3. BP13K200 - BOOKING APPOINTMENT FILE      *         
      *                   4. BP13K800 - SOC APPLN MASTER FILE         *         
      *                   5. BP13K820 - SOC OCCUPIER FILE             *         
      *                   6. BP13K830 - CORR ADDRESS FILE             *         
      *                                                               *         
      *    OUTPUT LIST :  1. BP13L246 - FINALIZED NAME LIST           *         
      *                                                               *         
      *                                                               *         
      *    CHGE-NO  BY    DATE   DESCRIPTION                          *         
      *    -------  ---  ------  ---------------------------------    *         
      *    SOC-PH8  JEP  940404  TO CHANGE BM06CTL  TO BP13F205       *         
      *                  940414  TO CHANGE BM06K010 TO BP13K200       *         
      *             BAL  950509  TO REPLACE NT COLUMN TO S/N COLUMN   *         
      *                          FROM THE REPORT FORMAT               *         
      *    SOC-PH9  GD   941101  TO CATER FOR ZONE AND FLAT TYPE      *         
      *    SOC-PH9  GD   941123  TO CHANGE BP13K200 TO BP13F200       *         
      *    C9506F9  LMS  950825  TO INCLUDE DECODING OF HHTY FOR CGO. *         
      *    BP130297 LMS  970519  TO OUTPUT CUR-REGN.                  *         
      *    BP130297 LMS  970527  TO OUTPUT SEQ-NO, REMOVE CUR-REGN.   *         
      *    BP131036 RAS  010614  TO CATER FOR ALLOC CDES: SFC & SRC   *         
      *    BP132031 MALC 031001  CATER FOR SALE OF SERS FLAT TO SINGLES         
      *    BP132095 MTD  230402  EXPAND LENGTH OF BP13F200            *         
      *                          (FROM 100 TO 300)                    *         
      *    BP132708 GAN1 070905  USE OF SERS TABLE (SERSCH)           *         
      *===============================================================*         
                                                                                
                                                                                
      ***********************                                                   
       ENVIRONMENT DIVISION.                                                    
      ***********************                                                   
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205 ASSIGN       TO BP13F205.                            
           SELECT BP13F200 ASSIGN       TO BP13F200.                            
           SELECT BP13K800 ASSIGN       TO BP13K800                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K800-NUM-REGN                        
                           FILE STATUS  IS WS-K800-STATUS.                      
           SELECT BP13K820 ASSIGN       TO BP13K820                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K820-KEY-FLD                         
                           FILE STATUS  IS WS-K820-STATUS.                      
           SELECT BP13K830 ASSIGN       TO BP13K830                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K830-KEY-FLD                         
                           ALTERNATE RECORD KEY IS K830-NUM-REGN                
                           FILE STATUS  IS WS-K830-STATUS.                      
           SELECT BP13L246 ASSIGN       TO BP13L246.                            
                                                                                
                                                                                
      ****************                                                          
       DATA DIVISION.                                                           
      ****************                                                          
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F205                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 80  CHARACTERS.                                     
       COPY BP13F205.                                                           
                                                                                
       FD   BP13F200                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13F200.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K830.                                                           
                                                                                
       FD   BP13L246                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   PRINT-REC                       PIC X(132).                         
                                                                                
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-INDICATORS.                                                       
           05  FILLER                       PIC X(10)  VALUE                    
               'INDICATORS'.                                                    
           05  WS-K200-STATUS               PIC 99     VALUE ZEROS.             
           05  WS-K800-STATUS               PIC 99     VALUE ZEROS.             
           05  WS-K820-STATUS               PIC 99     VALUE ZEROS.             
           05  WS-K830-STATUS               PIC 99     VALUE ZEROS.             
           05  WS-F205-EOF                  PIC X      VALUE 'N'.               
           05  WS-F200-EOF                  PIC X      VALUE 'N'.               
                                                                                
       01  WS-MATCH-FIELDS.                                                     
           05  WS-F200-MATCH-FIELDS.                                            
               10  WS-F200-DTE-ALLOC        PIC X(8).                           
               10  WS-F200-NUM-NT-ZONE      PIC X(3).                           
               10  WS-F200-NUM-FLAT-TYPE    PIC X(2).                           
           05  WS-F205-MATCH-FIELDS.                                            
               10  WS-F205-DTE-ALLOCN       PIC X(8).                           
               10  WS-F205-NUM-NT-ZONE      PIC X(3).                           
               10  WS-F205-NUM-FLAT-TYPE    PIC X(2).                           
                                                                                
       01  WS-COUNTERS.                                                         
           05  FILLER                       PIC X(08)  VALUE                    
               'COUNTERS'.                                                      
           05  WS-NUMBER                    PIC 99     VALUE 99.                
           05  WS-SERIAL                    PIC 9(4)   VALUE ZEROS.             
           05  WS-PAGE-CNT                  PIC 9(4)   VALUE ZEROS.             
                                                                                
       01  WS-SAVE-AREA.                                                        
           05  FILLER                       PIC X(09)  VALUE                    
               'SAVE AREA'.                                                     
           05  WS-NAME                      PIC X(22)  VALUE SPACES.            
           05  WS-PREV-FLAT-TYPE            PIC X(2)   VALUE SPACES.            
           05  WS-PREV-NT-ZONE-SW           PIC X(1)   VALUE SPACES.            
           05  WS-PREV-DTE-ALLOC            PIC X(8)   VALUE SPACES.            
           05  WS-PREV-NT-ZONE              PIC X(3)   VALUE SPACES.            
                                                                                
       01  SW-SERS-ALLO-SCHEME              PIC X(01)  VALUE SPACES.            
           88  C-SERS-ALLO-SCHEME           VALUE 'Y'.                          
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  FILLER                       PIC X(09)  VALUE                    
               'DATE VARS'.                                                     
           05  WS-CURR-DATE                 PIC X(8)   VALUE SPACES.            
           05  WS-ALLOCN-DATE.                                                  
               10  WS-ALLOCN-YY             PIC 9(4)   VALUE 0000.              
               10  WS-ALLOCN-MM             PIC 99     VALUE 00.                
               10  WS-ALLOCN-DD             PIC 99     VALUE 00.                
           05  WS-BKAPPT-DATE.                                                  
               10  WS-BKAPPT-YY             PIC 9(4)   VALUE 0000.              
               10  WS-BKAPPT-MM             PIC 99     VALUE 00.                
               10  WS-BKAPPT-DD             PIC 99     VALUE 00.                
           05  WS-EDITED-DATE.                                                  
               10  WS-EDIT-DD               PIC 99     VALUE 00.                
               10  FILLER                   PIC X      VALUE '/'.               
               10  WS-EDIT-MM               PIC 99     VALUE 00.                
               10  FILLER                   PIC X      VALUE '/'.               
               10  WS-EDIT-YY               PIC 9(4)   VALUE 0000.              
                                                                                
           05  WS-TIME.                                                         
               10  WS-HH                    PIC 99.                             
               10  WS-MM                    PIC 99.                             
                                                                                
       COPY SERSCH.                                                             
                                                                                
      *---------------------------------------------------------------*         
      *        P R I N T     L A Y O U T                              *         
      *---------------------------------------------------------------*         
      *-----------> LAYOUT 1 - FOR F205-NUM-NT-ZONE-SW = 'T' (NEW TOWN)         
       01  L246-HEADER-1.                                                       
           05  FILLER                       PIC X      VALUE SPACE.             
           05  FILLER                       PIC X(7)   VALUE 'PAGE :'.          
           05  L246-PAGE                    PIC ZZZ9.                           
                                                                                
       01  L246-HEADER-2.                                                       
           05  FILLER                       PIC X(42)  VALUE SPACES.            
           05  FILLER                       PIC X(41)  VALUE                    
               'CHECK LIST OF APPOINTMENT LETTERS PRINTED'.                     
           05  FILLER                       PIC X(24)  VALUE SPACES.            
           05  FILLER                       PIC X(21)  VALUE                    
               'LISTING-ID : BP13L246'.                                         
                                                                                
       01  L246-HEADER-3.                                                       
           05  FILLER                       PIC X(43)  VALUE SPACES.            
           05  FILLER                       PIC X(19)  VALUE SPACES.            
           05  FILLER                       PIC X(42)  VALUE SPACES.            
           05  FILLER                       PIC X(16)  VALUE                    
               '   RUNDATE :    '.                                              
           05  CONTROL-DATE                 PIC X(10)  VALUE SPACES.            
                                                                                
       01  L246-HEADER-4.                                                       
           05  FILLER                       PIC X(5)   VALUE SPACES.            
           05  FILLER                       PIC X(16)  VALUE                    
               'DATE OF ALLOC : '.                                              
           05  L246-ALLOC-DATE              PIC X(10)  VALUE SPACES.            
           05  FILLER                       PIC X(02)  VALUE SPACES.            
           05  FILLER                       PIC X(11)  VALUE                    
               'NEW TOWN : '.                                                   
           05  L246-NT-ZONE                 PIC X(3)   VALUE SPACES.            
           05  FILLER                       PIC X(21)  VALUE                    
               '         ROOM-TYPE : '.                                         
           05  L246-ROOM-TYPE               PIC X(2)   VALUE SPACES.            
                                                                                
       01  L246-LINE1.                                                          
           05  FILLER                       PIC X(27)  VALUE                    
               'Q/NO REGN-NO  FT JBCD ALSCH'.                                   
           05  FILLER                       PIC X(25)  VALUE                    
               'DATE/TIME OF APPOINTMENT '.                                     
           05  FILLER                       PIC X(28)  VALUE                    
               'CAT NAME'.                                                      
           05  FILLER                       PIC X(51)  VALUE                    
               'ADDRESS      '.                                                 
                                                                                
       01  L246-DATA1.                                                          
           05  L246-SERIAL                  PIC X(5)   VALUE SPACES.            
           05  FILLER                       PIC X(1)   VALUE SPACES.            
           05  L246-REGNO                   PIC X(8)   VALUE SPACES.            
           05  L246-FT                      PIC X(4)   VALUE SPACES.            
           05  FILLER                       PIC X(2)   VALUE SPACES.            
           05  L246-JB-CD                   PIC 9      VALUE ZERO.              
           05  FILLER                       PIC X(2)   VALUE SPACES.            
           05  L246-ALLOC-SCH               PIC X(3)   VALUE SPACES.            
           05  FILLER                       PIC X(2)   VALUE SPACES.            
           05  L246-DTE-BK-APPT             PIC X(10)  VALUE SPACES.            
           05  FILLER                       PIC X(1)   VALUE SPACES.            
           05  L246-APPT-HH                 PIC Z9.                             
           05  L246-DOT                     PIC X(1)   VALUE '.'.               
           05  L246-APPT-MM                 PIC 99.                             
           05  FILLER                       PIC X(1)   VALUE SPACES.            
           05  L246-AM-PM                   PIC X(2).                           
           05  FILLER                       PIC X(6)   VALUE SPACES.            
           05  L246-CAT                     PIC X(1)   VALUE SPACE.             
           05  FILLER                       PIC X(2)   VALUE SPACES.            
           05  L246-APPL-NAME               PIC X(22)  VALUE SPACES.            
           05  FILLER                       PIC X(2)   VALUE SPACES.            
           05  L246-BLK-NO                  PIC X(10)  VALUE SPACES.            
           05  FILLER                       PIC X(1)   VALUE SPACE.             
           05  L246-NUM-FLAT.                                                   
               10  L246-CHAR                PIC X(1)   VALUE SPACE.             
               10  L246-LEVEL               PIC X(2)   VALUE SPACES.            
               10  L246-DASH                PIC X(1)   VALUE SPACE.             
               10  L246-HSE-MAIN            PIC X(4)   VALUE SPACES.            
               10  L246-HSE-SUB             PIC X(1)   VALUE SPACE.             
           05  FILLER                       PIC X(1)   VALUE SPACE.             
           05  L246-STREET-NAME             PIC X(20)  VALUE SPACES.            
           05  FILLER                       PIC X(2)   VALUE SPACES.            
           05  L246-POSTAL-CD               PIC X(6)   VALUE SPACES.            
                                                                                
       01  L246-DATA2.                                                          
           05  FILLER                       PIC X(55)  VALUE SPACES.            
           05  L246-COAPPL-NAME             PIC X(22)  VALUE SPACES.            
                                                                                
      *-----------> LAYOUT 2 - FOR F205-NUM-NT-ZONE-SW = 'Z' (ZONE)             
       01  L246-HEADER-1B.                                                      
           05  FILLER                       PIC X(07)  VALUE                    
               'PAGE : '.                                                       
           05  L246-PAGEB                   PIC ZZZ9.                           
           05  FILLER                       PIC X(31)  VALUE SPACE.             
           05  FILLER                       PIC X(39)  VALUE                    
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                       PIC X(9)   VALUE SPACES.            
           05  FILLER                       PIC X(4)   VALUE                    
               'CAT3'.                                                          
           05  FILLER                       PIC X(6)   VALUE SPACES.            
           05  FILLER                       PIC X(21)  VALUE                    
               'LISTING-ID : BP13L246'.                                         
                                                                                
       01  L246-HEADER-2B.                                                      
           05  FILLER                       PIC X(50)  VALUE SPACES.            
           05  FILLER                       PIC X(50)  VALUE SPACES.            
           05  FILLER                       PIC X(10)  VALUE                    
               'RUNDATE : '.                                                    
           05  CONTROL-DATEB                PIC X(10)  VALUE SPACES.            
                                                                                
       01  L246-HEADER-3B.                                                      
           05  FILLER                       PIC X(42)  VALUE SPACES.            
           05  FILLER                       PIC X(41)  VALUE                    
               'CHECK LIST OF APPOINTMENT LETTERS PRINTED'.                     
           05  FILLER                       PIC X(24)  VALUE SPACES.            
           05  FILLER                       PIC X(21)  VALUE SPACES.            
                                                                                
       01  L246-HEADER-4B.                                                      
           05  FILLER                       PIC X(5)   VALUE SPACES.            
           05  FILLER                       PIC X(16)  VALUE                    
               'DATE OF ALLOC : '.                                              
           05  L246-ALLOC-DATEB             PIC X(10)  VALUE SPACES.            
           05  FILLER                       PIC X(02)  VALUE SPACES.            
           05  FILLER                       PIC X(11)  VALUE                    
               'ZONE     : '.                                                   
           05  L246-NT-ZONEB                PIC X(3)   VALUE SPACES.            
           05  FILLER                       PIC X(21)  VALUE                    
               '         ROOM-TYPE : '.                                         
           05  L246-ROOM-TYPEB              PIC X(2)   VALUE SPACES.            
                                                                                
       01  L246-LINE1B.                                                         
           05  FILLER                       PIC X(06)  VALUE                    
               'Q/NO  '.                                                        
           05  FILLER                       PIC X(03)  VALUE                    
               'HT/'.                                                           
           05  FILLER                       PIC X(18)  VALUE                    
               'REGN-NO           '.                                            
           05  FILLER                       PIC X(05)  VALUE                    
               'JBCD '.                                                         
           05  FILLER                       PIC X(06)  VALUE                    
               'ALSCH '.                                                        
           05  FILLER                       PIC X(17)  VALUE                    
               'DTE/TME OF APPMT '.                                             
           05  FILLER                       PIC X(27)  VALUE                    
               'CAT NAME (TRUNCATED)'.                                          
           05  FILLER                       PIC X(50)  VALUE                    
               'ADDRESS     '.                                                  
                                                                                
       01  L246-DATA1B.                                                         
           05  L246-Q-NO                    PIC X(5)   VALUE SPACES.            
           05  FILLER                       PIC X(2)   VALUE SPACES.            
           05  L246-HT                      PIC X(1)   VALUE SPACE.             
           05  L246-HT-SLASH                PIC X(1)   VALUE SPACE.             
           05  L246-SEQ-NO                  PIC X(7)   VALUE SPACES.            
           05  L246-SEQ-SLASH               PIC X(1)   VALUE SPACE.             
           05  L246-REGNOB                  PIC X(8)   VALUE SPACES.            
           05  FILLER                       PIC X(4)   VALUE SPACES.            
           05  L246-JB-CDB                  PIC 9(1)   VALUE ZERO.              
           05  FILLER                       PIC X(4)   VALUE SPACES.            
           05  L246-ALLOC-SCHB              PIC X(3)   VALUE SPACES.            
           05  FILLER                       PIC X(1)   VALUE SPACES.            
           05  L246-DTE-BK-APPTB            PIC X(10)  VALUE SPACES.            
           05  FILLER                       PIC X(1)   VALUE SPACES.            
           05  L246-APPT-HHB                PIC Z9     VALUE ZEROES.            
           05  L246-DOTB                    PIC X(1)   VALUE SPACE.             
           05  L246-APPT-MMB                PIC 9(2)   VALUE ZEROES.            
           05  FILLER                       PIC X(2)   VALUE SPACES.            
           05  L246-CATB                    PIC X(1)   VALUE SPACE.             
           05  FILLER                       PIC X(2)   VALUE SPACES.            
           05  L246-APPL-NAMEB              PIC X(22)  VALUE SPACES.            
           05  FILLER                       PIC X(2)   VALUE SPACES.            
           05  L246-BLK-NOB                 PIC X(10)  VALUE SPACES.            
           05  FILLER                       PIC X(1)   VALUE SPACE.             
           05  L246-NUM-FLATB.                                                  
               10  L246-CHARB               PIC X(1)   VALUE SPACE.             
               10  L246-LEVELB              PIC X(2)   VALUE SPACES.            
               10  L246-DASHB               PIC X(1)   VALUE SPACE.             
               10  L246-HSE-MAINB           PIC X(4)   VALUE SPACES.            
               10  L246-HSE-SUBB            PIC X(1)   VALUE SPACE.             
           05  FILLER                       PIC X(1)   VALUE SPACE.             
           05  L246-STREET-NAMEB            PIC X(20)  VALUE SPACES.            
           05  FILLER                       PIC X(2)   VALUE SPACES.            
           05  L246-POSTAL-CDB              PIC X(6)   VALUE SPACES.            
                                                                                
       01  L246-DATA2B.                                                         
           05  FILLER                       PIC X(8)   VALUE SPACES.            
           05  L246-SLASHB                  PIC X(1)   VALUE SPACES.            
           05  L246-JB-REGNB                PIC X(8)   VALUE SPACES.            
           05  FILLER                       PIC X(42)  VALUE SPACES.            
           05  L246-COAPPL-NAMEB            PIC X(22)  VALUE SPACES.            
                                                                                
                                                                                
                                                                                
      *********************                                                     
       PROCEDURE DIVISION.                                                      
      *********************                                                     
                                                                                
                                                                                
      ******************************************************************        
       000-MAIN-ROUTINE.                                                        
      ******************************************************************        
                                                                                
           PERFORM 100-OPEN-FILES-RTN      THRU   100-EXIT.                     
           PERFORM 200-READ-BP13F205       THRU   200-EXIT.                     
           MOVE    F205-NUM-NT-ZONE-SW     TO     WS-PREV-NT-ZONE-SW.           
           MOVE    SPACES                  TO     WS-F200-MATCH-FIELDS.         
           PERFORM 300-PROCESS-F200        THRU   300-EXIT                      
                       UNTIL WS-F205-EOF = 'Y'.                                 
           PERFORM 900-CLOSE-FILES-RTN     THRU   900-EXIT.                     
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       100-OPEN-FILES-RTN.                                                      
      ******************************************************************        
           OPEN INPUT  BP13F205                                                 
                       BP13F200                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BP13K830                                                 
                OUTPUT BP13L246.                                                
                                                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BP13K800 = ' WS-K800-STATUS              
              MOVE    WS-K800-STATUS  TO RETURN-CODE                            
              PERFORM 900-CLOSE-FILES-RTN.                                      
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BP13K820 = ' WS-K820-STATUS              
              MOVE    WS-K820-STATUS  TO RETURN-CODE                            
              PERFORM 900-CLOSE-FILES-RTN.                                      
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BP13K830 = ' WS-K830-STATUS              
              MOVE    WS-K830-STATUS  TO RETURN-CODE                            
              PERFORM 900-CLOSE-FILES-RTN.                                      
                                                                                
           MOVE 99  TO WS-NUMBER.                                               
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       200-READ-BP13F205.                                                       
      ******************************************************************        
                                                                                
           READ BP13F205                                                        
                AT END MOVE 'Y' TO WS-F205-EOF                                  
                GO TO  200-EXIT.                                                
                                                                                
           MOVE 'N' TO WS-F200-EOF.                                             
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       300-PROCESS-F200.                                                        
      ******************************************************************        
                                                                                
           MOVE    'N'                     TO    WS-F200-EOF.                   
                                                                                
           MOVE    ZEROS                   TO    WS-SERIAL.                     
                                                                                
           MOVE F205-DTE-ALLOCN          TO WS-F205-DTE-ALLOCN.                 
           MOVE F205-NUM-NT-ZONE         TO WS-F205-NUM-NT-ZONE.                
           MOVE F205-NUM-FLAT-TYPE       TO WS-F205-NUM-FLAT-TYPE.              
                                                                                
           PERFORM UNTIL WS-F200-EOF = 'Y'                                      
                   OR    WS-F200-MATCH-FIELDS = WS-F205-MATCH-FIELDS            
              PERFORM 400-READ-F200           THRU  400-EXIT                    
           END-PERFORM.                                                         
                                                                                
           PERFORM 500-READ-K800           THRU  500-EXIT                       
             UNTIL WS-F200-EOF = 'Y'.                                           
                                                                                
           PERFORM 200-READ-BP13F205  THRU  200-EXIT.                           
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       400-READ-F200.                                                           
      ******************************************************************        
                                                                                
           READ BP13F200                                                        
              AT END MOVE 'Y'   TO WS-F200-EOF                                  
           END-READ.                                                            
                                                                                
           MOVE SPACES       TO L246-DATA1 L246-DATA1B.                         
           MOVE '.'          TO L246-DOT   L246-DOTB.                           
           MOVE '/'          TO L246-HT-SLASH                                   
                                L246-SEQ-SLASH.                                 
                                                                                
           MOVE F200-DTE-ALLOC        TO WS-F200-DTE-ALLOC.                     
           MOVE F200-NUM-NT-ZONE      TO WS-F200-NUM-NT-ZONE.                   
           MOVE F200-NUM-FLAT-TYPE    TO WS-F200-NUM-FLAT-TYPE.                 
                                                                                
           IF WS-F200-MATCH-FIELDS  > WS-F205-MATCH-FIELDS                      
              MOVE 'Y'          TO WS-F200-EOF                                  
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       500-READ-K800.                                                           
      ******************************************************************        
                                                                                
           MOVE F200-NUM-REGN  TO   K800-NUM-REGN.                              
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = 00                                               
              PERFORM 550-READ-K820 THRU 550-EXIT                               
           ELSE                                                                 
           IF WS-K800-STATUS = 23                                               
              MOVE SPACES TO L246-APPL-NAME                                     
                             L246-COAPPL-NAME                                   
              MOVE SPACES TO L246-APPL-NAMEB                                    
                             L246-COAPPL-NAMEB                                  
              DISPLAY 'RECORD NOT FOUND IN BP13K800 ' WS-K800-STATUS            
              DISPLAY '       K800-NUM-REGN = ' K800-NUM-REGN                   
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K800 = ' WS-K800-STATUS                
              DISPLAY '      K800-NUM-REGN    = ' K800-NUM-REGN                 
              MOVE    WS-K800-STATUS  TO  RETURN-CODE                           
              PERFORM 900-CLOSE-FILES-RTN.                                      
                                                                                
           PERFORM 700-GET-ADDRESS    THRU 700-EXIT.                            
           PERFORM 800-PROCESS-PRINT  THRU 800-EXIT.                            
           MOVE    F200-NUM-FLAT-TYPE      TO    WS-PREV-FLAT-TYPE              
                                                                                
           PERFORM 400-READ-F200      THRU 400-EXIT.                            
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       550-READ-K820.                                                           
      ******************************************************************        
           MOVE    K800-NUM-REGN    TO    K820-NUM-REGN.                        
           MOVE    K800-NUM-NRIC1   TO    K820-NUM-NRIC.                        
           PERFORM 560-ACCESS-K820  THRU  560-EXIT.                             
           MOVE    WS-NAME          TO    L246-APPL-NAME.                       
           MOVE    WS-NAME          TO    L246-APPL-NAMEB.                      
                                                                                
                                                                                
           IF K800-NUM-NRIC2(2:1)  NOT =  '#'    AND                            
              K800-NUM-NRIC2       NOT =  SPACES AND LOW-VALUES                 
              MOVE     K800-NUM-REGN   TO   K820-NUM-REGN                       
              MOVE     K800-NUM-NRIC2  TO   K820-NUM-NRIC                       
              PERFORM  560-ACCESS-K820 THRU 560-EXIT                            
              MOVE     WS-NAME         TO   L246-COAPPL-NAME                    
                                            L246-COAPPL-NAMEB                   
           ELSE                                                                 
              MOVE     SPACES          TO   L246-COAPPL-NAME                    
                                            L246-COAPPL-NAMEB.                  
                                                                                
       550-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       560-ACCESS-K820.                                                         
      ******************************************************************        
                                                                                
           READ BP13K820 KEY IS K820-KEY-FLD.                                   
                                                                                
           IF WS-K820-STATUS = 00                                               
              MOVE    K820-NME-OCCP  TO WS-NAME                                 
           ELSE                                                                 
           IF WS-K820-STATUS = 23                                               
              MOVE    SPACES         TO WS-NAME                                 
              DISPLAY 'RECORD NOT FOUND IN K820 ' WS-K820-STATUS                
              DISPLAY '       K820-KEY-FLD = ' K820-KEY-FLD                     
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K820 = ' WS-K820-STATUS                
              DISPLAY '      K820-KEY-FLD     = ' K820-KEY-FLD                  
              MOVE    WS-K820-STATUS  TO RETURN-CODE                            
              PERFORM 900-CLOSE-FILES-RTN.                                      
                                                                                
       560-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       700-GET-ADDRESS.                                                         
      ******************************************************************        
           MOVE F200-NUM-REGN TO K830-NUM-REGN.                                 
                                                                                
           READ BP13K830 KEY  IS K830-NUM-REGN.                                 
                                                                                
           IF WS-K830-STATUS = 00 OR 02                                         
              IF K830-NUM-MAIL-ADDR   =  SPACES OR LOW-VALUES                   
                 IF K830-NUM-ADDR-TYPE = 'H'                                    
                    IF F205-NUM-NT-ZONE-SW  = 'T'                               
                       MOVE SPACES                 TO L246-STREET-NAME          
                       MOVE K830-NUM-BLK           TO L246-BLK-NO               
                       MOVE K830-NUM-LVL-CHAR      TO L246-CHAR                 
                       MOVE K830-NUM-LVL-NUM       TO L246-LEVEL                
                       MOVE K830-NUM-LVL-DASH      TO L246-DASH                 
                       MOVE K830-NUM-HSEMAIN       TO L246-HSE-MAIN             
                       MOVE K830-NUM-HSESUB        TO L246-HSE-SUB              
                       MOVE K830-NME-STREET-BLDG   TO L246-STREET-NAME          
                       MOVE K830-NUM-POSTAL        TO L246-POSTAL-CD            
                    ELSE                                                        
                       MOVE SPACES                 TO L246-STREET-NAMEB         
                       MOVE K830-NUM-BLK           TO L246-BLK-NOB              
                       MOVE K830-NUM-LVL-CHAR      TO L246-CHARB                
                       MOVE K830-NUM-LVL-NUM       TO L246-LEVELB               
                       MOVE K830-NUM-LVL-DASH      TO L246-DASHB                
                       MOVE K830-NUM-HSEMAIN       TO L246-HSE-MAINB            
                       MOVE K830-NUM-HSESUB        TO L246-HSE-SUBB             
                       MOVE K830-NME-STREET-BLDG   TO L246-STREET-NAMEB         
                       MOVE K830-NUM-POSTAL        TO L246-POSTAL-CDB           
                    END-IF                                                      
                 ELSE                                                           
                    IF F205-NUM-NT-ZONE-SW  = 'T'                               
                       MOVE SPACES                 TO L246-STREET-NAME          
                       MOVE K830-NUM-BLK           TO L246-BLK-NO               
                       MOVE K830-NUM-FLAT          TO L246-NUM-FLAT             
                       MOVE K830-NME-STREET-BLDG   TO L246-STREET-NAME          
                       MOVE K830-NUM-POSTAL        TO L246-POSTAL-CD            
                    ELSE                                                        
                       MOVE SPACES                 TO L246-STREET-NAMEB         
                       MOVE K830-NUM-BLK           TO L246-BLK-NOB              
                       MOVE K830-NUM-FLAT          TO L246-NUM-FLATB            
                       MOVE K830-NME-STREET-BLDG   TO L246-STREET-NAMEB         
                       MOVE K830-NUM-POSTAL        TO L246-POSTAL-CDB           
                    END-IF                                                      
                 END-IF                                                         
              ELSE                                                              
                 IF K830-NUM-MAIL-ADDR-TYPE =  'H'                              
                    IF F205-NUM-NT-ZONE-SW  = 'T'                               
                       MOVE SPACES                 TO L246-STREET-NAME          
                       MOVE K830-NUM-MAIL-BLK      TO L246-BLK-NO               
                       MOVE K830-NUM-MAIL-LVL-CHAR TO L246-CHAR                 
                       MOVE K830-NUM-MAIL-LVL-NUM  TO L246-LEVEL                
                       MOVE K830-NUM-MAIL-LVL-DASH TO L246-DASH                 
                       MOVE K830-NUM-MAIL-HSEMAIN  TO L246-HSE-MAIN             
                       MOVE K830-NUM-MAIL-HSESUB   TO L246-HSE-SUB              
                       MOVE K830-NME-MAIL-STR-BLDG TO L246-STREET-NAME          
                       MOVE K830-NUM-MAIL-POSTAL   TO L246-POSTAL-CD            
                    ELSE                                                        
                       MOVE SPACES                 TO L246-STREET-NAMEB         
                       MOVE K830-NUM-MAIL-BLK      TO L246-BLK-NOB              
                       MOVE K830-NUM-MAIL-LVL-CHAR TO L246-CHARB                
                       MOVE K830-NUM-MAIL-LVL-NUM  TO L246-LEVELB               
                       MOVE K830-NUM-MAIL-LVL-DASH TO L246-DASHB                
                       MOVE K830-NUM-MAIL-HSEMAIN  TO L246-HSE-MAINB            
                       MOVE K830-NUM-MAIL-HSESUB   TO L246-HSE-SUBB             
                       MOVE K830-NME-MAIL-STR-BLDG TO L246-STREET-NAMEB         
                       MOVE K830-NUM-MAIL-POSTAL   TO L246-POSTAL-CDB           
                    END-IF                                                      
                 ELSE                                                           
                    IF F205-NUM-NT-ZONE-SW = 'T'                                
                       MOVE SPACES                 TO L246-STREET-NAME          
                       MOVE K830-NUM-MAIL-BLK      TO L246-BLK-NO               
                       MOVE K830-NUM-MAIL-FLAT     TO L246-NUM-FLAT             
                       MOVE K830-NME-MAIL-STR-BLDG TO L246-STREET-NAME          
                       MOVE K830-NUM-MAIL-POSTAL   TO L246-POSTAL-CD            
                    ELSE                                                        
                       MOVE SPACES                 TO L246-STREET-NAMEB         
                       MOVE K830-NUM-MAIL-BLK      TO L246-BLK-NOB              
                       MOVE K830-NUM-MAIL-FLAT     TO L246-NUM-FLATB            
                       MOVE K830-NME-MAIL-STR-BLDG TO L246-STREET-NAMEB         
                       MOVE K830-NUM-MAIL-POSTAL   TO L246-POSTAL-CDB           
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS = 23                                               
              MOVE SPACES    TO  L246-BLK-NO     L246-STREET-NAME               
                                 L246-NUM-FLAT   L246-POSTAL-CD                 
              MOVE SPACES    TO  L246-BLK-NOB    L246-STREET-NAMEB              
                                 L246-NUM-FLATB  L246-POSTAL-CDB                
              DISPLAY 'RECORD NOT FOUND IN BP13K830 ' WS-K830-STATUS            
              DISPLAY '       K830-NUM-REGN  = ' K830-NUM-REGN.                 
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 02 AND 23                             
              DISPLAY 'ERROR READING BP13K830 = ' WS-K830-STATUS                
              DISPLAY '      K830-NUM-REGN    = ' K830-NUM-REGN                 
              MOVE    WS-K830-STATUS  TO  RETURN-CODE                           
              PERFORM 900-CLOSE-FILES-RTN.                                      
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       800-PROCESS-PRINT.                                                       
      ******************************************************************        
                                                                                
      *----------------> 820-PRINT-HEADING - FOR NEW TOWN                       
      *----------------> 821-PRINT-HEADING - FOR ZONE                           
                                                                                
           IF F205-NUM-NT-ZONE-SW NOT = WS-PREV-NT-ZONE-SW                      
              MOVE ZEROS                     TO WS-PAGE-CNT                     
              PERFORM 810-CHECK-HEADING THRU 810-EXIT                           
              MOVE F205-NUM-NT-ZONE-SW       TO WS-PREV-NT-ZONE-SW              
              MOVE F200-NUM-FLAT-TYPE        TO WS-PREV-FLAT-TYPE               
              MOVE F200-DTE-ALLOC            TO WS-PREV-DTE-ALLOC               
              MOVE F200-NUM-NT-ZONE          TO WS-PREV-NT-ZONE                 
           ELSE                                                                 
              IF (F200-NUM-FLAT-TYPE NOT = WS-PREV-FLAT-TYPE) OR                
                 (F200-DTE-ALLOC     NOT = WS-PREV-DTE-ALLOC) OR                
                 (F200-NUM-NT-ZONE   NOT = WS-PREV-NT-ZONE)                     
                 PERFORM 810-CHECK-HEADING THRU 810-EXIT                        
                 MOVE F200-NUM-FLAT-TYPE        TO WS-PREV-FLAT-TYPE            
                 MOVE F200-DTE-ALLOC            TO WS-PREV-DTE-ALLOC            
                 MOVE F200-NUM-NT-ZONE          TO WS-PREV-NT-ZONE              
                 MOVE 0                         TO WS-SERIAL                    
              ELSE                                                              
                 IF WS-NUMBER > 15                                              
                    PERFORM 810-CHECK-HEADING THRU 810-EXIT                     
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *----------------> 840-PRINT-DETAIL  - FOR NEW TOWN                       
      *----------------> 841-PRINT-DETAIL  - FOR ZONE                           
                                                                                
           IF F205-NUM-NT-ZONE-SW = 'T'                                         
              PERFORM 840-PRINT-DETAIL   THRU   840-EXIT                        
           ELSE                                                                 
           IF F205-NUM-NT-ZONE-SW = 'Z'                                         
              PERFORM 841-PRINT-DETAIL   THRU   841-EXIT                        
           END-IF.                                                              
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       810-CHECK-HEADING.                                                       
      ******************************************************************        
                                                                                
           IF F205-NUM-NT-ZONE-SW = 'T'                                         
              PERFORM 820-PRINT-HEADING THRU 820-EXIT                           
           ELSE                                                                 
           IF F205-NUM-NT-ZONE-SW = 'Z'                                         
              PERFORM 821-PRINT-HEADING THRU 821-EXIT.                          
                                                                                
       810-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       820-PRINT-HEADING.                                                       
      ******************************************************************        
                                                                                
      *--> 820-PRINT-HEADING - FOR NEW TOWN                                     
                                                                                
           ADD    1                   TO  WS-PAGE-CNT.                          
           MOVE   ZEROS               TO  WS-NUMBER.                            
           MOVE   WS-PAGE-CNT         TO  L246-PAGE.                            
           WRITE  PRINT-REC         FROM  L246-HEADER-1 AFTER PAGE.             
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE.                          
           STRING WS-CURR-DATE (7:2) '/'                                        
                  WS-CURR-DATE (5:2) '/'                                        
                  WS-CURR-DATE (1:4)                                            
                  DELIMITED BY SIZE                                             
                  INTO CONTROL-DATE.                                            
           WRITE  PRINT-REC         FROM  L246-HEADER-2 AFTER 1.                
           WRITE  PRINT-REC         FROM  L246-HEADER-3 AFTER 1.                
           MOVE   F200-DTE-ALLOC      TO  WS-ALLOCN-DATE.                       
           MOVE   WS-ALLOCN-DD        TO  WS-EDIT-DD.                           
           MOVE   WS-ALLOCN-MM        TO  WS-EDIT-MM.                           
           MOVE   WS-ALLOCN-YY        TO  WS-EDIT-YY.                           
           MOVE   WS-EDITED-DATE      TO  L246-ALLOC-DATE.                      
           MOVE   F200-NUM-NT-ZONE    TO  L246-NT-ZONE.                         
           MOVE   F200-NUM-FLAT-TYPE  TO  L246-ROOM-TYPE.                       
           WRITE  PRINT-REC         FROM  L246-HEADER-4 AFTER 2.                
           WRITE  PRINT-REC         FROM  L246-LINE1    AFTER 2.                
           MOVE   SPACES              TO  PRINT-REC.                            
                                                                                
       820-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       821-PRINT-HEADING.                                                       
      ******************************************************************        
                                                                                
      *--> 821-PRINT-HEADING - FOR ZONE                                         
                                                                                
           ADD    1                   TO  WS-PAGE-CNT.                          
           MOVE   ZEROS               TO  WS-NUMBER.                            
           MOVE   WS-PAGE-CNT         TO  L246-PAGEB.                           
           WRITE  PRINT-REC         FROM  L246-HEADER-1B AFTER PAGE.            
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE.                          
           STRING WS-CURR-DATE (7:2) '/'                                        
                  WS-CURR-DATE (5:2) '/'                                        
                  WS-CURR-DATE (1:4)                                            
                  DELIMITED BY SIZE                                             
                  INTO CONTROL-DATEB.                                           
           WRITE  PRINT-REC         FROM  L246-HEADER-2B AFTER 1.               
           WRITE  PRINT-REC         FROM  L246-HEADER-3B AFTER 2.               
           MOVE   F200-DTE-ALLOC      TO  WS-ALLOCN-DATE.                       
           MOVE   WS-ALLOCN-DD        TO  WS-EDIT-DD.                           
           MOVE   WS-ALLOCN-MM        TO  WS-EDIT-MM.                           
           MOVE   WS-ALLOCN-YY        TO  WS-EDIT-YY.                           
           MOVE   WS-EDITED-DATE      TO  L246-ALLOC-DATEB.                     
           MOVE   F200-NUM-NT-ZONE    TO  L246-NT-ZONEB.                        
           MOVE   F200-NUM-FLAT-TYPE  TO  L246-ROOM-TYPEB.                      
           WRITE  PRINT-REC         FROM  L246-HEADER-4B AFTER 2.               
           WRITE  PRINT-REC         FROM  L246-LINE1B    AFTER 2.               
           MOVE   SPACES              TO  PRINT-REC.                            
                                                                                
       821-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       840-PRINT-DETAIL.                                                        
      ******************************************************************        
                                                                                
      *--> 840-PRINT-DETAIL - FOR NEW TOWN                                      
           ADD   1                          TO   WS-NUMBER.                     
           MOVE  F200-NUM-HHTY-QUEUE-SERIAL TO   L246-SERIAL.                   
           MOVE  K800-NUM-REGN              TO   L246-REGNO.                    
           PERFORM 888-CHK-SERS-ALSCH       THRU 888-EXIT.                      
           IF C-SERS-ALLO-SCHEME                                                
              STRING '(' F200-NUM-ACTUAL-FT ')'                                 
                DELIMITED BY SIZE                                               
                INTO L246-FT                                                    
              END-STRING                                                        
           ELSE                                                                 
              MOVE SPACES             TO L246-FT                                
           END-IF.                                                              
                                                                                
           MOVE  K800-NUM-JT-BALLOT-MTF    TO   L246-JB-CD.                     
           MOVE  K800-NUM-ALLO-SCHEME      TO   L246-ALLOC-SCH.                 
           MOVE  K800-NUM-CAT              TO   L246-CAT.                       
           MOVE  F200-DTE-BKAPPMT-DATE     TO   WS-BKAPPT-DATE.                 
           MOVE  WS-BKAPPT-DD              TO   WS-EDIT-DD.                     
           MOVE  WS-BKAPPT-MM              TO   WS-EDIT-MM.                     
           MOVE  WS-BKAPPT-YY              TO   WS-EDIT-YY.                     
           MOVE  WS-EDITED-DATE            TO   L246-DTE-BK-APPT.               
           MOVE  F200-TME-BKAPPMT          TO   WS-TIME.                        
                                                                                
           IF WS-HH = 12                                                        
              MOVE 'PM'                    TO   L246-AM-PM                      
           ELSE                                                                 
           IF WS-HH < 12                                                        
              MOVE 'AM'                    TO   L246-AM-PM                      
           ELSE                                                                 
              MOVE 'PM'                    TO   L246-AM-PM                      
              SUBTRACT 12 FROM WS-HH.                                           
                                                                                
           MOVE  WS-HH                     TO   L246-APPT-HH.                   
           MOVE  WS-MM                     TO   L246-APPT-MM.                   
                                                                                
           WRITE   PRINT-REC   FROM   L246-DATA1 AFTER 2.                       
           WRITE   PRINT-REC   FROM   L246-DATA2 AFTER 1.                       
                                                                                
       840-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       841-PRINT-DETAIL.                                                        
      ******************************************************************        
                                                                                
      *--> 841-PRINT-DETAIL - FOR ZONE                                          
           ADD   1                         TO   WS-NUMBER.                      
           MOVE  K800-NUM-REGN             TO   L246-REGNOB.                    
           MOVE  K800-NUM-JT-BALLOT-MTF    TO   L246-JB-CDB.                    
           MOVE  K800-NUM-ALLO-SCHEME      TO   L246-ALLOC-SCHB.                
           MOVE  K800-NUM-CAT              TO   L246-CATB.                      
           MOVE  F200-DTE-BKAPPMT-DATE     TO   WS-BKAPPT-DATE.                 
           MOVE  WS-BKAPPT-DD              TO   WS-EDIT-DD.                     
           MOVE  WS-BKAPPT-MM              TO   WS-EDIT-MM.                     
           MOVE  WS-BKAPPT-YY              TO   WS-EDIT-YY.                     
           MOVE  WS-EDITED-DATE            TO   L246-DTE-BK-APPTB.              
           MOVE  F200-TME-BKAPPMT          TO   WS-TIME.                        
                                                                                
           MOVE  WS-HH                     TO   L246-APPT-HHB.                  
           MOVE  WS-MM                     TO   L246-APPT-MMB.                  
                                                                                
           EVALUATE F200-NUM-HHTY-QUEUE-ALPHA                                   
             WHEN 'H'                                                           
                  MOVE 'F' TO L246-HT                                           
             WHEN 'G'                                                           
                  MOVE 'U' TO L246-HT                                           
             WHEN 'C'                                                           
                  MOVE 'G' TO L246-HT                                           
             WHEN OTHER                                                         
                  MOVE F200-NUM-HHTY-QUEUE-ALPHA TO L246-HT                     
           END-EVALUATE.                                                        
                                                                                
           MOVE F200-NUM-HHTY-QUEUE-SERIAL TO   L246-Q-NO.                      
                                                                                
           MOVE F200-NUM-SEQUENCE TO L246-SEQ-NO.                               
                                                                                
           IF (K800-NUM-JT-BALLOT-MTF = 1 OR 2 OR 3 OR 5) OR                    
              (K800-NUM-ALLO-SCHEME   = 'JB ')                                  
               MOVE '/'                     TO   L246-SLASHB                    
               MOVE F200-NUM-JB-PARENT-REGN TO   L246-JB-REGNB                  
           ELSE                                                                 
               MOVE SPACES                  TO   L246-SLASHB                    
               MOVE SPACES                  TO   L246-JB-REGNB                  
           END-IF.                                                              
                                                                                
           WRITE   PRINT-REC   FROM   L246-DATA1B AFTER 2.                      
           WRITE   PRINT-REC   FROM   L246-DATA2B AFTER 1.                      
                                                                                
       841-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       888-CHK-SERS-ALSCH.                                                      
      *-------------------------------------------------------------            
      *----------------------------------------------------------------         
      * SEARCH SERS TABLE TO CHECK ALLOC SCHEME                                 
      *----------------------------------------------------------------         
           SET WS-SERS-PTR TO 1.                                                
           SEARCH WS-SERS-SCH                                                   
              AT END                                                            
                 MOVE 'N'      TO SW-SERS-ALLO-SCHEME                           
              WHEN WS-SERS-SCHEME (WS-SERS-PTR) = K800-NUM-ALLO-SCHEME          
                 MOVE 'Y'      TO SW-SERS-ALLO-SCHEME                           
           END-SEARCH.                                                          
                                                                                
       888-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       900-CLOSE-FILES-RTN.                                                     
      ******************************************************************        
           CLOSE BP13F205                                                       
                 BP13F200                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K830                                                       
                 BP13L246.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00                                           
              DISPLAY 'FILE BP13K800  CLOSE ERROR ' WS-K800-STATUS              
              MOVE    WS-K800-STATUS  TO    RETURN-CODE.                        
                                                                                
           IF WS-K820-STATUS NOT = 00                                           
              DISPLAY 'FILE BP13K820  CLOSE ERROR ' WS-K820-STATUS              
              MOVE    WS-K820-STATUS  TO    RETURN-CODE.                        
                                                                                
           IF WS-K830-STATUS NOT = 00                                           
              DISPLAY 'FILE BP13K830  CLOSE ERROR ' WS-K830-STATUS              
              MOVE    WS-K830-STATUS  TO    RETURN-CODE.                        
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
