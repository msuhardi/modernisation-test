       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13CE19.                                                  
      *AUTHOR.       GURU.                                                      
      *DATE-WRITTEN. 23/03/2020.                                                
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BM06)                                *            
      * ========================================================== *            
      *  OBJECTIVE: PRINT REPORT ON SINGLE FTS WITH SIGNLE PREMIUM *            
      *           : ELDERLY PREMIUM                                *            
      *                                                            *            
      *  INPUT   :  BP13F800                                       *            
      *          :  BM06K110                                       *            
      *  OUTPUT  :  P13F800A                                       *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG-NO    BY     DATE     DETAILS                          *            
      * -------- ----  --------  -------                           *            
      * BP138260 KR13  23032020  NEW PROGRAM.                      *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K110-KEY-FLD                               
                  FILE STATUS     IS WS-K110-STATUS.                            
                                                                                
           SELECT P13F800A ASSIGN TO P13F800A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F800                                                             
           BLOCK CONTAINS 0 CHARACTERS                                          
           RECORD CONTAINS 2000 CHARACTERS                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F800.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  P13F800A                                                             
           BLOCK CONTAINS 0 CHARACTERS                                          
           RECORD CONTAINS 120 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       01  P13F800A-REC.                                                        
           05 WS-SCH-ACC-NO              PIC X(11).                             
           05 WS-FILLER01                PIC X(01).                             
           05 WS-REGN-NO                 PIC X(08).                             
           05 WS-FILLER02                PIC X(01).                             
           05 WS-AMT-SING-PREM           PIC 9(05).                             
           05 WS-FILLER03                PIC X(01).                             
           05 WS-AMT-ELDR-PREM           PIC 9(05).                             
           05 WS-FILLER04                PIC X(01).                             
           05 WS-ALLOC-SCH               PIC X(03).                             
           05 WS-FILLER05                PIC X(01).                             
           05 WS-DT-KEY-ISS              PIC X(08).                             
           05 WS-FILLER06                PIC X(01).                             
           05 WS-DT-ACPT                 PIC X(08).                             
           05 WS-FILLER07                PIC X(01).                             
           05 WS-ELDER                   PIC X(01).                             
           05 WS-FILLER08                PIC X(01).                             
           05 WS-ELDER-PREM              PIC X(01).                             
           05 WS-FILLER09                PIC X(01).                             
           05 WS-NUM-PPO                 PIC X(01).                             
           05 WS-FILLER10                PIC X(01).                             
           05 WS-NUM-NTIMER              PIC X(01).                             
           05 WS-FILLER11                PIC X(01).                             
           05 WS-NUM-ELIG                PIC X(03).                             
           05 WS-FILLER12                PIC X(01).                             
           05 WS-NUM-INCOME              PIC X(05).                             
           05 WS-FILLER13                PIC X(01).                             
           05 WS-TENURE                  PIC X(02).                             
           05 WS-FILLER14                PIC X(01).                             
           05 FILLER                     PIC X(44) VALUE SPACES.                
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-F800-EOF                   PIC X    VALUE 'N'.                    
       01  WS-K110-STATUS                PIC 99   VALUE ZEROS.                  
                                                                                
                                                                                
       01  WS-HEADER.                                                           
           05  FILLER                   PIC X(11) VALUE 'SCH ACC NO'.           
           05  FILLER                   PIC X(01) VALUE ';'.                    
           05  FILLER                   PIC X(08) VALUE 'REGN'.                 
           05  FILLER                   PIC X(01) VALUE ';'.                    
           05  FILLER                   PIC X(05) VALUE 'SPREM'.                
           05  FILLER                   PIC X(01) VALUE ';'.                    
           05  FILLER                   PIC X(05) VALUE 'EPREM'.                
           05  FILLER                   PIC X(01) VALUE ';'.                    
           05  FILLER                   PIC X(03) VALUE 'ALO'.                  
           05  FILLER                   PIC X(01) VALUE ';'.                    
           05  FILLER                   PIC X(08) VALUE 'KEY-ISS'.              
           05  FILLER                   PIC X(01) VALUE ';'.                    
           05  FILLER                   PIC X(08) VALUE 'DT-ACPT'.              
           05  FILLER                   PIC X(01) VALUE ';'.                    
           05  FILLER                   PIC X(03) VALUE 'ELD'.                  
           05  FILLER                   PIC X(01) VALUE ';'.                    
           05  FILLER                   PIC X(08) VALUE 'ELD-PREM'.             
           05  FILLER                   PIC X(01) VALUE ';'.                    
           05  FILLER                   PIC X(03) VALUE 'PPO'.                  
           05  FILLER                   PIC X(01) VALUE ';'.                    
           05  FILLER                   PIC X(04) VALUE 'NTME'.                 
           05  FILLER                   PIC X(01) VALUE ';'.                    
           05  FILLER                   PIC X(03) VALUE 'ELG'.                  
           05  FILLER                   PIC X(01) VALUE ';'.                    
           05  FILLER                   PIC X(06) VALUE 'INCOME'.               
           05  FILLER                   PIC X(01) VALUE ';'.                    
           05  FILLER                   PIC X(06) VALUE 'TENURE'.               
           05  FILLER                   PIC X(01) VALUE ';'.                    
           05  FILLER                   PIC X(25) VALUE SPACES.                 
                                                                                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F800READ               PIC 9(9) VALUE ZEROS.                  
           05  WS-K110-READ              PIC 9(9) VALUE ZEROS.                  
           05  WS-K110-NOTFND            PIC 9(9) VALUE ZEROS.                  
           05  WS-ANY-PREM               PIC 9(9) VALUE ZEROS.                  
           05  WS-NO-PREM                PIC 9(9) VALUE ZEROS.                  
           05  WS-SING-PREM              PIC 9(9) VALUE ZEROS.                  
           05  WS-ELDR-PREM              PIC 9(9) VALUE ZEROS.                  
           05  WS-BOTH-PREM              PIC 9(9) VALUE ZEROS.                  
           05  WS-F800A-WRITE            PIC 9(9) VALUE ZEROS.                  
           05  WS-F800N-WRITE            PIC 9(9) VALUE ZEROS.                  
           05  WS-NO-SCH-ACC             PIC 9(9) VALUE ZEROS.                  
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-LOGIC.                                                          
      *-------------------------------------------------------------            
            PERFORM 100-OPEN-FILES   THRU 100-EXIT.                             
            PERFORM 200-READ-F800    THRU 200-EXIT.                             
            PERFORM 300-PROCESS-F800 THRU 300-EXIT                              
                    UNTIL WS-F800-EOF = 'Y'.                                    
            PERFORM 900-CLOSE-FILES  THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F800                                                 
                       BM06K110                                                 
                OUTPUT P13F800A.                                                
                                                                                
           IF WS-K110-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K110-STATUS ' WS-K110-STATUS              
              MOVE WS-K110-STATUS       TO RETURN-CODE                          
              PERFORM 900-CLOSE-FILES THRU 900-EXIT.                            
                                                                                
           WRITE P13F800A-REC FROM WS-HEADER.                                   
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-F800.                                                           
      *-------------------------------------------------------------            
           READ BP13F800 AT END                                                 
                MOVE 'Y' TO WS-F800-EOF                                         
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1         TO WS-F800READ.                                        
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS-F800.                                                        
      *-------------------------------------------------------------            
           MOVE SPACES                TO K110-KEY-FLD.                          
           MOVE F800-NUM-SCH-ACC      TO K110-KEY-FLD.                          
           IF  F800-NUM-SCH-ACC = SPACES                                        
              ADD 1 TO WS-NO-SCH-ACC                                            
              PERFORM 200-READ-F800    THRU 200-EXIT                            
              GO TO 300-EXIT.                                                   
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-STATUS                                              
           WHEN 00                                                              
              ADD 1                        TO WS-K110-READ                      
              PERFORM 400-OUTPUT THRU 400-EXIT                                  
           WHEN 23                                                              
              DISPLAY 'REC NOT FOUND IN BM06K110 : ' F800-NUM-SCH-ACC           
                      ', STATUS ' WS-K110-STATUS                                
              ADD 1                        TO WS-K110-NOTFND                    
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BM06K110 : ' F800-NUM-SCH-ACC              
                      ', STATUS ' WS-K110-STATUS                                
              MOVE WS-K110-STATUS TO RETURN-CODE                                
              PERFORM 900-CLOSE-FILES THRU 900-EXIT                             
           END-EVALUATE.                                                        
                                                                                
           PERFORM 200-READ-F800    THRU 200-EXIT.                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       400-OUTPUT.                                                              
            MOVE SPACES TO P13F800A-REC.                                        
            MOVE ';'  TO WS-FILLER01 WS-FILLER02 WS-FILLER03 WS-FILLER04        
                         WS-FILLER05 WS-FILLER06 WS-FILLER07 WS-FILLER08        
                        WS-FILLER09 WS-FILLER10 WS-FILLER11 WS-FILLER12         
                        WS-FILLER13 WS-FILLER14.                                
                                                                                
            IF K110-AMT-SINGLES-PREMIUM NOT NUMERIC                             
               MOVE ZEROES             TO K110-AMT-SINGLES-PREMIUM.             
                                                                                
            IF K110-AMT-ELDERLY-PREMIUM NOT NUMERIC                             
               MOVE ZEROES             TO K110-AMT-ELDERLY-PREMIUM.             
                                                                                
            IF  K110-AMT-SINGLES-PREMIUM > ZEROES                               
            AND K110-AMT-ELDERLY-PREMIUM = ZEROES                               
               ADD 1 TO WS-SING-PREM.                                           
                                                                                
            IF  K110-AMT-SINGLES-PREMIUM = ZEROES                               
            AND K110-AMT-ELDERLY-PREMIUM > ZEROES                               
               ADD 1 TO WS-ELDR-PREM.                                           
                                                                                
            IF  K110-AMT-SINGLES-PREMIUM > ZEROES                               
            AND K110-AMT-ELDERLY-PREMIUM > ZEROES                               
                ADD 1 TO WS-BOTH-PREM.                                          
                                                                                
            IF K110-AMT-SINGLES-PREMIUM > ZEROES                                
            OR K110-AMT-ELDERLY-PREMIUM > ZEROES                                
               ADD 1 TO WS-ANY-PREM                                             
               MOVE K110-SCH-ACC-NO          TO WS-SCH-ACC-NO                   
               MOVE K110-REGN-NO             TO WS-REGN-NO                      
               MOVE K110-DTE-ACCEPTANCE      TO WS-DT-ACPT                      
               MOVE K110-DTE-KEY-ISSUED      TO WS-DT-KEY-ISS                   
               MOVE F800-NUM-ALLO-SCHEME     TO WS-ALLOC-SCH                    
               MOVE F800-NUM-ELIG-SCHEME     TO WS-NUM-ELIG                     
               MOVE K110-AMT-SINGLES-PREMIUM TO WS-AMT-SING-PREM                
               MOVE K110-AMT-ELDERLY-PREMIUM TO WS-AMT-ELDR-PREM                
               MOVE K110-NUM-LEASE-TENURE    TO WS-TENURE                       
               MOVE F800-NUM-ELDERLY         TO WS-ELDER                        
               MOVE F800-NUM-NTIMER          TO WS-NUM-NTIMER                   
               MOVE F800-NUM-PPO             TO WS-NUM-PPO                      
               MOVE F800-AMT-HSE-INCOME      TO WS-NUM-INCOME                   
               WRITE P13F800A-REC                                               
           ELSE                                                                 
               ADD 1 TO WS-NO-PREM                                              
           END-IF.                                                              
                                                                                
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       900-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
           DISPLAY '- BP13CE19 CONTROL TOTAL --------------'.                   
           DISPLAY ' 1.      REC READ BP13F800   = ' WS-F800READ.               
           DISPLAY ' 2.       REC   NOT BOOKED   = ' WS-NO-SCH-ACC.             
           DISPLAY ' 3.     REC READ  BM06K110   = ' WS-K110-READ.              
           DISPLAY ' 4. REC READ  BM06K110 NOTFD = ' WS-K110-NOTFND.            
           DISPLAY ' 5. SINGLE / ELDER PREMIUM   = ' WS-ANY-PREM.               
           DISPLAY ' 6. SINGLE & ELDER PREMIUM   = ' WS-BOTH-PREM.              
           DISPLAY ' 7.             NO PREMIUM   = ' WS-NO-PREM.                
           DISPLAY ' 8.    ONLY SINGLE PREMIUM   = ' WS-SING-PREM.              
           DISPLAY ' 9.     ONLY ELDER PREMIUM   = ' WS-ELDR-PREM.              
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13F800                                                       
                 BM06K110                                                       
                 P13F800A.                                                      
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BM06K110, STATUS ' WS-K110-STATUS          
              MOVE WS-K110-STATUS        TO RETURN-CODE.                        
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
