       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C382.                                                 
      *AUTHOR.        SAUL SIO.                                                 
      *DATE-WRITTEN.  10/07/2002.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE    :  PRINT CHECKLIST AND CONTROL LIST FOR       *            
      *                 REQUEST ON CHG OF MAILING ADDRESS RECEIVED *            
      *                 VIA INTERNET APPLICATION (BP13ADDR)        *            
      *                                                            *            
      * INPUT FILES  :  1.  BP13F833                               *            
      *                 2.  BP13K820                               *            
      *                 2.  BP13K830                               *            
      *                                                            *            
      * OUTPUT LISTS :  1.  P13L382A - CHECKLIST                   *            
      *                 2.  P13L382B - CONTROL LIST                *            
      *                                                            *            
      * ========================================================== *            
      * BP132161 20021007 SSS2  NEW PROGRAM                        *            
      * BP135700 20150429 ESA1  ADD BP13F839 FOR ADDRESS UPD IN HLA*            
      *                         ADD UPDATING OF BP13K830           *            
      * BP135826 20150603 ESA1  TO UPDATE K830-DTE-UPDATE-MAIL AND *            
      *                                   K830-NUM-USERID-MAIL     *            
      * BP135767 20151111 ZDD1  TO UPDATE BP13K820 AND BP13K857    *            
      * BP135767 20151214 ZDD1  TO CATER FOR PREV DATE             *            
      * BP136109 20150219 ZDD1  TO ADD ADDITIONAL FOOTNOTE         *            
      * ===========================================================*            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F833  ASSIGN       TO BP13F833.                           
                                                                                
           SELECT BP13K820  ASSIGN       TO BP13K820                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K820-KEY-FLD                        
                            FILE STATUS  IS BP13K820-STATUS.                    
                                                                                
           SELECT BP13K857  ASSIGN       TO BP13K857                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K857-KEY-FLD                        
                            FILE STATUS  IS BP13K857-STATUS.                    
                                                                                
           SELECT BP13K830  ASSIGN       TO BP13K830                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K830-KEY-FLD                        
                            ALTERNATE RECORD KEY IS  K830-NUM-REGN              
                            FILE STATUS  IS BP13K830-STATUS.                    
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS BP13K800-STATUS.                    
                                                                                
           SELECT SY02F001  ASSIGN       TO SY02F001.                           
           SELECT P13L382A  ASSIGN       TO P13L382A.                           
           SELECT P13L382B  ASSIGN       TO P13L382B.                           
           SELECT P13L382C  ASSIGN       TO P13L382C.                           
           SELECT BP13F839  ASSIGN       TO BP13F839.                           
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F833                                                            
            RECORD CONTAINS 400 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F833.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K857                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K857.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K830.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   P13L382A                                                            
            RECORD CONTAINS 90 CHARACTERS                                       
            RECORDING MODE  IS  F.                                              
       01   L382A-PRTREC                PIC X(90).                              
                                                                                
       FD   P13L382B                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       01   L382B-PRTREC                PIC X(132).                             
                                                                                
       FD   P13L382C                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       01   L382C-PRTREC                PIC X(132).                             
                                                                                
       FD   BP13F839                                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F839.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  BP13K857-STATUS              PIC 99     VALUE 00.                    
       01  BP13K820-STATUS              PIC 99     VALUE 00.                    
       01  BP13K830-STATUS              PIC 99     VALUE 00.                    
       01  BP13K800-STATUS              PIC 99     VALUE 00.                    
       01  WS-EOF-BP13F833              PIC X      VALUE 'N'.                   
       01  WS-EOF-F001                  PIC X      VALUE 'N'.                   
       01  WS-FND-K820                  PIC X      VALUE 'N'.                   
       01  WS-DATE                      PIC X(8)   VALUE SPACES.                
                                                                                
       01  WS-L381B-LINECNT             PIC 9(2)   VALUE 77.                    
       01  WS-L381B-PAGECNT             PIC 9(4)   VALUE ZEROS.                 
                                                                                
       01  WS-L381C-LINECNT             PIC 9(2)   VALUE 77.                    
       01  WS-L381C-PAGECNT             PIC 9(4)   VALUE ZEROS.                 
       01  WS-L381C-SNO                 PIC 9(4)   VALUE ZEROS.                 
                                                                                
                                                                                
       01  WS-SNO                       PIC 9(4)   VALUE ZEROS.                 
       01  WS-CHECKLIST-PRINTED         PIC 9(5)   VALUE ZEROS.                 
       01  WS-K830-REWRITE              PIC 9(5)   VALUE ZEROS.                 
       01  WS-K820-REWRITE              PIC 9(5)   VALUE ZEROS.                 
       01  WS-K857-REWRITE              PIC 9(5)   VALUE ZEROS.                 
       01  WS-F839-WRITE                PIC 9(5)   VALUE ZEROS.                 
                                                                                
       01  WS-PARSING.                                                          
           05 WS-STRING-TO-PARSE            PIC X(65).                          
           05 WS-PARSED-STRING              PIC X(65).                          
           05 WS-STRING-LENGTH              PIC 9(02).                          
           05 WS-CUR-POS-SP                 PIC 9(02).                          
           05 WS-CUR-POS-PS                 PIC 9(02).                          
           05 WS-SPACE-FLAG                 PIC X    VALUE 'N'.                 
           05 WS-INSERT-SPACE-FLAG          PIC X    VALUE 'N'.                 
                                                                                
      *-------------------------------------------------------------            
      * CHECKLIST LAYOUT                                                        
      *-------------------------------------------------------------            
       01  L382A-LINE01.                                                        
           05 FILLER                PIC X(27)  VALUE SPACES.                    
           05 L382A-DTE-REQUEST     PIC X(10)  VALUE SPACES.                    
           05 FILLER                PIC X      VALUE SPACES.                    
           05 L382A-TME-REQUEST     PIC X(8)   VALUE SPACES.                    
           05 FILLER                PIC X(4)   VALUE ' HRS'.                    
                                                                                
       01  L382A-LINE02.                                                        
           05 FILLER                PIC X(22)  VALUE SPACES.                    
           05 L382A-NUM-REGN        PIC X(8)   VALUE SPACES.                    
                                                                                
       01  L382A-LINE03.                                                        
           05 FILLER                PIC X(17)  VALUE SPACES.                    
           05 L382A-NME-REQUESTOR   PIC X(50)  VALUE SPACES.                    
           05 FILLER                PIC X(11)  VALUE SPACES.                    
           05 L382A-NUM-NRIC        PIC X(9)   VALUE SPACES.                    
                                                                                
       01  L382A-LINE04.                                                        
           05 FILLER                PIC X(17)  VALUE SPACES.                    
           05 L382A-NUM-BLOCK       PIC X(16)  VALUE SPACES.                    
           05 L382A-HOUSE-LEVEL     PIC X(11)  VALUE SPACES.                    
                                                                                
       01  L382A-LINE05.                                                        
           05 FILLER                PIC X(17)  VALUE SPACES.                    
           05 L382A-STREET-NAME     PIC X(65)  VALUE SPACES.                    
                                                                                
       01  L382A-LINE06.                                                        
           05 FILLER                PIC X(17)  VALUE SPACES.                    
           05 L382A-NUM-POSTAL      PIC X(20)  VALUE SPACES.                    
                                                                                
       01  L382A-LINE07.                                                        
           05 FILLER                PIC X(22)  VALUE SPACES.                    
           05 L382A-TYPE-ADDR       PIC X(7)   VALUE SPACES.                    
                                                                                
       01  L382A-LINE08.                                                        
           05 FILLER                PIC X(28)  VALUE SPACES.                    
           05 L382A-NUM-HSE-TEL     PIC X(8)   VALUE SPACES.                    
                                                                                
       01  L382A-LINE09.                                                        
           05 FILLER                PIC X(28)  VALUE SPACES.                    
           05 L382A-NUM-OFF-TEL     PIC X(8)   VALUE SPACES.                    
                                                                                
       01  L382A-LINE10.                                                        
           05 FILLER                PIC X(28)  VALUE SPACES.                    
           05 L382A-NUM-MOBILE      PIC X(8)   VALUE SPACES.                    
                                                                                
       01  L382A-LINE11.                                                        
           05 FILLER                PIC X(28)  VALUE SPACES.                    
           05 L382A-NUM-PAGER       PIC X(8)   VALUE SPACES.                    
                                                                                
       01  L382A-LINE12.                                                        
           05 FILLER                PIC X(28)  VALUE SPACES.                    
           05 L382A-EMAIL           PIC X(40)  VALUE SPACES.                    
                                                                                
      *-------------------------------------------------------------            
      * CONTROL LIST LAYOUT                                                     
      *-------------------------------------------------------------            
       01  L382B-HEADER1.                                                       
           05 FILLER                    PIC X(55)  VALUE 'P13L382B'.            
           05 FILLER                    PIC X(20)  VALUE                        
              'SYSTEM OF COMMITMENT'.                                           
           05 FILLER                    PIC X(38)  VALUE SPACES.                
           05 FILLER                    PIC X(7)   VALUE 'DATE : '.             
           05 L382B-DATE                PIC X(10)  VALUE SPACES.                
                                                                                
       01  L382B-HEADER2.                                                       
           05 FILLER                    PIC X(40)  VALUE SPACES.                
           05 FILLER                    PIC X(50)  VALUE                        
              'REPORT ON NOTIFICATION OF CHANGE OF ADDRESS'.                    
           05 FILLER                    PIC X(23)  VALUE SPACES.                
           05 FILLER                    PIC X(7)   VALUE 'PAGE : '.             
           05 L382B-PAGE                PIC ZZZ9   VALUE ZEROS.                 
                                                                                
       01  L382B-HEADER3.                                                       
           05 FILLER                     PIC X(132) VALUE                       
              'SNO   REGN       NOTIFIED BY         DATE        TIME'.          
                                                                                
       01  L382B-HEADER4.                                                       
           05 FILLER                     PIC X(17)  VALUE SPACES.               
           05 FILLER                     PIC X(98)  VALUE                       
              'TY CURRENT/NEW BLK  FLAT       STREET NAME'.                     
           05 FILLER                     PIC X(17)  VALUE                       
              'POSTAL (RES/MAIL)'.                                              
                                                                                
       01  L382B-HEADER5.                                                       
           05 FILLER                     PIC X(132) VALUE ALL '-'.              
                                                                                
       01  L382B-DETAIL.                                                        
           05 L382B-SNO                  PIC Z(3)9.                             
           05 FILLER                     PIC X(2)  VALUE SPACES.                
           05 L382B-NUM-REGN             PIC X(8)  VALUE SPACES.                
           05 FILLER                     PIC X(3)  VALUE SPACES.                
           05 L382B-NUM-NOTIFIED         PIC X(9)  VALUE SPACES.                
           05 FILLER                     PIC X(11) VALUE SPACES.                
           05 L382B-DTE-NOTIFIED         PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X(2)  VALUE SPACES.                
           05 L382B-TME-NOTIFIED         PIC X(10) VALUE SPACES.                
                                                                                
       01  L382B-DETAIL2.                                                       
           05 FILLER                     PIC X(17) VALUE SPACES.                
           05 L382B-TYPE-ADDR-CUR        PIC X     VALUE SPACES.                
           05 FILLER                     PIC X(2)  VALUE SPACES.                
           05 L382B-ADDR-CUR.                                                   
              10 L382B-ADDR-CUR-BLK      PIC X(16) VALUE SPACES.                
              10 FILLER                  PIC X     VALUE SPACE.                 
              10 L382B-ADDR-CUR-FLAT     PIC X(11) VALUE SPACES.                
              10 FILLER                  PIC X     VALUE SPACE.                 
              10 L382B-ADDR-CUR-STREET   PIC X(65) VALUE SPACES.                
              10 FILLER                  PIC X     VALUE SPACE.                 
              10 L382B-ADDR-CUR-POSTAL   PIC X(6)  VALUE SPACES.                
           05 FILLER                     PIC X(2)  VALUE SPACES.                
           05 L382B-ADDR-CUR-RESMAIL     PIC X(4)  VALUE SPACES.                
                                                                                
       01  L382B-DETAIL3.                                                       
           05 FILLER                     PIC X(17) VALUE SPACES.                
           05 L382B-TYPE-ADDR-NEW        PIC X     VALUE SPACES.                
           05 FILLER                     PIC X(2)  VALUE SPACES.                
           05 L382B-ADDR-NEW.                                                   
              10 L382B-ADDR-NEW-BLK      PIC X(16) VALUE SPACES.                
              10 FILLER                  PIC X     VALUE SPACE.                 
              10 L382B-ADDR-NEW-FLAT     PIC X(11) VALUE SPACES.                
              10 FILLER                  PIC X     VALUE SPACE.                 
              10 L382B-ADDR-NEW-STREET   PIC X(65) VALUE SPACES.                
              10 FILLER                  PIC X     VALUE SPACE.                 
              10 L382B-ADDR-NEW-POSTAL   PIC X(6)  VALUE SPACES.                
                                                                                
       01  L382B-FOOTER.                                                        
           05 FILLER                    PIC X(25)  VALUE SPACES.                
           05 FILLER                    PIC X(40)  VALUE                        
              'TOTAL NUMBER OF CHECKLIST PRINTED : '.                           
           05 L382B-TOTAL-PRINTED       PIC Z(4)9  VALUE ZEROS.                 
                                                                                
       01  L382B-FOOTER1.                                                       
           05 FILLER                    PIC X(10)  VALUE SPACES.                
           05 FILLER                    PIC X(40)  VALUE                        
              'CHECKING OFFICER : ____________________'.                        
           05 FILLER                    PIC X(10)  VALUE SPACES.                
           05 FILLER                    PIC X(40)  VALUE                        
              'DATE OF CHECKING : ____________________'.                        
                                                                                
       01  L382B-FOOTER2.                                                       
           05 FILLER                    PIC X(10)  VALUE SPACES.                
           05 FILLER                    PIC X(40)  VALUE                        
              'DESIGNATION      : ____________________'.                        
      *-------------------------------------------------------------            
      * CONTROL LIST LAYOUT L382C - REPORT                                      
      *-------------------------------------------------------------            
       01  L382C-HEADER1.                                                       
           05 FILLER                    PIC X(55)  VALUE 'P13L382C'.            
           05 FILLER                    PIC X(20)  VALUE                        
              'SYSTEM OF COMMITMENT'.                                           
           05 FILLER                    PIC X(38)  VALUE SPACES.                
           05 FILLER                    PIC X(7)   VALUE 'DATE : '.             
           05 L382C-DATE                PIC X(10)  VALUE SPACES.                
                                                                                
       01  L382C-HEADER2.                                                       
           05 FILLER                    PIC X(40)  VALUE SPACES.                
           05 FILLER                    PIC X(50)  VALUE                        
              'REPORT ON NOTIFICATION OF CHANGE OF CONTACT'.                    
           05 FILLER                    PIC X(23)  VALUE SPACES.                
           05 FILLER                    PIC X(7)   VALUE 'PAGE : '.             
           05 L382C-PAGE                PIC ZZZ9   VALUE ZEROS.                 
                                                                                
       01  L382C-HEADER3.                                                       
           05 FILLER                  PIC X(02)  VALUE SPACES.                  
           05 FILLER                  PIC X(03)  VALUE 'SNO'.                   
           05 FILLER                  PIC X(02)  VALUE SPACES.                  
           05 FILLER                  PIC X(08)  VALUE 'REGN NUM'.              
           05 FILLER                  PIC X(02)  VALUE SPACES.                  
           05 FILLER                  PIC X(09)  VALUE 'NRIC     '.             
           05 FILLER                  PIC X(02)  VALUE SPACES.                  
           05 FILLER                  PIC X(25)  VALUE 'PHONE (RES)'.           
           05 FILLER                  PIC X(02)  VALUE SPACES.                  
           05 FILLER                  PIC X(25)  VALUE 'PHONE (OFF)'.           
           05 FILLER                  PIC X(02)  VALUE SPACES.                  
           05 FILLER                  PIC X(25)  VALUE 'MOBILE     '.           
                                                                                
       01  L382C-HEADER4.                                                       
           05 FILLER                  PIC X(02)  VALUE SPACES.                  
           05 FILLER                  PIC X(03)  VALUE SPACES.                  
           05 FILLER                  PIC X(02)  VALUE SPACES.                  
           05 FILLER                  PIC X(08)  VALUE SPACES.                  
           05 FILLER                  PIC X(02)  VALUE SPACES.                  
           05 FILLER                  PIC X(09)  VALUE SPACES.                  
           05 FILLER                  PIC X(02)  VALUE SPACES.                  
           05 FILLER                  PIC X(25)  VALUE                          
                                 'CURRENT / NEW            '.                   
           05 FILLER                  PIC X(02)  VALUE SPACES.                  
           05 FILLER                  PIC X(25)  VALUE                          
                                 'CURRENT / NEW            '.                   
           05 FILLER                  PIC X(02)  VALUE SPACES.                  
           05 FILLER                  PIC X(25)  VALUE                          
                                 'CURRENT / NEW            '.                   
                                                                                
       01  L382C-HEADER5.                                                       
           05 FILLER                     PIC X(132) VALUE ALL '-'.              
                                                                                
       01  L382C-DETAIL.                                                        
           05 L382C-SNO                  PIC Z(3)9.                             
           05 FILLER                     PIC X(2)  VALUE SPACES.                
           05 L382C-NUM-REGN             PIC X(8)  VALUE SPACES.                
           05 FILLER                     PIC X(2)  VALUE SPACES.                
           05 L382C-NUM-NRIC             PIC X(9)  VALUE SPACES.                
           05 FILLER                     PIC X(2)  VALUE SPACES.                
           05 L382C-PHONE-RES            PIC X(25) VALUE SPACES.                
           05 FILLER                     PIC X(2)  VALUE SPACES.                
           05 L382C-PHONE-OFF            PIC X(25) VALUE SPACES.                
           05 FILLER                     PIC X(2)  VALUE SPACES.                
           05 L382C-MOBILE               PIC X(25) VALUE SPACES.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            PERFORM 1000-OPEN-ROUTINE         THRU  1000-EXIT.                  
            PERFORM 2000-READ-BP13F833        THRU  2000-EXIT.                  
            PERFORM 2100-READ-SY02F001        THRU  2100-EXIT.                  
            PERFORM 3000-PROCESS-PARA         THRU  3000-EXIT                   
                         UNTIL WS-EOF-BP13F833 = 'Y'.                           
            PERFORM 8800-PRINT-CONTROL        THRU  8800-EXIT.                  
            PERFORM 9000-CLOSE-ROUTINE        THRU  9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            OPEN INPUT  BP13F833                                                
                        BP13K800                                                
                 I-O    BP13K820                                                
                        BP13K830                                                
                        BP13K857                                                
                        SY02F001                                                
                 OUTPUT P13L382A                                                
                        P13L382B                                                
                        P13L382C                                                
                        BP13F839.                                               
                                                                                
           IF BP13K820-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP13K820-STATUS ' BP13K820-STATUS         
              MOVE     BP13K820-STATUS TO RETURN-CODE                           
              PERFORM  9000-CLOSE-ROUTINE.                                      
                                                                                
           IF BP13K830-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP13K830-STATUS ' BP13K830-STATUS         
              MOVE     BP13K830-STATUS  TO RETURN-CODE                          
              PERFORM  9000-CLOSE-ROUTINE.                                      
                                                                                
           IF BP13K800-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP13K800-STATUS ' BP13K800-STATUS         
              MOVE     BP13K800-STATUS TO RETURN-CODE                           
              PERFORM  9000-CLOSE-ROUTINE.                                      
                                                                                
           IF BP13K857-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP13K857-STATUS ' BP13K857-STATUS         
              MOVE     BP13K857-STATUS TO RETURN-CODE                           
              PERFORM  9000-CLOSE-ROUTINE.                                      
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
              DELIMITED BY SIZE INTO L382B-DATE.                                
                                                                                
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
              DELIMITED BY SIZE INTO L382C-DATE.                                
                                                                                
           MOVE  SPACES             TO L382A-PRTREC.                            
           WRITE L382A-PRTREC       AFTER PAGE.                                 
           WRITE L382A-PRTREC.                                                  
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       2000-READ-BP13F833.                                                      
      *-------------------------------------------------------------            
           READ BP13F833                                                        
                AT END MOVE 'Y' TO WS-EOF-BP13F833                              
                GO TO  2000-EXIT.                                               
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       2100-READ-SY02F001.                                                      
      *-------------------------------------------------------------            
             READ SY02F001 AT END MOVE 'Y' TO WS-EOF-F001                       
                  GO TO 2100-EXIT.                                              
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       3000-PROCESS-PARA.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES                   TO L382A-NUM-REGN                      
                                            L382A-DTE-REQUEST                   
                                            L382A-TME-REQUEST                   
                                            L382A-NME-REQUESTOR                 
                                            L382A-NUM-NRIC                      
                                            L382A-NUM-BLOCK                     
                                            L382A-HOUSE-LEVEL                   
                                            L382A-STREET-NAME                   
                                            L382A-NUM-POSTAL                    
                                            L382A-TYPE-ADDR                     
                                            L382A-NUM-HSE-TEL                   
                                            L382A-NUM-OFF-TEL                   
                                            L382A-NUM-MOBILE                    
                                            L382A-NUM-PAGER                     
                                            L382A-EMAIL                         
                                            L382B-NUM-REGN                      
                                            L382B-NUM-NOTIFIED                  
                                            L382B-DTE-NOTIFIED                  
                                            L382B-TME-NOTIFIED                  
                                            L382B-TYPE-ADDR-CUR                 
                                            L382B-ADDR-CUR                      
                                            L382B-ADDR-CUR-RESMAIL              
                                            L382B-TYPE-ADDR-NEW                 
                                            L382B-ADDR-NEW.                     
                                                                                
           IF (F001-DTE-CURRENT = F833-DTE-RECEIVED) OR                         
              (F001-DTE-PREV    = F833-DTE-RECEIVED)                            
                                                                                
              PERFORM 3100-READ-BP13K800 THRU 3100-EXIT                         
                                                                                
              PERFORM 4000-GET-OCCP-NAME THRU 4000-EXIT                         
                                                                                
              PERFORM 5000-READ-BP13K830 THRU 5000-EXIT                         
              PERFORM 5100-READ-BP13K857 THRU 5100-EXIT                         
                                                                                
              PERFORM 6000-PRINT-LETTER  THRU 6000-EXIT                         
              PERFORM 7000-PRINT-LISTING THRU 7000-EXIT                         
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F833 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       3100-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
                                                                                
            MOVE F833-NUM-REGN           TO K800-NUM-REGN.                      
                                                                                
            READ BP13K800.                                                      
                                                                                
            IF BP13K800-STATUS = 00                                             
               CONTINUE                                                         
            ELSE                                                                
            IF BP13K800-STATUS = 23                                             
               MOVE SPACES               TO BP13K800-MASTER                     
               DISPLAY 'REC NOTFND IN BP13K800 : ' BP13K800-STATUS              
                       ',  K800-NUM-REGN : ' K800-NUM-REGN                      
            ELSE                                                                
               DISPLAY 'ERR READING BP13K800 : ' BP13K800-STATUS                
                       ',  K800-NUM-REGN : ' K800-NUM-REGN.                     
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       4000-GET-OCCP-NAME.                                                      
      *-------------------------------------------------------------            
            MOVE SPACES                  TO L382A-NME-REQUESTOR                 
                                            L382A-NUM-NRIC                      
                                            L382B-NUM-NOTIFIED.                 
                                                                                
            MOVE F833-NUM-REGN           TO K820-NUM-REGN                       
                                            L382A-NUM-REGN                      
                                            L382B-NUM-REGN.                     
            MOVE F833-NUM-NRIC1          TO K820-NUM-NRIC                       
                                            L382A-NUM-NRIC                      
                                            L382B-NUM-NOTIFIED.                 
                                                                                
            PERFORM 4100-READ-K820    THRU 4100-EXIT.                           
            MOVE K820-NME-OCCP(1:50)  TO L382A-NME-REQUESTOR                    
            IF WS-FND-K820 = 'Y'                                                
              ADD   1      TO WS-L381C-SNO                                      
              MOVE  WS-L381C-SNO       TO L382C-SNO                             
              MOVE F833-NUM-REGN       TO L382C-NUM-REGN                        
              MOVE F833-NUM-NRIC1      TO L382C-NUM-NRIC                        
                                                                                
              STRING K820-NUM-PHONE ' / ' F833-NUM-PHONE-RES1                   
                DELIMITED BY SIZE INTO L382C-PHONE-RES                          
                                                                                
              STRING K820-NUM-PHONE-OFF ' / ' F833-NUM-PHONE-OFF1               
                DELIMITED BY SIZE INTO L382C-PHONE-OFF                          
                                                                                
              STRING K820-NUM-HP-PGR    ' / ' F833-NUM-MOBILE1                  
                DELIMITED BY SIZE INTO L382C-MOBILE                             
                                                                                
              PERFORM 5700-WRITE-P13L382C  THRU 5700-EXIT                       
              IF F833-NUM-PHONE-RES1 NOT = SPACES AND LOW-VALUES                
                 MOVE F833-NUM-PHONE-RES1 TO K820-NUM-PHONE                     
              END-IF                                                            
              IF F833-NUM-PHONE-OFF1 NOT = SPACES AND LOW-VALUES                
                 MOVE F833-NUM-PHONE-OFF1 TO K820-NUM-PHONE-OFF                 
              END-IF                                                            
              IF F833-NUM-MOBILE1    NOT = SPACES AND LOW-VALUES                
                 MOVE F833-NUM-MOBILE1    TO K820-NUM-HP-PGR                    
              END-IF                                                            
              PERFORM 5600-REWRITE-BP13K820 THRU 5600-EXIT                      
            END-IF.                                                             
                                                                                
            IF F833-NUM-NRIC2 NOT =  SPACES AND LOW-VALUES                      
              MOVE SPACES                  TO BP13K820-REC                      
              MOVE F833-NUM-REGN           TO K820-NUM-REGN                     
              MOVE F833-NUM-NRIC2          TO K820-NUM-NRIC                     
              PERFORM 4100-READ-K820    THRU 4100-EXIT                          
              IF WS-FND-K820 = 'Y'                                              
                MOVE SPACES              TO L382C-DETAIL                        
      *         MOVE SPACES              TO L382C-SNO                           
      *         MOVE SPACES              TO L382C-NUM-REGN                      
                MOVE F833-NUM-NRIC2      TO L382C-NUM-NRIC                      
                                                                                
                STRING K820-NUM-PHONE ' / ' F833-NUM-PHONE-RES2                 
                   DELIMITED BY SIZE INTO L382C-PHONE-RES                       
                                                                                
                STRING K820-NUM-PHONE-OFF ' / ' F833-NUM-PHONE-OFF2             
                   DELIMITED BY SIZE INTO L382C-PHONE-OFF                       
                                                                                
                STRING K820-NUM-HP-PGR    ' / ' F833-NUM-MOBILE2                
                   DELIMITED BY SIZE INTO L382C-MOBILE                          
                                                                                
                PERFORM 5700-WRITE-P13L382C  THRU 5700-EXIT                     
                IF  F833-NUM-PHONE-RES2   NOT = SPACES AND LOW-VALUES           
                   MOVE F833-NUM-PHONE-RES2 TO K820-NUM-PHONE                   
                END-IF                                                          
                IF  F833-NUM-PHONE-OFF2   NOT = SPACES AND LOW-VALUES           
                   MOVE F833-NUM-PHONE-OFF2 TO K820-NUM-PHONE-OFF               
                END-IF                                                          
                IF F833-NUM-MOBILE2       NOT = SPACES AND LOW-VALUES           
                   MOVE F833-NUM-MOBILE2    TO K820-NUM-HP-PGR                  
                END-IF                                                          
                PERFORM 5600-REWRITE-BP13K820 THRU 5600-EXIT                    
              END-IF                                                            
            END-IF.                                                             
                                                                                
            IF F833-NUM-NRIC3 NOT =  SPACES AND LOW-VALUES                      
              MOVE SPACES                  TO BP13K820-REC                      
              MOVE F833-NUM-REGN           TO K820-NUM-REGN                     
              MOVE F833-NUM-NRIC3          TO K820-NUM-NRIC                     
              PERFORM 4100-READ-K820    THRU 4100-EXIT                          
              IF WS-FND-K820 = 'Y'                                              
                MOVE SPACES              TO L382C-DETAIL                        
      *         MOVE SPACES              TO L382C-SNO                           
      *         MOVE SPACES              TO L382C-NUM-REGN                      
                MOVE F833-NUM-NRIC3      TO L382C-NUM-NRIC                      
                STRING K820-NUM-PHONE ' / ' F833-NUM-PHONE-RES3                 
                   DELIMITED BY SIZE INTO L382C-PHONE-RES                       
                                                                                
                STRING K820-NUM-PHONE-OFF ' / ' F833-NUM-PHONE-OFF3             
                   DELIMITED BY SIZE INTO L382C-PHONE-OFF                       
                                                                                
                STRING K820-NUM-HP-PGR    ' / ' F833-NUM-MOBILE3                
                   DELIMITED BY SIZE INTO L382C-MOBILE                          
                PERFORM 5700-WRITE-P13L382C  THRU 5700-EXIT                     
                IF F833-NUM-PHONE-RES3 NOT = SPACES AND LOW-VALUES              
                   MOVE F833-NUM-PHONE-RES3 TO K820-NUM-PHONE                   
                END-IF                                                          
                IF F833-NUM-PHONE-OFF3 NOT = SPACES AND LOW-VALUES              
                   MOVE F833-NUM-PHONE-OFF3 TO K820-NUM-PHONE-OFF               
                END-IF                                                          
                IF  F833-NUM-MOBILE3  NOT = SPACES AND LOW-VALUES               
                    MOVE F833-NUM-MOBILE3    TO K820-NUM-HP-PGR                 
                END-IF                                                          
                PERFORM 5600-REWRITE-BP13K820 THRU 5600-EXIT                    
              END-IF                                                            
            END-IF.                                                             
                                                                                
            IF F833-NUM-NRIC4 NOT =  SPACES AND LOW-VALUES                      
              MOVE SPACES                  TO BP13K820-REC                      
              MOVE F833-NUM-REGN           TO K820-NUM-REGN                     
              MOVE F833-NUM-NRIC4          TO K820-NUM-NRIC                     
              PERFORM 4100-READ-K820    THRU 4100-EXIT                          
              IF WS-FND-K820 = 'Y'                                              
                MOVE SPACES              TO L382C-DETAIL                        
      *         MOVE SPACES              TO L382C-SNO                           
      *         MOVE SPACES              TO L382C-NUM-REGN                      
                MOVE F833-NUM-NRIC4      TO L382C-NUM-NRIC                      
                STRING K820-NUM-PHONE ' / ' F833-NUM-PHONE-RES4                 
                   DELIMITED BY SIZE INTO L382C-PHONE-RES                       
                                                                                
                STRING K820-NUM-PHONE-OFF ' / ' F833-NUM-PHONE-OFF4             
                   DELIMITED BY SIZE INTO L382C-PHONE-OFF                       
                                                                                
                STRING K820-NUM-HP-PGR    ' / ' F833-NUM-MOBILE4                
                   DELIMITED BY SIZE INTO L382C-MOBILE                          
                PERFORM 5700-WRITE-P13L382C  THRU 5700-EXIT                     
                IF F833-NUM-PHONE-RES4 NOT = SPACES AND LOW-VALUES              
                  MOVE F833-NUM-PHONE-RES4 TO K820-NUM-PHONE                    
                END-IF                                                          
                IF F833-NUM-PHONE-OFF4 NOT = SPACES AND LOW-VALUES              
                   MOVE F833-NUM-PHONE-OFF4 TO K820-NUM-PHONE-OFF               
                END-IF                                                          
                IF F833-NUM-MOBILE4 NOT = SPACES AND LOW-VALUES                 
                   MOVE F833-NUM-MOBILE4    TO K820-NUM-HP-PGR                  
                END-IF                                                          
                PERFORM 5600-REWRITE-BP13K820 THRU 5600-EXIT                    
              END-IF                                                            
            END-IF.                                                             
                                                                                
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       4100-READ-K820.                                                          
      *-------------------------------------------------------------            
            READ BP13K820.                                                      
                                                                                
            IF BP13K820-STATUS = 00                                             
               MOVE 'Y'   TO WS-FND-K820                                        
            ELSE                                                                
            IF BP13K820-STATUS = 23                                             
               MOVE 'N'   TO WS-FND-K820                                        
               DISPLAY 'REC NOTFND IN BP13K820 : ' BP13K820-STATUS              
                       ',  K820-KEY-FLD : ' K820-KEY-FLD                        
            ELSE                                                                
               DISPLAY 'ERR READING BP13K820 : ' BP13K820-STATUS                
                       ',  K820-KEY-FLD : ' K820-KEY-FLD.                       
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       5000-READ-BP13K830.                                                      
      *-------------------------------------------------------------            
           MOVE F833-NUM-REGN            TO K830-NUM-REGN.                      
           READ BP13K830 KEY IS K830-NUM-REGN.                                  
                                                                                
           IF BP13K830-STATUS = 00                                              
              IF K830-NUM-MAIL-ADDR-TYPE NOT = SPACES                           
                 MOVE K830-NUM-MAIL-ADDR-TYPE TO L382B-TYPE-ADDR-CUR            
                 MOVE K830-NUM-MAIL-BLK       TO L382B-ADDR-CUR-BLK             
                 MOVE K830-NUM-MAIL-FLAT      TO L382B-ADDR-CUR-FLAT            
                 MOVE K830-NME-MAIL-STR-BLDG  TO L382B-ADDR-CUR-STREET          
                 MOVE K830-NUM-MAIL-POSTAL    TO L382B-ADDR-CUR-POSTAL          
                 MOVE 'MAIL'                  TO L382B-ADDR-CUR-RESMAIL         
              ELSE                                                              
                 MOVE K830-NUM-ADDR-TYPE     TO L382B-TYPE-ADDR-CUR             
                 MOVE K830-NUM-BLK           TO L382B-ADDR-CUR-BLK              
                 MOVE K830-NUM-FLAT          TO L382B-ADDR-CUR-FLAT             
                 MOVE K830-NME-STREET-BLDG   TO L382B-ADDR-CUR-STREET           
                 MOVE K830-NUM-POSTAL        TO L382B-ADDR-CUR-POSTAL           
                 MOVE 'RES'                  TO L382B-ADDR-CUR-RESMAIL          
              END-IF                                                            
              PERFORM 5500-REWRITE-BP13K830  THRU 5500-EXIT                     
           ELSE                                                                 
           IF BP13K830-STATUS = 23                                              
              DISPLAY 'REC NOTFND IN BP13K830 : ' BP13K830-STATUS               
                      ',  K830-NUM-REGN  : ' K830-NUM-REGN                      
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K830 : ' BP13K830-STATUS               
                      ',  K830-NUM-REGN : ' K830-NUM-REGN.                      
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       5100-READ-BP13K857.                                                      
      *-------------------------------------------------------------            
           MOVE F833-NUM-REGN            TO K857-NUM-REGN.                      
           READ BP13K857.                                                       
                                                                                
           IF BP13K857-STATUS = 00                                              
              IF F833-EMAIL NOT = SPACES AND LOW-VALUES                         
                MOVE F833-EMAIL   TO K857-NUM-EMAIL                             
                PERFORM 5200-REWRITE-BP13K857  THRU 5200-EXIT                   
              END-IF                                                            
           ELSE                                                                 
           IF BP13K830-STATUS = 23                                              
              DISPLAY 'REC NOTFND IN BP13K830 : ' BP13K830-STATUS               
                      ',  K830-NUM-REGN  : ' K830-NUM-REGN                      
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K830 : ' BP13K830-STATUS               
                      ',  K830-NUM-REGN : ' K830-NUM-REGN.                      
                                                                                
       5100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       5200-REWRITE-BP13K857.                                                   
      *-----------------------------------------------------------              
                                                                                
                                                                                
           REWRITE  BP13K857-REC.                                               
           EVALUATE BP13K857-STATUS                                             
           WHEN  00                                                             
                 ADD   1               TO WS-K857-REWRITE                       
           WHEN  OTHER                                                          
              DISPLAY 'ERROR UPDATING BP13K857 : ' BP13K857-STATUS              
                      ',  K857-NUM-REGN : ' K857-NUM-REGN                       
           END-EVALUATE.                                                        
                                                                                
       5200-EXIT.                                                               
            EXIT.                                                               
                                                                                
       5500-REWRITE-BP13K830.                                                   
      *-----------------------------------------------------------              
                                                                                
           MOVE F833-NUM-ADDR-TYPE  TO K830-NUM-MAIL-ADDR-TYPE.                 
           MOVE F833-NUM-BLK-HDR    TO K830-NUM-MAIL-BLK-HDR.                   
           MOVE F833-NUM-MAIN-BLK   TO K830-NUM-MAIL-MAIN-BLK.                  
           MOVE F833-NUM-BLK-DASH   TO K830-NUM-MAIL-BLK-DASH.                  
           MOVE F833-NUM-SUB-BLK    TO K830-NUM-MAIL-SUB-BLK.                   
           MOVE F833-NUM-LVL-CHAR   TO K830-NUM-MAIL-LVL-CHAR.                  
           MOVE F833-NUM-LVL-NUM    TO K830-NUM-MAIL-LVL-NUM.                   
           MOVE F833-NUM-LVL-DASH   TO K830-NUM-MAIL-LVL-DASH.                  
           MOVE F833-NUM-HSEMAIN    TO K830-NUM-MAIL-HSEMAIN.                   
           MOVE F833-NUM-HSESUB     TO K830-NUM-MAIL-HSESUB.                    
           INSPECT F833-NME-STREET-BLDG REPLACING ALL                           
                   'STREET' BY 'ST    '.                                        
           INSPECT F833-NME-STREET-BLDG REPLACING ALL                           
                   'DRIVE' BY 'DR   '.                                          
           INSPECT F833-NME-STREET-BLDG REPLACING ALL                           
                   'AVENUE' BY 'AVE   '.                                        
           MOVE SPACES       TO WS-STRING-TO-PARSE.                             
                                                                                
                                                                                
           MOVE F833-NME-STREET-BLDG TO WS-STRING-TO-PARSE.                     
           MOVE LENGTH OF WS-STRING-TO-PARSE                                    
                             TO WS-STRING-LENGTH.                               
           MOVE 'Y'          TO WS-INSERT-SPACE-FLAG.                           
           PERFORM 8910-PARSE-STRING    THRU 8910-EXIT.                         
           MOVE WS-PARSED-STRING    TO F833-NME-STREET-BLDG                     
           MOVE F833-NME-STREET-BLDG(1:20)   TO K830-NME-MAIL-STREET.           
           MOVE F833-NME-STREET-BLDG(21:45)  TO K830-NME-MAIL-BLDG.             
                                                                                
      *    MOVE WS-PARSED-STRING    TO K830-NME-MAIL-STREET                     
      *                                F833-NME-STREET.                         
                                                                                
      *    MOVE F833-NME-BLDG       TO K830-NME-MAIL-BLDG.                      
                                                                                
           MOVE F833-NUM-POSTAL     TO K830-NUM-MAIL-POSTAL.                    
           MOVE FUNCTION CURRENT-DATE(1:8)                                      
                                    TO K830-DTE-UPDATE-MAIL.                    
           MOVE 'P13C382'           TO K830-NUM-USERID-MAIL.                    
                                                                                
           REWRITE  BP13K830-REC.                                               
           EVALUATE BP13K830-STATUS                                             
           WHEN  00                                                             
                 ADD   1               TO WS-K830-REWRITE                       
                 IF K800-NUM-HLA NOT = SPACES AND LOW-VALUES                    
                    PERFORM 8900-CREATE-HLA-TRANS THRU 8900-EXIT                
                 END-IF                                                         
           WHEN  OTHER                                                          
              DISPLAY 'ERROR UPDATING BP13K830 : ' BP13K830-STATUS              
                      ',  K830-NUM-REGN : ' K830-NUM-REGN                       
           END-EVALUATE.                                                        
                                                                                
       5500-EXIT.                                                               
            EXIT.                                                               
                                                                                
       5600-REWRITE-BP13K820.                                                   
      *-----------------------------------------------------------              
                                                                                
           REWRITE  BP13K820-REC.                                               
                                                                                
           EVALUATE BP13K820-STATUS                                             
           WHEN  00                                                             
                 ADD   1               TO WS-K820-REWRITE                       
           WHEN  OTHER                                                          
              DISPLAY 'ERROR UPDATING BP13K830 : ' BP13K820-STATUS              
                      ',  K820-KEY-FLD  : ' K820-KEY-FLD                        
           END-EVALUATE.                                                        
                                                                                
       5600-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       5700-WRITE-P13L382C.                                                     
      *-------------------------------------------------------------            
                                                                                
           IF WS-L381C-LINECNT > 55                                             
              PERFORM 7600-PRINT-HEADER THRU 7600-EXIT.                         
                                                                                
           MOVE  SPACES                  TO L382C-PRTREC.                       
           WRITE L382C-PRTREC            FROM L382C-DETAIL AFTER 2.             
           ADD   1                       TO WS-L381C-LINECNT.                   
                                                                                
       5700-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       6000-PRINT-LETTER.                                                       
      *-------------------------------------------------------------            
           STRING F833-DTE-RECEIVED(7:2) '/'                                    
                  F833-DTE-RECEIVED(5:2) '/'                                    
                  F833-DTE-RECEIVED(1:4)                                        
                  DELIMITED BY SIZE INTO L382A-DTE-REQUEST.                     
           MOVE L382A-DTE-REQUEST TO L382B-DTE-NOTIFIED.                        
                                                                                
           STRING F833-TME-RECEIVED(1:2) ':'                                    
                  F833-TME-RECEIVED(3:2) ':'                                    
                  F833-TME-RECEIVED(5:2)                                        
                  DELIMITED BY SIZE INTO L382A-TME-REQUEST.                     
           MOVE L382A-TME-REQUEST TO L382B-TME-NOTIFIED.                        
                                                                                
           MOVE F833-NUM-BLK         TO L382A-NUM-BLOCK                         
                                        L382B-ADDR-NEW-BLK.                     
           MOVE F833-NUM-FLAT        TO L382A-HOUSE-LEVEL                       
                                        L382B-ADDR-NEW-FLAT.                    
           MOVE F833-NME-STREET-BLDG TO L382A-STREET-NAME                       
                                        L382B-ADDR-NEW-STREET.                  
                                                                                
           IF F833-NUM-POSTAL  = SPACES OR LOW-VALUES OR ZEROES                 
              MOVE SPACES            TO L382A-NUM-POSTAL                        
                                        L382B-ADDR-NEW-POSTAL                   
           ELSE                                                                 
              STRING 'SINGAPORE '  F833-NUM-POSTAL                              
              DELIMITED BY SIZE    INTO L382A-NUM-POSTAL                        
              MOVE F833-NUM-POSTAL   TO L382B-ADDR-NEW-POSTAL.                  
                                                                                
           EVALUATE F833-NUM-ADDR-TYPE                                          
           WHEN 'P'                                                             
              MOVE 'PO BOX ' TO L382A-TYPE-ADDR                                 
           WHEN 'H'                                                             
              MOVE 'HDB    ' TO L382A-TYPE-ADDR                                 
           WHEN 'O'                                                             
              MOVE 'OVERSEA' TO L382A-TYPE-ADDR                                 
           WHEN 'N'                                                             
              MOVE 'PRIVATE' TO L382A-TYPE-ADDR                                 
           END-EVALUATE.                                                        
                                                                                
           MOVE F833-NUM-ADDR-TYPE TO L382B-TYPE-ADDR-NEW.                      
                                                                                
           MOVE F833-NUM-PHONE-RES1 TO L382A-NUM-HSE-TEL.                       
           MOVE F833-NUM-PHONE-OFF1 TO L382A-NUM-OFF-TEL.                       
           MOVE F833-NUM-MOBILE1    TO L382A-NUM-MOBILE.                        
           MOVE F833-NUM-PAGER     TO L382A-NUM-PAGER.                          
           MOVE F833-EMAIL         TO L382A-EMAIL.                              
                                                                                
           MOVE  SPACES             TO L382A-PRTREC.                            
           WRITE L382A-PRTREC       AFTER PAGE.                                 
           WRITE L382A-PRTREC       FROM L382A-LINE01.                          
           WRITE L382A-PRTREC       FROM L382A-LINE02 AFTER 3.                  
                                                                                
           WRITE L382A-PRTREC       FROM L382A-LINE03 AFTER 3.                  
           WRITE L382A-PRTREC       FROM L382A-LINE04 AFTER 4.                  
           WRITE L382A-PRTREC       FROM L382A-LINE05.                          
           WRITE L382A-PRTREC       FROM L382A-LINE06.                          
           WRITE L382A-PRTREC       FROM L382A-LINE07 AFTER 4.                  
           WRITE L382A-PRTREC       FROM L382A-LINE08 AFTER 2.                  
           WRITE L382A-PRTREC       FROM L382A-LINE09.                          
           WRITE L382A-PRTREC       FROM L382A-LINE10.                          
           WRITE L382A-PRTREC       FROM L382A-LINE11.                          
           WRITE L382A-PRTREC       FROM L382A-LINE12.                          
                                                                                
           ADD 1 TO WS-CHECKLIST-PRINTED.                                       
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       7000-PRINT-LISTING.                                                      
      *-------------------------------------------------------------            
           IF WS-L381B-LINECNT > 55                                             
              PERFORM 7500-PRINT-HEADER THRU 7500-EXIT.                         
                                                                                
           ADD   1      TO WS-SNO.                                              
           MOVE  WS-SNO TO L382B-SNO.                                           
                                                                                
           MOVE  SPACES                  TO L382B-PRTREC.                       
           WRITE L382B-PRTREC            FROM L382B-DETAIL AFTER 2.             
           WRITE L382B-PRTREC            FROM L382B-DETAIL2.                    
           WRITE L382B-PRTREC            FROM L382B-DETAIL3.                    
           ADD   4                       TO WS-L381B-LINECNT.                   
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       7500-PRINT-HEADER.                                                       
      *-------------------------------------------------------------            
           MOVE  SPACES             TO L382B-PRTREC.                            
                                                                                
           ADD   1                  TO WS-L381B-PAGECNT.                        
           MOVE  WS-L381B-PAGECNT   TO L382B-PAGE.                              
                                                                                
           WRITE L382B-PRTREC       FROM L382B-HEADER1 AFTER PAGE.              
           WRITE L382B-PRTREC       FROM L382B-HEADER2.                         
           WRITE L382B-PRTREC       FROM L382B-HEADER3 AFTER 2.                 
           WRITE L382B-PRTREC       FROM L382B-HEADER4.                         
           WRITE L382B-PRTREC       FROM L382B-HEADER5.                         
                                                                                
           MOVE  6          TO WS-L381B-LINECNT.                                
                                                                                
       7500-EXIT.                                                               
            EXIT.                                                               
                                                                                
       7600-PRINT-HEADER.                                                       
      *-------------------------------------------------------------            
           MOVE  SPACES             TO L382C-PRTREC.                            
                                                                                
           ADD   1                  TO WS-L381C-PAGECNT.                        
           MOVE  WS-L381C-PAGECNT   TO L382C-PAGE.                              
                                                                                
           WRITE L382C-PRTREC       FROM L382C-HEADER1 AFTER PAGE.              
           WRITE L382C-PRTREC       FROM L382C-HEADER2.                         
           WRITE L382C-PRTREC       FROM L382C-HEADER3 AFTER 2.                 
           WRITE L382C-PRTREC       FROM L382C-HEADER4.                         
                                                                                
           MOVE  6          TO WS-L381C-LINECNT.                                
                                                                                
       7600-EXIT.                                                               
            EXIT.                                                               
                                                                                
       8800-PRINT-CONTROL.                                                      
      *-------------------------------------------------------------            
           IF WS-L381B-LINECNT > 55                                             
              PERFORM 7500-PRINT-HEADER THRU 7500-EXIT.                         
                                                                                
           MOVE WS-CHECKLIST-PRINTED TO L382B-TOTAL-PRINTED.                    
           WRITE L382B-PRTREC       FROM L382B-FOOTER AFTER 2.                  
           WRITE L382B-PRTREC       FROM L382B-FOOTER1 AFTER 2.                 
           WRITE L382B-PRTREC       FROM L382B-FOOTER2 AFTER 1.                 
                                                                                
       8800-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       8900-CREATE-HLA-TRANS.                                                   
      *---------------------------------------------------------                
      * TO UPDATE CORRESPONDING ADDRESS IN LEAS                                 
           MOVE SPACES                       TO   BP13F839-REC.                 
           INITIALIZE                             BP13F839-REC.                 
                                                                                
           MOVE K800-NUM-NRIC1         TO   F839-NUM-UIN1.                      
           MOVE K800-NUM-HLA           TO   F839-NUM-HLA.                       
           MOVE K800-NUM-REGN          TO   F839-NUM-REGN.                      
                                                                                
           MOVE F833-NUM-MAIN-BLK      TO   F839-NUM-MAIN-BLK.                  
           MOVE F833-NUM-SUB-BLK       TO   F839-NUM-SUB-BLK.                   
           MOVE F833-NUM-LVL-NUM       TO   F839-NUM-LEVEL.                     
           MOVE F833-NUM-HSEMAIN       TO   F839-NUM-MAIN-UNIT.                 
           MOVE F833-NUM-HSESUB        TO   F839-NUM-SUB-UNIT.                  
           MOVE F833-NME-STREET        TO   F839-NME-STREET-BLDG.               
           MOVE F833-NUM-POSTAL        TO   F839-NUM-POSTAL.                    
                                                                                
           WRITE BP13F839-REC.                                                  
                                                                                
       8900-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       8910-PARSE-STRING.                                                       
      *---------------------------------------------------------                
           MOVE SPACES                    TO   WS-PARSED-STRING.                
           MOVE 1                         TO   WS-CUR-POS-PS.                   
           ADD  1                         TO   WS-STRING-LENGTH.                
           PERFORM 8920-GET-CHAR          THRU 8920-EXIT                        
              VARYING WS-CUR-POS-SP FROM 1 BY 1                                 
              UNTIL WS-CUR-POS-SP = WS-STRING-LENGTH.                           
                                                                                
       8910-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       8920-GET-CHAR.                                                           
      *---------------------------------------------------------                
           IF WS-STRING-TO-PARSE(WS-CUR-POS-SP:1) NOT = SPACES                  
              IF WS-SPACE-FLAG = 'Y' AND                                        
                 WS-INSERT-SPACE-FLAG = 'Y' AND                                 
                 WS-PARSED-STRING NOT = SPACES                                  
                 MOVE ' ' TO WS-PARSED-STRING(WS-CUR-POS-PS:1)                  
                 ADD 1           TO  WS-CUR-POS-PS                              
              END-IF                                                            
              MOVE WS-STRING-TO-PARSE(WS-CUR-POS-SP:1)                          
                              TO  WS-PARSED-STRING(WS-CUR-POS-PS:1)             
              ADD 1           TO  WS-CUR-POS-PS                                 
              MOVE    'N'      TO WS-SPACE-FLAG                                 
           ELSE                                                                 
              MOVE 'Y'     TO WS-SPACE-FLAG                                     
           END-IF.                                                              
                                                                                
       8920-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           CLOSE BP13F833                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K830                                                       
                 BP13K857                                                       
                 SY02F001                                                       
                 P13L382A                                                       
                 P13L382B                                                       
                 P13L382C                                                       
                 BP13F839.                                                      
                                                                                
           IF BP13K820-STATUS  NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BP13K820, STAT ' BP13K820-STATUS.          
                                                                                
           IF BP13K830-STATUS  NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BP13K830, STAT ' BP13K830-STATUS.          
                                                                                
           IF BP13K800-STATUS  NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BP13K800, STAT ' BP13K800-STATUS.          
           STOP RUN.                                                            
                                                                                
           IF BP13K857-STATUS  NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BP13K857, STAT ' BP13K857-STATUS.          
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
