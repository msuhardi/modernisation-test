       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C466.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  18 DEC 2017.                                              
      *========================================================*                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *      1.  DELETE RECORD IN BP13K482 AND WRITE IN        *                
      *          BP13K492 IF AMT-LOAN OR  AMT-LOAN-INSTAL = 0  *                
      *--------------------------------------------------------*                
      * CHG-NO    BY  DATE    DESCRIPTION                      *                
      * -------  ---  ------  -----------                      *                
      * BP137140 FNP1 181217  NEW PROGRAM                      *                
      *========================================================*                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F410 ASSIGN TO BP13F410.                                  
                                                                                
           SELECT BP13K482  ASSIGN        TO BP13K482                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K482-KEY-FLD                       
                            FILE STATUS   IS K482-STATUS.                       
                                                                                
           SELECT BP13K486  ASSIGN        TO BP13K486                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K486-KEY-FLD                       
                            FILE STATUS   IS K486-STATUS.                       
                                                                                
           SELECT BP13K492  ASSIGN        TO BP13K492                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K492-KEY-FLD                       
                            FILE STATUS   IS K492-STATUS.                       
                                                                                
           SELECT BP13K496  ASSIGN        TO BP13K496                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K496-KEY-FLD                       
                            FILE STATUS   IS K496-STATUS.                       
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K482                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13K482.                                                           
                                                                                
       FD  BP13K492                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13K492.                                                           
                                                                                
       FD  BP13K486                                                             
           RECORD CONTAINS  400 CHARACTERS.                                     
       COPY BP13K486.                                                           
                                                                                
       FD  BP13K496                                                             
           RECORD CONTAINS  400 CHARACTERS.                                     
       COPY BP13K496.                                                           
                                                                                
       FD  BP13F410                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           RECORDING MODE IS F                                                  
           LABEL RECORD IS STANDARD.                                            
       COPY BP13F410.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  K482-STATUS                    PIC 9(2)  VALUE ZERO.                 
       01  K492-STATUS                    PIC 9(2)  VALUE ZERO.                 
       01  K486-STATUS                    PIC 9(2)  VALUE ZERO.                 
       01  K496-STATUS                    PIC 9(2)  VALUE ZERO.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F410-READ          PIC 9(7)    VALUE ZEROES.                   
           05 WS-F410-VALID         PIC 9(7)    VALUE ZEROES.                   
           05 CNT-K482-FND          PIC 9(7)    VALUE ZEROES.                   
           05 WS-K482-DELETED       PIC 9(7)    VALUE ZEROES.                   
           05 WS-K486-DELETED       PIC 9(7)    VALUE ZEROES.                   
           05 WS-K492-WRITTEN       PIC 9(7)    VALUE ZEROES.                   
           05 WS-K496-WRITTEN       PIC 9(7)    VALUE ZEROES.                   
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F410-EOF          PIC X       VALUE 'N'.                      
           05  WS-K482-FND          PIC X(01)   VALUE SPACES.                   
               88  K482-FND                        VALUE 'Y'.                   
           05  WS-K482-EOF          PIC X(01)  VALUE SPACES.                    
               88  K482-EOF                        VALUE 'Y'.                   
                                                                                
       01  WS-DATE-TIME.                                                        
           05  WS-DATE         PIC 9(08)  VALUE ZEROES.                         
           05  WS-TIME         PIC 9(08)  VALUE ZEROES.                         
       01  WS-TIME-OUT.                                                         
           05  WS-TIME-OUT-HM  PIC 9(08)  VALUE ZEROES.                         
           05  WS-TIME-OUT-S   PIC 9(01)  VALUE ZEROES.                         
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-CUR-NRICS.                                                    
               10  WS-CUR-NRIC OCCURS 4 TIMES PIC X(09) VALUE SPACES.           
                                                                                
           05  WS-I             PIC 9 VALUE ZEROES.                             
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *------------------------------------------------------------             
       0000-MAIN.                                                               
      *------------------------------------------------------------             
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 1100-READ-BP13F410   THRU 1100-EXIT.                         
           PERFORM 2000-PROCESS-RECORDS THRU 2000-EXIT                          
             UNTIL WS-F410-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       1000-OPEN-ROUTINE.                                                       
      *------------------------------------------------------------             
                                                                                
           OPEN INPUT  BP13F410                                                 
                I-O    BP13K482                                                 
                       BP13K486                                                 
                       BP13K492                                                 
                       BP13K496.                                                
                                                                                
           IF K482-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, K482-STATUS ' K482-STATUS                    
              MOVE K482-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF K486-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, K486-STATUS ' K486-STATUS                    
              MOVE K486-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF K492-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, K492-STATUS ' K492-STATUS                    
              MOVE K492-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF K496-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, K496-STATUS ' K496-STATUS                    
              MOVE K496-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------             
       1100-READ-BP13F410.                                                      
      *------------------------------------------------------------             
                                                                                
           READ BP13F410 AT END                                                 
              MOVE 'Y' TO WS-F410-EOF                                           
                GO TO 1100-EXIT.                                                
                                                                                
           ADD 1 TO WS-F410-READ.                                               
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       2000-PROCESS-RECORDS.                                                    
      *------------------------------------------------------------             
                                                                                
           IF F410-NUM-CONTRA-TYPE = 'TM' OR  F410-AMT-LOAN-INSTAL = 0          
              ADD 1 TO WS-F410-VALID                                            
              MOVE SPACES                   TO K482-KEY-FLD                     
              MOVE F410-NUM-SCH-ACC         TO K482-NUM-HDBREF-NEW              
              PERFORM 3000-START-BP13K482   THRU 3000-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 1100-READ-BP13F410     THRU 1100-EXIT.                       
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3000-START-BP13K482.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE 'N'                   TO WS-K482-EOF.                           
                                                                                
           START BP13K482 KEY >= K482-KEY-FLD.                                  
                                                                                
           IF K482-STATUS = 00 OR 02 OR 22                                      
              PERFORM 3100-READ-NEXT-BP13K482  THRU 3100-EXIT                   
                 UNTIL WS-K482-EOF = 'Y'                                        
           ELSE                                                                 
              IF K482-STATUS = 10 OR 23                                         
                 MOVE 'Y'             TO WS-K482-EOF                            
              ELSE                                                              
                 DISPLAY 'BP13K482 - START ERROR (' K482-STATUS                 
                          '), KEY IS ('  K482-KEY-FLD ')'                       
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-READ-NEXT-BP13K482.                                                 
      *-------------------------------------------------------------            
                                                                                
           READ BP13K482 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y' TO WS-K482-EOF                                         
                GO       TO 3100-EXIT                                           
           END-READ.                                                            
                                                                                
           IF K482-STATUS = 00 OR 02 OR 22                                      
              IF K482-NUM-HDBREF-NEW = F410-NUM-SCH-ACC                         
                 ADD 1 TO CNT-K482-FND                                          
                 MOVE 'TM' TO K482-CDE-DELETE                                   
                 PERFORM 3500-PROCESS-BP13K486 THRU 3500-EXIT                   
                 PERFORM 4000-WRITE-BP13K492   THRU 4000-EXIT                   
                 PERFORM 4100-DELETE-BP13K482  THRU 4100-EXIT                   
              ELSE                                                              
                 MOVE 'Y' TO WS-K482-EOF                                        
              END-IF                                                            
           ELSE                                                                 
              IF K482-STATUS = 10 OR 23                                         
                 MOVE 'Y'               TO WS-K482-EOF                          
              ELSE                                                              
                 MOVE 'Y'               TO WS-K482-EOF                          
                 DISPLAY 'BP13K482 - READ NEXT ERROR (' K482-STATUS             
                         ') ,KEY IS ('  K482-KEY-FLD ')'                        
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3500-PROCESS-BP13K486.                                                   
      *-------------------*                                                     
           MOVE K482-NUM-NRIC1-NEW   TO WS-CUR-NRIC(1).                         
           MOVE K482-NUM-NRIC2-NEW   TO WS-CUR-NRIC(2).                         
           MOVE K482-NUM-NRIC3-NEW   TO WS-CUR-NRIC(3).                         
           MOVE K482-NUM-NRIC4-NEW   TO WS-CUR-NRIC(4).                         
                                                                                
           PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > 4                      
              IF WS-CUR-NRIC(WS-I)  NOT = SPACES  AND LOW-VALUES                
                 MOVE SPACES                   TO BP13K486-REC                  
                 INITIALIZE                       BP13K486-REC                  
                 MOVE WS-CUR-NRIC(WS-I)        TO K486-NUM-NRIC                 
                 MOVE K482-NUM-HDBREF-NEW      TO K486-NUM-HDBREF-NEW           
                 MOVE K482-NUM-HDBREF-DISPOSAL TO K486-NUM-HDBREF-EXIST         
                 PERFORM 3600-READ-BP13K486    THRU 3600-EXIT                   
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3600-READ-BP13K486.                                                      
      *-------------------*                                                     
           READ BP13K486.                                                       
                                                                                
           IF K486-STATUS = 00 OR 02                                            
              PERFORM 3700-WRITE-BP13K496   THRU 3700-EXIT                      
              PERFORM 3800-DELETE-BP13K486  THRU 3800-EXIT                      
           ELSE                                                                 
              IF K486-STATUS = 23                                               
                 CONTINUE                                                       
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K486, STATUS IS '                   
                    K486-STATUS ' KEY IS ' K486-KEY-FLD                         
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3700-WRITE-BP13K496.                                                     
      *-------------------*                                                     
                                                                                
           MOVE SPACES                   TO BP13K496-REC.                       
           INITIALIZE                       BP13K496-REC.                       
           MOVE BP13K486-REC             TO BP13K496-REC.                       
                                                                                
           MOVE 'BP13'                   TO K496-NUM-SYSTEM-UPDATE.             
           MOVE 'BP13C466'               TO K496-NUM-USERID.                    
           MOVE FUNCTION CURRENT-DATE    TO WS-DATE-TIME.                       
           MOVE WS-TIME                  TO WS-TIME-OUT-HM.                     
           ADD  1                        TO WS-TIME-OUT-S.                      
           MOVE WS-DATE                  TO K496-DTE-UPDATE.                    
           MOVE WS-TIME-OUT              TO K496-TME-UPDATE.                    
                                                                                
           WRITE BP13K496-REC.                                                  
                                                                                
           IF K496-STATUS NOT = 00                                              
              DISPLAY 'ERROR WRITING BP13K496, KEY IS '                         
                       K496-KEY-FLD                                             
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           ELSE                                                                 
              ADD 1 TO WS-K496-WRITTEN                                          
           END-IF.                                                              
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       3800-DELETE-BP13K486.                                                    
      *------------------------------------------------------------             
                                                                                
           DELETE BP13K486.                                                     
                                                                                
           IF K486-STATUS = 00                                                  
              ADD 1                  TO WS-K486-DELETED                         
           ELSE                                                                 
              DISPLAY 'BP13K486 - DELETE  ERROR (' K486-STATUS ')'              
                      ', KEY (' K486-KEY-FLD ')'                                
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
       3800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4000-WRITE-BP13K492.                                                     
      *-------------------*                                                     
                                                                                
           MOVE SPACES                   TO BP13K492-REC.                       
           INITIALIZE                       BP13K492-REC.                       
           MOVE BP13K482-REC             TO BP13K492-REC.                       
                                                                                
           MOVE 'BP13'                   TO K492-NUM-SYSTEM.                    
           MOVE 'BP13C466'               TO K492-USERID.                        
           MOVE FUNCTION CURRENT-DATE    TO WS-DATE-TIME.                       
           MOVE WS-TIME                  TO WS-TIME-OUT-HM.                     
           ADD  1                        TO WS-TIME-OUT-S.                      
           MOVE WS-DATE                  TO K492-DTE-UPDATE.                    
           MOVE WS-TIME-OUT              TO K492-TME-UPDATE.                    
                                                                                
           WRITE BP13K492-REC.                                                  
                                                                                
           IF K492-STATUS NOT = 00                                              
              DISPLAY 'ERROR WRITING BP13K492, KEY IS '                         
                       K492-NUM-HDBREF-NEW                                      
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           ELSE                                                                 
              ADD 1 TO WS-K492-WRITTEN                                          
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       4100-DELETE-BP13K482.                                                    
      *------------------------------------------------------------             
                                                                                
           DELETE BP13K482.                                                     
                                                                                
           IF K482-STATUS = 00                                                  
              ADD 1                  TO WS-K482-DELETED                         
           ELSE                                                                 
              DISPLAY 'BP13K482 - DELETE  ERROR (' K482-STATUS ')'              
                      ', KEY (' K482-KEY-FLD ')'                                
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       9000-CLOSE-ROUTINE.                                                      
      *------------------------------------------------------------             
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '***********BP13C466 END OF JOB*******************'.         
           DISPLAY '*-----------------------------------------------*'.         
           DISPLAY 'NO OF RECORDS READ FROM F410    ' WS-F410-READ.             
           DISPLAY 'NO OF RECORDS VALID FROM F410   ' WS-F410-VALID.            
           DISPLAY 'NO OF RECORDS FOUND IN BP13K482 ' CNT-K482-FND.             
           DISPLAY 'NO OF RECORDS WRITTEN  BP13K492 ' WS-K492-WRITTEN.          
           DISPLAY 'NO OF RECORDS DELETED  BP13K482 ' WS-K482-DELETED.          
           DISPLAY 'NO OF RECORDS WRITTEN  BP13K496 ' WS-K496-WRITTEN.          
           DISPLAY 'NO OF RECORDS DELETED  BP13K486 ' WS-K486-DELETED.          
           DISPLAY ' '.                                                         
                                                                                
                                                                                
           CLOSE BP13F410                                                       
                 BP13K482                                                       
                 BP13K486                                                       
                 BP13K492                                                       
                 BP13K496.                                                      
                                                                                
           IF K482-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, K482-STATUS ' K482-STATUS                   
              MOVE K482-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           IF K486-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, K486-STATUS ' K486-STATUS                   
              MOVE K486-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           IF K492-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, K492-STATUS ' K492-STATUS                   
              MOVE K492-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           IF K496-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, K496-STATUS ' K496-STATUS                   
              MOVE K496-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
