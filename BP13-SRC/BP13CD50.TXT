       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD50.                                                 
      *AUTHOR.        BO JIANG.                                                 
      *DATE-WRITTEN.  08/08/08.                                                 
      * ========================================================== *            
      * SYSTEM OF COMMITMENT  (BP13)                               *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE   : PRINT LETTER FOR NOTIFICATION OF TOP FOR    *            
      *                DBSS FLAT                                   *            
      *                                                            *            
      *  INPUT FILES : 1.  BP13F800  -                             *            
      *                2.  BP13KB20  -                             *            
      *                3.  BP13K820  -                             *            
      *                4.  BP13K830  -                             *            
      *                                                            *            
      *  LETTERS     : 1.  BP13PD50  -                             *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF  BY    DATE    DESCRIPTION                         *            
      * -------- ---- -------- -----------                         *            
      * BP133367 JB8  20080804 NEW PROGRAM.                        *            
      * BP134134 ZDD1 20110201 CATER FOR THE CHANGES ON FORM       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
      *-------------------------------------------------------------            
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800  ASSIGN        TO BP13F800.                          
           SELECT SY02F001  ASSIGN        TO SY02F001.                          
                                                                                
           SELECT BP13KB20  ASSIGN        TO BP13KB20                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KB20-KEY-FLD                       
                            FILE STATUS   IS WS-KB20-STATUS.                    
                                                                                
           SELECT BP13K820  ASSIGN        TO BP13K820                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K820-KEY-FLD                       
                            FILE STATUS   IS WS-K820-STATUS.                    
                                                                                
           SELECT BP13K830  ASSIGN        TO BP13K830                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K830-KEY-FLD                       
                            ALTERNATE KEY IS K830-NUM-REGN                      
                            FILE STATUS   IS WS-K830-STATUS.                    
                                                                                
           SELECT BP13PD50  ASSIGN        TO BP13PD50.                          
           SELECT BP13LD50  ASSIGN        TO BP13LD50.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       COPY SY02F001.                                                           
                                                                                
       FD   BP13F800                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13F800.                                                           
                                                                                
       FD   BP13KB20                                                            
            RECORD CONTAINS 3300 CHARACTERS.                                    
       COPY BP13KB20.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K830.                                                           
                                                                                
       FD  BP13LD50                                                             
           RECORDING MODE  IS  F.                                               
       01  LD50-PRTREC                      PIC X(132).                         
                                                                                
       FD  BP13PD50                                                             
           RECORDING MODE  IS  F.                                               
       01  PD50-PRTREC                      PIC X(90).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-STATUS.                                                           
           05  WS-KB20-STATUS               PIC 99    VALUE ZEROS.              
           05  WS-K820-STATUS               PIC 99    VALUE ZEROS.              
           05  WS-K830-STATUS               PIC 99    VALUE ZEROS.              
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F800-READ-CTR             PIC 9(06)  VALUE ZEROS.             
           05  WS-PD50-WRT-CTR              PIC 9(06)  VALUE ZEROS.             
           05  WS-CNT-PAGE-LD50             PIC 9(04)  VALUE ZEROS.             
           05  WS-CNT-LINE-LD50             PIC 9(06)  VALUE ZEROS.             
           05  WS-CNT-CUR-LINE              PIC 9(02)  VALUE 56.                
           05  WS-CTR                       PIC 9(03)  VALUE ZEROS.             
           05  WS-SKIP                      PIC 9(06)  VALUE ZEROS.             
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F800-EOF                  PIC X     VALUE 'N'.                
           05  WS-K800-FND                  PIC X     VALUE 'N'.                
           05  WS-K820-NME-OCCP             PIC X(66) VALUE SPACES.             
           05  WS-K820-NME-HA1              PIC X(66) VALUE SPACES.             
           05  WS-F001-EOF                  PIC X(1)  VALUE 'N'.                
               88 EOF-F001                            VALUE 'Y'.                
           05  WS-RETCODE                   PIC S9(8)                           
                                            SIGN IS LEADING SEPARATE.           
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-DATE.                                                         
               10  WS-CCYY                  PIC 9(4)  VALUE 0000.               
               10  WS-MM                    PIC 99    VALUE 00.                 
               10  WS-DD                    PIC 99    VALUE 00.                 
           05  WS-CURR-DATE                 PIC 9(8)  VALUE ZEROES.             
                                                                                
      *-------------------------------------------------------------            
      * BP13C913 PARAMETERS                                                     
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH-ACC.                                              
              10 WS-LINK-NUM-SCH       PIC X(4).                                
              10 WS-LINK-NUM-ACC       PIC X(5).                                
       COPY P13COMM8.                                                           
      *===============================                                          
      *   LINKAGE SECTION FOR AV02C015                                          
      *===============================                                          
       01  WS-LINK-AV02C015.                                                    
           05  AV02C015-IN-POSTCODE             PIC X(6).                       
           05  AV02C015-OUT-POSTCODE-CHKSUM     PIC X(9).                       
                                                                                
      *------------------------------------------------------------*            
      *        LAY-OUT FOR LETTER TO APPLICANT                     *            
      *------------------------------------------------------------*            
       01  PD50-LINE01.                                                         
           05  FILLER                   PIC X(72) VALUE SPACES.                 
           05  PD50-DATE                PIC X(10) VALUE SPACES.                 
                                                                                
       01  PD50-LINE02.                                                         
           05  FILLER                   PIC X(72) VALUE SPACES.                 
           05  PD50-NUM-REGN            PIC X(08) VALUE SPACES.                 
                                                                                
       01  PD50-LINE02A.                                                        
           05  FILLER                   PIC X(72) VALUE SPACES.                 
           05  PD50-NUM-SERIAL          PIC X(04) VALUE SPACES.                 
                                                                                
       01  PD50-LINE03.                                                         
           05  FILLER                   PIC X(12) VALUE SPACES.                 
           05  PD50-NAME                PIC X(66) VALUE SPACES.                 
                                                                                
       01  PD50-LINE04.                                                         
           05  FILLER                   PIC X(12) VALUE SPACES.                 
           05  PD50-NAME2               PIC X(66) VALUE SPACES.                 
                                                                                
       01  PD50-LINE05.                                                         
           05  FILLER                   PIC X(12) VALUE SPACES.                 
           05  PD50-BLK                 PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  PD50-UNIT                PIC X(11) VALUE SPACES.                 
                                                                                
       01  PD50-LINE06.                                                         
           05  FILLER                   PIC X(12) VALUE SPACES.                 
           05  PD50-ADDR-L2             PIC X(47) VALUE SPACES.                 
                                                                                
       01  PD50-LINE07.                                                         
           05  FILLER                   PIC X(12) VALUE SPACES.                 
           05  PD50-ADDR-L3             PIC X(20) VALUE SPACES.                 
                                                                                
       01  PD50-LINE08.                                                         
           05  FILLER                   PIC X(10) VALUE SPACES.                 
           05  PD50-BAR-CODE            PIC X(09) VALUE SPACES.                 
                                                                                
       01  PD50-LINE09.                                                         
           05  FILLER                   PIC X(12) VALUE SPACES.                 
           05  FILLER                   PIC X(12) VALUE                         
               'SALE OF BLK '.                                                  
           05  PD50-PIDB-BLK            PIC X(05) VALUE SPACES.                 
           05  FILLER                   PIC X(20) VALUE                         
               ' BISHAN STREET 24 # '.                                          
           05  PD50-PIDB-LEVEL          PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE '-'.                    
           05  PD50-PIDB-UNIT           PIC X(04) VALUE SPACES.                 
           05  FILLER                   PIC X(10) VALUE                         
               ' SINGAPORE'.                                                    
                                                                                
      *------------------------------------------------------------*            
      *        LAY-OUT FOR CONTROL LISTING                         *            
      *------------------------------------------------------------*            
       01  LD50-HDG1.                                                           
           05  LD50-LISTING             PIC X(14) VALUE 'BP13LD50'.             
           05  FILLER                   PIC X(8)  VALUE 'HDB3'.                 
           05  FILLER                   PIC X(18) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(18) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE: '.               
           05  LD50-DATE                PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(8)  VALUE ' PAGE: '.              
           05  LD50-PAGE                PIC ZZZZ9.                              
                                                                                
       01  LD50-HDG2.                                                           
           05  FILLER                   PIC X(41) VALUE SPACES.                 
           05  FILLER                   PIC X(37) VALUE                         
               'CONTROL LIST FOR NOTIFICATION OF TOP '.                         
                                                                                
       01  LD50-HDG3.                                                           
           05  FILLER                   PIC X(4)  VALUE SPACES.                 
           05  FILLER                   PIC X(5)  VALUE '  S/N'.                
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  FILLER                   PIC X(8)  VALUE 'REGN    '.             
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  FILLER                   PIC X(6)  VALUE 'SERIAL'.               
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  FILLER                   PIC X(09) VALUE 'NRIC1'.                
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  FILLER                   PIC X(30) VALUE 'NAME1'.                
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  FILLER                   PIC X(18) VALUE 'ADDRESS'.              
                                                                                
       01  LD50-DTL.                                                            
           05  FILLER                   PIC X(4)  VALUE SPACES.                 
           05  LD50-SN                  PIC ZZZZ9 VALUE SPACES.                 
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  LD50-REGN                PIC X(8)  VALUE SPACES.                 
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  LD50-SERIAL              PIC X(4)  VALUE SPACES.                 
           05  FILLER                   PIC X(4)  VALUE SPACES.                 
           05  LD50-NRIC1               PIC X(09) VALUE SPACES.                 
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  LD50-NAME1               PIC X(30) VALUE SPACES.                 
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  LD50-PIDB-ADDR           PIC X(20) VALUE SPACES.                 
                                                                                
      *=============================================================            
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-FILES             THRU 1000-EXIT.                  
           PERFORM 3000-PROCESS-RECORDS        THRU 3000-EXIT                   
                   UNTIL WS-F800-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE          THRU 9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-FILES.                                                         
      *=============================================================            
           OPEN INPUT BP13F800                                                  
                      SY02F001                                                  
                      BP13KB20                                                  
                      BP13K820                                                  
                      BP13K830                                                  
               OUTPUT BP13PD50                                                  
                      BP13LD50.                                                 
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K820 STATUS ' WS-K820-STATUS               
              MOVE     WS-K820-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE.                                       
                                                                                
           IF WS-KB20-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR KB20 STATUS ' WS-KB20-STATUS               
              MOVE     WS-KB20-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE.                                       
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K830 STATUS ' WS-K830-STATUS               
              MOVE     WS-K830-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE.                                       
                                                                                
           READ SY02F001 AT END MOVE 'Y'  TO WS-F001-EOF.                       
           IF EOF-F001                                                          
              DISPLAY 'RECORD NOT FOUND IN SY01F001 '                           
              DISPLAY 'PROCESSING TERMINATED '                                  
              GO TO 9000-CLOSE-ROUTINE                                          
           ELSE                                                                 
              MOVE F001-DTE-CURRENT      TO   WS-DATE WS-CURR-DATE              
           END-IF.                                                              
           STRING '24/'                                                         
                  WS-MM '/'                                                     
                  WS-CCYY DELIMITED BY SIZE                                     
                  INTO PD50-DATE.                                               
           MOVE PD50-DATE                TO LD50-DATE.                          
                                                                                
           PERFORM 2000-READ-BP13F800  THRU 2000-EXIT.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       2000-READ-BP13F800.                                                      
      *=============================================================            
                                                                                
            READ BP13F800 AT END                                                
                 MOVE 'Y'  TO  WS-F800-EOF                                      
                 GO TO 2000-EXIT.                                               
                                                                                
            ADD  1  TO WS-F800-READ-CTR.                                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-PROCESS-RECORDS.                                                    
      *=============================================================            
                                                                                
           INITIALIZE     PD50-LINE02 PD50-LINE02A PD50-LINE03                  
                          PD50-LINE04 PD50-LINE05 PD50-LINE06                   
                          PD50-LINE07 PD50-LINE08 PD50-LINE09.                  
                                                                                
           MOVE F800-NUM-REGN               TO PD50-NUM-REGN                    
           MOVE F800-NUM-SCH-ACC(1:9)       TO WS-LINK-NUM-SCH-ACC              
                                                                                
           MOVE F800-NUM-REGN               TO K820-NUM-REGN                    
           MOVE F800-NUM-NRIC1              TO K820-NUM-NRIC                    
           PERFORM 4600-READ-K820         THRU 4600-EXIT                        
           MOVE WS-K820-NME-OCCP            TO PD50-NAME                        
                                               WS-K820-NME-HA1.                 
                                                                                
           IF F800-NUM-NRIC2  = SPACES OR LOW-VALUES                            
              MOVE SPACES                   TO PD50-NAME2                       
           ELSE                                                                 
              MOVE F800-NUM-NRIC2           TO K820-NUM-NRIC                    
              PERFORM 4600-READ-K820      THRU 4600-EXIT                        
              MOVE WS-K820-NME-OCCP         TO PD50-NAME2.                      
                                                                                
           PERFORM 4800-GET-ADDR          THRU 4800-EXIT.                       
           PERFORM 5000-READ-BP13KB20     THRU 5000-EXIT.                       
           PERFORM 6000-READ-PIDB         THRU 6000-EXIT.                       
                                                                                
           PERFORM 7000-PRT-LETTER        THRU 7000-EXIT                        
           PERFORM 8000-PRN-CNTRL-LIST    THRU 8000-EXIT.                       
                                                                                
           PERFORM 2000-READ-BP13F800     THRU 2000-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       4600-READ-K820.                                                          
      *=============================================================            
           MOVE    SPACES                TO  WS-K820-NME-OCCP                   
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = 00 OR 02                                         
              MOVE    K820-NME-OCCP      TO  WS-K820-NME-OCCP                   
           ELSE                                                                 
           IF WS-K820-STATUS = 23                                               
              DISPLAY 'K820 REC NOT FOUND : '   K820-KEY-FLD                    
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K820 = '   WS-K820-STATUS              
                      ', K820-KEY-FLD = '   K820-KEY-FLD                        
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                      
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       4800-GET-ADDR.                                                           
      *=============================================================            
                                                                                
           MOVE F800-NUM-REGN TO K830-NUM-REGN                                  
           READ BP13K830  KEY IS K830-NUM-REGN.                                 
                                                                                
           IF WS-K830-STATUS = 00                                               
              IF (K830-NUM-MAIL-ADDR =  SPACES OR LOW-VALUES) OR                
                 ((K830-NUM-MAIL-LVL-CHAR =  '#') AND                           
                 (K830-NUM-MAIL-LVL-NUM =  SPACES OR LOW-VALUES))               
                 MOVE K830-NUM-BLK         TO PD50-BLK                          
                 IF K830-NUM-LVL-NUM = SPACES AND                               
                    K830-NUM-HSEMAIN = SPACES                                   
                    MOVE SPACES            TO PD50-UNIT                         
                 ELSE                                                           
                    MOVE K830-NUM-FLAT     TO PD50-UNIT                         
                 END-IF                                                         
                 MOVE K830-NME-STREET-BLDG(1:47) TO PD50-ADDR-L2                
                 IF K830-NUM-POSTAL  = SPACES OR LOW-VALUES OR ZEROES           
                    MOVE SPACES            TO PD50-ADDR-L3                      
                    DISPLAY ' RES POSTAL CDE IS ' K830-NUM-POSTAL               
                            ' FOR REC KEY-FLD ' K830-KEY-FLD                    
                 ELSE                                                           
                    STRING 'SINGAPORE '  K830-NUM-POSTAL                        
                       DELIMITED BY SIZE INTO PD50-ADDR-L3                      
                 END-IF                                                         
      *------ TO GET THE BAR CODE ------                                        
                 MOVE SPACES                    TO WS-LINK-AV02C015             
                 MOVE K830-NUM-POSTAL           TO AV02C015-IN-POSTCODE         
                 PERFORM 4850-CALL-AV02C015     THRU 4850-EXIT                  
                 MOVE AV02C015-OUT-POSTCODE-CHKSUM TO PD50-BAR-CODE             
                                                                                
              ELSE                                                              
                 MOVE K830-NUM-MAIL-BLK        TO PD50-BLK                      
                 IF K830-NUM-MAIL-LVL-NUM = SPACES AND                          
                    K830-NUM-MAIL-HSEMAIN = SPACES                              
                    MOVE SPACES                TO PD50-UNIT                     
                 ELSE                                                           
                    MOVE K830-NUM-MAIL-FLAT    TO PD50-UNIT                     
                 END-IF                                                         
                 MOVE K830-NME-MAIL-STR-BLDG(1:47) TO PD50-ADDR-L2              
                 IF K830-NUM-MAIL-POSTAL = SPACES OR LOW-VALUES OR ZEROS        
                    MOVE SPACES                TO PD50-ADDR-L3                  
                    DISPLAY ' MAIL POSTAL CDE IS ' K830-NUM-POSTAL              
                            ' FOR REC KEY-FLD ' K830-KEY-FLD                    
                 ELSE                                                           
                    STRING 'SINGAPORE '      K830-NUM-MAIL-POSTAL               
                           DELIMITED BY SIZE INTO PD50-ADDR-L3                  
                 END-IF                                                         
      *------ TO GET THE BAR CODE ------                                        
                 MOVE SPACES                    TO WS-LINK-AV02C015             
                 MOVE K830-NUM-MAIL-POSTAL      TO AV02C015-IN-POSTCODE         
                 PERFORM 4850-CALL-AV02C015     THRU 4850-EXIT                  
                 MOVE AV02C015-OUT-POSTCODE-CHKSUM TO PD50-BAR-CODE             
              END-IF                                                            
                                                                                
           ELSE                                                                 
           IF WS-K830-STATUS = 23                                               
              INITIALIZE     PD50-LINE05                                        
                             PD50-LINE06                                        
                             PD50-LINE07                                        
                             PD50-LINE08                                        
              DISPLAY 'BP13K830 RECORD NOT FOUND = ' WS-K830-STATUS             
                      ',  K830-KEY-FLD IS ' K830-NUM-REGN                       
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K830 = ' WS-K830-STATUS                
                      ',  K830-KEY-FLD IS ' K830-NUM-REGN                       
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                        
                                                                                
       4800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       4850-CALL-AV02C015.                                                      
      *=============================================================            
                                                                                
           CALL 'AV02C015'          USING                                       
                                    AV02C015-IN-POSTCODE                        
                                    AV02C015-OUT-POSTCODE-CHKSUM.               
       4850-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       5000-READ-BP13KB20.                                                      
      *=============================================================            
                                                                                
           MOVE SPACES                         TO BP13KB20-REC.                 
           INITIALIZE                             BP13KB20-REC.                 
                                                                                
           MOVE F800-NUM-REGN                  TO KB20-NUM-REGN.                
                                                                                
           READ BP13KB20.                                                       
                                                                                
           EVALUATE WS-KB20-STATUS                                              
              WHEN 00                                                           
                 MOVE KB20-NUM-SERIAL          TO PD50-NUM-SERIAL               
              WHEN 23                                                           
                 MOVE SPACES                   TO PD50-NUM-SERIAL               
              WHEN OTHER                                                        
                 DISPLAY 'BP13KB20 - ERROR READING : ' WS-KB20-STATUS           
                 GO TO 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-READ-PIDB.                                                          
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                  TO PD50-PIDB-BLK                        
                                           PD50-PIDB-LEVEL                      
                                           PD50-PIDB-UNIT.                      
           CALL 'BP13C913' USING WS-LINK-REC,                                   
                                 BP13COMM8-REC.                                 
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
           WHEN +000                                                            
              MOVE COMM8-NUM-BLK        TO PD50-PIDB-BLK                        
              MOVE COMM8-NUM-LEVEL      TO PD50-PIDB-LEVEL                      
              MOVE COMM8-NUM-UNIT-MAIN(1:4) TO PD50-PIDB-UNIT                   
           WHEN +100                                                            
              MOVE SPACES   TO BP13COMM8-REC                                    
              INITIALIZE       BP13COMM8-REC                                    
              DISPLAY 'REC NOT FOUND IN PIDB: ' WS-LINK-NUM-SCH-ACC             
           WHEN OTHER                                                           
              MOVE COMM8-CDE-SYSERR      TO WS-RETCODE                          
              DISPLAY 'ERROR IN CALLING BP13C913, RETCODE: '                    
                      WS-RETCODE                                                
              PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       7000-PRT-LETTER.                                                         
      *=============================================================            
                                                                                
            MOVE SPACES           TO   PD50-PRTREC.                             
            WRITE PD50-PRTREC AFTER PAGE.                                       
                                                                                
            WRITE PD50-PRTREC    FROM  PD50-LINE01 AFTER 2.                     
            WRITE PD50-PRTREC    FROM  PD50-LINE02.                             
            WRITE PD50-PRTREC    FROM  PD50-LINE02A.                            
            WRITE PD50-PRTREC    FROM  PD50-LINE03 AFTER 2.                     
            WRITE PD50-PRTREC    FROM  PD50-LINE04.                             
            WRITE PD50-PRTREC    FROM  PD50-LINE05.                             
            WRITE PD50-PRTREC    FROM  PD50-LINE06.                             
            WRITE PD50-PRTREC    FROM  PD50-LINE07.                             
            WRITE PD50-PRTREC    FROM  PD50-LINE08 AFTER 4.                     
            WRITE PD50-PRTREC    FROM  PD50-LINE09 AFTER 6.                     
            ADD   1               TO    WS-PD50-WRT-CTR.                        
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *=============================================================            
       8000-PRN-CNTRL-LIST.                                                     
      *=============================================================            
                                                                                
           INITIALIZE LD50-DTL.                                                 
                                                                                
           MOVE F800-NUM-REGN             TO LD50-REGN                          
           MOVE KB20-NUM-SERIAL           TO LD50-SERIAL                        
           MOVE F800-NUM-NRIC1            TO LD50-NRIC1                         
           MOVE WS-K820-NME-HA1           TO LD50-NAME1                         
           STRING 'BLK' COMM8-NUM-BLK                                           
                  ' # ' COMM8-NUM-LEVEL                                         
                  ' - ' COMM8-NUM-UNIT-MAIN(1:4)                                
              DELIMITED BY SIZE INTO LD50-PIDB-ADDR.                            
                                                                                
           ADD 1                          TO WS-CNT-LINE-LD50.                  
           ADD 1                          TO WS-CNT-CUR-LINE.                   
           MOVE WS-CNT-LINE-LD50          TO LD50-SN                            
           IF WS-CNT-CUR-LINE > 55                                              
              ADD 1                       TO WS-CNT-PAGE-LD50                   
              MOVE WS-CNT-PAGE-LD50       TO LD50-PAGE                          
              WRITE LD50-PRTREC FROM LD50-HDG1 AFTER PAGE                       
              WRITE LD50-PRTREC FROM LD50-HDG2 AFTER 2                          
              WRITE LD50-PRTREC FROM LD50-HDG3 AFTER 3                          
              MOVE 6                      TO WS-CNT-CUR-LINE                    
           END-IF                                                               
                                                                                
           WRITE LD50-PRTREC FROM LD50-DTL.                                     
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       9000-CLOSE-ROUTINE.                                                      
      **************************************************************            
           DISPLAY ' '.                                                         
           DISPLAY ' ====  BP13CD50 CONTROL TOTALS  ======'.                    
           DISPLAY ' '.                                                         
           DISPLAY ' F800 RECORDS READ          : ' WS-F800-READ-CTR.           
           DISPLAY ' NO OF CASES FOR BP13I0     : ' WS-PD50-WRT-CTR.            
           DISPLAY ' NO OF CASES WRITE          : ' WS-CNT-LINE-LD50.           
           DISPLAY ' '.                                                         
           DISPLAY ' ====================================='.                    
                                                                                
           CLOSE BP13F800                                                       
                 SY02F001                                                       
                 BP13KB20                                                       
                 BP13K820                                                       
                 BP13K830                                                       
                 BP13PD50                                                       
                 BP13LD50.                                                      
                                                                                
           IF WS-KB20-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR BP13KB20-STAT ' WS-KB20-STATUS.            
                                                                                
           IF WS-K820-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR BP13K820-STAT ' WS-K820-STATUS.            
                                                                                
           IF WS-K830-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR BP13K830-STAT ' WS-K830-STATUS.            
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
