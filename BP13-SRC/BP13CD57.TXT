       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD57.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  30/12/08.                                                 
      * ========================================================== *            
      * SYSTEM OF COMMITMENT  (BP13)                               *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE   : OUTPUT REPORT FOR DBSS                      *            
      *                                                            *            
      *  INPUT FILES : 1.  BM06F120  -                             *            
      *                2.  BP13K800  -                             *            
      *                3.  BP13K820  -                             *            
      *                4.  BP13K830  -                             *            
      *                                                            *            
      *  REPORT      : 1.  BP13L800  -                             *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF  BY    DATE    DESCRIPTION                         *            
      * -------- ---- -------- -----------                         *            
      * BP133505 JB8  20090106 NEW PROGRAM.                        *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
      *-------------------------------------------------------------            
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06F120  ASSIGN        TO BM06F120.                          
                                                                                
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-REGN                      
                            FILE STATUS   IS WS-K800-STATUS.                    
                                                                                
           SELECT BP13K820  ASSIGN        TO BP13K820                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K820-KEY-FLD                       
                            FILE STATUS   IS WS-K820-STATUS.                    
                                                                                
           SELECT BP13K830  ASSIGN        TO BP13K830                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K830-KEY-FLD                       
                            ALTERNATE KEY IS K830-NUM-REGN                      
                            FILE STATUS   IS WS-K830-STATUS.                    
                                                                                
           SELECT BP13L800  ASSIGN        TO BP13L800.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BM06F120                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 700 CHARACTERS.                                     
       COPY BM06F120.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K830.                                                           
                                                                                
       FD   BP13L800                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 550 CHARACTERS.                                     
       01   BP13L800-REC                     PIC X(550).                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-STATUS.                                                           
           05  WS-K800-STATUS               PIC 99    VALUE ZEROS.              
           05  WS-K820-STATUS               PIC 99    VALUE ZEROS.              
           05  WS-K830-STATUS               PIC 99    VALUE ZEROS.              
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F120-READ-CTR             PIC 9(06)  VALUE ZEROS.             
           05  WS-K800-NOTFND               PIC 9(06)  VALUE ZEROS.             
           05  WS-K820-NOTFND               PIC 9(06)  VALUE ZEROS.             
           05  WS-K830-NOTFND               PIC 9(06)  VALUE ZEROS.             
           05  WS-L800-WRITE                PIC 9(06)  VALUE ZEROS.             
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-EOF-F120                  PIC X(1)  VALUE 'N'.                
                                                                                
      *---------------*                                                         
      * REPORT LAYOUT                                                           
      *---------------*                                                         
       01  BP13L800-HEADER.                                                     
           05  FILLER                   PIC X(08) VALUE 'REGN'.                 
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(14) VALUE 'SCH-ACC'.              
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(09) VALUE 'DBSS-ELIG'.            
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(51) VALUE 'PIDB ADDRESS'.         
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  FILLER                  PIC X(105) VALUE 'CORR ADDRESS'.         
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(09) VALUE 'NRIC1'.                
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(66) VALUE 'NAME1'.                
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(09) VALUE 'NRIC2'.                
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(66) VALUE 'NAME2'.                
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(09) VALUE 'NRIC3'.                
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(66) VALUE 'NAME3'.                
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(09) VALUE 'NRIC4'.                
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(66) VALUE 'NAME4'.                
           05  FILLER                   PIC X(39) VALUE SPACES.                 
                                                                                
       01  BP13L800-DASH.                                                       
           05  FILLER                   PIC X(08) VALUE ALL '-'.                
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(14) VALUE ALL '-'.                
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(09) VALUE ALL '-'.                
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(51) VALUE ALL '-'.                
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(105) VALUE ALL '-'.               
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(09) VALUE ALL '-'.                
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(66) VALUE ALL '-'.                
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(09) VALUE ALL '-'.                
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(66) VALUE ALL '-'.                
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(09) VALUE ALL '-'.                
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(66) VALUE ALL '-'.                
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(09) VALUE ALL '-'.                
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(66) VALUE ALL '-'.                
           05  FILLER                   PIC X(39) VALUE SPACES.                 
                                                                                
       01  BP13L800-DETAIL.                                                     
           05  L800-NUM-REGN            PIC X(08) VALUE SPACES.                 
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  L800-NUM-SCH-ACC         PIC X(14) VALUE SPACES.                 
           05  FILLER                   PIC X(06) VALUE SPACES.                 
           05  L800-NUM-DBSS-ELGBTY     PIC X(01) VALUE SPACES.                 
           05  FILLER                   PIC X(06) VALUE SPACES.                 
           05  L800-PIDB-ADDRESS        PIC X(51) VALUE SPACES.                 
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  L800-CORR-ADDRESS        PIC X(105) VALUE SPACES.                
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  L800-NUM-NRIC1           PIC X(09) VALUE SPACES.                 
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  L800-NME-NRIC1           PIC X(66) VALUE SPACES.                 
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  L800-NUM-NRIC2           PIC X(09) VALUE SPACES.                 
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  L800-NME-NRIC2           PIC X(66) VALUE SPACES.                 
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  L800-NUM-NRIC3           PIC X(09) VALUE SPACES.                 
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  L800-NME-NRIC3           PIC X(66) VALUE SPACES.                 
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  L800-NUM-NRIC4           PIC X(09) VALUE SPACES.                 
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  L800-NME-NRIC4           PIC X(66) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE SPACES.                 
                                                                                
      *=============================================================            
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-FILES             THRU 1000-EXIT.                  
           PERFORM 3000-PROCESS-MATCH          THRU 3000-EXIT                   
             UNTIL WS-EOF-F120 = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE          THRU 9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-FILES.                                                         
      *=============================================================            
           OPEN INPUT BP13K800                                                  
                      BP13K820                                                  
                      BM06F120                                                  
                      BP13K830                                                  
               OUTPUT BP13L800.                                                 
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K800 STATUS ' WS-K800-STATUS               
              MOVE     WS-K800-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE.                                       
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K820 STATUS ' WS-K820-STATUS               
              MOVE     WS-K820-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE.                                       
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K830 STATUS ' WS-K830-STATUS               
              MOVE     WS-K830-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE.                                       
                                                                                
           WRITE BP13L800-REC FROM BP13L800-HEADER.                             
           WRITE BP13L800-REC FROM BP13L800-DASH.                               
           PERFORM 2000-READ-BM06F120  THRU 2000-EXIT.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       2000-READ-BM06F120.                                                      
      *=============================================================            
                                                                                
            READ BM06F120 AT END                                                
                 MOVE HIGH-VALUES TO F120-REGN-NO                               
                 MOVE 'Y'         TO WS-EOF-F120                                
                 GO TO 2000-EXIT.                                               
                                                                                
            ADD  1  TO WS-F120-READ-CTR.                                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       3000-PROCESS-MATCH.                                                      
      *=============================================================            
                                                                                
           IF F120-REGN-NO NOT = SPACES AND LOW-VALUES                          
              PERFORM 3001-INITIALIZE-BP13L800   THRU 3001-EXIT                 
              PERFORM 3100-READ-BP13K800         THRU 3100-EXIT                 
              IF K800-NUM-NRIC1 NOT = SPACES AND LOW-VALUES                     
                 MOVE F120-REGN-NO         TO K820-NUM-REGN                     
                 MOVE K800-NUM-NRIC1       TO K820-NUM-NRIC                     
                 PERFORM 3200-READ-BP13K820      THRU 3200-EXIT                 
                 MOVE K820-NUM-NRIC        TO L800-NUM-NRIC1                    
                 MOVE K820-NME-OCCP        TO L800-NME-NRIC1                    
              END-IF                                                            
              IF K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES                     
                 MOVE F120-REGN-NO         TO K820-NUM-REGN                     
                 MOVE K800-NUM-NRIC2       TO K820-NUM-NRIC                     
                 PERFORM 3200-READ-BP13K820      THRU 3200-EXIT                 
                 MOVE K820-NUM-NRIC        TO L800-NUM-NRIC2                    
                 MOVE K820-NME-OCCP        TO L800-NME-NRIC2                    
              END-IF                                                            
              IF K800-NUM-NRIC3 NOT = SPACES AND LOW-VALUES                     
                 MOVE F120-REGN-NO         TO K820-NUM-REGN                     
                 MOVE K800-NUM-NRIC3       TO K820-NUM-NRIC                     
                 PERFORM 3200-READ-BP13K820      THRU 3200-EXIT                 
                 MOVE K820-NUM-NRIC        TO L800-NUM-NRIC3                    
                 MOVE K820-NME-OCCP        TO L800-NME-NRIC3                    
              END-IF                                                            
              IF K800-NUM-NRIC4 NOT = SPACES AND LOW-VALUES                     
                 MOVE F120-REGN-NO         TO K820-NUM-REGN                     
                 MOVE K800-NUM-NRIC4       TO K820-NUM-NRIC                     
                 PERFORM 3200-READ-BP13K820      THRU 3200-EXIT                 
                 MOVE K820-NUM-NRIC        TO L800-NUM-NRIC4                    
                 MOVE K820-NME-OCCP        TO L800-NME-NRIC4                    
              END-IF                                                            
              PERFORM 3300-READ-BP13K830         THRU 3300-EXIT                 
              PERFORM 4000-OUTPUT-BP13L800       THRU 4000-EXIT                 
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BM06F120            THRU 2000-EXIT.                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       3001-INITIALIZE-BP13L800.                                                
      *=============================================================            
                                                                                
           MOVE SPACES                     TO BP13L800-DETAIL.                  
           INITIALIZE                         BP13L800-DETAIL.                  
           STRING F120-SCH-ACC-NO(1:4) '-'                                      
                  F120-SCH-ACC-NO(5:4) '-'                                      
                  F120-SCH-ACC-NO(9:1) '-'                                      
                  F120-SCH-ACC-NO(10:2)                                         
              DELIMITED BY SIZE INTO L800-NUM-SCH-ACC.                          
           STRING 'BLK ' F120-BLK-NO                                            
                  ' #'  F120-LEVEL-NO                                           
                  ' -'  F120-MAIN-UNIT                                          
                  ' '   F120-STREET-NAME                                        
              DELIMITED BY SIZE INTO L800-PIDB-ADDRESS.                         
           MOVE F120-REGN-NO               TO L800-NUM-REGN.                    
                                                                                
       3001-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       3100-READ-BP13K800.                                                      
      *=============================================================            
                                                                                
           MOVE SPACES                   TO  BP13K800-MASTER.                   
           INITIALIZE                        BP13K800-MASTER.                   
                                                                                
           MOVE F120-REGN-NO             TO  K800-NUM-REGN.                     
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = 00                                               
              MOVE K800-NUM-ELIG-TAG     TO L800-NUM-DBSS-ELGBTY                
           ELSE                                                                 
              IF WS-K800-STATUS = 23                                            
                 ADD 1 TO WS-K800-NOTFND                                        
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K800 = ' WS-K800-STATUS             
                         ', K800-KEY-FLD = ' K800-NUM-REGN                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       3200-READ-BP13K820.                                                      
      *=============================================================            
                                                                                
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-K820-STATUS = 23                                            
                 ADD 1 TO WS-K820-NOTFND                                        
                 MOVE SPACES             TO  BP13K820-REC                       
                 INITIALIZE                  BP13K820-REC                       
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K820 = ' WS-K820-STATUS             
                         ', K820-KEY-FLD = ' K820-KEY-FLD                       
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       3300-READ-BP13K830.                                                      
      *=============================================================            
                                                                                
           MOVE SPACES                   TO  BP13K830-REC.                      
           INITIALIZE                        BP13K830-REC.                      
                                                                                
           MOVE F120-REGN-NO             TO  K830-KEY-FLD.                      
                                                                                
           READ BP13K830.                                                       
                                                                                
           IF WS-K830-STATUS = 00                                               
              IF (K830-NUM-MAIL-ADDR =  SPACES OR LOW-VALUES) OR                
                 ((K830-NUM-MAIL-LVL-CHAR =  '#') AND                           
                 (K830-NUM-MAIL-LVL-NUM =  SPACES OR LOW-VALUES))               
                 STRING K830-NUM-BLK(1:10) ' '                                  
                        K830-NUM-FLAT   ' '                                     
                        K830-NME-STREET-BLDG                                    
                        ' SINGAPORE ' K830-NUM-POSTAL                           
                    DELIMITED BY SIZE INTO L800-CORR-ADDRESS                    
              ELSE                                                              
                 STRING K830-NUM-MAIL-BLK(1:10) ' '                             
                        K830-NUM-MAIL-FLAT    ' '                               
                        K830-NME-MAIL-STR-BLDG                                  
                        ' SINGAPORE ' K830-NUM-MAIL-POSTAL                      
                    DELIMITED BY SIZE INTO L800-CORR-ADDRESS                    
              END-IF                                                            
           ELSE                                                                 
              IF WS-K830-STATUS = 23                                            
                 ADD 1 TO WS-K830-NOTFND                                        
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K830 = ' WS-K830-STATUS             
                         ', K830-KEY-FLD = ' K830-KEY-FLD                       
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       4000-OUTPUT-BP13L800.                                                    
      **************************************************************            
                                                                                
           WRITE BP13L800-REC FROM BP13L800-DETAIL.                             
           ADD 1 TO WS-L800-WRITE.                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       9000-CLOSE-ROUTINE.                                                      
      **************************************************************            
           DISPLAY ' '.                                                         
           DISPLAY ' ====  BP13CD57 CONTROL TOTALS  ======'.                    
           DISPLAY ' '.                                                         
           DISPLAY ' F120 RECORDS READ          : ' WS-F120-READ-CTR.           
           DISPLAY ' K800 RECORDS NOT FOUND     : ' WS-K800-NOTFND.             
           DISPLAY ' K820 RECORDS NOT FOUND     : ' WS-K820-NOTFND.             
           DISPLAY ' K830 RECORDS NOT FOUND     : ' WS-K830-NOTFND.             
           DISPLAY ' L800 RECORDS WRITE         : ' WS-L800-WRITE.              
           DISPLAY ' ====================================='.                    
                                                                                
           CLOSE BP13K800                                                       
                 BM06F120                                                       
                 BP13K820                                                       
                 BP13K830                                                       
                 BP13L800.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR BP13K800-STAT ' WS-K800-STATUS.            
                                                                                
           IF WS-K820-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR BP13K820-STAT ' WS-K820-STATUS.            
                                                                                
           IF WS-K830-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR BP13K830-STAT ' WS-K830-STATUS.            
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
