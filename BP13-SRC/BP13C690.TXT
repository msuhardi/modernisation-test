       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C690.                                                 
      *AUTHOR.        EVM.                                                      
      *DATE-WRITTEN.  20/12/96.                                                 
      * ====================================================== *                
      *  SYSTEM OF COMMITMENT (BP13)                           *                
      * ====================================================== *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *    1.   PROGRAM TO MATCH SOC UIN WITH UIN OF DEBARMENT *                
      *         RECORD, IF FOUND AS CODE '62', '64', '66'      *                
      *         OUTPUT UIN FOR UPDATE TO UPGRADER IN BP13U705  *                
      *                                                        *                
      *    2.   PROGRAM TO F558-NUM-HDB-REF-DEBAR, F558-DTE-   *                
      *         DEBAR-EFF TO P13F558C WHEN F010-REASON-CODE IS *                
      *         'CA', 'CB', 'CC', 'CD', 'CE', 'CF', 'CH','CI'  *                
      *         & 'CJ'                                         *                
      *                                                        *                
      *    INPUT FILES :                                       *                
      *    1.   BP13F558                                       *                
      *    2.   P04K010                                        *                
      *                                                        *                
      *    OUTPUT FILES :                                      *                
      *    1.   P13F558A                                       *                
      *    2.   P13F558B                                       *                
      *    3.   P13F558C                                       *                
      *                                                        *                
      * CHG-NO.  BY  ON       DESCRIPTION                      *                
      * -------- --- -------- -----------                      *                
      * BP130214 EVM 19960725 NEW PROGRAM                      *                
      * BP130445 SSS 19980205 EXPAND F558 RECL FROM 150 TO 400 *                
      * BP130402 LSB 19980225 CHG P04K010 LAYOUT, OUTPUT DEBAR *                
      *                       REASON TO BP13F558               *                
      * BP130458 LSB 19980305 NOT MATCH ALSO OUTPUT REC        *                
      * BP130494 LSB 19980611 ADD GA, GB CODE                  *                
      * BP130542 LSB 19980811 ADD Z1,Z2,Z5,Z6,ZA,ZC            *                
      *                       ADD DET-DEBAR-EFF                *                
      * BP130645 SRA 19990415 WRITE TO P13F558A AS PER THE     *                
      *                       INCLUSION OF F558-NUM-DEBARMENT1 *                
      *                       DEBARMENT2, DEBARMENT3           *                
      * BP130788 MCC 20000127 ADD 20,50,51,52,70,71,73,40,41,48*                
      *                       WRITE TO P13F558D AND P13F558E   *                
      * BP130819 SRA 20000310 WHEN DEBARMENT=62,64,66,GA,GB &  *                
      *                       WHEN HH='H' CHGE TO 'G' AND UPD  *                
      *                       P13F558A.                        *                
      * BP130897 AKT 20000803 EXPAND F558 RECL FROM 400 TO 600 *                
      * BP131015 ANC 20010922 WRITE HUDC CASES TO P13F558A     *                
      * BP132059 ANC 20020204 ADD DEBARMENT CODE 4 & 5         *                
      * BP132258 ANC 20030109 INITIALIZE PRIO-TAG2 & PRIO-TAG3 *                
      * BP132387 ANC 20030808 ADD F558-NUM-SUBSD-LOAN          *                
      * BP132577 SK  20040824 TO CATER F558-NUM-SUBSD-LOAN     *                
      * BP135921 FNP 20150723 ADD DEBARMENT CODE 6             *                
      * BP138374 FP6 20200727 GROUP COUNTING OF Z1-Z8 REASON   *                
      *                       CODE FOR SUB-LOAN                *                
      * ====================================================== *                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
      *-------------------------------------------------------------            
       CONFIGURATION SECTION.                                                   
      *-------------------------------------------------------------            
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *-------------------------------------------------------------            
       INPUT-OUTPUT SECTION.                                                    
      *-------------------------------------------------------------            
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F558     ASSIGN TO BP13F558.                              
                                                                                
           SELECT P04K010      ASSIGN TO P04K010.                               
                                                                                
           SELECT P13F558A     ASSIGN TO P13F558A.                              
                                                                                
           SELECT P13F558B     ASSIGN TO P13F558B.                              
                                                                                
           SELECT P13F558C     ASSIGN TO P13F558C.                              
                                                                                
           SELECT P13F558D     ASSIGN TO P13F558D.                              
                                                                                
           SELECT P13F558E     ASSIGN TO P13F558E.                              
                                                                                
                                                                                
      **********************                                                    
       DATA DIVISION.                                                           
      **********************                                                    
                                                                                
      *-------------------------------------------------------------            
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD   BP13F558                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F558.                                                           
                                                                                
       FD   P04K010                                                             
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORD IS STANDARD.                                           
                                                                                
       COPY P04K010.                                                            
                                                                                
       FD   P13F558A                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORD IS STANDARD.                                           
       01   P13F558A-REC                PIC X(600).                             
                                                                                
       FD   P13F558B                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORD IS STANDARD.                                           
       01   P13F558B-REC                PIC X(600).                             
                                                                                
       FD   P13F558C                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORD IS STANDARD.                                           
       01   P13F558C-REC                PIC X(600).                             
                                                                                
       FD   P13F558D                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORD IS STANDARD.                                           
       01   P13F558D-REC                PIC X(600).                             
                                                                                
       FD   P13F558E                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORD IS STANDARD.                                           
       01   P13F558E-REC                PIC X(600).                             
                                                                                
                                                                                
      *-------------------------------------------------------------            
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-K010-STATUS              PIC 9(2)   VALUE ZEROS.                  
       01  WS-F558-EOF                 PIC X(1).                                
           88  F558-EOF                           VALUE 'Y'.                    
       01  WS-K010-EOF                 PIC X(1).                                
           88  K010-EOF                           VALUE 'Y'.                    
                                                                                
       01  WS-CURRENT-DATE             PIC 9(6)   VALUE ZEROS.                  
                                                                                
       01  WS-F558-READ                PIC 9(7)   VALUE ZEROES.                 
       01  WS-K010-READ                PIC 9(7)   VALUE ZEROES.                 
       01  WS-F558A-WRITE              PIC 9(7)   VALUE ZEROES.                 
       01  WS-F558B-WRITE              PIC 9(7)   VALUE ZEROES.                 
       01  WS-F558C-WRITE              PIC 9(7)   VALUE ZEROES.                 
       01  WS-F558D-WRITE              PIC 9(7)   VALUE ZEROES.                 
       01  WS-F558E-WRITE              PIC 9(7)   VALUE ZEROES.                 
       01  WS-CNT-F558-LESS            PIC 9(7)   VALUE ZEROES.                 
       01  WS-NRIC                     PIC X(9)   VALUE SPACES.                 
       01  WS-REASON1                  PIC X(2)   VALUE SPACES.                 
       01  WS-REASON2                  PIC X(2)   VALUE SPACES.                 
       01  WS-REASON3                  PIC X(2)   VALUE SPACES.                 
       01  WS-REASON4                  PIC X(2)   VALUE SPACES.                 
       01  WS-REASON5                  PIC X(2)   VALUE SPACES.                 
       01  WS-REASON6                  PIC X(2)   VALUE SPACES.                 
       01  WS-DEBAR-DATE               PIC X(8)   VALUE SPACES.                 
       01  WS-DEBAR-UPDATE             PIC 9(7)   VALUE ZEROS.                  
       01  WS-SCH-ACCTNO               PIC X(11)  VALUE SPACES.                 
       01  WS-HOUSEHOLD                PIC X(1)   VALUE SPACES.                 
       01  PRIO-DEBAR2-TAG             PIC X(1)   VALUE 'N'.                    
       01  PRIO-DEBAR3-TAG             PIC X(1)   VALUE 'N'.                    
       01  WS-F558-NUM-SUBSD-LOAN      PIC 9(2)   VALUE ZEROES.                 
                                                                                
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
      *-------------------------------------------------------------            
       MAIN-ROUTINE SECTION.                                                    
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13F558   THRU 2000-EXIT.                         
           PERFORM 3000-READ-P04K010    THRU 3000-EXIT.                         
           PERFORM 4000-PROCESS-MATCH   THRU 4000-EXIT                          
                        UNTIL F558-EOF  AND K010-EOF.                           
           PERFORM 9999-CLOSE-RTN THRU 9999-EXIT.                               
                                                                                
       MAIN-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT   BP13F558  P04K010                                       
                OUTPUT  P13F558A                                                
                OUTPUT  P13F558B                                                
                OUTPUT  P13F558C                                                
                OUTPUT  P13F558D                                                
                OUTPUT  P13F558E.                                               
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'P04K010 OPEN ERROR : ' WS-K010-STATUS                    
              PERFORM 9999-CLOSE-RTN          THRU 9999-EXIT.                   
                                                                                
           ACCEPT WS-CURRENT-DATE  FROM DATE.                                   
                                                                                
           MOVE SPACES             TO WS-F558-EOF WS-K010-EOF.                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F558.                                                      
      *-------------------------------------------------------------            
           READ BP13F558 AT END                                                 
                MOVE 'Y'          TO WS-F558-EOF                                
                MOVE HIGH-VALUES  TO F558-NUM-NRIC                              
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F558-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-READ-P04K010.                                                       
      *-------------------------------------------------------------            
           READ P04K010  AT END                                                 
                MOVE 'Y'          TO WS-K010-EOF                                
                MOVE HIGH-VALUES  TO K010-NUM-UINFIN                            
                GO TO 3000-EXIT.                                                
                                                                                
           ADD 1 TO WS-K010-READ.                                               
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-PROCESS-MATCH.                                                      
      *-------------------------------------------------------------            
           IF F558-NUM-NRIC = K010-NUM-UINFIN                                   
              MOVE F558-NUM-NRIC TO WS-NRIC                                     
              MOVE SPACES        TO WS-REASON1 WS-REASON2                       
              MOVE SPACES        TO WS-REASON3 WS-REASON4                       
              MOVE SPACES        TO WS-REASON5 WS-REASON6                       
              MOVE SPACES        TO WS-DEBAR-DATE                               
              MOVE SPACES        TO WS-SCH-ACCTNO                               
              MOVE SPACES        TO WS-HOUSEHOLD                                
              MOVE SPACES        TO P13F558A-REC                                
              MOVE SPACES        TO P13F558B-REC                                
              MOVE SPACES        TO P13F558C-REC                                
              MOVE SPACES        TO P13F558D-REC                                
              MOVE SPACES        TO P13F558E-REC                                
              MOVE SPACES        TO PRIO-DEBAR2-TAG                             
              MOVE SPACES        TO PRIO-DEBAR3-TAG                             
              MOVE 0             TO WS-F558-NUM-SUBSD-LOAN                      
              PERFORM 5000-BROWSE-BP04K010 THRU 5000-EXIT UNTIL                 
                 K010-NUM-UINFIN NOT = WS-NRIC                                  
                 OR K010-NUM-UINFIN = HIGH-VALUES                               
              PERFORM 6000-BROWSE-BP13F558 THRU 6000-EXIT UNTIL                 
                 F558-NUM-NRIC NOT = WS-NRIC                                    
                 OR F558-NUM-NRIC = HIGH-VALUES                                 
           ELSE                                                                 
              IF F558-NUM-NRIC > K010-NUM-UINFIN                                
                 PERFORM 3000-READ-P04K010    THRU 3000-EXIT                    
              ELSE                                                              
                 ADD 1              TO WS-CNT-F558-LESS                         
                 WRITE P13F558A-REC FROM BP13F558-REC                           
                 ADD 1              TO WS-F558A-WRITE                           
                 PERFORM 2000-READ-BP13F558   THRU 2000-EXIT.                   
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-BROWSE-BP04K010.                                                    
      *-------------------------------------------------------------            
      *    IF F558-NUM-SUBSD-LOAN NOT NUMERIC                                   
      *       MOVE ZEROES TO F558-NUM-SUBSD-LOAN                                
      *    END-IF.                                                              
                                                                                
           IF K010-REASON-CODE = 'Z1' OR 'Z2' OR 'Z3' OR 'Z4' OR                
                                 'Z5' OR 'Z6' OR 'Z7' OR 'Z8'                   
              ADD 1  TO WS-F558-NUM-SUBSD-LOAN                                  
           END-IF.                                                              
                                                                                
           IF  K010-REASON-CODE = 'Z1' OR 'Z2' OR 'Z5' OR 'Z6'                  
                                           OR  'ZA' OR  'ZC'                    
      *        IF (K010-REASON-CODE = 'Z1' OR 'Z2' OR 'Z5' OR 'Z6')             
      *           ADD 1  TO WS-F558-NUM-SUBSD-LOAN                              
      *        END-IF                                                           
               MOVE K010-REASON-CODE   TO WS-REASON1                            
           ELSE                                                                 
              IF K010-REASON-CODE  = 'CA' OR 'CB' OR 'CC' OR 'CD' OR            
                                   'CE' OR 'CF' OR 'CH' OR 'CI' OR 'CJ'         
                                        OR 'Z3' OR 'Z4' OR 'ZB' OR 'ZD'         
      *          IF (K010-REASON-CODE = 'Z3' OR 'Z4')                           
      *              ADD 1  TO WS-F558-NUM-SUBSD-LOAN                           
      *          END-IF                                                         
                 IF WS-REASON2 = 'Z3' OR 'Z4' OR 'ZB' OR 'ZD'                   
                    MOVE 'Y'                TO PRIO-DEBAR2-TAG                  
                    MOVE K010-REASON-CODE   TO WS-REASON2                       
                 END-IF                                                         
                 IF PRIO-DEBAR2-TAG NOT = 'Y'                                   
                    MOVE K010-REASON-CODE   TO WS-REASON2                       
                 END-IF                                                         
                 MOVE K010-EFF-DATE      TO WS-DEBAR-DATE                       
                 MOVE K010-EX-SCH-ACCTNO TO WS-SCH-ACCTNO(1:9)                  
                 MOVE K010-LSE-TEN-CODE  TO WS-SCH-ACCTNO(10:2)                 
              ELSE                                                              
                 IF K010-REASON-CODE = 'PA' OR 'PB' OR 'PC' OR 'PD' OR          
                                       'PE' OR 'PF' OR 'QA' OR 'QB' OR          
                                       'QC' OR 'QD' OR 'QE' OR 'QF'             
                    MOVE K010-REASON-CODE   TO WS-REASON3                       
                    MOVE K010-EFF-DATE      TO WS-DEBAR-DATE                    
                    MOVE K010-EX-SCH-ACCTNO TO WS-SCH-ACCTNO(1:9)               
                    MOVE K010-LSE-TEN-CODE  TO WS-SCH-ACCTNO(10:2)              
                    MOVE 'Y'                TO PRIO-DEBAR3-TAG                  
                 ELSE                                                           
                    IF K010-REASON-CODE = '62' OR '64' OR '66' OR               
                                          'GA' OR 'GB'                          
                       IF F558-CDE-HOUSEHOLD = 'H'                              
                          MOVE 'G' TO WS-HOUSEHOLD                              
                       END-IF                                                   
                       MOVE K010-REASON-CODE   TO WS-REASON4                    
                       MOVE K010-EFF-DATE      TO WS-DEBAR-DATE                 
                       MOVE K010-EX-SCH-ACCTNO TO WS-SCH-ACCTNO(1:9)            
                       MOVE K010-LSE-TEN-CODE  TO WS-SCH-ACCTNO(10:2)           
                    ELSE                                                        
                       IF K010-REASON-CODE = 'SF' OR 'VC'                       
                          MOVE K010-REASON-CODE   TO WS-REASON5                 
                          MOVE K010-EFF-DATE      TO WS-DEBAR-DATE              
                          MOVE K010-EX-SCH-ACCTNO TO WS-SCH-ACCTNO(1:9)         
                          MOVE K010-LSE-TEN-CODE  TO WS-SCH-ACCTNO(10:2)        
                       ELSE                                                     
                       IF K010-REASON-CODE = '33'                               
                          MOVE K010-REASON-CODE   TO WS-REASON6                 
                          MOVE K010-EFF-DATE      TO WS-DEBAR-DATE              
                          MOVE K010-EX-SCH-ACCTNO TO WS-SCH-ACCTNO(1:9)         
                          MOVE K010-LSE-TEN-CODE  TO WS-SCH-ACCTNO(10:2)        
                       ELSE                                                     
                       IF PRIO-DEBAR3-TAG = 'Y'                                 
                          CONTINUE                                              
                       ELSE                                                     
                          MOVE K010-REASON-CODE   TO WS-REASON3                 
                          MOVE K010-EFF-DATE      TO WS-DEBAR-DATE              
                          MOVE K010-EX-SCH-ACCTNO TO WS-SCH-ACCTNO(1:9)         
                          MOVE K010-LSE-TEN-CODE  TO WS-SCH-ACCTNO(10:2)        
                          MOVE 'N'                TO PRIO-DEBAR3-TAG            
      *                   IF (K010-REASON-CODE = 'Z7' OR 'Z8')                  
      *                       ADD 1  TO WS-F558-NUM-SUBSD-LOAN                  
      *                   END-IF                                                
                       END-IF                                                   
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF  K010-REASON-CODE = '20' OR '50' OR '51' OR '52'                  
                                       OR '70' OR '71' OR '73'                  
              MOVE K010-REASON-CODE    TO   F558-NUM-DEBARMENT-3                
              MOVE K010-EFF-DATE       TO   F558-DTE-DEBAR-EFF                  
              MOVE K010-EX-SCH-ACCTNO  TO   WS-SCH-ACCTNO(1:9)                  
              MOVE K010-LSE-TEN-CODE   TO   WS-SCH-ACCTNO(10:2)                 
              MOVE WS-SCH-ACCTNO       TO   F558-NUM-HDB-REF-DEBAR              
              WRITE P13F558D-REC       FROM BP13F558-REC                        
              ADD 1                    TO   WS-F558D-WRITE                      
              ADD 1                    TO   WS-DEBAR-UPDATE                     
           ELSE                                                                 
              IF K010-REASON-CODE  = '40' OR '41' OR '48'                       
                 MOVE K010-REASON-CODE    TO   F558-NUM-DEBARMENT-3             
                 MOVE K010-EFF-DATE       TO   F558-DTE-DEBAR-EFF               
                 MOVE K010-EX-SCH-ACCTNO  TO   WS-SCH-ACCTNO(1:9)               
                 MOVE K010-LSE-TEN-CODE   TO   WS-SCH-ACCTNO(10:2)              
                 MOVE WS-SCH-ACCTNO       TO   F558-NUM-HDB-REF-DEBAR           
                 WRITE P13F558E-REC       FROM BP13F558-REC                     
                 ADD 1                    TO   WS-F558E-WRITE                   
                 ADD 1                    TO   WS-DEBAR-UPDATE                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-P04K010    THRU 3000-EXIT.                         
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-BROWSE-BP13F558.                                                    
      *-------------------------------------------------------------            
           IF F558-NUM-NRIC  = WS-NRIC                                          
              MOVE WS-REASON1    TO F558-NUM-DEBARMENT-1                        
              MOVE WS-REASON2    TO F558-NUM-DEBARMENT-2                        
              MOVE WS-REASON3    TO F558-NUM-DEBARMENT-3                        
              MOVE WS-REASON4    TO F558-NUM-DEBARMENT-4                        
              MOVE WS-REASON5    TO F558-NUM-DEBARMENT-5                        
              MOVE WS-REASON6    TO F558-NUM-DEBARMENT-6                        
              MOVE WS-F558-NUM-SUBSD-LOAN                                       
                                 TO F558-NUM-SUBSD-LOAN                         
                                                                                
              IF WS-REASON4 = '62' OR '64' OR '66' OR 'GA' OR 'GB'              
                 IF WS-HOUSEHOLD = 'G'                                          
                    MOVE WS-HOUSEHOLD TO F558-CDE-HOUSEHOLD                     
                 END-IF                                                         
              END-IF                                                            
              MOVE WS-DEBAR-DATE TO F558-DTE-DEBAR-EFF                          
              MOVE WS-SCH-ACCTNO TO F558-NUM-HDB-REF-DEBAR                      
              WRITE P13F558A-REC FROM BP13F558-REC                              
              ADD 1              TO WS-F558A-WRITE                              
              ADD 1              TO WS-DEBAR-UPDATE                             
                                                                                
              IF WS-REASON5 = 'VC'                                              
                 MOVE WS-REASON5    TO F558-NUM-DEBARMENT-5                     
                 MOVE WS-DEBAR-DATE TO F558-DTE-DEBAR-EFF                       
                 MOVE WS-SCH-ACCTNO TO F558-NUM-HDB-REF-DEBAR                   
                 WRITE P13F558B-REC FROM BP13F558-REC                           
                 ADD 1              TO WS-F558B-WRITE                           
                 ADD 1              TO WS-DEBAR-UPDATE                          
              END-IF                                                            
              IF WS-REASON3 = 'PA' OR 'PB' OR 'PC' OR 'PD' OR                   
                              'PE' OR 'PF' OR 'QA' OR 'QB' OR                   
                              'QC' OR 'QD' OR 'QE' OR 'QF'                      
                 MOVE WS-REASON3  TO F558-NUM-DEBARMENT-3                       
                 WRITE P13F558C-REC FROM BP13F558-REC                           
                 ADD 1              TO WS-F558C-WRITE                           
                 ADD 1              TO WS-DEBAR-UPDATE                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F558   THRU 2000-EXIT.                         
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9999-CLOSE-RTN.                                                          
      *-------------------------------------------------------------            
           DISPLAY 'PROGRAM : BP13C690    '.                                    
           DISPLAY 'DATE    : ' WS-CURRENT-DATE.                                
           DISPLAY '*=========== COUNTERS ===========*'                         
           DISPLAY 'F558-REC-READ     : ' WS-F558-READ.                         
           DISPLAY 'K010-REC-READ     : ' WS-K010-READ.                         
           DISPLAY 'F558A-OUTPUT      : ' WS-F558A-WRITE.                       
           DISPLAY 'F558B-OUTPUT      : ' WS-F558B-WRITE.                       
           DISPLAY 'F558C-OUTPUT      : ' WS-F558C-WRITE.                       
           DISPLAY 'F558D-OUTPUT      : ' WS-F558D-WRITE.                       
           DISPLAY 'F558E-OUTPUT      : ' WS-F558E-WRITE.                       
           DISPLAY 'F558A-LESS        : ' WS-CNT-F558-LESS.                     
           DISPLAY 'DEBAR-CDE UPDATE  : ' WS-DEBAR-UPDATE.                      
                                                                                
           CLOSE BP13F558  P04K010                                              
                 P13F558A                                                       
                 P13F558B                                                       
                 P13F558C                                                       
                 P13F558D                                                       
                 P13F558E.                                                      
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'P04K010 CLOSE ERROR : ' WS-K010-STATUS.                  
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
