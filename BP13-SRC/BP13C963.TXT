      *--------------------------------------------------------*                
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------*                
       PROGRAM-ID.    BP13C963.                                                 
      *AUTHOR. SK.                                                              
      *DATE-WRITTEN.  31/05/2003.                                               
                                                                                
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE  : DETAILS OF FLAT                        *                
      *     INPUT FILES:                                       *                
      *           1. BM06K110                                  *                
      *           2. BM06K120                                  *                
      *                                                        *                
      *     OUTPUT FILE:                                       *                
      *           1. BP13F100                                  *                
      *                                                        *                
      * CHG-NO   BY    DATE     DETAILS                        *                
      * -------- ----  -------- -------------------------------*                
      * BP132360 SK    31072003 NEW PGM                        *                
      * BP134076 IL5   12112010 MOVE FIELDS NUM-SELECTION AND  *                
      *                         NUM-ALLOCN-QTR TO BP13F100     *                
      **********************************************************                
                                                                                
      *--------------------------------------------------------*                
       ENVIRONMENT DIVISION.                                                    
      *--------------------------------------------------------*                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06F110 ASSIGN TO BM06F110.                                  
           SELECT BM06F120 ASSIGN TO BM06F120.                                  
           SELECT BP13F100 ASSIGN TO BP13F100.                                  
                                                                                
      *--------------------------------------------------------*                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------*                
                                                                                
       FD  BM06F110                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BM06F110.                                                           
                                                                                
       FD  BM06F120                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 700 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BM06F120.                                                           
                                                                                
       FD  BP13F100                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 800 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F100.                                                           
                                                                                
      *--------------------------------------------------------*                
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------*                
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F110-EOF             PIC X       VALUE 'N'.                   
           05  WS-F120-EOF             PIC X       VALUE 'N'.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-CNT-F110-READ         PIC 9(8)    VALUE 0.                     
           05 WS-CNT-F120-READ         PIC 9(8)    VALUE 0.                     
           05 WS-CNT-F100-WRITE        PIC 9(8)    VALUE 0.                     
                                                                                
           05 WS-MATCH                 PIC 9(8)    VALUE 0.                     
           05 WS-NOT-MATCH             PIC 9(8)    VALUE 0.                     
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY             PIC 9(4).                                
           05  WS-SYS-MM               PIC 9(2).                                
           05  WS-SYS-DD               PIC 9(2).                                
                                                                                
       01  WS-EDITED-DATE              PIC X(10).                               
                                                                                
      *--------------------------------------------------------*                
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------*                
                                                                                
       0000-MAIN.                                                               
                                                                                
           PERFORM 1000-OPEN-FILE       THRU 1000-EXIT.                         
           PERFORM 1100-READ-F110       THRU 1100-EXIT.                         
           PERFORM 1200-READ-F120       THRU 1200-EXIT.                         
           PERFORM 2000-PROCESS         THRU 2000-EXIT                          
                   UNTIL WS-F110-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       1000-OPEN-FILE.                                                          
      *--------------------------------------------------------*                
                                                                                
           OPEN INPUT  BM06F110                                                 
                       BM06F120                                                 
                OUTPUT BP13F100.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATE.                      
           STRING WS-SYS-CCYY '/'                                               
                  WS-SYS-MM   '/'                                               
                  WS-SYS-DD                                                     
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE.                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       1100-READ-F110.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ BM06F110                                                        
                AT END  MOVE 'Y' TO WS-F110-EOF                                 
                GO TO 1100-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-CNT-F110-READ.                                           
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       1200-READ-F120.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ BM06F120                                                        
                AT END  MOVE 'Y' TO WS-F120-EOF                                 
                GO TO 1200-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-CNT-F120-READ.                                           
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2000-PROCESS.                                                            
      *--------------------------------------------------------*                
                                                                                
           IF F110-SCH-ACC-NO = F120-SCH-ACC-NO                                 
              ADD 1 TO  WS-MATCH                                                
              PERFORM 2100-WRITE-F100     THRU 2100-EXIT                        
              PERFORM 1100-READ-F110      THRU 1100-EXIT                        
              PERFORM 1200-READ-F120      THRU 1200-EXIT                        
           ELSE                                                                 
              IF F110-SCH-ACC-NO < F120-SCH-ACC-NO                              
                 DISPLAY ' SCH ACC NUMBER : '  F110-SCH-ACC-NO                  
                 ADD 1 TO WS-NOT-MATCH                                          
                 PERFORM 1100-READ-F110 THRU 1100-EXIT                          
              ELSE                                                              
                 PERFORM 1200-READ-F120 THRU 1200-EXIT                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2100-WRITE-F100.                                                         
      *--------------------------------------------------------*                
           MOVE SPACES                   TO BP13F100-REC.                       
           MOVE F110-SCH-ACC-NO          TO F100-NUM-HDB-REF.                   
           MOVE SPACES                   TO F100-NUM-RANDOM.                    
           MOVE F110-ESTATE              TO F100-NUM-ESTATE.                    
           MOVE F110-NEIGHBOURHOOD       TO F100-NUM-NGHBRD.                    
           MOVE F110-CONTRACT-NO         TO F100-NUM-CNTRCT.                    
           MOVE F110-BLK-NO              TO F100-NUM-BLOCK.                     
           MOVE F110-NUM-WARD            TO F100-NUM-WARD.                      
           MOVE F110-NUM-SELECTION       TO F100-NUM-ALLO-CAT.                  
           MOVE F110-NUM-ALLOCN-QTR      TO F100-DTE-BALLOT.                    
           MOVE F120-CDE-TYP-ACTUSE      TO F100-NUM-TYP-ACTUSE.                
           MOVE F120-CDE-MODL-DESG       TO F100-NUM-MODL-DESG.                 
           MOVE F120-STREET-NAME         TO F100-NME-STREET.                    
           MOVE F120-UNIT-NO             TO F100-NUM-UNIT.                      
           MOVE F120-NUM-NT              TO F100-NUM-NT.                        
                                                                                
           WRITE BP13F100-REC.                                                  
           ADD   1                       TO WS-CNT-F100-WRITE.                  
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       9000-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------*                
                                                                                
           CLOSE BM06F110                                                       
                 BM06F120                                                       
                 BP13F100.                                                      
                                                                                
           DISPLAY '******  BP13C963 *************'.                            
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY SPACE.                                                       
           DISPLAY 'NO. READ (BM06F110) : ' WS-CNT-F110-READ.                   
           DISPLAY 'NO. READ (BM06F120) : ' WS-CNT-F120-READ.                   
           DISPLAY 'NO. WRITE(BP13F100) : ' WS-CNT-F100-WRITE.                  
           DISPLAY SPACE.                                                       
           DISPLAY 'NO OF MATCH           : ' WS-MATCH.                         
           DISPLAY 'NO OF NOT MATCH       : ' WS-NOT-MATCH.                     
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
      *                         END OF PROGRAM                                  
      *--------------------------------------------------------------*          
