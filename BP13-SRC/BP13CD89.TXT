       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD89.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  22/08/2011.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      * OBJECTIVE    :  LOAN CONVERSION AFTER LOAN REDUCTION        *           
      *                 USED BY BP13M712                            *           
      *                                                             *           
      * INPUT FILES  :  1.  BP13F800                                *           
      *                 2.  SY02F001                                *           
      *                                                             *           
      * I-O FILES    :  1.  BP13K800 - SOC APPLICATION MASTER       *           
      *                 2.  BP13K482 - 2ND CONC LOAN BUY 1ST        *           
      *                 3.  BP13K640 - CA FILE                      *           
      *                 4.  BP13KD00 - DBSS MASTER                  *           
      *                 5.  BP13KD60 - STATEMENT ACCOUNT            *           
      *                                                             *           
      * OUTPUT       :  1.  BP13LD89 - REPORT                       *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG REF  OIC  DATE     DESCRIPTION                          *           
      * -------- ---  -------- ------------------------------------ *           
      * BP134029 JB8  20110822 NEW PROGRAM                          *           
      * BP134040 JB8  20111018 TO CATER FOR LOAN-CONVERSION DATE    *           
      * BP134457 AB9  20120302 CHANGE WS-CNVRSN-VALID LOGIC         *           
      * BP134269 AB9  20120306 REMOVE COND FOR REGN='5036489E'      *           
      * BP134243 AB9  20120404 BY PASS '5028558H',5032498B',5033407D*           
      * BP134527 AB9  20120504 REMOVE CONDN FOR REGN '5028558H'     *           
      *                                    '5033407D''5032498B'     *           
      * BP134422 AB9  20120803 CATER FOR REGN '5032354H','5034950J' *           
      *                                       '5039807B','5033215B' *           
      * BP134422 AB9  20120903 REMOVE CONDN - '5032354H','5034950J' *           
      *                                       '5039807B','5033215B' *           
      * BP134527 IMC  20130509 ADD BP13F136 OUTPUT                  *           
      * BP135126 RJE1 20131126 CHECK FOR K482-CDE-HYBRID = 'S'      *           
      * BP135161 ESD1 20130113 ADD CHECKING FOR LOAN BALANCE > 0    *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT BP13F800  ASSIGN       TO BP13F800.                           
           SELECT SY02F001  ASSIGN       TO SY02F001.                           
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS WS-K800-STATUS.                     
                                                                                
           SELECT BP13KD00  ASSIGN       TO BP13KD00                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS KD00-KEY-FLD                        
                            FILE STATUS  IS WS-KD00-STATUS.                     
                                                                                
           SELECT BP13KD60  ASSIGN       TO BP13KD60                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS KD60-KEY-FLD                        
                            FILE STATUS  IS WS-KD60-STATUS.                     
                                                                                
           SELECT BP13K482  ASSIGN       TO BP13K482                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K482-KEY-FLD                        
                            FILE STATUS  IS WS-K482-STATUS.                     
                                                                                
           SELECT BP13K640  ASSIGN       TO BP13K640                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K640-KEY-FLD                        
                            FILE STATUS  IS WS-K640-STATUS.                     
                                                                                
           SELECT BP13LD89  ASSIGN       TO BP13LD89.                           
                                                                                
           SELECT BP13F136  ASSIGN       TO BP13F136.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       COPY SY02F001.                                                           
                                                                                
       FD   BP13F800                                                            
            BLOCK  CONTAINS    0  RECORDS                                       
            RECORD CONTAINS 2000 CHARACTERS                                     
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13KD00                                                            
            RECORD CONTAINS 1600 CHARACTERS.                                    
       COPY BP13KD00.                                                           
                                                                                
       FD   BP13KD60                                                            
            RECORD CONTAINS 250  CHARACTERS.                                    
       COPY BP13KD60.                                                           
                                                                                
       FD   BP13K482                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K482.                                                           
                                                                                
       FD   BP13K640                                                            
            RECORD CONTAINS 500  CHARACTERS.                                    
       COPY BP13K640.                                                           
                                                                                
       FD   BP13LD89                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORD IS OMITTED                                             
            RECORDING MODE IS F.                                                
       01   LD89-PRTREC                 PIC X(132).                             
                                                                                
       FD   BP13F136                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 150  CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F136.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-INDICATORS.                                                       
           05  WS-K800-STATUS           PIC 99     VALUE 00.                    
           05  WS-KD00-STATUS           PIC 99     VALUE 00.                    
           05  WS-KD60-STATUS           PIC 99     VALUE 00.                    
           05  WS-K482-STATUS           PIC 99     VALUE 00.                    
           05  WS-K640-STATUS           PIC 99     VALUE 00.                    
                                                                                
       01  WS-COUNTERS-POINTERS.                                                
           05  WS-I                     PIC 9(2)   VALUE ZEROS.                 
           05  CNT-F800-READ            PIC 9(6)   VALUE ZEROS.                 
           05  CNT-K800-REWRITE         PIC 9(6)   VALUE ZEROS.                 
           05  CNT-KD00-REWRITE         PIC 9(6)   VALUE ZEROS.                 
           05  CNT-K482-REWRITE         PIC 9(6)   VALUE ZEROS.                 
           05  CNT-K640-REWRITE         PIC 9(6)   VALUE ZEROS.                 
           05  CNT-KD60-REWRITE         PIC 9(6)   VALUE ZEROS.                 
           05  CNT-LD89-WRITE           PIC 9(6)   VALUE ZEROS.                 
           05  CNT-F136-WRITE           PIC 9(6)   VALUE ZEROS.                 
           05  CNT-SNO                  PIC 9(2)   VALUE ZEROS.                 
           05  CNT-REC-PAGE             PIC 9(6)   VALUE 60.                    
           05  CNT-PAGE                 PIC 9(6)   VALUE ZEROS.                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F800-EOF              PIC X(01)  VALUE SPACES.                
               88  F800-EOF                        VALUE 'Y'.                   
           05  WS-K800-FND              PIC X(01)  VALUE SPACES.                
               88  K800-FND                        VALUE 'Y'.                   
           05  WS-K640-FND              PIC X(01)  VALUE SPACES.                
               88  K640-FND                        VALUE 'Y'.                   
           05  WS-K482-FND              PIC X(01)  VALUE SPACES.                
               88  K482-FND                        VALUE 'Y'.                   
           05  WS-KD00-FND              PIC X(01)  VALUE SPACES.                
               88  KD00-FND                        VALUE 'Y'.                   
           05  WS-KD60-FND              PIC X(01)  VALUE SPACES.                
               88  KD60-FND                        VALUE 'Y'.                   
           05  WS-K482-EOF              PIC X(01)  VALUE SPACES.                
               88  K482-EOF                        VALUE 'Y'.                   
           05  WS-CNVRSN-VALID          PIC X(01)  VALUE SPACES.                
               88  CNVRSN-VALID                    VALUE 'Y'.                   
           05  WS-AMT-UNDISBURSE        PIC 9(07)V99 VALUE ZEROES.              
           05  WS-BS-TAG-OLD            PIC X(01)  VALUE SPACES.                
           05  WS-BS-TAG-NEW            PIC X(01)  VALUE SPACES.                
           05  WS-LOAN-SCH-OLD          PIC X(01)  VALUE SPACES.                
           05  WS-LOAN-SCH-NEW          PIC X(01)  VALUE SPACES.                
                                                                                
       01  WS-DISPOSAL-FLAT.                                                    
           05  WS-FLATA OCCURS 4 TIMES  PIC X(11)  VALUE SPACES.                
           05  CNT-FLATA                PIC 9(02)  VALUE ZEROES.                
                                                                                
      *-------------------------------------------------------------            
      *- REPORT LAYOUT                                                          
      *-------------------------------------------------------------            
       01  LD89-HEADER-01.                                                      
           05  FILLER                   PIC X(107) VALUE                        
               'BP13LD89  HDB3                       S  Y  S  T  E  M           
      -        'O  F   C  O  M  M  I  T  M  E  N  T         DATE : '.           
           05  LD89-DATE                PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE ' PAGE: '.              
           05  LD89-PAGE                PIC ZZ9.                                
                                                                                
       01  LD89-HEADER-02.                                                      
           05  FILLER                   PIC X(42) VALUE SPACES.                 
           05  FILLER                   PIC X(44) VALUE                         
               'LOAN CONVERSION FOR DBSS 2ND CONC LOAN CASES'.                  
                                                                                
       01  LD89-DETAIL-01.                                                      
           05  FILLER                   PIC X(132) VALUE                        
               ' S/N   REGN NO  PURCHASE FLAT  DISPOSAL FLAT      BUY SE        
      -        'LL TAG  LOAN SCH '.                                             
                                                                                
       01  LD89-DETAIL-02.                                                      
           05  FILLER                   PIC X(132) VALUE                        
               ' ===== ======== ============== ================== ======        
      -        '======  ======== '.                                             
                                                                                
       01  LD89-DETAIL-03.                                                      
           05  FILLER                   PIC X(01)  VALUE SPACES.                
           05  LD89-NUM-SNO             PIC ZZZZ9  VALUE ZEROES.                
           05  FILLER                   PIC X(01)  VALUE SPACES.                
           05  LD89-NUM-REGN            PIC X(08)  VALUE SPACES.                
           05  FILLER                   PIC X(01)  VALUE SPACES.                
           05  LD89-PURCHASE-FLAT       PIC X(14)  VALUE SPACES.                
           05  FILLER                   PIC X(01)  VALUE SPACES.                
           05  LD89-DISPOSAL-FLAT       PIC X(14)  VALUE SPACES.                
           05  FILLER                   PIC X(01)  VALUE SPACES.                
           05  LD89-MORE-DISPOSAL       PIC X(03)  VALUE SPACES.                
           05  FILLER                   PIC X(04)  VALUE SPACES.                
           05  LD89-OLD-BS-TAG          PIC X(01)  VALUE SPACES.                
           05  FILLER                   PIC X(03)  VALUE ' / '.                 
           05  LD89-NEW-BS-TAG          PIC X(01)  VALUE SPACES.                
           05  FILLER                   PIC X(08)  VALUE SPACES.                
           05  LD89-OLD-LOAN-SCH        PIC X(01)  VALUE SPACES.                
           05  FILLER                   PIC X(03)  VALUE ' / '.                 
           05  LD89-NEW-LOAN-SCH        PIC X(01)  VALUE SPACES.                
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
            PERFORM 1000-OPEN-FILES-ROUTINE   THRU  1000-EXIT.                  
            PERFORM 2000-READ-BP13F800        THRU  2000-EXIT.                  
            PERFORM 3000-PROCESS-RECORD       THRU  3000-EXIT                   
              UNTIL F800-EOF.                                                   
            PERFORM 9000-CLOSE-ROUTINE        THRU  9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES-ROUTINE.                                                 
      *-------------------------------------------------------------            
            OPEN INPUT  BP13F800                                                
                        SY02F001                                                
                 I-O    BP13K482                                                
                        BP13K640                                                
                        BP13K800                                                
                        BP13KD00                                                
                        BP13KD60                                                
                 OUTPUT BP13LD89                                                
                        BP13F136.                                               
                                                                                
           IF WS-K482-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, WS-K482-STATUS ' WS-K482-STATUS           
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           IF WS-K640-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, WS-K640-STATUS ' WS-K640-STATUS           
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, WS-K800-STATUS ' WS-K800-STATUS           
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           IF WS-KD00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, WS-KD00-STATUS ' WS-KD00-STATUS           
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           IF WS-KD60-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, WS-KD60-STATUS ' WS-KD60-STATUS           
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F800.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F800                                                        
             AT END                                                             
                MOVE 'Y'               TO WS-F800-EOF                           
                GO                     TO 2000-EXIT                             
           END-READ.                                                            
                                                                                
           ADD 1                       TO CNT-F800-READ.                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-RECORD.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                         TO K482-KEY-FLD                  
           MOVE F800-NUM-SCH-ACC               TO K482-NUM-HDBREF-NEW           
           PERFORM 8001-START-BP13K482       THRU 8001-EXIT                     
           IF K482-EOF                                                          
              PERFORM 2000-READ-BP13F800     THRU 2000-EXIT                     
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE 'Y'                                TO WS-CNVRSN-VALID           
           MOVE ZEROES                             TO CNT-FLATA.                
           MOVE SPACES                             TO WS-DISPOSAL-FLAT.         
           INITIALIZE                                 WS-DISPOSAL-FLAT.         
                                                                                
           PERFORM 8002-READ-NEXT-BP13K482       THRU 8002-EXIT                 
           PERFORM UNTIL K482-EOF                                               
              IF K482-NUM-HDBREF-NEW = F800-NUM-SCH-ACC                         
                 ADD 1                           TO CNT-FLATA                   
                 MOVE K482-NUM-HDBREF-DISPOSAL   TO WS-FLATA(CNT-FLATA)         
                 PERFORM 3100-CHECK-CONVERSE     THRU 3100-EXIT                 
                 PERFORM 8002-READ-NEXT-BP13K482 THRU 8002-EXIT                 
              ELSE                                                              
                 MOVE 'Y'                          TO WS-K482-EOF               
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           IF CNVRSN-VALID AND CNT-FLATA > ZEROES                               
              PERFORM 3200-CONVERSE-LOAN-SCH     THRU 3200-EXIT                 
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F800        THRU 2000-EXIT.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-CHECK-CONVERSE.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE F800-NUM-REGN                      TO KD00-NUM-REGN             
           PERFORM 8003-READ-BP13KD00            THRU 8003-EXIT                 
           COMPUTE WS-AMT-UNDISBURSE = KD00-AMT-LOAN-GRANTED                    
                                     - KD00-AMT-LOAN-DISBURSED.                 
           PERFORM 3125-PROCESS                  THRU  3125-EXIT.               
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3125-PROCESS.                                                            
      *-------------------------------------------------------------            
                                                                                
           IF K482-CDE-HYBRID = 'S' AND                                         
              K482-CDE-LOAN-CNVERSN = 'Y' AND                                   
              KD00-AMT-LOAN-BAL <= ZEROES  AND                                  
              WS-AMT-UNDISBURSE > ZEROES                                        
              CONTINUE                                                          
           ELSE                                                                 
           IF (K482-AMT-TOTL-CASH-AVAIL > 0 OR                                  
               K482-AMT-TOTL-CPF1-AVAIL > 0 OR                                  
               K482-AMT-TOTL-CPF2-AVAIL > 0 OR                                  
               K482-AMT-TOTL-CPF3-AVAIL > 0 OR                                  
               K482-AMT-TOTL-CPF4-AVAIL > 0) AND                                
               K482-AMT-TOTL-CASH-PAID >= K482-AMT-TOTL-CASH-REQ  AND           
               K482-AMT-TOTL-CPF1-PAID >= K482-AMT-TOTL-CPF1-REQ  AND           
               K482-AMT-TOTL-CPF2-PAID >= K482-AMT-TOTL-CPF2-REQ  AND           
               K482-AMT-TOTL-CPF3-PAID >= K482-AMT-TOTL-CPF3-REQ  AND           
               K482-AMT-TOTL-CPF4-PAID >= K482-AMT-TOTL-CPF4-REQ  AND           
               K482-CDE-LOAN-CNVERSN = 'Y' AND                                  
               KD00-AMT-LOAN-BAL <= ZEROES  AND                                 
               WS-AMT-UNDISBURSE > ZEROES                                       
               CONTINUE                                                         
           ELSE                                                                 
              MOVE 'N'                             TO WS-CNVRSN-VALID           
           END-IF.                                                              
                                                                                
           IF WS-CNVRSN-VALID = 'N'                                             
             IF (K482-AMT-TOTL-CASH-PAID > 0 OR                                 
                 K482-AMT-TOTL-CPF1-PAID > 0 OR                                 
                 K482-AMT-TOTL-CPF2-PAID > 0 OR                                 
                 K482-AMT-TOTL-CPF3-PAID > 0 OR                                 
                 K482-AMT-TOTL-CPF4-PAID > 0) AND                               
                 K482-AMT-TOTL-CASH-PAID >= K482-AMT-TOTL-CASH-REQ  AND         
                 K482-AMT-TOTL-CPF1-PAID >= K482-AMT-TOTL-CPF1-REQ  AND         
                 K482-AMT-TOTL-CPF2-PAID >= K482-AMT-TOTL-CPF2-REQ  AND         
                 K482-AMT-TOTL-CPF3-PAID >= K482-AMT-TOTL-CPF3-REQ  AND         
                 K482-AMT-TOTL-CPF4-PAID >= K482-AMT-TOTL-CPF4-REQ  AND         
                 K482-CDE-LOAN-CNVERSN = 'Y' AND                                
                 KD00-AMT-LOAN-BAL > ZEROES  AND                                
                 WS-AMT-UNDISBURSE > ZEROES                                     
                 MOVE 'Y' TO WS-CNVRSN-VALID                                    
             END-IF                                                             
           END-IF.                                                              
                                                                                
           IF F001-DTE-CURRENT > K482-DTE-DISPOSAL-COMPLETN                     
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'CURRENT DATE < TR/RSL COMPLETION '                       
              DISPLAY 'TR/RSL COMPLETION: ' K482-DTE-DISPOSAL-COMPLETN          
              DISPLAY 'K482 KEY FIELD   : ' K482-KEY-FLD                        
              MOVE 'N'      TO WS-CNVRSN-VALID                                  
           END-IF.                                                              
                                                                                
       3125-EXIT.                                                               
            EXIT.                                                               
      *-------------------------------------------------------------            
       3200-CONVERSE-LOAN-SCH.                                                  
      *-------------------------------------------------------------            
      ** UPDATE BP13KD00                                                        
           MOVE F800-NUM-REGN                 TO KD00-NUM-REGN                  
           PERFORM 8003-READ-BP13KD00       THRU 8003-EXIT                      
           IF KD00-FND                                                          
              MOVE KD00-CDE-LOAN-SCH          TO WS-LOAN-SCH-OLD                
              MOVE 'P'                        TO KD00-CDE-LOAN-SCH              
                                                 WS-LOAN-SCH-NEW                
              STRING F001-DTE-CURRENT(1:6) '01'                                 
                        DELIMITED BY SIZE INTO KD00-DTE-LOAN-CNVRSE             
              MOVE F001-DTE-CURRENT           TO KD00-DTE-UPDATE                
              MOVE FUNCTION CURRENT-DATE(9:8) TO KD00-TME-UPDATE                
              MOVE 'BP13CD89'                 TO KD00-NUM-USERID                
              PERFORM 8006-REWRITE-BP13KD00 THRU 8006-EXIT                      
           END-IF.                                                              
                                                                                
      ** UPDATE BP13K800                                                        
           MOVE F800-NUM-REGN              TO K800-NUM-REGN                     
           PERFORM 8004-READ-BP13K800       THRU 8004-EXIT                      
           IF K800-FND                                                          
              MOVE K800-NUM-BUY-SELL-TAG   TO K800-NUM-BUY-SELL-TAG-OLD         
                                                 WS-BS-TAG-OLD                  
              MOVE 'S'                        TO K800-NUM-BUY-SELL-TAG          
                                                 WS-BS-TAG-NEW                  
              MOVE 'N'                        TO K800-NUM-NON-CON               
              MOVE F001-DTE-CURRENT           TO K800-DTE-UPDATE                
              MOVE 'BP13CD89'                 TO K800-NUM-USERID                
              PERFORM 8005-REWRITE-BP13K800 THRU 8005-EXIT                      
      ** OUTPUT F136                                                            
              PERFORM 4600-PROCESS-LOAN-CNVRSE THRU 4600-EXIT                   
           END-IF.                                                              
                                                                                
      ** UPDATE BP13K640                                                        
           MOVE F800-NUM-REGN                 TO K640-KEY-FLD                   
           PERFORM 8009-READ-UPD-BP13K640   THRU 8009-EXIT                      
           IF K640-FND                                                          
              MOVE 'P'                        TO K640-CDE-LOAN-OPTION           
              MOVE F001-DTE-CURRENT           TO K640-DTE-UPDATE                
              MOVE 'BP13CD89'                 TO K640-USERID                    
              PERFORM 8010-REWRITE-BP13K640 THRU 8010-EXIT                      
           END-IF.                                                              
                                                                                
                                                                                
      ** UPDATE BP13KD60                                                        
           MOVE F800-NUM-REGN                 TO KD60-NUM-REGN                  
           PERFORM 8011-READ-UPD-BP13KD60   THRU 8011-EXIT                      
           IF KD60-FND                                                          
              MOVE 'P'                        TO KD60-CDE-LOAN-SCH              
              MOVE F001-DTE-CURRENT           TO KD60-DTE-UPDATE                
              MOVE FUNCTION CURRENT-DATE(9:8) TO KD60-TME-UPDATE                
              MOVE 'BP13CD89'                 TO KD60-NUM-USERID                
              PERFORM 8012-REWRITE-BP13KD60 THRU 8012-EXIT                      
           END-IF.                                                              
                                                                                
      ** UPDATE BP13K482                                                        
           PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > 4                      
              IF WS-FLATA(WS-I) NOT = SPACES AND LOW-VALUES                     
                 MOVE K800-NUM-SCH-ACC     TO K482-NUM-HDBREF-NEW               
                 MOVE WS-FLATA(WS-I)       TO K482-NUM-HDBREF-DISPOSAL          
                 PERFORM 8007-READ-UPD-BP13K482  THRU 8007-EXIT                 
                 STRING F001-DTE-CURRENT(1:6) '01'                              
                        DELIMITED BY SIZE INTO K482-DTE-LOAN-CONVERSION         
                 MOVE F001-DTE-CURRENT           TO K482-DTE-UPDATE             
                 MOVE FUNCTION CURRENT-DATE(9:8) TO K482-TME-UPDATE             
                 PERFORM 8008-REWRITE-BP13K482 THRU 8008-EXIT                   
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           PERFORM 4000-OUTPUT-REPORT    THRU 4000-EXIT.                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-OUTPUT-REPORT.                                                      
      *-------------------------------------------------------------            
                                                                                
           ADD 1                              TO CNT-REC-PAGE.                  
           IF CNT-REC-PAGE > 60                                                 
              PERFORM 4100-OUTPUT-HEADER    THRU 4100-EXIT                      
              ADD 1                           TO CNT-REC-PAGE                   
           END-IF.                                                              
                                                                                
           ADD 1                              TO CNT-SNO                        
           MOVE CNT-SNO                       TO LD89-NUM-SNO                   
           MOVE F800-NUM-REGN                 TO LD89-NUM-REGN                  
           STRING F800-NUM-SCH-ACC(1:4) '-' F800-NUM-SCH-ACC(5:4) '-'           
                  F800-NUM-SCH-ACC(9:1) '-' F800-NUM-SCH-ACC(10:2)              
                  DELIMITED BY SIZE INTO LD89-PURCHASE-FLAT                     
           STRING WS-FLATA(1)(1:4) '-' WS-FLATA(1)(5:4) '-'                     
                  WS-FLATA(1)(9:1) '-' WS-FLATA(1)(10:2)                        
                  DELIMITED BY SIZE INTO LD89-DISPOSAL-FLAT                     
           IF WS-FLATA(2) NOT = SPACES AND LOW-VALUES                           
              MOVE '(*)'                      TO LD89-MORE-DISPOSAL             
           ELSE                                                                 
              MOVE SPACES                     TO LD89-MORE-DISPOSAL             
           END-IF.                                                              
           MOVE WS-BS-TAG-OLD                 TO LD89-OLD-BS-TAG                
           MOVE WS-BS-TAG-NEW                 TO LD89-NEW-BS-TAG                
           MOVE WS-LOAN-SCH-OLD               TO LD89-OLD-LOAN-SCH              
           MOVE WS-LOAN-SCH-NEW               TO LD89-NEW-LOAN-SCH.             
                                                                                
           WRITE LD89-PRTREC FROM LD89-DETAIL-03.                               
           ADD 1                              TO CNT-LD89-WRITE.                
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4100-OUTPUT-HEADER.                                                      
      *-------------------------------------------------------------            
                                                                                
           STRING F001-DTE-CURRENT(7:2) '/'                                     
                  F001-DTE-CURRENT(5:2) '/'                                     
                  F001-DTE-CURRENT(1:4)                                         
                  DELIMITED BY SIZE INTO LD89-DATE                              
           ADD 1                              TO CNT-PAGE.                      
           MOVE CNT-PAGE                      TO LD89-PAGE.                     
                                                                                
           MOVE SPACES                        TO LD89-PRTREC                    
           WRITE LD89-PRTREC AFTER PAGE.                                        
           WRITE LD89-PRTREC FROM LD89-HEADER-01 AFTER 3.                       
           WRITE LD89-PRTREC FROM LD89-HEADER-02.                               
           WRITE LD89-PRTREC FROM LD89-DETAIL-01 AFTER 2.                       
           WRITE LD89-PRTREC FROM LD89-DETAIL-02.                               
                                                                                
           MOVE 8                             TO CNT-REC-PAGE.                  
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4600-PROCESS-LOAN-CNVRSE.                                                
      ******************************************************************        
                                                                                
      ** LOAN SCH 'P'                                                           
           MOVE SPACES                  TO BP13F136-CASHTRAN                    
           INITIALIZE                      BP13F136-CASHTRAN.                   
                                                                                
           MOVE KD00-NUM-REGN           TO F136-NUM-REGN                        
           MOVE '73'                    TO F136-CDE-TRANS-TYPE                  
           MOVE 'LON'                   TO F136-CDE-PAYMENT-TYPE                
           MOVE KD00-NUM-SCH-ACCNT      TO F136-NUM-SCH-ACCT                    
      *    MOVE KD00-AMT-LOAN-BAL       TO F136-AMT-RECEIPT                     
           MOVE 'P'                     TO F136-NUM-LOAN-SCH                    
           IF KD00-AMT-LOAN-BAL > 0                                             
              MOVE 'D'                  TO F136-CDE-CRDR                        
              MOVE KD00-AMT-LOAN-BAL    TO F136-AMT-RECEIPT                     
           ELSE                                                                 
              COMPUTE F136-AMT-RECEIPT = 0 - KD00-AMT-LOAN-BAL                  
              MOVE 'C'                  TO F136-CDE-CRDR                        
           END-IF.                                                              
           MOVE F001-DTE-CURRENT        TO F136-DTE-POST F136-DTE-TRANS.        
                                                                                
           WRITE BP13F136-CASHTRAN.                                             
           ADD 1 TO CNT-F136-WRITE.                                             
                                                                                
      ** LOAN SCH 'A'                                                           
           MOVE SPACES                  TO BP13F136-CASHTRAN                    
           INITIALIZE                      BP13F136-CASHTRAN.                   
                                                                                
           MOVE KD00-NUM-REGN           TO F136-NUM-REGN                        
           MOVE '73'                    TO F136-CDE-TRANS-TYPE                  
           MOVE 'LON'                   TO F136-CDE-PAYMENT-TYPE                
           MOVE KD00-NUM-SCH-ACCNT      TO F136-NUM-SCH-ACCT                    
      *    MOVE KD00-AMT-LOAN-BAL       TO F136-AMT-RECEIPT                     
           MOVE 'A'                     TO F136-NUM-LOAN-SCH                    
           IF KD00-AMT-LOAN-BAL > 0                                             
              MOVE 'D'                  TO F136-CDE-CRDR                        
              MOVE KD00-AMT-LOAN-BAL    TO F136-AMT-RECEIPT                     
           ELSE                                                                 
              COMPUTE F136-AMT-RECEIPT = 0 - KD00-AMT-LOAN-BAL                  
              MOVE 'C'                  TO F136-CDE-CRDR                        
           END-IF.                                                              
           MOVE F001-DTE-CURRENT        TO F136-DTE-POST F136-DTE-TRANS.        
                                                                                
           WRITE BP13F136-CASHTRAN.                                             
           ADD 1 TO CNT-F136-WRITE.                                             
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8001-START-BP13K482.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE 'N'                   TO WS-K482-EOF.                           
                                                                                
           START BP13K482 KEY >= K482-KEY-FLD.                                  
                                                                                
           IF WS-K482-STATUS = 00 OR 02 OR 22                                   
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-K482-STATUS = 10 OR 23                                      
                 MOVE 'Y'             TO WS-K482-EOF                            
              ELSE                                                              
                 DISPLAY 'BP13K482 - START ERROR (' WS-K482-STATUS              
                          '), KEY IS ('  K482-KEY-FLD ')'                       
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8001-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8002-READ-NEXT-BP13K482.                                                 
      *-------------------------------------------------------------            
                                                                                
           READ BP13K482 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y' TO WS-K482-EOF                                         
                GO       TO 8002-EXIT                                           
           END-READ.                                                            
                                                                                
           IF WS-K482-STATUS = 00 OR 02 OR 22                                   
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-K482-STATUS = 10 OR 23                                      
                 MOVE 'Y'               TO WS-K482-EOF                          
              ELSE                                                              
                 MOVE 'Y'               TO WS-K482-EOF                          
                 DISPLAY 'BP13K482 - READ NEXT ERROR (' WS-K482-STATUS          
                         ') ,KEY IS ('  K482-KEY-FLD ')'                        
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF K482-AMT-TOTL-CASH-PAID  NOT NUMERIC                              
              MOVE ZEROES               TO K482-AMT-TOTL-CASH-PAID.             
           IF K482-AMT-TOTL-CPF1-PAID  NOT NUMERIC                              
              MOVE ZEROES               TO K482-AMT-TOTL-CPF1-PAID.             
           IF K482-AMT-TOTL-CPF2-PAID  NOT NUMERIC                              
              MOVE ZEROES               TO K482-AMT-TOTL-CPF2-PAID.             
           IF K482-AMT-TOTL-CPF3-PAID  NOT NUMERIC                              
              MOVE ZEROES               TO K482-AMT-TOTL-CPF3-PAID.             
           IF K482-AMT-TOTL-CPF4-PAID  NOT NUMERIC                              
              MOVE ZEROES               TO K482-AMT-TOTL-CPF4-PAID.             
           IF K482-AMT-TOTL-CASH-AVAIL NOT NUMERIC                              
              MOVE ZEROES               TO K482-AMT-TOTL-CASH-AVAIL.            
           IF K482-AMT-TOTL-CPF1-AVAIL NOT NUMERIC                              
              MOVE ZEROES               TO K482-AMT-TOTL-CPF1-AVAIL.            
           IF K482-AMT-TOTL-CPF2-AVAIL NOT NUMERIC                              
              MOVE ZEROES               TO K482-AMT-TOTL-CPF2-AVAIL.            
           IF K482-AMT-TOTL-CPF3-AVAIL NOT NUMERIC                              
              MOVE ZEROES               TO K482-AMT-TOTL-CPF3-AVAIL.            
           IF K482-AMT-TOTL-CPF4-AVAIL NOT NUMERIC                              
              MOVE ZEROES               TO K482-AMT-TOTL-CPF4-AVAIL.            
                                                                                
       8002-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8003-READ-BP13KD00.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13KD00.                                                       
                                                                                
           IF WS-KD00-STATUS = 00 OR 02                                         
              MOVE 'Y'                       TO WS-KD00-FND                     
           ELSE                                                                 
              IF WS-KD00-STATUS = 23                                            
                 MOVE 'N'                    TO WS-KD00-FND                     
                 DISPLAY 'BP13KD00 - RECORD NOT FOUND, KEY ('                   
                         KD00-NUM-REGN ')'                                      
              ELSE                                                              
                 DISPLAY 'BP13KD00 - READ ERROR (' WS-KD00-STATUS ')'           
                         ', KEY (' KD00-NUM-REGN ')'                            
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF KD00-AMT-LOAN-GRANTED   NOT NUMERIC                               
              MOVE ZEROES              TO KD00-AMT-LOAN-GRANTED.                
           IF KD00-AMT-LOAN-DISBURSED NOT NUMERIC                               
              MOVE ZEROES              TO KD00-AMT-LOAN-DISBURSED.              
                                                                                
       8003-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8004-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = 00                                               
              MOVE 'Y'                      TO WS-K800-FND                      
           ELSE                                                                 
              IF WS-K800-STATUS = 23                                            
                 MOVE 'N'                   TO WS-K800-FND                      
                 DISPLAY 'BP13K800 - RECORD NOT FOUND, KEY ('                   
                         K800-NUM-REGN ')'                                      
              ELSE                                                              
                 DISPLAY 'BP13K800 - READ ERROR (' WS-K800-STATUS ')'           
                         ', KEY (' K800-NUM-REGN ')'                            
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8004-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8005-REWRITE-BP13K800.                                                   
      *-------------------------------------------------------------            
                                                                                
           MOVE 00 TO WS-K800-STATUS                                            
           REWRITE BP13K800-MASTER.                                             
                                                                                
           IF WS-K800-STATUS = 00                                               
              ADD 1                  TO CNT-K800-REWRITE                        
           ELSE                                                                 
              DISPLAY 'BP13K800 - REWRITE ERROR (' WS-K800-STATUS ')'           
                      ', KEY (' K800-NUM-REGN ')'                               
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
       8005-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8006-REWRITE-BP13KD00.                                                   
      *-------------------------------------------------------------            
                                                                                
           MOVE 00 TO WS-KD00-STATUS                                            
                                                                                
           REWRITE BP13KD00-REC.                                                
                                                                                
           IF WS-KD00-STATUS = 00                                               
              ADD 1                  TO CNT-KD00-REWRITE                        
           ELSE                                                                 
              DISPLAY 'BP13KD00 - REWRITE ERROR (' WS-KD00-STATUS ')'           
                      ', KEY (' KD00-NUM-REGN ')'                               
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
       8006-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8007-READ-UPD-BP13K482.                                                  
      *-------------------------------------------------------------            
                                                                                
           READ BP13K482.                                                       
                                                                                
           IF WS-K482-STATUS = 00                                               
              MOVE 'Y'                   TO WS-K482-FND                         
           ELSE                                                                 
              IF WS-K482-STATUS = 23                                            
                 MOVE 'N'                TO WS-K482-FND                         
                 DISPLAY 'BP13K482 - RECORD NOT FOUND, KEY ('                   
                         K482-KEY-FLD ')'                                       
              ELSE                                                              
                 DISPLAY 'BP13K482 - READ ERROR (' WS-K482-STATUS ')'           
                         ', KEY (' K482-KEY-FLD ')'                             
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8007-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8008-REWRITE-BP13K482.                                                   
      *-------------------------------------------------------------            
                                                                                
           MOVE 00 TO WS-K482-STATUS                                            
           REWRITE BP13K482-REC.                                                
                                                                                
           IF WS-K482-STATUS = 00                                               
              ADD 1                  TO CNT-K482-REWRITE                        
           ELSE                                                                 
              DISPLAY 'BP13K482 - REWRITE ERROR (' WS-K482-STATUS ')'           
                      ', KEY (' K482-KEY-FLD ')'                                
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
       8008-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8009-READ-UPD-BP13K640.                                                  
      *-------------------------------------------------------------            
                                                                                
           READ BP13K640.                                                       
                                                                                
           IF WS-K640-STATUS = 00                                               
              MOVE 'Y'                   TO WS-K640-FND                         
           ELSE                                                                 
              IF WS-K640-STATUS = 23                                            
                 MOVE 'N'                TO WS-K640-FND                         
                 DISPLAY 'BP13K640 - RECORD NOT FOUND, KEY ('                   
                         K640-KEY-FLD ')'                                       
              ELSE                                                              
                 DISPLAY 'BP13K640 - READ ERROR (' WS-K640-STATUS ')'           
                         ', KEY (' K640-KEY-FLD ')'                             
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8009-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8010-REWRITE-BP13K640.                                                   
      *-------------------------------------------------------------            
                                                                                
           MOVE 00 TO WS-K640-STATUS                                            
           REWRITE BP13K640-TRANS-REC.                                          
                                                                                
           IF WS-K640-STATUS = 00                                               
              ADD 1                  TO CNT-K640-REWRITE                        
           ELSE                                                                 
              DISPLAY 'BP13K640 - REWRITE ERROR (' WS-K640-STATUS ')'           
                      ', KEY (' K640-KEY-FLD ')'                                
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
       8010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8011-READ-UPD-BP13KD60.                                                  
      *-------------------------------------------------------------            
                                                                                
           READ BP13KD60.                                                       
                                                                                
           IF WS-KD60-STATUS = 00                                               
              MOVE 'Y'                   TO WS-KD60-FND                         
           ELSE                                                                 
              IF WS-KD60-STATUS = 23                                            
                 MOVE 'N'                TO WS-KD60-FND                         
                 DISPLAY 'BP13KD60 - RECORD NOT FOUND, KEY ('                   
                         KD60-KEY-FLD ')'                                       
              ELSE                                                              
                 DISPLAY 'BP13KD60 - READ ERROR (' WS-KD60-STATUS ')'           
                         ', KEY (' KD60-KEY-FLD ')'                             
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8011-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8012-REWRITE-BP13KD60.                                                   
      *-------------------------------------------------------------            
                                                                                
           MOVE 00 TO WS-KD60-STATUS                                            
           REWRITE BP13KD60-REC.                                                
                                                                                
           IF WS-KD60-STATUS = 00                                               
              ADD 1                  TO CNT-KD60-REWRITE                        
           ELSE                                                                 
              DISPLAY 'BP13KD60 - REWRITE ERROR (' WS-KD60-STATUS ')'           
                      ', KEY (' KD60-KEY-FLD ')'                                
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
       8012-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY ' '.                                                         
           DISPLAY ' -- BP13CD89 CONTROL TOTAL -------'.                        
           DISPLAY ' '.                                                         
           DISPLAY ' CNT-F800-READ        : ' CNT-F800-READ.                    
           DISPLAY ' CNT-K800-REWRITE     : ' CNT-K800-REWRITE.                 
           DISPLAY ' CNT-KD00-REWRITE     : ' CNT-KD00-REWRITE.                 
           DISPLAY ' CNT-K482-REWRITE     : ' CNT-K482-REWRITE.                 
           DISPLAY ' CNT-K640-REWRITE     : ' CNT-K640-REWRITE.                 
           DISPLAY ' CNT-LD89-WRITE       : ' CNT-LD89-WRITE.                   
           DISPLAY ' CNT-F136-WRITE       : ' CNT-F136-WRITE.                   
           DISPLAY ' '.                                                         
                                                                                
           CLOSE SY02F001                                                       
                 BP13F800                                                       
                 BP13KD00                                                       
                 BP13K800                                                       
                 BP13K482                                                       
                 BP13K640                                                       
                 BP13LD89                                                       
                 BP13F136.                                                      
                                                                                
           IF WS-KD00-STATUS   NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BP13KD00, STAT ' WS-KD00-STATUS.           
                                                                                
           IF WS-K800-STATUS   NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BP13K800, STAT ' WS-K800-STATUS.           
                                                                                
           IF WS-K482-STATUS   NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BP13K482, STAT ' WS-K482-STATUS.           
                                                                                
           IF WS-K640-STATUS   NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BP13K640, STAT ' WS-K640-STATUS.           
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
