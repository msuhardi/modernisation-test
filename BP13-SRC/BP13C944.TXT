       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C944.                                                 
      *AUTHOR.        SATHAPPAN.                                                
      *DATE-WRITTEN.  25/10/2001.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE: UPDATE EXTRACTED SPOUSE NAME TO BP13K820       *            
      *                                                            *            
      *  INPUT FILES:                                              *            
      *      1. BP13F820                                           *            
      *  I-O    FILES:                                                *         
      *      1. BP13K820                                           *            
      * ---------------------------------------------------------- *            
      * CHG REF  BY   ON       DESCRIPTION                         *            
      * -------- ---- -------- -----------                         *            
      * BP130850 AS1  20011025 NEW PROGRAM                         *            
      * ========================================================= *             
      *--------------------*                                                    
       ENVIRONMENT DIVISION.                                                    
      *--------------------*                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K820  ASSIGN  TO  BP13K820                                
                       ACCESS       IS  RANDOM                                  
                       ORGANIZATION IS  INDEXED                                 
                       RECORD KEY   IS  K820-KEY-FLD                            
                       FILE STATUS  IS  WS-K820-STATUS.                         
                                                                                
           SELECT BP13F820  ASSIGN  TO  BP13F820.                               
                                                                                
      *--------------*                                                          
       DATA DIVISION.                                                           
      *--------------*                                                          
       FILE SECTION.                                                            
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BP13F820                                                             
           BLOCK  CONTAINS   0 RECORDS                                          
           RECORD CONTAINS 400  CHARACTERS                                      
           LABEL  RECORDS  ARE STANDARD                                         
           RECORDING MODE IS F.                                                 
       COPY BP13F820.                                                           
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
       01  FILE-STATUS.                                                         
           03  WS-K820-STATUS             PIC 9(2)   VALUE 0.                   
                                                                                
       01  WS-ACCUMULATORS.                                                     
           03  WS-TEMP-READ               PIC 9(10)   VALUE ZERO.               
           03  WS-K820-READ               PIC 9(10)   VALUE ZERO.               
           03  WS-K820-REWRITE            PIC 9(10)   VALUE ZERO.               
           03  WS-EDIT-TEMP-READ      PIC Z,ZZZ,ZZZ,Z99 VALUE ZERO.             
           03  WS-EDIT-K820-READ      PIC Z,ZZZ,ZZZ,Z99 VALUE ZERO.             
           03  WS-EDIT-K820-REWRITE   PIC Z,ZZZ,ZZZ,Z99 VALUE ZERO.             
                                                                                
       01  TEMP-EOF-IND                   PIC X       VALUE SPACES.             
           88 TEMP-EOF                    VALUE 'Y'.                            
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
       0000-MAIN-LOGIC.                                                         
      *-------------------*                                                     
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13F820   THRU 2000-EXIT.                         
           PERFORM 3000-PROCESS-ROUTINE THRU 3000-EXIT                          
                   UNTIL TEMP-EOF .                                             
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       1000-OPEN-ROUTINE.                                                       
      *-----------------*                                                       
                                                                                
           OPEN INPUT     BP13F820                                              
                I-O       BP13K820.                                             
                                                                                
           IF WS-K820-STATUS NOT = ZEROES AND 97                                
              DISPLAY ' OPENING BP13K820  ERROR ' WS-K820-STATUS                
              MOVE WS-K820-STATUS TO RETURN-CODE                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       2000-READ-BP13F820.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13F820  AT END MOVE 'Y' TO TEMP-EOF-IND                       
                                           GO TO 2000-EXIT.                     
                                                                                
           ADD       1                    TO WS-TEMP-READ.                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
      *---------------------*                                                   
       3000-PROCESS-ROUTINE.                                                    
      *---------------------*                                                   
                                                                                
           PERFORM 3100-READ-BP13K820      THRU   3100-EXIT.                    
           PERFORM 2000-READ-BP13F820      THRU   2000-EXIT.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3100-READ-BP13K820.                                                      
      *-------------------*                                                     
                                                                                
            MOVE F820-KEY-FLD            TO   K820-KEY-FLD.                     
            READ BP13K820.                                                      
                                                                                
            EVALUATE WS-K820-STATUS                                             
                WHEN 00                                                         
                     ADD     1           TO   WS-K820-READ                      
                     PERFORM 3200-REWRITE-BP13K820  THRU 3200-EXIT              
                WHEN 23                                                         
                     DISPLAY 'RECORD NOT FOUND IN BP13K820' K820-KEY-FLD        
                WHEN OTHER                                                      
                     DISPLAY 'ERROR IN READING -  BP13K820'                     
                     PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT              
            END-EVALUATE.                                                       
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------------*                                                   
       3200-REWRITE-BP13K820.                                                   
      *---------------------*                                                   
                                                                                
           IF F820-NUM-NRIC-SPOUSE NOT     =   SPACES                           
              IF K820-NUM-NRIC-SPOUSE      =   F820-NUM-NRIC-SPOUSE             
                 MOVE  F820-NME-SPOUSE    TO   K820-NME-SPOUSE                  
                 REWRITE BP13K820-REC                                           
                 ADD   1                  TO   WS-K820-REWRITE                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       9000-CLOSE-ROUTINE.                                                      
      *-------------------*                                                     
                                                                                
           CLOSE BP13F820                                                       
                 BP13K820.                                                      
                                                                                
           IF WS-K820-STATUS NOT = ZEROES AND 97                                
              DISPLAY ' CLOSING BP13K820 ERROR ' WS-K820-STATUS                 
              MOVE WS-K820-STATUS TO RETURN-CODE.                               
                                                                                
           MOVE WS-TEMP-READ          TO  WS-EDIT-TEMP-READ.                    
           MOVE WS-K820-READ          TO  WS-EDIT-K820-READ.                    
           MOVE WS-K820-REWRITE       TO  WS-EDIT-K820-REWRITE.                 
                                                                                
           DISPLAY '  '.                                                        
           DISPLAY ' BP13C944 CONTROL LIST'.                                    
           DISPLAY ' ---------------------'.                                    
           DISPLAY ' NO OF REC READ    IN  BP13F820= '                          
                                                   WS-EDIT-TEMP-READ.           
           DISPLAY ' NO OF REC READ    IN  BP13K820= '                          
                                                   WS-EDIT-K820-READ.           
           DISPLAY ' NO OF REC WRITE   TO  BP13K820= '                          
                                                   WS-EDIT-K820-REWRITE.        
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
