       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C884.                                                 
      *AUTHOR.        LSB.                                                      
      *DATE-WRITTEN.  16/07/96.                                                 
      * ====================================================== *                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      * ====================================================== *                
      *                                                        *                
      *    PROGRAM REF : BP13C512, DIFF INPUT BP13F565 INSTEAD *                
      *                  OF BP13F740, LREC 350 INSTEAD OF 300  *                
      *                                                        *                
      *    OBJECTIVE  :                                        *                
      *        1.  TO ASSIGN REGISTRATION NUMBER TO SUCCESSFUL *                
      *            CASES IN BP13K740                           *                
      *        2.  TO PRINT CONTROL LIST OF THE ASSIGNMENTS.   *                
      *                                                        *                
      *     INPUT FILES :                                      *                
      *       1. BP13F565  :  BALLOT FILE                      *                
      *       2. BP13K530  :  DATE/REGN FILE                   *                
      *       3. BP13K825  :  OCCUPIER FILE                    *                
      *       4. BP13K830  :  ADDRESS FILE                     *                
      *                                                        *                
      *     I-O FILES.                                         *                
      *       1. BP13K530  :  REGISTRATION NUMBER INVENTORY    *                
      *                       FILE                             *                
      *       2. BP13K740  :  BALLOT FILE                      *                
      *                                                        *                
      *     OUTPUT FILES :                                     *                
      *       1. BP13L884  : CONTROL LIST                      *                
      *                                                        *                
      *  CHG NO   BY   ON        DESCRIPTION                   *                
      *  ------   --   --        -----------                   *                
      * BP130118  LSB 960716 - NEW PGM                         *                
      * BP130347  IT1 971003 - UPDATE SEQ-NUM & JB-PARENT-REGN *                
      * BP130418  SIA 982303 - CATER FOR Y2000                 *                
      * BP130652  NOR 990222 - EXPAND K740 RECL FROM 300 TO 500*                
      * BP132500  YCH 140202 - UPD JB-PARENT-REG & SEQ-NUM     *                
      *                        ONLY FOR JB CASES OTHERS ARE    *                
      *                        BLANK BASED ON RANDOM NUMBER    *                
      * BP132626  SK  241104 - DELETE K740-DTE-REGN            *                
      * BP134838 YCH2 020213 - UPDATE REGNO TO K730            *                
      * BP137208 YCH2 060218 - TO UPDATE EMAIL REGNO           *                
      * ====================================================== *                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3900.                                               
       OBJECT-COMPUTER. IBM-3900.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F565  ASSIGN  TO  BP13F565.                               
           SELECT BP13K740  ASSIGN  TO  BP13K740                                
                ACCESS       IS RANDOM                                          
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K740-KEY-FLD                                    
                FILE STATUS  IS BP13K740-STATUS.                                
                                                                                
           SELECT BP13K530  ASSIGN  TO  BP13K530                                
                ACCESS       IS RANDOM                                          
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K530-KEY-FLD                                    
                FILE STATUS  IS BP13K530-STATUS.                                
                                                                                
           SELECT BP13F730  ASSIGN  TO  BP13F730.                               
                                                                                
           SELECT BP13K825  ASSIGN  TO  BP13K825                                
                ACCESS       IS DYNAMIC                                         
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K825-KEY-FLD                                    
                FILE STATUS  IS BP13K825-STATUS.                                
                                                                                
           SELECT BP13K830  ASSIGN  TO  BP13K830                                
                ACCESS       IS DYNAMIC                                         
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K830-KEY-FLD                                    
                FILE STATUS  IS BP13K830-STATUS.                                
                                                                                
           SELECT BP13K857  ASSIGN  TO  BP13K857                                
                ACCESS       IS RANDOM                                          
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K857-KEY-FLD                                    
                FILE STATUS  IS BP13K857-STATUS.                                
                                                                                
           SELECT BP13L884  ASSIGN  TO  BP13L884.                               
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F565                                                             
           BLOCK     CONTAINS 0   RECORDS                                       
           RECORD    CONTAINS 550 CHARACTERS                                    
           LABEL     RECORDS  ARE STANDARD                                      
           RECORDING MODE IS F.                                                 
       COPY BP13F565.                                                           
                                                                                
       FD  BP13K740                                                             
           RECORD    CONTAINS 500 CHARACTERS.                                   
       COPY BP13K740.                                                           
                                                                                
       FD  BP13F730                                                             
           BLOCK     CONTAINS 0   RECORDS                                       
           RECORD    CONTAINS 500 CHARACTERS                                    
           LABEL     RECORDS  ARE STANDARD                                      
           RECORDING MODE IS F.                                                 
       COPY BP13F730.                                                           
                                                                                
       FD  BP13K825                                                             
           RECORD    CONTAINS 200 CHARACTERS.                                   
       COPY BP13K825.                                                           
                                                                                
       FD  BP13K530                                                             
           RECORD    CONTAINS 50 CHARACTERS.                                    
       COPY BP13K530.                                                           
                                                                                
       FD  BP13K830                                                             
           RECORD    CONTAINS 250 CHARACTERS.                                   
       COPY BP13K830.                                                           
                                                                                
       FD  BP13K857                                                             
           RECORD    CONTAINS 200 CHARACTERS.                                   
       COPY BP13K857.                                                           
                                                                                
       FD  BP13L884                                                             
           BLOCK     CONTAINS   0 RECORDS                                       
           RECORD    CONTAINS 132 CHARACTERS                                    
           LABEL     RECORDS  ARE OMITTED                                       
           RECORDING MODE IS F.                                                 
       01  BP13L884-PRT-REC    PIC X(132).                                      
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  FILE-STATUS.                                                         
           05 BP13K740-STATUS           PIC 99 VALUE 0.                         
              88 BP13K740-WRITE-OK          VALUE 0.                            
           05 BP13K530-STATUS           PIC 99 VALUE 0.                         
              88 BP13K530-REC-FOUND         VALUE 0.                            
              88 BP13K530-REC-NOTFND        VALUE 23.                           
           05 BP13K825-STATUS           PIC 99 VALUE 0.                         
              88 BP13K825-REC-FOUND         VALUE 0.                            
              88 BP13K825-REC-NOTFND        VALUE 23.                           
           05 BP13K830-STATUS           PIC 99 VALUE 0.                         
              88 BP13K830-REC-FOUND         VALUE 0.                            
              88 BP13K830-REC-NOTFND        VALUE 23.                           
              88 BP13K830-WRITE-OK          VALUE 0.                            
           05 BP13K857-STATUS           PIC 99 VALUE 0.                         
              88 BP13K857-REC-FOUND         VALUE 0.                            
              88 BP13K857-REC-NOTFND        VALUE 23.                           
              88 BP13K857-WRITE-OK          VALUE 0.                            
                                                                                
       01  WS-ACCUMULATORS.                                                     
           05 WS-NUM-READ-BP13F565      PIC 9(06) VALUE ZERO.                   
           05 WS-NUM-REWRITE-BP13K740   PIC 9(06) VALUE ZERO.                   
           05 WS-NUM-NTFND-BP13K857     PIC 9(06) VALUE ZERO.                   
           05 WS-NUM-WRITE-BP13F730     PIC 9(06) VALUE ZERO.                   
           05 WS-NUM-WRITE-BP13K530     PIC 9(06) VALUE ZERO.                   
           05 WS-NUM-WRITE-BP13K857     PIC 9(06) VALUE ZERO.                   
           05 WS-NUM-REWRITE-BP13K530   PIC 9(06) VALUE ZERO.                   
           05 WS-NUM-REWRITE-BP13K830   PIC 9(06) VALUE ZERO.                   
           05 WS-NUM-DELETE-BP13K857    PIC 9(06) VALUE ZERO.                   
           05 WS-NUM-DELETE-BP13K825    PIC 9(06) VALUE ZERO.                   
           05 WS-NUM-WRITE-BP13K825     PIC 9(06) VALUE ZERO.                   
           05 WS-NUM-WRITE-BP13K830     PIC 9(06) VALUE ZERO.                   
           05 WS-LINECT                 PIC 9(02) VALUE 60.                     
           05 WS-PAGECT                 PIC 9(04) VALUE ZERO.                   
           05 WS-SEQCT                  PIC 9(04) VALUE ZERO.                   
                                                                                
       01  WS-SWITCHES.                                                         
           05 EOF-BP13F565-IND          PIC X(01) VALUE SPACE.                  
              88 EOF-BP13F565               VALUE 'Y'.                          
           05 WS-DATE-FOUND-IND         PIC X(01) VALUE SPACE.                  
              88 WS-DATE-FOUND              VALUE 'Y'.                          
                                                                                
       01  WS-K530-CURDTE-NOTFND        PIC X(01) VALUE SPACE.                  
       01  WS-K825-UPDATED              PIC X(01) VALUE SPACE.                  
                                                                                
       01  WS-CURR-DATE.                                                        
           05 WS-CURR-MM                PIC 9(02).                              
           05 FILLER                    PIC X(01).                              
           05 WS-CURR-DD                PIC 9(02).                              
           05 FILLER                    PIC X(01).                              
           05 WS-CURR-CC                PIC 9(02).                              
           05 WS-CURR-YY                PIC 9(02).                              
                                                                                
       01  WS-CURR-CCYYMMDD.                                                    
           05 WS-CC                     PIC 9(02).                              
           05 WS-CURR-YYMMDD.                                                   
              10 WS-YY                  PIC 9(02).                              
              10 WS-MM                  PIC 9(02).                              
              10 WS-DD                  PIC 9(02).                              
                                                                                
       01  FILLER  REDEFINES  WS-CURR-CCYYMMDD.                                 
           05 WS-CURR-YEAR.                                                     
              10 WS-YEAR-CC             PIC 9(02).                              
              10 WS-YEAR-YR             PIC 9(02).                              
           05 WS-YEAR REDEFINES WS-CURR-YEAR  PIC 9(04).                        
           05 FILLER                    PIC 9(04).                              
                                                                                
       01  WS-SYS-DATE.                                                         
           05 WS-SYS-YY                PIC 9(04).                               
           05 WS-SYS-MM                PIC 9(02).                               
           05 WS-SYS-DD                PIC 9(02).                               
                                                                                
       01  WS-RESULT                   PIC 9(4) COMP VALUE 0.                   
       01  WS-REM                      PIC 9(02) VALUE 0.                       
                                                                                
      ************************************************************              
      *    TABLE CONTAINING THE THE LAST DAY OF THE MONTH        *              
      ************************************************************              
       01  WS-MTHLDAY-TABLE.                                                    
           05 FILLER                   PIC X(04) VALUE '0131'.                  
           05 FILLER                   PIC X(04) VALUE '0228'.                  
           05 FILLER                   PIC X(04) VALUE '0331'.                  
           05 FILLER                   PIC X(04) VALUE '0430'.                  
           05 FILLER                   PIC X(04) VALUE '0531'.                  
           05 FILLER                   PIC X(04) VALUE '0630'.                  
           05 FILLER                   PIC X(04) VALUE '0731'.                  
           05 FILLER                   PIC X(04) VALUE '0831'.                  
           05 FILLER                   PIC X(04) VALUE '0930'.                  
           05 FILLER                   PIC X(04) VALUE '1031'.                  
           05 FILLER                   PIC X(04) VALUE '1130'.                  
           05 FILLER                   PIC X(04) VALUE '1231'.                  
                                                                                
       01  WS-MTHLDAY-TBL   REDEFINES  WS-MTHLDAY-TABLE.                        
           05 WS-MTHLDAY-REC               OCCURS 12 TIMES                      
              INDEXED BY WS-MLDAY-IDX.                                          
              10 WS-MTH                PIC X(02).                               
              10 WS-LDAY               PIC X(02).                               
                                                                                
      ************************************************************              
      *    VARIABLES FOR CREATION OF REGN NO. CHECK DIGIT        *              
      ************************************************************              
       01  WS-REGN-TAB.                                                         
           05 FILLER                   PIC X(33) VALUE                          
             '01A02B03C04D05E06F07G08H09I10Z11J'.                               
                                                                                
       01  WS-REGNO-TAB REDEFINES WS-REGN-TAB.                                  
           05 REGN-TAB                     OCCURS 11 TIMES                      
              INDEXED BY WS-CDG.                                                
              10 REGN-REMAINDER        PIC 9(02).                               
              10 REGN-CDG              PIC X(01).                               
                                                                                
       01  WS-REG                      PIC X(08) VALUE SPACES.                  
                                                                                
       01  WS-REGN-NUMERIC.                                                     
           05 REGN-NUM OCCURS 7 TIMES  PIC 9(01).                               
           05 REGN-FLD                 PIC X(01).                               
                                                                                
       01  WS-FLD.                                                              
           05 WS-FLD1                  PIC 9(04) VALUE 0.                       
           05 WS-FLD2                  PIC 9(04) VALUE 0.                       
           05 WS-FLD3                  PIC 9(02) VALUE 0.                       
                                                                                
       01  WS-CHK-DGT                  PIC X(01) VALUE SPACE.                   
                                                                                
       01  WS-TEMP-REG-NO.                                                      
           05 WS-TEMP-REG-NUM          PIC 9(07).                               
           05 WS-TEMP-CHK-DGT          PIC X(01).                               
                                                                                
       01  WS-SMALLEST-REGN            PIC X(8) VALUE SPACE.                    
       01  WS-JT-RANDOM                PIC X(28) VALUE SPACE.                   
                                                                                
      **************************************************************            
      *           LAY-OUT FOR CONTROL LISING                       *            
      **************************************************************            
       01  L884-HDG1.                                                           
           05 FILLER                   PIC X(14) VALUE 'BP13L884'.              
           05 FILLER                   PIC X(08) VALUE 'HDB3'.                  
           05 FILLER                   PIC X(18) VALUE SPACES.                  
           05 FILLER                   PIC X(39) VALUE                          
              'S Y S T E M   O F   C O M M I T M E N T'.                        
           05 FILLER                   PIC X(16) VALUE SPACES.                  
           05 FILLER                   PIC X(07) VALUE 'DATE: '.                
           05 HDG1-DATE                PIC X(10) VALUE SPACES.                  
           05 FILLER                   PIC X(03) VALUE SPACES.                  
           05 FILLER                   PIC X(07) VALUE 'PAGE: '.                
           05 HDG1-PAGECT              PIC ZZZZ9.                               
                                                                                
       01  L884-HDG2.                                                           
           05 FILLER                   PIC X(95) VALUE SPACES.                  
           05 FILLER                   PIC X(32) VALUE                          
              'SALES UNIT    (ALLOCATION  TEAM)'.                               
                                                                                
       01  L884-HDG3.                                                           
           05 FILLER                   PIC X(33) VALUE SPACES.                  
           05 FILLER                   PIC X(60) VALUE                          
           'LIST OF SUCCESSFUL SERS REQUEST ASSIGNED REGISTRATION NO'.          
                                                                                
       01  L884-HDG4.                                                           
           05 FILLER                   PIC X(05) VALUE ' S/N'.                  
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(29) VALUE                          
             '       RANDOM NO'.                                                
           05 FILLER                   PIC X(08) VALUE 'PREV-REF'.              
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 FILLER                   PIC X(08) VALUE 'CURR-REF'.              
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 FILLER                   PIC X(07) VALUE 'REGN NO'.               
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(03) VALUE 'CAT'.                   
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 FILLER                   PIC X(11) VALUE 'NRIC1/NRIC2'.           
           05 FILLER                   PIC X(09) VALUE SPACES.                  
           05 FILLER                   PIC X(04) VALUE ' JB '.                  
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(05) VALUE 'ALLOC'.                 
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(04) VALUE 'ELIG'.                  
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(04) VALUE ' NT '.                  
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(04) VALUE ' FT '.                  
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(04) VALUE ' JB '.                  
           05 FILLER                   PIC X(03) VALUE SPACES.                  
           05 FILLER                   PIC X(06) VALUE ' LOW  '.                
                                                                                
       01  L884-HDG5.                                                           
           05 FILLER                   PIC X(67) VALUE SPACES.                  
           05 FILLER                   PIC X(14) VALUE                          
             'APPLICANT NAME'.                                                  
           05 FILLER                   PIC X(06) VALUE SPACES.                  
           05 FILLER                   PIC X(04) VALUE 'CODE'.                  
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(05) VALUE ' SCH '.                 
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(04) VALUE 'SCH '.                  
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(04) VALUE 'CODE'.                  
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(04) VALUE 'TYPE'.                  
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(06) VALUE 'REF-NO'.                
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(05) VALUE 'FLOOR'.                 
                                                                                
       01  L884-DTL1.                                                           
           05 SERIAL-NO                PIC Z(4)9.                               
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 RANDOM-NO                PIC X(28) VALUE SPACES.                  
           05 FILLER                   PIC X(01) VALUE SPACE.                   
           05 PREV-RN                  PIC X(06) VALUE SPACES.                  
           05 FILLER                   PIC X(03) VALUE SPACES.                  
           05 CUR-RN                   PIC X(06) VALUE SPACES.                  
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 REGN-NO                  PIC X(08) VALUE SPACES.                  
           05 FILLER                   PIC X(03) VALUE SPACES.                  
           05 CAT                      PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 NRIC-1                   PIC X(09) VALUE SPACES.                  
           05 FILLER                   PIC X(01) VALUE '/'.                     
           05 NRIC-2                   PIC X(09) VALUE SPACES.                  
           05 FILLER                   PIC X(03) VALUE SPACES.                  
           05 JB-CODE                  PIC X(04) VALUE SPACES.                  
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 ALLOC-SCH                PIC X(05) VALUE SPACES.                  
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 L-CDE-ELIG               PIC X(03) VALUE SPACES.                  
           05 FILLER                   PIC X(03) VALUE SPACES.                  
           05 NT-CODE                  PIC X(04) VALUE SPACES.                  
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 FT-TYPE                  PIC X(04) VALUE SPACES.                  
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 JB-REF                   PIC X(06) VALUE SPACES.                  
           05 FILLER                   PIC X(02) VALUE SPACES.                  
           05 LOW-FLOOR                PIC X(01) VALUE SPACES.                  
                                                                                
       01  L884-DTL2.                                                           
           05 FILLER                   PIC X(67) VALUE SPACES.                  
           05 NAME                     PIC X(66) VALUE SPACES.                  
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       010-OPEN-ROUTINE.                                                        
      *-------------------------------------------------------------            
      *    READY TRACE.                                                         
                                                                                
           OPEN INPUT  BP13F565                                                 
                I-O    BP13K530                                                 
                       BP13K740                                                 
                       BP13K825                                                 
                       BP13K830                                                 
                       BP13K857                                                 
                OUTPUT BP13F730                                                 
                       BP13L884.                                                
                                                                                
           IF BP13K740-STATUS NOT = 0 AND 97                                    
              DISPLAY ' ERROR OPENING BP13K740 FILE, STATUS '                   
                      BP13K740-STATUS                                           
              MOVE BP13K740-STATUS TO RETURN-CODE                               
              PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K825-STATUS NOT = 0 AND 97                                    
              DISPLAY ' ERROR OPENING BP13K825 FILE, STATUS '                   
                      BP13K825-STATUS                                           
              MOVE BP13K825-STATUS TO RETURN-CODE                               
              PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K530-STATUS NOT = 0 AND 97                                    
              DISPLAY ' ERROR OPENING BP13K530 FILE, STATUS '                   
                      BP13K530-STATUS                                           
              MOVE BP13K530-STATUS TO RETURN-CODE                               
              PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K830-STATUS NOT = 0 AND 97                                    
              DISPLAY ' ERROR OPENING BP13K830 FILE, STATUS '                   
                      BP13K830-STATUS                                           
              MOVE BP13K830-STATUS TO RETURN-CODE                               
              PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K857-STATUS NOT = 0 AND 97                                    
              DISPLAY ' ERROR OPENING BP13K857 FILE, STATUS '                   
                      BP13K857-STATUS                                           
              MOVE BP13K857-STATUS TO RETURN-CODE                               
              PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT                           
           END-IF.                                                              
                                                                                
                                                                                
       020-PROCESS-ROUTINE.                                                     
      *-------------------------------------------------------------            
      ** SET CURRENT DATE FORMAT FOR REGN AND CONTROL LISTING **                
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-CCYYMMDD                       
                                         WS-SYS-DATE.                           
           MOVE WS-DD             TO WS-CURR-DD.                                
           MOVE WS-MM             TO WS-CURR-MM.                                
           MOVE WS-CC             TO WS-CURR-CC.                                
           MOVE WS-YY             TO WS-CURR-YY.                                
                                                                                
           STRING WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CC WS-CURR-YY           
                  DELIMITED BY SIZE INTO HDG1-DATE                              
           END-STRING.                                                          
                                                                                
           PERFORM 100-PROCESS-BP13K530 THRU 100-EXIT                           
              UNTIL WS-DATE-FOUND.                                              
           PERFORM 200-PROCESS-BP13F565 THRU 200-EXIT                           
              UNTIL EOF-BP13F565.                                               
           PERFORM 700-UPDATE-BP13K530  THRU 700-EXIT.                          
           PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT.                             
                                                                                
       020-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       030-CLOSE-ROUTINE.                                                       
      *-------------------------------------------------------------            
           CLOSE BP13F565                                                       
                 BP13K530                                                       
                 BP13F730                                                       
                 BP13K740                                                       
                 BP13K825                                                       
                 BP13K857                                                       
                 BP13K830                                                       
                 BP13L884.                                                      
                                                                                
           IF BP13K740-STATUS NOT = 0                                           
              DISPLAY ' ERROR CLOSING BP13K740 FILE, STATUS '                   
                      BP13K740-STATUS                                           
           END-IF.                                                              
                                                                                
           IF BP13K530-STATUS NOT = 0                                           
              DISPLAY ' ERROR CLOSING BP13K530 FILE, STATUS '                   
                      BP13K530-STATUS                                           
           END-IF.                                                              
                                                                                
           IF BP13K825-STATUS NOT = 0                                           
              DISPLAY ' ERROR CLOSING K825 FILE, STATUS '                       
                     BP13K825-STATUS                                            
           END-IF.                                                              
                                                                                
           IF BP13K830-STATUS NOT = 0                                           
              DISPLAY ' ERROR CLOSING K830 FILE, STATUS '                       
                     BP13K830-STATUS                                            
           END-IF.                                                              
                                                                                
           IF BP13K857-STATUS NOT = 0                                           
              DISPLAY ' ERROR CLOSING K857 FILE, STATUS '                       
                     BP13K857-STATUS                                            
           END-IF.                                                              
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY ' ** BP13C884 ** '.                                          
           DISPLAY ' NO. OF RECORDS READ FROM    BP13F565  = '                  
                     WS-NUM-READ-BP13F565.                                      
           DISPLAY ' NO. OF RECORDS WRITTEN   TO BP13F730  = '                  
                     WS-NUM-WRITE-BP13F730.                                     
           DISPLAY ' NO. OF RECORDS REWRITTEN TO BP13K740  = '                  
                     WS-NUM-REWRITE-BP13K740.                                   
           DISPLAY ' NO. OF RECORDS REWRITTEN TO BP13K530  = '                  
                     WS-NUM-REWRITE-BP13K530.                                   
           DISPLAY ' NO. OF RECORDS REWRITTEN TO BP13K830  = '                  
                     WS-NUM-REWRITE-BP13K830.                                   
           DISPLAY ' NO. OF RECORDS WRITTEN   TO BP13K830  = '                  
                     WS-NUM-WRITE-BP13K830.                                     
           DISPLAY ' '.                                                         
           DISPLAY ' NO. OF RECORDS DELETED FROM BP13K825  = '                  
                     WS-NUM-DELETE-BP13K825.                                    
           DISPLAY ' NO. OF RECORDS REWRITTEN TO BP13K825  = '                  
                     WS-NUM-WRITE-BP13K825.                                     
           DISPLAY ' NO. OF RECORDS DELETED FROM BP13K857  = '                  
                     WS-NUM-DELETE-BP13K857.                                    
           DISPLAY ' NO. OF RECORDS WRITTEN   TO BP13K857  = '                  
                     WS-NUM-WRITE-BP13K857.                                     
           DISPLAY ' NO. OF RECORDS NOT FOUND IN BP13K857  = '                  
                     WS-NUM-NTFND-BP13K857.                                     
           STOP RUN.                                                            
                                                                                
       030-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***********************************************************               
      *  1.  READ BP13K530 FILE USING CURRENT DATE              *               
      *        OR CURRENT DATE - 1                              *               
      *  2.  IF NOT FOUND                                       *               
      *         SUBTRACT 1 FROM DATE AND REPEAT ROUTINE         *               
      *         UNTIL DATE FOUND                                *               
      ***********************************************************               
       100-PROCESS-BP13K530.                                                    
      *-------------------------------------------------------------            
           MOVE WS-SYS-DATE TO K530-KEY-FLD.                                    
           READ BP13K530.                                                       
                                                                                
           IF BP13K530-REC-FOUND                                                
              MOVE 'Y'                TO WS-DATE-FOUND-IND                      
              MOVE K530-NUM-LAST-REGN TO WS-TEMP-REG-NO                         
           ELSE                                                                 
             IF BP13K530-REC-NOTFND                                             
                PERFORM 110-CONV-DATE THRU 110-EXIT                             
                MOVE 'Y' TO WS-K530-CURDTE-NOTFND                               
             ELSE                                                               
                DISPLAY ' ERROR READING BP13K530, STATUS '                      
                          BP13K530-STATUS                                       
                MOVE BP13K530-STATUS  TO RETURN-CODE                            
                PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT                         
             END-IF                                                             
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ****************************************************************          
      *  IF CURRENT DAY IS 1ST DAY OF THE YEAR                       *          
      *     SET CURRENT DATE TO LAST DAY OF LAST YEAR                *          
      *  ELSE IF CURRENT DAY IS 1ST DAY OF THE MONTH                 *          
      *     CALL ROUTINE TO SET CURRENT DATE TO LAST DAY OF LAST MTH *          
      *  ELSE                                                        *          
      *     REDUCE CURRENT DAY BY 1                                  *          
      ****************************************************************          
       110-CONV-DATE.                                                           
      *-------------------------------------------------------------            
           IF WS-SYS-MM = 1 AND WS-SYS-DD = 1                                   
              COMPUTE WS-SYS-YY = WS-SYS-YY - 1                                 
      *       MOVE WS-SYS-YY TO WS-CURR-YEAR                                    
              MOVE 12        TO WS-SYS-MM                                       
              MOVE 31        TO WS-SYS-DD                                       
           ELSE                                                                 
              IF WS-SYS-DD = 1                                                  
                 PERFORM 140-GET-LAST-DAY-OF-LAST-MTH THRU 140-EXIT             
              ELSE                                                              
                 COMPUTE WS-SYS-DD = WS-SYS-DD - 1                              
              END-IF                                                            
           END-IF.                                                              
                                                                                
       110-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ***********************************************************               
      *   COMPUTE LAST MONTH  THEN                              *               
      *   SEARCH THE MONTH TABLE TO GET LAST DAY OF LAST MONTH  *               
      ***********************************************************               
       140-GET-LAST-DAY-OF-LAST-MTH.                                            
      *-------------------------------------------------------------            
      ** COMPUTE FOR LAST MONTH **                                              
                                                                                
           COMPUTE WS-SYS-MM = WS-SYS-MM - 1.                                   
                                                                                
      ** SEARCH THE TABLE **                                                    
                                                                                
           SET WS-MLDAY-IDX TO 1.                                               
                                                                                
           SEARCH WS-MTHLDAY-REC                                                
               AT END MOVE 0 TO WS-SYS-DD                                       
                  DISPLAY ' ERROR IN OBTAINING LAST DAY OF MONTH '              
                          WS-SYS-MM                                             
                  PERFORM 030-CLOSE-ROUTINE                                     
             WHEN WS-MTH(WS-MLDAY-IDX) =  WS-SYS-MM                             
                  MOVE WS-LDAY(WS-MLDAY-IDX) TO WS-SYS-DD                       
           END-SEARCH.                                                          
                                                                                
      * COMPUTE TO CHECK FOR LEAP YEAR *                                        
                                                                                
           DIVIDE WS-YEAR BY 4 GIVING WS-RESULT                                 
                               REMAINDER WS-REM.                                
      *                                                                         
      *  IF LEAP YEAR AND LAST MONTH IS FEB, LET LAST DAY BE 29                 
      *                                                                         
           IF WS-REM = 0 AND WS-SYS-MM = 2                                      
              MOVE 29 TO WS-SYS-DD                                              
           END-IF.                                                              
                                                                                
       140-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ***********************************************************               
      *  1.  READ BP13F565 FILE AT END EXIT                     *               
      *  2.  IF (F565-CDE-UNSUCC NOT = SPACES AND LOW-VALUES)   *               
      *       AND FLAT-TYPE NOT = 3 OR 4S                       *               
      *         ASSIGN REGISTRATION NUMBER                      *               
      ***********************************************************               
       200-PROCESS-BP13F565.                                                    
      *-------------------------------------------------------------            
           READ BP13F565  AT END                                                
                MOVE 'Y' TO EOF-BP13F565-IND                                    
                GO TO 200-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-NUM-READ-BP13F565.                                       
                                                                                
           MOVE 'N'                       TO WS-K825-UPDATED.                   
           MOVE F565-KEY-FLD              TO K740-KEY-FLD.                      
                                                                                
           PERFORM 300-UPDATE-BP13K740    THRU  300-EXIT.                       
           PERFORM 400-UPDATE-BP13K830    THRU  400-EXIT.                       
           PERFORM 500-UPDATE-BP13K825                                          
              THRU 500-EXIT                                                     
             UNTIL WS-K825-UPDATED = 'Y'.                                       
           PERFORM 550-WRITE-BP13F730     THRU  550-EXIT.                       
           PERFORM 560-UPDATE-BP13K857    THRU  560-EXIT.                       
                                                                                
           PERFORM 600-PRINT-CONTROL-LIST THRU  600-EXIT.                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ***********************************************************               
      *  1.  INCREMENT LAST REG NO.                             *               
      *  2.  ASSIGN REGISTRATION NUMBER AND UPDATE              *               
      *      BP13K740 WITH CURRENT DATE AND REG. NO.            *               
      ***********************************************************               
       300-UPDATE-BP13K740.                                                     
      *-------------------------------------------------------------            
           READ BP13K740.                                                       
                                                                                
           IF BP13K740-STATUS = 23                                              
               MOVE BP13K740-STATUS TO RETURN-CODE                              
               DISPLAY 'BP13K740 RECORD NOT FOUND' BP13K740-STATUS              
            END-IF.                                                             
                                                                                
           IF BP13K740-STATUS NOT = 00 AND 23                                   
               DISPLAY 'ERROR READING BP13K740'    BP13K740-STATUS              
               PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT                          
            END-IF.                                                             
                                                                                
           ADD 1 TO WS-TEMP-REG-NUM.                                            
           PERFORM 320-FIND-CHK-DGT THRU 320-EXIT.                              
                                                                                
           MOVE WS-TEMP-REG-NO   TO F565-NUM-REGN                               
                                    K740-NUM-REGN.                              
                                                                                
           IF K740-DTE-REQUEST = SPACES OR LOW-VALUES OR ALL '0'                
              MOVE WS-CURR-CCYYMMDD TO K740-DTE-REQUEST                         
           END-IF                                                               
           MOVE 'R'              TO K740-CDE-PRINT-STATUS.                      
                                                                                
           IF K740-CDE-JB = '8'                                                 
              PERFORM 310-CHECK-SAME-JB-RANDOM  THRU 310-EXIT                   
           END-IF.                                                              
                                                                                
           REWRITE BP13K740-REC.                                                
                                                                                
           IF BP13K740-WRITE-OK                                                 
              ADD 1 TO WS-NUM-REWRITE-BP13K740                                  
              DISPLAY  'REG-NO '    K740-NUM-REGN                               
                       '--------------------------------------------'           
           ELSE                                                                 
              DISPLAY ' ERROR REWRITING BP13K740, STATUS '                      
                  BP13K740-STATUS                                               
              MOVE BP13K740-STATUS TO RETURN-CODE                               
              DISPLAY  'REGN NO' WS-TEMP-REG-NO                                 
              PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT                           
           END-IF.                                                              
                                                                                
           IF WS-K530-CURDTE-NOTFND = 'Y'                                       
              MOVE SPACES TO BP13K530-REC                                       
              MOVE WS-CURR-CCYYMMDD TO K530-KEY-FLD                             
                                       WS-SYS-DATE                              
              MOVE WS-TEMP-REG-NO TO K530-NUM-FIRST-REGN                        
                                     K530-NUM-LAST-REGN                         
              WRITE BP13K530-REC                                                
              MOVE SPACE TO WS-K530-CURDTE-NOTFND                               
              IF BP13K530-STATUS NOT = 0                                        
                  MOVE BP13K530-STATUS TO RETURN-CODE                           
                  DISPLAY 'WRITE ERROR - BP13K530 ' BP13K530-STATUS             
                  PERFORM  030-CLOSE-ROUTINE THRU 030-EXIT                      
              ELSE                                                              
                  ADD 1 TO  WS-NUM-WRITE-BP13K530                               
              END-IF                                                            
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
      *     CHECK SAME-JB-RANDOM NUMBER IF SO UPDATE THE         *              
      *     K740-NUM-SEQUENCE AND K740-NUM-JB-PARENT-REGN WITH   *              
      *     THE SMALLEST REGN NUMBER IN THE GROUP                *              
      ************************************************************              
       310-CHECK-SAME-JB-RANDOM.                                                
      *-------------------------------------------------------------            
                                                                                
           IF F565-NUM-RANDOM-JT-ALLOC = WS-JT-RANDOM                           
              MOVE WS-SMALLEST-REGN TO K740-NUM-SEQUENCE                        
                                       K740-NUM-JB-PARENT-REGN                  
           ELSE                                                                 
             MOVE F565-NUM-RANDOM-JT-ALLOC TO  WS-JT-RANDOM                     
             MOVE WS-TEMP-REG-NO           TO  WS-SMALLEST-REGN                 
             MOVE WS-SMALLEST-REGN         TO  K740-NUM-SEQUENCE                
                                               K740-NUM-JB-PARENT-REGN          
           END-IF.                                                              
                                                                                
       310-EXIT.                                                                
            EXIT.                                                               
                                                                                
                                                                                
      ************************************************************              
      *     CREATE REGISTRATION NUMBER CHECK DIGIT               *              
      ************************************************************              
       320-FIND-CHK-DGT.                                                        
      *-------------------------------------------------------------            
            MOVE WS-TEMP-REG-NO  TO WS-REG.                                     
            MOVE WS-REG          TO WS-REGN-NUMERIC.                            
            COMPUTE WS-FLD1 = REGN-NUM (1) * 2 + REGN-NUM (2) * 7 +             
                              REGN-NUM (3) * 6 + REGN-NUM (4) * 5 +             
                              REGN-NUM (5) * 4 + REGN-NUM (6) * 3 +             
                              REGN-NUM (7) * 2.                                 
            DIVIDE WS-FLD1 BY 11 GIVING WS-FLD2 REMAINDER WS-FLD3               
                COMPUTE WS-FLD3 = 11 - WS-FLD3.                                 
            SET WS-CDG TO 1.                                                    
                                                                                
            SEARCH REGN-TAB                                                     
                   AT END GO TO 320-EXIT                                        
                   WHEN REGN-REMAINDER (WS-CDG) = WS-FLD3                       
                        MOVE REGN-CDG (WS-CDG) TO WS-CHK-DGT                    
            END-SEARCH.                                                         
                                                                                
            MOVE WS-CHK-DGT TO WS-TEMP-CHK-DGT.                                 
                                                                                
       320-EXIT.                                                                
            EXIT.                                                               
                                                                                
                                                                                
      ***********************************************************               
      *      UPDATE BP13K830 FILE                               *               
      ***********************************************************               
       400-UPDATE-BP13K830.                                                     
      *-------------------------------------------------------------            
      * CDE-NT-WITH-LAND USED TO DISTINGUISH BETWEEN BOOKING AND*               
      *   WIS.                                                  *               
      *---------------------------------------------------------*               
      * AS OF DEC 94, REQUESTS UNDER BOOKING AND RFS HAS VALUE  *               
      * OF CDE-NT-WITH-LAND AS:                                 *               
      *                                                         *               
      *   'Y' FOR BOOKING RESERVE LIST IN NEW ESTATE            *               
      *                                                         *               
      *   'N' FOR BOOKING RESERVE LIST IN MATURE ESTATE         *               
      *                                                         *               
      *   'S' FOR SST REQUESTS                                  *               
      *                                                         *               
      *   ' ' FOR WIS / SURF REQUESTS                           *               
      *                                                         *               
      *---------------------------------------------------------*               
           MOVE SPACES                    TO BP13K830-REC.                      
                                                                                
           MOVE F565-NUM-REF    TO  K830-NUM-REF.                               
           READ BP13K830.                                                       
                                                                                
           IF BP13K830-STATUS NOT = 00 AND 02 AND 23                            
               MOVE BP13K830-STATUS TO RETURN-CODE                              
               DISPLAY 'ERROR READING BP13K830 '    BP13K830-STATUS             
                      ' KEY IS ' K830-KEY-FLD                                   
               PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT                          
           END-IF.                                                              
                                                                                
           IF BP13K830-STATUS = 00 OR 02                                        
              IF K830-NUM-REGN (1:1) = '#' OR SPACES OR LOW-VALUES              
                 MOVE K740-NUM-REGN TO K830-NUM-REGN                            
                                                                                
                 REWRITE BP13K830-REC                                           
                                                                                
                 IF BP13K830-STATUS = 00 OR 02                                  
                   ADD 1 TO WS-NUM-REWRITE-BP13K830                             
                 ELSE                                                           
                   DISPLAY 'ERROR REWRITING BP13K830 ' BP13K830-STATUS          
                          ' KEY IS ' K830-KEY-FLD                               
                   MOVE BP13K830-STATUS TO RETURN-CODE                          
                   PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT                      
                 END-IF                                                         
              ELSE                                                              
                 MOVE K740-NUM-REGN TO K830-NUM-REGN                            
                                       K830-KEY-FLD                             
                 PERFORM 410-WRITE-BP13K830 THRU 410-EXIT                       
                 DISPLAY 'REG-NO ' K830-NUM-REGN                                
                     ' --> CREATED WITH ADDRESS.'                               
              END-IF                                                            
           ELSE                                                                 
              IF BP13K830-STATUS = 23                                           
                 MOVE SPACES        TO BP13K830-REC                             
                 MOVE K740-NUM-REGN TO K830-NUM-REGN                            
                                       K830-KEY-FLD                             
                 PERFORM 410-WRITE-BP13K830 THRU 410-EXIT                       
                 DISPLAY 'REG-NO ' K830-NUM-REGN                                
                     ' --> CREATED WITHOUT ADDRESS! PLEASE UPDATE <--'          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ***********************************************************               
      *      WRITE  BP13K830 FILE                               *               
      ***********************************************************               
       410-WRITE-BP13K830.                                                      
      *-------------------------------------------------------------            
           WRITE BP13K830-REC.                                                  
                                                                                
           IF BP13K830-STATUS = 00                                              
             ADD      1          TO WS-NUM-WRITE-BP13K830                       
           ELSE                                                                 
             DISPLAY 'ERROR WRITING BP13K830 ' BP13K830-STATUS                  
                    ' KEY IS ' K830-KEY-FLD                                     
             MOVE BP13K830-STATUS      TO   RETURN-CODE                         
             PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT                            
           END-IF.                                                              
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ***********************************************************               
      *      UPDATE BP13K825 FILE                               *               
      ***********************************************************               
       500-UPDATE-BP13K825.                                                     
      *-------------------------------------------------------------            
      * N940017    SOC PH9                                      *               
      *---------------------------------------------------------*               
      * UNDER RFS (OCT 94 ONWARDS),                             *               
      *    ORIGINAL OCCUPIER RECS (BP13K825) WILL BE DELETED,   *               
      *    AND REPLACED BY A NEW OCCUPIER REC WITH THE KEY OF   *               
      *    THE NEW REGNO.                                       *               
      *                                                         *               
      *    THIS IS TO BRING SUCCESSSFUL REQUESTS (OTHER THAN    *               
      *    THOSE OF RFS) TO BE IN SYCHN WITH RFS.               *               
      *                                                         *               
      * NOTE:                                                   *               
      *   'Y' FOR BOOKING RESERVE LIST IN NEW ESTATE            *               
      *   'N' FOR BOOKING RESERVE LIST IN MATURE ESTATE         *               
      *   'S' FOR SST REQUESTS                                  *               
      *   ' ' FOR WIS / SURF REQUESTS                           *               
      *                                                         *               
      *---------------------------------------------------------*               
           MOVE SPACES                    TO BP13K825-REC.                      
                                                                                
           MOVE F565-NUM-REF           TO K825-NUM-REF.                         
           START BP13K825 KEY > K825-KEY-FLD.                                   
                                                                                
           IF BP13K825-STATUS NOT = 00 AND 02                                   
              MOVE 'Y'                    TO WS-K825-UPDATED                    
              GO TO 500-EXIT.                                                   
                                                                                
       500-READNEXT-BP13K825.                                                   
      *-------------------------------------------------------------            
           READ BP13K825 NEXT AT END                                            
                MOVE 'Y'                  TO WS-K825-UPDATED                    
                GO TO 500-EXIT.                                                 
                                                                                
           IF K825-NUM-REF NOT = K740-NUM-REF                                   
               MOVE 'Y'                   TO WS-K825-UPDATED                    
               GO TO 500-EXIT                                                   
                                                                                
           ELSE                                                                 
              DELETE BP13K825                                                   
              ADD 1 TO WS-NUM-DELETE-BP13K825                                   
                                                                                
              MOVE F565-NUM-REGN          TO K825-NUM-REGN                      
              WRITE BP13K825-REC                                                
                                                                                
              IF BP13K825-STATUS = 00 OR 02                                     
                 ADD 1 TO WS-NUM-WRITE-BP13K825                                 
                                                                                
              ELSE                                                              
                 DISPLAY 'ERROR REWRITING BP13K825 '  BP13K825-STATUS           
                        ' KEY IS ' K825-KEY-FLD                                 
                 MOVE BP13K825-STATUS  TO RETURN-CODE                           
                 PERFORM 030-CLOSE-ROUTINE.                                     
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***********************************************************               
      *      WRITE BP13F730 FILE                                *               
      ***********************************************************               
       550-WRITE-BP13F730.                                                      
                                                                                
           MOVE SPACES               TO BP13F730-REC.                           
           MOVE BP13K740-REC(29:472) TO BP13F730-REC(29:472).                   
           MOVE K740-NUM-REGN        TO F730-NUM-REF                            
                                        F730-NUM-REGN.                          
                                                                                
           WRITE                   BP13F730-REC.                                
           ADD  1               TO WS-NUM-WRITE-BP13F730.                       
                                                                                
       550-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***********************************************************               
      *      UPDATE BP13K857 FILE                               *               
      ***********************************************************               
       560-UPDATE-BP13K857.                                                     
      *-------------------------------------------------------------            
           MOVE SPACES                    TO BP13K857-REC.                      
                                                                                
           MOVE F565-NUM-REF    TO  K857-NUM-REGN.                              
           READ BP13K857.                                                       
                                                                                
           EVALUATE BP13K857-STATUS                                             
              WHEN 00                                                           
              WHEN 02                                                           
                   PERFORM 561-DELETE-BP13K857 THRU 561-EXIT                    
                                                                                
              WHEN 23                                                           
                   DISPLAY 'BP13K857-REC NOT FOUND ' BP13K857-STATUS            
                     ' KEY IS '    K857-KEY-FLD                                 
                   ADD 1 TO WS-NUM-NTFND-BP13K857                               
                                                                                
              WHEN OTHER                                                        
                   MOVE BP13K857-STATUS TO RETURN-CODE                          
                   DISPLAY 'ERROR READING BP13K857 '    BP13K857-STATUS         
                          ' KEY IS ' K857-KEY-FLD                               
                   PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       560-EXIT.                                                                
           EXIT.                                                                
                                                                                
       561-DELETE-BP13K857.                                                     
           DELETE BP13K857.                                                     
                                                                                
           EVALUATE BP13K857-STATUS                                             
             WHEN 00                                                            
                  ADD 1 TO WS-NUM-DELETE-BP13K857                               
                  PERFORM 562-ADD-BP13K857 THRU 562-EXIT                        
             WHEN OTHER                                                         
                  DISPLAY 'DELETE BP13K857 ERROR ' K857-NUM-REGN                
                           K857-NUM-REF                                         
                  MOVE BP13K857-STATUS TO RETURN-CODE                           
                  PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       561-EXIT.                                                                
           EXIT.                                                                
                                                                                
       562-ADD-BP13K857.                                                        
           MOVE F565-NUM-REGN          TO K857-NUM-REGN                         
                                          K857-NUM-REF                          
                                                                                
           WRITE BP13K857-REC                                                   
           EVALUATE BP13K857-STATUS                                             
              WHEN  00                                                          
              WHEN  02                                                          
                    ADD 1 TO WS-NUM-WRITE-BP13K857                              
              WHEN  OTHER                                                       
                    DISPLAY 'ERROR WRITING BP13K857 ' BP13K857-STATUS           
                           ' KEY IS ' K857-KEY-FLD                              
                    MOVE BP13K857-STATUS TO RETURN-CODE                         
                    PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       562-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ***********************************************************               
      *      PRINT CONTROL LIST                                 *               
      ***********************************************************               
       600-PRINT-CONTROL-LIST.                                                  
      *-------------------------------------------------------------            
           IF WS-LINECT > 50                                                    
              PERFORM 650-PRINT-HEADING                                         
           END-IF.                                                              
                                                                                
           ADD  1 TO WS-SEQCT.                                                  
                                                                                
           MOVE SPACES           TO BP13K825-REC.                               
           MOVE F565-NUM-REGN    TO K825-NUM-REGN.                              
           MOVE F565-NUM-NRIC1   TO K825-NUM-NRIC.                              
           READ BP13K825.                                                       
                                                                                
           IF BP13K825-STATUS = 00                                              
             MOVE K825-NME-OCCP     TO NAME                                     
           ELSE                                                                 
              IF BP13K825-STATUS = 23                                           
                DISPLAY 'K825-NOTFND' F565-NUM-NRIC1                            
                MOVE SPACES  TO NAME                                            
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K825' BP13K825-STATUS               
                        ' KEY IS ' K825-KEY-FLD                                 
                 PERFORM 030-CLOSE-ROUTINE THRU 030-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F565-NUM-NRIC2 (2:1) NOT = '#'                                    
              MOVE F565-NUM-NRIC2  TO NRIC-2                                    
           ELSE                                                                 
              MOVE SPACES          TO NRIC-2                                    
           END-IF.                                                              
                                                                                
           MOVE WS-SEQCT            TO SERIAL-NO.                               
           MOVE K740-NUM-REGN       TO REGN-NO.                                 
           MOVE F565-NUM-REF        TO CUR-RN.                                  
           MOVE F565-NUM-PREV-REF   TO PREV-RN.                                 
           MOVE F565-NUM-NRIC1      TO NRIC-1.                                  
           MOVE F565-CDE-CATEGORY   TO CAT.                                     
           MOVE F565-CDE-JB         TO JB-CODE.                                 
           MOVE F565-CDE-ALLOC-SCH  TO ALLOC-SCH.                               
           MOVE F565-CDE-ELIG-SCH   TO L-CDE-ELIG.                              
           MOVE F565-CDE-NT1        TO NT-CODE.                                 
                                                                                
           IF   F565-CDE-FLAT-TYPE = '3 ' AND F565-NUM-BUYBACK = 'Y'            
                MOVE '3B'           TO FT-TYPE                                  
           ELSE                                                                 
                MOVE F565-CDE-FLAT-TYPE  TO FT-TYPE                             
           END-IF.                                                              
                                                                                
           MOVE F565-NUM-JB-REF     TO JB-REF.                                  
      *                                                                         
           IF F565-NUM-PREV-REF = SPACES OR LOW-VALUES                          
               MOVE SPACES              TO PREV-RN                              
           ELSE                                                                 
               MOVE F565-NUM-PREV-REF   TO PREV-RN                              
           END-IF.                                                              
                                                                                
           MOVE F565-NUM-LOW-FLOOR  TO LOW-FLOOR.                               
                                                                                
           MOVE F565-NUM-RANDOM     TO RANDOM-NO.                               
                                                                                
           ADD  2                   TO WS-LINECT.                               
           WRITE BP13L884-PRT-REC   FROM L884-DTL1  AFTER 2.                    
           MOVE SPACES              TO BP13L884-PRT-REC.                        
                                                                                
           ADD  1                   TO WS-LINECT.                               
           WRITE BP13L884-PRT-REC   FROM L884-DTL2  AFTER 1.                    
           MOVE SPACES              TO BP13L884-PRT-REC.                        
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       650-PRINT-HEADING.                                                       
      *-------------------------------------------------------------            
           ADD   1 TO WS-PAGECT.                                                
           MOVE  1 TO WS-LINECT.                                                
           MOVE  WS-PAGECT TO HDG1-PAGECT.                                      
           WRITE BP13L884-PRT-REC FROM L884-HDG1 AFTER PAGE.                    
           WRITE BP13L884-PRT-REC FROM L884-HDG2 AFTER 1.                       
           WRITE BP13L884-PRT-REC FROM L884-HDG3 AFTER 2.                       
           WRITE BP13L884-PRT-REC FROM L884-HDG4 AFTER 2.                       
           WRITE BP13L884-PRT-REC FROM L884-HDG5 AFTER 1.                       
           MOVE  SPACES       TO BP13L884-PRT-REC.                              
           WRITE BP13L884-PRT-REC.                                              
                                                                                
       650-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ***********************************************************               
      *      UPDATE BP13K530 FILE                               *               
      ***********************************************************               
       700-UPDATE-BP13K530.                                                     
      *-------------------------------------------------------------            
           MOVE WS-SYS-DATE TO K530-KEY-FLD.                                    
           READ BP13K530.                                                       
                                                                                
           IF BP13K530-STATUS NOT = 0                                           
               MOVE BP13K530-STATUS TO RETURN-CODE                              
               DISPLAY 'READ ERROR - BP13K530 ' BP13K530-STATUS                 
               PERFORM  030-CLOSE-ROUTINE THRU 030-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE WS-TEMP-REG-NO TO K530-NUM-LAST-REGN.                           
                                                                                
           REWRITE BP13K530-REC.                                                
                                                                                
           IF BP13K530-STATUS NOT = 0                                           
               MOVE BP13K530-STATUS TO RETURN-CODE                              
               DISPLAY 'REWRITE ERROR - BP13K530 ' BP13K530-STATUS              
               PERFORM  030-CLOSE-ROUTINE THRU 030-EXIT                         
           ELSE                                                                 
               ADD 1 TO  WS-NUM-REWRITE-BP13K530                                
           END-IF.                                                              
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
