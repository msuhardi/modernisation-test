       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C27l.                                                 
      *AUTHOR.        KARL MAGALONA CABUG.                                      
      *DATE-WRITTEN.  02 NOV 2017.                                              
      *============================================================*            
      * BP13 - SYSTEM OF COMMITMENT                                *            
      *============================================================*            
      * OBJECTIVE :  To match BP13KL0A to BP13KL00 based on ACK-BY.*            
      *              Update BP13KL00 when kl00-num-ack-by is empty *            
      *              and fl0a-num-ack-by is not empty.             *            
      * INPUT     :  BP13FL0A                                      *            
      *   I-O     :  BP13KL00                                      *            
      *                                                            *            
      *----------------------------------------------------------- *            
      * CHG REF  DATE     BY     DESCRIPTION                       *            
      * -------- -------- ----   ------------------------------    *            
      * BP136951 02112017 KAM4   NEW PROGRAM.                      *            
      *============================================================*            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
      *-------------------------------------------------------------            
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13FL0A ASSIGN       TO BP13FL0A.                            
                                                                                
           SELECT BP13KL00 ASSIGN       TO BP13KL00                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS KL00-KEY-FLD                         
                           FILE STATUS  IS WS-KL00-STATUS.                      
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13FL0A                                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           RECORDING  MODE  IS F.                                               
       01  BP13FL0A-LETTER.                                                     
           05 FL0A-KEY-FLD.                                                     
              10 FL0A-NUM-REGN               PIC X(8).                          
              10 FL0A-NUM-STAGE              PIC X(5).                          
              10 FL0A-NUM-DOC-TYPE           PIC X(5).                          
           05 FILLER                         PIC X(33).                         
           05 FL0A-DTE-ACK                   PIC X(8).                          
           05 FL0A-TME-ACK                   PIC X(6).                          
           05 FILLER                         PIC X(84).                         
           05 FL0A-NUM-ACK-BY                PIC X(9).                          
           05 FILLER                         PIC X(42).                         
                                                                                
       FD  BP13KL00                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
       COPY BP13KL00.                                                           
                                                                                
      *------------------------------------------------------------*            
       WORKING-STORAGE SECTION.                                                 
      *------------------------------------------------------------*            
       01  WS-CURRENT-DATE               PIC 9(08).                             
       01  WS-CURR-DATE                  PIC 9(08).                             
                                                                                
       01  WS-COUNTER.                                                          
           05 WS-FL0A-READ-CTR           PIC 9(06) VALUE ZEROES.                
           05 WS-KL00-UPDT-CTR           PIC 9(06) VALUE ZEROES.                
           05 WS-KL00-NTFND-CTR          PIC 9(06) VALUE ZEROES.                
                                                                                
       01  WS-OTHER-VAR.                                                        
           05  WS-KL00-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-KL00-NOTFND            PIC 9(06)  VALUE ZEROES.               
           05  WS-KL00-FND               PIC X(01)  VALUE SPACES.               
           05  WS-KL0A-FND               PIC X(01)  VALUE SPACES.               
           05  WS-FL0A-EOF               PIC X(01)  VALUE SPACES.               
           05  WS-DATE-DUE-INT           PIC S9(09).                            
           05  WS-NUM-DTE-DUE            PIC 9(08).                             
      *------------------------------------------------------------*            
       PROCEDURE DIVISION.                                                      
      *------------------------------------------------------------*            
       0000-MAIN.                                                               
      *------------------------------------------------------------*            
           PERFORM 1000-OPEN-FILES            THRU 1000-EXIT.                   
           PERFORM 2000-READ-BP13FL0A         THRU 2000-EXIT.                   
           PERFORM 3000-MAIN-PROCESS          THRU 3000-EXIT                    
                   UNTIL WS-FL0A-EOF = 'Y'.                                     
                                                                                
           PERFORM 9000-CLOSE-FILES           THRU 9000-EXIT.                   
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT BP13FL0A                                                  
                  I-O BP13KL00.                                                 
                                                                                
           IF WS-KL00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, BP13KL00-STATUS = ' WS-KL00-STATUS        
              MOVE     WS-KL00-STATUS              TO RETURN-CODE               
              PERFORM  9000-CLOSE-FILES          THRU 9000-EXIT                 
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13FL0A.                                                      
      *-------------------------------------------------------------            
           READ BP13FL0A                                                        
            AT END                                                              
               MOVE 'Y'                        TO WS-FL0A-EOF                   
               GO TO 2000-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD 1  TO WS-FL0A-READ-CTR.                                          
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3000-MAIN-PROCESS.                                                       
      *------------------------------------------------------------*            
           MOVE 'N'                              TO WS-KL00-FND.                
           MOVE FL0A-KEY-FLD                     TO KL00-KEY-FLD.               
           PERFORM 3100-READ-BP13KL00          THRU 3100-EXIT.                  
                                                                                
           IF WS-KL00-FND = 'Y'                                                 
              IF (KL00-NUM-ACK-BY     = SPACES OR  LOW-VALUES) AND              
                 (FL0A-NUM-ACK-BY NOT = SPACES AND LOW-VALUES)                  
                  PERFORM 3200-REWRITE-BP13KL00 THRU 3200-EXIT                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13FL0A          THRU 2000-EXIT.                  
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-READ-BP13KL00.                                                      
      *-------------------------------------------------------------            
           READ BP13KL00                                                        
                                                                                
           EVALUATE WS-KL00-STATUS                                              
               WHEN 00                                                          
                    MOVE 'Y'                     TO WS-KL00-FND                 
               WHEN 23                                                          
                    ADD 1                        TO WS-KL00-NTFND-CTR           
                    DISPLAY 'KL00 - REC NOTFND : ' KL00-KEY-FLD                 
               WHEN OTHER                                                       
                    DISPLAY 'BP13KL00 - READ FILE ERROR, KEY: '                 
                            KL00-KEY-FLD                                        
                    MOVE WS-KL00-STATUS          TO RETURN-CODE                 
                    PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                   
           END-EVALUATE.                                                        
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3200-REWRITE-BP13KL00.                                                   
      *-------------------------------------------------------------            
           MOVE FL0A-DTE-ACK                TO KL00-DTE-ACK.                    
           MOVE FL0A-TME-ACK                TO KL00-TME-ACK.                    
           MOVE FL0A-NUM-ACK-BY             TO KL00-NUM-ACK-BY.                 
                                                                                
           REWRITE BP13KL00-LETTER.                                             
                                                                                
           IF WS-KL00-STATUS = ZEROS                                            
              ADD 1                         TO WS-KL00-UPDT-CTR                 
           ELSE                                                                 
              DISPLAY 'BP13KL00 - REWRITE ERROR, STATUS : '                     
                       WS-KL00-STATUS ' KEY : ' KL00-KEY-FLD                    
              MOVE WS-KL00-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                        
           END-IF.                                                              
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           DISPLAY '-------------BP13C27L CONTROL TOTAL -----------'.           
           DISPLAY 'NO OF RECORDS READ  (BP13FL0A) : ' WS-FL0A-READ-CTR         
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF REC(S) NOTFND (BP13KL00) : ' WS-KL00-NTFND-CTR        
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORD UPDATED(BP13KL00) : ' WS-KL00-UPDT-CTR         
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13FL0A                                                       
                 BP13KL00.                                                      
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
