       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C772.                                                 
       AUTHOR.        TON.                                                      
       DATE-WRITTEN.  07/12/92.                                                 
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *      1.  THIS IS A PROGRAM TO PRINT REPORT TO SHOW THE *                
      *          DETAIL REPORT OF RELOCATION CASES THAT DID    *                
      *          NOT BOOK FLAT ( SORTED BY BO )                *                
      *                                                        *                
      *     INPUT FILES:                                       *                
      *      1. BP13F800.                                      *                
      *      2. BP13K820.                                      *                
      *      3. BP13K830.                                      *                
      *                                                        *                
      *     OUTPUT FILES:                                      *                
      *     1.  BP13L772.                                      *                
      *                                                        *                
      * CHGE    BY   ON     DESCRIPTION                        *                
      * ------- ---- ------ -----------                        *                
      * N930012 TON  XX0793 SOC PH8 - COBOL 2                  *                
      *BP130078 ATH  160196 REGISTRATION DATE SHOULD BE WITHIN *                
      *                     2 MONTHS                           *                
      *BP130418 SIA  230798 CATER FOR YR2000                   *                
      *BP130657 LSB  140499 DTE-REGN WITHIN 2 MTH , 6 BYTES    *                
      *BP130671 LSB  250699 H/HOLD TYPE/10 YEAR CHECK FOR RELO-*                
      *                     CATION APPLICATIONS                *                
      *BP130790 SCK  020200 TO ADD IN IN-SOURCE DOCUMENTATION  *                
      *BP131050 ANC1 210701 ADD F800-NUM-ALLO-SCHEME = 'RED'   *                
      **********************************************************                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                ACCESS MODE  IS RANDOM                                          
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K820-KEY-FLD                                    
                FILE STATUS  IS BP13K820-STATUS.                                
                                                                                
           SELECT BP13K830 ASSIGN TO BP13K830                                   
                ACCESS MODE  IS RANDOM                                          
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K830-KEY-FLD                                    
               ALTERNATE KEY IS K830-NUM-REGN                                   
                FILE STATUS  IS BP13K830-STATUS.                                
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
           SELECT BP13L772 ASSIGN TO BP13L772.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F800                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD  BP13K820                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 400 CHARACTERS                                       
           LABEL RECORD IS STANDARD.                                            
       COPY BP13K820.                                                           
                                                                                
       FD  BP13K830                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 250 CHARACTERS                                       
           LABEL RECORD IS STANDARD.                                            
       COPY BP13K830.                                                           
                                                                                
       FD  BP13L772                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  BP13L772-REC                PIC X(132).                              
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-VARIABLES.                                                        
           05  BP13K820-STATUS           PIC 9(2)  VALUE ZEROS.                 
           05  BP13K830-STATUS           PIC 9(2)  VALUE ZEROS.                 
                                                                                
           05  WS-CNT-PROCESS            PIC 9(8)  VALUE ZEROS.                 
           05  WS-CNT-F800-READ          PIC 9(8)  VALUE ZEROS.                 
           05  WS-CNT-K820-READ          PIC 9(8)  VALUE ZEROS.                 
           05  WS-CNT-K830-READ          PIC 9(8)  VALUE ZEROS.                 
           05  WS-BRANCH-OFFICE          PIC X(2)  VALUE SPACES.                
           05  WS-SERIAL-NO              PIC 9(4)  VALUE ZEROS.                 
           05  WS-CNT-PRINTED            PIC 9(8)  VALUE ZEROS.                 
           05  WS-LINE                   PIC 9(2)  VALUE 70.                    
           05  WS-PAGE                   PIC 9(4)  VALUE ZEROS.                 
                                                                                
           05 WS-F800-EOF                PIC X     VALUE 'N'.                   
              88 F800-EOF                          VALUE 'Y'.                   
                                                                                
       01  WS-CURR-DATE.                                                        
           05 WS-CUR-YY                  PIC X(4).                              
           05 WS-CUR-MM                  PIC XX.                                
           05 WS-CUR-DD                  PIC XX.                                
                                                                                
       01  WS-EDIT-DATE.                                                        
           05 WS-CUR-DD                  PIC XX.                                
           05 FILLER                     PIC X  VALUE '/'.                      
           05 WS-CUR-MM                  PIC XX.                                
           05 FILLER                     PIC X  VALUE '/'.                      
           05 WS-CUR-YY                  PIC X(4).                              
                                                                                
       01  WS-SYS-DATE.                                                         
           05 WS-DATE.                                                          
              10 WS-YY                     PIC X(04).                           
              10 WS-YY-R REDEFINES WS-YY   PIC 9(04).                           
              10 WS-MM                     PIC X(02).                           
                 88 WS-JANUARY                       VALUE '01'.                
              10 WS-MM-R REDEFINES WS-MM   PIC 9(02).                           
              10 WS-DD                     PIC X(02).                           
                                                                                
       01  WS-PREVIOUS-DATE                PIC X(08).                           
       01  WS-CURRENT-DATE                 PIC X(08).                           
       01  WS-REM                          PIC 9(3)  VALUE ZEROES.              
       01  WS-YEAR                         PIC 9(4)  VALUE ZEROES.              
                                                                                
       01  DATE-REC.                                                            
           05  CAL-DATE.                                                        
               10  CAL-YEAR                 PIC 9(4).                           
                   88  VALID-YEAR               VALUE 1900 THRU 2100.           
               10  CAL-MONTH                PIC 9(2).                           
                   88  31-DAY-MONTH             VALUE 01, 03, 05, 07,           
                                                      08, 10, 12.               
                   88  30-DAY-MONTH             VALUE 04, 06, 09, 11.           
                   88  28-29-DAY-MONTH          VALUE 02.                       
                   88  VALID-MONTH              VALUE 01 THRU 12.               
               10  CAL-DAY                  PIC 9(2).                           
                   88  VALID-DAY                VALUE 01 THRU 31.               
                                                                                
      **********************************************************                
      *              REPORT FORMAT                             *                
      **********************************************************                
                                                                                
       01  L772-HEAD1.                                                          
           05 FILLER                     PIC X(8)  VALUE 'BP13L772'.            
           05 FILLER                     PIC X(33) VALUE SPACES.                
           05 FILLER                     PIC X(41) VALUE                        
              'S Y S T E M   O F   C O M M I T E M E N T'.                      
           05 FILLER                     PIC X(14) VALUE SPACES.                
           05 FILLER                     PIC X(7)  VALUE 'DATE : '.             
           05 L772-DATE                  PIC X(10).                             
           05 FILLER                     PIC X(5)  VALUE SPACES.                
           05 FILLER                     PIC X(7)  VALUE 'PAGE : '.             
           05 L772-PAGE                  PIC ZZZ9.                              
                                                                                
       01  L772-HEAD2.                                                          
           05 FILLER                     PIC X(38) VALUE SPACES.                
           05 FILLER                     PIC X(46) VALUE                        
              'DETAIL REPORT OF RELOCATION CASES THAT DID NOT'.                 
                                                                                
       01  L772-HEAD3.                                                          
           05 FILLER                     PIC X(50) VALUE SPACES.                
           05 FILLER                     PIC X(24) VALUE                        
              'BOOK FLAT (SORTED BY BO)'.                                       
                                                                                
       01  L772-HEAD4.                                                          
           05 FILLER                     PIC X(16) VALUE                        
              'BRANCH OFFICE : '.                                               
           05 L772-BRANCH-OFFICE         PIC X(2).                              
                                                                                
                                                                                
       01  L772-HEAD5.                                                          
           05 FILLER                     PIC X(4)  VALUE 'S/NO'.                
           05 FILLER                     PIC X(4)  VALUE SPACES.                
           05 FILLER                     PIC X(18) VALUE                        
              'RELOCATION ADDRESS'.                                             
           05 FILLER                     PIC X(47) VALUE SPACES.                
           05 FILLER                     PIC X(7)  VALUE 'REGN NO'.             
           05 FILLER                     PIC X(4)  VALUE SPACES.                
           05 FILLER                     PIC X(5)  VALUE 'NAME1'.               
           05 FILLER                     PIC X(21) VALUE SPACES.                
           05 FILLER                     PIC X(5)  VALUE 'NRIC1'.               
                                                                                
       01  L772-HEAD6.                                                          
           05 FILLER                     PIC X(84) VALUE SPACES.                
           05 FILLER                     PIC X(5)  VALUE 'NAME2'.               
           05 FILLER                     PIC X(21) VALUE SPACES.                
           05 FILLER                     PIC X(5)  VALUE 'NRIC2'.               
                                                                                
                                                                                
       01  L772-DETAIL1.                                                        
           05 L772-SERIAL-NO             PIC ZZZ9.                              
           05 FILLER                     PIC X(4)  VALUE SPACES.                
           05 L772-BLK-HDER              PIC X(4).                              
           05 FILLER                     PIC X     VALUE SPACES.                
           05 L772-BLK-NUM               PIC X(4).                              
           05 FILLER                     PIC X(2)  VALUE SPACES.                
           05 L772-STREET-NAME           PIC X(29).                             
           05 FILLER                     PIC X(2)  VALUE ' #'.                  
           05 L772-HSE-LVL               PIC X(2).                              
           05 FILLER                     PIC X     VALUE '-'.                   
           05 L772-HSE-NO.                                                      
              10 L772-HSE-MAIN           PIC X(4).                              
              10 L772-HYPHEN             PIC X.                                 
              10 L772-HSE-SUB            PIC X(3).                              
           05 FILLER                     PIC X(3)  VALUE ' S('.                 
           05 L772-POST-CODE             PIC X(6).                              
           05 FILLER                     PIC X     VALUE ')'.                   
           05 FILLER                     PIC X(2)  VALUE SPACES.                
           05 L772-REGN                  PIC X(8).                              
           05 FILLER                     PIC X(3)  VALUE SPACES.                
           05 L772-NAME1                 PIC X(22).                             
           05 FILLER                     PIC X(4)  VALUE SPACES.                
           05 L772-NRIC1                 PIC X(9).                              
                                                                                
       01  L772-DETAIL2.                                                        
           05 FILLER                     PIC X(84) VALUE SPACES.                
           05 L772-NAME2                 PIC X(22).                             
           05 FILLER                     PIC X(4)  VALUE SPACES.                
           05 L772-NRIC2                 PIC X(9).                              
                                                                                
                                                                                
      **********************************************************                
       PROCEDURE DIVISION.                                                      
      **********************************************************                
                                                                                
      *----------------*                                                        
       000-MAIN-ROUTINE.                                                        
      *----------------*                                                        
                                                                                
           PERFORM 100-OPEN-FILES      THRU 100-EXIT.                           
                                                                                
      *---- BP130078  BY ATH (CS90840)                                          
           PERFORM 900-DETERMINE-DATE  THRU 900-EXIT.                           
      *---- END                                                                 
                                                                                
           PERFORM 200-READ-BP13F800   THRU 200-EXIT.                           
           PERFORM 300-PROCESS-F800    THRU 300-EXIT                            
                   UNTIL F800-EOF.                                              
           PERFORM 800-CLOSE-ROUTINE   THRU 800-EXIT.                           
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------*                                                          
       100-OPEN-FILES.                                                          
      *--------------*                                                          
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP13K820                                                 
                       BP13K830                                                 
                OUTPUT BP13L772.                                                
                                                                                
           IF BP13K820-STATUS NOT = 0 AND 97                                    
              DISPLAY ' OPENING ERROR, BP13K820-STATUS '                        
                        BP13K820-STATUS                                         
              MOVE BP13K820-STATUS TO RETURN-CODE                               
              PERFORM 800-CLOSE-ROUTINE  THRU   800-EXIT.                       
                                                                                
           IF BP13K830-STATUS NOT = 0 AND 97                                    
              DISPLAY ' OPENING ERROR, BP13K830-STATUS '                        
                        BP13K830-STATUS                                         
              MOVE BP13K830-STATUS TO RETURN-CODE                               
              PERFORM 800-CLOSE-ROUTINE  THRU   800-EXIT.                       
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *----------------*                                                        
       200-READ-BP13F800.                                                       
      *----------------*                                                        
                                                                                
           READ BP13F800                 AT END                                 
           MOVE 'Y'                      TO WS-F800-EOF                         
           GO TO 200-EXIT.                                                      
                                                                                
           ADD 1                         TO WS-CNT-F800-READ.                   
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *----------------*                                                        
       300-PROCESS-F800.                                                        
      *----------------*                                                        
                                                                                
           IF F800-NUM-ALLO-SCHEME = 'REA' OR 'REE' OR 'REW' OR                 
                                     'RES' OR 'REN' OR 'RED'                    
                                                                                
              IF F800-NUM-SCH-ACC = SPACES                                      
                 ADD 1 TO WS-CNT-PROCESS                                        
                 PERFORM 400-SETUP-PRINT    THRU 400-EXIT                       
                 PERFORM 600-PRINT-REPORT   THRU 600-EXIT.                      
                                                                                
              PERFORM 200-READ-BP13F800     THRU 200-EXIT.                      
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *---------------*                                                         
       400-SETUP-PRINT.                                                         
      *---------------*                                                         
                                                                                
           MOVE SPACES                  TO BP13K830-REC.                        
           INITIALIZE                      BP13K830-REC.                        
           MOVE F800-NUM-REGN           TO K830-NUM-REGN.                       
           READ BP13K830 KEY IS K830-NUM-REGN.                                  
                                                                                
           IF BP13K830-STATUS = 00 OR 02                                        
              ADD 1                        TO WS-CNT-K830-READ                  
           ELSE                                                                 
           IF BP13K830-STATUS = 23                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K830, STATUS '                   
              BP13K830-STATUS ' KEY IS REGN NO ' K830-NUM-REGN                  
              GO TO 400-EXIT                                                    
           ELSE                                                                 
              DISPLAY ' ERROR READING BP13K830, STATUS '                        
              BP13K830-STATUS ' KEY IS REGN NO ' K830-NUM-REGN                  
              PERFORM 800-CLOSE-ROUTINE.                                        
                                                                                
           MOVE F800-NUM-REGN            TO K820-NUM-REGN                       
                                            L772-REGN.                          
           MOVE F800-NUM-NRIC1           TO K820-NUM-NRIC                       
                                            L772-NRIC1.                         
           PERFORM 500-READ-BP13K820  THRU 500-EXIT.                            
           MOVE K820-NME-OCCP           TO L772-NAME1                           
                                                                                
           MOVE F800-NUM-NRIC2           TO K820-NUM-NRIC                       
                                            L772-NRIC2.                         
           PERFORM 500-READ-BP13K820  THRU 500-EXIT.                            
           MOVE K820-NME-OCCP           TO L772-NAME2                           
                                                                                
           IF K830-NUM-MAIL-ADDR = SPACES OR LOW-VALUES                         
              MOVE K830-NUM-BLK-HDR  TO L772-BLK-HDER                           
              MOVE K830-NUM-MAIN-BLK TO L772-BLK-NUM                            
              MOVE K830-NME-STREET   TO L772-STREET-NAME                        
              MOVE K830-NUM-LVL-NUM  TO L772-HSE-LVL                            
              MOVE K830-NUM-HSEMAIN  TO L772-HSE-MAIN                           
              IF K830-NUM-HSESUB NOT = SPACES                                   
                 MOVE '-'               TO L772-HYPHEN                          
                 MOVE K830-NUM-HSESUB   TO L772-HSE-SUB                         
              ELSE                                                              
                 MOVE SPACES            TO L772-HYPHEN                          
                 MOVE SPACES            TO L772-HSE-SUB                         
              END-IF                                                            
              MOVE K830-NUM-POSTAL   TO L772-POST-CODE                          
           ELSE                                                                 
              MOVE K830-NUM-MAIL-BLK-HDR  TO L772-BLK-HDER                      
              MOVE K830-NUM-MAIL-MAIN-BLK TO L772-BLK-NUM                       
              MOVE K830-NME-MAIL-STREET   TO L772-STREET-NAME                   
              MOVE K830-NUM-MAIL-LVL-NUM  TO L772-HSE-LVL                       
              MOVE K830-NUM-MAIL-HSEMAIN  TO L772-HSE-MAIN                      
              IF K830-NUM-MAIL-HSESUB NOT = SPACES                              
                 MOVE '-'                    TO L772-HYPHEN                     
                 MOVE K830-NUM-MAIL-HSESUB   TO L772-HSE-SUB                    
              ELSE                                                              
                 MOVE SPACES                 TO L772-HYPHEN                     
                 MOVE SPACES                 TO L772-HSE-SUB                    
              END-IF                                                            
              MOVE K830-NUM-MAIL-POSTAL   TO L772-POST-CODE                     
            END-IF.                                                             
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *----------------*                                                        
       500-READ-BP13K820.                                                       
      *----------------*                                                        
                                                                                
           READ BP13K820.                                                       
                                                                                
           IF BP13K820-STATUS = 00                                              
              ADD 1                      TO WS-CNT-K820-READ                    
           ELSE                                                                 
           IF BP13K820-STATUS = 23                                              
              DISPLAY 'REC NOT FND IN BP13K820, REG NO: ' K820-NUM-REGN         
              MOVE SPACES TO K820-NME-OCCP                                      
           ELSE                                                                 
              DISPLAY ' ERROR READING BP13K820, STATUS '                        
              BP13K820-STATUS ' KEY IS REGN NO ' K820-NUM-REGN                  
              PERFORM 800-CLOSE-ROUTINE.                                        
                                                                                
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       600-PRINT-REPORT.                                                        
      *----------------*                                                        
                                                                                
           IF BP13K830-STATUS NOT = 00                                          
              GO TO 600-EXIT.                                                   
                                                                                
           IF F800-NUM-BO NOT = WS-BRANCH-OFFICE                                
              MOVE F800-NUM-BO    TO WS-BRANCH-OFFICE                           
                                     L772-BRANCH-OFFICE                         
              PERFORM 700-PRINT-HEAD  THRU 700-EXIT                             
              MOVE ZEROES             TO WS-SERIAL-NO                           
           ELSE                                                                 
              IF WS-LINE > 54                                                   
                 PERFORM 700-PRINT-HEAD     THRU 700-EXIT                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
           ADD  1                     TO WS-SERIAL-NO.                          
           MOVE WS-SERIAL-NO          TO L772-SERIAL-NO.                        
                                                                                
           WRITE BP13L772-REC FROM L772-DETAIL1 AFTER 2.                        
           WRITE BP13L772-REC FROM L772-DETAIL2 AFTER 1.                        
                                                                                
           ADD 3                      TO WS-LINE.                               
           ADD 1                      TO WS-CNT-PRINTED.                        
                                                                                
                                                                                
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------*                                                          
       700-PRINT-HEAD.                                                          
      *--------------*                                                          
                                                                                
           MOVE WS-BRANCH-OFFICE TO L772-BRANCH-OFFICE.                         
           ADD 1                         TO WS-PAGE.                            
           MOVE WS-PAGE                  TO L772-PAGE.                          
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE.                          
           MOVE CORR WS-CURR-DATE        TO WS-EDIT-DATE.                       
                                                                                
           MOVE WS-EDIT-DATE             TO L772-DATE.                          
                                                                                
           WRITE BP13L772-REC FROM L772-HEAD1 AFTER PAGE.                       
           WRITE BP13L772-REC FROM L772-HEAD2 AFTER 2.                          
           WRITE BP13L772-REC FROM L772-HEAD3 AFTER 1.                          
           WRITE BP13L772-REC FROM L772-HEAD4 AFTER 2.                          
           WRITE BP13L772-REC FROM L772-HEAD5 AFTER 2.                          
           WRITE BP13L772-REC FROM L772-HEAD6 AFTER 1.                          
                                                                                
           MOVE 9                        TO WS-LINE.                            
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       800-CLOSE-ROUTINE.                                                       
      *-----------------*                                                       
                                                                                
           CLOSE                                                                
                 BP13F800                                                       
                 BP13K820                                                       
                 BP13K830                                                       
                 BP13L772.                                                      
                                                                                
           IF BP13K820-STATUS NOT = 00                                          
              DISPLAY ' CLOSING ERROR, BP13K820-STATUS '                        
              BP13K820-STATUS.                                                  
                                                                                
           IF BP13K830-STATUS NOT = 00                                          
              DISPLAY ' CLOSING ERROR, BP13K830-STATUS '                        
              BP13K830-STATUS.                                                  
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY ' ******************* BP13C772 ******************'.          
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF VALID REC PROCESS FROM FILE    (BP13F800)  : '        
                   WS-CNT-PROCESS.                                              
           DISPLAY 'NO OF REC READ FROM          FILE    (BP13F800)  : '        
                   WS-CNT-F800-READ.                                            
           DISPLAY 'NO OF REC READ FROM          FILE    (BP13K820)  : '        
                   WS-CNT-K820-READ.                                            
           DISPLAY 'NO OF REC READ FROM          FILE    (BP13K830) : '         
                   WS-CNT-K830-READ.                                            
           DISPLAY 'NO OF REC PRINTED                    (BP13L772) : '         
                   WS-CNT-PRINTED.                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       900-DETERMINE-DATE.                                                      
      *---------------------*                                                   
                                                                                
      *----------------------------------------------------------*              
      *  REGISTRATION IS VALID IS MADE WITHIN 2 MONTHS OF CURDTE *              
      *    -  TO DETERMINE THE 2 MONTHS VALIDITY PERIOD          *              
      *----------------------------------------------------------*              
                                                                                
           MOVE  FUNCTION CURRENT-DATE TO WS-SYS-DATE.                          
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'TODAY IS           ==> ' WS-SYS-DATE.                       
                                                                                
      *---- CURRENT-DATE                                                        
           MOVE  WS-SYS-DATE        TO  WS-CURRENT-DATE.                        
                                                                                
      *---- PREVIOUS-DATE                                                       
           IF WS-JANUARY                                                        
              SUBTRACT  1           FROM WS-YY-R                                
              MOVE '12'             TO  WS-MM                                   
           ELSE                                                                 
              SUBTRACT  1           FROM WS-MM-R                                
           END-IF.                                                              
           MOVE    WS-SYS-DATE      TO    DATE-REC.                             
           PERFORM 910-PROCESS-DATE THRU  910-EXIT.                             
           MOVE    DATE-REC         TO    WS-PREVIOUS-DATE.                     
                                                                                
           DISPLAY 'CURRENT MONTH IS   ==> ' WS-CURRENT-DATE.                   
           DISPLAY 'PREVIOUS MONTH IS  ==> ' WS-PREVIOUS-DATE.                  
           DISPLAY SPACES.                                                      
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       910-PROCESS-DATE.                                                        
      ******************************************************************        
            IF 31-DAY-MONTH                                                     
               IF CAL-DAY IS NOT LESS THAN 32                                   
                  MOVE 31 TO CAL-DAY.                                           
                                                                                
            IF 30-DAY-MONTH                                                     
               IF CAL-DAY IS NOT LESS THAN 31                                   
                  MOVE 30 TO CAL-DAY.                                           
                                                                                
            IF 28-29-DAY-MONTH                                                  
               IF CAL-DAY IS LESS THAN 29                                       
                  GO TO 910-EXIT                                                
               ELSE                                                             
                  PERFORM 920-FEBRUARY                                          
            ELSE                                                                
               NEXT SENTENCE.                                                   
                                                                                
       910-EXIT.                                                                
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       920-FEBRUARY.                                                            
      ******************************************************************        
            DIVIDE 4 INTO CAL-YEAR   GIVING WS-YEAR REMAINDER WS-REM.           
                                                                                
            IF WS-REM IS EQUAL TO ZERO AND CAL-DAY IS GREATER THAN 29           
               MOVE 29 TO CAL-DAY.                                              
                                                                                
            IF WS-REM IS NOT EQUAL TO ZERO                                      
                                   AND CAL-DAY IS GREATER THAN 28               
               MOVE 28 TO CAL-DAY.                                              
                                                                                
       920-EXIT.                                                                
            EXIT.                                                               
                                                                                
