       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C23M.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  OCT 07 2009.                                              
      * ============================================================            
      * SYSTEM OF COMMITMENT (SOC)                                              
      * ============================================================            
      * OBJECTIVE  - SORT OUT NPL CASES AND BALLOT DATE SAME AS THE             
      *              INPUTTED BALLOT DATE IN BP13M205                           
      *                                                                         
      * INPUT FILES:  SY02F001                                                  
      *               BM06K110                                                  
      * OUTPUT     :  BM6F110A                                                  
      * ------------------------------------------------------------            
      * REF NO    DATE  BY  DESCRIPTIONS                                        
      * -------- ------ --- ------------                                        
      * BP133725 061009 ESA1 NEW PGM                                            
      * BP133924 240510 ESA1 CHANGE BP13F205 TO SY02F001                        
      * ============================================================            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT SY02F001 ASSIGN        TO SY02F001.                           
                                                                                
           SELECT BM06K110 ASSIGN        TO BM06K110                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS DYNAMIC                             
                           RECORD KEY    IS K110-KEY-FLD                        
                           FILE STATUS   IS K110-STATUS.                        
                                                                                
           SELECT BM6F110A ASSIGN TO BM6F110A.                                  
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------------*          
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BM6F110A                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       01   BM6F110A-REC         PIC X(500).                                    
                                                                                
      *--------------------------------------------------------------*          
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------*          
                                                                                
       01  WS-WORKING-VARIABLES.                                                
           05  K110-STATUS                PIC 9(2)  VALUE 0.                    
           05  WS-K110-EOF                PIC X(1)  VALUE 'N'.                  
           05  WS-F001-EOF                PIC X(1)  VALUE 'N'.                  
           05  WS-CNT-K110-READ           PIC 9(8)  VALUE 0.                    
           05  WS-CNT-F110-WRITE          PIC 9(8)  VALUE 0.                    
                                                                                
           05  WS-DTE-BALLOT.                                                   
               10 WS-BALLOT-CCYY          PIC 9(4)  VALUE 0.                    
               10 WS-BALLOT-MM            PIC 9(2)  VALUE 0.                    
                                                                                
      *--------------------------------------------------------------*          
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------------*          
                                                                                
      *--------------------------------------------------------------*          
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------------------------------------*          
                                                                                
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
           PERFORM 2000-READ-F001         THRU 2000-EXIT.                       
           PERFORM 3000-STARTBR-BM06K110  THRU 3000-EXIT.                       
           PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1000-OPEN-ROUTINE.                                                       
      *--------------------------------------------------------------*          
                                                                                
           OPEN INPUT  SY02F001                                                 
                       BM06K110                                                 
                OUTPUT BM6F110A.                                                
                                                                                
           IF K110-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN FAILED FOR BM06K110,STATUS ' K110-STATUS            
              MOVE K110-STATUS TO RETURN-CODE                                   
              PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                        
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       2000-READ-F001.                                                          
      *--------------------------------------------------------------*          
                                                                                
           READ SY02F001 AT END                                                 
                MOVE 'Y' TO WS-F001-EOF                                         
                GO TO 2000-EXIT                                                 
           END-READ.                                                            
                                                                                
           MOVE F001-DTE-CURRENT(1:4)     TO  WS-BALLOT-CCYY.                   
           ADD 1                          TO  F001-CURR-MM.                     
           MOVE F001-CURR-MM              TO  WS-BALLOT-MM.                     
                                                                                
           IF WS-BALLOT-MM > 12                                                 
              MOVE 1                      TO  WS-BALLOT-MM                      
              ADD  1                      TO  WS-BALLOT-CCYY                    
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       3000-STARTBR-BM06K110.                                                   
      *--------------------------------------------------------------*          
           MOVE  'N'             TO WS-K110-EOF.                                
           MOVE LOW-VALUES       TO K110-KEY-FLD.                               
                                                                                
           START BM06K110 KEY >= K110-KEY-FLD.                                  
                                                                                
           IF K110-STATUS = 00 OR 02                                            
              PERFORM 4000-READNEXT-BM06K110   THRU 4000-EXIT                   
                        UNTIL WS-K110-EOF = 'Y'                                 
           ELSE                                                                 
              DISPLAY 'START BROWSE ERROR BM06K110,STATUS IS '                  
                          K110-STATUS                                           
              MOVE K110-STATUS  TO RETURN-CODE                                  
              PERFORM 9999-CLOSE-ROUTINE   THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       4000-READNEXT-BM06K110.                                                  
      *--------------------------------------------------------------*          
           READ BM06K110 NEXT RECORD                                            
                AT END                                                          
                   MOVE 'Y'  TO  WS-K110-EOF                                    
                NOT AT END                                                      
                   ADD  1    TO  WS-CNT-K110-READ                               
           END-READ.                                                            
                                                                                
                                                                                
           IF K110-STATUS = 00 OR 02                                            
              IF ((WS-DTE-BALLOT = K110-NUM-ALLOCN-QTR) AND                     
                  (K110-NUM-SELECTION = 'NPL' ))                                
                  PERFORM 5000-UPDATE-BM6F110A   THRU 5000-EXIT                 
              END-IF                                                            
           ELSE                                                                 
              IF K110-STATUS = 10 OR 23                                         
                 MOVE 'Y'       TO   WS-K110-EOF                                
              ELSE                                                              
                 DISPLAY ' ERROR READING, BM06K110 = ' K110-STATUS              
                 MOVE K110-STATUS  TO RETURN-CODE                               
                 PERFORM 9999-CLOSE-ROUTINE  THRU  9999-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------*          
       5000-UPDATE-BM6F110A.                                                    
      *--------------------------------------------------------------*          
                                                                                
            WRITE BM6F110A-REC FROM K110-REC.                                   
            ADD  1  TO WS-CNT-F110-WRITE.                                       
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------*          
       9999-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------------*          
                                                                                
           CLOSE SY02F001                                                       
                 BM06K110                                                       
                 BM6F110A.                                                      
                                                                                
           IF K110-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE FAILED FOR BM06K110 ' K110-STATUS                  
              MOVE K110-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '- BP13C23M CONTROL TOTAL -------------'.                    
           DISPLAY ' TOTAL BM06K110 RECS READ    : ' WS-CNT-K110-READ.          
           DISPLAY ' TOTAL BM6F110A RECS WRITE   : ' WS-CNT-F110-WRITE.         
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************** E N D  O F  P R O G R A M *****************             
