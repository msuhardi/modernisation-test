       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C89Y.                                                 
       AUTHOR.        LLOYD CAPARRO.                                            
       DATE-WRITTEN.  19/09/2005.                                               
                                                                                
      *************************************************************             
      *  SYSTEM NAME : SYSTEM OF COMMITMENT (BP13)                *             
      *===========================================================*             
      *  OBJECTIVE  :  PRINTING OF DOCUMENTS TO BRING             *             
      *                                                           *             
      *  BP13CR18 - PRINTING OF DOCUMENTS TO BRING                *             
      *                                                           *             
      *             :  DOCUMENTS TO BRING (1 ORIG + 1 COPY)       *             
      *                                                           *             
      *  INPUT      :  1.  BP13F740 - BP13.F740.MATUREBA          *             
      *                2.  BP13K060 - BP13.K060.CODETAB           *             
      *                3.  BP13KA10 - BP13.KA10.PRJCT             *             
      *                4.  BP13KB78 - BP13.KB78.BTO               *             
      *                5.  BP13K800 - BP13.K800.APPLN             *             
      *                6.  BP13K820 - BP13.K820.SOCOCCP           *             
      *                7.  BP13K830 - BP13.K830.ADDR              *             
      *                8.  BP13K200 - BP13.K200                   *             
      *  OUTPUT     :  1.  P13P89DA - DOCUMENTS TO BRING          *             
      *                             - NON-DELETION                *             
      *                             - APPLICATION LETTER AND      *             
      *                               HOUSING LOAN LETTER         *             
      *             :  2.  P13P89DB - FILE COPY OF                *             
      *                             - INVITATION LETTER           *             
      *                             - DOCUMENTS                   *             
      *                             - APPLICATION LETTER AND      *             
      *                               HOUSING LOAN LETTER         *             
      *                3.  P13L89DA - INTERNET CASES W/ PPO       *             
      *                4.  P13L89DB - CONTROL LIST FOR INTERNET   *             
      *                               APPLICATION AND PPO         *             
      *                                                           *             
      *===========================================================*             
      *  REVISION HISTORY :                                       *             
      *  CHGE-NO   OIC   DATE        DESCRIPTION                  *             
      *  --------  ----  ----------  ---------------------------- *             
      *  BP132747  CG7   19/09/2005  NEW PROGRAM                  *             
      *  BP133157  LMS1  22/01/2008  TO RETRIEVE DEVELOPER NAME   *             
      *  BP133610  ESA1  11/05/2009  REPLACE BP13K767 W/ BP13K816 *             
      *************************************************************             
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT BP13F740     ASSIGN       TO BP13F740.                        
                                                                                
           SELECT BP13K060     ASSIGN       TO BP13K060                         
                               ORGANIZATION IS INDEXED                          
                               ACCESS MODE  IS RANDOM                           
                               RECORD KEY   IS K060-KEY-FLD                     
                               FILE STATUS  IS WS-K060-STATUS.                  
                                                                                
           SELECT BP13KA10     ASSIGN       TO BP13KA10                         
                               ORGANIZATION IS INDEXED                          
                               ACCESS MODE  IS RANDOM                           
                               RECORD KEY   IS KA10-KEY-FLD                     
                               FILE STATUS  IS WS-KA10-STATUS.                  
                                                                                
           SELECT BP13KB78     ASSIGN       TO BP13KB78                         
                               ORGANIZATION IS INDEXED                          
                               ACCESS MODE  IS RANDOM                           
                               RECORD KEY   IS KB78-KEY-FLD                     
                               FILE STATUS  IS WS-KB78-STATUS.                  
                                                                                
           SELECT BP13K816     ASSIGN       TO BP13K816                         
                               ACCESS MODE  IS RANDOM                           
                               ORGANIZATION IS INDEXED                          
                               RECORD KEY   IS K816-KEY-FLD                     
                               FILE STATUS  IS WS-K816-STATUS.                  
                                                                                
           SELECT BP13K200     ASSIGN       TO BP13K200                         
                               ORGANIZATION IS INDEXED                          
                               ACCESS MODE  IS RANDOM                           
                               RECORD KEY   IS K200-KEY-FLD                     
                               ALTERNATE KEY IS K200-NUM-REGN                   
                               FILE STATUS  IS WS-K200-STATUS.                  
                                                                                
           SELECT BP13K800     ASSIGN       TO BP13K800                         
                               ORGANIZATION IS INDEXED                          
                               ACCESS MODE  IS RANDOM                           
                               RECORD KEY   IS K800-NUM-REGN                    
                               FILE STATUS  IS WS-K800-STATUS.                  
                                                                                
           SELECT BP13K820     ASSIGN       TO BP13K820                         
                               ORGANIZATION IS INDEXED                          
                               ACCESS MODE  IS DYNAMIC                          
                               RECORD KEY   IS K820-KEY-FLD                     
                               FILE STATUS  IS WS-K820-STATUS.                  
                                                                                
           SELECT BP13K830     ASSIGN       TO BP13K830                         
                               ACCESS MODE  IS RANDOM                           
                               ORGANIZATION IS INDEXED                          
                               RECORD KEY   IS K830-KEY-FLD                     
                               ALTERNATE RECORD KEY IS  K830-NUM-REGN           
                               FILE STATUS  IS WS-K830-STATUS.                  
                                                                                
           SELECT BP13F398     ASSIGN       TO BP13F398.                        
                                                                                
           SELECT BP13F200     ASSIGN       TO BP13F200.                        
                                                                                
           SELECT P13P89DA     ASSIGN       TO P13P89DA.                        
                                                                                
           SELECT P13P89DB     ASSIGN       TO P13P89DB.                        
                                                                                
           SELECT P13L89DA     ASSIGN       TO P13L89DA.                        
                                                                                
           SELECT P13L89DB     ASSIGN       TO P13L89DB.                        
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F740           IS GLOBAL                                        
                               RECORD CONTAINS 500 CHARACTERS                   
                               LABEL RECORDS IS STANDARD                        
                               RECORDING MODE IS F.                             
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K060           IS GLOBAL                                        
                               RECORD CONTAINS 25 CHARACTERS.                   
                                                                                
       COPY BP13K060.                                                           
                                                                                
       FD  BP13K816            IS GLOBAL                                        
                               RECORD CONTAINS 400 CHARACTERS.                  
       COPY BP13K816.                                                           
                                                                                
       FD   BP13KA10           IS GLOBAL                                        
                               RECORD CONTAINS 800  CHARACTERS.                 
                                                                                
       COPY BP13KA10.                                                           
                                                                                
       FD   BP13KB78           IS GLOBAL                                        
                               RECORD CONTAINS 3300 CHARACTERS.                 
                                                                                
       COPY BP13KB78.                                                           
                                                                                
       FD   BP13K200           IS GLOBAL                                        
                               RECORD CONTAINS 300 CHARACTERS.                  
                                                                                
       COPY BP13K200.                                                           
                                                                                
       FD   BP13K800           IS GLOBAL                                        
                               RECORD CONTAINS 2000 CHARACTERS.                 
                                                                                
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820           IS GLOBAL                                        
                               RECORD CONTAINS 400 CHARACTERS.                  
                                                                                
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830           IS GLOBAL                                        
                               RECORD CONTAINS 250 CHARACTERS.                  
                                                                                
       COPY BP13K830.                                                           
                                                                                
       FD   BP13F398           IS GLOBAL                                        
            RECORDING MODE IS F                                                 
            RECORD CONTAINS  2500 CHARACTERS.                                   
                                                                                
       COPY BP13F398.                                                           
                                                                                
       FD   BP13F200                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 300 CHARACTERS.                                     
                                                                                
       COPY BP13F200.                                                           
                                                                                
       FD   P13P89DA           IS GLOBAL                                        
                               RECORD CONTAINS 100 CHARACTERS                   
                               LABEL RECORDS IS STANDARD                        
                               RECORDING MODE IS  F.                            
       01   P13P89DA-REC                 PIC X(100) GLOBAL.                     
                                                                                
       FD   P13P89DB           IS GLOBAL                                        
                               RECORD CONTAINS 100 CHARACTERS                   
                               LABEL RECORDS IS STANDARD                        
                               RECORDING MODE IS  F.                            
       01   P13P89DB-REC                 PIC X(100) GLOBAL.                     
                                                                                
       FD   P13L89DA           IS GLOBAL                                        
                               RECORD CONTAINS 132 CHARACTERS                   
                               LABEL RECORDS IS STANDARD                        
                               RECORDING MODE IS F.                             
       01   P13L89DA-REC                 PIC X(132) GLOBAL.                     
                                                                                
       FD   P13L89DB           IS GLOBAL                                        
                               RECORD CONTAINS 132  CHARACTERS                  
                               LABEL RECORDS IS STANDARD                        
                               RECORDING MODE IS F.                             
       01   P13L89DB-REC                 PIC X(132) GLOBAL.                     
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-SWITCHES.                                                    
           05  WS-F740-EOF               PIC X(01)  VALUE 'N'.                  
           05  WS-K800-EOF               PIC X(01)  VALUE 'N'.                  
           05  WS-K820-EOF               PIC X(01)  VALUE 'N'.                  
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F740-READ-CNT          PIC 9(06)  VALUE ZEROES.               
           05  WS-K820-READ-CNT          PIC 9(06)  VALUE ZEROES.               
           05  WS-KB78-FND-CNT           PIC 9(06)  VALUE ZEROES.               
           05  WS-KB78-NFND-CNT          PIC 9(06)  VALUE ZEROES.               
           05  WS-K200-FND-CNT           PIC 9(06)  VALUE ZEROES.               
           05  WS-K200-NFND-CNT          PIC 9(06)  VALUE ZEROES.               
           05  WS-K800-FND-CNT           PIC 9(06)  VALUE ZEROES.               
           05  WS-K800-NFND-CNT          PIC 9(06)  VALUE ZEROES.               
           05  WS-K800-FND               PIC X(01)  VALUE 'N'.                  
           05  WS-K820-FND               PIC X(01)  VALUE 'N'.                  
           05  WS-K200-FND               PIC X(01)  VALUE 'N'.                  
                                                                                
       01  WS-AGE                        PIC 9(08) VALUE ZEROES.                
       01  WS-NUM-AGE  REDEFINES WS-AGE.                                        
           05 WS-AGE-CMPLTD              PIC 9(04).                             
           05 WS-NUM-REMNG               PIC 9(04).                             
                                                                                
       01  WS-DTE-LETTER                 PIC 9(08) VALUE ZEROES.                
       01  WS-NUM-DTE-LETTER             PIC 9(07) VALUE ZEROES.                
                                                                                
       01  WS-FILE-STATUS-G GLOBAL.                                             
           05  WS-K060-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-KA10-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-KB78-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-K200-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-K800-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-K820-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-K830-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-K555-STATUS            PIC 9(02)  VALUE ZEROES.               
           05  WS-K816-STATUS            PIC 9(02)  VALUE ZEROES.               
                                                                                
       01  WS-VARIABLES-G GLOBAL.                                               
           05  WS-CURRENT-DATE           PIC X(08)  VALUE SPACES.               
           05  WS-DATE                   PIC 9(08)  VALUE ZEROES.               
           05  WS-BIRTH-DATE             PIC 9(08)  VALUE ZEROES.               
           05  WS-TOT-PAGE-CNT           PIC 9(06)  VALUE ZEROES.               
           05  WS-CASE-PAGE-CNT          PIC 9(03)  VALUE ZEROES.               
           05  WS-F398-WRITE-CNT         PIC 9(08)  VALUE ZEROES.               
           05  WS-F200-WRITE-CNT         PIC 9(08)  VALUE ZEROES.               
           05  WS-CNT-WRITE-L89DA        PIC 9(08)  VALUE ZEROES.               
           05  WS-CNT-WRITE-L89DB        PIC 9(08)  VALUE ZEROES.               
           05  WS-REC-SKIPPED            PIC 9(08)  VALUE ZEROES.               
           05  WS-TOT-LET-PRINT          PIC 9(08)  VALUE ZEROES.               
           05  WS-CNT-LINE-ON-PAGE-A     PIC 9(08)  VALUE ZEROES.               
           05  WS-CNT-LINE-ON-PAGE-B     PIC 9(08)  VALUE ZEROES.               
           05  WS-CNT-PRINT-L89DA        PIC 9(08)  VALUE 1.                    
           05  WS-CNT-PRINT-L89DB        PIC 9(08)  VALUE 1.                    
           05  WS-NO-OF-PAGE-A           PIC 9(08)  VALUE ZEROES.               
           05  WS-NO-OF-PAGE-B           PIC 9(08)  VALUE ZEROES.               
           05  WS-CURR-DATE              PIC X(10)  VALUE SPACES.               
           05  WS-FORM-PRTED             PIC 9(06)  VALUE ZEROES.               
           05  WS-HH                     PIC X(01)  VALUE SPACES.               
           05  WS-FT                     PIC X(01)  VALUE SPACES.               
           05  WS-CTR                    PIC 9(02)  VALUE ZEROES.               
           05  WS-NRIC-CNT               PIC 9(02)  VALUE ZEROES.               
           05  WS-APPL-CNT               PIC 9(02)  VALUE ZEROES.               
           05  WS-OCCP-CNT               PIC 9(02)  VALUE ZEROES.               
           05  WS-F398-OCCP-CNT          PIC 9(02)  VALUE ZEROES.               
                                                                                
      * PARAMETERS FOR INVITATION AND UNDERTAKING LETTERS                       
      * SIMILAR FROM BP13C245                                                   
                                                                                
       01  WS-C245-PARAMETERS GLOBAL.                                           
      *    05  WS-C245-LETTER-TYPE        PIC X(01).                            
           05  WS-C245-NUM-ALLO-SCHEME    PIC X(03).                            
           05  WS-C245-ROOM               PIC X(02).                            
           05  WS-C245-REGN-NO            PIC X(08).                            
           05  WS-C245-ALLOC-SCH          PIC X(07).                            
      *    05  WS-C245-HT                 PIC X(01).                            
           05  WS-C245-Q-NO               PIC X(05).                            
           05  WS-C245-ALLOC-DATE         PIC X(10).                            
           05  WS-C245-SELECT-ROOM        PIC X(09).                            
           05  WS-C245-ZONE               PIC X(10).                            
           05  WS-C245-NT-ZONE-NAME       PIC X(20).                            
           05  WS-C245-BALLOT-QTR         PIC X(03).                            
           05  WS-C245-BALLOT-YR          PIC X(04).                            
           05  WS-C245-BALLOT-MNT-YR      PIC X(14).                            
           05  WS-C245-APPMT-DATE         PIC X(10).                            
           05  WS-C245-APPMT-TIME.                                              
               10  WS-C245-APPMT-HR       PIC X(02).                            
               10  WS-C245-APPMT-SEP      PIC X(01).                            
               10  WS-C245-APPMT-MIN      PIC X(02).                            
               10  FILLER                 PIC X(01).                            
               10  WS-C245-APPMT-AM-PM    PIC X(02).                            
           05  WS-C245-BOOKFEE            PIC X(07).                            
           05  WS-C245-PERCENT-DESC       PIC X(07).                            
           05  WS-C245-PERCENT            PIC 9(02).                            
           05  WS-C245-REMAINDER          PIC X(04).                            
           05  WS-C245-TITLE              PIC X(12).                            
           05  WS-C245-APPLN-NAME         PIC X(66).                            
           05  WS-C245-COAPPLN-NAME       PIC X(66).                            
           05  WS-C245-COAPPLN2-NAME      PIC X(66).                            
           05  WS-C245-COAPPLN3-NAME      PIC X(66).                            
           05  WS-C245-APPLN-NRIC         PIC X(09).                            
           05  WS-C245-COAPPLN-NRIC       PIC X(09).                            
           05  WS-C245-COAPPLN2-NRIC      PIC X(09).                            
           05  WS-C245-COAPPLN3-NRIC      PIC X(09).                            
           05  WS-C245-ADDRESS1.                                                
               10  FILLER                 PIC X(10).                            
               10  WS-C245-BLK-NO         PIC X(10).                            
               10  FILLER                 PIC X(2).                             
               10  WS-C245-NUM-FLAT.                                            
                   15  WS-C245-CHAR       PIC X(01).                            
                   15  WS-C245-LEVEL      PIC X(02).                            
                   15  WS-C245-DASH       PIC X(01).                            
                   15  WS-C245-HSE-MAIN   PIC X(04).                            
                   15  WS-C245-HSE-SUB    PIC X(03).                            
           05  WS-C245-ADDRESS2.                                                
               10  WS-C245-STREET-NAME    PIC X(65).                            
           05  WS-C245-CODE               PIC X(16).                            
           05  WS-C245-CC                 PIC X(22).                            
      *    05  WS-C245-BO                 PIC X(02).                            
           05  WS-C245-NME-OIC            PIC X(40).                            
      *    05  WS-C245-OIC-DESIG          PIC X(40).                            
           05  WS-C245-SHOW-FLAT          PIC X(35).                            
           05  WS-C245-BTO-REOPT          PIC X(01).                            
           05  WS-C245-NUM-JB-MTF         PIC X(01).                            
           05  WS-C245-NUM-LOW-FLOOR      PIC X(01).                            
           05  WS-C245-NUM-LOAN-TAG       PIC X(01).                            
           05  FILLER                     PIC X(96).                            
                                                                                
      * PARAMETERS FOR DOCUMENT LETTERS                                         
                                                                                
       01 WS-C398-PARAMETERS GLOBAL.                                            
          02 WS-C398-LTR-VARS.                                                  
             05 WS-C398-NUM-REGN                   PIC X(08).                   
             05 WS-C398-NME-DEVELOPER              PIC X(60).                   
             05 WS-C398-PRT-DATE                   PIC X(10).                   
             05 WS-C398-ESS-MBRS                   PIC X(01).                   
             05 WS-C398-ORPHANS-DECL               PIC X(01).                   
             05 WS-C398-MCPS-FRM                   PIC X(01).                   
             05 WS-C398-APPLN-FRM                  PIC X(01).                   
             05 WS-C398-APPLICANTS OCCURS 4 TIMES.                              
                10 WS-C398-APP-NRIC                PIC X(09).                   
                10 WS-C398-CDE-APPLICANT           PIC X(01).                   
                10 WS-C398-NME-APPLICANT           PIC X(66).                   
                10 WS-C398-AGE-APPLICANT           PIC 9(03).                   
                10 WS-C398-APP-DOC-REQD.                                        
                   20 WS-C398-APP-ID-CARD          PIC X(01).                   
                   20 WS-C398-APP-PASS-PERMIT      PIC X(01).                   
                   20 WS-C398-APP-BIRTH-CERTF      PIC X(01).                   
                   20 WS-C398-APP-MARRIAGE-CERTF   PIC X(01).                   
                   20 WS-C398-APP-PAY-SLIP         PIC X(01).                   
                   20 WS-C398-APP-UNEMPLYMNT-DECL  PIC X(01).                   
                   20 WS-C398-APP-STUDENT-PASS     PIC X(01).                   
                   20 WS-C398-APP-DEATH-CERTF      PIC X(01).                   
                   20 WS-C398-APP-NRIC-DTH-CERTF   PIC X(09).                   
                   20 WS-C398-APP-NME-DTH-CERTF    PIC X(66).                   
                   20 WS-C398-APP-DIVORCE-CERTF    PIC X(01).                   
                   20 WS-C398-APP-PHOTOGRAPH       PIC X(01).                   
                   20 WS-C398-APP-MARRIAGE-CONSENT PIC X(01).                   
             05 WS-C398-OCCUPIERS OCCURS 8 TIMES.                               
                10 WS-C398-OCC-NRIC                PIC X(09).                   
                10 WS-C398-CDE-OCCUPIER            PIC X(01).                   
                10 WS-C398-NME-OCCUPIER            PIC X(66).                   
                10 WS-C398-AGE-OCCUPIER            PIC 9(03).                   
                10 WS-C398-OCC-DOC-REQD.                                        
                   20 WS-C398-OCC-ID-CARD          PIC X(01).                   
                   20 WS-C398-OCC-PASS-PERMIT      PIC X(01).                   
                   20 WS-C398-OCC-BIRTH-CERTF      PIC X(01).                   
                   20 WS-C398-OCC-MARRIAGE-CERTF   PIC X(01).                   
                   20 WS-C398-OCC-PAY-SLIP         PIC X(01).                   
                   20 WS-C398-OCC-UNEMPLYMNT-DECL  PIC X(01).                   
                   20 WS-C398-OCC-STUDENT-PASS     PIC X(01).                   
                   20 WS-C398-OCC-DEATH-CERTF      PIC X(01).                   
                   20 WS-C398-OCC-NRIC-DTH-CERTF   PIC X(09).                   
                   20 WS-C398-OCC-NME-DTH-CERTF    PIC X(66).                   
                   20 WS-C398-OCC-DIVORCE-CERTF    PIC X(01).                   
                   20 WS-C398-OCC-PHOTOGRAPH       PIC X(01).                   
                   20 WS-C398-OCC-MARRIAGE-CONSENT PIC X(01).                   
             05 WS-C398-NUM-QUEUE                  PIC X(05).                   
             05 FILLER                             PIC X(32).                   
          02 WS-C398-LETTER-CNT                    PIC 9(02).                   
                                                                                
       PROCEDURE DIVISION.                                                      
      ************************************************************              
       0000-MAIN.                                                               
      ************************************************************              
           PERFORM 1000-OPEN-ROUTINE   THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13F740  THRU 2000-EXIT.                          
           PERFORM 3000-PROCESS-RECORD THRU 3000-EXIT                           
             UNTIL WS-F740-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ************************************************************              
       1000-OPEN-ROUTINE.                                                       
      ************************************************************              
           OPEN INPUT  BP13F740                                                 
                       BP13K060                                                 
                       BP13K816                                                 
                       BP13KA10                                                 
                       BP13KB78                                                 
                       BP13K200                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BP13K830                                                 
                OUTPUT BP13F398                                                 
                       P13P89DA                                                 
                       P13P89DB                                                 
                       BP13F200                                                 
                       P13L89DA                                                 
                       P13L89DB.                                                
                                                                                
           IF WS-K060-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K060 OPEN ERROR, STATUS ' WS-K060-STATUS             
              MOVE WS-K060-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K816-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K816 OPEN ERROR, STATUS ' WS-K816-STATUS             
              MOVE WS-K816-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-KA10-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KA10 OPEN ERROR, STATUS ' WS-KA10-STATUS             
              MOVE WS-KA10-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-KB78-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KB78 OPEN ERROR, STATUS ' WS-KB78-STATUS             
              MOVE WS-KB78-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K200-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K200 OPEN ERROR, STATUS ' WS-K200-STATUS             
              MOVE WS-K200-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 OPEN ERROR, STATUS ' WS-K800-STATUS             
              MOVE WS-K800-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 OPEN ERROR, STATUS ' WS-K820-STATUS             
              MOVE WS-K820-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K830 OPEN ERROR, STATUS ' WS-K830-STATUS             
              MOVE WS-K830-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CURRENT-DATE.                  
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-DATE.                          
                                                                                
           STRING WS-CURRENT-DATE(7:2) '/'                                      
                  WS-CURRENT-DATE(5:2) '/'                                      
                  WS-CURRENT-DATE(1:4)                                          
                  DELIMITED BY SIZE                                             
             INTO WS-CURR-DATE.                                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
       2000-READ-BP13F740.                                                      
      ************************************************************              
           READ BP13F740                                                        
             AT END                                                             
                MOVE 'Y'                 TO WS-F740-EOF                         
            NOT AT END                                                          
                ADD 1                    TO WS-F740-READ-CNT                    
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
       3000-PROCESS-RECORD.                                                     
      ************************************************************              
           PERFORM 3100-INITIALIZE     THRU 3100-EXIT.                          
           PERFORM 3200-READ-BP13K800  THRU 3200-EXIT.                          
                                                                                
           IF WS-K800-FND = 'N'                                                 
              ADD 1 TO WS-K800-NFND-CNT                                         
              PERFORM 2000-READ-BP13F740 THRU 2000-EXIT                         
              GO TO 3000-EXIT                                                   
           ELSE                                                                 
              ADD 1 TO WS-K800-FND-CNT                                          
              MOVE K800-NUM-DBSS-PRJCT        TO   KA10-KEY-FLD                 
              PERFORM 3110-READ-BP13KA10      THRU 3110-EXIT                    
              PERFORM 3300-READ-BP13K200      THRU 3300-EXIT                    
              PERFORM 3400-PROCESS-BP13K820   THRU 3400-EXIT                    
              PERFORM 4000-APPLN-HSELOAN-LET  THRU 4000-EXIT                    
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F740 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
       3100-INITIALIZE.                                                         
      ************************************************************              
           MOVE  ZEROS      TO WS-APPL-CNT WS-OCCP-CNT.                         
           MOVE   4         TO WS-F398-OCCP-CNT.                                
           MOVE   'N'       TO WS-K820-EOF.                                     
           MOVE  SPACES     TO WS-C398-LTR-VARS                                 
                               BP13F398-REC.                                    
           INITIALIZE          WS-C398-LTR-VARS                                 
                               BP13F398-REC.                                    
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3110-READ-BP13KA10.                                                      
      *---------------------------------------------------------------*         
                                                                                
           READ BP13KA10 KEY IS KA10-KEY-FLD.                                   
           EVALUATE WS-KA10-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              MOVE  KA10-NME-DVLPR-PARENT TO WS-C398-NME-DEVELOPER              
           WHEN 23                                                              
              MOVE  SPACES            TO WS-C398-NME-DEVELOPER                  
              DISPLAY 'BP13KA10 PRJCT NOT FOUND : ' KA10-KEY-FLD                
                      'FOR REGN ' K800-NUM-REGN                                 
           WHEN OTHER                                                           
              DISPLAY 'BP13KA10 - READ ERROR, STATUS ' WS-KA10-STATUS           
              MOVE WS-KA10-STATUS    TO RETURN-CODE                             
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       3110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************************************************              
       3200-READ-BP13K800.                                                      
      ************************************************************              
           MOVE F740-NUM-REGN TO K800-NUM-REGN.                                 
           READ BP13K800.                                                       
           EVALUATE WS-K800-STATUS                                              
             WHEN   00                                                          
                    IF K800-NUM-STATUS = 'C'                                    
                       MOVE 'N' TO WS-K800-FND                                  
                    ELSE                                                        
                       MOVE 'Y' TO WS-K800-FND                                  
                    END-IF                                                      
             WHEN   23                                                          
                    MOVE 'N' TO WS-K800-FND                                     
             WHEN   OTHER                                                       
                    DISPLAY 'READ BP13K800 ERROR ' K800-NUM-REGN                
                    DISPLAY 'STATUS ' WS-K800-STATUS                            
                    MOVE WS-K800-STATUS TO RETURN-CODE                          
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
                                                                                
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************************************************              
       3300-READ-BP13K200.                                                      
      ************************************************************              
           MOVE 'N'                      TO WS-K200-FND.                        
           MOVE SPACES                   TO BP13K200-REC.                       
           INITIALIZE                       BP13K200-REC.                       
                                                                                
           MOVE F740-NUM-REGN            TO K200-NUM-REGN                       
                                            F398-NUM-REGN                       
                                            WS-C398-NUM-REGN.                   
                                                                                
           MOVE F740-NUM-NT-FT-QUEUE     TO WS-C398-NUM-QUEUE.                  
                                                                                
           MOVE K800-NUM-ELIG-SCHEME     TO F398-CDE-SCHEME.                    
                                                                                
           READ BP13K200 KEY IS K200-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K200-STATUS                                              
               WHEN 00                                                          
                    ADD 1                TO WS-K200-FND-CNT                     
                    MOVE K200-DTE-ALLOC  TO WS-DTE-LETTER                       
                    MOVE 'Y'             TO WS-K200-FND                         
                    MOVE BP13K200-REC    TO BP13F200-REC                        
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1                TO WS-K200-NFND-CNT                    
                    MOVE ZEROES          TO WS-DTE-LETTER                       
                    DISPLAY 'REGN NO. NOT FOUND IN BP13K200. REGN = '           
                            K200-NUM-REGN                                       
               WHEN OTHER                                                       
                    DISPLAY 'READ ERROR BP13K200, STATUS '                      
                            WS-K200-STATUS ' ' K200-NUM-REGN                    
                    MOVE WS-K200-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
           IF WS-DTE-LETTER = ZEROES                                            
              MOVE  SPACES          TO   WS-C398-PRT-DATE                       
           ELSE                                                                 
              COMPUTE WS-NUM-DTE-LETTER =                                       
                      FUNCTION INTEGER-OF-DATE(WS-DTE-LETTER)                   
              SUBTRACT    10        FROM WS-NUM-DTE-LETTER                      
              COMPUTE WS-DTE-LETTER =                                           
                      FUNCTION DATE-OF-INTEGER(WS-NUM-DTE-LETTER)               
              STRING WS-DTE-LETTER(7:2) '/'                                     
                     WS-DTE-LETTER(5:2) '/'                                     
                     WS-DTE-LETTER(1:4)                                         
                     DELIMITED BY SIZE INTO WS-C398-PRT-DATE                    
           END-IF.                                                              
                                                                                
      * CKK7                                                                    
           MOVE WS-CURR-DATE           TO WS-C398-PRT-DATE.                     
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************************************************              
       3400-PROCESS-BP13K820.                                                   
      ************************************************************              
           MOVE 'N'             TO WS-K820-FND.                                 
           MOVE ZEROES          TO WS-C398-LETTER-CNT                           
                                                                                
           MOVE ZEROES          TO WS-NRIC-CNT.                                 
           MOVE ZEROES          TO WS-C398-LETTER-CNT.                          
           MOVE LOW-VALUES      TO K820-KEY-FLD.                                
           MOVE K800-NUM-REGN   TO K820-NUM-REGN.                               
                                                                                
           PERFORM 3410-START-BP13K820    THRU 3410-EXIT.                       
                                                                                
           IF WS-K820-FND = 'Y'                                                 
              IF K800-NUM-ALLO-SCHEME =  'MCP'                                  
                 MOVE   'Y'     TO WS-C398-MCPS-FRM                             
                                   F398-MCPS-FRM                                
              END-IF                                                            
                                                                                
              MOVE 'N'      TO  F398-ESS-MBRS-FRM                               
                                WS-C398-ESS-MBRS                                
                                                                                
              IF K800-NUM-ELIG-SCHEME = 'OR '                                   
                 MOVE  'Y'  TO  WS-C398-ORPHANS-DECL                            
                                F398-ORPHAN-SCH-FRM                             
              END-IF                                                            
                                                                                
              MOVE 'Y'      TO  F398-APPLN-FRM                                  
                                                                                
              IF WS-C398-LETTER-CNT < 2                                         
                ADD  1      TO  WS-C398-LETTER-CNT                              
              END-IF                                                            
                                                                                
              PERFORM 3405-READ-K816 THRU 3405-EXIT                             
                                                                                
              CALL 'BP13CR31'                                                   
                                                                                
              IF K800-NUM-ALLO-SCHEME = 'MCP'                                   
                 CALL 'BP13CR32'                                                
              END-IF                                                            
                                                                                
              PERFORM 5000-WRITE-BP13F398        THRU 5000-EXIT                 
              PERFORM 6000-WRITE-BP13F200        THRU 6000-EXIT                 
                                                                                
           END-IF.                                                              
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3405-READ-K816.                                                          
           MOVE K800-NUM-NEW-TOWN TO K816-KEY-FLD.                              
           READ BP13K816.                                                       
                                                                                
           IF WS-K816-STATUS = ZERO                                             
              MOVE K816-NME-SITE TO WS-C245-NT-ZONE-NAME                        
           ELSE                                                                 
              MOVE SPACES        TO WS-C245-NT-ZONE-NAME                        
           END-IF.                                                              
                                                                                
       3405-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ** PRINT APPLICATION FORMS AND LOAN LETTER                                
      *-------------------------------------------------------------            
       3410-START-BP13K820.                                                     
      *-------------------------------------------------------------            
                                                                                
           START BP13K820 KEY >= K820-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K820-STATUS                                              
             WHEN   00                                                          
                    PERFORM 3420-READNEXT-BP13K820 THRU 3420-EXIT               
                       UNTIL K820-NUM-REGN NOT = K800-NUM-REGN                  
                        OR   WS-K820-EOF = 'Y'                                  
             WHEN   23                                                          
                    MOVE 'N' TO WS-K820-FND                                     
             WHEN   46                                                          
                    MOVE 'Y' TO WS-K820-EOF                                     
             WHEN   OTHER                                                       
                    DISPLAY 'ERROR BROWSE BP13K820, STAT = '                    
                           K820-KEY-FLD ' ' WS-K820-STATUS                      
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3410-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3420-READNEXT-BP13K820.                                                  
      *---------------------------------------------------------------*         
           READ BP13K820 NEXT                                                   
                         AT END MOVE 'Y' TO WS-K820-EOF                         
                         GO TO 3420-EXIT.                                       
                                                                                
           EVALUATE WS-K820-STATUS                                              
             WHEN 00                                                            
                  IF K820-NUM-REGN = K800-NUM-REGN                              
                     ADD    1   TO WS-NRIC-CNT                                  
                     PERFORM 3430-MOVE-K820-FLDS   THRU 3430-EXIT               
                     ADD    1   TO WS-K820-READ-CNT                             
                     MOVE 'Y'   TO WS-K820-FND                                  
                END-IF                                                          
             WHEN 23                                                            
                  MOVE 'Y'   TO WS-K820-FND                                     
             WHEN 46                                                            
                  MOVE 'Y'   TO WS-K820-EOF                                     
             WHEN OTHER                                                         
                DISPLAY 'BP18K820 - READNXT ERROR, STATUS: '                    
                                                     WS-K820-STATUS             
                MOVE     WS-K820-STATUS    TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3420-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3430-MOVE-K820-FLDS.                                                     
      *---------------------------------------------------------------*         
      *-----------------------------------------*                               
      * CALCULATE COMPLETED AGE FROM BIRTH DATE *                               
      *-----------------------------------------*                               
           IF K820-DTE-BIRTH IS NOT NUMERIC                                     
              MOVE ZEROES                TO K820-DTE-BIRTH                      
           END-IF.                                                              
                                                                                
           MOVE K820-DTE-BIRTH        TO WS-BIRTH-DATE.                         
           COMPUTE WS-AGE  =  WS-DATE  -  WS-BIRTH-DATE.                        
                                                                                
      *---------------------------------------------------*                     
      * IF NRIC FOUND IN BP13K800, PERFORM APPLICANT PARA *                     
      * ELSE PERFORM OCCUPIER PARA                        *                     
      *---------------------------------------------------*                     
           IF  (K820-NUM-NRIC  =  K800-NUM-NRIC1)  OR                           
               (K820-NUM-NRIC  =  K800-NUM-NRIC2)  OR                           
               (K820-NUM-NRIC  =  K800-NUM-NRIC3)  OR                           
               (K820-NUM-NRIC  =  K800-NUM-NRIC4)                               
               ADD 1 TO WS-APPL-CNT                                             
               PERFORM 3440-PROCESS-APPLICANT     THRU 3440-EXIT                
           ELSE                                                                 
               ADD 1 TO WS-OCCP-CNT WS-F398-OCCP-CNT                            
               PERFORM 3450-PROCESS-OCCUPIER      THRU 3450-EXIT                
           END-IF.                                                              
                                                                                
       3430-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3440-PROCESS-APPLICANT.                                                  
      *---------------------------------------------------------------*         
      *    IF  (K820-NUM-NRIC = K800-NUM-NRIC1)                                 
      *         MOVE  1     TO WS-APPL-CNT                                      
      *    END-IF.                                                              
                                                                                
      *    IF  (K820-NUM-NRIC = K800-NUM-NRIC2)                                 
      *         MOVE  2     TO WS-APPL-CNT                                      
      *    END-IF.                                                              
                                                                                
      *    IF  (K820-NUM-NRIC = K800-NUM-NRIC3)                                 
      *         MOVE  3     TO WS-APPL-CNT                                      
      *    END-IF.                                                              
                                                                                
      *    IF  (K820-NUM-NRIC = K800-NUM-NRIC4)                                 
      *         MOVE  4     TO WS-APPL-CNT                                      
      *    END-IF.                                                              
                                                                                
      *------------------------------------*                                    
      * NRIC, NAME, AGE AND OCCUPIER CODE  *                                    
      *------------------------------------*                                    
           MOVE K820-NUM-NRIC     TO                                            
                             WS-C398-APP-NRIC(WS-APPL-CNT)                      
                             F398-NUM-NRIC(WS-APPL-CNT).                        
           MOVE   'A'             TO                                            
                             WS-C398-CDE-APPLICANT(WS-APPL-CNT)                 
                             F398-CDE-OCCP(WS-APPL-CNT).                        
           MOVE   K820-NME-OCCP   TO                                            
                             WS-C398-NME-APPLICANT(WS-APPL-CNT)                 
                             F398-NME-OCCP(WS-APPL-CNT).                        
           MOVE   WS-AGE-CMPLTD   TO                                            
                             WS-C398-AGE-APPLICANT(WS-APPL-CNT)                 
                             F398-NUM-AGE(WS-APPL-CNT).                         
                                                                                
      *-------------------------------*                                         
      * IDENTITY CARD                 *                                         
      *-------------------------------*                                         
           MOVE   'Y'             TO                                            
                             WS-C398-APP-ID-CARD(WS-APPL-CNT)                   
                             F398-ID-CARD(WS-APPL-CNT).                         
                                                                                
      *-------------------------------*                                         
      * BIRTH CERTIFICATE             *                                         
      *-------------------------------*                                         
           IF K820-NUM-RELATIONSHIP = '41' OR '39' OR '06'                      
               MOVE   'Y'         TO                                            
                             WS-C398-APP-BIRTH-CERTF(WS-APPL-CNT)               
                             F398-BIRTH-CERTF(WS-APPL-CNT)                      
           END-IF.                                                              
                                                                                
           IF K800-NUM-ALLO-SCHEME =  'MTF'                                     
              IF K820-NUM-RELATIONSHIP = '02' OR '12' OR '01' OR                
                                         '43' OR '45'                           
                 MOVE   'Y'       TO                                            
                             WS-C398-APP-BIRTH-CERTF(WS-APPL-CNT)               
                             F398-BIRTH-CERTF(WS-APPL-CNT)                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *-------------------------------*                                         
      * MARRIAGE CERTIFICATE          *                                         
      *-------------------------------*                                         
           IF K820-NUM-MARITAL-STATUS  =  '2' OR 'M'                            
               MOVE   'Y'         TO                                            
                             WS-C398-APP-MARRIAGE-CERTF(WS-APPL-CNT)            
                             F398-MARRIAGE-CERTF(WS-APPL-CNT)                   
               PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL                         
                                             WS-CTR = WS-NRIC-CNT               
                 IF K820-NUM-NRIC-SPOUSE = WS-C398-APP-NRIC(WS-CTR)             
                    MOVE   'N'    TO                                            
                             WS-C398-APP-MARRIAGE-CERTF(WS-APPL-CNT)            
                             F398-MARRIAGE-CERTF(WS-APPL-CNT)                   
                 END-IF                                                         
               END-PERFORM                                                      
           END-IF.                                                              
                                                                                
      *----------------------------------*                                      
      * PAYSLIP/UNEMPLOYMENT DECLARATION *                                      
      *----------------------------------*                                      
           IF K820-AMT-INCOME  >  0                                             
               MOVE   'Y'         TO                                            
                             WS-C398-APP-PAY-SLIP(WS-APPL-CNT)                  
                             F398-PAY-SLIP(WS-APPL-CNT)                         
           ELSE                                                                 
               IF (WS-AGE-CMPLTD >= 31)  AND (WS-AGE-CMPLTD <= 55)              
                  MOVE   'Y'      TO                                            
                             WS-C398-APP-UNEMPLYMNT-DECL(WS-APPL-CNT)           
                             F398-UNEMPLY-DECL(WS-APPL-CNT)                     
               ELSE                                                             
                  IF (WS-AGE-CMPLTD >= 18)  AND (WS-AGE-CMPLTD <= 30)           
                     MOVE   'Y'      TO                                         
                             WS-C398-APP-UNEMPLYMNT-DECL(WS-APPL-CNT)           
                             F398-UNEMPLY-DECL(WS-APPL-CNT)                     
                             WS-C398-APP-STUDENT-PASS(WS-APPL-CNT)              
                             F398-STUDENT-PASS(WS-APPL-CNT)                     
                  END-IF                                                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *-------------------------------*                                         
      * DEATH CERTIFICATE             *                                         
      *-------------------------------*                                         
           IF K820-NUM-MARITAL-STATUS  =  '3' OR 'W'                            
               MOVE   'Y'         TO                                            
                             WS-C398-APP-DEATH-CERTF(WS-APPL-CNT)               
                             F398-DEATH-CERTF(WS-APPL-CNT)                      
               MOVE K820-NUM-NRIC-SPOUSE   TO                                   
                             WS-C398-APP-NRIC-DTH-CERTF(WS-APPL-CNT)            
                             F398-NRIC-DEATH-CERTF(WS-APPL-CNT)                 
               MOVE K820-NME-SPOUSE        TO                                   
                             WS-C398-APP-NME-DTH-CERTF(WS-APPL-CNT)             
                             F398-NME-DEATH-CERTF(WS-APPL-CNT)                  
           END-IF.                                                              
                                                                                
      *-------------------------------*                                         
      * DIVORCE CERTIFICATE           *                                         
      *-------------------------------*                                         
           IF K820-NUM-MARITAL-STATUS  =  '4' OR 'D'                            
               MOVE   'Y'         TO                                            
                             WS-C398-APP-DIVORCE-CERTF(WS-APPL-CNT)             
                             F398-DIVORCE-CERTF(WS-APPL-CNT)                    
           END-IF.                                                              
                                                                                
      *-------------------------------*                                         
      * PHOTOGRAPH                    *                                         
      *-------------------------------*                                         
           MOVE   'Y'             TO                                            
                             WS-C398-APP-PHOTOGRAPH(WS-APPL-CNT)                
                             F398-PHOTOGRAPH(WS-APPL-CNT).                      
                                                                                
       3440-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3450-PROCESS-OCCUPIER.                                                   
      *---------------------------------------------------------------*         
      *    ADD   1     TO WS-OCCP-CNT.                                          
      *------------------------------------*                                    
      * NRIC, NAME, AGE AND OCCUPIER CODE  *                                    
      *------------------------------------*                                    
           MOVE K820-NUM-NRIC     TO                                            
                             WS-C398-OCC-NRIC(WS-OCCP-CNT)                      
                             F398-NUM-NRIC(WS-F398-OCCP-CNT).                   
           MOVE   'O'             TO                                            
                             WS-C398-CDE-OCCUPIER(WS-OCCP-CNT)                  
                             F398-CDE-OCCP(WS-F398-OCCP-CNT).                   
           MOVE   K820-NME-OCCP   TO                                            
                             WS-C398-NME-OCCUPIER(WS-OCCP-CNT)                  
                             F398-NME-OCCP(WS-F398-OCCP-CNT).                   
           MOVE   WS-AGE-CMPLTD   TO                                            
                             WS-C398-AGE-OCCUPIER(WS-OCCP-CNT)                  
                             F398-NUM-AGE(WS-F398-OCCP-CNT).                    
                                                                                
           DISPLAY 'AGE ' K820-NUM-NRIC ' ' WS-AGE-CMPLTD                       
                                                                                
      *-------------------------------*                                         
      * IDENTITY CARD                 *                                         
      *-------------------------------*                                         
           IF (K820-NUM-CITIZENSHIP = '10' OR '20')  AND                        
              (WS-AGE-CMPLTD >= 15)                                             
              MOVE   'Y'          TO                                            
                             WS-C398-OCC-ID-CARD(WS-OCCP-CNT)                   
                             F398-ID-CARD(WS-F398-OCCP-CNT)                     
           END-IF.                                                              
                                                                                
      *-------------------------------*                                         
      * EMPLOYMENT PASS/WORK PERMIT   *                                         
      *-------------------------------*                                         
           IF (K820-NUM-CITIZENSHIP  NOT =  '10')                               
              MOVE   'Y'          TO                                            
                             WS-C398-OCC-PASS-PERMIT(WS-OCCP-CNT)               
                             F398-PASS-PERMIT(WS-F398-OCCP-CNT)                 
           END-IF.                                                              
                                                                                
      *-------------------------------*                                         
      * BIRTH CERTIFICATE             *                                         
      *-------------------------------*                                         
           IF K820-NUM-RELATIONSHIP = '41' OR '39' OR '06'                      
               MOVE   'Y'         TO                                            
                             WS-C398-OCC-BIRTH-CERTF(WS-OCCP-CNT)               
                             F398-BIRTH-CERTF(WS-F398-OCCP-CNT)                 
           END-IF.                                                              
                                                                                
           IF K800-NUM-ALLO-SCHEME =  'MTF'                                     
              IF K820-NUM-RELATIONSHIP = '02' OR '12' OR '01' OR                
                                         '43' OR '45'                           
                 MOVE   'Y'       TO                                            
                             WS-C398-OCC-BIRTH-CERTF(WS-OCCP-CNT)               
                             F398-BIRTH-CERTF(WS-F398-OCCP-CNT)                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *-------------------------------*                                         
      * MARRIAGE CERTIFICATE          *                                         
      *-------------------------------*                                         
           IF K820-NUM-MARITAL-STATUS  =  '2' OR 'M'                            
              MOVE   'Y'          TO                                            
                             WS-C398-OCC-MARRIAGE-CERTF(WS-OCCP-CNT)            
                             F398-MARRIAGE-CERTF(WS-F398-OCCP-CNT)              
              PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL                          
                                             WS-CTR = WS-NRIC-CNT               
                 IF K820-NUM-NRIC-SPOUSE = WS-C398-OCC-NRIC(WS-CTR)             
                    MOVE   'N'    TO                                            
                             WS-C398-OCC-MARRIAGE-CERTF(WS-OCCP-CNT)            
                             F398-MARRIAGE-CERTF(WS-F398-OCCP-CNT)              
                 END-IF                                                         
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
      *----------------------------------*                                      
      * PAYSLIP/UNEMPLOYMENT DECLARATION *                                      
      *----------------------------------*                                      
           IF K820-AMT-INCOME  >  0                                             
               MOVE   'Y'         TO                                            
                             WS-C398-OCC-PAY-SLIP(WS-OCCP-CNT)                  
                             F398-PAY-SLIP(WS-F398-OCCP-CNT)                    
           ELSE                                                                 
               IF (WS-AGE-CMPLTD >= 31)  AND (WS-AGE-CMPLTD <= 55)              
                  MOVE   'Y'      TO                                            
                             WS-C398-OCC-UNEMPLYMNT-DECL(WS-OCCP-CNT)           
                             F398-UNEMPLY-DECL(WS-F398-OCCP-CNT)                
               ELSE                                                             
                  IF (WS-AGE-CMPLTD >= 18)  AND (WS-AGE-CMPLTD <= 30)           
                     MOVE   'Y'   TO                                            
                             WS-C398-OCC-UNEMPLYMNT-DECL(WS-OCCP-CNT)           
                             F398-UNEMPLY-DECL(WS-F398-OCCP-CNT)                
                             WS-C398-OCC-STUDENT-PASS(WS-OCCP-CNT)              
                             F398-STUDENT-PASS(WS-F398-OCCP-CNT)                
                  END-IF                                                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *-------------------------------*                                         
      * DEATH CERTIFICATE             *                                         
      *-------------------------------*                                         
           IF K820-NUM-MARITAL-STATUS  =  '3' OR 'W'                            
               MOVE   'Y'         TO                                            
                             WS-C398-OCC-DEATH-CERTF(WS-OCCP-CNT)               
                             F398-DEATH-CERTF(WS-F398-OCCP-CNT)                 
               MOVE K820-NUM-NRIC-SPOUSE   TO                                   
                             WS-C398-OCC-NRIC-DTH-CERTF(WS-OCCP-CNT)            
                             F398-NRIC-DEATH-CERTF(WS-F398-OCCP-CNT)            
               MOVE K820-NME-SPOUSE        TO                                   
                             WS-C398-OCC-NME-DTH-CERTF(WS-OCCP-CNT)             
                             F398-NME-DEATH-CERTF(WS-F398-OCCP-CNT)             
           END-IF.                                                              
                                                                                
      *-------------------------------*                                         
      * DIVORCE CERTIFICATE           *                                         
      *-------------------------------*                                         
           IF K820-NUM-MARITAL-STATUS  =  '4' OR 'D'                            
               MOVE   'Y'         TO                                            
                             WS-C398-OCC-DIVORCE-CERTF(WS-OCCP-CNT)             
                             F398-DIVORCE-CERTF(WS-F398-OCCP-CNT)               
           END-IF.                                                              
                                                                                
      *-------------------------------*                                         
      * MARRIAGE CONSENT OF MINOR     *                                         
      *-------------------------------*                                         
           IF (K820-NUM-RELATIONSHIP  =  '09')  AND                             
              (WS-AGE-CMPLTD >= 18  AND  WS-AGE-CMPLTD < 21)                    
               MOVE   'Y'         TO                                            
                             WS-C398-OCC-MARRIAGE-CONSENT(WS-OCCP-CNT)          
                             F398-MARRIAGE-CONSNT(WS-F398-OCCP-CNT)             
           END-IF.                                                              
                                                                                
       3450-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
       4000-APPLN-HSELOAN-LET.                                                  
      ************************************************************              
           MOVE SPACES                   TO BP13KB78-REC.                       
           INITIALIZE                       BP13KB78-REC.                       
                                                                                
           MOVE F740-NUM-REF             TO KB78-NUM-REGN.                      
                                                                                
           READ BP13KB78.                                                       
                                                                                
           EVALUATE WS-KB78-STATUS                                              
               WHEN 00                                                          
                    ADD 1                TO WS-KB78-FND-CNT                     
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1                TO WS-KB78-NFND-CNT                    
                    DISPLAY 'REGN NO. NOT FOUND IN BP13KB78. REGN = '           
                            F740-NUM-REGN                                       
               WHEN OTHER                                                       
                    DISPLAY 'READ ERROR BP13KB78, STATUS '                      
                            WS-KB78-STATUS                                      
                    MOVE WS-KB78-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       5000-WRITE-BP13F398.                                                     
      *---------------------------------------------------------------*         
           MOVE WS-CURRENT-DATE TO F398-DTE-UPDATE.                             
           MOVE 'BP13C398'      TO F398-NUM-USERID.                             
           WRITE BP13F398-REC.                                                  
           ADD   1   TO WS-F398-WRITE-CNT.                                      
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       6000-WRITE-BP13F200.                                                     
      *---------------------------------------------------------------*         
           WRITE BP13F200-REC.                                                  
           ADD   1   TO WS-F200-WRITE-CNT.                                      
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
       9000-CLOSE-ROUTINE.                                                      
      ************************************************************              
           DISPLAY ' '.                                                         
           DISPLAY 'PROGRAM  : BP13C89Y'.                                       
           DISPLAY 'RUN DATE : ' WS-CURR-DATE.                                  
           DISPLAY ' '.                                                         
           DISPLAY '        BP13C89Y CONTROL TOTALS        '.                   
           DISPLAY '---------------------------------------'.                   
           DISPLAY ' NO OF RECS READ        (F740): ' WS-F740-READ-CNT.         
           DISPLAY ' NO OF RECS FOUND       (K800): ' WS-K800-FND-CNT.          
           DISPLAY ' NO OF RECS NFND/CANCEL (K800): ' WS-K800-NFND-CNT.         
           DISPLAY ' '.                                                         
           DISPLAY ' NO OF RECS FOUND       (K200): ' WS-K200-FND-CNT.          
           DISPLAY ' NO OF RECS NFND        (K200): ' WS-K200-NFND-CNT.         
           DISPLAY ' NO OF RECS WRITTEN     (F200): ' WS-F200-WRITE-CNT.        
           DISPLAY ' '.                                                         
           DISPLAY ' NO OF RECS FOUND       (KB78): ' WS-KB78-FND-CNT.          
           DISPLAY ' NO OF RECS NOT FOUND   (KB78): ' WS-KB78-NFND-CNT.         
           DISPLAY ' '.                                                         
           DISPLAY ' NO OF TOTAL PAGES WRITTEN    : ' WS-TOT-PAGE-CNT.          
           DISPLAY ' NO. OF QUESTIONNAIRE PRINTED : ' WS-FORM-PRTED.            
           DISPLAY ' '.                                                         
           DISPLAY ' NO OF RECS WRITTEN     (F398): ' WS-F398-WRITE-CNT.        
                                                                                
           CLOSE BP13F740                                                       
                 BP13F200                                                       
                 BP13K060                                                       
                 BP13KB78                                                       
                 BP13K816                                                       
                 BP13K200                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K830                                                       
                 P13P89DA                                                       
                 P13P89DB                                                       
                 P13L89DA                                                       
                 P13L89DB.                                                      
                                                                                
           IF WS-K060-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR BP13K060, STATUS ' WS-K060-STATUS            
              MOVE WS-K060-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-KB78-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR BP13KB78, STATUS ' WS-KB78-STATUS            
              MOVE WS-KB78-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K816-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR BP13K816, STATUS ' WS-K816-STATUS            
              MOVE WS-K816-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K200-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR BP13K200, STATUS ' WS-K200-STATUS            
              MOVE WS-K200-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR BP13K800, STATUS ' WS-K800-STATUS            
              MOVE WS-K800-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR BP13K820, STATUS ' WS-K820-STATUS            
              MOVE WS-K820-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE ERROR BP13K830, STATUS ' WS-K830-STATUS            
              MOVE WS-K830-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       COPY BP13CR31.                                                           
       COPY BP13CR32.                                                           
       END PROGRAM BP13C89Y.                                                    
