      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13C770.                                                 
      *AUTHOR.        ZARITA DANE SARMIENTO.                                    
      *DATE-WRITTEN.  04 SEPT 2021.                                             
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : THIS PROGRAM GENERATE REPORT FROM           *           
      *                 LEGAL DOCUMENT.                             *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP138863 04/09/2021 ZAR7 NEW PROGRAM                        *           
      * BP138863 13/09/2021 ZAR7 TO AMEND THE REPORT FORMAT         *           
      * BP139043 10/03/2022 ZAR7 TO CORRECT/AMEND REPORT            *           
      * BP139043 11/03/2022 ZAR7 TO CORRECT/AMEND REPORT            *           
      * BP139092 14/03/2022 ZAR7 TO CORRECT/AMEND REPORT            *           
      * BP139159 16/06/2022 ZAR7 TO CORRECT REC LENGTH OF L770 FOR  *           
      *                          EMAIL SENDING                      *           
      * BP139197 08/07/2022 ZAR7 TO CORRECT REPORT FOOTER           *           
      * BP139240 13/07/2022 ZAR7 TO CORRECT REPORT ISSUE FOR MISSING*           
      *                          DATA                               *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
                                                                                
           SELECT P13L770A   ASSIGN        TO  P13L770A.                        
                                                                                
           SELECT AB05K006   ASSIGN        TO  AB05K006                         
                             ACCESS MODE   IS  RANDOM                           
                             ORGANIZATION  IS  INDEXED                          
                             RECORD KEY    IS  K006-HDB-REF-SF                  
                             FILE STATUS   IS  AB05K006-STATUS.                 
                                                                                
           SELECT BM06K110   ASSIGN        TO  BM06K110                         
                             ACCESS MODE   IS  RANDOM                           
                             ORGANIZATION  IS  INDEXED                          
                             RECORD KEY    IS  K110-KEY-FLD                     
                             FILE STATUS   IS  BM06K110-STATUS.                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  P13L770A                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORDING MODE IS V                              
                               LABEL RECORD IS STANDARD.                        
       01  L770A-PRINT-REC            PIC X(200).                               
                                                                                
       FD   AB05K006                                                            
            RECORD CONTAINS  800 CHARACTERS.                                    
       COPY B05K006.                                                            
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  FILE-STATUS.                                                         
           05  AB05K006-STATUS        PIC 99     VALUE 0.                       
           05  BM06K110-STATUS        PIC 99     VALUE 0.                       
                                                                                
       01  WS-CTR                 PIC 9(02) VALUE ZEROES.                       
       01  CNT-LINE-ON-PAGE       PIC 9(05) VALUE ZEROES.                       
       01  CNT-PRT-CASES          PIC 9(05) VALUE ZEROES.                       
       01  CNT-PAGE               PIC 9(05) VALUE ZEROES.                       
       01  WS-CURSOR-EOF          PIC X     VALUE SPACE.                        
       01  WS-EOF                 PIC X     VALUE SPACE.                        
       01  WS-SQLCODE             PIC ---9.                                     
       01  WS-PREV-NUM-LIST       PIC X(10).                                    
       01  WS-PREV-NUM-BSNS-REF   PIC X(15).                                    
       01  WS-RETURN-CODE         PIC 9(02) VALUE ZEROES.                       
       01  WS-SCH-ACCT            PIC X(11) VALUE SPACES.                       
       01  WS-DTE-CCYYMMDD.                                                     
           05 WS-DTE-CCYY                   PIC X(04) VALUE SPACES.             
           05 WS-DTE-MM                     PIC X(02) VALUE SPACES.             
           05 WS-DTE-DD                     PIC X(02) VALUE SPACES.             
       01  WS-TIME-HHMMSS.                                                      
           05 WS-TIME-HH                    PIC X(02) VALUE SPACES.             
           05 WS-TIME-MM                    PIC X(02) VALUE SPACES.             
           05 WS-TIME-SS                    PIC X(02) VALUE SPACES.             
           05 WS-TIME-MS                    PIC X(02) VALUE SPACES.             
       01  WS-LEFT-JUST-TXT                 PIC X(05) VALUE SPACES.             
       01  WS-RIGHT-JUST-TXT                PIC X(05) VALUE SPACES              
                                          JUST RIGHT.                           
       01  WS-LEFT-JUST-TXT-4               PIC X(04) VALUE SPACES.             
       01  WS-RIGHT-JUST-TXT-4              PIC X(04) VALUE SPACES              
                                          JUST RIGHT.                           
       01  WS-CHR-CNT                       PIC 9(02) VALUE ZEROES.             
       01  WS-BLANK-CNT                     PIC 9(02) VALUE ZEROES.             
       01  WS-CHR-LEN                       PIC 9(02) VALUE ZEROES.             
       01  WS-START-POSN                    PIC 9(02) VALUE ZEROES.             
       01  WS-CURRENT-DATE                  PIC 9(08) VALUE ZEROES.             
       01  WS-YESTERDAY                     PIC 9(08) VALUE ZEROES.             
       01  WS-DTE-CREATE                    PIC 9(08) VALUE ZEROES.             
       01  WS-BYPASS                        PIC 9(05) VALUE ZEROES.             
                                                                                
       01  WS-SQL-STATUS.                                                       
           05 WS-SQL-OK                PIC S9(03) VALUE +000.                   
           05 WS-SQL-NFND              PIC S9(03) VALUE +100.                   
           05 WS-SQL-DUPREC            PIC S9(03) VALUE -803.                   
                                                                                
       01  WS-FORMAT-INPUT.                                                     
           05  WS-BLK                      PIC X(05)   VALUE SPACES.            
           05  WS-BLK1                     PIC X(05)   VALUE SPACES.            
           05  WS-SUB                      PIC 9(01)   VALUE ZEROES.            
           05  WS-SUB1                     PIC 9(01)   VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-FTMP-EOF                 PIC X(01)   VALUE 'N'.               
           05  WS-CHAR-FLAG                PIC X(01)   VALUE 'N'.               
                                                                                
                                                                                
       01  WS-VAR.                                                              
               10  WS1-NUM-SA             PIC X(1)   VALUE SPACE.               
               10  WS1-NUM-LIE            PIC X(1)   VALUE SPACE.               
               10  WS1-NUM-LIED           PIC X(1)   VALUE SPACE.               
               10  WS1-NUM-MIE            PIC X(1)   VALUE SPACE.               
               10  WS1-NUM-DOA            PIC X(1)   VALUE SPACE.               
               10  WS1-NUM-SU             PIC X(1)   VALUE SPACE.               
               10  WS1-NUM-DOA-ST         PIC X(1)   VALUE SPACE.               
                                                                                
       01      WS-AB05K006-FND-TAG    PIC X(1)   VALUE SPACE.                   
               88  AB05K006-FND                  VALUE 'Y'.                     
                                                                                
       01  WS2-REPORT-VARIABLES.                                                
      *----------------------------------------------------------------*        
      *        ACCUMULATORS                                            *        
      *----------------------------------------------------------------*        
           05  WS2-NUM-VARIABLES.                                               
               10  WS2-NUM-SA             PIC S9(4)  VALUE +0.                  
               10  WS2-NUM-LIE            PIC S9(4)  VALUE +0.                  
               10  WS2-NUM-LIED           PIC S9(4)  VALUE +0.                  
               10  WS2-NUM-MIE            PIC S9(4)  VALUE +0.                  
               10  WS2-NUM-DOA            PIC S9(4)  VALUE +0.                  
               10  WS2-NUM-SU             PIC S9(4)  VALUE +0.                  
               10  WS2-NUM-DOA-ST         PIC S9(4)  VALUE +0.                  
               10  WS2-NUM-SA-TOTAL       PIC 9(4)   VALUE  0.                  
               10  WS2-NUM-LIE-TOTAL      PIC 9(4)   VALUE  0.                  
               10  WS2-NUM-LIED-TOTAL     PIC 9(4)   VALUE  0.                  
               10  WS2-NUM-MIE-TOTAL      PIC 9(4)   VALUE  0.                  
               10  WS2-NUM-DOA-TOTAL      PIC 9(5)   VALUE  0.                  
               10  WS2-NUM-SU-TOTAL       PIC 9(5)   VALUE  0.                  
               10  WS2-NUM-DOA-ST-TOTAL   PIC 9(4)   VALUE  0.                  
               10  WS2-TOTAL              PIC 9(4)   VALUE  0.                  
      *----------------------------------------------------------------*        
      *        COUNTERS FOR REPORT  (FILE SENT)                        *        
      *----------------------------------------------------------------*        
               10  WS2-CNT-PAGE-FS    PIC 9(5)   COMP-3  VALUE 0.               
               10  WS2-SERIAL-NO-FS   PIC 9(3)   COMP-3  VALUE 0.               
               10  WS2-CNT-LINES-FS   PIC 9(3)   COMP-3  VALUE 0.               
                                                                                
       01  WS1-DATE-VARIABLES.                                                  
           05  WS1-CURRENT-DATE.                                                
               10  WS1-CURRENT-CC     PIC 99.                                   
               10  WS1-CURRENT-YY     PIC 99.                                   
               10  WS1-CURRENT-MM     PIC 99.                                   
               10  WS1-CURRENT-DD     PIC 99.                                   
                                                                                
       01  WS5-DEFAULT-VALUES.                                                  
           05  WS5-MAX-LINE-FS        PIC 9(2)   VALUE 31.                      
           05  WS4-PRTED-ACK-IND      PIC X      VALUE SPACE.                   
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *        C O M M O N     L A Y - O U T   (HEADINGS)  FOR :       *        
      *            LIST OF FILES SENT TO BRANCH OFFICE     AND         *        
      *            LIST OF SALES ORDERS SENT TO BRANCH OFFICE          *        
      *----------------------------------------------------------------*        
       01  L770-HDG-1.                                                          
           05  FILLER                 PIC X(8)   VALUE 'BP13L770'.              
           05  FILLER                 PIC X(37)  VALUE SPACES.                  
           05  FILLER                 PIC X(39)  VALUE                          
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                 PIC X(30)  VALUE SPACES.                  
           05  FILLER                 PIC X(8)   VALUE ' PAGE : '.              
           05  L770-HD2B-PAGE-NO      PIC ZZZZ9.                                
                                                                                
       01  L770-HDG-2A.                                                         
           05  FILLER                 PIC X(55)  VALUE SPACES.                  
           05  FILLER                 PIC X(30)  VALUE                          
              'AGREEMENT TEAM (SALES)'.                                         
                                                                                
       01  L770-HDG-2B.                                                         
           05  FILLER                 PIC X(43)  VALUE SPACES.                  
           05  L770-HD2B-DESC         PIC X(52)  VALUE SPACES.                  
           05  FILLER                 PIC X(4)   VALUE SPACES.                  
           05  FILLER                 PIC X(9)   VALUE 'REPORT '.               
           05  FILLER                 PIC X(8)   VALUE 'DATE :'.                
           05  L770-HD2B-DATE.                                                  
               10  L770-HD2B-DATE-DD  PIC X(2)   VALUE SPACES.                  
               10  FILLER             PIC X      VALUE '/'.                     
               10  L770-HD2B-DATE-MM  PIC X(2)   VALUE SPACES.                  
               10  FILLER             PIC X      VALUE '/'.                     
               10  L770-HD2B-DATE-CC  PIC X(2)   VALUE SPACES.                  
               10  L770-HD2B-DATE-YY  PIC X(2)   VALUE SPACES.                  
                                                                                
       01  L770-HDG-4.                                                          
           05  FILLER                 PIC X(43)  VALUE SPACES.                  
           05  FILLER                 PIC X(10)  VALUE 'LIST-NO :'.             
           05  L770-HD4-LIST-NO       PIC X(10)  VALUE SPACES.                  
                                                                                
      *----------------------------------------------------------------*        
      *        REPORT LAY-OUT FOR LISTING OF FILES SENT TO AREA OFFICE *        
      *----------------------------------------------------------------*        
       01  L770-FS-HDG-5.                                                       
           05  FILLER                 PIC X(74)  VALUE SPACES.                  
           05  FILLER                 PIC X(30)  VALUE                          
               'SA   LIE   LIE   MIE  DOA  SU '.                                
           05  FILLER                 PIC X(28)  VALUE                          
               '  DOAT STAMP FEE  KEY ISSUE '.                                  
                                                                                
       01  L770-FS-HDG-5A.                                                      
           05  FILLER                 PIC X      VALUE SPACE.                   
           05  FILLER                 PIC X(15)  VALUE 'S/N  REGN NO'.          
           05  FILLER                 PIC X(13)  VALUE 'SCHEME A/C NO'.         
           05  FILLER                 PIC X(02)  VALUE SPACES.                  
           05  FILLER                 PIC X(07)  VALUE 'ADDRESS'.               
           05  FILLER                 PIC X(36)  VALUE SPACES.                  
           05  FILLER                 PIC X(31)  VALUE                          
               ' O   O     DUP   O    DUP  O   '.                               
           05  FILLER                 PIC X(24)  VALUE                          
               '  O    BALANCE    DATE'.                                        
           05  FILLER                 PIC X(05)  VALUE SPACES.                  
                                                                                
      *----------------------------------------------------------------*        
      *        DETAIL LINE FOR REPORT FORMAT  (LIST OF FILES SENT)     *        
      *----------------------------------------------------------------*        
       01  L770-FS-DETAIL-1.                                                    
           05  FILLER                 PIC X      VALUE SPACE.                   
           05  L770-FS-SERIAL-NO      PIC ZZ9    VALUE ZEROES.                  
           05  FILLER                 PIC X(2)   VALUE SPACES.                  
           05  L770-FS-REGN-NO        PIC X(8)   VALUE SPACES.                  
           05  FILLER                 PIC X(2)   VALUE SPACE.                   
           05  L770-FS-SCHEME         PIC 9(4)   VALUE ZEROES.                  
           05  FILLER                 PIC X      VALUE '-'.                     
           05  L770-FS-ACC            PIC 9(4)   VALUE ZEROES.                  
           05  FILLER                 PIC X      VALUE '-'.                     
           05  L770-FS-CHK            PIC 9      VALUE ZERO.                    
           05  FILLER                 PIC X      VALUE '-'.                     
           05  L770-FS-ACC-TYPE       PIC 99     VALUE ZEROES.                  
           05  FILLER                 PIC X(1)   VALUE SPACES.                  
           05  FILLER                 PIC X(4)   VALUE 'BLK'.                   
           05  L770-FS-BLK-NO         PIC X(5)   VALUE SPACES.                  
           05  FILLER                 PIC XX     VALUE ' #'.                    
           05  L770-FS-LEVEL-NO       PIC X(2)   VALUE SPACES.                  
           05  FILLER                 PIC X      VALUE '-'.                     
           05  L770-FS-MAIN-UNIT      PIC X(5)   VALUE SPACES.                  
           05  FILLER                 PIC X      VALUE SPACE.                   
           05  L770-FS-ST-NAME        PIC X(20)  VALUE SPACES.                  
           05  FILLER                 PIC X(4)   VALUE SPACES.                  
           05  L770-FS-SA             PIC X      VALUE SPACE.                   
           05  FILLER                 PIC X(3)   VALUE SPACES.                  
           05  L770-FS-LIE            PIC X      VALUE SPACE.                   
           05  FILLER                 PIC X(6)   VALUE SPACES.                  
           05  L770-FS-LIED           PIC X      VALUE SPACE.                   
           05  FILLER                 PIC X(4)   VALUE SPACES.                  
           05  L770-FS-MIE            PIC X      VALUE SPACE.                   
           05  FILLER                 PIC X(4)   VALUE SPACES.                  
           05  L770-FS-DOA            PIC X      VALUE SPACE.                   
           05  FILLER                 PIC X(4)   VALUE SPACES.                  
           05  L770-FS-SU             PIC X      VALUE SPACE.                   
           05  FILLER                 PIC X(4)   VALUE SPACES.                  
           05  L770-FS-DOA-ST         PIC X      VALUE SPACE.                   
           05  FILLER                 PIC X(2)   VALUE SPACES.                  
           05  L770-STAMP-FEE-BAL     PIC Z(8)9.99.                             
           05  FILLER                 PIC X(1)   VALUE SPACES.                  
           05  L770-KEY-ISSUE-DATE    PIC X(10)  VALUE SPACE.                   
                                                                                
      *----------------------------------------------------------------*        
      *       REPORT LAY-OUT FOR ACKNOWLEDGEMENT DETAILS               *        
      *----------------------------------------------------------------*        
       01  L770-SO-DETAIL-1.                                                    
           05  FILLER                 PIC X(63)  VALUE SPACES.                  
           05  FILLER                 PIC X(11)  VALUE 'SUB-TOTAL'.             
           05  L770-FS-NUM-SA         PIC Z9.                                   
           05  FILLER                 PIC X(2)   VALUE SPACES.                  
           05  L770-FS-NUM-LIE        PIC Z9.                                   
           05  FILLER                 PIC X(5)   VALUE SPACE.                   
           05  L770-FS-NUM-LIED       PIC Z9.                                   
           05  FILLER                 PIC X(3)   VALUE SPACES.                  
           05  L770-FS-NUM-MIE        PIC Z9.                                   
           05  FILLER                 PIC X(3)   VALUE SPACES.                  
           05  L770-FS-NUM-DOA        PIC Z9.                                   
           05  FILLER                 PIC X(3)   VALUE SPACES.                  
           05  L770-FS-NUM-SU         PIC Z9.                                   
           05  FILLER                 PIC X(3)   VALUE SPACE.                   
           05  L770-FS-NUM-DOA-ST     PIC Z9.                                   
           05  FILLER                 PIC X(27)  VALUE SPACE.                   
                                                                                
       01  L770-SO-DETAIL-1A.                                                   
           05  FILLER                 PIC X(67)  VALUE SPACES.                  
           05  FILLER                 PIC X(6)   VALUE  'TOTAL'.                
           05  L770-FS-TOT-SA         PIC ZZ9.                                  
           05  FILLER                 PIC X(54)  VALUE SPACES.                  
                                                                                
       01  L770-SO-DETAIL-2.                                                    
           05  FILLER                 PIC X(2)   VALUE SPACES.                  
           05  FILLER                 PIC X(28)  VALUE                          
               'TO: PROPERTY LEGAL (LEASES) '.                                  
           05  FILLER                 PIC X(23)  VALUE                          
               'DEPARTMENT, LEGAL GROUP'.                                       
                                                                                
       01  L770-SO-DETAIL-3.                                                    
           05  FILLER                 PIC X(6)   VALUE SPACES.                  
      *    05  FILLER                 PIC X(17)  VALUE                          
      *        'THE ATTACHED ARE'.                                              
      *    05  L770-SO-SET1           PIC ZZ9    VALUE ZEROES.                  
      *    05  FILLER                 PIC X(46)  VALUE                          
      *        ' COPIES OF LEGAL DOCUMENTS FOR YOUR FOLLOW-UP '.                
      *    05  FILLER                 PIC X(7)   VALUE 'ACTION.'.               
                                                                                
       01  L770-SO-DETAIL-4.                                                    
           05  FILLER                 PIC X(6)   VALUE SPACES.                  
           05  FILLER                 PIC X(48)  VALUE                          
               'PLEASE ACKNOWLEDGE RECEIPT BY SIGNING BELOW AND'.               
           05  FILLER                 PIC X(34)  VALUE                          
               'RETURNING A COPY TO THE SENDER'.                                
           05  FILLER                 PIC X(29)  VALUE                          
               '(SALES DEPT, AGREEMENT TEAM).'.                                 
                                                                                
       01  L770-SO-DETAIL-5.                                                    
           05  FILLER                 PIC X(6)   VALUE SPACES.                  
           05  FILLER                 PIC X(21)  VALUE                          
               'NAME OF SENDER    :'.                                           
           05  FILLER                 PIC X(25)  VALUE ALL '_'.                 
           05  FILLER                 PIC X(10)  VALUE SPACES.                  
           05  FILLER                 PIC X(13)  VALUE 'SIGNATURE :'.           
           05  FILLER                 PIC X(15)  VALUE ALL '_'.                 
           05  FILLER             PIC X(18)  VALUE ' LEGAL DOC  SENT '.         
           05  FILLER                 PIC X(8)   VALUE 'DATE :  '.              
           05  FILLER                 PIC X(18)  VALUE ALL '_'.                 
                                                                                
       01  L770-SO-DETAIL-6.                                                    
           05  FILLER                 PIC X(6)   VALUE SPACES.                  
           05  FILLER                 PIC X(21)  VALUE                          
               'NAME OF RECIPIENT :'.                                           
           05  FILLER                 PIC X(25)  VALUE ALL '_'.                 
           05  FILLER                 PIC X(10)  VALUE SPACES.                  
           05  FILLER                 PIC X(13)  VALUE 'SIGNATURE :'.           
           05  FILLER                 PIC X(15)  VALUE ALL '_'.                 
           05  FILLER             PIC X(18)  VALUE ' ACKNOWLEDGEMENT '.         
           05  FILLER                 PIC X(8)   VALUE 'DATE :'.                
           05  FILLER                 PIC X(18)  VALUE ALL '_'.                 
                                                                                
      *----------------------------------------------------------------*        
      *        DEFAULT HEADINGS FOR THE VARIOUS LISTINGS               *        
      *----------------------------------------------------------------*        
       01      WS5-HD2B-DESC-FS       PIC X(52)  VALUE                          
               'LIST  OF  LEGAL  DOCUMENTS  SENT  TO  LEGAL DEPT   '.           
                                                                                
                                                                                
      *--------------------------------------------------------------*          
      *                          SQLCA                               *          
      *--------------------------------------------------------------*          
           EXEC SQL INCLUDE SQLCA          END-EXEC.                            
           EXEC SQL INCLUDE LEGBTITM       END-EXEC.                            
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 3000-PROCESS-REC  THRU 3000-EXIT                             
                   UNTIL WS-EOF = 'Y'.                                          
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN                                                                 
                INPUT  AB05K006                                                 
                       BM06K110                                                 
                OUTPUT P13L770A.                                                
                                                                                
           IF AB05K006-STATUS NOT = 00 AND NOT = 97                             
              DISPLAY 'OPENING ERROR, AB05K006-STATUS ' AB05K006-STATUS         
              MOVE AB05K006-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES.                                         
                                                                                
           IF BM06K110-STATUS NOT = 00 AND NOT = 97                             
              DISPLAY 'OPENING ERROR, K110-STATUS ' BM06K110-STATUS             
              MOVE BM06K110-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES.                                         
                                                                                
                                                                                
           ACCEPT WS-DTE-CCYYMMDD   FROM   DATE YYYYMMDD.                       
           ACCEPT WS-TIME-HHMMSS    FROM   TIME.                                
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-CURRENT-DATE.                
           COMPUTE WS-YESTERDAY = FUNCTION DATE-OF-INTEGER                      
                        (FUNCTION INTEGER-OF-DATE(WS-CURRENT-DATE) - 1)         
                                                                                
           PERFORM 3100-DECLARE-CURSOR   THRU 3100-EXIT.                        
           PERFORM 3200-OPEN-CURSOR      THRU 3200-EXIT.                        
           PERFORM UNTIL WS-EOF = 'Y'                                           
             PERFORM 3400-FETCH-CURSOR     THRU 3400-EXIT                       
             PERFORM 3500-PRINT-REPORT     THRU 3500-EXIT                       
           END-PERFORM.                                                         
           PERFORM 3300-CLOSE-CURSOR     THRU 3300-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3100-DECLARE-CURSOR.                                                     
      *-----------------*                                                       
                                                                                
                                                                                
           INITIALIZE     SQLCODE                                               
                          L770A-PRINT-REC.                                      
                                                                                
           EXEC SQL                                                             
              DECLARE CUR_LEGLIST1 CURSOR FOR                                   
               SELECT *                                                         
                 FROM LEGAL_DOC_BATCH_ITEM                                      
                 ORDER BY NUM_LIST   ASC, NUM_BSNS_REF ASC                      
           END-EXEC.                                                            
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3200-OPEN-CURSOR.                                                        
      *-------------------*                                                     
                                                                                
           EXEC SQL OPEN CUR_LEGLIST1 END-EXEC.                                 
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN 000                                                             
                CONTINUE                                                        
           WHEN OTHER                                                           
                 DISPLAY 'ERROR OPEN CURSOR : ' '/' SQLSTATE                    
                 DISPLAY 'SQLCODE IS : ' SQLCODE                                
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3300-CLOSE-CURSOR.                                                       
      *-------------------*                                                     
           EXEC SQL CLOSE CUR_LEGLIST1 END-EXEC.                                
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN 000                                                             
                CONTINUE                                                        
           WHEN OTHER                                                           
                 DISPLAY 'ERROR CLOSE CURSOR : ' '/' SQLSTATE                   
                 DISPLAY 'SQLCODE IS : ' SQLCODE                                
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3400-FETCH-CURSOR.                                                       
      *---------------------*                                                   
                                                                                
           EXEC SQL FETCH CUR_LEGLIST1 INTO                                     
                   :DCLLEGAL-DOC-BATCH-ITEM.NUM-MASTER-LIST     ,               
                   :DCLLEGAL-DOC-BATCH-ITEM.NUM-LIST            ,               
                   :DCLLEGAL-DOC-BATCH-ITEM.NUM-BSNS-REF        ,               
                   :DCLLEGAL-DOC-BATCH-ITEM.NUM-DOC-TYPE        ,               
                   :DCLLEGAL-DOC-BATCH-ITEM.DTE-CREATE          ,               
                   :DCLLEGAL-DOC-BATCH-ITEM.DTE-RETURN          ,               
                   :DCLLEGAL-DOC-BATCH-ITEM.NUM-RETURN-USERID   ,               
                   :DCLLEGAL-DOC-BATCH-ITEM.DTE-ACK             ,               
                   :DCLLEGAL-DOC-BATCH-ITEM.NUM-ACK-USERID      ,               
                   :DCLLEGAL-DOC-BATCH-ITEM.TME-CHECK-IN        ,               
                   :DCLLEGAL-DOC-BATCH-ITEM.NUM-CHECK-IN-USERID ,               
                   :DCLLEGAL-DOC-BATCH-ITEM.TME-CHECK-OUT       ,               
                   :DCLLEGAL-DOC-BATCH-ITEM.NUM-CHECK-OUT-USERID,               
                   :DCLLEGAL-DOC-BATCH-ITEM.NUM-SCHEME          ,               
                   :DCLLEGAL-DOC-BATCH-ITEM.NUM-ACNT            ,               
                   :DCLLEGAL-DOC-BATCH-ITEM.NUM-TNCY            ,               
                   :DCLLEGAL-DOC-BATCH-ITEM.NUM-BLDNG           ,               
                   :DCLLEGAL-DOC-BATCH-ITEM.NME-STREET          ,               
                   :DCLLEGAL-DOC-BATCH-ITEM.NUM-LEVEL           ,               
                   :DCLLEGAL-DOC-BATCH-ITEM.NUM-UNIT            ,               
                   :DCLLEGAL-DOC-BATCH-ITEM.NUM-POST            ,               
                   :DCLLEGAL-DOC-BATCH-ITEM.NUM-UNIT-ID                         
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN 000                                                             
                CONTINUE                                                        
           WHEN 100                                                             
                MOVE 'Y'                    TO WS-EOF                           
           WHEN OTHER                                                           
                 DISPLAY 'ERROR FETCH CURSOR : ' '/' SQLSTATE                   
                 DISPLAY 'SQLCODE IS : ' SQLCODE                                
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3500-PRINT-REPORT.                                                       
      *-----------------*                                                       
           IF WS-EOF = 'Y'                                                      
                 PERFORM 6200-LIST-FILE-SENT  THRU 6200-EXIT                    
                 PERFORM 6300-PRINT-ACKNOW-LINE THRU 6300-EXIT                  
                 GO TO 3500-EXIT                                                
           END-IF.                                                              
                                                                                
           MOVE DTE-CREATE(1:4)    TO WS-DTE-CREATE(1:4)                        
           MOVE DTE-CREATE(6:2)    TO WS-DTE-CREATE(5:2)                        
           MOVE DTE-CREATE(9:2)    TO WS-DTE-CREATE(7:2)                        
      *    DISPLAY 'DTE-CREATE -' WS-DTE-CREATE                                 
      *    DISPLAY 'CURR-DTE   -' WS-CURRENT-DATE                               
      *    DISPLAY 'YEST-DTE   -' WS-YESTERDAY                                  
                                                                                
           IF (WS-DTE-CREATE = WS-CURRENT-DATE) OR                              
              (WS-DTE-CREATE = WS-YESTERDAY)                                    
              ADD 1 TO CNT-PRT-CASES                                            
           ELSE                                                                 
              ADD 1 TO WS-BYPASS                                                
              GO TO 3500-EXIT                                                   
           END-IF.                                                              
      *------------------------------------------------------------*            
      *    AT START OF PROGRAM (WS2-CNT-LINE-FS = 0)               *            
      *       -  PRINT HEADING OF FILE SENT & SALES ORDER          *            
      *       -  SET VARIABLES FOR CONTROLLING PAGE-THROW          *            
      *------------------------------------------------------------*            
           IF WS2-CNT-LINES-FS = 0                                              
              MOVE ZEROES                    TO WS2-NUM-SA                      
                                                WS2-NUM-LIE                     
                                                WS2-NUM-LIED                    
                                                WS2-NUM-MIE                     
                                                WS2-NUM-DOA                     
                                                WS2-NUM-SU                      
                                                WS2-NUM-DOA-ST                  
              MOVE ZEROES                      TO    WS2-CNT-PAGE-FS            
              MOVE    NUM-LIST                 TO    L770-HD4-LIST-NO           
              PERFORM 6000-HEADING             THRU  6000-EXIT                  
              MOVE NUM-LIST                TO WS-PREV-NUM-LIST                  
              MOVE NUM-BSNS-REF            TO WS-PREV-NUM-BSNS-REF              
           ELSE                                                                 
      *------------------------------------------------------------*            
      *    CHANGE OF LIST NO. (PRINT HEADINGS FOR FILE SENT)       *            
      *------------------------------------------------------------*            
             IF (NUM-LIST       NOT  =  WS-PREV-NUM-LIST) AND                   
                WS-EOF = 'Y'                                                    
                MOVE SPACE    TO WS-VAR                                         
                IF WS-EOF = 'Y'                                                 
                 PERFORM 6200-LIST-FILE-SENT    THRU 6200-EXIT                  
                 PERFORM 6300-PRINT-ACKNOW-LINE THRU 6300-EXIT                  
                END-IF                                                          
      *          GO TO 3500-EXIT                                                
             ELSE                                                               
                                                                                
              IF (NUM-LIST       NOT  =  WS-PREV-NUM-LIST) OR                   
                 WS-EOF = 'Y'                                                   
                PERFORM 6200-LIST-FILE-SENT     THRU 6200-EXIT                  
      *         MOVE SPACES   TO WS-VAR                                         
                MOVE ZEROES                   TO    WS2-CNT-PAGE-FS             
                MOVE NUM-LIST                 TO    WS-PREV-NUM-LIST            
                                                    L770-HD4-LIST-NO            
                MOVE ZEROES   TO WS2-SERIAL-NO-FS                               
                IF WS-EOF NOT = 'Y'                                             
                 PERFORM 6100-PRINT-ACKNOW-HDG THRU  6100-EXIT                  
                 MOVE NUM-BSNS-REF            TO WS-PREV-NUM-BSNS-REF           
                ELSE                                                            
                 PERFORM 6300-PRINT-ACKNOW-LINE THRU 6300-EXIT                  
                 GO TO 3500-EXIT                                                
                END-IF                                                          
                MOVE ZEROES                    TO WS2-NUM-SA                    
                                                  WS2-NUM-LIE                   
                                                  WS2-NUM-LIED                  
                                                  WS2-NUM-MIE                   
                                                  WS2-NUM-DOA                   
                                                  WS2-NUM-SU                    
                                                  WS2-NUM-DOA-ST                
      *         MOVE SPACES TO WS-VAR                                           
              END-IF                                                            
             END-IF                                                             
           END-IF.                                                              
                                                                                
           IF NUM-BSNS-REF NOT = WS-PREV-NUM-BSNS-REF                           
             PERFORM 6200-LIST-FILE-SENT     THRU 6200-EXIT                     
             MOVE NUM-BSNS-REF            TO WS-PREV-NUM-BSNS-REF               
             MOVE SPACES                  TO WS-VAR                             
      *      MOVE ZEROES                      TO WS1-NUM-SA                     
      *                                          WS1-NUM-LIE                    
      *                                          WS1-NUM-LIED                   
      *                                          WS1-NUM-MIE                    
      *                                          WS1-NUM-DOA                    
      *                                          WS1-NUM-SU                     
      *                                          WS1-NUM-DOA-ST                 
                                                                                
             PERFORM 3600-SAVE-DETAILS      THRU  3600-EXIT                     
           ELSE                                                                 
             PERFORM 3600-SAVE-DETAILS      THRU  3600-EXIT                     
           END-IF.                                                              
                                                                                
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       3600-SAVE-DETAILS.                                                       
      *-----------------*                                                       
             MOVE    NUM-LIST                 TO    L770-HD4-LIST-NO            
             MOVE    NUM-LEVEL                TO    L770-FS-LEVEL-NO            
                                                                                
      *      MOVE    NUM-UNIT                 TO    L770-FS-MAIN-UNIT           
             MOVE   4                         TO    WS-CHR-LEN.                 
             MOVE SPACES                      TO    WS-RIGHT-JUST-TXT           
                                                    WS-LEFT-JUST-TXT.           
             MOVE    NUM-UNIT                 TO    WS-LEFT-JUST-TXT.           
             PERFORM 4200-LEFT-JUSTIFY-TXT-4 THRU   4200-EXIT                   
             MOVE WS-RIGHT-JUST-TXT           TO    L770-FS-MAIN-UNIT.          
                                                                                
      *      MOVE    NUM-BLDNG                TO    L770-FS-BLK-NO              
             MOVE SPACES                      TO WS-BLK WS-BLK1.                
             MOVE 5                           TO WS-SUB1.                       
             MOVE    NUM-BLDNG                TO    WS-BLK.                     
             PERFORM 4100-LEFT-JUSTIFY-TXT   THRU   4100-EXIT.                  
             MOVE WS-BLK1                     TO    L770-FS-BLK-NO.             
                                                                                
             MOVE    NME-STREET               TO    L770-FS-ST-NAME             
             MOVE    NUM-BSNS-REF             TO    L770-FS-REGN-NO             
             MOVE    NUM-SCHEME               TO    L770-FS-SCHEME              
             MOVE    NUM-ACNT(1:4)            TO    L770-FS-ACC                 
             MOVE    NUM-ACNT(5:1)            TO    L770-FS-CHK                 
             MOVE    NUM-TNCY                 TO    L770-FS-ACC-TYPE            
             STRING NUM-SCHEME NUM-ACNT NUM-TNCY DELIMITED BY SIZE INTO         
                    WS-SCH-ACCT.                                                
                                                                                
             IF NUM-DOC-TYPE = 'SA   '                                          
                ADD    1                  TO  WS2-NUM-SA-TOTAL                  
                ADD    1                  TO  WS2-NUM-SA                        
                MOVE  '1'                 TO  WS1-NUM-SA                        
             END-IF                                                             
                                                                                
             IF NUM-DOC-TYPE = 'LIEO '                                          
                ADD    1                  TO  WS2-NUM-LIE-TOTAL                 
                ADD    1                  TO  WS2-NUM-LIE                       
                MOVE  '1'                 TO  WS1-NUM-LIE                       
             END-IF                                                             
                                                                                
                                                                                
             IF NUM-DOC-TYPE = 'LIEDU'                                          
                ADD    1                  TO  WS2-NUM-LIED-TOTAL                
                ADD    1                  TO  WS2-NUM-LIED                      
                MOVE  '1'                 TO  WS1-NUM-LIED                      
             END-IF                                                             
                                                                                
             IF NUM-DOC-TYPE = 'DOADU'                                          
                ADD    1                  TO  WS2-NUM-DOA-TOTAL                 
                ADD    1                  TO  WS2-NUM-DOA                       
                MOVE '1'                  TO  WS1-NUM-DOA                       
             END-IF                                                             
                                                                                
             IF NUM-DOC-TYPE = 'SUA  '                                          
                ADD    1                  TO  WS2-NUM-SU-TOTAL                  
                ADD    1                  TO  WS2-NUM-SU                        
                MOVE  '1'                 TO  WS1-NUM-SU                        
             END-IF                                                             
                                                                                
             IF NUM-DOC-TYPE = 'MIE  '                                          
                ADD    1                  TO  WS2-NUM-MIE-TOTAL                 
                ADD    1                  TO  WS2-NUM-MIE                       
                MOVE  '1'                 TO  WS1-NUM-MIE                       
             END-IF                                                             
                                                                                
             IF NUM-DOC-TYPE = 'DOATO'                                          
                ADD    1                  TO  WS2-NUM-DOA-ST-TOTAL              
                ADD    1                  TO  WS2-NUM-DOA-ST                    
                MOVE  '1'                 TO  WS1-NUM-DOA-ST                    
             END-IF.                                                            
                                                                                
            MOVE WS1-NUM-SA           TO  L770-FS-SA.                           
            MOVE WS1-NUM-LIE          TO  L770-FS-LIE.                          
            MOVE WS1-NUM-LIED         TO  L770-FS-LIED.                         
            MOVE WS1-NUM-MIE          TO  L770-FS-MIE.                          
            MOVE WS1-NUM-DOA          TO  L770-FS-DOA.                          
            MOVE WS1-NUM-SU           TO  L770-FS-SU.                           
            MOVE WS1-NUM-DOA-ST       TO  L770-FS-DOA-ST.                       
            PERFORM 3700-STAMP-FEE-BALANCE   THRU 3700-EXIT.                    
            PERFORM 3900-ISSUE-DATE          THRU 3900-EXIT.                    
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------*                                                  
       3700-STAMP-FEE-BALANCE.                                                  
      *----------------------*                                                  
           MOVE    WS-SCH-ACCT          TO   K006-HDB-REF-SF.                   
           PERFORM 3800-READ-AB05K006    THRU 3800-EXIT.                        
                                                                                
           IF AB05K006-FND                                                      
              IF K006-BALANCES NOT NUMERIC                                      
                 MOVE ZEROES            TO   K006-BALANCES                      
              END-IF                                                            
              MOVE K006-BALANCES        TO   L770-STAMP-FEE-BAL                 
           ELSE                                                                 
              MOVE  ZERO                TO   L770-STAMP-FEE-BAL                 
           END-IF.                                                              
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       3800-READ-AB05K006.                                                      
      *----------------------*                                                  
           READ AB05K006.                                                       
                                                                                
           EVALUATE AB05K006-STATUS                                             
           WHEN 00                                                              
              MOVE  'Y'               TO   WS-AB05K006-FND-TAG                  
           WHEN 23                                                              
              MOVE  SPACE             TO   WS-AB05K006-FND-TAG                  
              DISPLAY 'RECORD NOT FOUND IN AB05K006, SCH : '                    
                                           WS-SCH-ACCT                          
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING AB05K006, STATUS ' AB05K006-STATUS         
                      ', SCH : '  WS-SCH-ACCT                                   
              MOVE AB05K006-STATUS    TO   RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES                                          
           END-EVALUATE.                                                        
                                                                                
       3800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       3900-ISSUE-DATE.                                                         
      *----------------------*                                                  
           MOVE    WS-SCH-ACCT          TO   K110-KEY-FLD.                      
           PERFORM 4000-READ-BM06K110    THRU 4000-EXIT.                        
                                                                                
           IF K110-DTE-KEY-ISSUED NOT NUMERIC                                   
              MOVE ZEROES               TO   K110-DTE-KEY-ISSUED                
           END-IF.                                                              
                                                                                
           STRING K110-DTE-KEY-ISSUED(7:2) '/'                                  
                  K110-DTE-KEY-ISSUED(5:2) '/'                                  
                  K110-DTE-KEY-ISSUED(1:4)                                      
                  DELIMITED BY SIZE INTO L770-KEY-ISSUE-DATE                    
           END-STRING.                                                          
                                                                                
       3900-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       4000-READ-BM06K110.                                                      
      *----------------------*                                                  
           READ BM06K110.                                                       
                                                                                
           EVALUATE BM06K110-STATUS                                             
           WHEN 00                                                              
              CONTINUE                                                          
           WHEN 23                                                              
              MOVE ZEROES             TO   K110-DTE-KEY-ISSUED                  
              DISPLAY 'RECORD NOT FOUND IN BM06K110, SCH : '                    
                                           WS-SCH-ACCT                          
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BM06K110, STATUS ' BM06K110-STATUS         
                      ', SCH : '  WS-SCH-ACCT                                   
              MOVE BM06K110-STATUS    TO   RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES                                          
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       4100-LEFT-JUSTIFY-TXT.                                                   
      *------------------------*                                                
                                                                                
           PERFORM VARYING WS-SUB FROM 5 BY -1 UNTIL WS-SUB < 1                 
              IF WS-BLK(WS-SUB:1) NOT = SPACES AND LOW-VALUES                   
                 IF WS-BLK(WS-SUB:1) IS NOT NUMERIC                             
                    MOVE 'Y'                TO WS-CHAR-FLAG                     
                 ELSE                                                           
                    MOVE 'N'                TO WS-CHAR-FLAG                     
                 END-IF                                                         
                 IF WS-CHAR-FLAG = 'Y' AND WS-SUB1 = 5                          
                    MOVE WS-BLK(WS-SUB:1)   TO WS-BLK1(WS-SUB1:1)               
                 END-IF                                                         
                 IF WS-CHAR-FLAG = 'N'                                          
                    COMPUTE WS-SUB1 = WS-SUB1 - 1                               
                    MOVE WS-BLK(WS-SUB:1)   TO WS-BLK1(WS-SUB1:1)               
                 END-IF                                                         
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       4200-LEFT-JUSTIFY-TXT-4.                                                 
      *------------------------*                                                
                                                                                
           MOVE ZEROES   TO WS-CHR-CNT, WS-START-POSN.                          
           INSPECT WS-LEFT-JUST-TXT TALLYING WS-CHR-CNT                         
                  FOR CHARACTERS BEFORE INITIAL SPACE.                          
           COMPUTE WS-START-POSN = (WS-CHR-LEN  -  WS-CHR-CNT) + 1              
           MOVE WS-LEFT-JUST-TXT(1:WS-CHR-CNT)  TO                              
                WS-RIGHT-JUST-TXT.                                              
                                                                                
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       6000-HEADING.                                                            
      *-----------------*                                                       
      ******************************************************************        
      *    PRINT HEADINGS FOR LIST OF FILES SENT                       *        
      ******************************************************************        
           ADD    1                 TO   WS2-CNT-PAGE-FS.                       
           MOVE   1                 TO   WS2-CNT-LINES-FS.                      
                                                                                
           MOVE   WS5-HD2B-DESC-FS  TO   L770-HD2B-DESC.                        
           MOVE   WS2-CNT-PAGE-FS   TO   L770-HD2B-PAGE-NO.                     
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO  WS1-CURRENT-DATE.                   
                                                                                
           MOVE   WS1-CURRENT-DD    TO   L770-HD2B-DATE-DD.                     
           MOVE   WS1-CURRENT-MM    TO   L770-HD2B-DATE-MM.                     
           MOVE   WS1-CURRENT-YY    TO   L770-HD2B-DATE-YY.                     
           MOVE   WS1-CURRENT-CC    TO   L770-HD2B-DATE-CC.                     
                                                                                
                                                                                
           WRITE L770A-PRINT-REC FROM L770-HDG-1     AFTER PAGE.                
           WRITE L770A-PRINT-REC FROM L770-HDG-2A    AFTER ADVANCING 1.         
           WRITE L770A-PRINT-REC FROM L770-HDG-2B    AFTER ADVANCING 1.         
           WRITE L770A-PRINT-REC FROM L770-HDG-4     AFTER ADVANCING 1.         
           WRITE L770A-PRINT-REC FROM L770-FS-HDG-5  AFTER ADVANCING 2.         
           WRITE L770A-PRINT-REC FROM L770-FS-HDG-5A AFTER ADVANCING 1.         
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *    AT CHANGE OF SALES/BRANCH OFFICE                            *        
      *       -   PRINT ACKNOWLEDGEMENT FOR PREVIOUS SALES TYPE        *        
      *       -   PRINT HEADINGS FOR CURRENT SALES TYPE                *        
      ******************************************************************        
      *----------------------*                                                  
       6100-PRINT-ACKNOW-HDG.                                                   
      *----------------------*                                                  
           PERFORM 6300-PRINT-ACKNOW-LINE  THRU  6300-EXIT.                     
           PERFORM 6000-HEADING             THRU  6000-EXIT.                    
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       6200-LIST-FILE-SENT.                                                     
      *----------------------*                                                  
           ADD   1                 TO    WS2-SERIAL-NO-FS                       
                                         WS2-CNT-LINES-FS.                      
                                                                                
           MOVE  WS2-SERIAL-NO-FS  TO    L770-FS-SERIAL-NO.                     
                                                                                
           WRITE L770A-PRINT-REC   FROM  L770-FS-DETAIL-1 AFTER 1.              
                                                                                
           IF WS2-CNT-LINES-FS = WS5-MAX-LINE-FS                                
              PERFORM 6100-PRINT-ACKNOW-HDG  THRU 6100-EXIT                     
              MOVE    1                     TO    WS2-CNT-LINES-FS.             
                                                                                
      *    MOVE    ZEROS             TO   WS2-NUM-DOA   WS2-NUM-SU              
      *                                   WS2-NUM-LIE   WS2-NUM-MIE             
      *                                   WS2-NUM-SA    WS2-NUM-DOA-ST.         
                                                                                
                                                                                
       6200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       6300-PRINT-ACKNOW-LINE.                                                  
      *----------------------*                                                  
           MOVE    SPACE             TO    WS4-PRTED-ACK-IND                    
      *                                    L770-SO-DETAIL-1.                    
      *    INITIALIZE                      L770-SO-DETAIL-1.                    
                                                                                
           MOVE WS2-NUM-SA           TO L770-FS-NUM-SA                          
           MOVE WS2-NUM-LIE          TO L770-FS-NUM-LIE                         
           MOVE WS2-NUM-LIED         TO L770-FS-NUM-LIED                        
           MOVE WS2-NUM-MIE          TO L770-FS-NUM-MIE                         
           MOVE WS2-NUM-DOA          TO L770-FS-NUM-DOA                         
           MOVE WS2-NUM-SU           TO L770-FS-NUM-SU                          
           MOVE WS2-NUM-DOA-ST       TO L770-FS-NUM-DOA-ST                      
                                                                                
           WRITE   L770A-PRINT-REC   FROM  L770-SO-DETAIL-1  AFTER 2.           
                                                                                
           COMPUTE WS2-TOTAL    = WS2-NUM-SA   +                                
                                 WS2-NUM-LIE  +                                 
                                 WS2-NUM-LIED +                                 
                                 WS2-NUM-MIE  +                                 
                                 WS2-NUM-DOA  +                                 
                                 WS2-NUM-SU   +                                 
                                 WS2-NUM-DOA-ST.                                
                                                                                
           MOVE WS2-TOTAL      TO L770-FS-TOT-SA.                               
                                                                                
      *    COMPUTE WS2-NUM-DOA = WS2-NUM-SU   +  WS2-NUM-DOA    +               
      *                          WS2-NUM-LIE  +  WS2-NUM-MIE    +               
      *                          WS2-NUM-LIE  +                                 
      *                          WS2-NUM-SA   +  WS2-NUM-DOA-ST                 
      *                                                                         
      *    MOVE    WS2-NUM-DOA       TO    L770-SO-SET1                         
      *                                    L770-FS-TOT-SA.                      
           WRITE   L770A-PRINT-REC   FROM  L770-SO-DETAIL-1A AFTER 1.           
           WRITE   L770A-PRINT-REC   FROM  L770-SO-DETAIL-2 AFTER 1.            
      *    WRITE   L770A-PRINT-REC   FROM  L770-SO-DETAIL-3 AFTER 2.            
           WRITE   L770A-PRINT-REC   FROM  L770-SO-DETAIL-4 AFTER 2.            
           WRITE   L770A-PRINT-REC   FROM  L770-SO-DETAIL-5 AFTER 5.            
           WRITE   L770A-PRINT-REC   FROM  L770-SO-DETAIL-6 AFTER 5.            
           MOVE    SPACES            TO    L770A-PRINT-REC.                     
           WRITE   L770A-PRINT-REC.                                             
                                                                                
      *    MOVE    ZEROS             TO   WS2-NUM-DOA   WS2-NUM-SU              
      *                                   WS2-NUM-LIE   WS2-NUM-MIE             
      *                                   WS2-NUM-SA    WS2-NUM-DOA-ST.         
                                                                                
       6300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13C770 *************'.                            
           DISPLAY 'NO OF RECS WRITTEN TO BP13L770  : ' CNT-PRT-CASES.          
           DISPLAY 'NO OF RECS BYPASS(DATE)         : ' WS-BYPASS.              
                                                                                
           CLOSE P13L770A                                                       
                 AB05K006.                                                      
                                                                                
           IF AB05K006-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, AB05K006-STATUS ' AB05K006-STATUS         
              MOVE AB05K006-STATUS TO RETURN-CODE.                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------------------------------------------------         
       9999-SQL-ERROR.                                                          
      *----------------------------------------------------------------         
           CALL 'DBATIAR'  USING SQLCA.                                         
           EXEC SQL ROLLBACK               END-EXEC.                            
           MOVE 99                         TO    WS-RETURN-CODE.                
           PERFORM 9000-CLOSE-FILES        THRU  9000-EXIT.                     
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
