      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13C306.                                                 
       AUTHOR.        TOH SHIOK KUAN.                                           
      *DATE-WRITTEN.  04/09/89.                                                 
      *DATE-COMPILED.                                                           
      *REMARKS.                                                                 
                                                                                
      ***************************************************************           
      *  SYSTEM OF COMMITMENT                                       *           
      *  ---------------------------------------------------------- *           
      *                                                             *           
      *  OBJECTIVE  :  TO MERGE DAILY AGREEMENT ORDER FILE INTO     *           
      *                ACCUMULATED AGREEMENT ORDER FILE             *           
      *                                                             *           
      *                                                             *           
      *  I/O FILE                                                   *           
      *  ********                                                   *           
      *      INPUT  :  BP13F300 -- DAILY AGREEMENT ORDER FILE (SEQ) *           
      *        I-O  :  BP13F330 -- ACCUMULATED AGREEMENT ORDER      *           
      *                            FILE (SEQ)                       *           
      *     OUTPUT  :  BP13OUT  -- TRANSACTION FILE (SEQ)           *           
      *                                                             *           
      *                                                             *           
      *  SUBPROGRAM :  NIL                                          *           
      *                                                             *           
      *                                                             *           
      *  FUNCTION   :  1. BATCH PROGRAMMING IN COBOL                *           
      *                2. COMBINE RECORDS IN F300 AND F330 INTO A   *           
      *                   SEQUENTIAL FILE                                       
      * SOCPH8B  LJL 23/07/93 TO CONVERT THE REGN NO FROM 7 BYTES   *           
      *                       TO 8 BYTES.                           *           
      * BP130418 LJL 18/08/98 TO CATER FOR Y2000.                   *           
      ***************************************************************           
                                                                                
      *                                                                         
      *                                                                         
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
       CONFIGURATION SECTION.                                                   
      *======================                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
      *=====================                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F300  ASSIGN TO BP13F300.                                 
           SELECT BP13F330  ASSIGN TO BP13F330.                                 
           SELECT BP13OUT   ASSIGN TO BP13OUT.                                  
                                                                                
      *                                                                         
      *                                                                         
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
                                                                                
       FILE SECTION.                                                            
      *=============                                                            
       FD  BP13F300                                                             
           BLOCK  CONTAINS 0   RECORDS                                          
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F300.                                                           
                                                                                
       FD  BP13F330                                                             
           BLOCK  CONTAINS 0   RECORDS                                          
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F330.                                                           
                                                                                
       FD  BP13OUT                                                              
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD.                                          
       01  BP13OUT-REC                      PIC X(2000).                        
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *========================                                                 
                                                                                
      *---------------------------------------------------------*               
      *  DEFINE SUMMARY REPORT                                  *               
      *---------------------------------------------------------*               
       01  WS-REPORT-HEADER.                                                    
           05  FILLER                       PIC X     VALUE SPACES.             
           05  FILLER                       PIC X(18) VALUE ALL '*'.            
           05  FILLER                       PIC X(20) VALUE                     
               '   SUMMARY REPORT   '.                                          
           05  FILLER                       PIC X(18) VALUE ALL '*'.            
      *                                                                         
       01  WS-READ-CASES.                                                       
           05  FILLER                       PIC X(54) VALUE                     
               ' NO. OF RECORDS READ FROM AGRORDER (BP13F300)       = '.        
           05  WS-EDIT-F300-READ            PIC ZZ9.                            
      *                                                                         
       01  WS-TRANSFER-CASES.                                                   
           05  FILLER                       PIC X(54) VALUE                     
               ' NO. OF RECORDS TRANSFERRED FROM ACCUMAO (BP13F330) = '.        
           05  WS-EDIT-F330-TRANSFER        PIC ZZ9.                            
      *                                                                         
       01  WS-BP13OUT-WRITE-CASES.                                              
           05  FILLER                       PIC X(54) VALUE                     
               ' NO. OF RECORDS WRITTEN INTO BP13OUT                = '.        
           05  WS-EDIT-BP13OUT-WRITE        PIC ZZ9.                            
      *                                                                         
       01  WS-REPORT-TRAILER.                                                   
           05  FILLER                       PIC X     VALUE SPACES.             
           05  FILLER                       PIC X(56) VALUE ALL '*'.            
                                                                                
                                                                                
      *                                                                         
      *---------------------------------------------------------*               
      *  LEVEL-88 VARIABLES                                     *               
      *---------------------------------------------------------*               
       01  WS-END-OF-F300             PIC X(3).                                 
           88  END-F300          VALUE 'YES'.                                   
      *                                                                         
       01  WS-END-OF-F330             PIC X(3).                                 
           88  END-F330          VALUE 'YES'.                                   
                                                                                
                                                                                
      *                                                                         
      *---------------------------------------------------------*               
      *  OTHER LOCAL VARIABLES                                  *               
      *---------------------------------------------------------*               
       01  WS-COUNTERS.                                                         
           05  WS-F300-READ-CNT             PIC 9(3)  VALUE ZEROS.              
           05  WS-F330-TRANSFER-CNT         PIC 9(3)  VALUE ZEROS.              
           05  WS-BP13OUT-WRITE-CNT         PIC 9(3)  VALUE ZEROS.              
      *                                                                         
       01  WS-SEQ-NUM                       PIC 9     VALUE ZERO.               
       01  WS-BLANK-LINE                    PIC X     VALUE SPACES.             
                                                                                
                                                                                
      *                                                                         
      *                                                                         
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ***************************************************************           
      *      THE PROGRAM STRUCTURE IS :                             *           
      *      --------------------------                             *           
      *      0000-CONTROL-ROUTINE                                   *           
      *          0100-OPEN-FILES                                    *           
      *          0200-READ-F300                                     *           
      *          0300-READ-F330                                     *           
      *          1000-COMPARE-RECORDS-LOOP                          *           
      *              1100-TRANSFER-F330-TO-BP13OUT                  *           
      *                  0300-READ-F330                             *           
      *              1200-WRITE-F300-TO-BP13OUT                     *           
      *                  0200-READ-F300                             *           
      *              1300-TRANSFER-F330-WRITE-F300                  *           
      *                  1310-TRANSFER-AND-GET-REC-SEQ              *           
      *                      0300-READ-F330                         *           
      *                  0200-READ-F300                             *           
      *          2000-CLOSE-FILES                                   *           
      *          3000-WRITE-SUMMARY-REPORT                          *           
      *                                                             *           
      *                                                             *           
      *      SHARED ROUTINES                                        *           
      *      ---------------                                        *           
      *      0200-READ-F300                                         *           
      *      0300-READ-F330                                         *           
      *                                                             *           
      ***************************************************************           
                                                                                
      *=========================================================*               
       0000-CONTROL-ROUTINE.                                                    
      *=========================================================*               
                                                                                
           PERFORM  0100-OPEN-FILES THRU  0100-EXIT.                            
           PERFORM  0200-READ-F300 THRU 0200-EXIT.                              
           PERFORM  0300-READ-F330 THRU  0300-EXIT.                             
           PERFORM  1000-COMPARE-RECORDS-LOOP THRU  1000-EXIT                   
              UNTIL END-F300 AND END-F330.                                      
           PERFORM  2000-CLOSE-FILES THRU  2000-EXIT.                           
           PERFORM  3000-WRITE-SUMMARY-REPORT THRU  3000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
           STOP RUN.                                                            
                                                                                
      *=========================================================*               
       0100-OPEN-FILES.                                                         
      *=========================================================*               
                                                                                
           OPEN INPUT  BP13F300                                                 
                       BP13F330                                                 
                OUTPUT BP13OUT.                                                 
           MOVE 'NO' TO WS-END-OF-F300.                                         
           MOVE 'NO' TO WS-END-OF-F330.                                         
       0100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=========================================================*               
       0200-READ-F300.                                                          
      *=========================================================*               
      *    -------------------------------                                      
      *    READ BP13F300 UNTIL END OF FILE                                      
      *    -------------------------------                                      
           READ BP13F300                                                        
             AT END                                                             
                MOVE 'YES' TO WS-END-OF-F300                                    
                MOVE HIGH-VALUE TO F300-CUR-REG                                 
                GO TO 0200-EXIT.                                                
           ADD 1 TO WS-F300-READ-CNT.                                           
       0200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=========================================================*               
       0300-READ-F330.                                                          
      *=========================================================*               
      *    -------------------------------                                      
      *    READ BP13F330 UNTIL END OF FILE                                      
      *    -------------------------------                                      
           READ BP13F330                                                        
             AT END                                                             
                MOVE 'YES' TO WS-END-OF-F330                                    
                MOVE HIGH-VALUE TO F330-CUR-REG                                 
                GO TO 0300-EXIT.                                                
           ADD 1 TO WS-F330-TRANSFER-CNT.                                       
       0300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=========================================================*               
      *    SAME RECORD, MOVE ALL F330-REC WITH THIS CUR-REG TO                  
      *    BP13OUT AND APPEND F300-REC TO THIS LIST                             
      *    TRANS > MAS, MOVE MAS TO BP13OUT                                     
      *    TRANS < MAS, WRITE TRANS TO BP13OUT                                  
      *=========================================================*               
       1000-COMPARE-RECORDS-LOOP.                                               
      *=========================================================*               
           IF F300-CUR-REG = F330-CUR-REG                                       
              PERFORM 1100-WRITE-F330 THRU 1100-EXIT                            
                UNTIL F300-CUR-REG NOT = F330-CUR-REG  OR END-F330              
              PERFORM 1200-WRITE-F300 THRU 1200-EXIT                            
              PERFORM  0200-READ-F300 THRU  0200-EXIT                           
           ELSE                                                                 
              IF F300-CUR-REG > F330-CUR-REG                                    
                 WRITE BP13OUT-REC FROM BP13F330-REC                            
                 ADD 1 TO WS-BP13OUT-WRITE-CNT                                  
                 PERFORM  0300-READ-F330 THRU  0300-EXIT                        
              ELSE                                                              
                 MOVE '1'          TO F300-SEQ-NO                               
                 MOVE F300-CUR-REG TO F300-CURR-REG                             
                 WRITE BP13OUT-REC FROM BP13F300-REC                            
                 ADD 1 TO WS-BP13OUT-WRITE-CNT                                  
                 PERFORM  0200-READ-F300 THRU  0200-EXIT.                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
      *    ---------------------                                                
      *    WRITE F300 TO BP13OUT                                                
      *    TRANSFER THE CURRENT MAS REC TO BP13OUT                              
      *    SET F330-KEY-FLD AND INCREMENT SEQ-NO BY 1                           
      *    ---------------------------------------                              
       1100-WRITE-F330.                                                         
           MOVE F330-SEQ-NO TO WS-SEQ-NUM.                                      
           WRITE BP13OUT-REC FROM BP13F330-REC.                                 
           ADD 1 TO WS-BP13OUT-WRITE-CNT.                                       
           PERFORM  0300-READ-F330 THRU  0300-EXIT.                             
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1200-WRITE-F300.                                                         
           ADD  1            TO WS-SEQ-NUM.                                     
           MOVE F300-CUR-REG TO F300-CURR-REG.                                  
           MOVE WS-SEQ-NUM   TO F300-SEQ-NO.                                    
           WRITE BP13OUT-REC FROM BP13F300-REC.                                 
           ADD 1 TO WS-BP13OUT-WRITE-CNT.                                       
           ADD  0            TO WS-SEQ-NUM.                                     
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=========================================================*               
       2000-CLOSE-FILES.                                                        
      *=========================================================*               
                                                                                
           CLOSE BP13F300                                                       
                 BP13F330                                                       
                 BP13OUT.                                                       
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=========================================================*               
       3000-WRITE-SUMMARY-REPORT.                                               
      *=========================================================*               
                                                                                
           MOVE WS-F300-READ-CNT     TO WS-EDIT-F300-READ.                      
           MOVE WS-F330-TRANSFER-CNT TO WS-EDIT-F330-TRANSFER.                  
           MOVE WS-BP13OUT-WRITE-CNT TO WS-EDIT-BP13OUT-WRITE.                  
                                                                                
           DISPLAY WS-BLANK-LINE.                                               
           DISPLAY WS-REPORT-HEADER.                                            
           DISPLAY WS-BLANK-LINE.                                               
           DISPLAY WS-READ-CASES.                                               
           DISPLAY WS-TRANSFER-CASES.                                           
           DISPLAY WS-BP13OUT-WRITE-CASES.                                      
           DISPLAY WS-BLANK-LINE.                                               
           DISPLAY WS-REPORT-TRAILER.                                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
