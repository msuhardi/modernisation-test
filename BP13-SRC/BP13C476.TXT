      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID. BP13C476.                                                    
       AUTHOR. RJB1.                                                            
      *DATE-WRITTEN. 15/04/2015.                                                
                                                                                
      ******************************************************************        
      *                    SYSTEM OF COMMITMENT (BP13)                 *        
      *================================================================*        
      *  OBJECTIVE:  1.  TO PASS A DAILY INTERFACING FILE FOR TEMPLOAN *        
      *                  CASES TO SMS                                  *        
      *================================================================*        
      *  FILES DESCRIPTION:-                                           *        
      *                 ACCESS       REC                               *        
      *  DD NAME  I/O/E  MODE        LEN   KEY           REMARKS       *        
      *  -------  ----- ------       ---   ---           -------       *        
      * BP13K480    I/O RANDOM      1000   K480-NUM-REGN VSAM          *        
      * BM06K110    I   RANDOM       500   K110-REGN-NO  VSAM          *        
      * BP13K410    I   RANDOM      2000   K410-NUM-REGN VSAM          *        
      * BB24F004    I   SEQ           50                 SEQ           *        
      * BP13CNTR    O   SEQ           50                 SEQ           *        
      * AB03F123    O   SEQ          250                 SEQ           *        
      * BP13L476    O   SEQ          132                 REPORT        *        
      *================================================================*        
      *  MODIFICATIONS :                                               *        
      *  CHGE REQ #  DATE          AUTHOR         REMARKS              *        
      *  ----------  ----          ------         -------              *        
      *  BP135476    15/04/2015    RJB1           NEW PROGRAM          *        
      *  BP136788    12/05/2017    RJB1           REMOVE CHECKING OF   *        
      *                                           ABOVE-65YRS          *        
      ******************************************************************        
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K480 ASSIGN TO BP13K480                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K480-KEY-FLD                               
                  FILE STATUS     IS BP13K480-STATUS.                           
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K110-KEY-FLD                               
                  FILE STATUS     IS BM06K110-STATUS.                           
                                                                                
           SELECT BP13K410 ASSIGN TO BP13K410                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K410-KEY-FLD                               
                  FILE STATUS     IS BP13K410-STATUS.                           
                                                                                
           SELECT BB24F004 ASSIGN TO BB24F004.                                  
                                                                                
           SELECT BP13CNTR ASSIGN TO BP13CNTR.                                  
                                                                                
           SELECT AB03F123 ASSIGN TO AB03F123.                                  
                                                                                
           SELECT BP13L476 ASSIGN TO BP13L476.                                  
                                                                                
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
       FD   BP13K480                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K480.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BP13K410                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K410.                                                           
                                                                                
       FD   BB24F004                                                            
            RECORD CONTAINS 50 CHARACTERS                                       
            RECORDING MODE  IS   F.                                             
       COPY BB24F004.                                                           
                                                                                
      * LAYOUT SIMILAR WITH BB24F004-REC                                        
       FD   BP13CNTR                                                            
            RECORD CONTAINS 50 CHARACTERS                                       
            RECORDING MODE  IS   F.                                             
       01   BP13CNTR-REC    PIC X(50).                                          
                                                                                
       FD   AB03F123                                                            
            BLOCK  CONTAINS 0   RECORDS                                         
            RECORD CONTAINS 250 CHARACTERS                                      
            LABEL  RECORD   IS  OMITTED                                         
            RECORDING MODE IS F.                                                
       COPY AB03F123.                                                           
                                                                                
       FD   BP13L476                                                            
            BLOCK  CONTAINS 0   RECORDS                                         
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL  RECORD   IS  OMITTED                                         
            DATA   RECORD   IS  BP13L476-REC                                    
            RECORDING MODE IS F.                                                
       01   BP13L476-REC         PIC X(132).                                    
                                                                                
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
       01  WS-DTE-CCYYMMDD.                                                     
           05  WS-DTE-CC                 PIC X(2).                              
           05  WS-DTE-CURRENT.                                                  
               10  WS-DTE-YY             PIC X(2).                              
               10  WS-DTE-MM             PIC X(2).                              
               10  WS-DTE-DD             PIC X(2).                              
                                                                                
       01  WS-FILE-FLAGS.                                                       
           05  WS-BB24-EOF               PIC X     VALUE 'N'.                   
           05  BP13K410-STATUS           PIC 9(2)  VALUE ZEROS.                 
           05  BM06K110-STATUS           PIC 9(2)  VALUE ZEROS.                 
           05  BP13K480-STATUS           PIC 9(2)  VALUE ZEROS.                 
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-K110-FND-SW            PIC X     VALUE SPACES.                
           05  WS-ERROR-SW               PIC X     VALUE SPACES.                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-L476-CNT-PAGE          PIC 9(5)  VALUE ZEROS.                 
           05  WS-L476-CNT-LINE          PIC 9(5)  VALUE 56.                    
           05  WS-L476-CNT-REC           PIC 9(8)  VALUE ZEROS.                 
           05  WS-BB24-CNT-READ          PIC 9(8)  VALUE ZEROS.                 
           05  WS-K480-CNT-READ          PIC 9(8)  VALUE ZEROS.                 
           05  WS-K480-CNT-REWR          PIC 9(8)  VALUE ZEROS.                 
           05  WS-K480-CNT-NOTFND        PIC 9(8)  VALUE ZEROS.                 
           05  WS-K410-CNT-READ          PIC 9(8)  VALUE ZEROS.                 
           05  WS-K410-CNT-NOTFND        PIC 9(8)  VALUE ZEROS.                 
           05  WS-K110-CNT-READ          PIC 9(8)  VALUE ZEROS.                 
           05  WS-K110-CNT-NOTFND        PIC 9(8)  VALUE ZEROS.                 
           05  WS-F123-CNT-WRITE         PIC 9(8)  VALUE ZEROS.                 
           05  WS-F473-CNT-WRITE         PIC 9(8)  VALUE ZEROS.                 
                                                                                
       01  WS-TOT-AMT-CONTRA             PIC S9(7)V99 VALUE ZEROS.              
                                                                                
      *---------------------------------------------------------------          
      *   LAY-OUT FOR BP13L476 REPORT                                           
      *---------------------------------------------------------------          
       01  WS-PAGE-HEADING1.                                                    
           05  FILLER                    PIC X(14) VALUE 'BP13L476'.            
           05  FILLER                    PIC X(42) VALUE 'HDB3'.                
           05  FILLER                    PIC X(44)                              
               VALUE 'SYSTEM OF COMMITMENT'.                                    
           05  FILLER                    PIC X(7)  VALUE 'DATE : '.             
           05  L476-DTE-HDR-DD           PIC X(2).                              
           05  FILLER                    PIC X     VALUE '/'.                   
           05  L476-DTE-HDR-MM           PIC X(2).                              
           05  FILLER                    PIC X     VALUE '/'.                   
           05  L476-DTE-HDR-CC           PIC X(2).                              
           05  L476-DTE-HDR-YY           PIC X(5).                              
           05  FILLER                    PIC X(7)  VALUE 'PAGE : '.             
           05  L476-PAGE-NUM             PIC ZZZ9.                              
           05  FILLER                    PIC X(2)  VALUE SPACES.                
                                                                                
       01  WS-PAGE-HEADING2.                                                    
           05  FILLER                    PIC X(41) VALUE SPACES.                
           05  FILLER                    PIC X(52)                              
               VALUE 'REPORT ON INTERFACING FILE FOR TEMPLOAN CASES TO S        
      -              'MS'.                                                      
           05  FILLER                    PIC X(37) VALUE SPACES.                
                                                                                
       01  WS-PAGE-HEADING3.                                                    
           05  FILLER                    PIC X(50) VALUE SPACES.                
           05  FILLER                    PIC X(83)                              
               VALUE '(SALES UNIT - AGREEMENT TEAM)'.                           
                                                                                
       01  WS-PAGE-COL-HEADING1.                                                
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(05)                              
               VALUE 'S/NO '.                                                   
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(08)                              
               VALUE 'REGN NO '.                                                
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(10)                              
               VALUE '  ABOVE   '.                                              
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(14)                              
               VALUE ' SCHEME ACCT  '.                                          
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(10)                              
               VALUE ' SO DATE  '.                                              
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(10)                              
               VALUE 'EFFECTIVE '.                                              
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(14)                              
               VALUE 'DATE OF RESALE'.                                          
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(12)                              
               VALUE '    LOAN    '.                                            
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(12)                              
               VALUE 'TOTAL CONTRA'.                                            
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(15)                              
               VALUE 'CONTRA FACILITY'.                                         
           05  FILLER                    PIC X(02) VALUE SPACES.                
                                                                                
       01  WS-PAGE-COL-HEADING2.                                                
           05  FILLER                    PIC X(19) VALUE SPACES.                
           05  FILLER                    PIC X(10)                              
               VALUE '65 YRS OLD'.                                              
           05  FILLER                    PIC X(30) VALUE SPACES.                
           05  FILLER                    PIC X(10)                              
               VALUE 'SALE DATE '.                                              
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(14)                              
               VALUE '  COMPLETION  '.                                          
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(12)                              
               VALUE ' AMOUNT ($) '.                                            
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(12)                              
               VALUE ' AMOUNT ($) '.                                            
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  FILLER                    PIC X(15)                              
               VALUE '      CODE     '.                                         
           05  FILLER                    PIC X(02) VALUE SPACES.                
                                                                                
       01  WS-HORIZONTAL-LINE.                                                  
           05  FILLER                    PIC X(132)                             
               VALUE ALL '-'.                                                   
                                                                                
       01  WS-PAGE-DATA-LINE.                                                   
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  L476-NUM-SERIAL           PIC ZZZZZ.                             
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  L476-NUM-REGN             PIC X(08) VALUE SPACES.                
           05  FILLER                    PIC X(06) VALUE SPACES.                
           05  L476-NUM-ABOVE-65         PIC X(01) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE SPACES.                
           05  L476-NUM-SCH-ACC          PIC X(14) VALUE SPACES.                
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  L476-DTE-SO               PIC X(10) VALUE SPACES.                
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  L476-DTE-EFF-SALE         PIC X(10) VALUE SPACES.                
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  L476-DTE-RES-COMP         PIC X(10) VALUE SPACES.                
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  L476-AMT-LOAN             PIC Z,ZZZ,ZZ9.99.                      
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  L476-AMT-CONTRA           PIC Z,ZZZ,ZZ9.99.                      
           05  FILLER                    PIC X(08) VALUE SPACES.                
           05  L476-NUM-CONTRA-TYPE      PIC X(2)  VALUE SPACES.                
           05  FILLER                    PIC X(09) VALUE SPACES.                
                                                                                
       01  WS-END-OF-REPORT.                                                    
           05  FILLER                    PIC X(55) VALUE SPACES.                
           05  FILLER                    PIC X(21)                              
               VALUE '*** END OF REPORT ***'.                                   
           05  FILLER                    PIC X(56) VALUE SPACES.                
                                                                                
       01  WS-EMPTY-REPORT.                                                     
           05  FILLER                    PIC X(56) VALUE SPACES.                
           05  FILLER                    PIC X(20)                              
               VALUE '*** EMPTY REPORT ***'.                                    
           05  FILLER                    PIC X(56) VALUE SPACES.                
                                                                                
                                                                                
      *------------------------*                                                
       PROCEDURE DIVISION.                                                      
      *------------------------*                                                
      ******************************************************************        
       0000-CONTROL.                                                            
      ******************************************************************        
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-BB24F004 THRU 2000-EXIT                            
                   UNTIL (WS-BB24-EOF = 'Y').                                   
           PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-FILES.                                                         
      ******************************************************************        
           MOVE   FUNCTION CURRENT-DATE TO WS-DTE-CCYYMMDD.                     
                                                                                
           MOVE WS-DTE-DD TO L476-DTE-HDR-DD.                                   
           MOVE WS-DTE-MM TO L476-DTE-HDR-MM.                                   
           MOVE WS-DTE-CC TO L476-DTE-HDR-CC.                                   
           MOVE WS-DTE-YY TO L476-DTE-HDR-YY.                                   
                                                                                
           OPEN INPUT  BP13K410                                                 
                       BM06K110                                                 
                       BB24F004                                                 
                I-O    BP13K480                                                 
                OUTPUT AB03F123                                                 
                       BP13L476                                                 
                       BP13CNTR.                                                
                                                                                
           IF BP13K410-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP13K410-STATUS ' BP13K410-STATUS         
              MOVE BP13K410-STATUS TO RETURN-CODE                               
              MOVE 'Y' TO WS-ERROR-SW                                           
              PERFORM 9000-CLOSE-FILES.                                         
                                                                                
           IF BM06K110-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BM06K110-STATUS ' BM06K110-STATUS         
              MOVE BM06K110-STATUS TO RETURN-CODE                               
              MOVE 'Y' TO WS-ERROR-SW                                           
              PERFORM 9000-CLOSE-FILES.                                         
                                                                                
           IF BP13K480-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP13K480-STATUS ' BP13K480-STATUS         
              MOVE BP13K480-STATUS TO RETURN-CODE                               
              MOVE 'Y' TO WS-ERROR-SW                                           
              PERFORM 9000-CLOSE-FILES.                                         
                                                                                
           PERFORM 7000-PRINT-HEADERS THRU 7000-EXIT.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       2000-READ-BB24F004.                                                      
      ******************************************************************        
           READ BB24F004 AT END                                                 
                         MOVE 'Y' TO WS-BB24-EOF                                
                         GO TO 2000-EXIT                                        
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-BB24-CNT-READ.                                           
                                                                                
           PERFORM 3000-RETRIEVE-REGNO THRU 3000-EXIT.                          
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       3000-RETRIEVE-REGNO.                                                     
      ******************************************************************        
           MOVE  SPACES               TO BP13K480-REC.                          
                                                                                
           PERFORM 3100-READ-BM06K110 THRU 3100-EXIT.                           
                                                                                
           IF WS-K110-FND-SW = 'Y'                                              
              MOVE  K110-REGN-NO      TO K480-NUM-REGN                          
           ELSE                                                                 
              GO                      TO 3000-EXIT                              
           END-IF                                                               
                                                                                
           READ BP13K480.                                                       
                                                                                
           EVALUATE BP13K480-STATUS                                             
              WHEN 00                                                           
                   ADD 1              TO WS-K480-CNT-READ                       
                   PERFORM 3200-READ-BP13K410 THRU 3200-EXIT                    
              WHEN 23                                                           
                   ADD 1              TO WS-K480-CNT-NOTFND                     
                   DISPLAY 'BP13K480 - RECORD NOT FOUND '                       
                   DISPLAY 'KEY : ' K480-NUM-REGN                               
                           ' SCHEME : ' F004-NUM-HDB-REF-CONTRA                 
                   PERFORM 5100-WRITE-KIV-FILE  THRU 5100-EXIT                  
              WHEN OTHER                                                        
                   DISPLAY 'ERROR READING BP13K480      '                       
                   DISPLAY 'KEY : ' K480-NUM-REGN                               
                           ' SCHEME : ' F004-NUM-HDB-REF-CONTRA                 
                   DISPLAY 'STATUS : '     BP13K480-STATUS                      
                   INITIALIZE BP13K480-REC                                      
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       3100-READ-BM06K110.                                                      
      ******************************************************************        
           MOVE  SPACES                TO WS-K110-FND-SW.                       
           MOVE  F004-NUM-HDB-REF-CONTRA TO K110-SCH-ACC-NO.                    
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE BM06K110-STATUS                                             
              WHEN 00                                                           
                   ADD 1           TO WS-K110-CNT-READ                          
                   MOVE 'Y'        TO WS-K110-FND-SW                            
              WHEN 23                                                           
                   ADD 1           TO WS-K110-CNT-NOTFND                        
                   DISPLAY 'BM06K110 - RECORD NOT FOUND '                       
                   DISPLAY 'KEY : ' K110-SCH-ACC-NO                             
                   PERFORM 5100-WRITE-KIV-FILE  THRU 5100-EXIT                  
              WHEN OTHER                                                        
                   DISPLAY 'ERROR READING BM06K110      '                       
                   DISPLAY 'KEY : ' K110-SCH-ACC-NO                             
                   DISPLAY 'STATUS : '  BM06K110-STATUS                         
                   INITIALIZE K110-REC                                          
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       3200-READ-BP13K410.                                                      
      ******************************************************************        
           MOVE  SPACES               TO BP13K410-REC.                          
                                                                                
           MOVE  K480-NUM-REGN        TO K410-NUM-REGN.                         
                                                                                
           READ BP13K410.                                                       
                                                                                
           EVALUATE BP13K410-STATUS                                             
              WHEN 00                                                           
                   ADD 1 TO WS-K410-CNT-READ                                    
                   PERFORM 4000-UPDATE-BP13K480 THRU 4000-EXIT                  
              WHEN 23                                                           
                   ADD 1 TO WS-K410-CNT-NOTFND                                  
                   DISPLAY 'BP13K410 - RECORD NOT FOUND '                       
                   DISPLAY 'KEY     : ' K410-NUM-REGN                           
                           'SCHEME  : ' F004-NUM-HDB-REF-CONTRA                 
                   PERFORM 5100-WRITE-KIV-FILE  THRU 5100-EXIT                  
              WHEN OTHER                                                        
                   DISPLAY 'ERROR READING BP13K410      '                       
                   DISPLAY 'KEY     : ' K410-NUM-REGN                           
                           'SCHEME  : ' F004-NUM-HDB-REF-CONTRA                 
                   DISPLAY 'STATUS  : ' BP13K410-STATUS                         
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       4000-UPDATE-BP13K480.                                                    
      ******************************************************************        
           MOVE 'Y'                  TO  K480-NUM-SMS-PASS.                     
           MOVE F004-DTE-RESL-CMPLN  TO  K480-DTE-RESALE-COMPLTN.               
                                                                                
           REWRITE BP13K480-REC.                                                
                                                                                
           EVALUATE BP13K480-STATUS                                             
              WHEN 00                                                           
                   ADD 1        TO WS-K480-CNT-REWR                             
                   MOVE ZEROES  TO WS-TOT-AMT-CONTRA                            
                                                                                
                   IF K480-AMT-TEMP-LOAN-CASH NOT NUMERIC                       
                      MOVE ZEROES TO K480-AMT-TEMP-LOAN-CASH                    
                   END-IF                                                       
                   IF K480-AMT-TEMP-LOAN-CPF1 NOT NUMERIC                       
                      MOVE ZEROES TO K480-AMT-TEMP-LOAN-CPF1                    
                   END-IF                                                       
                   IF K480-AMT-TEMP-LOAN-CPF2 NOT NUMERIC                       
                      MOVE ZEROES TO K480-AMT-TEMP-LOAN-CPF2                    
                   END-IF                                                       
                   IF K480-AMT-TEMP-LOAN-CPF3 NOT NUMERIC                       
                      MOVE ZEROES TO K480-AMT-TEMP-LOAN-CPF3                    
                   END-IF                                                       
                   IF K480-AMT-TEMP-LOAN-CPF4 NOT NUMERIC                       
                      MOVE ZEROES TO K480-AMT-TEMP-LOAN-CPF4                    
                   END-IF                                                       
                                                                                
                   COMPUTE WS-TOT-AMT-CONTRA = K480-AMT-TEMP-LOAN-CASH +        
                                               K480-AMT-TEMP-LOAN-CPF1 +        
                                               K480-AMT-TEMP-LOAN-CPF2 +        
                                               K480-AMT-TEMP-LOAN-CPF3 +        
                                               K480-AMT-TEMP-LOAN-CPF4          
                   PERFORM 5000-WRITE-SMS-FILE     THRU 5000-EXIT               
                   PERFORM 6000-PRINT-DETAILS      THRU 6000-EXIT               
              WHEN OTHER                                                        
                   DISPLAY 'ERROR IN REWRITING BP13K480      '                  
                   DISPLAY 'KEY    : ' K480-KEY-FLD                             
                           'SCHEME : ' F004-NUM-HDB-REF-CONTRA                  
                   DISPLAY 'STATUS : ' BP13K480-STATUS                          
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       5000-WRITE-SMS-FILE.                                                     
      ******************************************************************        
           MOVE SPACES                        TO AB03F123-REC.                  
           INITIALIZE                            AB03F123-REC.                  
                                                                                
           MOVE K410-NUM-SCH-ACC              TO F123-NUM-HDB-REF.              
           MOVE K410-CDE-BO                   TO F123-CDE-BO.                   
                                                                                
           MOVE K410-NRIC1                    TO F123-NUM-CPFREF1.              
           MOVE K410-NRIC2                    TO F123-NUM-CPFREF2.              
           MOVE K410-NRIC3                    TO F123-NUM-CPFREF3.              
           MOVE K410-NRIC4                    TO F123-NUM-CPFREF4.              
                                                                                
           IF K410-AMT-LOAN NOT NUMERIC                                         
              MOVE ZEROES                     TO K410-AMT-LOAN                  
           END-IF.                                                              
                                                                                
           MOVE K410-AMT-LOAN                 TO F123-AMT-LOAN-GRANTD.          
                                                                                
           MOVE K480-RESL-SCH-ACC             TO F123-NUM-HDBREF-RESALE.        
           MOVE K480-DTE-RESALE-COMPLTN       TO F123-DTE-RESL-COMPL.           
                                                                                
           MOVE K480-AMT-TEMP-LOAN-CASH       TO F123-AMT-CASH.                 
           MOVE K480-AMT-TEMP-LOAN-CPF1       TO F123-AMT-CPF1.                 
           MOVE K480-AMT-TEMP-LOAN-CPF2       TO F123-AMT-CPF2.                 
           MOVE K480-AMT-TEMP-LOAN-CPF3       TO F123-AMT-CPF3.                 
           MOVE K480-AMT-TEMP-LOAN-CPF4       TO F123-AMT-CPF4.                 
                                                                                
           MOVE WS-TOT-AMT-CONTRA             TO F123-AMT-TOTL.                 
                                                                                
           MOVE WS-DTE-CCYYMMDD               TO F123-DTE-UPDATE.               
                                                                                
           MOVE 'TL'                          TO F123-CDE-TRANS.                
           MOVE K410-DTE-KEY-ISSUE            TO F123-DTE-SALE-EFF.             
                                                                                
           MOVE SPACES                        TO F123-DTE-PROPOSD               
                                                 F123-CDE-ERROR.                
                                                                                
           INSPECT AB03F123-REC REPLACING ALL LOW-VALUES BY SPACES.             
                                                                                
           WRITE AB03F123-REC.                                                  
                                                                                
           ADD 1                              TO WS-F123-CNT-WRITE.             
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       5100-WRITE-KIV-FILE.                                                     
      ******************************************************************        
           MOVE SPACES                        TO BP13CNTR-REC.                  
           INITIALIZE                            BP13CNTR-REC.                  
                                                                                
           WRITE BP13CNTR-REC                 FROM BB24F004-REC.                
                                                                                
           ADD 1                              TO WS-F473-CNT-WRITE.             
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       6000-PRINT-DETAILS.                                                      
      ******************************************************************        
      *----------------------------------------------------------------*        
      *  READ THE SEQUENTIAL FILE THEN PRINT THE RECORD.               *        
      *----------------------------------------------------------------*        
           IF WS-L476-CNT-LINE > 60                                             
              PERFORM 7000-PRINT-HEADERS THRU 7000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE SPACES                        TO WS-PAGE-DATA-LINE.             
           ADD  1                             TO WS-L476-CNT-REC.               
           MOVE WS-L476-CNT-REC               TO L476-NUM-SERIAL.               
           MOVE K410-NUM-REGN                 TO L476-NUM-REGN.                 
           IF K410-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES                      
              STRING K410-NUM-SCH-ACC(1:4) '-'                                  
                     K410-NUM-SCH-ACC(5:4) '-'                                  
                     K410-NUM-SCH-ACC(9:1) '-'                                  
                     K410-NUM-SCH-ACC(10:2)                                     
                     DELIMITED BY SIZE                                          
                     INTO L476-NUM-SCH-ACC                                      
           ELSE                                                                 
              MOVE '      -       '           TO L476-NUM-SCH-ACC               
           END-IF.                                                              
                                                                                
           IF K410-DTE-SO-EXECUTION NOT = SPACES AND LOW-VALUES                 
              STRING K410-DTE-SO-EXECUTION(7:2) '/'                             
                     K410-DTE-SO-EXECUTION(5:2) '/'                             
                     K410-DTE-SO-EXECUTION(1:4)                                 
                     DELIMITED BY SIZE                                          
                     INTO L476-DTE-SO                                           
           ELSE                                                                 
              MOVE '    -     '               TO L476-DTE-SO                    
           END-IF.                                                              
                                                                                
           IF K410-DTE-SO-EFFECTIVE NOT = SPACES AND LOW-VALUES                 
              STRING K410-DTE-SO-EFFECTIVE(7:2) '/'                             
                     K410-DTE-SO-EFFECTIVE(5:2) '/'                             
                     K410-DTE-SO-EFFECTIVE(1:4)                                 
                     DELIMITED BY SIZE                                          
                     INTO L476-DTE-EFF-SALE                                     
           ELSE                                                                 
              MOVE '    -     '               TO L476-DTE-EFF-SALE              
           END-IF.                                                              
                                                                                
           IF K480-DTE-RESALE-COMPLTN NOT = SPACES AND LOW-VALUES               
              STRING K480-DTE-RESALE-COMPLTN(7:2) '/'                           
                     K480-DTE-RESALE-COMPLTN(5:2) '/'                           
                     K480-DTE-RESALE-COMPLTN(1:4)                               
                     DELIMITED BY SIZE                                          
                     INTO L476-DTE-RES-COMP                                     
           ELSE                                                                 
              MOVE '    -     '               TO L476-DTE-RES-COMP              
           END-IF.                                                              
                                                                                
           MOVE WS-TOT-AMT-CONTRA             TO L476-AMT-CONTRA.               
           MOVE K410-NUM-CONTRA-TYPE          TO L476-NUM-CONTRA-TYPE.          
                                                                                
           IF K480-NUM-ABOVE-65YRS = SPACES OR LOW-VALUES                       
              MOVE 'N'                        TO L476-NUM-ABOVE-65              
           ELSE                                                                 
              MOVE K480-NUM-ABOVE-65YRS       TO L476-NUM-ABOVE-65              
           END-IF.                                                              
                                                                                
           IF K410-AMT-LOAN NOT NUMERIC                                         
              MOVE ZEROES                     TO K410-AMT-LOAN                  
           END-IF.                                                              
                                                                                
           MOVE K410-AMT-LOAN                 TO L476-AMT-LOAN.                 
                                                                                
           WRITE BP13L476-REC FROM WS-PAGE-DATA-LINE                            
                 AFTER ADVANCING 1 LINE.                                        
                                                                                
           ADD 1                   TO WS-L476-CNT-LINE.                         
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       7000-PRINT-HEADERS.                                                      
      ******************************************************************        
      *----------------------------------------------------------------*        
      *  PRINTS THE PAGE HEADER.                                       *        
      *----------------------------------------------------------------*        
           ADD  1                TO WS-L476-CNT-PAGE.                           
           MOVE WS-L476-CNT-PAGE TO L476-PAGE-NUM.                              
                                                                                
           WRITE BP13L476-REC    FROM WS-PAGE-HEADING1                          
                 AFTER ADVANCING PAGE.                                          
           WRITE BP13L476-REC    FROM WS-PAGE-HEADING2                          
                 AFTER ADVANCING 1 LINE.                                        
           WRITE BP13L476-REC    FROM WS-PAGE-HEADING3                          
                 AFTER ADVANCING 1 LINE.                                        
           WRITE BP13L476-REC    FROM WS-PAGE-COL-HEADING1                      
                 AFTER ADVANCING 2 LINES.                                       
           WRITE BP13L476-REC    FROM WS-PAGE-COL-HEADING2                      
                 AFTER ADVANCING 1 LINE.                                        
           WRITE BP13L476-REC    FROM WS-HORIZONTAL-LINE                        
                 AFTER ADVANCING 1 LINE.                                        
                                                                                
           MOVE 7                TO WS-L476-CNT-LINE.                           
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       9000-CLOSE-FILES.                                                        
      ******************************************************************        
           DISPLAY '                                  '.                        
           DISPLAY ' *****  BP13C476  *****           '.                        
           DISPLAY '                                  '.                        
           DISPLAY ' NO. OF REC READ FROM BB24  = ' WS-BB24-CNT-READ.           
           DISPLAY ' NO. OF REC READ FROM K480  = ' WS-K480-CNT-READ.           
           DISPLAY ' NO. OF REC REWR TO   K480  = ' WS-K480-CNT-REWR.           
           DISPLAY ' NO. OF REC NOTFND IN K480  = ' WS-K480-CNT-NOTFND.         
           DISPLAY ' NO. OF REC READ FROM K410  = ' WS-K410-CNT-READ.           
           DISPLAY ' NO. OF REC NOTFND IN K410  = ' WS-K410-CNT-NOTFND.         
           DISPLAY ' NO. OF REC NOTFND IN K110  = ' WS-K110-CNT-NOTFND.         
           DISPLAY ' NO. OF REC WRITTEN IN F123 = ' WS-F123-CNT-WRITE.          
           DISPLAY ' NO. OF REC WRITTEN IN F473 = ' WS-F473-CNT-WRITE.          
           DISPLAY ' NO. OF REC PRINTED         = ' WS-L476-CNT-REC.            
                                                                                
           CLOSE BP13K410                                                       
                 BM06K110                                                       
                 BP13K480                                                       
                 BB24F004                                                       
                 AB03F123                                                       
                 BP13CNTR.                                                      
                                                                                
           IF BP13K410-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, BP13K410-STATUS ' BP13K410-STATUS         
              MOVE 'Y' TO WS-ERROR-SW                                           
              MOVE BP13K410-STATUS TO RETURN-CODE.                              
                                                                                
           IF BM06K110-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, BM06K110-STATUS ' BM06K110-STATUS         
              MOVE 'Y' TO WS-ERROR-SW                                           
              MOVE BM06K110-STATUS TO RETURN-CODE.                              
                                                                                
           IF BP13K480-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, BP13K480-STATUS ' BP13K480-STATUS         
              MOVE 'Y' TO WS-ERROR-SW                                           
              MOVE BP13K480-STATUS TO RETURN-CODE.                              
                                                                                
           PERFORM 9100-PRINT-END-EMPTY THRU 9100-EXIT.                         
                                                                                
           CLOSE BP13L476.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       9100-PRINT-END-EMPTY.                                                    
      ******************************************************************        
           IF WS-L476-CNT-REC = ZEROES                                          
              IF WS-ERROR-SW = 'Y'                                              
                 PERFORM 7000-PRINT-HEADERS THRU 7000-EXIT                      
              END-IF                                                            
              MOVE  SPACES TO BP13L476-REC                                      
              WRITE BP13L476-REC AFTER ADVANCING 1 LINE                         
              MOVE  SPACES TO BP13L476-REC                                      
              IF WS-ERROR-SW = 'Y'                                              
                 MOVE 'FILE ERROR !!! ' TO   WS-EMPTY-REPORT                    
                 WRITE BP13L476-REC FROM WS-EMPTY-REPORT                        
                       AFTER ADVANCING 1 LINE                                   
              ELSE                                                              
                 WRITE BP13L476-REC FROM WS-EMPTY-REPORT                        
                       AFTER ADVANCING 1 LINE                                   
              END-IF                                                            
           ELSE                                                                 
              MOVE  SPACES TO BP13L476-REC                                      
              WRITE BP13L476-REC AFTER ADVANCING 1 LINE                         
              MOVE  SPACES TO BP13L476-REC                                      
              WRITE BP13L476-REC FROM WS-END-OF-REPORT                          
                    AFTER ADVANCING 1 LINE                                      
           END-IF.                                                              
                                                                                
       9100-EXIT.                                                               
           EXIT.                                                                
      *----------------------------------------------------------------*        
      * >>>>>>>>>>>>>>>>>>>>>>> END OF BP13C476 <<<<<<<<<<<<<<<<<<<<<< *        
      *----------------------------------------------------------------*        
