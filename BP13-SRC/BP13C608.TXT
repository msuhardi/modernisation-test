       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C608.                                                 
       AUTHOR.        ANNALYN C BANTA.                                          
       DATE-WRITTEN.  03/10/94.                                                 
      *========================================================*                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : TO EXTRACT DATES FROM BP13K454 BASED  *                
      *                  FROM BP13F465 FILE.                   *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY   DATE     DETAILS                          *                
      *========================================================*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F465 ASSIGN TO BP13F465.                                  
                                                                                
           SELECT BP13F454 ASSIGN TO BP13F454.                                  
                                                                                
           SELECT BP13K454 ASSIGN TO BP13K454                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K454-KEY-FLD                               
                  FILE STATUS     IS K454-STATUS.                               
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F465                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 50 CHARACTERS                                        
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F465.                                                           
                                                                                
       FD  BP13F454                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 100 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F454.                                                           
                                                                                
       FD  BP13K454                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K454.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05 K454-STATUS                 PIC 9(2)  VALUE ZEROS.                
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F465-READ                PIC 9(5)  VALUE ZEROS.                
           05 WS-F454-WRITE               PIC 9(5)  VALUE ZEROS.                
                                                                                
       01  WS-EOF-F465                    PIC 9     VALUE 0.                    
           88 EOF-F465                    VALUE 1.                              
                                                                                
       01  WS-EOF-K454                    PIC 9     VALUE 0.                    
           88 EOF-K454                    VALUE 1.                              
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       000-MAIN-ROUTINE.                                                        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F465      THRU 200-EXIT.                            
           PERFORM 300-PROCESS-RTN    THRU 300-EXIT                             
             UNTIL EOF-F465.                                                    
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       100-INITIALIZATION.                                                      
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F465                                                 
                       BP13K454                                                 
                OUTPUT BP13F454.                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       200-READ-F465.                                                           
      ******************************************************************        
                                                                                
           READ BP13F465 AT END                                                 
                MOVE 1   TO WS-EOF-F465                                         
                GO TO 200-EXIT.                                                 
                                                                                
           ADD  1                       TO WS-F465-READ.                        
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       300-PROCESS-RTN.                                                         
      ******************************************************************        
                                                                                
           PERFORM 310-STARTBR-K454   THRU 310-EXIT.                            
           PERFORM 320-READNXT-K454   THRU 320-EXIT.                            
                                                                                
           IF F465-DTE-APPT = K454-DTE-APPT                                     
              MOVE ZEROS                TO WS-EOF-K454                          
              PERFORM 330-WRITE-F454  THRU 330-EXIT                             
                UNTIL EOF-K454.                                                 
                                                                                
           PERFORM 200-READ-F465      THRU 200-EXIT.                            
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       310-STARTBR-K454.                                                        
      ******************************************************************        
                                                                                
           MOVE  SPACES                  TO K454-KEY-FLD.                       
           MOVE  F465-DTE-APPT           TO K454-DTE-APPT.                      
                                                                                
           START BP13K454 KEY NOT < K454-KEY-FLD.                               
                                                                                
           IF    K454-STATUS = 00                                               
                 NEXT SENTENCE                                                  
           ELSE                                                                 
           IF    K454-STATUS = 23                                               
                 DISPLAY 'RECORD NOTFND IN BP13K454, ' K454-KEY-FLD             
                 PERFORM 200-READ-F465   THRU 200-EXIT                          
                 GO TO 300-EXIT                                                 
           ELSE                                                                 
                 MOVE K454-STATUS          TO RETURN-CODE                       
                 DISPLAY 'ERROR READING BP13K454, ' K454-KEY-FLD                
                 GO TO 999-CLOSE-FILES.                                         
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       320-READNXT-K454.                                                        
      ******************************************************************        
                                                                                
           READ BP13K454 NEXT                                                   
             AT END                                                             
                MOVE 1 TO WS-EOF-K454.                                          
                                                                                
       320-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       330-WRITE-F454.                                                          
      ******************************************************************        
                                                                                
           IF F465-DTE-APPT NOT = K454-DTE-APPT                                 
              MOVE 1                     TO WS-EOF-K454                         
              GO TO 330-EXIT.                                                   
                                                                                
           WRITE BP13F454-REC          FROM BP13K454-REC.                       
           ADD   1                       TO WS-F454-WRITE.                      
                                                                                
           PERFORM 320-READNXT-K454    THRU 320-EXIT.                           
                                                                                
       330-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       999-CLOSE-FILES.                                                         
      ******************************************************************        
           CLOSE BP13F465                                                       
                 BP13F454                                                       
                 BP13K454.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13C608 ================*'.              
           DISPLAY '    NO. OF F465 RECORDS READ    = ' WS-F465-READ.           
           DISPLAY '    NO. OF F454 RECORDS WRITTEN = ' WS-F454-WRITE.          
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
                                                                                
