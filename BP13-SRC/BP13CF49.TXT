       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13CF49.                                                  
      *AUTHOR.       ZDD1.                                                      
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *  OBJECTIVE:                                                *            
      *  TO UPDATE NUM-MGPS AND MGPS-QUEUE                         *            
      *                                                            *            
      *  I    :  BP13F740                                          *            
      *  I/O  :  BP13K800                                          *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS WS-K800-STATUS.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F740   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 500 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13F740.                                                           
                                                                                
                                                                                
       FD  BP13K800   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 2000 CHARACTERS                           
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13K800.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-SWITCHES.                                                         
           05  WS-K800-STATUS            PIC 99    VALUE ZEROS.                 
           05  WS-F740-EOF               PIC X     VALUE 'N'.                   
           05  WS-FOUND                  PIC X     VALUE 'N'.                   
           05  WS-F740-READ              PIC 9(6)  VALUE ZEROS.                 
           05  WS-K800-READ              PIC 9(6)  VALUE ZEROS.                 
           05  WS-K800-REWRITE           PIC 9(6)  VALUE ZEROS.                 
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-LOGIC.                                                          
            PERFORM 100-OPEN-FILES      THRU 100-EXIT.                          
            PERFORM 200-READ-BP13F740   THRU 200-EXIT.                          
            PERFORM 400-PROCESS-RECORD  THRU 400-EXIT                           
                UNTIL WS-F740-EOF = 'Y'                                         
            PERFORM 999-CLOSE-FILES     THRU 999-EXIT.                          
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F740                                                 
                I-O    BP13K800.                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, K800-STATUS ' WS-K800-STATUS              
              MOVE WS-K800-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-BP13F740.                                                       
      *-------------------------------------------------------------            
           READ BP13F740 AT END                                                 
           MOVE 'Y'                      TO WS-F740-EOF                         
           GO TO 200-EXIT.                                                      
                                                                                
           ADD 1                         TO WS-F740-READ.                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-PROCESS-RECORD.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES      TO BP13K800-MASTER                                  
           MOVE F740-NUM-REGN    TO K800-NUM-REGN                               
           PERFORM 500-READ-BP13K800   THRU 500-EXIT                            
           IF WS-FOUND = 'Y'                                                    
              MOVE F740-NUM-MGPS         TO K800-NUM-MGPS                       
              MOVE F740-NUM-NT-FT-QUEUE  TO K800-NUM-QUEUE-MGPS                 
              MOVE F740-NUM-JB-REGN      TO K800-NUM-JT-BALLOT-REGN             
                                                                                
              IF F740-CDE-REQUEST-STATUS = 'S' OR 'R' OR 'W' AND                
                 K800-NUM-STATUS = 'C'                                          
                 MOVE 'N'                TO K800-NUM-STATUS                     
                 MOVE SPACES             TO K800-NUM-ALLOC-TAG                  
                                            K800-NUM-CANCEL-REASON              
                                            K800-DTE-CANCEL                     
              END-IF                                                            
                                                                                
              PERFORM 600-UPDATE-BP13K800 THRU 600-EXIT                         
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-BP13F740 THRU 200-EXIT.                             
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-READ-BP13K800.                                                       
      *-------------------------------------------------------------            
             READ BP13K800                                                      
                                                                                
             IF WS-K800-STATUS = 00                                             
                MOVE 'Y'  TO WS-FOUND                                           
                ADD 1 TO  WS-K800-READ                                          
             ELSE                                                               
                IF WS-K800-STATUS = 23                                          
                   MOVE 'N'  TO WS-FOUND                                        
                   DISPLAY 'RECORD NOT FND IN BP13K800 ' WS-K800-STATUS         
                           ' REGN - '  K800-NUM-REGN                            
                ELSE                                                            
                   DISPLAY 'ERROR READ BP13K800,STATUS ' WS-K800-STATUS         
                   MOVE WS-K800-STATUS     TO RETURN-CODE                       
                   GO TO 999-CLOSE-FILES.                                       
                                                                                
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       600-UPDATE-BP13K800.                                                     
      *-------------------------------------------------------------            
                                                                                
            REWRITE BP13K800-MASTER                                             
                                                                                
            IF WS-K800-STATUS = 00                                              
               ADD 1                 TO WS-K800-REWRITE                         
            ELSE                                                                
               DISPLAY 'ERROR REWRITING BP13K800 STATUS '                       
                                               WS-K800-STATUS                   
                           ' REGN - '  K800-NUM-REGN                            
               MOVE WS-K800-STATUS      TO RETURN-CODE                          
               PERFORM 999-CLOSE-FILES  THRU 999-EXIT                           
            END-IF.                                                             
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
       999-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
           DISPLAY '* ------------ BP13CF49 ------------- * '.                  
           DISPLAY 'NO OF REC READ    BP13F740 = ' WS-F740-READ.                
           DISPLAY 'NO OF REC READ    BP13K800 = ' WS-K800-READ.                
           DISPLAY 'NO OF REC UPDATED BP13K800 = ' WS-K800-REWRITE.             
                                                                                
           CLOSE BP13F740                                                       
                 BP13K800.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K800, STATUS ' WS-K800-STATUS          
              MOVE WS-K800-STATUS        TO RETURN-CODE.                        
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
