       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C501.                                                 
      *AUTHOR.        MTD.                                                      
      *DATE-WRITTEN.  31JAN2003.                                                
      ***************************************************************           
      * SYSTEM OF COMMITMENT (SOC)                                  *           
      ***************************************************************           
      * OBJECTIVE : TO UPDATE SOC OOCUPIER FILE BASED ON OCCUPIER   *           
      *             FILE FROM BB22.                                 *           
      * ------------------------------------------------------------*           
      * MATCH APPLICANT'S NRIC BETWEEN SOC OCCUPIER FILE AND BB22   *           
      * OCCUPIER FILE, IF MATCH AND SPOUSE NRIC EQUAL, UPDATE SPOUSE*           
      * NAME IN SOC OCCUPIER FILE BASED FROM BB22.                  *           
      * ------------------------------------------------------------*           
      * CHG REF  DATE     BY   DESCRIPTION                          *           
      * -------- -------- --- --------------------------------------*           
      * BP132289 31/01/03 MTD NEW PGM                               *           
      ***************************************************************           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F825  ASSIGN       TO BP13F825.                           
           SELECT BB22F070  ASSIGN       TO BB22F070.                           
                                                                                
           SELECT BP13K820  ASSIGN       TO BP13K820                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K820-KEY-FLD                        
                            FILE STATUS  IS WS-K820-STATUS.                     
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F825                RECORD CONTAINS 200 CHARACTERS              
                                    LABEL RECORDS ARE STANDARD                  
                                    RECORDING MODE IS F.                        
       COPY BP13F825.                                                           
                                                                                
       FD   BB22F070                RECORD CONTAINS 200 CHARACTERS              
                                    LABEL RECORDS ARE STANDARD                  
                                    RECORDING MODE IS F.                        
       COPY BB22F070.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FLAG-N-STATUS.                                                    
           05  WS-FOUND                     PIC X     VALUE 'N'.                
           05  WS-K820-STATUS               PIC 99    VALUE ZEROS.              
           05  WS-F070-EOF-FLAG             PIC X(01)  VALUE 'N'.               
               88  WS-F070-EOF              VALUE 'Y'.                          
           05  WS-F825-EOF-FLAG             PIC X(01)  VALUE 'N'.               
               88  WS-F825-EOF              VALUE 'Y'.                          
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F825-READ                 PIC 9(05)  VALUE ZEROS.             
           05  WS-F825-NMR                  PIC 9(05)  VALUE ZEROS.             
           05  WS-F070-READ                 PIC 9(05)  VALUE ZEROS.             
           05  WS-F070-NMR                  PIC 9(05)  VALUE ZEROS.             
           05  WS-K820-UPD                  PIC 9(05)  VALUE ZEROS.             
           05  WS-K820-READ                 PIC 9(05)  VALUE ZEROS.             
           05  WS-K820-NOT-FND              PIC 9(05)  VALUE ZEROS.             
           05  WS-F070-MR-F825              PIC 9(05)  VALUE ZEROS.             
           05  WS-SKIPPED                   PIC 9(05)  VALUE ZEROS.             
           05  WS-SPOUSE-IC-BLANK           PIC 9(05)  VALUE ZEROS.             
           05  WS-SPOUSE-IC-NMR             PIC 9(05)  VALUE ZEROS.             
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-YY                    PIC X(04)  VALUE SPACES.            
           05  WS-SYS-MM                    PIC X(02)  VALUE SPACES.            
           05  WS-SYS-DD                    PIC X(02)  VALUE SPACES.            
                                                                                
       PROCEDURE DIVISION.                                                      
      ***************************                                               
       0000-MAIN.                                                               
      ***************************                                               
                                                                                
           PERFORM 1000-OPEN-FILES    THRU  1000-EXIT.                          
           PERFORM 2000-READ-BB22F070 THRU  2000-EXIT.                          
           PERFORM 2010-READ-BP13F825 THRU  2010-EXIT.                          
           PERFORM 3000-PROCESS       THRU  3000-EXIT                           
             UNTIL F070-NUM-CSTMR-ID = HIGH-VALUES                              
             AND   F825-NUM-NRIC = HIGH-VALUES.                                 
           PERFORM 9000-CLOSE-FILES   THRU  9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       1000-OPEN-FILES.                                                         
      ***************************                                               
           OPEN INPUT BB22F070                                                  
                      BP13F825                                                  
                  I-O BP13K820.                                                 
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K820 STATUS IS ' WS-K820-STATUS            
              MOVE     WS-K820-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-FILES.                                         
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)     TO  WS-SYSTEM-DATE.              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       2000-READ-BB22F070.                                                      
      ***************************                                               
           READ BB22F070                                                        
             AT END                                                             
                MOVE 'Y'                    TO  WS-F070-EOF-FLAG                
                MOVE HIGH-VALUES            TO  F070-NUM-CSTMR-ID               
                GO TO 2000-EXIT                                                 
            NOT AT END                                                          
                ADD   1                     TO  WS-F070-READ                    
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       2010-READ-BP13F825.                                                      
      ***************************                                               
           READ BP13F825                                                        
             AT END                                                             
                MOVE 'Y'                    TO  WS-F825-EOF-FLAG                
                MOVE HIGH-VALUES            TO  F825-NUM-NRIC                   
                GO TO 2010-EXIT                                                 
            NOT AT END                                                          
                ADD  1                      TO  WS-F825-READ                    
           END-READ.                                                            
                                                                                
       2010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       3000-PROCESS.                                                            
      ***************************                                               
           IF F070-NUM-CSTMR-ID = F825-NUM-NRIC                                 
              ADD 1 TO WS-F070-MR-F825                                          
              IF F070-NUM-NRIC-SPOUSE = SPACES OR LOW-VALUES                    
                 ADD 1 TO WS-SPOUSE-IC-BLANK                                    
              ELSE                                                              
                 IF F070-NUM-NRIC-SPOUSE = F825-NUM-NRIC-SPOUSE                 
                    PERFORM 7000-UPDATE-K820    THRU  7000-EXIT                 
                 ELSE                                                           
                    DISPLAY 'SPOUSE-IC IN F070 ' F070-NUM-NRIC-SPOUSE           
                     ' NOT EQUAL TO F825 ' F825-NUM-NRIC                        
                    ADD 1    TO WS-SPOUSE-IC-NMR                                
                 END-IF                                                         
              END-IF                                                            
              PERFORM 2010-READ-BP13F825     THRU  2010-EXIT                    
              PERFORM 2000-READ-BB22F070     THRU  2000-EXIT                    
           ELSE                                                                 
              IF F070-NUM-CSTMR-ID < F825-NUM-NRIC                              
                 ADD 1 TO WS-F070-NMR                                           
                 PERFORM 2000-READ-BB22F070 THRU 2000-EXIT                      
              ELSE                                                              
                 ADD 1 TO WS-F825-NMR                                           
                 PERFORM 2010-READ-BP13F825 THRU 2010-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       7000-UPDATE-K820.                                                        
      ***************************                                               
           MOVE SPACES               TO    BP13K820-REC.                        
           MOVE F825-NUM-REGN        TO    K820-NUM-REGN.                       
           MOVE F825-NUM-NRIC        TO    K820-NUM-NRIC.                       
                                                                                
           READ BP13K820.                                                       
           EVALUATE WS-K820-STATUS                                              
           WHEN 00                                                              
                ADD 1 TO WS-K820-READ                                           
                IF K820-NUM-NRIC-SPOUSE = F825-NUM-NRIC-SPOUSE                  
                  MOVE F070-NUM-NME-SPOUSE     TO K820-NME-SPOUSE               
                  PERFORM 7100-REWRITE-K820  THRU 7100-EXIT                     
                ELSE                                                            
                  DISPLAY 'SPOUSE IC IN K820 ' K820-NUM-NRIC-SPOUSE             
                          ' NOT = F825 SPOUSE IC ' F825-NUM-NRIC                
                  ADD 1    TO WS-SKIPPED                                        
                END-IF                                                          
           WHEN 23                                                              
                ADD 1 TO WS-K820-NOT-FND                                        
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K820 - ' WS-K820-STATUS              
                MOVE     WS-K820-STATUS TO RETURN-CODE                          
                PERFORM 9000-CLOSE-FILES                                        
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       7100-REWRITE-K820.                                                       
      ***************************                                               
           REWRITE BP13K820-REC.                                                
           EVALUATE WS-K820-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
                ADD 1    TO WS-K820-UPD                                         
           WHEN OTHER                                                           
                DISPLAY 'ERROR REWRITING BP13K820 - ' WS-K820-STATUS            
                MOVE     WS-K820-STATUS TO RETURN-CODE                          
                PERFORM 9000-CLOSE-FILES                                        
           END-EVALUATE.                                                        
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       9000-CLOSE-FILES.                                                        
      ***************************                                               
           DISPLAY SPACES.                                                      
           DISPLAY 'RUN DATE : ' WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-YY.         
           DISPLAY '========== BP13C501 STATISTICS =============='      '.      
           DISPLAY 'NO OF RECORDS READ IN BP13F825 : ' WS-F825-READ.            
           DISPLAY 'NO OF RECORDS READ IN BB22F070 : ' WS-F070-READ.            
           DISPLAY SPACES                                                       
           DISPLAY 'NO OF F070 MR F825 BY IC       : ' WS-F070-MR-F825.         
           DISPLAY 'NO OF BLANK SPOUSE IC          : '                          
                    WS-SPOUSE-IC-BLANK.                                         
           DISPLAY 'NO OF SPOUSE IC NMR IN F825    : ' WS-SPOUSE-IC-NMR.        
           DISPLAY 'NO OF RECORDS READ IN BP13K820 : ' WS-K820-READ.            
           DISPLAY 'NO OF RECORDS UPDATED IN  K820 : ' WS-K820-UPD.             
           DISPLAY 'NO OF RECORDS NOT IN BP13K820  : ' WS-K820-NOT-FND.         
           DISPLAY SPACES                                                       
           DISPLAY 'NO OF F825 NOT FOUND IN F070   : ' WS-F825-NMR.             
           DISPLAY 'NO OF F070 NOT FOUND IN F825   : ' WS-F070-NMR.             
           DISPLAY 'NO OF SPOUSE IC NMR IN K820    : ' WS-SKIPPED.              
           DISPLAY SPACES.                                                      
           DISPLAY '============================================='.             
                                                                                
           CLOSE BP13F825                                                       
                 BB22F070                                                       
                 BP13K820.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
      ********************** END OF PROGRAM ***************************         
