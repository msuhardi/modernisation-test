       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CCAA.                                                 
       AUTHOR. ELAINE S ARGA.                                                   
       DATE-WRITTEN.  23/01/09.                                                 
                                                                                
      **********************************************************                
      *                                                        *                
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                    *                
      *                                                        *                
      *  SYSTEM ID   : BP13                                    *                
      *                                                        *                
      *  OBJECTIVE  : THIS PROGRAM IS TO READ BP13.K140.KIVTRAN*                
      *               SEQUENTIALLY AND OUTPUT VALID CASES TO*                   
      *               BP13K140 & BP13F125 & DELETE             *                
      *               VALID CASES FROM BP13K140-KIVTRAN FOR DBS*                
      *                                                        *                
      *    OBJECTIVE  :                                        *                
      *                                                        *                
      *     INPUT FILES:                                       *                
      *           1. BP13KD00 (SUB-TRANSFER FILE)              *                
      *           2. BP13KD40 (PROGRESS PAYMENT)               *                
      *           3. BP13KD03 (SUB-TRANSFER HISTORY FILE)      *                
      *                                                        *                
      *     INPUT-OUTPUT FILES:                                *                
      *           1. BP13K140 (KIV FILE)                       *                
      *                                                        *                
      *     OUTPUT FILE:                                       *                
      *           1. BP13F125 (CPF TRANSACTIONS FILE)          *                
      *           2. BP13F140 (KIV FILE)                       *                
      *                                                        *                
      *  CHG-NO  BY   DATE   DESCRIPTION                       *                
      * -------- ---- ------ -------------------               *                
      * BP133480 ESA1 230109 NEW PROGRAM(MODEL AFTER BP13CC15) *                
      * BP133545 JB8  230209 READ BP13KD03 IF KD00 NOT FND     *                
      * BP133545 JB8  020309 MOVE 'Y' TO F125-NUM-DBSS-TOP IF  *                
      *                      KD00 NOT FND BUT KD03 FND         *                
      * BP133588 JB8  290409 TO READ KD00 WHEN 'PSP' 'DCV' 'PP'*                
      *                      TO CATER FOR 'LS' AND 'IT'        *                
      **********************************************************                
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F140 ASSIGN TO BP13F140.                                  
                                                                                
           SELECT BP13K140 ASSIGN TO BP13K140                                   
                ACCESS MODE IS SEQUENTIAL                                       
                ORGANIZATION IS INDEXED                                         
                RECORD KEY IS K140-KEY-FLD                                      
                FILE STATUS IS K140-STATUS.                                     
                                                                                
           SELECT BP13KD00 ASSIGN TO BP13KD00                                   
                ACCESS MODE  IS RANDOM                                          
                ORGANIZATION IS INDEXED                                         
                RECORD KEY IS KD00-KEY-FLD                                      
                FILE STATUS IS KD00-STATUS.                                     
                                                                                
           SELECT BP13KD03 ASSIGN TO BP13KD03                                   
                ACCESS MODE  IS RANDOM                                          
                ORGANIZATION IS INDEXED                                         
                RECORD KEY IS KD03-KEY-FLD                                      
                FILE STATUS IS KD03-STATUS.                                     
                                                                                
           SELECT BP13KD40 ASSIGN TO BP13KD40                                   
                  ACCESS MODE IS DYNAMIC                                        
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY  IS KD40-KEY-FLD                                   
                  FILE STATUS IS KD40-STATUS.                                   
                                                                                
           SELECT BP13F125 ASSIGN TO BP13F125.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F140                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F140.                                                           
                                                                                
       FD  BP13F125                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       COPY BP13F125.                                                           
                                                                                
                                                                                
       FD  BP13K140                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13K140.                                                           
                                                                                
                                                                                
       FD  BP13KD00                                                             
           RECORD CONTAINS 1600 CHARACTERS.                                     
       COPY BP13KD00.                                                           
                                                                                
       FD  BP13KD03                                                             
           RECORD CONTAINS 1600 CHARACTERS.                                     
       COPY BP13KD03.                                                           
                                                                                
       FD  BP13KD40                                                             
           RECORD CONTAINS 1500 CHARACTERS.                                     
       COPY BP13KD40.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  K140-STATUS                    PIC 99      VALUE ZEROES.             
       01  KD00-STATUS                    PIC 99      VALUE ZEROES.             
       01  KD03-STATUS                    PIC 99      VALUE ZEROES.             
       01  KD40-STATUS                    PIC 99      VALUE ZEROES.             
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-KD40-EOF-FLAG           PIC X       VALUE 'N'.                
           05  WS-K140-EOF-FLAG           PIC X       VALUE 'N'.                
                                                                                
       01  WS-KD00-FND                    PIC X(1)    VALUE 'N'.                
       01  WS-KD03-FND                    PIC X(1)    VALUE 'N'.                
           88  KD03-FND                   VALUE 'Y'.                            
       01  WS-KD40-FND                    PIC X(1)    VALUE 'N'.                
       01  WS-CNT-KD00-READ               PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-KD00-NOTFD              PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-KD40-READ               PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-KD40-NOTFD              PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-K140-READ               PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-K140-DEL                PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-K140-UPD                PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-F140-WRITE              PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-F125-VALID              PIC 9(5)    VALUE ZEROES.             
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
                                                                                
      ******************************************************************        
      *        MAIN LINE PROCESS                                       *        
      ******************************************************************        
       MAIN-PROCESS.                                                            
           PERFORM 100-OPEN-FILE                THRU 100-EXIT.                  
           PERFORM 300-READ-K140                THRU 300-EXIT.                  
           PERFORM 400-PROCESS-K140-CODE-VALID  THRU 400-EXIT                   
               UNTIL WS-K140-EOF-FLAG = 'Y'                                     
           PERFORM 1000-CLOSE-ROUTINE           THRU 1000-EXIT.                 
                                                                                
      ***************************************************************           
      *    OPENING FILES SECTION                                    *           
      ***************************************************************           
                                                                                
       100-OPEN-FILE.                                                           
           OPEN INPUT  BP13KD00                                                 
                       BP13KD03                                                 
                       BP13KD40                                                 
                I-O    BP13K140                                                 
                OUTPUT BP13F125                                                 
                       BP13F140.                                                
                                                                                
           IF K140-STATUS NOT = 0  AND K140-STATUS NOT = 97                     
              DISPLAY ' OPENING ERROR, BP13K140 STATUS ' K140-STATUS            
              MOVE K140-STATUS           TO RETURN-CODE                         
              PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT.                        
                                                                                
           IF KD00-STATUS NOT = 0  AND KD00-STATUS NOT = 97                     
              DISPLAY ' OPENING ERROR, BP13KD00 STATUS ' KD00-STATUS            
              MOVE KD00-STATUS           TO RETURN-CODE                         
              PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT.                        
                                                                                
           IF KD03-STATUS NOT = 0  AND KD03-STATUS NOT = 97                     
              DISPLAY ' OPENING ERROR, BP13KD03 STATUS ' KD03-STATUS            
              MOVE KD03-STATUS           TO RETURN-CODE                         
              PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT.                        
                                                                                
           IF KD40-STATUS NOT = 0  AND KD40-STATUS NOT = 97                     
              DISPLAY ' OPENING ERROR, BP13KD40 STATUS ' KD40-STATUS            
              MOVE KD40-STATUS           TO RETURN-CODE                         
              PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT.                        
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-READ-K140.                                                           
      *-------------------------------------------------------------            
           READ BP13K140 AT END                                                 
                MOVE 'Y' TO WS-K140-EOF-FLAG                                    
                GO TO 300-EXIT.                                                 
                                                                                
           ADD 1 TO WS-CNT-K140-READ.                                           
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       400-PROCESS-K140-CODE-VALID.                                             
           IF K140-CDE-VALID = 'V'                                              
              MOVE 'N' TO WS-KD03-FND                                           
              PERFORM 410-READ-KD00       THRU 410-EXIT                         
              IF K140-CDE-PAYMENT-TYPE = 'PP ' OR 'DSP' OR 'DCV'                
                 PERFORM 420-READ-KD40    THRU 420-EXIT                         
                 IF WS-KD40-FND = 'Y'                                           
                    PERFORM 435-MOVE-F125-DATA  THRU 435-EXIT                   
                    PERFORM 500-WRITE-F140      THRU 500-EXIT                   
                 END-IF                                                         
              ELSE                                                              
              IF K140-CDE-PAYMENT-TYPE = 'INS' OR 'LS ' OR 'IT '                
                 IF WS-KD00-FND = 'Y'                                           
                    PERFORM 435-MOVE-F125-DATA  THRU 435-EXIT                   
                    PERFORM 500-WRITE-F140      THRU 500-EXIT                   
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
           PERFORM 300-READ-K140      THRU 300-EXIT.                            
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
      *    SEARCH K140-NUM-REGN  IN BP13KD00                          *         
      *****************************************************************         
                                                                                
       410-READ-KD00.                                                           
           MOVE   SPACES             TO     BP13KD00-REC.                       
           INITIALIZE                       BP13KD00-REC.                       
           MOVE  K140-NUM-REGN       TO     KD00-NUM-REGN.                      
           READ BP13KD00.                                                       
           IF KD00-STATUS = 00 OR 02                                            
              MOVE 'Y'               TO     WS-KD00-FND                         
              ADD 1                  TO     WS-CNT-KD00-READ                    
           ELSE                                                                 
             IF KD00-STATUS = 23                                                
                PERFORM 411-READ-KD03 THRU 411-EXIT                             
                IF KD03-FND                                                     
                   MOVE 'Y'          TO     WS-KD00-FND                         
                   ADD 1             TO     WS-CNT-KD00-READ                    
                ELSE                                                            
                   MOVE 'N'          TO     WS-KD00-FND                         
                   ADD 1             TO     WS-CNT-KD00-NOTFD                   
                   PERFORM 600-REWRITE-K140 THRU 600-EXIT                       
                END-IF                                                          
             ELSE                                                               
                DISPLAY 'ERROR WITH READ OF BP13KD00. STATUS IS '               
                       KD00-STATUS                                              
                MOVE   KD00-STATUS   TO   RETURN-CODE                           
                PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT                       
             END-IF                                                             
           END-IF.                                                              
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
       411-READ-KD03.                                                           
                                                                                
           MOVE   SPACES             TO     BP13KD03-REC.                       
           INITIALIZE                       BP13KD03-REC.                       
           MOVE  K140-NUM-REGN       TO     KD03-NUM-REGN.                      
           READ BP13KD03.                                                       
           IF KD03-STATUS = 00 OR 02                                            
              MOVE 'Y'               TO     WS-KD03-FND                         
           ELSE                                                                 
             IF KD00-STATUS = 23                                                
                MOVE 'N'             TO     WS-KD03-FND                         
             ELSE                                                               
                DISPLAY 'ERROR WITH READ OF BP13KD03. STATUS IS '               
                       KD03-STATUS                                              
                MOVE   KD03-STATUS   TO   RETURN-CODE                           
                PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT                       
             END-IF                                                             
           END-IF.                                                              
                                                                                
       411-EXIT.                                                                
           EXIT.                                                                
                                                                                
       420-READ-KD40.                                                           
           MOVE  SPACES               TO     KD40-KEY-FLD.                      
           MOVE  K140-NUM-REGN        TO     KD40-NUM-REGN.                     
           MOVE  K140-NUM-PP-SCHEDULE TO     KD40-NUM-PP-SCHDL.                 
           READ BP13KD40.                                                       
                                                                                
           IF KD40-STATUS = 00 OR 02                                            
              MOVE 'Y' TO WS-KD40-FND                                           
              ADD 1    TO WS-CNT-KD40-READ                                      
           ELSE                                                                 
             IF KD40-STATUS = 23                                                
                MOVE 'N' TO WS-KD40-FND                                         
                ADD 1    TO WS-CNT-KD40-NOTFD                                   
                PERFORM 600-REWRITE-K140   THRU 600-EXIT                        
             ELSE                                                               
                DISPLAY 'ERROR WITH READ OF BP13KD40. STATUS IS '               
                       KD40-STATUS                                              
                MOVE KD40-STATUS   TO   RETURN-CODE                             
                PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT                       
             END-IF                                                             
           END-IF.                                                              
                                                                                
        420-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       435-MOVE-F125-DATA.                                                      
                                                                                
           MOVE SPACES                TO BP13F125-CASHTRAN.                     
           INITIALIZE                    BP13F125-CASHTRAN.                     
                                                                                
           MOVE K140-CDE-TRANS-TYPE      TO   F125-CDE-TRANS-TYPE.              
           MOVE K140-NUM-REGN            TO   F125-NUM-REGN.                    
           MOVE K140-DTE-POST            TO   F125-DTE-POST.                    
           MOVE K140-COUNT-HISTORY       TO   F125-COUNT-HISTORY.               
           MOVE K140-NUM-NRIC            TO   F125-NUM-NRIC.                    
           MOVE K140-NME-NAME            TO   F125-NME-NAME.                    
           MOVE K140-NUM-RECPT-JRNO      TO   F125-NUM-RECPT-JRNO.              
           MOVE K140-CDE-APPLT-CPF       TO   F125-CDE-APPLT-CPF.               
           MOVE K140-AMT-RECEIPT         TO   F125-AMT-RECEIPT.                 
           MOVE K140-CDE-CRDR            TO   F125-CDE-CRDR.                    
           MOVE K140-DTE-TRANS           TO   F125-DTE-TRANS.                   
           MOVE K140-CDE-PAYMENT-TYPE    TO   F125-CDE-PAYMENT-TYPE.            
           MOVE K140-NUM-SCH-ACCT        TO   F125-NUM-SCH-ACCT.                
           MOVE K140-CDE-ERROR           TO   F125-CDE-ERROR.                   
           MOVE K140-CDE-VALID           TO   F125-CDE-VALID.                   
           MOVE K140-NUM-VR-DDD          TO   F125-NUM-VR-DDD.                  
           MOVE K140-NUM-VR-YY           TO   F125-NUM-VR-YY.                   
           MOVE K140-NUM-PP-SCHEDULE     TO   F125-NUM-PP-SCHEDULE.             
           MOVE K140-DTE-PP-DUE          TO   F125-DTE-PP-DUE.                  
           IF KD03-FND                                                          
              MOVE 'Y'                   TO   F125-NUM-DBSS-TOP                 
           END-IF.                                                              
           WRITE BP13F125-CASHTRAN.                                             
                                                                                
           ADD 1 TO WS-CNT-F125-VALID.                                          
                                                                                
       435-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-WRITE-F140.                                                          
      *-------------------------------------------------------------            
                                                                                
           MOVE   BP13K140-KIVTRAN   TO  BP13F140-KIVTRAN.                      
           WRITE  BP13F140-KIVTRAN.                                             
           ADD    1                  TO  WS-CNT-F140-WRITE.                     
                                                                                
           DELETE BP13K140.                                                     
                                                                                
           IF K140-STATUS NOT = 00                                              
              DISPLAY 'DELETING ERROR, BP13K140-STATUS'                         
                       K140-STATUS                                              
              MOVE K140-STATUS TO RETURN-CODE                                   
              PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT                         
           ELSE                                                                 
              ADD    1         TO  WS-CNT-K140-DEL                              
           END-IF.                                                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       600-REWRITE-K140.                                                        
      *-------------------------------------------------------------            
                                                                                
           MOVE   SPACES             TO  K140-CDE-VALID.                        
                                                                                
           REWRITE BP13K140-KIVTRAN.                                            
                                                                                
           IF K140-STATUS NOT = 00                                              
              DISPLAY 'UPDATE ERROR, BP13K140-STATUS'                           
                       K140-STATUS                                              
              MOVE K140-STATUS TO RETURN-CODE                                   
              PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT                         
           ELSE                                                                 
              ADD    1         TO  WS-CNT-K140-UPD                              
           END-IF.                                                              
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       1000-CLOSE-ROUTINE.                                                      
                                                                                
           DISPLAY '******  BP13CCAA *************'.                            
           DISPLAY 'NO OF K140 REC READ   (K140) : ' WS-CNT-K140-READ.          
           DISPLAY 'NO OF KD00 REC READ   (KD00) : ' WS-CNT-KD00-READ.          
           DISPLAY 'NO OF KD00 NOT FOUND  (KD00) : ' WS-CNT-KD00-NOTFD.         
           DISPLAY 'NO OF KD40 REC READ   (KD40) : ' WS-CNT-KD40-READ.          
           DISPLAY 'NO OF KD40 NOT FOUND  (KD40) : ' WS-CNT-KD40-NOTFD.         
           DISPLAY 'NO OF F125 REC WRITE  (F125) : ' WS-CNT-F125-VALID.         
           DISPLAY 'NO OF F140 REC WRITE  (F140) : '                            
                                             WS-CNT-F140-WRITE.                 
           DISPLAY 'NO OF K140 REC UPDATE (K140) : '                            
                                             WS-CNT-K140-UPD.                   
           DISPLAY 'NO OF K140 REC DELETE (K140) : ' WS-CNT-K140-DEL.           
                                                                                
           CLOSE BP13F140                                                       
                 BP13F125                                                       
                 BP13K140                                                       
                 BP13KD00                                                       
                 BP13KD03                                                       
                 BP13KD40.                                                      
                                                                                
           IF K140-STATUS NOT = 0 AND K140-STATUS NOT = 97                      
               DISPLAY ' CLOSING ERROR, BP13K140 STATUS ' K140-STATUS.          
                                                                                
           IF KD00-STATUS NOT = 0 AND KD00-STATUS NOT = 97                      
               DISPLAY ' CLOSING ERROR, BP13KD00 STATUS ' KD00-STATUS.          
                                                                                
           IF KD03-STATUS NOT = 0 AND KD03-STATUS NOT = 97                      
               DISPLAY ' CLOSING ERROR, BP13KD03 STATUS ' KD03-STATUS.          
                                                                                
           IF KD40-STATUS NOT = 0 AND KD40-STATUS NOT = 97                      
               DISPLAY ' CLOSING ERROR, BP13KD40 STATUS ' KD40-STATUS.          
                                                                                
                                                                                
       1000-EXIT.                                                               
           STOP RUN.                                                            
