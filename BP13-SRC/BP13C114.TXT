       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C114.                                                 
      *AUTHOR.        M.PRINCE.                                                 
      *DATE-WRITTEN.  03/08/1998.                                               
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVE   : PROGRAM TO DELETE EX-APPLN FILE IF    *                
      *                  REGN DELETED FROM SOC MASTER.         *                
      *                                                        *                
      *    INPUT FILE  :                                       *                
      *                  1. BP13K150 (EX-APPLT FILE)           *                
      *                  2. BP13K800 (APPLN    FILE)           *                
      *                                                        *                
      *    I-O FILE    :                                       *                
      *                  1. BP13K150 (EX-APPLT    FILE)        *                
      *   CHGE-NO   BY     DATE    DESCRIPTION                 *                
      *   -------   ---   -------- --------------------------  *                
      *   BP130461  MPA   03081998 NEW PROGRAM                 *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K150 ASSIGN TO  BP13K150                                  
                     ORGANIZATION IS INDEXED                                    
                     ACCESS MODE  IS SEQUENTIAL                                 
                     RECORD KEY   IS K150-KEY-FLD                               
                     FILE STATUS  IS WS-K150-STATUS.                            
                                                                                
           SELECT BP13K800 ASSIGN TO  BP13K800                                  
                     ORGANIZATION IS INDEXED                                    
                     ACCESS MODE  IS RANDOM                                     
                     RECORD KEY   IS K800-NUM-REGN                              
                     FILE STATUS  IS WS-K800-STATUS.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K150                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13K150.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-K800-STATUS                 PIC 99         VALUE 0.               
       01  WS-K150-STATUS                 PIC 99         VALUE 0.               
       01  WS-K150-READ                   PIC 9(5)       VALUE 0.               
       01  WS-K150-DELETED                PIC 9(5)       VALUE 0.               
       01  WS-K800-FND                    PIC 9(5)       VALUE 0.               
       01  WS-K800-NOTFND                 PIC 9(5)       VALUE 0.               
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-K150-EOF                PIC X          VALUE 'N'.             
                                                                                
       PROCEDURE DIVISION.                                                      
      *-----------------------------------------------------------------        
       000-MAIN.                                                                
      *-----------------------------------------------------------------        
                                                                                
           PERFORM 100-OPEN-FILE          THRU 100-EXIT.                        
           PERFORM 200-START-BP13K150     THRU 200-EXIT.                        
           PERFORM 220-READ-NEXT-K150     THRU 220-EXIT.                        
           PERFORM 250-PROCESS-BP13K800   THRU 250-EXIT                         
                   UNTIL WS-K150-EOF = 'Y'.                                     
           PERFORM 900-CLOSE-ROUTINE      THRU 900-EXIT.                        
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-OPEN-FILE.                                                           
      *-----------------------------------------------------------------        
                                                                                
           OPEN I-O    BP13K150                                                 
                INPUT  BP13K800.                                                
                                                                                
           IF WS-K150-STATUS NOT = 00                                           
              DISPLAY 'OPENING ERROR - WS-K150-STATUS : '                       
              WS-K150-STATUS                                                    
              MOVE WS-K150-STATUS     TO   RETURN-CODE                          
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                          
                                                                                
           IF WS-K800-STATUS NOT = 00                                           
              DISPLAY 'OPENING ERROR - WS-K800-STATUS : '                       
              WS-K800-STATUS                                                    
              MOVE WS-K800-STATUS     TO   RETURN-CODE                          
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                          
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-START-BP13K150.                                                      
      *-----------------------------------------------------------------        
                                                                                
           MOVE LOW-VALUES TO K150-KEY-FLD.                                     
           START BP13K150  KEY NOT LESS THAN K150-KEY-FLD.                      
           EVALUATE WS-K150-STATUS                                              
           WHEN  00                                                             
               CONTINUE                                                         
           WHEN  OTHER                                                          
               DISPLAY 'ERROR IN READING K150  = ' WS-K150-STATUS               
               DISPLAY '      K150-NUM-REGN    = ' K150-NUM-REGN                
               PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                          
           END-EVALUATE.                                                        
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       220-READ-NEXT-K150.                                                      
      *-----------------------------------------------------------------        
                                                                                
           READ BP13K150  NEXT                                                  
                AT END                                                          
                MOVE 'Y' TO WS-K150-EOF                                         
                GO TO 220-EXIT.                                                 
                                                                                
           ADD 1 TO WS-K150-READ.                                               
                                                                                
       220-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       250-PROCESS-BP13K800.                                                    
      *-----------------------------------------------------------------        
                                                                                
                                                                                
           MOVE    K150-NUM-REGN          TO   K800-NUM-REGN.                   
           PERFORM 300-READ-K800          THRU 300-EXIT.                        
                                                                                
           PERFORM 220-READ-NEXT-K150     THRU 220-EXIT.                        
                                                                                
       250-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-READ-K800.                                                           
      *-----------------------------------------------------------------        
                                                                                
           READ BP13K800.                                                       
           EVALUATE WS-K800-STATUS                                              
           WHEN  00                                                             
               ADD   1 TO WS-K800-FND                                           
           WHEN  23                                                             
               ADD   1 TO WS-K800-NOTFND                                        
               DISPLAY 'REGNNO NOTFND IN K800  = ' K800-NUM-REGN                
               PERFORM 400-DELETE-K150 THRU 400-EXIT                            
           WHEN  OTHER                                                          
               DISPLAY 'ERROR IN READING K800  = ' WS-K800-STATUS               
               DISPLAY '      K800-NUM-REGN    = ' K800-NUM-REGN                
               PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                          
           END-EVALUATE.                                                        
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-DELETE-K150.                                                         
      *-----------------------------------------------------------------        
                                                                                
           DELETE BP13K150.                                                     
                                                                                
           EVALUATE WS-K150-STATUS                                              
           WHEN  00                                                             
               ADD   1 TO WS-K150-DELETED                                       
               DISPLAY 'DELETING K150 REGN NO  = ' K150-NUM-REGN                
           WHEN  OTHER                                                          
               DISPLAY 'ERROR IN DELETING K150 = ' WS-K150-STATUS               
               DISPLAY '      K150-NUM-REGN    = ' K150-NUM-REGN                
               PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                          
           END-EVALUATE.                                                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------------        
      *     CLOSE ALL FILES AND STOP RUN.                           *           
      *-----------------------------------------------------------------        
       900-CLOSE-ROUTINE.                                                       
      *-----------------------------------------------------------------        
                                                                                
            DISPLAY '%--------------  BP13C114 -----------------%'.             
            DISPLAY '1) NO OF RECORDS PROCESSED (BP13K150) = '                  
                        WS-K150-READ.                                           
            DISPLAY '2) NO OF RECORDS FOUND     (BP13K800) = '                  
                        WS-K800-FND.                                            
            DISPLAY '3) NO OF RECORDS NOTFND    (BP13K800) = '                  
                        WS-K800-NOTFND.                                         
            DISPLAY '4) NO OF RECORDS DELETED   (BP13K150) = '                  
                        WS-K150-DELETED.                                        
                                                                                
            DISPLAY '  '.                                                       
            DISPLAY 'CHECK (1) = (2) + (3)'                                     
            DISPLAY '      (3) = (4)'                                           
                                                                                
            CLOSE BP13K150                                                      
                  BP13K800.                                                     
                                                                                
            IF WS-K150-STATUS NOT = '00'                                        
               DISPLAY 'CLOSING ERROR-BP13K150 ' WS-K150-STATUS                 
               MOVE WS-K150-STATUS TO RETURN-CODE.                              
            IF WS-K800-STATUS NOT = '00'                                        
               DISPLAY 'CLOSING ERROR-BP13K800 ' WS-K800-STATUS                 
               MOVE WS-K800-STATUS TO RETURN-CODE.                              
                                                                                
            STOP RUN.                                                           
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
