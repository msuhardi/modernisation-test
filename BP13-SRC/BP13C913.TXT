      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C913.                                                 
      *AUTHOR.        ROMAR ANDREW S MARALIT.                                   
      *DATE-WRITTEN.  21/07/2000.                                               
                                                                                
      ***************************************************************           
      *            SYSTEM OF COMMITMENT (BP13)                      *           
      *=============================================================*           
      *  OBJECTIVE  : TO RETRIEVE PROPERTY DATA FROM PIDB BASED ON  *           
      *               SCHEME ACCOUNT NUMBER.                        *           
      *                                                             *           
      *  INPUT      : SCHEME ACCOUNT NO.                            *           
      *  OUTPUT     : PIDB INFO.                                    *           
      *  COMMAREA   : 400 BYTES                                     *           
      *  NOTE       : THIS PROGRAM IS MODELLED AFTER BP13C904/C909  *           
      ***************************************************************           
      * MODIFICATIONS :                                           *             
      *                                                           *             
      * CHGE REQ #  DATE        AUTHOR  REMARKS                   *             
      * ----------  ----------  ------  ------------------------- *             
      * BP130883    21/07/2000  RASM    NEW PROGRAM.              *             
      * BP132560    10/07/2004  MTD     ADD QTY-AREA-ORGNL        *             
      * BP135601    10/07/2004  MTD     ADD UNIT-ID               *             
      *************************************************************             
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-SQL-CODES.                                                        
           05  WS-SQL-FOUND                 PIC S9(4) COMP VALUE +000.          
           05  WS-SQL-NOT-FOUND             PIC S9(4) COMP VALUE +100.          
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NUM-SCHM-ACNT             PIC X(9).                           
                                                                                
           EXEC SQL INCLUDE P13VINTF END-EXEC.                                  
           EXEC SQL INCLUDE SQLCA    END-EXEC.                                  
                                                                                
                                                                                
      *-----------------------*                                                 
       LINKAGE SECTION.                                                         
      *-----------------------*                                                 
       01  LINK-REC.                                                            
           05  LINK-NUM-SCH-ACC.                                                
               10  LINK-NUM-SCH             PIC X(4).                           
               10  LINK-NUM-ACC             PIC X(5).                           
                                                                                
       COPY P13COMM8.                                                           
                                                                                
                                                                                
      *------------------*                                                      
       PROCEDURE DIVISION USING LINK-REC, BP13COMM8-REC.                        
      *------------------*                                                      
                                                                                
           EXEC SQL WHENEVER NOT FOUND                                          
                             CONTINUE END-EXEC.                                 
           EXEC SQL WHENEVER SQLERROR                                           
                             GO TO 9000-ABORT-PROCESS END-EXEC.                 
           EXEC SQL WHENEVER SQLWARNING                                         
                             GO TO 9000-ABORT-PROCESS END-EXEC.                 
                                                                                
                                                                                
      *-----------------------*                                                 
       0000-START-ROUTINE.                                                      
      *-----------------------*                                                 
           MOVE SPACES TO  BP13COMM8-REC.                                       
           INITIALIZE      BP13COMM8-REC.                                       
                                                                                
           MOVE LINK-NUM-SCH-ACC TO COMM8-NUM-SCH-ACC.                          
                                                                                
           MOVE SPACE            TO COMM8-CDE-ERROR.                            
           MOVE ZEROES           TO COMM8-CDE-SYSERR.                           
                                                                                
           PERFORM 1000-READ-PIDB THRU 1000-EXIT.                               
                                                                                
           EXEC SQL COMMIT END-EXEC.                                            
                                                                                
           GOBACK.                                                              
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------*                    
      *    RETRIEVES UNIT INFORMATION USING THE SCHEME AND                      
      *    ACCOUNT NUMBER FROM THE UNIT LESSEE MASTER FILE.                     
      *----------------------------------------------------*                    
      *-----------------------*                                                 
       1000-READ-PIDB.                                                          
      *-----------------------*                                                 
           MOVE COMM8-NUM-SCH-ACC TO WS-NUM-SCHM-ACNT.                          
                                                                                
           EXEC SQL SELECT NUM_SCHM_ACNT,                                       
                           NUM_UNIT_ID,                                         
                           NUM_BLDNG_GL,                                        
                           NUM_BLK,                                             
                           CDE_ST,                                              
                           NME_STREET,                                          
                           NME_STFULL,                                          
                           NUM_POSTAL_CODE,                                     
                           NUM_ESTATE_CIM,                                      
                           NUM_NGHBHD_CIM,                                      
                           NUM_CNTRCT_CIM,                                      
                           NUM_LEVEL,                                           
                           NUM_UNIT,                                            
                           CDE_CLS_DESG,                                        
                           CDE_TYP_DESG,                                        
                           CDE_MODL_DESG,                                       
                           CDE_CONFIG_DESG,                                     
                           QTY_AREA_TOTAL,                                      
                           QTY_AREA_ORGNL,                                      
                           CDE_CLS_ACTLUSE,                                     
                           CDE_TYP_ACTLUSE,                                     
                           ACNT_CDE_STAT,                                       
                           QTY_MAX_FLRLVL,                                      
                           NUM_MGMT_TYPE,                                       
                           NUM_MGMT,                                            
                           NUM_MNTNC_TYPE,                                      
                           NUM_MNTNC,                                           
                           NUM_ED_GL                                            
                    INTO :DCLPIDB-INTERFACE.NUM-SCHM-ACNT,                      
                         :DCLPIDB-INTERFACE.NUM-UNIT-ID,                        
                         :DCLPIDB-INTERFACE.NUM-BLDNG-GL,                       
                         :DCLPIDB-INTERFACE.NUM-BLK,                            
                         :DCLPIDB-INTERFACE.CDE-ST,                             
                         :DCLPIDB-INTERFACE.NME-STREET,                         
                         :DCLPIDB-INTERFACE.NME-STFULL,                         
                         :DCLPIDB-INTERFACE.NUM-POSTAL-CODE,                    
                         :DCLPIDB-INTERFACE.NUM-ESTATE-CIM,                     
                         :DCLPIDB-INTERFACE.NUM-NGHBHD-CIM,                     
                         :DCLPIDB-INTERFACE.NUM-CNTRCT-CIM,                     
                         :DCLPIDB-INTERFACE.NUM-LEVEL,                          
                         :DCLPIDB-INTERFACE.NUM-UNIT,                           
                         :DCLPIDB-INTERFACE.CDE-CLS-DESG,                       
                         :DCLPIDB-INTERFACE.CDE-TYP-DESG,                       
                         :DCLPIDB-INTERFACE.CDE-MODL-DESG,                      
                         :DCLPIDB-INTERFACE.CDE-CONFIG-DESG,                    
                         :DCLPIDB-INTERFACE.QTY-AREA-TOTAL,                     
                         :DCLPIDB-INTERFACE.QTY-AREA-ORGNL,                     
                         :DCLPIDB-INTERFACE.CDE-CLS-ACTLUSE,                    
                         :DCLPIDB-INTERFACE.CDE-TYP-ACTLUSE,                    
                         :DCLPIDB-INTERFACE.ACNT-CDE-STAT,                      
                         :DCLPIDB-INTERFACE.QTY-MAX-FLRLVL,                     
                         :DCLPIDB-INTERFACE.NUM-MGMT-TYPE,                      
                         :DCLPIDB-INTERFACE.NUM-MGMT,                           
                         :DCLPIDB-INTERFACE.NUM-MNTNC-TYPE,                     
                         :DCLPIDB-INTERFACE.NUM-MNTNC,                          
                         :DCLPIDB-INTERFACE.NUM-ED-GL                           
                    FROM PIDB_INTERFACE                                         
                    WHERE NUM_SCHM_ACNT = :WS-NUM-SCHM-ACNT                     
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = WS-SQL-FOUND                                        
              DISPLAY '-----------------------------------------------'         
              DISPLAY 'REC NOTFD IN PIDB_INTERFACE = ' WS-NUM-SCHM-ACNT         
              DISPLAY 'SQLCODE                     = ' SQLCODE                  
              DISPLAY '-----------------------------------------------'         
              MOVE SQLCODE  TO  COMM8-CDE-SYSERR                                
              GO TO 1000-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE NUM-BLDNG-GL        TO COMM8-NUM-BLDNG-GL.                      
           MOVE NUM-UNIT-ID         TO COMM8-UNIT-ID.                           
           MOVE NUM-BLK             TO COMM8-NUM-BLK.                           
           MOVE NUM-LEVEL           TO COMM8-NUM-LEVEL.                         
           MOVE NUM-UNIT(1:4)       TO COMM8-NUM-UNIT-MAIN.                     
           MOVE NUM-UNIT(5:3)       TO COMM8-NUM-UNIT-SUB.                      
           MOVE ACNT-CDE-STAT       TO COMM8-ACNT-CDE-STAT.                     
           MOVE CDE-ST              TO COMM8-CDE-ST.                            
           MOVE NME-STREET          TO COMM8-NME-STREET.                        
           MOVE NME-STFULL          TO COMM8-NME-STFULL.                        
           MOVE NUM-POSTAL-CODE     TO COMM8-NUM-POSTAL-CODE.                   
           MOVE CDE-CLS-DESG        TO COMM8-CDE-CLS-DESG.                      
           MOVE CDE-TYP-DESG        TO COMM8-CDE-TYP-DESG.                      
           MOVE CDE-MODL-DESG       TO COMM8-CDE-MODL-DESG.                     
           MOVE CDE-CONFIG-DESG     TO COMM8-CDE-CONFIG-DESG.                   
           MOVE CDE-CLS-ACTLUSE     TO COMM8-CDE-CLS-ACTLUSE.                   
           MOVE CDE-TYP-ACTLUSE     TO COMM8-CDE-TYP-ACTLUSE.                   
           MOVE QTY-AREA-TOTAL      TO COMM8-QTY-AREA-TOTAL.                    
           MOVE QTY-AREA-ORGNL      TO COMM8-QTY-AREA-ORGNL.                    
           MOVE QTY-MAX-FLRLVL      TO COMM8-QTY-MAX-FLRLVL.                    
                                                                                
           IF NUM-MNTNC-TYPE = 'TC'                                             
               MOVE NUM-MNTNC       TO COMM8-NUM-MNTNC                          
           ELSE                                                                 
               MOVE SPACES          TO COMM8-NUM-MNTNC                          
           END-IF.                                                              
                                                                                
           MOVE NUM-MNTNC-TYPE      TO COMM8-NUM-MNTNC-TYPE.                    
           MOVE NUM-MGMT            TO COMM8-NUM-MGMT.                          
           MOVE NUM-ED-GL           TO COMM8-NUM-ED-GL.                         
           MOVE NUM-ESTATE-CIM      TO COMM8-NUM-ESTATE-CIM.                    
           MOVE NUM-NGHBHD-CIM      TO COMM8-NUM-NGHBHD-CIM.                    
           MOVE NUM-CNTRCT-CIM      TO COMM8-NUM-CNTRCT-CIM.                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------*                                                 
       9000-ABORT-PROCESS.                                                      
      *-----------------------*                                                 
           CALL 'DBATIAR' USING SQLCA.                                          
           MOVE SQLCODE       TO COMM8-CDE-SYSERR.                              
           MOVE SQLCODE       TO RETURN-CODE.                                   
           GOBACK.                                                              
