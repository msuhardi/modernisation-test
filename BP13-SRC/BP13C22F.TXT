       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C22F.                                                 
       AUTHOR.        RACHELLE SAN BUENAVENTURA.                                
       DATE-WRITTEN.  13/04/2007.                                               
      * ========================================================= *             
      *             UPDATE BP13K225 FROM BP13F225                 *             
      * ========================================================= *             
      *  PURPOSE OF THIS PROGRAM: READ BP13F225, THEN UPDATE      *             
      *                           CORRESPONDING RECORD IN BP13K225*             
      *                                                           *             
      *  INPUT        :  BP13F225                                 *             
      *  INPUT/OUTPUT :  BP13K225                                 *             
      *                                                           *             
      * ----------------------------------------------------------*             
      * CHG REF  DATE     BY   DESCRIPTION                        *             
      * -------- -------- --- ------------------------------------*             
      * BP133026 13/04/07 RB12 NEW PGM                            *             
      * ==========================================================*             
                                                                                
       ENVIRONMENT    DIVISION.                                                 
       CONFIGURATION  SECTION.                                                  
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT BP13F225  ASSIGN       TO BP13F225.                           
                                                                                
           SELECT BP13K225  ASSIGN        TO BP13K225                           
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K225-KEY-FLD                       
                            ACCESS MODE   IS RANDOM                             
                            FILE STATUS   IS K225-FILE-STATUS.                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F225                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13F225.                                                           
                                                                                
       FD   BP13K225                                                            
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13K225.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  K225-FILE-STATUS               PIC 9(2) VALUE 0.                     
       01  WS-F225-READ-CNT               PIC 9(8) VALUE ZEROES.                
       01  WS-K225-KEY-FND                PIC 9(8) VALUE ZEROES.                
       01  WS-K225-REWRITE-CNT            PIC 9(8) VALUE ZEROES.                
       01  WS-K225-REWRITE-ERR            PIC 9(8) VALUE ZEROES.                
                                                                                
       01  WS-DTE-CURRENT.                                                      
           05  WS-DTE-CCYY                PIC 9(4) VALUE ZEROES.                
           05  WS-DTE-MM                  PIC 9(2) VALUE ZEROES.                
           05  WS-DTE-DD                  PIC 9(2) VALUE ZEROES.                
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F225-EOF-FLAG           PIC X VALUE 'N'.                      
               88 WS-F225-EOF-YES               VALUE 'Y'.                      
               88 WS-F225-EOF-NO                VALUE 'N'.                      
           05  WS-K225-KEY-FOUND-FLAG     PIC X VALUE 'N'.                      
               88 WS-K225-KEY-FOUND             VALUE 'Y'.                      
               88 WS-K225-KEY-NOT-FOUND         VALUE 'N'.                      
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 1000-PRE-PROCESS         THRU 1000-EXIT.                     
           PERFORM 1500-READ-F225           THRU 1500-EXIT.                     
           PERFORM 2000-PROCESS-MASTER      THRU 2000-EXIT                      
                   UNTIL WS-F225-EOF-YES.                                       
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       1000-PRE-PROCESS.                                                        
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F225                                                 
                  I-O  BP13K225.                                                
                                                                                
           IF K225-FILE-STATUS NOT = 00 AND 97                                  
              DISPLAY 'ERROR OPENING BP13K225 : ' K225-FILE-STATUS              
              MOVE     K225-FILE-STATUS    TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1500-READ-F225.                                                          
      *-------------------------------------------------------------            
           READ BP13F225                                                        
             AT END                                                             
                SET WS-F225-EOF-YES TO TRUE                                     
           END-READ.                                                            
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       2000-PROCESS-MASTER.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES               TO K225-KEY-FLD.                           
                                                                                
           MOVE F225-NUM-QUEUE       TO K225-NUM-QUEUE.                         
                                                                                
           READ BP13K225.                                                       
                                                                                
           EVALUATE K225-FILE-STATUS                                            
                                                                                
           WHEN 00                                                              
              ADD 1                  TO WS-K225-KEY-FND                         
              PERFORM 2100-UPDATE-BP13K225 THRU 2100-EXIT                       
                                                                                
           WHEN 23                                                              
              DISPLAY 'F225 KEY NOT FOUND IN BP13K225: '                        
                          K225-ALT-KEY                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K225'                                  
              DISPLAY 'K225-FILE-STATUS: ' K225-FILE-STATUS                     
                                                                                
           END-EVALUATE.                                                        
                                                                                
           ADD 1 TO WS-F225-READ-CNT.                                           
                                                                                
           PERFORM 1500-READ-F225 THRU 1500-EXIT.                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       2100-UPDATE-BP13K225.                                                    
      *-------------------------------------------------------------            
                                                                                
           MOVE BP13F225-REC          TO BP13K225-REC.                          
                                                                                
           MOVE FUNCTION CURRENT-DATE TO K225-DTE-UPDATE.                       
           ACCEPT K225-TME-UPDATE   FROM TIME.                                  
           MOVE 'BP13C22F'            TO K225-NUM-USERID.                       
                                                                                
           REWRITE BP13K225-REC.                                                
                                                                                
           IF K225-FILE-STATUS EQUAL 00 OR 02                                   
              ADD 1 TO WS-K225-REWRITE-CNT                                      
           ELSE                                                                 
              DISPLAY 'ERROR REWRITING K225 - ' K225-FILE-STATUS                
                      ' K225 REC NOT UPDATED: ' K225-KEY-FLD                    
              ADD 1 TO WS-K225-REWRITE-ERR                                      
           END-IF.                                                              
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '******  BP13C22F  *************'.                           
           DISPLAY 'RECS READ FROM  BP13F225    : ' WS-F225-READ-CNT.           
           DISPLAY '                              '.                            
           DISPLAY 'KEYS FOUND IN BP13K225      : ' WS-K225-KEY-FND.            
           DISPLAY 'RECS REWRITTEN IN BP13K225  : ' WS-K225-REWRITE-CNT.        
           DISPLAY 'RECS ERROR REWRITE BP13K225 : ' WS-K225-REWRITE-ERR.        
           DISPLAY '                              '.                            
                                                                                
           CLOSE   BP13F225                                                     
                   BP13K225.                                                    
                                                                                
           IF K225-FILE-STATUS NOT = 00                                         
              DISPLAY 'ERROR CLOSING BP13K225 : ' K225-FILE-STATUS              
              MOVE     K225-FILE-STATUS    TO RETURN-CODE.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
