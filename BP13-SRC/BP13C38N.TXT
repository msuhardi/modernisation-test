      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C38N.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  JULY 16, 2015.                                            
      *===============================================================*         
      * OBJECTIVE  :  PRINT UPDATE TP/SATP CHECK CASES                *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :                                                  *         
      *               1. BP13F429                                     *         
      *                                                                         
      * OUTPUT FILE:  1. BP13L38N                                     *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP135893 16/07/2015 PCL4 NEW PROGRAM.                         *         
      * BP135893 03/08/2015 PCL4 TO CATER FOR FORMAT HEADERS          *         
      * BP136082 26/01/2016 KVL1 TO REPLACE  SU1 W/ HWM2 IN THE CC    *         
      * BP136477 06/10/2016 KVL1 ADDED CYM3 IN THE TO RECIPIENTS      *         
      * BP136477 18/10/2016 RJB1 TO REMOVE LIM KIM HUAT               *         
      * BP138257 19/03/2020 KV5  REMOVED THE SANGEETHA FROM COMMENTS  *         
      * BP139030 07/01/2022 ZAR7 REPLACE CHUA YEE MIEN TO LIM KOK CHUN          
      * BP139276 05/04/2024 KAC1 REPLACE LIM KOK CHUN BY              *         
      *                                  KATHIJAH_BEE_ALI_MOHAMED     *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F429  ASSIGN       TO BP13F429.                          
                                                                                
           SELECT  BM06K110  ASSIGN       TO BM06K110                           
                             ACCESS MODE  IS RANDOM                             
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS K110-KEY-FLD OF K110-REC           
                             FILE STATUS  IS WS-K110-STATUS.                    
                                                                                
           SELECT  BP13L38N  ASSIGN        TO BP13L38N.                         
           SELECT  MAILC38N  ASSIGN        TO MAILC38N.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F429                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 400 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F429.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BP13L38N                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 100 CHARACTERS                                      
            LABEL RECORD IS OMITTED                                             
            RECORDING MODE IS F.                                                
       01   BP13L38N-REC               PIC X(100).                              
                                                                                
       FD   MAILC38N                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS  100 CHARACTERS.                                    
       01   MAILC38N-REC               PIC X(100).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F429             PIC X(1)  VALUE 'N'.                     
           05  WS-REC-FND              PIC X(1)  VALUE 'N'.                     
           05  WS-K110-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-I                    PIC 9(1)  VALUE ZEROES.                  
           05  WS-F429-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-SNO                  PIC 9(7)  VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(2)  VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(4)  VALUE ZEROES.                  
           05  WS-CNT-L38N-WRITE       PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-WRITE            PIC 9(5)  VALUE 2.                       
           05  WS-CNT-DEBAR            PIC 9(7)  VALUE ZEROES.                  
           05  WS-MAX-LINE             PIC 9(2)  VALUE 55.                      
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-SYSTEM-DATE           PIC 9(8)  VALUE ZEROES.                  
           05 WS-SNO-ZZZZ9             PIC ZZZZ9 VALUE ZEROES.                  
           05 WS-FIRST-WRITE           PIC X(1)  VALUE 'Y'.                     
                                                                                
       01 WS-CURR-DATE.                                                         
          05  WS-CURR-CCYY                 PIC 9(04).                           
          05  WS-CURR-MM                   PIC 9(02).                           
          05  WS-CURR-DD                   PIC 9(02).                           
                                                                                
       01  L38N-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13L38N'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  L38N-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L38N-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
       01  L38N-HEADER02.                                                       
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(33)     VALUE                          
                   'REPORT ON CASES WITH BYPASS DEBAR'.                         
           05  FILLER              PIC X(20) VALUE                              
                   'MENT TAG CREATED ON '.                                      
           05  L38N-HDR-CREATE     PIC X(08)     VALUE SPACES.                  
                                                                                
       01  L38N-HEADER03.                                                       
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  FILLER              PIC X(05)  VALUE '  S/N'.                    
           05  FILLER              PIC X(02)  VALUE SPACES.                     
           05  FILLER              PIC X(08)  VALUE 'REGN NO '.                 
           05  FILLER              PIC X(02)  VALUE SPACES.                     
           05  FILLER              PIC X(14)  VALUE 'LAST UPDATE BY'.           
           05  FILLER              PIC X(03)  VALUE SPACES.                     
           05  FILLER              PIC X(14)  VALUE 'KEY-ISSUE-DTE='.           
           05  FILLER              PIC X(16)  VALUE 'REC-DTE-CREATED?'.         
                                                                                
       01  L38N-HEADER3A.                                                       
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  FILLER              PIC X(08)  VALUE 'REASON  '.                 
           05  FILLER              PIC X(24)  VALUE SPACES.                     
           05  FILLER              PIC X(02)  VALUE SPACES.                     
                                                                                
       01  L38N-DETAIL01.                                                       
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  L38N-SN             PIC X(05)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  L38N-REGN           PIC X(08)        VALUE SPACES.               
           05  FILLER              PIC X(05)        VALUE SPACES.               
           05  L38N-USERID         PIC X(11)        VALUE SPACES.               
           05  FILLER              PIC X(16)        VALUE SPACES.               
           05  L38N-VERIFY         PIC X(01)        VALUE SPACES.               
           05  FILLER              PIC X(08)        VALUE SPACES.               
                                                                                
       01  L38N-DETAIL02.                                                       
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  L38N-REASON1        PIC X(50)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  L38N-REASON2        PIC X(50)        VALUE SPACES.               
                                                                                
       01  L38N-DETAIL03.                                                       
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  L38N-REASON3        PIC X(50)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  L38N-REASON4        PIC X(50)        VALUE SPACES.               
                                                                                
       01  WS-REPORT-DTL.                                                       
           05  WS-EMPTY-RPT.                                                    
               10 FILLER                  PIC X(30) VALUE SPACES.               
               10 FILLER                  PIC X(21)                             
                                          VALUE '<   EMPTY  REPORT   >'.        
           05  WS-DATE-RPT-DTL.                                                 
               10 FILLER                PIC X(6)  VALUE 'DATE: '.               
               10 WS-DATE-RPT           PIC X(10) VALUE SPACES.                 
           05  WS-MAIL-RPT1             PIC X(12) VALUE 'HELO SGPHDB1'.         
           05  WS-MAIL-RPT2             PIC X(27)                               
               VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.                                
           05  WS-MAIL-RPT3A            PIC X(50)                               
               VALUE 'RCPT TO:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.           
           05  WS-MAIL-RPT4             PIC X(50)                               
               VALUE 'RCPT TO:<LOW_SOO_CHIEW@HDB.GOV.SG>'.                      
           05  WS-MAIL-RPT5             PIC X(50)                               
               VALUE 'RCPT TO:<HEE_WAN_MENG@HDB.GOV.SG>'.                       
           05  WS-MAIL-RPT6             PIC X(50)                               
               VALUE 'RCPT TO:<JAMILAH_BAHROM@HDB.GOV.SG>'.                     
           05  WS-MAIL-RPT7             PIC X(4)                                
               VALUE 'DATA'.                                                    
           05  WS-MAIL-RPT8             PIC X(29)                               
               VALUE 'FROM:Soc System - Email Alert'.                           
           05  WS-MAIL-RPT9A            PIC X(50)                               
               VALUE 'TO:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                
           05  WS-MAIL-RPT10            PIC X(50)                               
               VALUE 'CC:<LOW_SOO_CHIEW@HDB.GOV.SG>'.                           
           05  WS-MAIL-RPT11            PIC X(50)                               
               VALUE 'CC:<HEE_WAN_MENG@HDB.GOV.SG>'.                            
           05  WS-MAIL-RPT12            PIC X(50)                               
               VALUE 'CC:<JAMILAH_BAHROM@HDB.GOV.SG>'.                          
           05  WS-MAIL-RPT13            PIC X(100).                             
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           IF WS-REC-FND = 'Y'                                                  
              PERFORM 3000-PROCESS-RECORDS THRU 3000-EXIT                       
                UNTIL WS-EOF-F429 = 'Y'                                         
           ELSE                                                                 
              MOVE SPACES                    TO L38N-DETAIL01                   
              WRITE BP13L38N-REC           FROM L38N-DETAIL01                   
                                                                                
              WRITE MAILC38N-REC           FROM L38N-DETAIL01                   
                                                                                
              MOVE '                       NO RECORD FOUND!        '            
                                             TO L38N-DETAIL01                   
                                                                                
              WRITE BP13L38N-REC           FROM L38N-DETAIL01                   
              WRITE MAILC38N-REC           FROM L38N-DETAIL01                   
           END-IF.                                                              
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F429                                                 
                       BM06K110                                                 
              OUTPUT   MAILC38N                                                 
                       BP13L38N.                                                
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 - ERROR OPENING : ' WS-K110-STATUS              
              MOVE WS-K110-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F429      THRU 2000-EXIT.                      
           PERFORM 6000-PRINT-HEADER       THRU 6000-EXIT.                      
           PERFORM 7000-HEADER-EMAIL       THRU 7000-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F429.                                                      
      ******************************************************************        
                                                                                
           READ BP13F429           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F429                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO WS-F429-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           PERFORM 4000-PRINT-REPORT               THRU 4000-EXIT.              
           PERFORM 2000-READ-BP13F429              THRU 2000-EXIT.              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-PRINT-REPORT.                                                       
      ******************************************************************        
                                                                                
           MOVE SPACES                    TO L38N-DETAIL01.                     
           INITIALIZE                        L38N-DETAIL01.                     
                                                                                
           ADD 1                          TO WS-SNO.                            
           MOVE WS-SNO                    TO WS-SNO-ZZZZ9.                      
           MOVE WS-SNO-ZZZZ9              TO L38N-SN.                           
           MOVE F429-KEY-FLD              TO L38N-REGN.                         
           MOVE F429-NUM-USERID           TO L38N-USERID.                       
                                                                                
           IF F429-NUM-SCH-ACCT NOT = SPACES AND LOW-VALUES                     
              PERFORM 4200-READ-BM06K110 THRU 4200-EXIT                         
           END-IF.                                                              
                                                                                
           IF K110-DTE-KEY-ISSUED = F429-DTE-UPDATE                             
              MOVE 'Y'                    TO L38N-VERIFY                        
           ELSE                                                                 
              MOVE 'N'                    TO L38N-VERIFY                        
           END-IF.                                                              
                                                                                
           WRITE BP13L38N-REC           FROM L38N-DETAIL01.                     
           WRITE MAILC38N-REC           FROM L38N-DETAIL01.                     
                                                                                
           MOVE SPACES                    TO L38N-DETAIL01.                     
           INITIALIZE                        L38N-DETAIL01.                     
           WRITE BP13L38N-REC           FROM L38N-DETAIL01.                     
           WRITE MAILC38N-REC           FROM L38N-DETAIL01.                     
                                                                                
           MOVE F429-NUM-REASON1          TO L38N-REASON1.                      
           MOVE F429-NUM-REASON2          TO L38N-REASON2.                      
                                                                                
           WRITE BP13L38N-REC           FROM L38N-DETAIL02.                     
           WRITE MAILC38N-REC           FROM L38N-DETAIL02.                     
                                                                                
           IF F429-NUM-REASON3 NOT = SPACES AND LOW-VALUES                      
              MOVE F429-NUM-REASON3       TO L38N-REASON3                       
              MOVE F429-NUM-REASON4       TO L38N-REASON4                       
                                                                                
              WRITE BP13L38N-REC        FROM L38N-DETAIL03                      
              WRITE MAILC38N-REC        FROM L38N-DETAIL03                      
           END-IF.                                                              
                                                                                
           MOVE SPACES                    TO L38N-DETAIL01.                     
           INITIALIZE                        L38N-DETAIL01.                     
           WRITE BP13L38N-REC           FROM L38N-DETAIL01.                     
           WRITE MAILC38N-REC           FROM L38N-DETAIL01.                     
                                                                                
           MOVE SPACES                    TO L38N-DETAIL01.                     
           INITIALIZE                        L38N-DETAIL01.                     
           WRITE BP13L38N-REC           FROM L38N-DETAIL01.                     
           WRITE MAILC38N-REC           FROM L38N-DETAIL01.                     
                                                                                
           ADD 1                          TO WS-CNT-L38N-WRITE.                 
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       4200-READ-BM06K110.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                   TO K110-REC.                           
           INITIALIZE                       K110-REC.                           
                                                                                
           MOVE F429-NUM-SCH-ACCT        TO K110-KEY-FLD OF K110-REC.           
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-STATUS                                              
               WHEN 00                                                          
                    CONTINUE                                                    
                                                                                
               WHEN 23                                                          
                    MOVE SPACES                           TO K110-REC           
                    INITIALIZE                               K110-REC           
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BM06K110 : ' WS-K110-STATUS          
                            ' KEY  = ' K110-KEY-FLD OF K110-REC                 
                    PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT             
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6000-PRINT-HEADER.                                                       
      ******************************************************************        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)     TO WS-SYSTEM-DATE                
                                                  WS-CURR-DATE.                 
                                                                                
           MOVE 'Y'                            TO WS-REC-FND.                   
                                                                                
           ADD 1                               TO WS-PAGE-CTR.                  
           MOVE ZEROES                         TO L38N-PAGE-HDR.                
           MOVE WS-PAGE-CTR                    TO L38N-PAGE-HDR.                
                                                                                
                                                                                
           IF F429-DTE-UPDATE NOT = SPACES AND LOW-VALUES AND ZEROES            
              STRING F429-DTE-UPDATE(7:2) '/'                                   
                     F429-DTE-UPDATE(5:2) '/'                                   
                     F429-DTE-UPDATE(3:2) ' '                                   
                   DELIMITED BY SIZE INTO L38N-HDR-CREATE                       
           ELSE                                                                 
              MOVE 'N'                         TO WS-REC-FND                    
                                                                                
              STRING WS-SYSTEM-DATE(7:2) '/'                                    
                     WS-SYSTEM-DATE(5:2) '/'                                    
                     WS-SYSTEM-DATE(3:2) ' '                                    
                   DELIMITED BY SIZE INTO L38N-HDR-CREATE                       
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7000-HEADER-EMAIL.                                                       
      ******************************************************************        
                                                                                
            WRITE MAILC38N-REC FROM WS-MAIL-RPT1.                               
            WRITE MAILC38N-REC FROM WS-MAIL-RPT2.                               
            WRITE MAILC38N-REC FROM WS-MAIL-RPT3A.                              
            WRITE MAILC38N-REC FROM WS-MAIL-RPT4.                               
            WRITE MAILC38N-REC FROM WS-MAIL-RPT5.                               
            WRITE MAILC38N-REC FROM WS-MAIL-RPT6.                               
            WRITE MAILC38N-REC FROM WS-MAIL-RPT7.                               
            WRITE MAILC38N-REC FROM WS-MAIL-RPT8.                               
            WRITE MAILC38N-REC FROM WS-MAIL-RPT9A.                              
            WRITE MAILC38N-REC FROM WS-MAIL-RPT10.                              
            WRITE MAILC38N-REC FROM WS-MAIL-RPT11.                              
            WRITE MAILC38N-REC FROM WS-MAIL-RPT12.                              
                                                                                
            STRING 'SUBJECT: '                                                  
                   L38N-HEADER02                                                
                DELIMITED BY SIZE INTO WS-MAIL-RPT13.                           
                                                                                
            WRITE MAILC38N-REC FROM WS-MAIL-RPT13.                              
                                                                                
            MOVE SPACES TO MAILC38N-REC.                                        
            WRITE MAILC38N-REC.                                                 
                                                                                
            WRITE MAILC38N-REC FROM L38N-HEADER01.                              
            WRITE MAILC38N-REC FROM L38N-HEADER02.                              
                                                                                
            MOVE SPACES TO MAILC38N-REC.                                        
            WRITE MAILC38N-REC.                                                 
                                                                                
            WRITE MAILC38N-REC FROM L38N-HEADER03.                              
            WRITE MAILC38N-REC FROM L38N-HEADER3A.                              
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C38N'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F429 RECORDS READ............. : '            
                    WS-F429-READ.                                               
           DISPLAY '(2) NO OF BP13L38N RECORDS DELETED.......... : '            
                    WS-CNT-L38N-WRITE.                                          
                                                                                
           IF WS-REC-FND = 'N'                                                  
              DISPLAY '(3) NO RECORD FOUND!'                                    
           END-IF.                                                              
                                                                                
           CLOSE    BP13F429                                                    
                    BM06K110                                                    
                    MAILC38N                                                    
                    BP13L38N.                                                   
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 - ERROR CLOSING : ' WS-K110-STATUS              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
