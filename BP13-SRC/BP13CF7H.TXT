       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13CF7H.                                                  
       AUTHOR.       AM25.                                                      
       DATE-WRITTEN. 27/02/2023.                                                
      *****************************************************************         
      *                SOC - SYSTEM OF COMMITMENT (BP13)              *         
      *===============================================================*         
      *  OBJECTIVES :                                                 *         
      *     - TO UPDATE NUM-CAP-UPGRADER IN BP13K010                  *         
      *===============================================================*         
      * FILES DESCRIPTION :-                                          *         
      *                                                               *         
      *                ACCESS     REC                                 *         
      * DD NAME  I/O/E MODE       LEN  KEY FIELDS    REMARKS          *         
      * -------- ----- ---------- ---- ------------- ---------------- *         
      * BP13F010 I     SEQUENTIAL 600                                 *         
      * BP13F762 I     SEQUENTIAL 200                                 *         
      * BP13K010 I/O   RANDOM     600  K010-KEY-FLD                   *         
      *===============================================================*         
      * MODIFICATIONS :                                               *         
      *                                                               *         
      * CHG-REQ#  DDMMCCYY   BY   REMARKS                             *         
      * --------  --------  ----  ----------------------------------- *         
      * BP139516  27022023  AM25  NEW PROGRAM                         *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F010 ASSIGN      TO BP13F010.                             
           SELECT BP13F762 ASSIGN      TO BP13F762.                             
                                                                                
           SELECT BP13K010 ASSIGN      TO BP13K010                              
              ORGANIZATION  IS INDEXED                                          
              ACCESS MODE   IS RANDOM                                           
              RECORD KEY    IS K010-KEY-FLD                                     
              FILE STATUS   IS WS-K010-STATUS.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F010                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F010.                                                           
                                                                                
       FD  BP13F762                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F762.                                                           
                                                                                
       FD  BP13K010                                                             
           RECORD CONTAINS 600 CHARACTERS.                                      
       COPY BP13K010.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-K010-STATUS          PIC 9(2)        VALUE ZEROES.            
           05  WS-NUM-CAP-UPGRADER     PIC X           VALUE SPACES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F010-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-F762-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-K010-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-K010-UPDATE          PIC 9(6)        VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES-FLAGS.                                                   
           05  WS-F010-EOF             PIC X           VALUE 'N'.               
           05  WS-F762-EOF             PIC X           VALUE 'N'.               
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 2000-READ-BP13F010    THRU 2000-EXIT.                        
           PERFORM 2100-READ-BP13F762    THRU 2100-EXIT.                        
           PERFORM 3000-MAIN-PROCESSING  THRU 3000-EXIT                         
              UNTIL WS-F010-EOF = 'Y' AND WS-F762-EOF = 'Y'.                    
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN INPUT  BP13F010                                                 
                       BP13F762                                                 
                I-O    BP13K010.                                                
                                                                                
           IF (WS-K010-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K010 FAIL STATUS=' WS-K010-STATUS               
              MOVE WS-K010-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-READ-BP13F010.                                                      
      *****************************************************************         
                                                                                
           READ BP13F010                                                        
              AT END                                                            
                 MOVE HIGH-VALUES      TO F010-KEY-FLD                          
                 MOVE 'Y'              TO WS-F010-EOF                           
              NOT AT END                                                        
                 ADD 1                 TO WS-F010-READ                          
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2100-READ-BP13F762.                                                      
      *****************************************************************         
                                                                                
           READ BP13F762                                                        
              AT END                                                            
                 MOVE HIGH-VALUES      TO F762-KEY-FLD                          
                 MOVE 'Y'              TO WS-F762-EOF                           
              NOT AT END                                                        
                 ADD 1                 TO WS-F762-READ                          
           END-READ.                                                            
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-MAIN-PROCESSING.                                                    
      *****************************************************************         
                                                                                
           EVALUATE TRUE                                                        
              WHEN F010-KEY-FLD = F762-KEY-FLD                                  
                 MOVE 'Y'              TO WS-NUM-CAP-UPGRADER                   
                 PERFORM 3100-READ-BP13K010 THRU 3100-EXIT                      
                                                                                
                 PERFORM 2000-READ-BP13F010 THRU 2000-EXIT                      
                 PERFORM 2100-READ-BP13F762 THRU 2100-EXIT                      
                                                                                
              WHEN F010-KEY-FLD < F762-KEY-FLD                                  
                 MOVE SPACES           TO WS-NUM-CAP-UPGRADER                   
                 PERFORM 3100-READ-BP13K010 THRU 3100-EXIT                      
                                                                                
                 PERFORM 2000-READ-BP13F010 THRU 2000-EXIT                      
                                                                                
              WHEN F010-KEY-FLD > F762-KEY-FLD                                  
                 PERFORM 2100-READ-BP13F762 THRU 2100-EXIT                      
                                                                                
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3100-READ-BP13K010.                                                      
      *****************************************************************         
                                                                                
           MOVE F010-KEY-FLD           TO K010-KEY-FLD.                         
                                                                                
           READ BP13K010.                                                       
                                                                                
           EVALUATE WS-K010-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K010-READ                          
                 PERFORM 3150-UPDATE-BP13K010 THRU 3150-EXIT                    
                                                                                
              WHEN 23                                                           
                 DISPLAY 'RECORD NOT FOUND IN BP13K010: ' K010-KEY-FLD          
                                                                                
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K010 FAIL STATUS=' WS-K010-STATUS            
                 MOVE WS-K010-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                  
                                                                                
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3150-UPDATE-BP13K010.                                                    
      *****************************************************************         
                                                                                
           MOVE WS-NUM-CAP-UPGRADER    TO K010-NUM-CAP-UPGRADER.                
                                                                                
           REWRITE BP13K010-TENDER.                                             
                                                                                
           IF WS-K010-STATUS = ZEROES                                           
              ADD 1                    TO WS-K010-UPDATE                        
           ELSE                                                                 
              DISPLAY 'REWRITE BP13K010 FAIL STATUS=' WS-K010-STATUS            
              MOVE WS-K010-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       3150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           CLOSE BP13F010                                                       
                 BP13F762                                                       
                 BP13K010.                                                      
                                                                                
           IF WS-K010-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K010 FAIL STATUS=' WS-K010-STATUS              
              MOVE WS-K010-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           DISPLAY '******************************'.                            
           DISPLAY '* PROGRAM BP13CF7H           *'.                            
           DISPLAY '* CONTROL COUNTS             *'.                            
           DISPLAY '*----------------------------*'.                            
           DISPLAY '* REC READ F010     : ' WS-F010-READ ' *'.                  
           DISPLAY '* REC READ F762     : ' WS-F762-READ ' *'.                  
           DISPLAY '* REC READ K010     : ' WS-K010-READ ' *'.                  
           DISPLAY '* REC UPDATE K010   : ' WS-K010-UPDATE ' *'.                
           DISPLAY '******************************'.                            
                                                                                
       9000-EXIT.                                                               
           STOP RUN.                                                            
           EXIT.                                                                
                                                                                
      *======================  END OF PROGRAM  ======================*          
