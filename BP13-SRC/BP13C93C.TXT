      *--------------------------------------------------------*                
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------*                
       PROGRAM-ID.     BP13C93C.                                                
      *AUTHOR.         IRVIN CHING.                                             
      *DATE-WRITTEN.   27/05/2014.                                              
      *--------------------------------------------------------*                
      *========================================================*                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *      1. PROGRAM TO READ FIS BM06F120 FOR FLAT DATA     *                
      *         PROGRAM TO READ SOC BP13K820 FOR BOOK OCCUP    *                
      *         PROGRAM TO READ SOC BP13K895 FOR TP   OCCUP    *                
      *         PROGRAM TO READ SOC BB14K500 FOR ECD DATE      *                
      *      2. PROGRAM TO CREATE FILE BP13F213 FOR ELECTION   *                
      *         DEPARTMENT ON BOOKED/TP WITHIN HALF-YEAR PERIOD*                
      *      3. OUTPUT FILES FOR HDB FLAT BOOKED, TP           *                
      *         OUTPUT FILES FOR DBS FLAT BOOKED, TP           *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO    BY  DATE    DESCRIPTION                      *                
      * -------  ---  ------  -----------                      *                
      * BP135352 IMC1 100614  NEW PROGRAM                      *                
      * BP135352 IL5  130115  RENAME FIELD                     *                
      *                       F216-DTE-EST-COMPLETION TO       *                
      *                       F213-DTE-EST-COMPLETION          *                
      * BP135797 SMR2 180515  ADJUST CUTOFF COMPUTATION        *                
      * BP136256 FNP1 200516  IF DTE-ACCEPT = 0, OUTPUT REC, NO*                
      *                       NEED TO READ K820/K895           *                
      * BP136800 FNP1 050517  REMOVE CHECKING OF CUTOFF PERIOD *                
      *                       FOR BOOKED CASES AND ADD CHECKING*                
      *                       HANDOVER DATE FOR NOT BOOKED CASES                
      *========================================================*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT SY02F001  ASSIGN        TO SY02F001.                          
                                                                                
           SELECT BM06F120  ASSIGN        TO BM06F120.                          
                                                                                
           SELECT BP13K820  ASSIGN        TO BP13K820                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K820-KEY-FLD                       
                            FILE STATUS   IS K820-STATUS.                       
                                                                                
           SELECT BP13K895  ASSIGN        TO BP13K895                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K895-KEY-FLD                       
                            FILE STATUS   IS K895-STATUS.                       
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K110-KEY-FLD                       
                            FILE STATUS   IS K110-STATUS.                       
                                                                                
           SELECT BM06K100  ASSIGN        TO BM06K100                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K100-KEY-FLD                       
                            FILE STATUS   IS K100-STATUS.                       
                                                                                
           SELECT BB14K500  ASSIGN        TO BB14K500                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K500-KEY-FLD                       
                            FILE STATUS   IS K500-STATUS.                       
                                                                                
      *    SELECT BP13KA10 ASSIGN         TO BP13KA10                           
      *                    ORGANIZATION   IS INDEXED                            
      *                    ACCESS MODE    IS RANDOM                             
      *                    RECORD KEY     IS KA10-KEY-FLD                       
      *                    FILE STATUS    IS KA10-STATUS.                       
                                                                                
           SELECT AB03F089 ASSIGN         TO AB03F089.                          
                                                                                
           SELECT P13F213A ASSIGN         TO P13F213A.                          
                                                                                
           SELECT P13F213B ASSIGN         TO P13F213B.                          
                                                                                
      *--------------------------------------------------------*                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------*                
       COPY SY02F001.                                                           
                                                                                
       FD   BM06F120                                                            
            RECORD CONTAINS 700 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       COPY BM06F120.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K895                                                            
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K895.                                                           
                                                                                
       FD   AB03F089                                                            
            RECORD CONTAINS 250 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY AB03F089.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K100.                                                           
                                                                                
       FD   BB14K500                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BB14K500.                                                           
                                                                                
      *FD   BP13KA10                                                            
      *     RECORD CONTAINS 800 CHARACTERS.                                     
      *COPY BP13KA10.                                                           
                                                                                
       FD   P13F213A                                                            
            RECORD CONTAINS 150 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       01   P13F213A-REC    PIC X(150).                                         
                                                                                
       FD   P13F213B                                                            
            RECORD CONTAINS 150 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       01   P13F213B-REC    PIC X(150).                                         
                                                                                
      *--------------------------------------------------------*                
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------*                
       01  WORK-AREAS.                                                          
           05 WS-READ-F120              PIC 9(8)    VALUE 0.                    
           05 WS-READ-F089              PIC 9(8)    VALUE 0.                    
           05 WS-WRITE-REGN             PIC 9(8)    VALUE 0.                    
           05 WS-MATCH                  PIC 9(8)    VALUE 0.                    
           05 WS-NOT-MATCH              PIC 9(8)    VALUE 0.                    
           05 WS-WRITE-F213A            PIC 9(8)    VALUE 0.                    
           05 WS-WRITE-F213B            PIC 9(8)    VALUE 0.                    
           05 WS-BOOKED                 PIC 9(8)    VALUE 0.                    
           05 WS-NOTBK                  PIC 9(8)    VALUE 0.                    
           05 WS-TP                     PIC 9(8)    VALUE 0.                    
           05 WS-F213-STATUS            PIC 9(02)   VALUE ZEROES.               
           05 WS-FIRST                  PIC X(1)    VALUE 'N'.                  
           05 WS-F120-EOF               PIC X(1)    VALUE 'N'.                  
           05 WS-K820-FND               PIC X(1)    VALUE 'N'.                  
           05 WS-K820-EOF               PIC X(1)    VALUE 'N'.                  
           05 WS-K895-EOF               PIC X(1)    VALUE 'N'.                  
           05 WS-OK                     PIC X(1)    VALUE 'N'.                  
           05 K895-STATUS               PIC 9(2)    VALUE 0.                    
           05 K820-STATUS               PIC 9(2)    VALUE 0.                    
           05 K100-STATUS               PIC 9(2)    VALUE 0.                    
           05 K110-STATUS               PIC 9(2)    VALUE 0.                    
           05 K500-STATUS               PIC 9(2)    VALUE 0.                    
           05 KA10-STATUS               PIC 9(2)    VALUE 0.                    
           05 WS-DTE-EST-COMPLETION     PIC X(8)    VALUE SPACES.               
           05 WS-DTE-1ST-SALE           PIC X(8)    VALUE SPACES.               
           05 WS-CNT-5YR                PIC 9(8)    VALUE ZEROES.               
                                                                                
       01  WS-BP13F213-REC              PIC X(150)  VALUE SPACES.               
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-CDE-BLK               PIC X(5)    VALUE SPACES.               
           05  WS-CUTOFF-START.                                                 
               10 WS-YYYY-ST            PIC 9(4)    VALUE ZEROES.               
               10 WS-MM-ST              PIC 9(2)    VALUE ZEROES.               
               10 WS-DD-ST              PIC 9(2)    VALUE ZEROES.               
           05  WS-CUTOFF-END.                                                   
               10 WS-YYYY-END           PIC 9(4)    VALUE ZEROES.               
               10 WS-MM-END             PIC 9(2)    VALUE ZEROES.               
               10 WS-DD-END             PIC 9(2)    VALUE ZEROES.               
           05  WS-CUTOFF-5YR.                                                   
               10 WS-YYYY-5YR           PIC 9(4)    VALUE ZEROES.               
               10 WS-MM-5YR             PIC 9(2)    VALUE ZEROES.               
               10 WS-DD-5YR             PIC 9(2)    VALUE ZEROES.               
                                                                                
       COPY BP13F213.                                                           
      *--------------------------------------------------------*                
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------*                
                                                                                
      *--------------------------------------------------------*                
       0000-MAIN.                                                               
      *--------------------------------------------------------*                
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-F120     THRU 2000-EXIT.                           
           PERFORM 3000-READ-F089     THRU 3000-EXIT.                           
                                                                                
           PERFORM 4000-COMPARE       THRU 4000-EXIT                            
             UNTIL F120-SCH-ACC-NO  = HIGH-VALUES                               
                OR WS-F120-EOF = 'Y'                                            
                                                                                
           PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       1000-OPEN-FILES.                                                         
      *--------------------------------------------------------*                
                                                                                
           OPEN INPUT  BM06F120                                                 
                       BP13K820                                                 
                       BP13K895                                                 
                       BM06K100                                                 
                       BM06K110                                                 
                       BB14K500                                                 
      *                BP13KA10                                                 
                       AB03F089                                                 
                       SY02F001                                                 
           OUTPUT      P13F213A                                                 
                       P13F213B.                                                
                                                                                
           IF K820-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, K820-STATUS ' K820-STATUS                    
           END-IF.                                                              
                                                                                
           IF K895-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, K895-STATUS ' K895-STATUS                    
           END-IF.                                                              
                                                                                
           IF K100-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, K100-STATUS ' K100-STATUS                    
           END-IF.                                                              
                                                                                
           IF K110-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, K110-STATUS ' K110-STATUS                    
           END-IF.                                                              
                                                                                
      *    IF KA10-STATUS NOT = 00 AND 97                                       
      *       DISPLAY 'OPEN ERROR, KA10-STATUS ' KA10-STATUS                    
      *    END-IF.                                                              
                                                                                
           IF K500-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, K500-STATUS ' K500-STATUS                    
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
                                                                                
           MOVE F001-DTE-CURRENT       TO  WS-CUTOFF-START                      
           MOVE F001-DTE-LAST-DAY-PREV TO  WS-CUTOFF-END                        
                                                                                
           IF WS-MM-ST <= 6                                                     
              COMPUTE WS-YYYY-ST = WS-YYYY-ST - 1                               
              COMPUTE WS-MM-ST = WS-MM-ST + 6                                   
              MOVE  1        TO WS-DD-ST                                        
           ELSE                                                                 
              COMPUTE WS-MM-ST = WS-MM-ST - 6                                   
              MOVE  1        TO WS-DD-ST                                        
           END-IF.                                                              
                                                                                
      ****GET 5-YEAR CUTOFF DATE FOR NOT BOOKED CASES WITH HO DATE              
           COMPUTE WS-YYYY-5YR = WS-YYYY-END - 5.                               
           MOVE WS-MM-END TO WS-MM-5YR.                                         
           MOVE WS-DD-END TO WS-DD-5YR.                                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2000-READ-F120.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ BM06F120                                                        
                AT END                                                          
                   MOVE HIGH-VALUES TO F120-SCH-ACC-NO                          
                   MOVE 'Y' TO WS-F120-EOF                                      
                NOT AT END                                                      
                   ADD 1 TO WS-READ-F120                                        
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       3000-READ-F089.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ AB03F089                                                        
                AT END                                                          
                   MOVE HIGH-VALUES TO F089-NUM-SCH-ACCNT                       
                NOT AT END                                                      
                   ADD 1 TO WS-READ-F089                                        
           END-READ.                                                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4000-COMPARE.                                                            
      *--------------------------------------------------------*                
           MOVE ZEROS TO WS-DTE-EST-COMPLETION.                                 
           MOVE ZEROS TO WS-DTE-1ST-SALE.                                       
                                                                                
           IF F120-SCH-ACC-NO(1:9) = F089-NUM-SCH-ACCNT                         
              ADD 1 TO WS-MATCH                                                 
              PERFORM 4400-READ-BM06K110  THRU 4400-EXIT                        
              PERFORM 4100-WRITE-F213  THRU 4100-EXIT                           
                UNTIL F120-SCH-ACC-NO(1:9) NOT = F089-NUM-SCH-ACCNT             
                   OR WS-F120-EOF = 'Y'                                         
              PERFORM 3000-READ-F089   THRU 3000-EXIT                           
           ELSE                                                                 
              IF F120-SCH-ACC-NO(1:9) > F089-NUM-SCH-ACCNT                      
                 PERFORM 3000-READ-F089   THRU 3000-EXIT                        
              ELSE                                                              
                 ADD 1 TO WS-NOT-MATCH                                          
                 PERFORM 2000-READ-F120       THRU 2000-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4050-CHECK-DATE.                                                         
      *--------------------------------------------------------*                
           IF F120-DTE-KEY-ISSUED = SPACES OR ZEROES OR LOW-VALUES              
              PERFORM 4060-NOT-KEY-ISSUED THRU 4060-EXIT                        
           ELSE                                                                 
              IF F120-DTE-KEY-ISSUED >= WS-CUTOFF-START AND                     
                 F120-DTE-KEY-ISSUED <= WS-CUTOFF-END                           
                 MOVE 'Y'  TO WS-OK                                             
                 ADD 1 TO WS-TP                                                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4060-NOT-KEY-ISSUED.                                                     
      *--------------------------------------------------------*                
                                                                                
           IF F120-DTE-ACCEPTANCE = SPACES OR ZEROES OR LOW-VALUES              
              ADD 1 TO WS-NOTBK                                                 
              IF F120-DTE-HANDOVER >= WS-CUTOFF-5YR                             
                 MOVE 'Y'  TO WS-OK                                             
                 ADD 1 TO WS-CNT-5YR                                            
              END-IF                                                            
           ELSE                                                                 
              MOVE 'Y'  TO WS-OK                                                
              ADD 1 TO WS-BOOKED                                                
           END-IF.                                                              
                                                                                
       4060-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4100-WRITE-F213.                                                         
      *--------------------------------------------------------*                
                                                                                
           MOVE 'N'                    TO WS-OK.                                
           PERFORM 4050-CHECK-DATE     THRU 4050-EXIT.                          
           IF WS-OK = 'Y'                                                       
              CONTINUE                                                          
           ELSE                                                                 
              PERFORM 2000-READ-F120   THRU 2000-EXIT                           
              GO TO 4100-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE SPACES             TO BP13F213-REC                              
                                      WS-BP13F213-REC.                          
           INITIALIZE                 BP13F213-REC                              
                                      WS-BP13F213-REC.                          
                                                                                
           MOVE F089-CDE-TYP-ACTLUSE   TO F213-NUM-FLAT-TYPE                    
           MOVE F089-CDE-ST            TO F213-NUM-STREET                       
           MOVE F089-NUM-POSTAL-DSTRCT TO F213-NUM-POSTAL                       
           MOVE F089-NUM-BLK           TO F213-NUM-BLOCK                        
           MOVE F089-NUM-HSE           TO F213-NUM-UNIT                         
           MOVE F120-DTE-KEY-ISSUED    TO F213-DTE-KEY-ISSUED                   
           MOVE F120-DTE-ACCEPTANCE    TO F213-DTE-ACCEPTANCE                   
           MOVE F120-CDE-TYP-ACTUSE    TO F213-NUM-FLAT-TYPE                    
                                                                                
      *    IF F120-NUM-SELECTION = 'DBS'                                        
      *       PERFORM 4900-READ-BP13KA10  THRU 4900-EXIT                        
      *    ELSE                                                                 
              IF F213-DTE-KEY-ISSUED = SPACES OR LOW-VALUES OR ZEROES           
                 IF F120-DTE-HANDOVER NOT = SPACES AND LOW-VALUES AND           
                                            '00000000'                          
                    MOVE F120-DTE-HANDOVER TO F213-DTE-EST-COMPLETION           
                 ELSE                                                           
                    MOVE WS-DTE-EST-COMPLETION TO                               
                                               F213-DTE-EST-COMPLETION          
                 END-IF                                                         
              END-IF                                                            
      *    END-IF.                                                              
                                                                                
           MOVE BP13F213-REC           TO WS-BP13F213-REC                       
                                                                                
           IF F120-DTE-ACCEPTANCE = SPACES OR ZEROES OR LOW-VALUES              
              IF F120-DTE-HANDOVER >= WS-CUTOFF-5YR                             
                 MOVE WS-BP13F213-REC   TO BP13F213-REC                         
                 MOVE SPACES            TO F213-NUM-NRIC                        
                 MOVE SPACES            TO F213-NME-OCCP                        
                 WRITE P13F213A-REC FROM BP13F213-REC                           
                 ADD 1 TO WS-WRITE-F213A                                        
              END-IF                                                            
              PERFORM 2000-READ-F120       THRU 2000-EXIT                       
              GO TO 4100-EXIT                                                   
           END-IF.                                                              
                                                                                
           PERFORM 4200-START-BP13K820 THRU 4200-EXIT                           
           IF WS-K820-FND = 'N'                                                 
              PERFORM 4700-START-BP13K895 THRU 4700-EXIT                        
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F120       THRU 2000-EXIT.                         
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4200-START-BP13K820.                                                     
      *---------------------------------------------------------------*         
           MOVE SPACES             TO K820-KEY-FLD.                             
           MOVE 'N'                TO WS-K820-EOF                               
                                      WS-K820-FND.                              
           MOVE 'Y'                TO WS-FIRST.                                 
           MOVE F120-REGN-NO       TO K820-NUM-REGN.                            
                                                                                
           START BP13K820 KEY >= K820-KEY-FLD                                   
                                                                                
           EVALUATE K820-STATUS                                                 
           WHEN 00                                                              
                ADD 1   TO WS-WRITE-REGN                                        
                PERFORM 4300-READ-BP13K820 THRU 4300-EXIT                       
                  UNTIL WS-K820-EOF = 'Y'                                       
           WHEN 23                                                              
                MOVE SPACES            TO F213-NUM-NRIC                         
                MOVE SPACE             TO F213-NME-OCCP                         
                MOVE 'Y'               TO WS-K820-EOF                           
                                                                                
                DISPLAY 'RECORD NOT FOUND IN BP13K820, KEY IS '                 
                         K820-KEY-FLD ' ' F120-SCH-ACC-NO                       
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING IN BP13K820, STATUS IS '                 
                         K820-STATUS  ' ' F120-SCH-ACC-NO                       
                MOVE 'Y'               TO WS-K820-EOF                           
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4300-READ-BP13K820.                                                      
      *---------------------------------------------------------------*         
           READ BP13K820 NEXT RECORD                                            
                                                                                
           EVALUATE K820-STATUS                                                 
           WHEN 00                                                              
                IF K820-NUM-REGN = F120-REGN-NO                                 
                   IF WS-FIRST = 'Y'                                            
                      ADD 1    TO WS-WRITE-REGN                                 
                      MOVE 'N' TO WS-FIRST                                      
                   END-IF                                                       
                   MOVE 'Y'               TO WS-K820-FND                        
                   MOVE WS-BP13F213-REC   TO BP13F213-REC                       
                   MOVE K820-NUM-NRIC     TO F213-NUM-NRIC                      
                   MOVE K820-NME-OCCP     TO F213-NME-OCCP                      
                   IF F213-DTE-KEY-ISSUED = SPACES OR LOW-VALUES OR             
                                            ZEROES                              
                      WRITE P13F213A-REC FROM BP13F213-REC                      
                      ADD 1 TO WS-WRITE-F213A                                   
                   ELSE                                                         
                      WRITE P13F213B-REC FROM BP13F213-REC                      
                      ADD 1 TO WS-WRITE-F213B                                   
                   END-IF                                                       
                ELSE                                                            
                   MOVE 'Y'               TO WS-K820-EOF                        
                END-IF                                                          
           WHEN 23                                                              
                MOVE 'Y'               TO WS-K820-EOF                           
                                                                                
                DISPLAY 'RECORD NOT FOUND IN BP13K820, KEY IS '                 
                         K820-KEY-FLD ' ' F120-SCH-ACC-NO                       
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING IN BP13K820, STATUS IS '                 
                         K820-STATUS  ' ' F120-SCH-ACC-NO                       
                MOVE 'Y'               TO WS-K820-EOF                           
           END-EVALUATE.                                                        
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       4400-READ-BM06K110.                                                      
      *===============================================================*         
                                                                                
           MOVE SPACES                   TO K110-REC.                           
           INITIALIZE                       K110-REC.                           
                                                                                
           MOVE F120-SCH-ACC-NO        TO K110-KEY-FLD.                         
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE K110-STATUS                                                 
               WHEN 00                                                          
                    PERFORM 4500-READ-BM06K100 THRU 4500-EXIT                   
               WHEN 23                                                          
                    DISPLAY 'BM06K110 NOT FOUND: ' K100-KEY-FLD ' '             
                             F120-SCH-ACC-NO                                    
                    MOVE ZEROS              TO WS-DTE-EST-COMPLETION            
               WHEN OTHER                                                       
                    DISPLAY 'BM06K110 READ ERROR ' K110-STATUS                  
                    DISPLAY 'K110 KEY: ' K110-KEY-FLD                           
                    MOVE K110-STATUS           TO RETURN-CODE                   
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *===============================================================*         
       4500-READ-BM06K100.                                                      
      *===============================================================*         
                                                                                
           MOVE SPACES                   TO K100-REC.                           
           INITIALIZE                       K100-REC.                           
                                                                                
           MOVE K110-AIX1              TO K100-KEY-FLD.                         
                                                                                
           READ BM06K100.                                                       
                                                                                
           EVALUATE K100-STATUS                                                 
               WHEN 00                                                          
                    MOVE K100-DTE-1ST-SALE TO WS-DTE-1ST-SALE                   
                    IF K100-DTE-PACD = SPACES OR LOW-VALUES OR ZEROES           
                       MOVE K100-CDE-BLK  TO WS-CDE-BLK                         
                       PERFORM 4600-READ-BB14K500  THRU 4600-EXIT               
                    ELSE                                                        
                       MOVE K100-DTE-PACD TO WS-DTE-EST-COMPLETION              
                    END-IF                                                      
               WHEN 23                                                          
                    DISPLAY 'BM06K100 NOT FOUND: ' K100-KEY-FLD ' '             
                             F120-SCH-ACC-NO                                    
                    MOVE ZEROES           TO WS-DTE-EST-COMPLETION              
               WHEN OTHER                                                       
                    DISPLAY 'BM06K100 READ ERROR ' K100-STATUS                  
                    DISPLAY 'K100 KEY: ' K100-KEY-FLD                           
                    MOVE K100-STATUS           TO RETURN-CODE                   
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *===============================================================*         
       4600-READ-BB14K500.                                                      
      *===============================================================*         
                                                                                
           MOVE SPACES                   TO BB14K500-REC.                       
           INITIALIZE                       BB14K500-REC.                       
                                                                                
           MOVE WS-CDE-BLK               TO K500-KEY-FLD.                       
                                                                                
           READ BB14K500.                                                       
                                                                                
           EVALUATE K500-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    MOVE K500-DTE-BLDG-COMPLETE                                 
                      TO WS-DTE-EST-COMPLETION                                  
               WHEN 10                                                          
               WHEN 23                                                          
                    DISPLAY 'BB14K500 NOT FOUND: ' K500-KEY-FLD                 
                    MOVE ZEROES     TO WS-DTE-EST-COMPLETION                    
               WHEN OTHER                                                       
                    DISPLAY 'BB14K500 READ ERROR ' K500-STATUS                  
                    DISPLAY 'K500 KEY: ' K500-KEY-FLD                           
                    MOVE K500-STATUS           TO RETURN-CODE                   
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4600-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       4700-START-BP13K895.                                                     
      *************************************************************             
           MOVE SPACES                   TO BP13K895-REC.                       
           INITIALIZE                       BP13K895-REC.                       
                                                                                
           MOVE F120-REGN-NO             TO K895-NUM-REGN-HIST.                 
           MOVE 'N'                      TO WS-K895-EOF.                        
           MOVE 'Y'                      TO WS-FIRST.                           
                                                                                
           START BP13K895 KEY >= K895-NUM-REGN-HIST.                            
                                                                                
           EVALUATE K895-STATUS                                                 
               WHEN 00                                                          
                    PERFORM 4800-READNEXT-BP13K895 THRU 4800-EXIT               
                      UNTIL K895-NUM-REGN-HIST NOT = F120-REGN-NO               
                         OR WS-K895-EOF = 'Y'                                   
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'BP13K895 START ERROR ' K895-STATUS                 
                    DISPLAY 'K895 KEY: ' K895-KEY-FLD                           
                    MOVE K895-STATUS  TO RETURN-CODE                            
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4700-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       4800-READNEXT-BP13K895.                                                  
      *************************************************************             
                                                                                
           READ BP13K895 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'                 TO WS-K895-EOF                         
           END-READ.                                                            
                                                                                
           EVALUATE K895-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    IF K895-NUM-REGN-HIST = F120-REGN-NO                        
                       IF WS-FIRST = 'Y'                                        
                          ADD 1    TO WS-WRITE-REGN                             
                          MOVE 'N' TO WS-FIRST                                  
                       END-IF                                                   
                       MOVE WS-BP13F213-REC   TO BP13F213-REC                   
                       MOVE K895-NUM-NRIC     TO F213-NUM-NRIC                  
                       MOVE K895-NME-OCCP     TO F213-NME-OCCP                  
                       IF F213-DTE-KEY-ISSUED = SPACES OR LOW-VALUES OR         
                                                ZEROES                          
                          WRITE P13F213A-REC FROM BP13F213-REC                  
                          ADD 1 TO WS-WRITE-F213A                               
                       ELSE                                                     
                          WRITE P13F213B-REC FROM BP13F213-REC                  
                          ADD 1 TO WS-WRITE-F213B                               
                       END-IF                                                   
                    ELSE                                                        
                       MOVE 'Y'               TO WS-K895-EOF                    
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE 'Y'               TO WS-K895-EOF                       
               WHEN OTHER                                                       
                    DISPLAY 'BP13K895 READ NEXT ERROR ' K895-STATUS             
                    DISPLAY 'K895 KEY: ' K895-KEY-FLD                           
                    MOVE K895-STATUS  TO RETURN-CODE                            
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4800-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------           
      *4900-READ-BP13KA10.                                                      
      *--------------------------------------------------------------           
      *                                                                         
      *    INITIALIZE KA10-KEY-FLD.                                             
      *                                                                         
      *    MOVE FA30-NUM-PRJCT           TO KA10-KEY-FLD.                       
      *                                                                         
      *    READ BP13KA10.                                                       
      *                                                                         
      *    IF KA10-STATUS = 00                                                  
      *       MOVE KA10-DTE-ESTMD-TOP    TO F216-DTE-EST-COMPLETION             
      *    ELSE                                                                 
      *       IF KA10-STATUS = 23                                               
      *          DISPLAY 'BP13KA10 - REC NOT FND '                              
      *                  ',KEY IS '  KA10-KEY-FLD                               
      *       ELSE                                                              
      *          DISPLAY 'BP13KA10 - ERROR ON READ ' KA10-STATUS                
      *                  ',KEY IS '  KA10-KEY-FLD                               
      *       END-IF                                                            
      *    END-IF.                                                              
      *                                                                         
      *4900-EXIT.                                                               
      *    EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       9999-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------*                
                                                                                
           DISPLAY '*********** BP13C93C **************'.                       
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF RECORDS READ (BM06F120) :' WS-READ-F120.             
           DISPLAY 'NO. OF RECORDS READ (AB03F089) :' WS-READ-F089.             
           DISPLAY 'NO. OF RECORDS WRITE(P13F213A) :' WS-WRITE-F213A.           
           DISPLAY 'NOT BK ACCEPT DATE  (P13F213A) :' WS-NOTBK.                 
           DISPLAY '      BOOKED        (P13F213A) :' WS-BOOKED.                
           DISPLAY 'NO. OF RECORDS WRITE(P13F213B) :' WS-WRITE-F213B.           
           DISPLAY '      TP            (P13F213B) :' WS-TP.                    
           DISPLAY 'NO. OF REGN NO WRITE           :' WS-WRITE-REGN.            
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF RECORDS MATCH           :' WS-MATCH.                 
           DISPLAY 'NO. OF RECORDS NOT MATCH       :' WS-NOT-MATCH.             
           DISPLAY 'NO. OF RECORDS W/ HO <= 5 YRS  :' WS-CNT-5YR.               
           DISPLAY '************************************'.                      
                                                                                
           CLOSE       BM06F120                                                 
                       BM06K100                                                 
                       BM06K110                                                 
      *                BP13KA10                                                 
                       BP13K820                                                 
                       BP13K895                                                 
                       BB14K500                                                 
                       AB03F089                                                 
                       P13F213A                                                 
                       P13F213B.                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
