      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.   BP13CF65.                                                  
      *AUTHOR.       ZARITA DANE SARMIENTO.                                     
      *DATE-WRITTEN. 14/09/12.                                                  
                                                                                
      **************************************************************            
      * SYSTEM NAME :                                              *            
      *============================================================*            
      * OBJECTIVE :  TO GENERATE A REPORT AND UPDATE HH            *            
      *              DIFFERENCE IN HOUSEHOLD(HH)STATUS FOR         *            
      *              EC APPLICATION                                *            
      *============================================================*            
      * FILE DESCRIPTION :                                         *            
      * DD NAME   I/O/E  MODE        LEN   REMARKS                 *            
      * --------  -----  ----------  ---   ---------------------   *            
      * BP13F730    I    SEQUENTIAL        REGISTRATION NUMBERS    *            
      * BP13K825    I    VSAM DATASET      DATA                    *            
      * BP13KB78    I    VSAM DATASET      APPLICATION             *            
      * BP13LF65    O    SEQUENTIAL  136   REPORT FILE             *            
      **************************************************************            
      *   MODIFICATION LOG                                         *            
      *   ----------------                                         *            
      *   MODIFY DATE       CHGE REQ  DESCRIPTION OF CHANGE        *            
      *   ------ ---------- --------  ----------------------       *            
      *   ZDD1   13092012   BP13XXXX  NEW PROGRAM                  *            
      *   SMR2   22092014   BP135505  CATER FOR FT2T               *            
      **************************************************************            
                                                                                
      ***********************                                                   
       ENVIRONMENT DIVISION.                                                    
      ***********************                                                   
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730  ASSIGN TO BP13F730                                  
                           FILE STATUS IS WS-F730-STATUS.                       
                                                                                
           SELECT BP13K825  ASSIGN        TO BP13K825                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K825-KEY-FLD                       
                            FILE STATUS   IS WS-K825-STATUS.                    
                                                                                
           SELECT BP13KB78  ASSIGN        TO BP13KB78                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KB78-KEY-FLD                       
                            FILE STATUS   IS WS-KB78-STATUS.                    
                                                                                
           SELECT BP13KA10  ASSIGN        TO BP13KA10                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KA10-KEY-FLD                       
                            FILE STATUS   IS WS-KA10-STATUS.                    
                                                                                
           SELECT BP13LF65  ASSIGN TO BP13LF65.                                 
           SELECT P13F730A  ASSIGN TO P13F730A.                                 
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F730         RECORD CONTAINS 500 CHARACTERS                      
                            RECORDING MODE IS F.                                
       COPY BP13F730.                                                           
                                                                                
       FD  P13F730A         RECORD CONTAINS 500 CHARACTERS                      
                            RECORDING MODE IS F.                                
       01 P13F730A-REC  PIC X(500).                                             
                                                                                
       FD BP13K825.                                                             
       COPY BP13K825.                                                           
                                                                                
       FD BP13KB78.                                                             
       COPY BP13KB78.                                                           
                                                                                
       FD BP13KA10.                                                             
       COPY BP13KA10.                                                           
                                                                                
       FD  BP13LF65         RECORD CONTAINS 132 CHARACTERS                      
                            LABEL RECORDS ARE OMITTED                           
                            RECORDING MODE IS F.                                
                                                                                
       01 BP13LF65-REC       PIC X(132).                                        
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01 FILE-STATUS.                                                          
          03 WS-K825-STATUS       PIC 9(2)     VALUE ZEROES.                    
          03 WS-F730-STATUS       PIC 9(2)     VALUE ZEROES.                    
          03 WS-KB78-STATUS       PIC 9(2)     VALUE ZEROES.                    
          03 WS-KA10-STATUS       PIC 9(2)     VALUE ZEROES.                    
          03 WS-K730-STATUS       PIC 9(2)     VALUE ZEROES.                    
                                                                                
       01 FLAGS.                                                                
          03 WS-F730-EOF          PIC X(1)     VALUE 'N'.                       
          03 WS-K825-EOF          PIC X(1)     VALUE 'N'.                       
          03 WS-K825-NOTFD        PIC X(1)     VALUE 'N'.                       
          03 WS-FOUND-KB78        PIC X(1)     VALUE 'N'.                       
          03 WS-FOUND-K730        PIC X(1)     VALUE 'N'.                       
          03 WS-PRINT-NOT         PIC X(1)     VALUE 'N'.                       
          03 WS-PRINT             PIC X(1)     VALUE 'N'.                       
                                                                                
       01 COUNTERS.                                                             
          03 WS-SNO               PIC 9(5)     VALUE ZEROES.                    
          03 WS-CHILD-CTR         PIC 9(2)     VALUE ZEROES.                    
          03 WS-CTR               PIC 9(3)     VALUE ZEROES.                    
          03 WS-COUNTER           PIC 9(6)     VALUE ZEROES.                    
          03 WS-PAGE-CNTR         PIC 9(6)     VALUE ZEROES.                    
          03 WS-LINE-CNTR         PIC 9(2)     VALUE 60.                        
          03 WS-F730-READ         PIC 9(6)     VALUE ZEROES.                    
          03 WS-K825-READ         PIC 9(6)     VALUE ZEROES.                    
          03 WS-KB78-READ         PIC 9(6)     VALUE ZEROES.                    
          03 WS-K730-READ         PIC 9(6)     VALUE ZEROES.                    
          03 WS-K730-UPD          PIC 9(6)     VALUE ZEROES.                    
          03 WS-NOTFD-CNT-K825    PIC 9(6)     VALUE ZEROES.                    
          03 WS-NOTFD-CNT-KB78    PIC 9(6)     VALUE ZEROES.                    
          03 WS-EXCEPTION         PIC 9(6)     VALUE ZEROES.                    
          03 WS-HH-UPD            PIC 9(6)     VALUE ZEROES.                    
          03 WS-NO-UPD            PIC 9(6)     VALUE ZEROES.                    
          03 WS-PREV-NT1          PIC X(3)     VALUE SPACES.                    
                                                                                
       01 ACCUMULATORS.                                                         
          03 WS-BP13LF65-WRITE     PIC 9(6)     VALUE ZEROES.                   
                                                                                
       01 VARIABLES.                                                            
          05 WS-REGN-PREV         PIC X(8).                                     
          05 WS-K825-DETAILS         OCCURS 10 TIMES.                           
             10 WS-NRIC              PIC X(9).                                  
      *      10 WS-NAME              PIC X(46).                                 
             10 WS-NAME              PIC X(35).                                 
             10 WS-PHONE             PIC X(8).                                  
             10 WS-REL-CODE          PIC X(2).                                  
             10 WS-STAT-CODE         PIC X(1).                                  
             10 WS-DOB               PIC X(8).                                  
             10 WS-CITI-CODE         PIC X(2).                                  
             10 WS-REL-DESC          PIC X(12).                                 
             10 WS-STAT-DESC         PIC X(9).                                  
             10 WS-CITI-DESC         PIC X(20).                                 
                                                                                
       01 DATES.                                                                
          03 WS-CURRENT-DATE.                                                   
             05 WS-CURR-CC        PIC 9(2).                                     
             05 WS-CURR-YY        PIC 9(2).                                     
             05 WS-CURR-MM        PIC 9(2).                                     
             05 WS-CURR-DD        PIC 9(2).                                     
                                                                                
       01 BLANK-LINE.                                                           
          03 FILLER               PIC X(132)   VALUE SPACES.                    
                                                                                
       01 HDNG-1.                                                               
           05  FILLER              PIC X(08)     VALUE 'BP13LF65'.              
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(25)     VALUE SPACES.                  
           05  FILLER              PIC X(41)     VALUE                          
               'S Y S T E M    O F    C O M M I T M E N T'.                     
          03 FILLER               PIC X(12)     VALUE SPACES.                   
          03 FILLER               PIC X(7)     VALUE 'DATE : '.                 
          03 HDNG-CURR-DD         PIC X(2)     VALUE SPACES.                    
          03 FILLER               PIC X(1)     VALUE '/'.                       
          03 HDNG-CURR-MM         PIC X(2)     VALUE SPACES.                    
          03 FILLER               PIC X(1)     VALUE '/'.                       
          03 HDNG-CURR-CC         PIC X(2)     VALUE SPACES.                    
          03 HDNG-CURR-YY         PIC X(2)     VALUE SPACES.                    
          03 FILLER               PIC X(2)     VALUE SPACES.                    
          03 FILLER               PIC X(07)     VALUE 'PAGE : '.                
          03 HDNG-PAGE-CNTR       PIC Z,ZZ9    VALUE SPACES.                    
                                                                                
       01 HDNG-2.                                                               
          03 FILLER               PIC X(14)    VALUE 'PROJECT CODE :'.          
          03 FILLER               PIC X(01)    VALUE SPACES.                    
          03 LF65-PROJ-NAME       PIC X(25)    VALUE SPACES.                    
          03 FILLER               PIC X(47)    VALUE                            
             'EXCEPTION REPORT ON DIFFERENCE IN HOUSEHOLD(HH)'.                 
                                                                                
       01 HDNG-3.                                                               
          03 FILLER               PIC X(56)    VALUE SPACES.                    
          03 FILLER               PIC X(26)    VALUE                            
                   'STATUS FOR EC APPLICATIONS'.                                
          03 FILLER               PIC X(50)     VALUE SPACES.                   
                                                                                
       01 HDNG-4.                                                               
          03 FILLER               PIC X(05)    VALUE 'S/NO.'.                   
          03 FILLER               PIC X(02)    VALUE SPACES.                    
          03 FILLER               PIC X(08)    VALUE 'REGN NO.'.                
          03 FILLER               PIC X(2)     VALUE SPACES.                    
          03 FILLER               PIC X(20)    VALUE                            
                   'NAME OF APPL/OCCU'.                                         
          03 FILLER               PIC X(02)    VALUE SPACES.                    
          03 FILLER               PIC X(08)    VALUE 'PHONE NO'.                
          03 FILLER               PIC X(02)    VALUE SPACES.                    
          03 FILLER               PIC X(30)    VALUE 'EMAIL ADDRESS'.           
          03 FILLER               PIC X(2)     VALUE SPACES.                    
          03 FILLER               PIC X(09)    VALUE 'NRIC NO  '.               
          03 FILLER               PIC X(02)    VALUE SPACES.                    
          03 FILLER               PIC X(12)    VALUE 'DECLARED HH '.            
          03 FILLER               PIC X(2)     VALUE SPACES.                    
          03 FILLER               PIC X(13)    VALUE 'DETERMINED HH'.           
          03 FILLER               PIC X(2)     VALUE SPACES.                    
          03 FILLER               PIC X(9)     VALUE 'BALLOT HH'.               
                                                                                
       01 HDNG-5.                                                               
          03 FILLER               PIC X(132)   VALUE ALL '-'.                   
                                                                                
       01 DETL-1.                                                               
          03 DETL-SNO             PIC ZZZZ9.                                    
          03 FILLER               PIC X(2)     VALUE SPACES.                    
          03 DETL-REGN            PIC X(08)    VALUE SPACES.                    
          03 FILLER               PIC X(02)    VALUE SPACES.                    
          03 DETL-NAME            PIC X(20)    VALUE SPACES.                    
          03 FILLER               PIC X(02)    VALUE SPACES.                    
          03 DETL-PHONE           PIC X(08)    VALUE SPACES.                    
          03 FILLER               PIC X(02)    VALUE SPACES.                    
          03 DETL-EMAIL           PIC X(30)    VALUE SPACES.                    
          03 FILLER               PIC X(2)     VALUE SPACES.                    
          03 DETL-NRIC            PIC X(09)    VALUE SPACES.                    
          03 FILLER               PIC X(2)     VALUE SPACES.                    
          03 DETL-DEC-HH          PIC X(12)    VALUE SPACES.                    
          03 FILLER               PIC X(2)     VALUE SPACES.                    
          03 DETL-DET-HH          PIC X(13)    VALUE SPACES.                    
          03 FILLER               PIC X(2)     VALUE SPACES.                    
          03 DETL-BAL-HH          PIC X(9)     VALUE SPACES.                    
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *****************************************************************         
       MAIN.                                                                    
      *****************************************************************         
                                                                                
            PERFORM 0000-OPENING       THRU 0000-EXIT.                          
            PERFORM 1000-READ-BP13F730 THRU 1000-EXIT.                          
            PERFORM 2000-PROCESS-DATA  THRU 2000-EXIT                           
                        UNTIL WS-F730-EOF = 'Y'.                                
            PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                          
                                                                                
      *****************************************************************         
       0000-OPENING.                                                            
      *****************************************************************         
                                                                                
            OPEN INPUT  BP13F730                                                
                        BP13K825                                                
                        BP13KB78                                                
                        BP13KA10                                                
                 OUTPUT BP13LF65                                                
                        P13F730A.                                               
                                                                                
            IF WS-K825-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING BP13K825 ERROR ' WS-K825-STATUS                 
               MOVE     WS-K825-STATUS      TO RETURN-CODE                      
               PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                      
            END-IF.                                                             
                                                                                
            IF WS-KB78-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING BP13KB78 ERROR ' WS-KB78-STATUS                 
               MOVE     WS-KB78-STATUS      TO RETURN-CODE                      
               PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                      
            END-IF.                                                             
                                                                                
            IF WS-KA10-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING BP13KA10 ERROR ' WS-KA10-STATUS                 
               MOVE     WS-KA10-STATUS      TO RETURN-CODE                      
               PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                      
            END-IF.                                                             
                                                                                
            MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE.                      
            MOVE WS-CURR-DD TO HDNG-CURR-DD.                                    
            MOVE WS-CURR-MM TO HDNG-CURR-MM.                                    
            MOVE WS-CURR-CC TO HDNG-CURR-CC.                                    
            MOVE WS-CURR-YY TO HDNG-CURR-YY.                                    
                                                                                
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       1000-READ-BP13F730.                                                      
      *****************************************************************         
                                                                                
            READ BP13F730                                                       
               AT END MOVE 'Y' TO WS-F730-EOF                                   
               GO TO 1000-EXIT.                                                 
                                                                                
            ADD 1      TO WS-F730-READ.                                         
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       2000-PROCESS-DATA.                                                       
      *****************************************************************         
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1                                   
             UNTIL WS-CTR > 10                                                  
                MOVE SPACES TO WS-K825-DETAILS (WS-CTR)                         
           END-PERFORM.                                                         
                                                                                
           MOVE SPACES TO K825-KEY-FLD.                                         
           MOVE F730-NUM-REGN TO K825-NUM-REGN.                                 
                                                                                
           START BP13K825 KEY IS >= K825-KEY-FLD END-START.                     
           MOVE 0 TO WS-CTR.                                                    
           MOVE 0 TO WS-CHILD-CTR.                                              
           MOVE 'N' TO WS-PRINT-NOT.                                            
           MOVE 'N' TO WS-K825-EOF.                                             
           MOVE 'N' TO WS-PRINT                                                 
                                                                                
           PERFORM 3000-READ-K825 THRU 3000-EXIT.                               
                                                                                
           PERFORM 3500-PROCESS-K825 THRU 3500-EXIT                             
              UNTIL F730-NUM-REGN NOT EQUAL K825-NUM-REGN                       
              OR WS-K825-EOF = 'Y'                                              
              OR WS-PRINT-NOT = 'Y'.                                            
                                                                                
                                                                                
           IF WS-PRINT-NOT = 'N'                                                
              MOVE 1 TO WS-CTR                                                  
              MOVE SPACES              TO BP13KB78-REC                          
              MOVE F730-NUM-REGN       TO KB78-NUM-REGN                         
              PERFORM 4600-READ-KB78   THRU 4600-EXIT                           
              IF KB78-NUM-HHTYPE-DECLARE = F730-CDE-HOUSEHOLD                   
                  IF F730-NUM-FT2T = 'Y' AND F730-CDE-HOUSEHOLD = 'G'           
                     MOVE 'F'                   TO                              
                                 F730-CDE-BALLOT-HOUSEHOLD                      
                  ELSE                                                          
                     MOVE F730-CDE-HOUSEHOLD    TO                              
                                 F730-CDE-BALLOT-HOUSEHOLD                      
                  END-IF                                                        
                  WRITE P13F730A-REC    FROM BP13F730-REC                       
                  ADD 1 TO WS-K730-UPD                                          
                  PERFORM 5100-PRINT-DETAILS THRU 5100-EXIT                     
                      UNTIL WS-CTR > 10 OR WS-NAME(WS-CTR) = SPACES             
                  MOVE 'Y' TO WS-PRINT                                          
              END-IF                                                            
              IF KB78-NUM-HHTYPE-DECLARE = SPACES OR LOW-VALUES                 
                  IF F730-NUM-FT2T = 'Y' AND F730-CDE-HOUSEHOLD = 'G'           
                     MOVE 'F'                   TO                              
                                 F730-CDE-BALLOT-HOUSEHOLD                      
                  ELSE                                                          
                     MOVE F730-CDE-HOUSEHOLD    TO                              
                                 F730-CDE-BALLOT-HOUSEHOLD                      
                  END-IF                                                        
                  WRITE P13F730A-REC    FROM BP13F730-REC                       
                  ADD 1 TO WS-K730-UPD                                          
                  PERFORM 5100-PRINT-DETAILS THRU 5100-EXIT                     
                      UNTIL WS-CTR > 10 OR WS-NAME(WS-CTR) = SPACES             
                  MOVE 'Y' TO WS-PRINT                                          
              END-IF                                                            
              IF KB78-NUM-HHTYPE-DECLARE = 'G'                                  
                 IF F730-CDE-HOUSEHOLD = 'H' OR 'T'                             
                  MOVE F730-CDE-HOUSEHOLD    TO                                 
                                 F730-CDE-BALLOT-HOUSEHOLD                      
                  WRITE P13F730A-REC    FROM BP13F730-REC                       
                  ADD 1 TO WS-K730-UPD                                          
                  PERFORM 5100-PRINT-DETAILS THRU 5100-EXIT                     
                      UNTIL WS-CTR > 10 OR WS-NAME(WS-CTR) = SPACES             
                  MOVE 'Y' TO WS-PRINT                                          
                 END-IF                                                         
              END-IF                                                            
              IF KB78-NUM-HHTYPE-DECLARE = 'H' OR 'T'                           
                 IF F730-CDE-HOUSEHOLD = 'G'                                    
                  IF F730-NUM-FT2T = 'Y'                                        
                     MOVE 'F'                   TO                              
                                 F730-CDE-BALLOT-HOUSEHOLD                      
                  ELSE                                                          
                     MOVE F730-CDE-HOUSEHOLD    TO                              
                                 F730-CDE-BALLOT-HOUSEHOLD                      
                  END-IF                                                        
                  WRITE P13F730A-REC    FROM BP13F730-REC                       
                  ADD 1 TO WS-K730-UPD                                          
                  PERFORM 5100-PRINT-DETAILS THRU 5100-EXIT                     
                      UNTIL WS-CTR > 10 OR WS-NAME(WS-CTR) = SPACES             
                  MOVE 'Y' TO WS-PRINT                                          
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-PRINT = 'Y'                                                    
              ADD 1 TO WS-HH-UPD                                                
           ELSE                                                                 
              ADD 1 TO WS-NO-UPD                                                
           END-IF.                                                              
                                                                                
           PERFORM 1000-READ-BP13F730 THRU 1000-EXIT.                           
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       3000-READ-K825.                                                          
      *****************************************************************         
                                                                                
            ADD 1 TO WS-CTR.                                                    
                                                                                
            READ BP13K825 NEXT RECORD.                                          
                                                                                
            IF WS-K825-STATUS  =  00                                            
               ADD 1 TO WS-K825-READ                                            
               IF F730-NUM-REGN = K825-NUM-REGN                                 
                  MOVE K825-NUM-NRIC TO WS-NRIC(WS-CTR)                         
                  MOVE K825-NME-OCCP TO WS-NAME(WS-CTR)                         
                  MOVE K825-NUM-PHONE TO WS-PHONE(WS-CTR)                       
               END-IF                                                           
            ELSE                                                                
               IF WS-K825-STATUS  =  10                                         
                  MOVE 'Y'  TO WS-K825-EOF                                      
               ELSE                                                             
                 IF WS-K825-STATUS  =  23                                       
                    ADD 1 TO WS-NOTFD-CNT-K825                                  
                    MOVE  'Y'  TO WS-K825-NOTFD                                 
                    DISPLAY 'REC NOT FND IN K825 REGN : ' K825-NUM-REGN         
                 ELSE                                                           
                   DISPLAY 'ERROR READING BP13K825   : ' WS-K825-STATUS         
                   MOVE     WS-K825-STATUS      TO RETURN-CODE                  
                   PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                  
                 END-IF                                                         
               END-IF                                                           
            END-IF.                                                             
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       3500-PROCESS-K825.                                                       
      *****************************************************************         
                                                                                
                                                                                
           PERFORM 3000-READ-K825 THRU 3000-EXIT.                               
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       4600-READ-KB78.                                                          
      *****************************************************************         
            READ BP13KB78.                                                      
                                                                                
            IF WS-KB78-STATUS  =  00                                            
               ADD 1 TO WS-KB78-READ                                            
               MOVE 'Y'   TO WS-FOUND-KB78                                      
               MOVE KB78-NUM-EMAIL           TO DETL-EMAIL                      
               MOVE KB78-NUM-HHTYPE-DECLARE  TO DETL-DEC-HH                     
            ELSE                                                                
               IF WS-KB78-STATUS  =  23                                         
                  MOVE 'N'   TO WS-FOUND-KB78                                   
                  ADD 1 TO WS-NOTFD-CNT-KB78                                    
                  DISPLAY 'REC NOT FND IN KB78 SN   : ' KB78-KEY-FLD            
               ELSE                                                             
                  DISPLAY 'ERROR READING BP13KB78   : ' WS-KB78-STATUS          
                  MOVE     WS-KB78-STATUS      TO RETURN-CODE                   
                  PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                   
               END-IF                                                           
            END-IF.                                                             
                                                                                
       4600-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       4700-READ-KA10.                                                          
      *****************************************************************         
            READ BP13KA10.                                                      
                                                                                
            IF WS-KA10-STATUS  =  00                                            
               CONTINUE                                                         
            ELSE                                                                
               IF WS-KA10-STATUS  =  23                                         
                  DISPLAY 'REC NOT FND IN KA10 SN   : ' KA10-KEY-FLD            
               ELSE                                                             
                  DISPLAY 'ERROR READING BP13KA10   : ' WS-KA10-STATUS          
                  MOVE     WS-KA10-STATUS      TO RETURN-CODE                   
                  PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                   
               END-IF                                                           
            END-IF.                                                             
                                                                                
       4700-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       5000-PRINT-HEADING.                                                      
      *****************************************************************         
            IF F730-CDE-NT1 NOT = WS-PREV-NT1                                   
              MOVE F730-CDE-NT1     TO WS-PREV-NT1                              
              MOVE SPACES           TO BP13KA10-REC                             
              MOVE KB78-NUM-PROJECT TO KA10-NUM-PRJCT                           
              PERFORM 4700-READ-KA10   THRU 4700-EXIT                           
            END-IF                                                              
                                                                                
            MOVE KA10-NME-PRJCT        TO LF65-PROJ-NAME                        
                                                                                
                                                                                
            ADD 1                TO WS-PAGE-CNTR.                               
            MOVE WS-PAGE-CNTR    TO HDNG-PAGE-CNTR.                             
                                                                                
            WRITE BP13LF65-REC    FROM BLANK-LINE AFTER PAGE.                   
            WRITE BP13LF65-REC    FROM HDNG-1.                                  
            WRITE BP13LF65-REC    FROM HDNG-2.                                  
            WRITE BP13LF65-REC    FROM HDNG-3.                                  
            WRITE BP13LF65-REC    FROM BLANK-LINE.                              
            WRITE BP13LF65-REC    FROM HDNG-4.                                  
            WRITE BP13LF65-REC    FROM HDNG-5.                                  
            WRITE BP13LF65-REC    FROM BLANK-LINE.                              
            MOVE 8               TO WS-LINE-CNTR.                               
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       5100-PRINT-DETAILS.                                                      
      *****************************************************************         
                                                                                
            IF WS-LINE-CNTR > 55                                                
               MOVE 0  TO WS-LINE-CNTR                                          
               PERFORM 5000-PRINT-HEADING THRU 5000-EXIT                        
            ELSE                                                                
               IF F730-CDE-NT1 NOT =  WS-PREV-NT1                               
                 MOVE 0  TO WS-LINE-CNTR                                        
                 PERFORM 5000-PRINT-HEADING THRU 5000-EXIT                      
               END-IF                                                           
            END-IF.                                                             
                                                                                
            IF F730-NUM-REGN NOT EQUAL WS-REGN-PREV                             
               ADD 1 TO WS-SNO                                                  
               MOVE WS-SNO TO DETL-SNO                                          
               MOVE F730-NUM-REGN TO WS-REGN-PREV                               
                                     DETL-REGN                                  
               MOVE WS-NAME(WS-CTR)           TO DETL-NAME                      
               MOVE WS-PHONE(WS-CTR)          TO DETL-PHONE                     
               MOVE WS-NRIC(WS-CTR)           TO DETL-NRIC                      
               MOVE F730-CDE-HOUSEHOLD        TO DETL-DET-HH                    
               MOVE F730-CDE-BALLOT-HOUSEHOLD TO DETL-BAL-HH                    
            ELSE                                                                
               MOVE WS-NAME(WS-CTR)           TO DETL-NAME                      
               MOVE WS-PHONE(WS-CTR)          TO DETL-PHONE                     
               MOVE WS-NRIC(WS-CTR)           TO DETL-NRIC                      
            END-IF                                                              
                                                                                
                                                                                
            WRITE BP13LF65-REC             FROM DETL-1.                         
            ADD 1 TO WS-LINE-CNTR                                               
                     WS-CTR                                                     
                     WS-BP13LF65-WRITE.                                         
                                                                                
            MOVE SPACES TO DETL-1.                                              
                                                                                
       5100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       9000-CLOSE-ROUTINE.                                                      
      *****************************************************************         
                                                                                
            CLOSE BP13F730                                                      
                  BP13K825                                                      
                  BP13KB78                                                      
                  BP13KA10                                                      
                  BP13LF65                                                      
                  P13F730A.                                                     
                                                                                
            IF WS-K825-STATUS NOT = 00                                          
               DISPLAY 'ERROR CLOSING BP13K825 ... ' WS-K825-STATUS             
               MOVE WS-K825-STATUS TO RETURN-CODE                               
            END-IF.                                                             
                                                                                
            IF WS-KB78-STATUS NOT = 00                                          
               DISPLAY 'ERROR CLOSING BP13KB78 ... ' WS-KB78-STATUS             
               MOVE WS-KB78-STATUS TO RETURN-CODE                               
            END-IF.                                                             
                                                                                
            IF WS-KA10-STATUS NOT = 00                                          
               DISPLAY 'ERROR CLOSING BP13KA10 ... ' WS-KA10-STATUS             
               MOVE WS-KA10-STATUS TO RETURN-CODE                               
            END-IF.                                                             
                                                                                
            DISPLAY '                          '.                               
            DISPLAY '  PROGRAM : BP13CF65      '.                               
            DISPLAY 'BP13F730 RECORDS READ   : ' WS-F730-READ.                  
            DISPLAY 'BP13K825 RECORDS READ   : ' WS-K825-READ.                  
            DISPLAY 'BP13KB78 RECORDS READ   : ' WS-KB78-READ.                  
            DISPLAY 'BP13F730 RECORDS UPD    : ' WS-K730-UPD.                   
            DISPLAY 'HH UPDATED              : ' WS-HH-UPD.                     
            DISPLAY 'HH NO UPDATE            : ' WS-NO-UPD.                     
            DISPLAY 'BP13LF65 RECORDS WRITTEN: ' WS-BP13LF65-WRITE.             
            DISPLAY 'NUMBER OF EXCEPTIONS    : ' WS-EXCEPTION.                  
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *======================  END OF PROGRAM  ======================*          
