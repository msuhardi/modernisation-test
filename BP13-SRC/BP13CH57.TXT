       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CH57.                                                 
      *AUTHOR.        ZDD1.                                                     
      *DATE-WRITTEN.  12/07/2016.                                               
      * ========================================================== *            
      *             SYSTEM OF COMMITMENT  (BP13)                   *            
      * ========================================================== *            
      *                                                            *            
      *  DESCRIPTION :  TO GENERATE REPORT FOR THOSE CASES THAT    *            
      *                 EMAIL WAS NOT SENT                         *            
      *  INPUT FILE  :  BP13F491                                   *            
      *                 BP13K857                                   *            
      *              :  BP13LH57                                   *            
      * ---------------------------------------------------------- *            
      * CHG-NO.  BY   ON          DESCRIPTION                      *            
      * -------- ---  ----------  -----------                      *            
      * BP135986 ZDD1 13/07/2016  TO CHECK IF F491 IS EXISTING AND *            
      *                           EAMIL WAS SENT ALREADY.          *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F491  ASSIGN       TO BP13F491.                           
           SELECT BP13LH57  ASSIGN       TO BP13LH57.                           
                                                                                
           SELECT BP13K857  ASSIGN       TO BP13K857                            
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K857-KEY-FLD                      
                             FILE STATUS   IS WS-K857-STATUS.                   
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *---------------------------------------------------------------*         
       FD  BP13F491                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F491.                                                           
                                                                                
       FD   BP13LH57                                                            
            RECORD CONTAINS   132 CHARACTERS                                    
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
       01   PRINT-REC             PIC X(132).                                   
                                                                                
                                                                                
       FD   BP13K857                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K857.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-VARIABLES.                                                        
           05  WS-F491-EOF         PIC X(01)  VALUE SPACES.                     
               88 F491-END-OF-FILE   VALUE 'Y'.                                 
           05  WS-F491-READ-CNT    PIC 9(08)  VALUE ZEROES.                     
           05  WS-WRT-RPT1A        PIC 9(08)  VALUE ZEROES.                     
           05  WS-BYPASS           PIC 9(08)  VALUE ZEROES.                     
           05  WS-OTHER-PROJ       PIC 9(08)  VALUE ZEROES.                     
           05  WS-F50A-CNT         PIC 9(08)  VALUE ZEROES.                     
           05  WS-VAR              PIC X(01)  VALUE "'".                        
           05  WS-K857-NFND        PIC 9(08)  VALUE ZEROES.                     
           05  WS-K857-FND         PIC 9(08)  VALUE ZEROES.                     
           05  WS-K857-FOUND       PIC X(01)  VALUE SPACE.                      
           05  WS-K857-STATUS      PIC X(02)  VALUE SPACES.                     
                                                                                
       01  WS-DATE-TIME            PIC X(15)  VALUE SPACES.                     
                                                                                
       01  WS-CURR-DATE            PIC X(10)  VALUE SPACES.                     
       01  WS-CURR-TIME            PIC X(08)  VALUE SPACES.                     
       01  CNT-LINE-ON-PAGE       PIC 9(02) VALUE 66.                           
       01  CNT-PRT-CASES          PIC 9(06) VALUE ZEROES.                       
       01  CNT-PAGE               PIC 9(03) VALUE ZEROES.                       
                                                                                
       01  WS-EDT-CNT              PIC Z(7)9.                                   
                                                                                
       01  LH57-PR-HEAD-01.                                                     
           05  FILLER             PIC X(08) VALUE 'BP13LH57'.                   
           05  FILLER             PIC X(05) VALUE SPACES.                       
           05  FILLER             PIC X(08) VALUE 'HDBCAT 3'.                   
           05  FILLER             PIC X(24) VALUE SPACES.                       
           05  FILLER             PIC X(39) VALUE                               
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER             PIC X(17) VALUE SPACES.                       
           05  FILLER             PIC X(06) VALUE 'DATE: '.                     
           05  LH57-DATE          PIC X(10).                                    
           05  FILLER             PIC X(03) VALUE SPACES.                       
           05  FILLER             PIC X(06) VALUE 'PAGE :'.                     
           05  LH57-PAGE          PIC ZZ9 .                                     
                                                                                
       01  LH57-PR-HEAD-02.                                                     
           05  FILLER             PIC X(40) VALUE SPACES.                       
           05  FILLER             PIC X(47) VALUE                               
               '          LIST OF CASES OF UNSENT PCD EMAIL    '.               
           05  FILLER             PIC X(33) VALUE SPACES.                       
                                                                                
       01  LH57-PR-HEAD-03.                                                     
           05  FILLER             PIC X(03) VALUE SPACES.                       
           05  FILLER             PIC X(51) VALUE                               
               'S/NO               REGN NO              EMAIL ADDR '.           
           05  FILLER             PIC X(04) VALUE SPACES.                       
                                                                                
       01  LH57-PR-DETAILS.                                                     
           05  FILLER             PIC X(01) VALUE SPACES.                       
           05  LH57-SNO           PIC ZZZZZ9.                                   
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  FILLER             PIC X(11).                                    
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  LH57-REG-NO        PIC X(08).                                    
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  FILLER             PIC X(09).                                    
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  LH57-EMAIL         PIC X(50) VALUE SPACES.                       
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *---------------------------------------------------------------*         
       0000-CONTROL.                                                            
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES        THRU 1000-EXIT.                       
           PERFORM 2000-READ-BP13F491     THRU 2000-EXIT.                       
           PERFORM 3000-PROCESS-F491      THRU 3000-EXIT                        
                                 UNTIL F491-END-OF-FILE.                        
           PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN INPUT  BP13F491                                                 
                       BP13K857                                                 
              OUTPUT   BP13LH57.                                                
                                                                                
            IF WS-K857-STATUS NOT = 00 AND 97                                   
               DISPLAY 'BP13K857 - ERROR OPENING : ' WS-K857-STATUS             
               MOVE WS-K857-STATUS                   TO RETURN-CODE             
               PERFORM  9000-CLOSE-ROUTINE         THRU 9000-EXIT               
            END-IF.                                                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13F491.                                                      
      *---------------------------------------------------------------*         
           READ BP13F491 AT END                                                 
                          MOVE 'Y'  TO WS-F491-EOF                              
                         NOT AT END                                             
                          ADD  1    TO WS-F491-READ-CNT                         
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-PROCESS-F491.                                                       
      *---------------------------------------------------------------*         
                                                                                
           PERFORM 4000-READ-BP13K857   THRU 4000-EXIT.                         
                                                                                
           IF WS-K857-FOUND = 'Y'                                               
              ADD 1 TO WS-K857-FND                                              
           ELSE                                                                 
               ADD 1 TO WS-K857-NFND                                            
               IF CNT-LINE-ON-PAGE > 46                                         
                  MOVE 1 TO CNT-LINE-ON-PAGE                                    
                  PERFORM 4100-REPORT-HEADING THRU 4100-EXIT                    
               ELSE                                                             
                  ADD 1 TO CNT-LINE-ON-PAGE                                     
               END-IF                                                           
               MOVE SPACES                   TO LH57-PR-DETAILS                 
                                                                                
               ADD 1 TO CNT-PRT-CASES                                           
               MOVE CNT-PRT-CASES            TO LH57-SNO                        
               MOVE F491-NUM-REGN            TO LH57-REG-NO                     
               MOVE F491-NUM-EMAIL           TO LH57-EMAIL                      
               WRITE PRINT-REC FROM LH57-PR-DETAILS AFTER 1                     
           END-IF                                                               
                                                                                
           PERFORM 2000-READ-BP13F491      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-READ-BP13K857.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                                TO BP13K857-REC.          
           INITIALIZE                                    BP13K857-REC.          
                                                                                
           MOVE 'N'                                   TO WS-K857-FOUND.         
           MOVE F491-NUM-REGN       TO K857-KEY-FLD.                            
                                                                                
           READ BP13K857.                                                       
                                                                                
           EVALUATE WS-K857-STATUS                                              
               WHEN 00                                                          
                    IF K857-NUM-EMAIL-TAG-SENT = 'Y'                            
                      MOVE 'Y'                    TO WS-K857-FOUND              
                    END-IF                                                      
               WHEN 23                                                          
                    MOVE 'N'                    TO WS-K857-FOUND                
                    MOVE SPACES                       TO BP13K857-REC           
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K857 : ' WS-K857-STATUS          
                            ' KEY  = ' K857-KEY-FLD                             
                    PERFORM  9000-CLOSE-ROUTINE     THRU 9000-EXIT              
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4100-REPORT-HEADING.                                                     
      ******************************************************************        
                                                                                
            ADD 1           TO CNT-PAGE.                                        
            MOVE CNT-PAGE   TO LH57-PAGE.                                       
            MOVE SPACES     TO PRINT-REC.                                       
            WRITE PRINT-REC AFTER PAGE.                                         
            WRITE PRINT-REC FROM LH57-PR-HEAD-01 AFTER 2.                       
            WRITE PRINT-REC FROM LH57-PR-HEAD-02 AFTER 3.                       
            WRITE PRINT-REC FROM LH57-PR-HEAD-03 AFTER 3.                       
            MOVE ALL '-'    TO PRINT-REC.                                       
            WRITE PRINT-REC.                                                    
            MOVE SPACES     TO PRINT-REC.                                       
            WRITE PRINT-REC AFTER 1.                                            
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-ROUTINE.                                                      
      *---------------------------------------------------------------*         
           DISPLAY ' '.                                                         
           DISPLAY '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'.             
           DISPLAY '   PROGRAM : BP13CH57 ' .                                   
           DISPLAY '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'.             
           DISPLAY ' '.                                                         
           DISPLAY ' REC READ FROM BP13F491 - '   WS-F491-READ-CNT.             
           DISPLAY ' REC READ FROM BP13K857 - '   WS-K857-FND.                  
           DISPLAY ' REC NFND FROM BP13K857 - '   WS-K857-NFND.                 
           DISPLAY ' REC WRITTEN IN LH57    - '   CNT-PRT-CASES.                
           DISPLAY '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'.             
                                                                                
           CLOSE BP13F491                                                       
                 BP13K857                                                       
                 BP13LH57.                                                      
                                                                                
            IF WS-K857-STATUS NOT = 00 AND 97                                   
               DISPLAY 'BP13K857 - ERROR CLOSING : ' WS-K857-STATUS             
               MOVE WS-K857-STATUS                   TO RETURN-CODE             
            END-IF.                                                             
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
