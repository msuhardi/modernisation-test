       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C22V.                                                 
       AUTHOR.        NINO ANGELO GARILLO.                                      
      *DATE-WRITTEN.  JUL2005.                                                  
      *****************************************************************         
      * SYSTEM OF COMMITMENT  (BP13)                                  *         
      *****************************************************************         
      * OBJECTIVES:  TO UPDATE BP13K255 FROM BP13K254                 *         
      *---------------------------------------------------------------*         
      * CHG REQ#  DATE        BY    DESCRIPTION                       *         
      * --------  ----------  ----  --------------------------------- *         
      * BP132725  20/07/2005  GAN1  NEW PROGRAM.                      *         
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K254  ASSIGN       TO BP13K254                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS SEQUENTIAL                          
                            RECORD KEY   IS K254-KEY-FLD                        
                            FILE STATUS  IS WS-K254-STATUS.                     
                                                                                
           SELECT BP13K255  ASSIGN       TO BP13K255                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS RANDOM                              
                            RECORD KEY   IS K255-KEY-FLD                        
                            FILE STATUS  IS WS-K255-STATUS.                     
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13K254                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K254.                                                           
                                                                                
       FD   BP13K255                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K255.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01 WS-END-OF-FILE                   PIC X(01) VALUE 'N'.                 
       01 WS-K254-STATUS                   PIC 9(02) VALUE ZEROES.              
       01 WS-K255-STATUS                   PIC 9(02) VALUE ZEROES.              
       01 WS-K254-COUNT                    PIC 9(06) VALUE ZEROES.              
       01 WS-K254-FOUND                    PIC 9(06) VALUE ZEROES.              
       01 WS-K254-NOTFD                    PIC 9(06) VALUE ZEROES.              
       01 WS-K255-UPDTD                    PIC 9(06) VALUE ZEROES.              
       01 WS-K255-ADDED                    PIC 9(06) VALUE ZEROES.              
       01 WS-K254-DLETD                    PIC 9(06) VALUE ZEROES.              
       01 WS-OTHER-VARIABLES.                                                   
          05 WS-CUR-DATE.                                                       
             10 WS-CUR-CCYY                PIC X(04) VALUE SPACES.              
             10 WS-CUR-MM                  PIC X(02) VALUE SPACES.              
             10 WS-CUR-DD                  PIC X(02) VALUE SPACES.              
          05 WS-EDT-DATE.                                                       
             10 WS-EDT-DD                  PIC X(02).                           
             10 FILLER                     PIC X(01) VALUE '/'.                 
             10 WS-EDT-MM                  PIC X(02).                           
             10 FILLER                     PIC X(01) VALUE '/'.                 
             10 WS-EDT-CCYY                PIC X(04).                           
                                                                                
       PROCEDURE DIVISION.                                                      
      *--------------------------------*                                        
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 1000-INITIALIZATION    THRU 1000-EXIT.                       
           PERFORM 2000-PROCESS-RECORDS   THRU 2000-EXIT                        
             UNTIL WS-END-OF-FILE = 'Y'.                                        
           PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       1000-INITIALIZATION.                                                     
      *--------------------------------*                                        
                                                                                
           OPEN I-O BP13K254                                                    
                    BP13K255.                                                   
                                                                                
           IF WS-K254-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K254, OPEN ERROR, STATUS = ' WS-K254-STATUS          
              MOVE WS-K254-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES                                          
           END-IF.                                                              
                                                                                
           IF WS-K255-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K255, OPEN ERROR, STATUS = ' WS-K255-STATUS          
              MOVE WS-K255-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES                                          
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-DATE.                           
                                                                                
           MOVE WS-CUR-DD             TO WS-EDT-DD.                             
           MOVE WS-CUR-MM             TO WS-EDT-MM.                             
           MOVE WS-CUR-CCYY           TO WS-EDT-CCYY.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2000-PROCESS-RECORDS.                                                    
      *--------------------------------*                                        
                                                                                
           READ BP13K254  AT END                                                
                MOVE 'Y'   TO  WS-END-OF-FILE                                   
                GO TO 2000-EXIT.                                                
                                                                                
           IF K254-DTE-START-EXERCISE = WS-CUR-DATE                             
              ADD  1                    TO   WS-K254-COUNT                      
              PERFORM 3000-READ-K255    THRU 3000-EXIT                          
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3000-READ-K255.                                                          
      *--------------------------------*                                        
                                                                                
           MOVE K254-NUM-SELECTION TO K255-NUM-SELECTION.                       
           MOVE K254-NUM-ZONE-NT   TO K255-NUM-ZONE-NT.                         
           READ BP13K255.                                                       
                                                                                
           IF WS-K255-STATUS  =  00                                             
              ADD 1 TO WS-K254-FOUND                                            
              PERFORM 4000-UPDATE-K255        THRU 4000-EXIT                    
           ELSE                                                                 
              IF WS-K255-STATUS  =  23                                          
                 ADD 1 TO WS-K254-NOTFD                                         
                 PERFORM 5000-WRITE-K255      THRU 5000-EXIT                    
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K255 : ' WS-K255-STATUS             
                 MOVE     WS-K255-STATUS      TO RETURN-CODE                    
                 PERFORM  9000-CLOSE-FILES    THRU 9000-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       4000-UPDATE-K255.                                                        
      *--------------------------------*                                        
                                                                                
           MOVE  BP13K254-REC TO BP13K255-REC.                                  
           REWRITE BP13K255-REC.                                                
                                                                                
           IF WS-K255-STATUS NOT = '00'                                         
              DISPLAY 'ERROR UPDATING BP13K255 --> ' WS-K255-STATUS             
              DISPLAY 'SEL/NT: ' K254-NUM-SELECTION ' ' K254-NUM-ZONE-NT        
              MOVE WS-K255-STATUS      TO   RETURN-CODE                         
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           ELSE                                                                 
              ADD 1 TO WS-K255-UPDTD                                            
              PERFORM 6000-DELETE-K254 THRU 6000-EXIT                           
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       5000-WRITE-K255.                                                         
      *--------------------------------*                                        
                                                                                
           MOVE  BP13K254-REC TO BP13K255-REC.                                  
           WRITE BP13K255-REC.                                                  
                                                                                
           IF WS-K255-STATUS NOT = '00'                                         
              DISPLAY 'ERROR WRITING BP13K255 --> ' WS-K255-STATUS              
              DISPLAY 'SEL/NT: ' K254-NUM-SELECTION ' ' K254-NUM-ZONE-NT        
              MOVE WS-K255-STATUS        TO   RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           ELSE                                                                 
              ADD 1 TO WS-K255-ADDED                                            
              PERFORM 6000-DELETE-K254   THRU 6000-EXIT                         
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       6000-DELETE-K254.                                                        
      *--------------------------------*                                        
                                                                                
           DELETE BP13K254.                                                     
                                                                                
           IF WS-K254-STATUS NOT = '00'                                         
              DISPLAY 'ERROR DELETING POSTDATE FILE --> ' WS-K254-STATUS        
              DISPLAY 'SEL/NT: ' K254-NUM-SELECTION ' ' K254-NUM-ZONE-NT        
              MOVE WS-K254-STATUS        TO   RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           ADD 1 TO WS-K254-DLETD.                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       9000-CLOSE-FILES.                                                        
      *--------------------------------*                                        
                                                                                
           DISPLAY 'PROGRAM ID   : BP13C22V'.                                   
           DISPLAY 'CURRENT DATE : ' WS-EDT-DATE.                               
           DISPLAY SPACES.                                                      
           DISPLAY '*=============== COUNTERS ===============*'.                
           DISPLAY 'BP13K254 RECORDS READ    : ' WS-K254-COUNT.                 
           DISPLAY 'BP13K254 REC FOUND(K255) : ' WS-K254-FOUND.                 
           DISPLAY 'BP13K254 REC NOTFD(K255) : ' WS-K254-NOTFD.                 
           DISPLAY 'BP13K255 RECORDS UPDATED : ' WS-K255-UPDTD.                 
           DISPLAY 'BP13K255 RECORDS ADDED   : ' WS-K255-ADDED.                 
           DISPLAY 'BP13K254 RECORDS DELETED : ' WS-K254-DLETD.                 
                                                                                
           CLOSE BP13K254                                                       
                 BP13K255.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
