       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C91I.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  16/10/2013.                                               
      *===============================================================*         
      * SYSTEM OF COMMITMENT  (BP13)                                  *         
      *===============================================================*         
      *                                                               *         
      *  OBJECTIVE  :  DELETE UNTAGGED PARENT/CHILD UNITS IN BP13K120 *         
      *                FOR PARENT ONLY, WRITE UNTAGGED UNITS IN F100  *         
      *                                                               *         
      *  INPUT      :  BP13F120  UNITS TO BE DELETED IN BP13K120      *         
      *  I-O        :  BP13K120  BP13.K120.MGPSUNIT.CHILD             *         
      *  OUTPUT     :  BP13F100  BP13.F100.SLCTUNIT.CHILD             *         
      *                                                               *         
      *===============================================================*         
      * CHG-REF#  BY  DDMMCCYY DESCRIPTION                            *         
      * -------- ---- -------- -------------------------------------- *         
      * BP135140 SMR2 16102013 NEW PROGRAM                            *         
      *===============================================================*         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F120  ASSIGN       TO BP13F120.                           
           SELECT BP13K120  ASSIGN       TO BP13K120                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS RANDOM                              
                            RECORD KEY   IS K120-KEY-FLD                        
                            FILE STATUS  IS WS-K120-STATUS.                     
           SELECT BP13F100  ASSIGN       TO BP13F100.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F120                                                            
            RECORDING MODE   IS F                                               
            RECORD CONTAINS 200 CHARACTERS.                                     
       01   BP13F120-REC.                                                       
            05 F120-KEY-FLD                PIC X(55).                           
            05 FILLER                      PIC X(145).                          
                                                                                
       FD   BP13K120                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K120.                                                           
                                                                                
       FD   BP13F100                                                            
            RECORDING MODE   IS F                                               
            RECORD CONTAINS 800 CHARACTERS.                                     
       COPY BP13F100.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K120-STATUS              PIC 9(02)   VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F120-READ                PIC 9(08)   VALUE ZEROES.            
           05  WS-K120-FND                 PIC 9(08)   VALUE ZEROES.            
           05  WS-K120-NTFND               PIC 9(08)   VALUE ZEROES.            
           05  WS-K120-DELETE              PIC 9(08)   VALUE ZEROES.            
           05  WS-F100-WRITE               PIC 9(08)   VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F120-EOF                 PIC X(01)   VALUE 'N'.               
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-EDT-DATE                 PIC X(10)   VALUE SPACES.            
           05  WS-CUR-DATE                 PIC X(08)   VALUE SPACES.            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-EDIT1                    PIC Z(07)9.                          
           05  WS-PARM-NUM-CDE             PIC X(01).                           
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *---------------------------------------------------------------*         
       0000-MAIN-ROUTINE.                                                       
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F120       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-RECS        THRU 3000-EXIT                      
             UNTIL WS-F120-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN INPUT  BP13F120                                                 
                I-O    BP13K120                                                 
                OUTPUT BP13F100.                                                
                                                                                
           IF WS-K120-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPEN ERROR, BP13K120 STATUS ' WS-K120-STATUS             
              MOVE WS-K120-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE       TO WS-CUR-DATE.                     
           STRING WS-CUR-DATE(7:2), '/',                                        
                  WS-CUR-DATE(5:2), '/',                                        
                  WS-CUR-DATE(1:4)  DELIMITED BY SIZE                           
                  INTO WS-EDT-DATE.                                             
                                                                                
      *** ACCEPT VALUE FROM JCL ***                                             
           ACCEPT WS-PARM-NUM-CDE.                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13F120.                                                      
      *---------------------------------------------------------------*         
           READ BP13F120 AT END                                                 
                MOVE 'Y'                    TO WS-F120-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F120-READ.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-PROCESS-RECS.                                                       
      *---------------------------------------------------------------*         
           PERFORM 4000-READ-BP13K120       THRU 4000-EXIT.                     
           PERFORM 2000-READ-BP13F120       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-READ-BP13K120.                                                      
      *---------------------------------------------------------------*         
           MOVE SPACES                      TO BP13K120-REC.                    
           INITIALIZE                          BP13K120-REC.                    
                                                                                
           MOVE F120-KEY-FLD                TO K120-KEY-FLD.                    
                                                                                
           READ BP13K120.                                                       
                                                                                
           EVALUATE WS-K120-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                         TO WS-K120-FND                      
              IF WS-PARM-NUM-CDE = 'P'                                          
                 PERFORM 5000-WRITE-BP13F100   THRU 5000-EXIT                   
              END-IF                                                            
              PERFORM 6000-DELETE-BP13K120  THRU 6000-EXIT                      
                                                                                
           WHEN 23                                                              
              ADD 1                         TO WS-K120-NTFND                    
              DISPLAY 'RECORD NOT FOUND IN BP13K120 : ' K120-KEY-FLD            
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K120 : ' WS-K120-STATUS                
              MOVE WS-K120-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       5000-WRITE-BP13F100.                                                     
      *---------------------------------------------------------------*         
           MOVE SPACES                      TO BP13F100-REC.                    
           MOVE K120-NUM-HDB-REF            TO F100-NUM-HDB-REF.                
           MOVE K120-NUM-RANDOM             TO F100-NUM-RANDOM.                 
           MOVE K120-NUM-PRIO               TO F100-NUM-PRIO.                   
           MOVE K120-NUM-FT-BALLOT          TO F100-NUM-FT-BALLOT.              
           MOVE K120-NUM-PRIO-UPDATE        TO F100-NUM-PRIO-UPDATE.            
           MOVE K120-NUM-ESTATE             TO F100-NUM-ESTATE.                 
           MOVE K120-NUM-NGHBRD             TO F100-NUM-NGHBRD.                 
           MOVE K120-NUM-CNTRCT             TO F100-NUM-CNTRCT.                 
           MOVE K120-NUM-BLOCK              TO F100-NUM-BLOCK.                  
           MOVE K120-NUM-TYP-ACTUSE         TO F100-NUM-TYP-ACTUSE.             
           MOVE K120-NUM-MODL-DESG          TO F100-NUM-MODL-DESG.              
           MOVE K120-NME-STREET             TO F100-NME-STREET.                 
           MOVE K120-NUM-UNIT               TO F100-NUM-UNIT.                   
           MOVE K120-NUM-WARD               TO F100-NUM-WARD.                   
           MOVE K120-NUM-NT                 TO F100-NUM-NT.                     
           MOVE K120-NUM-ALLO-CAT           TO F100-NUM-ALLO-CAT.               
           MOVE K120-DTE-BALLOT             TO F100-DTE-BALLOT.                 
           MOVE K120-NUM-PARENT-FT          TO F100-NUM-PARENT-FT.              
           MOVE K120-NUM-UNIT-TAG           TO F100-NUM-MGPS.                   
           WRITE BP13F100-REC.                                                  
           ADD 1                            TO WS-F100-WRITE.                   
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       6000-DELETE-BP13K120.                                                    
      *---------------------------------------------------------------*         
           DELETE BP13K120.                                                     
                                                                                
           EVALUATE WS-K120-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                         TO WS-K120-DELETE                   
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR DELETING BP13K120. STATUS IS - '                   
                      WS-K120-STATUS                                            
              MOVE WS-K120-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-ROUTINE.                                                      
      *---------------------------------------------------------------*         
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY '      PROGRAM : BP13C91I       ' WS-EDT-DATE.               
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
           MOVE WS-F120-READ                TO WS-EDIT1.                        
           DISPLAY 'NO OF RECS READ FROM BP13F120    : ' WS-EDIT1.              
           MOVE WS-K120-FND                 TO WS-EDIT1.                        
           DISPLAY 'NO OF RECS FOUND IN BP13K120     : ' WS-EDIT1.              
           MOVE WS-K120-NTFND               TO WS-EDIT1.                        
           DISPLAY 'NO OF RECS NOT FOUND IN BP13K120 : ' WS-EDIT1.              
           MOVE WS-F100-WRITE               TO WS-EDIT1.                        
           DISPLAY 'NO OF RECS WRITTEN TO BP13F100   : ' WS-EDIT1.              
           MOVE WS-K120-DELETE              TO WS-EDIT1.                        
           DISPLAY 'NO OF RECS DELETED ON BP13K120   : ' WS-EDIT1.              
           DISPLAY '*---------------------------------------------*'.           
                                                                                
           CLOSE   BP13F120                                                     
                   BP13K120                                                     
                   BP13F100.                                                    
                                                                                
           IF WS-K120-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSE ERROR, BP13K120 STATUS ' WS-K120-STATUS            
              MOVE WS-K120-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
