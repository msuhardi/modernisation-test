       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C716.                                                 
      *DATE-WRITTEN.  20/02/2003.                                               
      *AUTHOR.        SURESH NARAYANAN.                                         
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE    :  1. PRINT CANCELLATION LETTER.              *            
      *                    BP13Q5 - ETHNIC FULL                    *            
      *                    BP13Q6 - FLAT FULL                      *            
      *                 2. UPDATE F705 AND K595 WITH 'NU' OR 'NE'  *            
      *                    ELIG-TAG AND UPDATE STATUS TO 'C'       *            
      *                                                            *            
      * INPUT FILES  :  1.  BP13F705                               *            
      *                 2.  BP13K703                               *            
      *                 3.  BP13K825                               *            
      *                 4.  BP13K060                               *            
      *                 5.  BP13K830                               *            
      *                 6.  BP13K595                               *            
      *                                                            *            
      * OUTPUT LISTS :  1.  BP13F705                               *            
      *                 2.  P13P716A                               *            
      *                 3.  P13P716B                               *            
      *                                                            *            
      * ========================================================== *            
      * CHG REF  DATE     OIC   DESCRIPTION                        *            
      * -------- -------- ---   -----------                        *            
      * BP133092 20070703 IL5   NEW PROGRAM                        *            
      * BP133132 20070825 IL5   CHANGE PRINT LAYOUT FOR BP13Q6     *            
      * BP134681 20120924 IMC1  CATER TO '91' K060-SERIAL-NO       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F705  ASSIGN       TO BP13F705.                           
                                                                                
           SELECT BP13K703  ASSIGN       TO BP13K703                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K703-KEY-FLD                        
                            FILE STATUS  IS BP13K703-STATUS.                    
                                                                                
           SELECT BP13K825  ASSIGN       TO BP13K825                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K825-KEY-FLD                        
                            FILE STATUS  IS BP13K825-STATUS.                    
                                                                                
           SELECT BP13K830  ASSIGN       TO BP13K830                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K830-KEY-FLD                        
                            FILE STATUS  IS BP13K830-STATUS.                    
                                                                                
           SELECT BP13K060  ASSIGN       TO BP13K060                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K060-KEY-FLD                        
                            FILE STATUS  IS BP13K060-STATUS.                    
                                                                                
           SELECT BP13K595  ASSIGN       TO BP13K595                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K595-KEY-FLD                        
                            FILE STATUS  IS BP13K595-STATUS.                    
                                                                                
           SELECT P13P716A   ASSIGN      TO P13P716A.                           
                                                                                
           SELECT P13P716B   ASSIGN      TO P13P716B.                           
           SELECT P13L716A   ASSIGN      TO P13L716A.                           
           SELECT P13L716B   ASSIGN      TO P13L716B.                           
                                                                                
                                                                                
       DATA DIVISION.                                                           
      *-------------------------------------------------------------            
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD   BP13F705                                                            
            RECORDING MODE  F                                                   
            LABEL RECORDS   ARE STANDARD                                        
            BLOCK CONTAINS  0   RECORDS                                         
            RECORD CONTAINS 500  CHARACTERS.                                    
       COPY BP13F705.                                                           
                                                                                
       FD   BP13K703                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K703.                                                           
                                                                                
       FD   BP13K825                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K825.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K830.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS 25 CHARACTERS.                                      
       COPY BP13K060.                                                           
                                                                                
       FD   BP13K595                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K595.                                                           
                                                                                
       FD  P13P716A                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 90  CHARACTERS.                                      
       01  P716A-PRTREC                     PIC X(90).                          
                                                                                
       FD  P13P716B                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 90  CHARACTERS.                                      
       01  P716B-PRTREC                     PIC X(90).                          
                                                                                
       FD  P13L716A                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  L716A-PRTREC                     PIC X(132).                         
                                                                                
       FD  P13L716B                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  L716B-PRTREC                     PIC X(132).                         
                                                                                
      *-------------------------------------------------------------            
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-STATUS.                                                           
           05  BP13K703-STATUS           PIC 99     VALUE 00.                   
           05  BP13K825-STATUS           PIC 99     VALUE 00.                   
           05  BP13K830-STATUS           PIC 99     VALUE 00.                   
           05  BP13K060-STATUS           PIC 99     VALUE 00.                   
           05  BP13K595-STATUS           PIC 99     VALUE 00.                   
           05  WS-BP13F705-EOF           PIC X      VALUE 'N'.                  
                                                                                
       01  WS-TEMP.                                                             
           05  WS-DATE                   PIC X(8)   VALUE SPACES.               
           05  WS-SYS-DATE               PIC X(10)  VALUE SPACES.               
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-CURR-DATE              PIC 9(8)   VALUE ZERO.                 
           05  WS-LINE-CNT               PIC 9(2)   VALUE 70.                   
           05  WS-LINE-CNT1              PIC 9(2)   VALUE 70.                   
           05  WS-PAGE-CNT               PIC 9(4)   VALUE ZEROS.                
           05  WS-PAGE-CNT1              PIC 9(4)   VALUE ZEROS.                
           05  WS-SNO                    PIC 9(7)   VALUE ZEROS.                
           05  WS-SNO1                   PIC 9(7)   VALUE ZEROS.                
           05  WS-K595-REWRITE           PIC 9(7)   VALUE ZEROS.                
           05  WS-LTR-PRINTED            PIC 9(7)   VALUE ZEROS.                
           05  K825-FOUND                PIC X      VALUE 'N'.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F705-READ              PIC 9(8)   VALUE ZEROES.               
           05  WS-F705-WROTE             PIC 9(8)   VALUE ZEROES.               
           05  WS-K703-READ              PIC 9(8)   VALUE ZEROES.               
           05  WS-K825-READ              PIC 9(8)   VALUE ZEROS.                
           05  WS-K830-READ              PIC 9(8)   VALUE ZEROS.                
           05  WS-K825-NOTFD             PIC 9(8)   VALUE ZEROS.                
           05  WS-K830-NOTFD             PIC 9(8)   VALUE ZEROS.                
                                                                                
      *-----------------------------------------------------------*             
      *    SEND DJDE TO CHANGE FORM                               *             
      *-----------------------------------------------------------*             
       01  DJDE-LINE-FORM-Q6             PIC X(90) VALUE                        
           '$DJDE$ FORMS=BP13Q6,DUPLEX=NO,BOF=66,END;'.                         
                                                                                
       01  DJDE-LINE-FORM-Q5             PIC X(90) VALUE                        
           '$DJDE$ FORMS=BP13Q5,DUPLEX=NO,BOF=66,END;'.                         
                                                                                
      *-----------------------------------------------------------*             
      *    REPORT LAYOUT - CANCEL LETTER                          *             
      *-----------------------------------------------------------*             
       01  P716-LINE-1.                                                         
           05  FILLER                    PIC X(28)  VALUE SPACES.               
           05  P716-REF-NO               PIC X(08)  VALUE SPACES.               
                                                                                
       01  P716-LINE-2.                                                         
           05  FILLER                    PIC X(28)  VALUE SPACES.               
           05  P716-DATE                 PIC X(10)  VALUE SPACES.               
                                                                                
       01  P716-LINE-3.                                                         
           05  FILLER                    PIC X(12)  VALUE SPACES.               
           05  P716-APPL-NAME            PIC X(66)  VALUE SPACES.               
                                                                                
       01  P716-LINE-4.                                                         
           05  FILLER                    PIC X(12)  VALUE SPACES.               
           05  P716-COAPPL-NAME          PIC X(66)  VALUE SPACES.               
                                                                                
       01  P716-LINE-5.                                                         
           05 FILLER                     PIC X(12)  VALUE SPACES.               
           05 P716-NUM-BLOCK             PIC X(16)  VALUE SPACES.               
           05 P716-HOUSE-LEVEL           PIC X(11)  VALUE SPACES.               
                                                                                
       01  P716-LINE-6.                                                         
           05 FILLER                     PIC X(12)  VALUE SPACES.               
           05 P716-STREET-NAME           PIC X(65)  VALUE SPACES.               
                                                                                
       01  P716-LINE-7.                                                         
           05 FILLER                     PIC X(12)  VALUE SPACES.               
           05 P716-NUM-POSTAL            PIC X(17)  VALUE SPACES.               
                                                                                
       01  P716-LINE-8.                                                         
           05 FILLER                     PIC X(10)  VALUE SPACES.               
           05 P716-BAR-CODE              PIC X(9)   VALUE SPACES.               
                                                                                
       01  P716-LINE-9.                                                         
           05  FILLER                    PIC X(18)  VALUE SPACES.               
           05  P716-FLAT-TYPE            PIC X(2)   VALUE SPACES.               
           05  FILLER                    PIC X(18)  VALUE SPACES.               
           05  P716-CDE-NT               PIC X(15)  VALUE SPACES.               
                                                                                
       01  P716-LINE-10.                                                        
           05  FILLER                    PIC X(21)  VALUE SPACES.               
           05  P716-BALLOT-QTR           PIC X(14)  VALUE SPACES.               
                                                                                
       01  P716-LINE-11.                                                        
           05  FILLER                    PIC X(47)  VALUE SPACES.               
           05  P716-FLAT-TYPE2           PIC X(2)   VALUE SPACES.               
           05  FILLER                    PIC X(13)  VALUE SPACES.               
           05  P716-CDE-NT2              PIC X(15)  VALUE SPACES.               
                                                                                
       01  P716-LINE-12.                                                        
           05  FILLER                    PIC X(13)  VALUE SPACES.               
           05  P716-BALLOT-QTR2          PIC X(14)  VALUE SPACES.               
                                                                                
       01  P716-LINE-13.                                                        
           05  FILLER                    PIC X(57)  VALUE SPACES.               
           05  P716-DTE-REPLY            PIC X(11)  VALUE SPACES.               
                                                                                
       01  P716-LINE-14.                                                        
           05  FILLER                    PIC X(68)  VALUE SPACES.               
           05  P716-DTE-REPLY2           PIC X(11)  VALUE SPACES.               
                                                                                
       01  P716-LINE-15.                                                        
           05  FILLER                    PIC X(78)  VALUE SPACES.               
           05  P716-BALLOT-QTR3          PIC X(12)  VALUE SPACES.               
                                                                                
       01  P716-LINE-16.                                                        
           05  FILLER                    PIC X(49)  VALUE SPACES.               
           05  P716-NEXT-SELECTION       PIC X(25)  VALUE SPACES.               
                                                                                
      *-------------------------------------------------------------            
      * REPORT LAYOUT - CONTROL LISTING FOR UNIT FULL                           
      *-------------------------------------------------------------            
       01  L716A-HEADER1.                                                       
           05 FILLER                    PIC X(14)  VALUE 'P13L716A'.            
           05 FILLER                    PIC X(8)   VALUE 'HDB3'.                
           05 FILLER                    PIC X(33)  VALUE SPACES.                
           05 FILLER                    PIC X(20)  VALUE                        
              'SYSTEM OF COMMITMENT'.                                           
           05 FILLER                    PIC X(38)  VALUE SPACES.                
           05 FILLER                    PIC X(7)   VALUE 'DATE : '.             
           05 L716A-DATE                PIC X(10)  VALUE SPACES.                
                                                                                
       01  L716A-HEADER2.                                                       
           05 FILLER                    PIC X(40)  VALUE SPACES.                
           05 FILLER                    PIC X(50)  VALUE                        
              'CONTROL LIST OF LETTER PRINTED FOR QUOTA FULL '.                 
           05 FILLER                    PIC X(23)  VALUE SPACES.                
           05 FILLER                    PIC X(7)   VALUE 'PAGE : '.             
           05 L716A-PAGE                PIC ZZZ9   VALUE ZEROS.                 
                                                                                
       01  L716A-HEADER3.                                                       
           05 FILLER                     PIC X(50)  VALUE                       
              'SNO         REF     ALLOC  CDE           FLAT  '.                
           05 FILLER                     PIC X(45)  VALUE                       
              '      NRIC/NAME 1 - 2'.                                          
                                                                                
       01  L716A-HEADER4.                                                       
           05 FILLER                     PIC X(50)  VALUE                       
              '            NO.     MODE   NT            TYPE  '.                
                                                                                
       01  L716A-HEADER5.                                                       
           05 FILLER                     PIC X(130) VALUE ALL '-'.              
                                                                                
       01  L716A-DETAIL.                                                        
           05 L716A-SNO                  PIC Z(6)9.                             
           05 FILLER                     PIC X(2)  VALUE SPACES.                
           05 L716A-NUM-REF              PIC X(8)  VALUE SPACES.                
           05 FILLER                     PIC X(2)  VALUE SPACES.                
           05 L716A-ALLOC-MODE           PIC X(5)  VALUE SPACES.                
           05 FILLER                     PIC X(4)  VALUE SPACES.                
           05 L716A-CDE-NT               PIC X(15) VALUE SPACES.                
           05 L716A-FLAT-TYPE            PIC X(2)  VALUE SPACES.                
           05 FILLER                     PIC X(5)  VALUE SPACES.                
           05 L716A-NUM-NRIC1            PIC X(9)  VALUE SPACES.                
           05 FILLER                     PIC X(3)  VALUE SPACES.                
           05 L716A-APPL-NAME            PIC X(60) VALUE SPACES.                
           05 FILLER                     PIC X(10) VALUE SPACES.                
                                                                                
       01  L716A-DETAIL2.                                                       
           05 FILLER                     PIC X(50) VALUE SPACES.                
           05 L716A-NUM-NRIC2            PIC X(9)  VALUE SPACES.                
           05 FILLER                     PIC X(3)  VALUE SPACES.                
           05 L716A-COAPPL-NAME          PIC X(60) VALUE SPACES.                
                                                                                
       01  L716A-DETAIL3.                                                       
           05 FILLER                     PIC X(19) VALUE SPACES.                
           05 L716A-DETAIL-BLK           PIC X(11) VALUE SPACES.                
           05 L716A-HOUSE-LEVEL          PIC X(11) VALUE SPACES.                
           05 L716A-STREET-NAME          PIC X(65) VALUE SPACES.                
           05 L716A-NUM-POSTAL           PIC X(17) VALUE SPACES.                
                                                                                
      *-------------------------------------------------------------            
      * REPORT LAYOUT - CONTROL LISTING FOR ETHNIC FULL                         
      *-------------------------------------------------------------            
       01  L716B-HEADER1.                                                       
           05 FILLER                    PIC X(14)  VALUE 'P13L716B'.            
           05 FILLER                    PIC X(8)   VALUE 'HDB3'.                
           05 FILLER                    PIC X(33)  VALUE SPACES.                
           05 FILLER                    PIC X(20)  VALUE                        
              'SYSTEM OF COMMITMENT'.                                           
           05 FILLER                    PIC X(38)  VALUE SPACES.                
           05 FILLER                    PIC X(7)   VALUE 'DATE : '.             
           05 L716B-DATE                PIC X(10)  VALUE SPACES.                
                                                                                
       01  L716B-HEADER2.                                                       
           05 FILLER                    PIC X(40)  VALUE SPACES.                
           05 FILLER                    PIC X(50)  VALUE                        
              'CONTROL LIST OF LETTER PRINTED FOR ETHNIC FULL'.                 
           05 FILLER                    PIC X(23)  VALUE SPACES.                
           05 FILLER                    PIC X(7)   VALUE 'PAGE : '.             
           05 L716B-PAGE                PIC ZZZ9   VALUE ZEROS.                 
                                                                                
       01  L716B-HEADER3.                                                       
           05 FILLER                     PIC X(50)  VALUE                       
              'SNO         REF    ALLOC    CDE          FLAT  '.                
           05 FILLER                     PIC X(45)  VALUE                       
              '      NRIC/NAME 1 - 2'.                                          
                                                                                
       01  L716B-HEADER4.                                                       
           05 FILLER                     PIC X(50)  VALUE                       
              '            NO.    MODE     NT           TYPE  '.                
                                                                                
       01  L716B-HEADER5.                                                       
           05 FILLER                     PIC X(130) VALUE ALL '-'.              
                                                                                
       01  L716B-DETAIL.                                                        
           05 L716B-SNO                  PIC Z(6)9.                             
           05 FILLER                     PIC X(2)  VALUE SPACES.                
           05 L716B-NUM-REF              PIC X(8)  VALUE SPACES.                
           05 FILLER                     PIC X(2)  VALUE SPACES.                
           05 L716B-ALLOC-MODE           PIC X(5)  VALUE SPACES.                
           05 FILLER                     PIC X(4)  VALUE SPACES.                
           05 L716B-CDE-NT               PIC X(15) VALUE SPACES.                
           05 L716B-FLAT-TYPE            PIC X(2)  VALUE SPACES.                
           05 FILLER                     PIC X(5)  VALUE SPACES.                
           05 L716B-NUM-NRIC1            PIC X(9)  VALUE SPACES.                
           05 FILLER                     PIC X(3)  VALUE SPACES.                
           05 L716B-APPL-NAME            PIC X(60) VALUE SPACES.                
           05 FILLER                     PIC X(10) VALUE SPACES.                
                                                                                
       01  L716B-DETAIL2.                                                       
           05 FILLER                     PIC X(50) VALUE SPACES.                
           05 L716B-NUM-NRIC2            PIC X(9)  VALUE SPACES.                
           05 FILLER                     PIC X(3)  VALUE SPACES.                
           05 L716B-COAPPL-NAME          PIC X(60) VALUE SPACES.                
                                                                                
       01  L716B-DETAIL3.                                                       
           05 FILLER                     PIC X(19) VALUE SPACES.                
           05 L716B-DETAIL-BLK           PIC X(11) VALUE SPACES.                
           05 L716B-HOUSE-LEVEL          PIC X(11) VALUE SPACES.                
           05 L716B-STREET-NAME          PIC X(65) VALUE SPACES.                
           05 L716B-NUM-POSTAL           PIC X(17) VALUE SPACES.                
                                                                                
      *-----------------------------------------------------------*             
      *  LINKAGE FOR AV02C015  *                                                
      *-----------------------------------------------------------*             
        01  WS-LINK-AV02C015.                                                   
            05  AV02C015-IN-POSTCODE         PIC X(6).                          
            05  AV02C015-OUT-POSTCODE-CHKSUM PIC X(9).                          
                                                                                
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
            PERFORM 1000-OPEN-ROUTINE         THRU  1000-EXIT.                  
            PERFORM 2000-READ-BP13F705        THRU  2000-EXIT.                  
            PERFORM 3000-PROCESS-PARA         THRU  3000-EXIT                   
                         UNTIL WS-BP13F705-EOF = 'Y'.                           
            PERFORM 9000-CLOSE-ROUTINE        THRU  9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
            OPEN INPUT  BP13K703                                                
                        BP13K825                                                
                        BP13K830                                                
                        BP13K060                                                
                 I-O    BP13F705                                                
                        BP13K595                                                
                 OUTPUT P13P716A                                                
                        P13P716B                                                
                        P13L716A                                                
                        P13L716B.                                               
                                                                                
           IF BP13K703-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP13K703-STATUS ' BP13K703-STATUS         
              MOVE     BP13K703-STATUS TO RETURN-CODE                           
              PERFORM  9000-CLOSE-ROUTINE.                                      
                                                                                
           IF BP13K825-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP13K825-STATUS ' BP13K825-STATUS         
              MOVE     BP13K825-STATUS TO RETURN-CODE                           
              PERFORM  9000-CLOSE-ROUTINE.                                      
                                                                                
           IF BP13K830-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP13K830-STATUS ' BP13K830-STATUS         
              MOVE     BP13K830-STATUS TO RETURN-CODE                           
              PERFORM  9000-CLOSE-ROUTINE.                                      
                                                                                
           IF BP13K060-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP13K060-STATUS ' BP13K060-STATUS         
              MOVE     BP13K060-STATUS TO RETURN-CODE                           
              PERFORM  9000-CLOSE-ROUTINE.                                      
                                                                                
           IF BP13K595-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP13K595-STATUS ' BP13K595-STATUS         
              MOVE     BP13K595-STATUS TO RETURN-CODE                           
              PERFORM  9000-CLOSE-ROUTINE.                                      
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
              DELIMITED BY SIZE INTO WS-SYS-DATE.                               
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F705.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F705                                                        
                AT END MOVE 'Y' TO WS-BP13F705-EOF                              
                GO TO  2000-EXIT.                                               
           ADD    1   TO   WS-F705-READ.                                        
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-PARA.                                                       
      *-------------------------------------------------------------            
                                                                                
            MOVE  SPACES             TO   K703-KEY-FLD.                         
                                                                                
            MOVE  'ENDRESER'         TO   K703-NUM-SOCLIST                      
            MOVE  F705-CDE-NT1       TO   K703-NUM-NT                           
            MOVE  F705-CDE-FLAT-TYPE TO   K703-NUM-FLAT                         
                                                                                
            READ  BP13K703.                                                     
                                                                                
            IF BP13K703-STATUS = 00                                             
               ADD        1          TO   WS-K703-READ                          
                                                                                
                                                                                
               IF (F705-DTE-BALLOT      = K703-DTE-BALLOT-ORIGIN)               
                   PERFORM 4000-CHECK-CONDITION  THRU  4000-EXIT                
                   UNTIL (WS-BP13F705-EOF  = 'Y')                  OR           
                         (F705-CDE-NT1       NOT = K703-NUM-NT )   OR           
                         (F705-CDE-FLAT-TYPE NOT = K703-NUM-FLAT ) OR           
                         (F705-DTE-BALLOT  NOT = K703-DTE-BALLOT-ORIGIN)        
               ELSE                                                             
                  PERFORM 2000-READ-BP13F705  THRU  2000-EXIT                   
               END-IF                                                           
                                                                                
            ELSE                                                                
            IF BP13K703-STATUS = 23                                             
               DISPLAY 'REC NOTFND IN BP13K703 , FILE STATUS : '                
               BP13K703-STATUS ', K703-KEY-FLD IS ' K703-KEY-FLD                
               PERFORM   2000-READ-BP13F705   THRU   2000-EXIT                  
            ELSE                                                                
              DISPLAY 'ERROR READING BP13K703, STAT : ' BP13K703-STATUS         
                      ', K703-KEY-FLD IS ' K703-KEY-FLD                         
              MOVE     BP13K703-STATUS TO RETURN-CODE                           
              PERFORM  9000-CLOSE-ROUTINE.                                      
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       4000-CHECK-CONDITION.                                                    
      *-------------------------------------------------------------            
            IF (F705-DTE-BALLOT     = K703-DTE-BALLOT-ORIGIN) AND               
               ( F705-CDE-NT1       = K703-NUM-NT   )  AND                      
               ( F705-CDE-FLAT-TYPE = K703-NUM-FLAT )                           
                                                                                
               PERFORM 5000-MOVE-TO-BP13F705  THRU  5000-EXIT                   
               PERFORM 8500-READ-K595         THRU  8500-EXIT                   
            END-IF                                                              
                                                                                
            PERFORM     2000-READ-BP13F705   THRU   2000-EXIT.                  
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       5000-MOVE-TO-BP13F705.                                                   
      *-------------------------------------------------------------            
                                                                                
           PERFORM 6000-PROCESS        THRU  6000-EXIT.                         
                                                                                
           MOVE 'C'                    TO    F705-CDE-STATUS.                   
           MOVE K703-CDE-ELIG-TAG      TO    F705-CDE-ELIG-TAG.                 
           PERFORM 5500-WRITE-BP13F705 THRU  5500-EXIT.                         
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       5500-WRITE-BP13F705.                                                     
      *-------------------------------------------------------------            
                                                                                
            REWRITE BP13F705-REC.                                               
            ADD   1    TO  WS-F705-WROTE.                                       
                                                                                
       5500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       6000-PROCESS.                                                            
      *-------------------------------------------------------------            
                                                                                
      ****INITIALIZE PRINTING VARIABLES                                         
           MOVE SPACES TO P716-LINE-1                                           
                          P716-LINE-2                                           
                          P716-LINE-3                                           
                          P716-LINE-4                                           
                          P716-LINE-5                                           
                          P716-LINE-6                                           
                          P716-LINE-7                                           
                          P716-LINE-8                                           
                          P716-LINE-9                                           
                          P716-LINE-10                                          
                          P716-LINE-11                                          
                          P716-LINE-12                                          
                          P716-LINE-13                                          
                          P716-LINE-14                                          
                          P716-LINE-15                                          
                          P716-LINE-16                                          
                          L716A-DETAIL                                          
                          L716A-DETAIL2                                         
                          L716A-DETAIL3                                         
                          L716B-DETAIL                                          
                          L716B-DETAIL2                                         
                          L716B-DETAIL3.                                        
                                                                                
      **** GET CORR ADDRESS                                                     
           PERFORM 6100-READ-BP13K830        THRU  6100-EXIT.                   
                                                                                
      **** GET APPLICANT NAME                                                   
           MOVE 'N' TO K825-FOUND                                               
           IF F705-NUM-NRIC1 NOT = LOW-VALUES AND SPACES                        
              MOVE SPACES                    TO   K825-KEY-FLD                  
              MOVE F705-NUM-REF              TO   K825-NUM-REF                  
              MOVE F705-NUM-NRIC1            TO   K825-NUM-NRIC                 
                                                  L716A-NUM-NRIC1               
                                                  L716B-NUM-NRIC1               
              PERFORM 6400-READ-BP13K825     THRU 6400-EXIT                     
           END-IF.                                                              
                                                                                
           IF K825-FOUND = 'Y'                                                  
              MOVE K825-NME-OCCP             TO P716-APPL-NAME                  
                                                L716A-APPL-NAME                 
                                                L716B-APPL-NAME                 
           END-IF.                                                              
                                                                                
      **** GET CO-APPLICANT NAME                                                
           MOVE 'N' TO K825-FOUND                                               
           IF (F705-NUM-NRIC2 NOT = LOW-VALUES AND SPACES) AND                  
              F705-NUM-NRIC2(1:2) NOT = '##'                                    
              MOVE SPACES                    TO   K825-KEY-FLD                  
              MOVE F705-NUM-REF              TO   K825-NUM-REF                  
              MOVE F705-NUM-NRIC2            TO   K825-NUM-NRIC                 
                                                  L716A-NUM-NRIC2               
                                                  L716B-NUM-NRIC2               
              PERFORM 6400-READ-BP13K825     THRU 6400-EXIT                     
           END-IF.                                                              
                                                                                
              IF K825-FOUND = 'Y'                                               
                 MOVE K825-NME-OCCP          TO P716-COAPPL-NAME                
                                                L716A-COAPPL-NAME               
                                                L716B-COAPPL-NAME               
              END-IF.                                                           
                                                                                
           PERFORM 6500-READ-BP13K060        THRU 6500-EXIT.                    
           PERFORM 7000-MOVE-OTHER-DTLS      THRU 7000-EXIT.                    
           PERFORM 7100-PRINT-LETTER         THRU 7100-EXIT.                    
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       6100-READ-BP13K830.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES       TO K830-KEY-FLD.                                   
           MOVE F705-NUM-REF TO K830-NUM-REF.                                   
                                                                                
           READ BP13K830 KEY IS K830-KEY-FLD.                                   
                                                                                
           ADD  1  TO  WS-K830-READ.                                            
                                                                                
           IF BP13K830-STATUS = 00 OR 02                                        
              IF K830-NUM-MAIL-ADDR-TYPE NOT = SPACES AND LOW-VALUES            
                 PERFORM 6200-GET-MAILING-ADDR THRU 6200-EXIT                   
              ELSE                                                              
                 PERFORM 6300-GET-HOUSE-ADDR  THRU 6300-EXIT                    
              END-IF                                                            
           ELSE                                                                 
           IF BP13K830-STATUS = 23                                              
              DISPLAY 'BP13K830 RECORD NOT FOUND  = ' BP13K830-STATUS           
                      ', K830-NUM-REF = ' K830-NUM-REF                          
              ADD  1  TO  WS-K830-NOTFD                                         
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K830, STAT : ' BP13K830-STATUS         
                      ', K830-NUM-REF = ' K830-NUM-REF                          
              MOVE     BP13K830-STATUS TO RETURN-CODE                           
              PERFORM  9000-CLOSE-ROUTINE.                                      
                                                                                
       6100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       6200-GET-MAILING-ADDR.                                                   
      *-------------------------------------------------------------            
                                                                                
           MOVE K830-NUM-MAIL-BLK               TO P716-NUM-BLOCK.              
           MOVE K830-NUM-MAIL-BLK(1:11)         TO L716A-DETAIL-BLK             
                                                   L716B-DETAIL-BLK.            
           MOVE K830-NUM-MAIL-FLAT              TO P716-HOUSE-LEVEL             
                                                   L716A-HOUSE-LEVEL            
                                                   L716B-HOUSE-LEVEL.           
           MOVE K830-NME-MAIL-STR-BLDG          TO P716-STREET-NAME             
                                                   L716A-STREET-NAME            
                                                   L716B-STREET-NAME.           
           IF K830-NUM-MAIL-POSTAL = SPACES OR ZEROES OR LOW-VALUES             
              MOVE SPACES                       TO P716-NUM-POSTAL              
                                                   L716A-NUM-POSTAL             
                                                   L716B-NUM-POSTAL             
              DISPLAY ' MAIL POSTAL EMPTY ' K830-NUM-MAIL-POSTAL                
                      ', REGN IS ' K830-NUM-REGN                                
           ELSE                                                                 
              STRING 'SINGAPORE ' K830-NUM-MAIL-POSTAL                          
                 DELIMITED BY SIZE      INTO P716-NUM-POSTAL                    
              STRING 'SINGAPORE ' K830-NUM-MAIL-POSTAL                          
                 DELIMITED BY SIZE      INTO L716A-NUM-POSTAL                   
              STRING 'SINGAPORE ' K830-NUM-MAIL-POSTAL                          
                 DELIMITED BY SIZE      INTO L716B-NUM-POSTAL                   
           END-IF.                                                              
                                                                                
      * TO GET THE BAR CODE                                                     
           MOVE SPACES                       TO WS-LINK-AV02C015.               
           MOVE K830-NUM-MAIL-POSTAL         TO AV02C015-IN-POSTCODE.           
           PERFORM 8300-CALL-AV02C015        THRU 8300-EXIT.                    
           MOVE AV02C015-OUT-POSTCODE-CHKSUM TO P716-BAR-CODE.                  
                                                                                
       6200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       6300-GET-HOUSE-ADDR.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE K830-NUM-BLK                    TO P716-NUM-BLOCK.              
           MOVE K830-NUM-BLK(1:11)              TO L716A-DETAIL-BLK             
                                                   L716B-DETAIL-BLK.            
           MOVE K830-NUM-FLAT                   TO P716-HOUSE-LEVEL             
                                                   L716A-HOUSE-LEVEL            
                                                   L716B-HOUSE-LEVEL.           
           MOVE K830-NME-STREET-BLDG            TO P716-STREET-NAME             
                                                   L716A-STREET-NAME            
                                                   L716B-STREET-NAME.           
           IF K830-NUM-POSTAL = SPACES OR ZEROES OR LOW-VALUES                  
              MOVE SPACES                       TO P716-NUM-POSTAL              
                                                   L716A-NUM-POSTAL             
                                                   L716B-NUM-POSTAL             
              DISPLAY ' RES POSTAL EMPTY ' K830-NUM-MAIL-POSTAL                 
                      ', REGN = ' K830-NUM-REGN                                 
           ELSE                                                                 
              STRING 'SINGAPORE ' K830-NUM-POSTAL                               
                 DELIMITED BY SIZE     INTO P716-NUM-POSTAL                     
              STRING 'SINGAPORE ' K830-NUM-POSTAL                               
                 DELIMITED BY SIZE     INTO L716A-NUM-POSTAL                    
              STRING 'SINGAPORE ' K830-NUM-POSTAL                               
                 DELIMITED BY SIZE     INTO L716B-NUM-POSTAL                    
           END-IF.                                                              
                                                                                
      * TO GET THE BAR CODE                                                     
                                                                                
           MOVE SPACES                         TO WS-LINK-AV02C015.             
           MOVE K830-NUM-POSTAL                TO AV02C015-IN-POSTCODE.         
           PERFORM 8300-CALL-AV02C015          THRU 8300-EXIT.                  
           MOVE AV02C015-OUT-POSTCODE-CHKSUM   TO P716-BAR-CODE.                
                                                                                
       6300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       6400-READ-BP13K825.                                                      
      *-------------------------------------------------------------            
      *    USE F705-NUM-REF AND F705-NUM-NRIC1 TO READ BP13K825                 
      *        TO GET APPLICANT'S NAME                                          
      *-------------------------------------------------------------            
                                                                                
           READ BP13K825 KEY   IS K825-KEY-FLD.                                 
                                                                                
           ADD  1  TO  WS-K825-READ.                                            
                                                                                
           IF BP13K825-STATUS = 00                                              
              MOVE 'Y' TO K825-FOUND                                            
           ELSE                                                                 
           IF BP13K825-STATUS = 23                                              
              DISPLAY 'REC NOT FND, K825-STATUS ' BP13K825-STATUS               
                      ', K825-NUM-REF = ' K825-NUM-REF                          
              ADD  1  TO  WS-K825-NOTFD                                         
           ELSE                                                                 
              DISPLAY 'READ ERROR, K825-STATUS  = ' BP13K825-STATUS             
                      ', K825-NUM-REF = ' K825-NUM-REF                          
              PERFORM  9000-CLOSE-ROUTINE.                                      
                                                                                
       6400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       6500-READ-BP13K060.                                                      
      *-------------------------------------------------------------            
                                                                                
            MOVE  SPACES            TO    K060-KEY-FLD.                         
                                                                                
            IF F705-NUM-ALLO-CAT = 'BTO'                                        
               MOVE '91'                TO K060-SERIAL-NO                       
            ELSE                                                                
               MOVE '02'                TO K060-SERIAL-NO                       
            END-IF                                                              
                                                                                
            MOVE  F705-CDE-NT1      TO    K060-CODE.                            
                                                                                
            READ BP13K060.                                                      
                                                                                
            EVALUATE BP13K060-STATUS                                            
                WHEN 00                                                         
                     MOVE K060-DESC TO    P716-CDE-NT                           
                                          P716-CDE-NT2                          
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K060,STATUS:'                   
                     BP13K060-STATUS ', K060-KEY-FLD     ' K060-KEY-FLD         
                     MOVE     BP13K060-STATUS TO RETURN-CODE                    
                     PERFORM  9000-CLOSE-ROUTINE                                
            END-EVALUATE.                                                       
                                                                                
       6500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       7000-MOVE-OTHER-DTLS.                                                    
      *-------------------------------------------------------------            
                                                                                
           MOVE F705-NUM-REF          TO P716-REF-NO.                           
                                                                                
           MOVE WS-SYS-DATE           TO P716-DATE.                             
           MOVE F705-CDE-FLAT-TYPE    TO P716-FLAT-TYPE                         
                                         P716-FLAT-TYPE2.                       
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       7100-PRINT-LETTER.                                                       
      *-------------------------------------------------------------            
      **** PRINT LETTER 1ST PAGE                                                
           IF K703-CDE-ELIG-TAG = 'NU'                                          
               MOVE  SPACES               TO P716A-PRTREC                       
               WRITE P716A-PRTREC FROM DJDE-LINE-FORM-Q6 AFTER PAGE             
               WRITE P716A-PRTREC FROM P716-LINE-1 AFTER 6                      
               WRITE P716A-PRTREC FROM P716-LINE-2 AFTER 1                      
                                                                                
               WRITE P716A-PRTREC FROM P716-LINE-3 AFTER 3                      
               WRITE P716A-PRTREC FROM P716-LINE-4 AFTER 1                      
                                                                                
               WRITE P716A-PRTREC FROM P716-LINE-5 AFTER 1                      
               WRITE P716A-PRTREC FROM P716-LINE-6 AFTER 1                      
               WRITE P716A-PRTREC FROM P716-LINE-7 AFTER 1                      
               WRITE P716A-PRTREC FROM P716-LINE-8 AFTER 2                      
                                                                                
      ****     WRITE P716A-PRTREC FROM P716-LINE-9 AFTER 5                      
      ****     WRITE P716A-PRTREC FROM P716-LINE-10 AFTER 1                     
               WRITE P716A-PRTREC FROM P716-LINE-11 AFTER 8                     
               WRITE P716A-PRTREC FROM P716-LINE-12 AFTER 1                     
               WRITE P716A-PRTREC FROM P716-LINE-13 AFTER 13                    
               WRITE P716A-PRTREC FROM P716-LINE-14 AFTER 1                     
               WRITE P716A-PRTREC FROM P716-LINE-15 AFTER 1                     
               WRITE P716A-PRTREC FROM P716-LINE-16 AFTER 4                     
                                                                                
               PERFORM 8000-PRINT-LISTING        THRU 8000-EXIT                 
           END-IF.                                                              
                                                                                
           IF K703-CDE-ELIG-TAG = 'NE'                                          
               MOVE  SPACES               TO P716A-PRTREC                       
               WRITE P716B-PRTREC FROM DJDE-LINE-FORM-Q5 AFTER PAGE             
               WRITE P716B-PRTREC FROM P716-LINE-1 AFTER 6                      
               WRITE P716B-PRTREC FROM P716-LINE-2 AFTER 1                      
                                                                                
               WRITE P716B-PRTREC FROM P716-LINE-3 AFTER 3                      
               WRITE P716B-PRTREC FROM P716-LINE-4 AFTER 1                      
                                                                                
               WRITE P716B-PRTREC FROM P716-LINE-5 AFTER 1                      
               WRITE P716B-PRTREC FROM P716-LINE-6 AFTER 1                      
               WRITE P716B-PRTREC FROM P716-LINE-7 AFTER 1                      
               WRITE P716B-PRTREC FROM P716-LINE-8 AFTER 2                      
                                                                                
      *        WRITE P716B-PRTREC FROM P716-LINE-9 AFTER 5                      
      *        WRITE P716B-PRTREC FROM P716-LINE-10 AFTER 1                     
               WRITE P716B-PRTREC FROM P716-LINE-11 AFTER 8                     
               WRITE P716B-PRTREC FROM P716-LINE-12 AFTER 1                     
               WRITE P716B-PRTREC FROM P716-LINE-13 AFTER 13                    
               WRITE P716B-PRTREC FROM P716-LINE-14 AFTER 1                     
               WRITE P716B-PRTREC FROM P716-LINE-15 AFTER 1                     
               WRITE P716B-PRTREC FROM P716-LINE-16 AFTER 4                     
                                                                                
               PERFORM 8050-PRINT-LISTING        THRU 8050-EXIT                 
            END-IF.                                                             
                                                                                
           ADD 1 TO WS-LTR-PRINTED.                                             
                                                                                
       7100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       8000-PRINT-LISTING.                                                      
      *-------------------------------------------------------------            
                                                                                
           IF WS-LINE-CNT > 56                                                  
              PERFORM 8100-PRINT-HEADER THRU 8100-EXIT.                         
           ADD   1      TO WS-SNO.                                              
           MOVE  WS-SNO TO L716A-SNO.                                           
                                                                                
           MOVE F705-NUM-REF             TO L716A-NUM-REF.                      
           MOVE F705-CDE-ALLOC-SCH       TO L716A-ALLOC-MODE.                   
           MOVE F705-CDE-FLAT-TYPE       TO L716A-FLAT-TYPE.                    
           MOVE F705-CDE-NT1             TO L716A-CDE-NT.                       
                                                                                
           MOVE  SPACES                  TO L716A-PRTREC.                       
           WRITE L716A-PRTREC            FROM L716A-DETAIL AFTER 2.             
           ADD   2          TO WS-LINE-CNT.                                     
                                                                                
           IF L716A-NUM-NRIC2 NOT = SPACES AND LOW-VALUES                       
              WRITE L716A-PRTREC         FROM L716A-DETAIL2                     
              ADD   1       TO WS-LINE-CNT                                      
           END-IF.                                                              
                                                                                
           WRITE L716A-PRTREC            FROM L716A-DETAIL3.                    
           ADD   1          TO WS-LINE-CNT.                                     
                                                                                
       8000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       8050-PRINT-LISTING.                                                      
      *-------------------------------------------------------------            
                                                                                
           IF WS-LINE-CNT1 > 56                                                 
              PERFORM 8200-PRINT-HEADER THRU 8200-EXIT.                         
           ADD   1      TO WS-SNO1.                                             
           MOVE  WS-SNO1 TO L716B-SNO.                                          
                                                                                
           MOVE F705-NUM-REF             TO L716B-NUM-REF.                      
           MOVE F705-CDE-ALLOC-SCH       TO L716B-ALLOC-MODE.                   
           MOVE F705-CDE-FLAT-TYPE       TO L716B-FLAT-TYPE.                    
           MOVE F705-CDE-NT1             TO L716B-CDE-NT.                       
                                                                                
           MOVE  SPACES                  TO L716B-PRTREC.                       
           WRITE L716B-PRTREC            FROM L716B-DETAIL AFTER 2.             
           ADD   2          TO WS-LINE-CNT1.                                    
                                                                                
           IF L716B-NUM-NRIC2 NOT = SPACES AND LOW-VALUES                       
              WRITE L716B-PRTREC         FROM L716B-DETAIL2                     
              ADD   1       TO WS-LINE-CNT1                                     
           END-IF.                                                              
                                                                                
           WRITE L716B-PRTREC            FROM L716B-DETAIL3.                    
           ADD   1          TO WS-LINE-CNT1.                                    
                                                                                
       8050-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       8100-PRINT-HEADER.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE  SPACES             TO L716A-PRTREC.                            
                                                                                
           MOVE WS-SYS-DATE         TO L716A-DATE.                              
           ADD   1                  TO WS-PAGE-CNT.                             
           MOVE  WS-PAGE-CNT        TO L716A-PAGE.                              
                                                                                
           WRITE L716A-PRTREC       FROM L716A-HEADER1 AFTER PAGE.              
           WRITE L716A-PRTREC       FROM L716A-HEADER2.                         
           WRITE L716A-PRTREC       FROM L716A-HEADER3 AFTER 2.                 
           WRITE L716A-PRTREC       FROM L716A-HEADER4.                         
           WRITE L716A-PRTREC       FROM L716A-HEADER5.                         
                                                                                
           MOVE  6          TO WS-LINE-CNT.                                     
                                                                                
       8100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       8200-PRINT-HEADER.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE  SPACES             TO L716B-PRTREC.                            
                                                                                
           MOVE WS-SYS-DATE         TO L716B-DATE.                              
           ADD   1                  TO WS-PAGE-CNT1.                            
           MOVE  WS-PAGE-CNT1       TO L716B-PAGE.                              
                                                                                
           WRITE L716B-PRTREC       FROM L716B-HEADER1 AFTER PAGE.              
           WRITE L716B-PRTREC       FROM L716B-HEADER2.                         
           WRITE L716B-PRTREC       FROM L716B-HEADER3 AFTER 2.                 
           WRITE L716B-PRTREC       FROM L716B-HEADER4.                         
           WRITE L716B-PRTREC       FROM L716B-HEADER5.                         
                                                                                
           MOVE  6          TO WS-LINE-CNT1.                                    
                                                                                
       8200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       8300-CALL-AV02C015.                                                      
      *-------------------------------------------------------------            
                                                                                
           CALL 'AV02C015'          USING                                       
                                    AV02C015-IN-POSTCODE                        
                                    AV02C015-OUT-POSTCODE-CHKSUM.               
       8300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8500-READ-K595.                                                          
      *-------------------------------------------------------------            
                                                                                
           MOVE F705-NUM-REF             TO K595-KEY-FLD.                       
                                                                                
           START BP13K595 KEY = K595-NUM-REGN-REF.                              
                                                                                
           IF BP13K595-STATUS NOT = 0                                           
              IF BP13K595-STATUS = 10 OR 23                                     
                 DISPLAY 'K595 RECORD NOT FOUND    ' F705-NUM-REF               
                 GO TO 8500-EXIT                                                
              ELSE                                                              
                 DISPLAY 'ERROR STARTING BP13K595; STATUS = '                   
                    BP13K595-STATUS                                             
                 MOVE BP13K595-STATUS    TO RETURN-CODE                         
                 GO TO 9000-CLOSE-ROUTINE.                                      
                                                                                
           PERFORM 8900-UPDATE-BP13K595                                         
              THRU 8900-EXIT                                                    
             UNTIL K595-NUM-REGN-REF NOT = F705-NUM-REF.                        
                                                                                
       8500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8900-UPDATE-BP13K595.                                                    
      *-------------------------------------------------------------            
                                                                                
           READ BP13K595 NEXT AT END                                            
              MOVE HIGH-VALUES           TO K595-NUM-REGN-REF                   
              GO TO 8900-EXIT.                                                  
                                                                                
           IF K595-NUM-REGN-REF NOT = F705-NUM-REF                              
              GO TO 8900-EXIT.                                                  
                                                                                
      **   IF (K595-CDE-REQUEST-STATUS     = 'W')                               
      **       MOVE 'C' TO  K595-CDE-REQUEST-STATUS                             
      **       MOVE K703-CDE-ELIG-TAG  TO  K595-CDE-ELIG-TAG                    
                                                                                
           IF (K595-CDE-REQUEST-STATUS     = 'W')                               
               MOVE 'C' TO  K595-NUM-RESERVE-STATUS                             
               MOVE K703-CDE-ELIG-TAG  TO  K595-CDE-ELIG-TAG                    
                                                                                
               REWRITE BP13K595-REC                                             
                                                                                
              IF BP13K595-STATUS NOT = 0 AND 02                                 
                 DISPLAY 'ERROR IN REWRITING K595 ELIG-TAG    = '               
                    BP13K595-STATUS                                             
                 DISPLAY '                   BP13K595-REC     = '               
                    BP13K595-REC                                                
                 DISPLAY '--------------------------------------'               
                 MOVE BP13K595-STATUS    TO RETURN-CODE                         
                 GO TO 9000-CLOSE-ROUTINE                                       
              ELSE                                                              
                 ADD 1                   TO WS-K595-REWRITE                     
              END-IF                                                            
           ELSE                                                                 
               DISPLAY 'REGN NO : ' K595-NUM-REGN-REF ' NOT UPDATED'            
           END-IF.                                                              
                                                                                
       8900-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
            CLOSE BP13F705                                                      
                  BP13K703                                                      
                  BP13K825                                                      
                  BP13K060                                                      
                  BP13K830                                                      
                  BP13K595                                                      
                  P13P716A                                                      
                  P13P716B                                                      
                  P13L716A                                                      
                  P13L716B.                                                     
                                                                                
            IF BP13K703-STATUS  NOT = 00 AND 97                                 
               DISPLAY 'CLOSING ERROR BP13K703, STAT ' BP13K703-STATUS.         
                                                                                
            IF BP13K825-STATUS  NOT = 00 AND 97                                 
               DISPLAY 'CLOSING ERROR BP13K825, STAT ' BP13K825-STATUS.         
                                                                                
            IF BP13K830-STATUS  NOT = 00 AND 97                                 
               DISPLAY 'CLOSING ERROR BP13K830, STAT ' BP13K830-STATUS.         
                                                                                
            IF BP13K060-STATUS  NOT = 00 AND 97                                 
               DISPLAY 'CLOSING ERROR BP13K060, STAT ' BP13K060-STATUS.         
                                                                                
            DISPLAY  ' --- C714 CONTROL TOTAL -----------------'.               
            DISPLAY  ' NO. OF READ  FROM BP13F705 : ' WS-F705-READ.             
            DISPLAY  ' NO. OF READ  FROM BP13K703 : ' WS-K703-READ.             
            DISPLAY  ' NO. OF READ  FROM BP13K825 : ' WS-K825-READ.             
            DISPLAY  ' NO. OF NOTFND IN  BP13K825 : ' WS-K825-NOTFD.            
            DISPLAY  ' NO. OF READ  FROM BP13K830 : ' WS-K830-READ.             
            DISPLAY  ' NO. OF NOTFND IN  BP13K830 : ' WS-K830-NOTFD.            
            DISPLAY  ' NO. OF WRITE INTO BP13F705 : ' WS-F705-WROTE.            
            DISPLAY  ' NO. OF WRITE INTO BP13K595 : ' WS-K595-REWRITE.          
            DISPLAY  ' NO. OF WRITE INTO P13P716A : ' WS-LTR-PRINTED.           
            DISPLAY  SPACES.                                                    
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
