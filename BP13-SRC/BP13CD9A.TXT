      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CD9A.                                                 
       AUTHOR.        IRVIN CHING.                                              
       DATE-WRITTEN.  30/10/12.                                                 
      *****************************************************************         
      * OBJECTIVE - UPDATE SNP DATE AND FRESH SALE PURCHASE FOR FILES           
      *             BP13K300, BP13KD15.                                         
      *                                                                         
      *     INPUT FILES :                                                       
      *                  1. BP13F032                                            
      *                  2. BP18K300                                            
      *                  3. BM06K100                                            
      *                  4. BP18K300                                            
      *----------------------------------------------------------------         
      * REF NO    DD/MM/YY  BY   AMENDMENTS/ENHANCEMENTS                        
      * --------  --------  ---  ------------------------                       
      * BP134787  18/02/13  IMC1 NEW PROGRAM.                                   
      * BP134834  21/03/13  IMC1 CHANGE DATE FORMAT.                            
      * BP134799  07/05/13  IMC1 CHANGE FIELDS.                                 
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F032  ASSIGN        TO BP13F032.                          
                                                                                
           SELECT BP13KD15  ASSIGN        TO BP13KD15                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KD15-KEY-FLD                       
                            ALTERNATE KEY IS KD15-ALT-KEY1                      
                            FILE STATUS   IS WS-KD15-STATUS.                    
                                                                                
           SELECT BP18K300  ASSIGN        TO BP18K300                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K300-KEY-FLD                       
                            FILE STATUS   IS WS-K300-STATUS.                    
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
       FILE SECTION.                                                            
                                                                                
       COPY BP13F032.                                                           
                                                                                
       FD   BP13KD15                                                            
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13KD15.                                                           
                                                                                
       FD   BP18K300                                                            
            RECORD CONTAINS 800 CHARACTERS.                                     
       COPY BP18K300.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  WORK-FILE-VARIABLE.                                                  
           05  WS-KD15-STATUS          PIC 9(2)   VALUE ZEROES.                 
           05  WS-K300-STATUS          PIC 9(2)   VALUE ZEROES.                 
           05  WS-K100-STATUS          PIC 9(2)   VALUE ZEROES.                 
           05  WS-EOF-F032             PIC X      VALUE 'N'.                    
           05  WS-EOF-KD15             PIC X      VALUE 'N'.                    
           05  WS-EOF-K300             PIC X      VALUE 'N'.                    
           05  WS-EOF-K100             PIC X      VALUE 'N'.                    
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F032-READ            PIC 9(7)   VALUE ZEROES.                 
           05  WS-KD15-READ-CTR        PIC 9(7)   VALUE ZEROES.                 
           05  WS-KD15-NOTFND-CTR      PIC 9(7)   VALUE ZEROES.                 
           05  WS-KD15-UPD-CNT         PIC 9(7)   VALUE ZEROES.                 
           05  WS-K300-READ-CTR        PIC 9(7)   VALUE ZEROES.                 
           05  WS-K300-NOTFND-CTR      PIC 9(7)   VALUE ZEROES.                 
           05  WS-K300-UPD-CNT         PIC 9(7)   VALUE ZEROES.                 
           05  WS-K100-READ-CTR        PIC 9(7)   VALUE ZEROES.                 
           05  WS-K100-NOTFND-CTR      PIC 9(7)   VALUE ZEROES.                 
           05  WS-K100-UPD-CNT         PIC 9(7)   VALUE ZEROES.                 
           05  WS-MATCH                PIC 9(7)   VALUE ZEROES.                 
           05  WS-NUM-ALLOCN-QTR       PIC X(6)   VALUE SPACES.                 
                                                                                
       01  WS-DTE-SNP                  PIC X(8)   VALUE SPACES.                 
                                                                                
       01  WS-CURRENT-DATE             PIC 9(08)  VALUE ZEROES.                 
       01  WS-CURRENT-TIME             PIC 9(08)  VALUE ZEROES.                 
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F032      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F032 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F032                                                 
                I-O    BP13KD15                                                 
                       BP18K300.                                                
                                                                                
           IF WS-KD15-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13KD15 OPEN ERROR ' WS-KD15-STATUS                     
              MOVE WS-KD15-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K300-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP18K300 OPEN ERROR ' WS-K300-STATUS                     
              MOVE WS-K300-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F032.                                                      
      ******************************************************************        
                                                                                
           READ BP13F032           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F032                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO   WS-F032-READ.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           MOVE  F032-DTE-SNP              TO   WS-DTE-SNP.                     
                                                                                
           PERFORM 3100-READ-BP13KD15      THRU 3100-EXIT.                      
           PERFORM 3300-READ-BP18K300      THRU 3300-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F032      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       3100-READ-BP13KD15.                                                      
      ****************************                                              
                                                                                
           MOVE 'N'              TO WS-EOF-KD15                                 
                                    WS-EOF-K300.                                
                                                                                
           MOVE F032-PRJCT       TO KD15-NUM-PRJCT-KEY1.                        
           MOVE F032-NUM-REGN    TO KD15-NUM-REGN.                              
                                                                                
           READ BP13KD15 KEY IS KD15-ALT-KEY1.                                  
           EVALUATE WS-KD15-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 ADD 1 TO WS-KD15-READ-CTR                                      
                 PERFORM 3200-REWRITE-KD15   THRU 3200-EXIT                     
              WHEN 10                                                           
              WHEN 23                                                           
                   ADD 1 TO WS-KD15-NOTFND-CTR                                  
              WHEN OTHER                                                        
                   DISPLAY 'ERROR READING BP13KD15..' WS-KD15-STATUS            
                   MOVE WS-KD15-STATUS TO RETURN-CODE                           
                   PERFORM 9999-CLOSE-ROUTINE        THRU 9999-EXIT             
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       3200-REWRITE-KD15.                                                       
      ****************************                                              
                                                                                
           MOVE WS-DTE-SNP               TO KD15-DTE-SNP.                       
           MOVE ZEROES                   TO WS-KD15-STATUS.                     
                                                                                
           REWRITE BP13KD15-REC                                                 
                                                                                
           EVALUATE WS-KD15-STATUS                                              
               WHEN 00                                                          
                    ADD 1 TO WS-KD15-UPD-CNT                                    
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1 TO WS-KD15-NOTFND-CTR                                 
               WHEN OTHER                                                       
                   DISPLAY 'ERROR PROCESSING BP13KD15' WS-KD15-STATUS           
                   MOVE WS-KD15-STATUS               TO RETURN-CODE             
                   PERFORM 9999-CLOSE-ROUTINE        THRU 9999-EXIT             
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       3300-READ-BP18K300.                                                      
      ****************************                                              
                                                                                
           MOVE F032-NUM-REGN          TO K300-KEY-FLD.                         
                                                                                
           READ BP18K300                                                        
           END-READ.                                                            
                                                                                
           EVALUATE WS-K300-STATUS                                              
               WHEN 00                                                          
               WHEN 22                                                          
                    ADD 1 TO WS-K300-READ-CTR                                   
                    PERFORM 3350-UPDATE-BP18K300 THRU 3350-EXIT                 
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1 TO WS-K300-NOTFND-CTR                                 
               WHEN OTHER                                                       
                   DISPLAY 'ERROR PROCESSING BP18K300' WS-K300-STATUS           
                   MOVE WS-K300-STATUS               TO RETURN-CODE             
                   PERFORM 9999-CLOSE-ROUTINE        THRU 9999-EXIT             
           END-EVALUATE.                                                        
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       3350-UPDATE-BP18K300.                                                    
      ****************************                                              
           MOVE WS-DTE-SNP      TO K300-DTE-SALE-PRCHS.                         
           MOVE ZEROES                   TO WS-K300-STATUS.                     
                                                                                
           REWRITE BP18K300-REC                                                 
           EVALUATE WS-K300-STATUS                                              
             WHEN 00                                                            
                ADD  1     TO WS-K300-UPD-CNT                                   
             WHEN 23                                                            
                DISPLAY 'BP18K300 REWRITE KEY NOT FOUND, KEY-FLD : '            
                                                   K300-KEY-FLD                 
             WHEN OTHER                                                         
                DISPLAY 'BP18K300 REWRITE ERROR, STATUS : '                     
                                                   WS-K300-STATUS               
                MOVE WS-K300-STATUS  TO RETURN-CODE                             
                PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3350-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13CD9A'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY 'NO OF BP13F032 RECORDS READ............. : '                
                    WS-F032-READ.                                               
           DISPLAY 'NO OF BP13KD15 RECORDS READ............. : '                
                    WS-KD15-READ-CTR.                                           
           DISPLAY 'NO OF BP13KD15 RECORDS UPDATED.......... : '                
                    WS-KD15-UPD-CNT.                                            
           DISPLAY 'NO OF BP13KD15 RECORDS NOT FOUND........ : '                
                    WS-KD15-NOTFND-CTR.                                         
           DISPLAY 'NO OF BP18K300 RECORDS READ............. : '                
                    WS-K300-READ-CTR.                                           
           DISPLAY 'NO OF BP18K300 RECORDS UPDATED.......... : '                
                    WS-K300-UPD-CNT.                                            
           DISPLAY 'NO OF BP18K300 RECORDS NOT FOUND........ : '                
                    WS-K300-NOTFND-CTR.                                         
                                                                                
           CLOSE BP13F032                                                       
                 BP13KD15                                                       
                 BP18K300.                                                      
                                                                                
           IF WS-KD15-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13KD15 CLOSE ERROR ' WS-KD15-STATUS                    
              MOVE WS-KD15-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           IF WS-K300-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP18K300 CLOSE ERROR ' WS-K300-STATUS                    
              MOVE WS-K300-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
