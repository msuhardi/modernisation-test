       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CK17.                                                 
      *AUTHOR.        YCH2                                                      
      *DATE-WRITTEN.  13/02/2013.                                               
      *========================================================*                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *      1.  USE NRIC IN REQUEST FILE ON DETERMINED HHTYPE *                
      *          = 'H' OR 'T' TO FURTHER CHECK ON RESALE       *                
      *            EX-SPOUSE                                   *                
      *--------------------------------------------------------*                
      * CHG-NO    BY  DATE    DESCRIPTION                      *                
      * -------  ---  ------  -----------                      *                
      * BP134474 YCH2 13022013 STAGE IN VIA BP134554 CR        *                
      *========================================================*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F558   ASSIGN TO BP13F558.                                
                                                                                
           SELECT BB24K510  ASSIGN TO BB24K510                                  
                  ORGANIZATION     IS INDEXED                                   
                  ACCESS MODE      IS DYNAMIC                                   
                  RECORD KEY       IS K510-KEY-FLD                              
                  FILE STATUS      IS BB24K510-STATUS.                          
                                                                                
           SELECT BB24K500  ASSIGN TO BB24K500                                  
                  ORGANIZATION     IS INDEXED                                   
                  ACCESS MODE      IS RANDOM                                    
                  RECORD KEY       IS K500-KEY-FLD                              
                  FILE STATUS      IS BB24K500-STATUS.                          
                                                                                
           SELECT P13F558O  ASSIGN TO P13F558O.                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F558                                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F558.                                                           
                                                                                
       FD   P13F558O                                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
                                                                                
       01   P13F558O-REC            PIC X(600).                                 
                                                                                
       FD   BB24K510                                                            
            RECORD CONTAINS 50 CHARACTERS.                                      
                                                                                
       COPY BB24K510.                                                           
                                                                                
       FD   BB24K500                                                            
            RECORD CONTAINS 1700 CHARACTERS.                                    
                                                                                
       COPY BB24K500.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  BB24K500-STATUS          PIC 9(2)    VALUE ZEROES.                   
       01  BB24K510-STATUS          PIC 9(2)    VALUE ZEROES.                   
                                                                                
       01  WS-K510-READ             PIC 9(8)    VALUE 0.                        
       01  WS-K500-READ             PIC 9(8)    VALUE 0.                        
       01  WS-F558-READ             PIC 9(8)    VALUE 0.                        
       01  WS-OUTPUT-CNT            PIC 9(8)    VALUE 0.                        
       01  WS-MR-NRIC               PIC 9(8)    VALUE 0.                        
       01  WS-NMR-NRIC              PIC 9(8)    VALUE 0.                        
       01  WS-NOT-FND               PIC 9(8)    VALUE 0.                        
       01  WS-K510-SPOUSE           PIC 9(8)    VALUE 0.                        
       01  WS-K510-FD               PIC X       VALUE 'N'.                      
       01  WS-RSL-NOT-OM            PIC X       VALUE 'N'.                      
       01  WS-MULTIPLE-RESALE       PIC 9       VALUE 0.                        
       01  WS-MULTIPLE-DP-FLAT      PIC 9       VALUE 0.                        
       01  WS-FLAT-TYPE-TABLE.                                                  
           05  WS-FLAT-TYPE-TAB1    PIC X(8)    VALUE '12345EHG'.               
           05  FILLER REDEFINES WS-FLAT-TYPE-TAB1 OCCURS 8.                     
               10 WS-CDE-FLAT-TYPE-TAB  PIC X.                                  
       01  WS-NUMBER                PIC 9(2)    VALUE 0.                        
                                                                                
       01  WS-RESALE-OM             PIC X(01)   VALUE SPACES.                   
       01  WS-HDB-REF               PIC X(11)   VALUE SPACES.                   
       01  WS-FLAT-TYPE             PIC X(02)   VALUE SPACES.                   
       01  WS-FLAT-TYPE-2           PIC X(01)   VALUE SPACES.                   
       01  WS-FILE-STATUS           PIC X(03)   VALUE SPACES.                   
       01  WS-DTE-APPROVAL          PIC X(08)   VALUE SPACES.                   
       01  WS-DTE-APPROVAL2         PIC X(08)   VALUE SPACES.                   
       01  WS-DTE-RESALE-EFF        PIC X(08)   VALUE SPACES.                   
       01  WS-DTE-SOLD-SELLER       PIC X(08)   VALUE SPACES.                   
       01  WS-K510-CDE-STATUS       PIC X(01)   VALUE SPACES.                   
       01  WS-NUM-MTH-OWNED-RSL     PIC 9(03)   VALUE 0.                        
       01  WS-TOTAL                 PIC 9(5)    VALUE 0.                        
                                                                                
       01  EOF-BB24K510            PIC X        VALUE 'N'.                      
       01  EOF-BB24K500            PIC X        VALUE 'N'.                      
       01  EOF-BP13F558            PIC X        VALUE 'N'.                      
                                                                                
       01  WS-ALLOC-SCH             PIC X(3).                                   
           88 WS-ALLOC-SCH-RSL      VALUE 'AG1', 'AG2', 'AG3', 'A1A',           
                                          'A1B', 'A1C', 'A1D', 'A1E',           
                                          'A1F', 'A1G', 'A1H', 'A1I',           
                                          'A1J', 'A2A', 'A2B', 'A2C',           
                                          'A2D', 'A2E', 'A2F', 'A2G',           
                                          'A2H', 'A2I', 'A2J', 'A3A',           
                                          'A3B', 'A3C', 'A3D', 'A3E',           
                                          'A3F', 'A3G', 'A3H', 'A3I',           
                                          'A3J', 'B1J', 'B2J', 'B3J',           
                                          'C1J', 'C2J', 'C3J', 'HG1',           
                                          'HG2', 'HG3', 'H1A', 'H1B',           
                                          'H1C', 'H1D', 'H1E', 'H1F',           
                                          'H1G', 'H1H', 'H1I', 'H1J',           
                                          'H2A', 'H2B', 'H2C', 'H2D',           
                                          'H2E', 'H2F', 'H2G', 'H2H',           
                                          'H2I', 'H2J', 'H3A', 'H3B',           
                                          'H3C', 'H3D', 'H3E', 'H3F',           
                                          'H3G', 'H3H', 'H3I', 'H3J',           
                                          'I1J', 'I2J', 'I3J', 'J1J',           
                                          'J2J', 'J3J', 'T1J', 'T2J',           
                                          'T3J', 'U1J', 'U2J', 'U3J',           
                                          'V1J', 'V2J', 'V3J', 'W1J',           
                                          'W2J', 'W3J'.                         
                                                                                
       01  WS-DATE                 PIC X(8) VALUE SPACES.                       
                                                                                
       PROCEDURE DIVISION.                                                      
       000-MAIN.                                                                
                                                                                
           OPEN INPUT  BB24K510                                                 
                       BB24K500                                                 
                       BP13F558                                                 
                OUTPUT P13F558O.                                                
                                                                                
           IF BB24K510-STATUS NOT = ZEROS AND 97                                
               DISPLAY 'FILE BB24K510 OPEN ERROR'                               
               DISPLAY 'ERROR STATUS = ' BB24K510-STATUS                        
               MOVE BB24K510-STATUS TO RETURN-CODE                              
               PERFORM 900-CLOSE-ROUTINE.                                       
                                                                                
           IF BB24K500-STATUS NOT = ZEROS AND 97                                
               DISPLAY 'FILE BB24K500 OPEN ERROR'                               
               DISPLAY 'ERROR STATUS = ' BB24K500-STATUS                        
               MOVE BB24K500-STATUS TO RETURN-CODE                              
               PERFORM 900-CLOSE-ROUTINE.                                       
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-DATE.                        
                                                                                
           PERFORM 100-READ-F558-REC THRU 100-EXIT.                             
           PERFORM 200-PROCESS-REC   THRU 200-EXIT                              
                   UNTIL EOF-BP13F558 = 'Y'                                     
                     OR  F558-NUM-NRIC = HIGH-VALUE.                            
           PERFORM 900-CLOSE-ROUTINE.                                           
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-READ-F558-REC.                                                       
                                                                                
           READ BP13F558 AT END MOVE 'Y' TO EOF-BP13F558                        
                MOVE HIGH-VALUE TO F558-NUM-NRIC                                
                GO TO 100-EXIT.                                                 
           ADD 1 TO WS-F558-READ.                                               
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
      *                                                                         
       200-PROCESS-REC.                                                         
                                                                                
           MOVE 0             TO WS-MULTIPLE-RESALE.                            
           MOVE 0             TO WS-MULTIPLE-DP-FLAT.                           
           MOVE 0             TO WS-NUM-MTH-OWNED-RSL.                          
           MOVE 'N'           TO WS-K510-FD.                                    
           MOVE 'N'           TO WS-RSL-NOT-OM.                                 
           MOVE SPACES        TO K510-KEY-FLD.                                  
           MOVE F558-NUM-NRIC TO K510-NUM-UIN.                                  
           MOVE SPACES        TO WS-HDB-REF.                                    
           MOVE SPACES        TO WS-FLAT-TYPE.                                  
           MOVE SPACES        TO WS-FLAT-TYPE-2.                                
           MOVE SPACES        TO WS-FILE-STATUS.                                
           MOVE SPACES        TO WS-DTE-APPROVAL.                               
           MOVE SPACES        TO WS-DTE-APPROVAL2.                              
           MOVE SPACES        TO WS-DTE-RESALE-EFF.                             
           MOVE SPACES        TO WS-DTE-SOLD-SELLER.                            
                                                                                
           START BB24K510 KEY >=  K510-KEY-FLD.                                 
                                                                                
           IF  BB24K510-STATUS = ZEROES                                         
               PERFORM 210-READ-BB24K510 THRU 210-EXIT                          
                  UNTIL K510-NUM-UIN NOT = F558-NUM-NRIC                        
                    OR  WS-K510-FD = 'Y'                                        
                                                                                
               WRITE P13F558O-REC FROM BP13F558-REC                             
               ADD 1   TO WS-OUTPUT-CNT                                         
           ELSE                                                                 
               PERFORM 205-RECORD-NOTFD-RESALE THRU 205-EXIT                    
               DISPLAY 'NO RESALE ' K510-NUM-UIN                                
           END-IF                                                               
                                                                                
            PERFORM 100-READ-F558-REC THRU 100-EXIT.                            
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       205-RECORD-NOTFD-RESALE.                                                 
                                                                                
           WRITE P13F558O-REC FROM BP13F558-REC.                                
           ADD 1 TO WS-OUTPUT-CNT.                                              
           ADD 1 TO WS-NOT-FND.                                                 
                                                                                
       205-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
      * FOR REQUESTS FOR ALREADY CONSIDERED UPGRADER (G),      *                
      *    FROM SMS MATCHING, THEY WILL BE MATCHED WITH RECORDS*                
      *    IN RESALE FILE.                                     *                
      *                                                        *                
      * REQUESTS WHICH MATCH RESALE RECORDS WILL BE CONSIDERED *                
      * AS UPGRADER (G) UNLESS THEIR K500-RESALE-STATUS ARE OF *                
      *    THE FOLLOWING:                                      *                
      *      'BN' - RETURN TO HDB AT ORIGINAL PRICE            *                
      *      'PP' - NOT ELIGIBLE TO SELL IN OPEN MARKET        *                
      *      'SA' - REPURCHASED AT POSTED BY HDB               *                
      *      'AN' - AREARS                                     *                
      *      'AP' - AREARS                                     *                
      *      'TN' - OUTRIGHT TRANSFER                          *                
      *      'T ' - OUTRIGHT TRANSFER                          *                
      *      'TM' - OUTRIGHT TRANSFER                          *                
      *      'TD' - OUTRIGHT TRANSFER                          *                
      *      'IN' - TRANSFER BY INCLUSION/SUBSTITUTION         *                
      *      'TA' - TRANSFER OF SALES APPLICATION              *                
      *      'CM' - TCF SOLD 1ST FLAT OPEN MARKET DIRECT       *                
      *     'BCM' - TCF SOLD 1ST FLAT FROM OM (BUY BACK)       *                
      *     'BOM' - BUY FROM OM & SOLD IN OM (BUY BACK)        *                
      *--------------------------------------------------------*                
                                                                                
                                                                                
       210-READ-BB24K510.                                                       
                                                                                
           READ BB24K510 NEXT AT END MOVE HIGH-VALUE TO K510-KEY-FLD            
                                     GO TO 210-EXIT.                            
                                                                                
           IF  (F558-NUM-NRIC = K510-NUM-UIN)                                   
               ADD 1 TO WS-K510-READ                                            
               IF (K510-CDE-STATUS = 'P')                                       
                  MOVE 'Y' TO WS-K510-FD                                        
                  DISPLAY 'SPOUSE IC FND ' F558-NUM-REGN ' '                    
                  F558-NUM-NRIC                                                 
                  ADD 1 TO WS-K510-SPOUSE                                       
                  MOVE K510-CDE-STATUS TO WS-K510-CDE-STATUS                    
                  PERFORM 211-READ-BB24K500 THRU 211-EXIT                       
               END-IF                                                           
           ELSE                                                                 
               ADD 1 TO WS-NOT-FND                                              
           END-IF.                                                              
                                                                                
       210-EXIT.                                                                
           EXIT.                                                                
                                                                                
       211-READ-BB24K500.                                                       
                                                                                
           MOVE K510-NUM-HDB-REF   TO K500-NUM-HDB-REF.                         
           MOVE K510-DTE-APPROVAL  TO K500-DTE-APPROVAL.                        
                                                                                
           READ BB24K500.                                                       
                                                                                
           IF  BB24K500-STATUS = ZEROES                                         
               ADD 1 TO WS-K500-READ                                            
               MOVE K500-CDE-ALLOC-SCH-SELLER TO WS-ALLOC-SCH                   
               IF WS-ALLOC-SCH-RSL                                              
                  ADD 1 TO WS-MR-NRIC                                           
                  MOVE 'R'                      TO F558-CDE-FILE                
                  MOVE 'G' TO F558-CDE-HOUSEHOLD                                
                  MOVE K500-CDE-ALLOC-SCH-SELLER TO BP13F558-REC(597:3)         
                  MOVE K500-CDE-TYP-ACTLUSE(2:1)                                
                                         TO F558-CDE-FLAT-TYPE-RESALE           
                  MOVE K500-NUM-HDB-REF TO  F558-NUM-HDB-REF-RSL                
                  MOVE K500-DTE-APPROVAL TO F558-DTE-APPROVAL-RSL               
                  MOVE K500-DTE-RESL-EFF TO F558-DTE-RESALE-EFF                 
                  MOVE K510-CDE-STATUS   TO F558-NUM-SELLER-RSL                 
                  MOVE K500-DTE-SOLD-TO-SELLER                                  
                                         TO F558-DTE-SOLD-SELLER-RSL            
                  IF K500-CDE-RESALE-STATUS2 = LOW-VALUES OR SPACES             
                     MOVE K500-CDE-RESALE-STATUS                                
                                         TO F558-CDE-FILE-STATUS-RSL            
                  ELSE                                                          
                     IF F558-NUM-NRIC = K500-NUM-UIN-SELLER2 OR                 
                        F558-NUM-NRIC = K500-NUM-UIN-SPOUSE2                    
                        MOVE K500-CDE-RESALE-STATUS2                            
                                         TO F558-CDE-FILE-STATUS-RSL            
                     ELSE                                                       
                        MOVE K500-CDE-RESALE-STATUS                             
                                         TO F558-CDE-FILE-STATUS-RSL            
                     END-IF                                                     
                  END-IF                                                        
               ELSE                                                             
                  ADD 1 TO WS-NMR-NRIC                                          
               END-IF                                                           
           ELSE                                                                 
               MOVE 'N' TO WS-K510-FD                                           
               DISPLAY 'RECORD NOT FOUND IN BB24K500 ' F558-NUM-NRIC            
                       '--> ' K510-DTE-APPROVAL                                 
                       '--> ' K510-NUM-HDB-REF                                  
           END-IF.                                                              
                                                                                
       211-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY 'BP13CK16                 CURR-DATE : ' WS-DATE.             
           DISPLAY 'MATCH NRIC IN REQUEST FILE WITH RESALE FILE'.               
           DISPLAY '*-----------------------------------------------*'.         
           DISPLAY 'NO OF RECORDS READ FROM F558   ' WS-F558-READ.              
           DISPLAY 'NO OF RECORDS READ FROM K500   ' WS-K500-READ.              
           DISPLAY 'NO OF RECORDS READ FROM K510   ' WS-K510-READ.              
           DISPLAY 'NO OF RECORDS SPOUSE CASE      ' WS-K510-SPOUSE.            
           DISPLAY 'NO OF RECORDS OUTPUT BP13F558  ' WS-OUTPUT-CNT.             
           DISPLAY 'NO OF RECORDS NOT FND IN RSL   ' WS-NOT-FND.                
           DISPLAY 'NO OF RECORDS MR W/O ALLO MR   ' WS-NMR-NRIC.               
           DISPLAY 'NO OF RECORDS MR WITH ALLOC    ' WS-MR-NRIC.                
                                                                                
           CLOSE P13F558O                                                       
                 BP13F558                                                       
                 BB24K500                                                       
                 BB24K510.                                                      
                                                                                
           IF  BB24K510-STATUS NOT = ZEROS AND 97                               
               DISPLAY 'CLOSING ERROR '                                         
               DISPLAY 'BB24K510-STATUS ' BB24K510-STATUS.                      
           IF  BB24K500-STATUS NOT = ZEROS AND 97                               
               DISPLAY 'CLOSING ERROR '                                         
               DISPLAY 'BB24K500-STATUS ' BB24K500-STATUS.                      
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
