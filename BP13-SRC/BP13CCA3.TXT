      *-------------------------*                                               
       IDENTIFICATION DIVISION.                                                 
      *-------------------------*                                               
       PROGRAM-ID.    BP13CCA3.                                                 
       AUTHOR.        MICHAEL S  ARRIOLA.                                       
       DATE-WRITTEN.  21/10/08.                                                 
                                                                                
      ************************************************************              
      *  OBJECTIVE : TO ACCEPT CPF PAYMENT TO PAY TOP STAMP FEE  *              
      *              AND SURVEY FEE                              *              
      *              SOC WILL POST THE PAYMENT TO STAMP FEE      *              
      *              SYSTEM AND GL SYSTEM                        *              
      ************************************************************              
      *  CHG-NO   BY    ON         DESCRIPTION                   *              
      *  ------   ---   --------   ----------------------------- *              
      *  BP133468 MSD1  21/10/08   NEW PGM                       *              
      *  BP133505 JB8   16/01/09   OUTPUT SCH-ACC TO BP13F180    *              
      *  BP133905 JB8   14/10/10   TO CATER FOR PP8A             *              
      *  BP134034 ESA1  04/11/10   CATER FOR NEW ERP GL ACCOUNTS *              
      *           ESD1  10/02/11   CATER FOR INTERFACE FILE TO ERP              
      *  BP135093 IMC1  18/12/13   CHANGE B04F556 TO AB02F556    *              
      ************************************************************              
                                                                                
      *-------------------------*                                               
       ENVIRONMENT DIVISION.                                                    
      *-------------------------*                                               
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT AB02F556  ASSIGN        TO AB02F556.                          
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K110-KEY-FLD                       
                            FILE STATUS   IS BM06K110-STATUS.                   
           SELECT BP13KD40  ASSIGN        TO BP13KD40                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KD40-KEY-FLD                       
                            FILE STATUS   IS BP13KD40-STATUS.                   
                                                                                
           SELECT B05F005   ASSIGN        TO B05F005.                           
           SELECT BP13F180  ASSIGN        TO BP13F180.                          
           SELECT BP13LCA3  ASSIGN        TO BP13LCA3.                          
                                                                                
                                                                                
      *-------------------------*                                               
       DATA DIVISION.                                                           
      *-------------------------*                                               
                                                                                
       FILE SECTION.                                                            
                                                                                
       COPY AB02F556.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BM06K110.                                                           
                                                                                
       FD   BP13KD40                                                            
            RECORD CONTAINS  1500 CHARACTERS.                                   
       COPY BP13KD40.                                                           
                                                                                
       FD  B05F005                                                              
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 160 CHARACTERS.                                      
       COPY B05F005.                                                            
                                                                                
       FD  BP13F180                                                             
           RECORD CONTAINS 120 CHARACTERS.                                      
       COPY BP13F180.                                                           
                                                                                
       FD   BP13LCA3                                                            
            LABEL RECORDS ARE OMITTED.                                          
       01 BP13LCA3-REC             PIC X(132).                                  
                                                                                
      *-------------------------*                                               
       WORKING-STORAGE SECTION.                                                 
      *-------------------------*                                               
                                                                                
       01  WS-INDICATORS.                                                       
           05 BM06K110-STATUS      PIC 9(2)    VALUE ZEROES.                    
           05 BP13KD40-STATUS      PIC 9(2)    VALUE ZEROES.                    
           05 WS-F556-EOF          PIC X(1)    VALUE 'N'.                       
           05 WS-KD40-FND          PIC X(1)    VALUE 'N'.                       
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-CNT-F556          PIC 9999    VALUE 0.                         
           05 WS-CNT-F005          PIC 9999    VALUE 0.                         
           05 WS-F180-CR-CNT       PIC 9999    VALUE 0.                         
           05 WS-F180-DR-CNT       PIC 9999    VALUE 0.                         
           05 WS-K110-READ         PIC 9999    VALUE 0.                         
           05 WS-K110-NOTFD        PIC 9999    VALUE 0.                         
           05 WS-KD40-READ         PIC 9999    VALUE 0.                         
           05 WS-KD40-NFND         PIC 9999    VALUE 0.                         
                                                                                
       01  WS-NUM-VOUCHER.                                                      
           05 FILLER               PIC X(2)    VALUE 'SO'.                      
           05 WS-NUM-VOUCHER-MM    PIC X(2).                                    
           05 WS-NUM-VOUCHER-YY    PIC X(2).                                    
                                                                                
       01  WS-NUM-JR.                                                           
           05 FILLER               PIC X(1)    VALUE '0'.                       
           05 WS-NUM-JR-MM         PIC X(2).                                    
           05 WS-NUM-JR-YY         PIC X(2).                                    
                                                                                
       01  WS-DTE-POST.                                                         
           05 WS-DTE-POST-CC       PIC X(2).                                    
           05 WS-DTE-POST-YY       PIC X(2).                                    
           05 WS-DTE-POST-MM       PIC X(2).                                    
           05 FILLER REDEFINES WS-DTE-POST-MM.                                  
              10 WS-NUM-POST-MM    PIC 9(2).                                    
           05 WS-DTE-POST-DD       PIC X(2).                                    
                                                                                
       01 WS-OTHER-VARIABLES.                                                   
          05 WS-REGN               PIC X(18).                                   
          05 WS-HDB-REF            PIC X(11).                                   
          05 WS-CUR-DATE           PIC 9(08).                                   
          05 WS-PAGENO             PIC 9(03) VALUE ZERO.                        
          05 WS-LINE-CNT           PIC 9(02) VALUE 56.                          
          05 WS-SNO                PIC 9(04) VALUE ZERO.                        
                                                                                
      ***************************                                               
      * PRINT AREA                                                              
      ***************************                                               
       01 LCA3-HEADER1.                                                         
          05 FILLER             PIC X(08) VALUE 'BP13LCA3'.                     
          05 FILLER             PIC X(06) VALUE SPACES.                         
          05 FILLER             PIC X(04) VALUE 'HDB3'.                         
          05 FILLER             PIC X(38) VALUE SPACES.                         
          05 FILLER             PIC X(20) VALUE 'SYSTEM OF COMMITMENT'.         
          05 FILLER             PIC X(26) VALUE SPACES.                         
          05 FILLER             PIC X(07) VALUE 'DATE : '.                      
          05 LCA3-DATE          PIC X(10).                                      
          05 FILLER             PIC X(02) VALUE SPACES.                         
          05 FILLER             PIC X(07) VALUE 'PAGE : '.                      
          05 LCA3-PAGE          PIC Z(04).                                      
                                                                                
       01 LCA3-HEADER2.                                                         
          05 FILLER             PIC X(46) VALUE SPACES.                         
          05 FILLER             PIC X(40) VALUE                                 
             " WITHDRAWAL OF STAMP FEE FROM CPF ACCT ".                         
                                                                                
       01 LCA3-HEADER3.                                                         
          05 FILLER             PIC X(02) VALUE SPACES.                         
          05 FILLER             PIC X(06) VALUE 'SEQ NO'.                       
          05 FILLER             PIC X(02) VALUE SPACES.                         
          05 FILLER             PIC X(12) VALUE 'REGISTRATION'.                 
          05 FILLER             PIC X(02) VALUE SPACES.                         
          05 FILLER             PIC X(14) VALUE 'HDB REF'.                      
          05 FILLER             PIC X(02) VALUE SPACES.                         
          05 FILLER             PIC X(16) VALUE 'TRAN AMOUNT'.                  
          05 FILLER             PIC X(02) VALUE SPACES.                         
          05 FILLER             PIC X(16) VALUE 'TRAN DATE'.                    
          05 FILLER             PIC X(02) VALUE SPACES.                         
          05 FILLER             PIC X(20) VALUE 'REMARKS'.                      
                                                                                
       01 LCA3-DASH-LINE        PIC X(132) VALUE ALL '-'.                       
                                                                                
       01 LCA3-DETAILS.                                                         
          05 FILLER             PIC X(02) VALUE SPACES.                         
          05 LCA3-SNO           PIC Z(04).                                      
          05 FILLER             PIC X(06) VALUE SPACES.                         
          05 LCA3-REGN          PIC X(08).                                      
          05 FILLER             PIC X(04) VALUE SPACES.                         
          05 LCA3-NUM-HDB-REF   PIC X(11).                                      
          05 FILLER             PIC X(05) VALUE SPACES.                         
          05 LCA3-AMT-POSTED    PIC ZZZ,ZZ9.99.                                 
          05 FILLER             PIC X(07) VALUE SPACES.                         
          05 LCA3-DTE-POSTED    PIC X(10).                                      
          05 FILLER             PIC X(06) VALUE SPACES.                         
          05 LCA3-TXT-REMARK    PIC X(20).                                      
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
      *-------------------*                                                     
       MAIN-PROGRAM.                                                            
      *-------------------*                                                     
           PERFORM 000-OPEN-FILES      THRU  000-EXIT.                          
           PERFORM 100-READ-AB02F556   THRU  100-EXIT.                          
           PERFORM 200-PROCESS-PAYMENT THRU  200-EXIT                           
              UNTIL WS-F556-EOF = 'Y'.                                          
           PERFORM 500-CLOSE-ROUTINE.                                           
                                                                                
       MAIN-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------*                                                     
       000-OPEN-FILES.                                                          
      *-------------------*                                                     
           OPEN INPUT   AB02F556                                                
                        BM06K110                                                
                        BP13KD40                                                
               OUTPUT   B05F005                                                 
                        BP13F180                                                
                        BP13LCA3.                                               
                                                                                
           IF BM06K110-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BM06K110 : ' BM06K110-STATUS             
              MOVE     BM06K110-STATUS      TO RETURN-CODE                      
              PERFORM  500-CLOSE-ROUTINE THRU 500-EXIT.                         
                                                                                
           IF BP13KD40-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13KD40 : ' BP13KD40-STATUS             
              MOVE     BP13KD40-STATUS      TO RETURN-CODE                      
              PERFORM  500-CLOSE-ROUTINE THRU 500-EXIT.                         
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CUR-DATE.                      
           STRING WS-CUR-DATE(7:2) '/'                                          
                  WS-CUR-DATE(5:2) '/'                                          
                  WS-CUR-DATE(1:4) DELIMITED BY SIZE                            
                                         INTO LCA3-DATE.                        
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       100-READ-AB02F556.                                                       
      *-------------------*                                                     
           READ AB02F556                                                        
                         AT END MOVE 'Y' TO WS-F556-EOF                         
                         GO TO 100-EXIT.                                        
                                                                                
           ADD 1 TO WS-CNT-F556.                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       200-PROCESS-PAYMENT.                                                     
      *------------------------*                                                
           STRING F556-SCHACC F556-LESSEE                                       
              DELIMITED BY SIZE INTO WS-HDB-REF.                                
                                                                                
           PERFORM 291-READ-BM06K110      THRU 291-EXIT.                        
           PERFORM 292-READ-BP13KD40      THRU 292-EXIT.                        
                                                                                
           IF WS-KD40-FND = 'Y'                                                 
              ADD     1 TO WS-KD40-READ                                         
              PERFORM 220-WRITE-B05F005   THRU 220-EXIT                         
              PERFORM 230-CR-LEASE-STPFEE THRU 230-EXIT                         
              PERFORM 240-DR-CPF-BOARD    THRU 240-EXIT                         
              PERFORM 300-GENERATE-RPT    THRU 300-EXIT                         
           ELSE                                                                 
              ADD     1 TO WS-KD40-NFND                                         
           END-IF.                                                              
                                                                                
           PERFORM 100-READ-AB02F556      THRU 100-EXIT.                        
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       220-WRITE-B05F005.                                                       
      *------------------------*                                                
           MOVE SPACES TO F005-RECEIPT-REC.                                     
                                                                                
           MOVE WS-HDB-REF                TO  F005-HDB-REF-SF.                  
           MOVE '8'                       TO  F005-VR-TYPE.                     
           MOVE 'SO'                      TO  F005-SCHEDULE-NO(1:2).            
           MOVE '04'                      TO  F005-SYSTEM-CODE.                 
           MOVE '/'                       TO  F005-SCHEDULE-NO(8:1).            
                                                                                
           MOVE '0000000104250'           TO  F005-VOTE-NO                      
           MOVE F556-AMT-REG-DEP          TO  F005-TOTAL-AMT.                   
           MOVE 'C'                       TO  F005-CODE.                        
           MOVE F556-NUM-CPF              TO  F005-VR-NO.                       
           MOVE SPACES                    TO  F005-NAME.                        
                                                                                
           MOVE F556-PROC-DATE            TO  WS-DTE-POST                       
                                              F005-RECEIPT-DATE.                
           MOVE F556-PROC-DATE(1:6)       TO  F005-ACC-MTH-YR.                  
           MOVE WS-DTE-POST-YY            TO  F005-SCHEDULE-NO(9:2)             
                                                                                
           MOVE WS-DTE-POST-MM            TO  WS-NUM-VOUCHER-MM.                
           MOVE WS-DTE-POST-YY            TO  WS-NUM-VOUCHER-YY                 
           MOVE WS-NUM-VOUCHER            TO  F005-RECEIPT-NO.                  
                                                                                
           MOVE WS-DTE-POST-MM            TO  WS-NUM-JR-MM                      
           MOVE WS-DTE-POST-YY            TO  WS-NUM-JR-YY.                     
           MOVE WS-NUM-JR                 TO  F005-SCHEDULE-NO(3:5).            
                                                                                
           MOVE 'NS'                      TO  F005-CDE-SFTT.                    
           MOVE 'BP13'                    TO  F005-CDE-SYSTEM.                  
                                                                                
           WRITE F005-RECEIPT-REC.                                              
           ADD 1 TO WS-CNT-F005.                                                
                                                                                
       220-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       230-CR-LEASE-STPFEE.                                                     
      *------------------------*                                                
           MOVE SPACES                 TO F180-GL-REC.                          
           MOVE ZEROES                 TO F180-AMT-GST-RATE.                    
           MOVE 'SS'                   TO F180-JR-SC.                           
           MOVE WS-DTE-POST-YY         TO F180-JR-YY F180-TRANS-YY.             
           MOVE WS-DTE-POST-MM         TO F180-JR-MM F180-TRANS-MM.             
           MOVE WS-DTE-POST-DD         TO F180-TRANS-DD.                        
           IF WS-NUM-POST-MM    < 4                                             
              ADD 9 WS-NUM-POST-MM GIVING F180-PERIOD.                          
           IF WS-NUM-POST-MM    > 3                                             
              SUBTRACT 3 FROM WS-NUM-POST-MM GIVING F180-PERIOD.                
                                                                                
           COMPUTE F180-CR-AMT = 0 - F556-AMT-REG-DEP.                          
           MOVE '0000000104250'        TO F180-ACC-NO1.                         
           MOVE 'SOC-CREDIT TO LEASE STAMP FEE'  TO F180-TRANS-DESC.            
           MOVE WS-HDB-REF             TO F180-NUM-SCH-ACC.                     
                                                                                
           WRITE       F180-GL-REC.                                             
           ADD 1 TO WS-F180-CR-CNT.                                             
                                                                                
       230-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       240-DR-CPF-BOARD.                                                        
      *------------------------*                                                
           MOVE SPACES                 TO F180-GL-REC.                          
           MOVE ZEROES                 TO F180-AMT-GST-RATE.                    
           MOVE 'SS'                   TO F180-JR-SC.                           
           MOVE WS-DTE-POST-YY         TO F180-JR-YY F180-TRANS-YY.             
           MOVE WS-DTE-POST-MM         TO F180-JR-MM F180-TRANS-MM.             
           MOVE WS-DTE-POST-DD         TO F180-TRANS-DD.                        
           IF WS-NUM-POST-MM    < 4                                             
              ADD 9 WS-NUM-POST-MM GIVING F180-PERIOD.                          
           IF WS-NUM-POST-MM    > 3                                             
              SUBTRACT 3 FROM WS-NUM-POST-MM GIVING F180-PERIOD.                
                                                                                
           MOVE F556-AMT-REG-DEP       TO F180-CR-AMT.                          
           MOVE 'SOC-NON RECEIPT'      TO F180-TRANS-DESC.                      
           MOVE '0000000103617'        TO F180-ACC-NO1.                         
           MOVE WS-HDB-REF             TO F180-NUM-SCH-ACC.                     
                                                                                
           WRITE       F180-GL-REC.                                             
           ADD 1 TO WS-F180-DR-CNT.                                             
                                                                                
       240-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       291-READ-BM06K110.                                                       
      *-------------------------------------------------------------            
           MOVE 'N'               TO WS-KD40-FND                                
           MOVE WS-HDB-REF        TO K110-SCH-ACC-NO.                           
                                                                                
           READ BM06K110.                                                       
           EVALUATE BM06K110-STATUS                                             
             WHEN 00                                                            
                ADD             1 TO WS-K110-READ                               
                MOVE K110-REGN-NO TO WS-REGN                                    
             WHEN 23                                                            
                ADD             1 TO WS-K110-NOTFD                              
                MOVE SPACES       TO WS-REGN                                    
             WHEN OTHER                                                         
                DISPLAY 'BM06K110 READ ERROR,STATUS : ' BM06K110-STATUS         
                      ', K110 KEY FLD : ' K110-KEY-FLD                          
                MOVE BM06K110-STATUS       TO RETURN-CODE                       
                PERFORM 500-CLOSE-ROUTINE THRU 500-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       291-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       292-READ-BP13KD40.                                                       
      *-------------------------------------------------------------            
           IF WS-REGN = SPACES OR LOW-VALUES                                    
              GO TO 292-EXIT.                                                   
                                                                                
           MOVE WS-REGN      TO KD40-NUM-REGN.                                  
           MOVE '8A'         TO KD40-NUM-PP-SCHDL.                              
                                                                                
           READ BP13KD40.                                                       
           EVALUATE BP13KD40-STATUS                                             
             WHEN 00                                                            
                MOVE 'Y'    TO WS-KD40-FND                                      
             WHEN 23                                                            
                DISPLAY 'BP13KD40 READ NOTFD,STATUS : ' BP13KD40-STATUS         
                      ', KD40 KEY FLD : ' KD40-KEY-FLD                          
                PERFORM 293-READ-BP13KD40-8X THRU 293-EXIT                      
             WHEN OTHER                                                         
                DISPLAY 'BP13KD40 READ ERROR,STATUS : ' BP13KD40-STATUS         
                      ', KD40 KEY FLD : ' KD40-KEY-FLD                          
                MOVE BP13KD40-STATUS       TO RETURN-CODE                       
                PERFORM 500-CLOSE-ROUTINE THRU 500-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       292-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       293-READ-BP13KD40-8X.                                                    
      *-------------------------------------------------------------            
           IF WS-REGN = SPACES OR LOW-VALUES                                    
              GO TO 293-EXIT.                                                   
                                                                                
           MOVE WS-REGN      TO KD40-NUM-REGN.                                  
           MOVE '8X'         TO KD40-NUM-PP-SCHDL.                              
                                                                                
           READ BP13KD40.                                                       
           EVALUATE BP13KD40-STATUS                                             
             WHEN 00                                                            
                MOVE 'Y'    TO WS-KD40-FND                                      
             WHEN 23                                                            
                DISPLAY 'BP13KD40 READ NOTFD,STATUS : ' BP13KD40-STATUS         
                      ', KD40 KEY FLD : ' KD40-KEY-FLD                          
             WHEN OTHER                                                         
                DISPLAY 'BP13KD40 READ ERROR,STATUS : ' BP13KD40-STATUS         
                      ', KD40 KEY FLD : ' KD40-KEY-FLD                          
                MOVE BP13KD40-STATUS       TO RETURN-CODE                       
                PERFORM 500-CLOSE-ROUTINE THRU 500-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       293-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       300-GENERATE-RPT.                                                        
      *------------------------*                                                
           IF WS-LINE-CNT > 55                                                  
              PERFORM 310-PRINT-HEADER THRU 310-EXIT.                           
                                                                                
           MOVE SPACES                    TO LCA3-DETAILS.                      
                                                                                
           ADD                          1 TO WS-SNO.                            
           MOVE WS-SNO                    TO LCA3-SNO.                          
                                                                                
           MOVE WS-HDB-REF                TO LCA3-NUM-HDB-REF                   
           MOVE K110-REGN-NO              TO LCA3-REGN                          
           MOVE F556-AMT-REG-DEP          TO LCA3-AMT-POSTED                    
           MOVE F556-PROC-DATE            TO LCA3-DTE-POSTED                    
                                                                                
           WRITE BP13LCA3-REC           FROM LCA3-DETAILS.                      
           ADD                          1 TO WS-LINE-CNT.                       
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       310-PRINT-HEADER.                                                        
      *-------------------------------------------------------------            
           ADD   1            TO WS-PAGENO.                                     
           MOVE WS-PAGENO     TO LCA3-PAGE.                                     
                                                                                
           WRITE BP13LCA3-REC  FROM LCA3-HEADER1   AFTER PAGE.                  
           WRITE BP13LCA3-REC  FROM LCA3-HEADER2   AFTER 1.                     
           WRITE BP13LCA3-REC  FROM LCA3-HEADER3   AFTER 2.                     
           WRITE BP13LCA3-REC  FROM LCA3-DASH-LINE AFTER 1.                     
           MOVE   5              TO WS-LINE-CNT.                                
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       500-CLOSE-ROUTINE.                                                       
      *------------------------*                                                
                                                                                
           DISPLAY 'PROGRAM-ID : BP13CCA3 '.                                    
           DISPLAY 'RUN DATE   : ' WS-CUR-DATE.                                 
           DISPLAY 'AB02F556  RECORDS READ    : ' WS-CNT-F556.                  
           DISPLAY 'BM06K110 RECORDS READ    : ' WS-K110-READ.                  
           DISPLAY 'BM06K110 RECORDS NOTFD   : ' WS-K110-NOTFD.                 
           DISPLAY 'BP13KD40 RECORDS READ    : ' WS-KD40-READ.                  
           DISPLAY 'BP13KD40 RECORDS NOT FND : ' WS-KD40-NFND.                  
           DISPLAY 'B05F005  RECORDS WRITTEN : ' WS-CNT-F005.                   
           DISPLAY 'BP13F180 CR-REC  WRITTEN : ' WS-F180-CR-CNT.                
           DISPLAY 'BP13F180 DR-REC  WRITTEN : ' WS-F180-DR-CNT.                
                                                                                
           CLOSE AB02F556                                                       
                 BM06K110                                                       
                 BP13KD40                                                       
                 B05F005                                                        
                 BP13F180                                                       
                 BP13LCA3.                                                      
                                                                                
           IF BM06K110-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BM06K110 : ' BM06K110-STATUS             
              MOVE BM06K110-STATUS TO RETURN-CODE.                              
                                                                                
           IF BP13KD40-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13KD40 : ' BP13KD40-STATUS             
              MOVE BP13KD40-STATUS TO RETURN-CODE.                              
                                                                                
           STOP RUN.                                                            
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
