      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
       PROGRAM-ID.    BP13C206.                                                 
      *AUTHOR.        SGK.                                                      
      *DATE-WRITTEN.  APR 27 1998.                                              
                                                                                
      *****************************************************************         
      * OBJECTIVE  - LISTING OF RFS CASES CANCELLED UNDER CODE 64.              
      * INPUT FILES:  BP13F310                                                  
      *               BP13K800                                                  
      *                                                                         
      * REPORT:       BP13L206                                                  
      *                                                                         
      *----------------------------------------------------------------         
      * MODIFICATION HISTORY:                                                   
      * =====================                                                   
      * REF NO   DATE    BY   AMENDMENTS/ENHANCEMENTS                           
      * -------  ----    ---  -----------------------                           
      *BP13XXXX 27/04/98 SGK  NEW PGM.                                *         
      *BP130418 26/08/98 LSB  CHG LREC K800 TO X(2000)                *         
      *****************************************************************         
                                                                                
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
      *****************************************************************         
      *===============================================================*         
       CONFIGURATION SECTION.                                                   
      *===============================================================*         
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *===============================================================*         
       INPUT-OUTPUT SECTION.                                                    
      *===============================================================*         
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K800 ASSIGN        TO BP13K800                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS RANDOM                              
                           RECORD KEY    IS K800-NUM-REGN                       
                           FILE STATUS   IS K800-STATUS.                        
                                                                                
           SELECT BP13K820 ASSIGN        TO BP13K820                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS RANDOM                              
                           RECORD KEY    IS K820-KEY-FLD                        
                           FILE STATUS   IS K820-STATUS.                        
                                                                                
           SELECT BP13F310 ASSIGN        TO BP13F310.                           
           SELECT BP13L206 ASSIGN        TO BP13L206.                           
                                                                                
                                                                                
      *****************************************************************         
       DATA DIVISION.                                                           
      *****************************************************************         
      *===============================================================*         
       FILE SECTION.                                                            
      *===============================================================*         
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS  400 CHARACTERS.                                    
       COPY BP13K820.                                                           
                                                                                
       FD   BP13F310                                                            
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13F310.                                                           
                                                                                
       FD   BP13L206                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 133 CHARACTERS                                      
            BLOCK  CONTAINS 0   RECORDS.                                        
       01   L206-PRINT-REC               PIC X(133).                            
                                                                                
      *===============================================================*         
       WORKING-STORAGE SECTION.                                                 
      *===============================================================*         
       01  WS-FILE-STATUS.                                                      
           05  K800-STATUS               PIC 9(02) VALUE 0.                     
           05  K820-STATUS               PIC 9(02) VALUE 0.                     
                                                                                
       01  WS-F310-EOF                   PIC X(01) VALUE 'N'.                   
                                                                                
       01  WS-ERROR-FL                   PIC X(01) VALUE 'N'.                   
                                                                                
       01  WS-CURR-DATE.                                                        
           05 WS-YY                      PIC X(04).                             
           05 WS-MM                      PIC X(02).                             
           05 WS-DD                      PIC X(02).                             
       01   WS-CURR-DATE-R  REDEFINES  WS-CURR-DATE  PIC 9(8).                  
                                                                                
       01  WS-DISP-DATE.                                                        
           05 WS-DD                      PIC X(02).                             
           05 FILLER                     PIC X(01) VALUE '/'.                   
           05 WS-MM                      PIC X(02).                             
           05 FILLER                     PIC X(01) VALUE '/'.                   
           05 WS-YY                      PIC X(04).                             
                                                                                
       01  WS-INT-DATE                   PIC S9(9).                             
       01  WS-PROCESS-DATE               PIC 9(8).                              
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-CNT-F310-READ           PIC 9(07) VALUE 0.                     
           05 WS-CNT-F310-ERROR          PIC 9(07) VALUE 0.                     
           05 WS-CNT-K800-READ           PIC 9(07) VALUE 0.                     
           05 WS-CNT-K820-READ           PIC 9(07) VALUE 0.                     
           05 WS-K820-FND                PIC X     VALUE 'N'.                   
           05 WS-TOTAL-CNT               PIC 9(07) VALUE 0.                     
           05 WS-LINE-CNT                PIC 9(07) VALUE 0.                     
           05 WS-PAGE-CNT                PIC 9(07) VALUE 0.                     
           05 WS-SEQ-NO                  PIC 9(07) VALUE ZERO.                  
           05 WS-TOTAL-REFUND            PIC S9(08)V99 VALUE 0.                 
                                                                                
      *-----------------------------------------------------------*             
      *    LISTING VARIABLES                                      *             
      *-----------------------------------------------------------*             
       01  L206-PRT-HEAD-01.                                                    
           05 FILLER                       PIC X(01) VALUE SPACES.              
           05 FILLER                       PIC X(08) VALUE 'BP13L206'.          
           05 FILLER                       PIC X(04) VALUE SPACES.              
           05 FILLER                       PIC X(08) VALUE 'HDB3    '.          
           05 FILLER                       PIC X(15) VALUE SPACES.              
           05 FILLER                       PIC X(39) VALUE                      
             'S Y S T E M   O F   C O M M I T M E N T'.                         
           05 FILLER                       PIC X(23) VALUE SPACES.              
           05 FILLER                       PIC X(07) VALUE 'DATE: '.            
           05 L206-PRINT-DATE              PIC X(10) VALUE SPACES.              
           05 FILLER                       PIC X(03) VALUE SPACES.              
           05 FILLER                       PIC X(07) VALUE 'PAGE: '.            
           05 L206-PRINT-PAGE              PIC ZZZZ9.                           
                                                                                
       01  L206-PRT-HEAD-02.                                                    
           05 FILLER                       PIC X(20) VALUE SPACES.              
           05 FILLER                       PIC X(50) VALUE                      
             'LISTING OF RFS CASES CANCELLED UNDER CODE 64 ON '.                
           05 L206-PROCESS-DATE             PIC X(10) VALUE SPACES.             
           05 FILLER                       PIC X(12) VALUE                      
             ' DUE TO SERS'.                                                    
                                                                                
       01  L206-PRT-HEAD-03.                                                    
           05 FILLER                       PIC X(01) VALUE SPACES.              
           05 FILLER                       PIC X(04)                            
                    VALUE 'S.NO'.                                               
           05 FILLER                       PIC X(02) VALUE SPACES.              
           05 FILLER                       PIC X(50)                            
                    VALUE 'NAME           '.                                    
           05 FILLER                       PIC X(12) VALUE SPACES.              
           05 FILLER                       PIC X(04)                            
                    VALUE 'NRIC'.                                               
           05 FILLER                       PIC X(07) VALUE SPACES.              
           05 FILLER                       PIC X(02) VALUE SPACES.              
           05 FILLER                       PIC X(09)                            
                    VALUE 'RFS REGN.'.                                          
           05 FILLER                       PIC X(02) VALUE SPACES.              
           05 FILLER                       PIC X(13)                            
                    VALUE 'STAGE OF APPN'.                                      
           05 FILLER                       PIC X(02) VALUE SPACES.              
           05 FILLER                       PIC X(10)                            
                    VALUE 'REMARKS   '.                                         
                                                                                
                                                                                
       01  L206-PRT-DETAIL.                                                     
           05 FILLER                       PIC X(01) VALUE SPACES.              
           05 L206-SNO                     PIC ZZZZ.                            
           05 FILLER                       PIC X(02) VALUE SPACES.              
           05 L206-NAME                    PIC X(60) VALUE SPACES.              
           05 FILLER                       PIC X(02) VALUE SPACES.              
           05 L206-NRIC                    PIC X(09) VALUE SPACES.              
           05 FILLER                       PIC X(04) VALUE SPACES.              
           05 L206-REGN                    PIC X(09) VALUE SPACES.              
           05 FILLER                       PIC X(02) VALUE SPACES.              
           05 L206-APPN-STAGE              PIC X(13) VALUE SPACES.              
           05 FILLER                       PIC X(02) VALUE SPACES.              
           05 L206-REMARK                  PIC X(15) VALUE SPACES.              
                                                                                
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
                                                                                
      *===============================================================*         
       0000-CONTROL.                                                            
      *===============================================================*         
                                                                                
           PERFORM 1000-START-UP         THRU 1000-EXIT.                        
                                                                                
           PERFORM 2000-MAIN-ROUTINE     THRU 2000-EXIT                         
                UNTIL WS-F310-EOF = 'Y'.                                        
                                                                                
           PERFORM 9999-WRAP-UP.                                                
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *===============================================================*         
       1000-START-UP.                                                           
      *===============================================================*         
                                                                                
            OPEN INPUT  BP13F310                                                
                        BP13K800                                                
                        BP13K820                                                
                OUTPUT  BP13L206.                                               
                                                                                
            IF K800-STATUS NOT = 0 AND 97                                       
               DISPLAY 'OPEN FAILED FOR BP13K800 ' K800-STATUS                  
               PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                         
            END-IF.                                                             
                                                                                
            IF K820-STATUS NOT = 0 AND 97                                       
               DISPLAY 'OPEN FAILED FOR BP13K820 ' K820-STATUS                  
               PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                         
            END-IF.                                                             
                                                                                
            READ BP13F310                                                       
                 AT END  DISPLAY 'NO RECORDS FOUND!'                            
                         MOVE 'Y'          TO WS-F310-EOF                       
                         GO TO 1000-EXIT                                        
            END-READ.                                                           
                                                                                
            MOVE 60                  TO WS-LINE-CNT.                            
            ADD  1                   TO WS-CNT-F310-READ.                       
            MOVE FUNCTION CURRENT-DATE(1:8)                                     
                                     TO WS-CURR-DATE.                           
            MOVE CORR WS-CURR-DATE   TO WS-DISP-DATE.                           
            MOVE WS-DISP-DATE        TO L206-PRINT-DATE.                        
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *===============================================================*         
       2000-MAIN-ROUTINE.                                                       
      *===============================================================*         
                                                                                
            COMPUTE  WS-INT-DATE =                                              
                        FUNCTION  INTEGER-OF-DATE(WS-CURR-DATE-R).              
                                                                                
            COMPUTE  WS-INT-DATE =  WS-INT-DATE - 1.                            
                                                                                
      *---> TO  GET THE PREVIOUS DAY TRANSACTION                                
            COMPUTE  WS-PROCESS-DATE  =                                         
                     FUNCTION DATE-OF-INTEGER(WS-INT-DATE).                     
                                                                                
                                                                                
            IF F310-CDE-TRANS  = 'CA' AND                                       
               WS-PROCESS-DATE = F310-DTE-TRANS                                 
               PERFORM  3000-READ-K800  THRU  3000-EXIT                         
            ELSE                                                                
               ADD 1                   TO WS-CNT-F310-ERROR                     
            END-IF.                                                             
                                                                                
            READ BP13F310 AT END                                                
                 MOVE 'Y' TO WS-F310-EOF                                        
                 GO TO 2000-EXIT                                                
                 END-READ.                                                      
                                                                                
            ADD  1                          TO WS-CNT-F310-READ.                
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *===============================================================*         
       3000-READ-K800.                                                          
      *===============================================================*         
                                                                                
            MOVE   F310-REGN-NO           TO   K800-NUM-REGN.                   
            READ   BP13K800.                                                    
                                                                                
            EVALUATE  K800-STATUS                                               
              WHEN 00                                                           
                       CONTINUE                                                 
              WHEN 23                                                           
                       DISPLAY  'BP13K800 - RECORD NOT FOUND, KEY : '           
                          K800-NUM-REGN                                         
                       GO TO 3000-EXIT                                          
              WHEN OTHER                                                        
                       DISPLAY  'BP13K800 - FILE READ ERROR, STATUS: '          
                          K800-STATUS                                           
                       PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                 
            END-EVALUATE.                                                       
                                                                                
            ADD      1              TO      WS-CNT-K800-READ.                   
                                                                                
            IF K800-NUM-CANCEL-REASON = '64'                                    
               PERFORM  4000-PRINT-SUMMARY   THRU  4000-EXIT                    
            END-IF.                                                             
                                                                                
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *===============================================================*         
       4000-PRINT-SUMMARY.                                                      
      *===============================================================*         
                                                                                
            MOVE   SPACES          TO   L206-PRINT-REC.                         
            WRITE  L206-PRINT-REC  AFTER 1.                                     
                                                                                
           IF  K800-NUM-NRIC1 NOT = SPACES  AND  LOW-VALUES OR                  
               K800-NUM-NRIC2 NOT = SPACES  AND  LOW-VALUES OR                  
               K800-NUM-NRIC3 NOT = SPACES  AND  LOW-VALUES OR                  
               K800-NUM-NRIC4 NOT = SPACES  AND  LOW-VALUES                     
               ADD       1            TO       WS-SEQ-NO                        
               MOVE     K800-NUM-REGN TO       L206-REGN                        
               MOVE     WS-SEQ-NO     TO       L206-SNO.                        
                                                                                
            IF K800-NUM-SCH-ACC = SPACES OR LOW-VALUES                          
               MOVE 'RFS'              TO       L206-APPN-STAGE                 
            ELSE                                                                
              IF K800-DTE-RAISE-AO IS NOT NUMERIC                               
                 MOVE 'BOOK'           TO       L206-APPN-STAGE                 
              ELSE                                                              
                 MOVE 'SOCA'           TO       L206-APPN-STAGE                 
              END-IF                                                            
            END-IF.                                                             
                                                                                
           IF  K800-NUM-NRIC1 NOT = SPACES   AND  LOW-VALUES                    
               MOVE     K800-NUM-NRIC1   TO    K820-NUM-NRIC                    
               PERFORM  4050-READ-K820   THRU  4050-EXIT                        
               IF  WS-K820-FND = 'Y'                                            
                   PERFORM  4075-SETUP-REPORT  THRU  4075-EXIT                  
                   MOVE     ZERO               TO    L206-SNO                   
                   MOVE     SPACES             TO    L206-REGN                  
                   MOVE     SPACES             TO    L206-APPN-STAGE            
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  K800-NUM-NRIC2 NOT = SPACES   AND  LOW-VALUES                    
               MOVE     K800-NUM-NRIC2   TO    K820-NUM-NRIC                    
               PERFORM  4050-READ-K820   THRU  4050-EXIT                        
               IF  WS-K820-FND = 'Y'                                            
                   PERFORM  4075-SETUP-REPORT  THRU  4075-EXIT                  
                   MOVE     ZERO               TO    L206-SNO                   
                   MOVE     SPACES             TO    L206-REGN                  
                   MOVE     SPACES             TO    L206-APPN-STAGE            
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  K800-NUM-NRIC3 NOT = SPACES   AND  LOW-VALUES                    
               MOVE     K800-NUM-NRIC3   TO    K820-NUM-NRIC                    
               PERFORM  4050-READ-K820   THRU  4050-EXIT                        
               IF  WS-K820-FND = 'Y'                                            
                   PERFORM  4075-SETUP-REPORT  THRU  4075-EXIT                  
                   MOVE     ZERO               TO    L206-SNO                   
                   MOVE     SPACES             TO    L206-REGN                  
                   MOVE     SPACES             TO    L206-APPN-STAGE            
               END-IF                                                           
           END-IF.                                                              
                                                                                
           IF  K800-NUM-NRIC4 NOT = SPACES   AND  LOW-VALUES                    
               MOVE     K800-NUM-NRIC4   TO    K820-NUM-NRIC                    
               PERFORM  4050-READ-K820   THRU  4050-EXIT                        
               IF  WS-K820-FND = 'Y'                                            
                   PERFORM  4075-SETUP-REPORT  THRU  4075-EXIT                  
               END-IF                                                           
           END-IF.                                                              
                                                                                
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *===============================================================*         
       4050-READ-K820.                                                          
      *===============================================================*         
                                                                                
            MOVE  K800-NUM-REGN        TO    K820-NUM-REGN.                     
            MOVE  'N'                  TO    WS-K820-FND.                       
                                                                                
            READ   BP13K820.                                                    
                                                                                
            EVALUATE  K820-STATUS                                               
              WHEN 00                                                           
                        ADD       1    TO    WS-CNT-K820-READ                   
                        MOVE     'Y'   TO    WS-K820-FND                        
              WHEN 23                                                           
                        MOVE     'N'   TO    WS-K820-FND                        
              WHEN OTHER                                                        
                        DISPLAY  'BP13K820 - FILE READ ERROR, STATUS : '        
                                    K820-STATUS                                 
                        PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                
            END-EVALUATE.                                                       
                                                                                
                                                                                
       4050-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *===============================================================*         
       4075-SETUP-REPORT.                                                       
      *===============================================================*         
                                                                                
            MOVE   K820-NUM-NRIC     TO     L206-NRIC.                          
            MOVE   K820-NME-OCCP     TO     L206-NAME.                          
            STRING  F310-DTE-TRANS(7:2) '/' F310-DTE-TRANS(5:2)                 
                    '/'  F310-DTE-TRANS(1:4) DELIMITED BY SIZE                  
                    INTO                    L206-PROCESS-DATE.                  
                                                                                
            IF  WS-LINE-CNT  > 55                                               
                PERFORM 4100-PRINT-HEADING  THRU  4100-EXIT                     
            END-IF.                                                             
                                                                                
            WRITE  L206-PRINT-REC  FROM L206-PRT-DETAIL AFTER 1.                
                                                                                
       4075-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *===============================================================*         
       4100-PRINT-HEADING.                                                      
      *===============================================================*         
                                                                                
           ADD  1               TO WS-PAGE-CNT.                                 
           MOVE WS-PAGE-CNT     TO L206-PRINT-PAGE.                             
                                                                                
           MOVE  6                         TO WS-LINE-CNT.                      
                                                                                
           MOVE SPACES                     TO L206-PRINT-REC.                   
           WRITE L206-PRINT-REC            AFTER PAGE.                          
           WRITE L206-PRINT-REC  FROM L206-PRT-HEAD-01 AFTER 1.                 
           WRITE L206-PRINT-REC  FROM L206-PRT-HEAD-02 AFTER 1.                 
           WRITE L206-PRINT-REC  FROM L206-PRT-HEAD-03 AFTER 2.                 
           MOVE  ALL '='                   TO  L206-PRINT-REC.                  
           WRITE L206-PRINT-REC  AFTER 1.                                       
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *===============================================================*         
       9900-HANDLE-ABEND.                                                       
      *===============================================================*         
                                                                                
           DISPLAY  SPACES.                                                     
           DISPLAY '******* BP13C206 ABEND TOTALS ********'.                    
           DISPLAY 'PROGRAM-ID : BP13C206'.                                     
           DISPLAY  SPACES.                                                     
           DISPLAY 'TOTAL BP13F310 RECS READ     = ' WS-CNT-F310-READ.          
           DISPLAY 'TOTAL BP13F310 ERR RECS      = ' WS-CNT-F310-ERROR.         
           DISPLAY 'TOTAL BP13K800 RECS READ     = ' WS-CNT-K800-READ.          
           DISPLAY 'TOTAL BP13K820 RECS READ     = ' WS-CNT-K820-READ.          
                                                                                
           CLOSE BP13F310                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13L206.                                                      
                                                                                
           IF K800-STATUS NOT = 0                                               
              MOVE K800-STATUS TO RETURN-CODE                                   
              DISPLAY 'CLOSE FAILED FOR BP13K800 ' K800-STATUS                  
           END-IF.                                                              
                                                                                
           IF K820-STATUS NOT = 0                                               
              MOVE K820-STATUS TO RETURN-CODE                                   
              DISPLAY 'CLOSE FAILED FOR BP13K820 ' K820-STATUS                  
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9900-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *===============================================================*         
       9999-WRAP-UP.                                                            
      *===============================================================*         
                                                                                
           DISPLAY '******* BP13C206 AUDIT TOTALS ********'.                    
           DISPLAY 'PROGRAM-ID : BP13C206'.                                     
           DISPLAY  SPACES.                                                     
           DISPLAY 'TOTAL BP13F310 RECS READ     = ' WS-CNT-F310-READ.          
           DISPLAY 'TOTAL BP13F310 ERR RECS      = ' WS-CNT-F310-ERROR.         
           DISPLAY 'TOTAL BP13K800 RECS READ     = ' WS-CNT-K800-READ.          
           DISPLAY 'TOTAL BP13K820 RECS READ     = ' WS-CNT-K820-READ.          
                                                                                
           CLOSE BP13F310                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13L206.                                                      
                                                                                
           IF K800-STATUS NOT = 0                                               
              MOVE K800-STATUS TO RETURN-CODE                                   
              DISPLAY 'CLOSE FAILED FOR BP13K800 ' K800-STATUS                  
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
