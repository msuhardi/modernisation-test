      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.   BP13C002.                                                  
      *AUTHOR.       SATHAPPAN.                                                 
      *DATE-WRITTEN. 18/02/02.                                                  
                                                                                
      ***********************************************************               
      *  INPUT FILE :- BP13F740                                 *               
      *                BP13K853                                 *               
      *                BP13K410                                 *               
      *  I-O           BP13K665                                 *               
      *  OUTPUT FILE:- BP04K010                                 *               
      *                BP13L002                                 *               
      *                                                         *               
      ***********************************************************               
      *                                                         *               
      * CHG NO   BY  DATE        REMARKS                        *               
      * ------   --- ----------  -----------------------------  *               
      * BP132098 AS1 18/02/2002  NEW PROGRAM.                   *               
      * BP132113 LSB 01/04/2002  DEBARMENT PERIOD NUMERIC FIELD *               
      * BP135599 PCL 11/12/2014  TO UPDATE K010-INTERFACE-SYS   *               
      ***********************************************************               
                                                                                
      *************************                                                 
       ENVIRONMENT DIVISION.                                                    
      *************************                                                 
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *************************                                                 
       INPUT-OUTPUT SECTION.                                                    
      *************************                                                 
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
                                                                                
           SELECT BP13K853  ASSIGN TO BP13K853                                  
                  ORGANIZATION     IS INDEXED                                   
                  ACCESS MODE      IS DYNAMIC                                   
                  RECORD KEY       IS K853-KEY-FLD                              
                  FILE STATUS      IS WS-K853-STATUS.                           
                                                                                
           SELECT BP13K665 ASSIGN TO BP13K665                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS WS-K665-KEY-FLD                            
                  FILE STATUS     IS WS-K665-STATUS.                            
                                                                                
           SELECT BP13K410 ASSIGN TO BP13K410                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K410-KEY-FLD                               
                  FILE STATUS     IS WS-K410-STATUS.                            
                                                                                
           SELECT BP04K010 ASSIGN TO BP04K010                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K010-PRIME-KEY                             
                  FILE STATUS     IS WS-K010-STATUS.                            
                                                                                
           SELECT BP13L002 ASSIGN TO BP13L002.                                  
                                                                                
                                                                                
      *************************                                                 
       DATA DIVISION.                                                           
      *************************                                                 
       FILE SECTION.                                                            
                                                                                
       FD  BP13F740   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 500 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD                                
                      RECORDING MODE IS F.                                      
       COPY BP13F740.                                                           
                                                                                
       FD  BP13K665   RECORD CONTAINS 50 CHARACTERS.                            
       01  WS-BP13K665-REC.                                                     
           05  WS-K665-KEY-FLD         PIC X(08).                               
           05  FILLER                  PIC X(42).                               
                                                                                
       FD  BP13K853   RECORD CONTAINS 250 CHARACTERS.                           
       COPY BP13K853.                                                           
                                                                                
       FD  BP13K410   RECORD CONTAINS 2000 CHARACTERS.                          
       COPY BP13K410.                                                           
                                                                                
       FD  BP04K010   RECORD CONTAINS 300 CHARACTERS.                           
       COPY P04K010.                                                            
                                                                                
       FD BP13L002    RECORD CONTAINS 132 CHARACTERS                            
                      RECORDING MODE IS F.                                      
       01 BP13L002-REC                   PIC X(132).                            
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F740-EOF               PIC X    VALUE 'N'.                    
               88 F740-EOF                        VALUE 'Y'.                    
           05  WS-FOUND                  PIC X    VALUE SPACES.                 
           05  WS-K410-FOUND             PIC X    VALUE 'N'.                    
           05  WS-DEBARMENT-TAG          PIC X    VALUE SPACES.                 
           05  WS-DEBAR-TAG              PIC X    VALUE SPACES.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ-CNT-F740          PIC 9(6) VALUE ZEROS.                  
           05  WS-K665-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-K410-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-K665-WRITE             PIC 9(6) VALUE ZEROS.                  
           05  WS-K853-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-K010-WRITE             PIC 9(6) VALUE ZEROS.                  
           05  WS-LINE-CTR               PIC 9(6) VALUE 60.                     
           05  WS-PAGE-CTR               PIC 9(6) VALUE ZEROS.                  
           05  WS-SEQ-NO                 PIC 9(6) VALUE ZEROS.                  
           05  WS-PREV-REGN              PIC X(8) VALUE SPACES.                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K665-STATUS            PIC 99   VALUE ZEROS.                  
           05  WS-K410-STATUS            PIC 99   VALUE ZEROS.                  
           05  WS-K853-STATUS            PIC 99   VALUE ZEROS.                  
           05  WS-K010-STATUS            PIC 99   VALUE ZEROS.                  
                                                                                
       01  WS-CURR-DATE.                                                        
           05 WS-CURR-CC           PIC 9(02).                                   
           05 WS-YYMMDD.                                                        
              10 WS-CURR-YY        PIC 9(02).                                   
              10 WS-CURR-MM        PIC 9(02).                                   
              10 WS-CURR-DD        PIC 9(02).                                   
                                                                                
       COPY BP13K665.                                                           
                                                                                
      *--------------------------------------------------------------           
      *   REPORT LAYOUT                                                         
      *--------------------------------------------------------------           
       01  L002-PR-HEAD-01.                                                     
           05  FILLER              PIC X(8)      VALUE 'BP13L002'.              
           05  FILLER              PIC X(5)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                    'S Y S T E M   O F   '.                     
           05  FILLER              PIC X(19)     VALUE                          
                                    'C O M M I T M E N T'.                      
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  L002-DATE           PIC X(10).                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  L002-PAGENO         PIC ZZZZ9 .                                  
                                                                                
       01  L002-PR-HEAD-02.                                                     
           05  FILLER              PIC X(30)     VALUE SPACES .                 
           05  FILLER              PIC X(50)     VALUE                          
               'DEBARMENT REPORT FOR MARRIED CHILD PRIORITY'.                   
           05  FILLER              PIC X(30)     VALUE                          
               'SALES UNIT - APPLICATION TEAM '.                                
                                                                                
       01  L002-PR-HEAD-04.                                                     
           05  FILLER              PIC X(38)     VALUE                          
               'SEQ.    REGN      REF-NO  NAME'.                                
           05  FILLER              PIC X(20)     VALUE SPACES .                 
           05  FILLER              PIC X(60)     VALUE                          
               'I/C NO.    DEB-REF      REASON-CODE EFF-DATE   PERIOD'.         
           05  FILLER              PIC X(09)     VALUE SPACES.                  
                                                                                
       01  L002-PR-DTL-01.                                                      
           05  L002-SEQ-NO         PIC ZZZZ.                                    
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L002-NUM-REGN       PIC X(08).                                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  L002-NUM-REF        PIC X(06).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L002-NME-OCCP       PIC X(30).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L002-NUM-NRIC       PIC X(09).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L002-DEB-REF-NO     PIC X(13).                                   
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  L002-DEB-CODE       PIC X(02).                                   
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  L002-EFF-DATE       PIC X(10).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L002-PERIOD         PIC X(03).                                   
                                                                                
       01  L002-PR-LINE            PIC X(132)    VALUE ALL '-'.                 
                                                                                
      *************************                                                 
       PROCEDURE DIVISION.                                                      
      *************************                                                 
                                                                                
      *---------------*                                                         
       000-MAIN-LOGIC.                                                          
      *---------------*                                                         
            PERFORM 100-OPEN-FILES   THRU 100-EXIT.                             
            PERFORM 200-READ-F740    THRU 200-EXIT.                             
            PERFORM 300-PROCESS-K665 THRU 300-EXIT                              
                    UNTIL F740-EOF.                                             
            PERFORM 999-CLOSE-FILES  THRU 999-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       100-OPEN-FILES.                                                          
      *---------------*                                                         
                                                                                
           OPEN INPUT  BP13F740                                                 
                       BP13K853                                                 
                       BP13K410                                                 
                I-O    BP13K665                                                 
                       BP04K010                                                 
                OUTPUT BP13L002.                                                
                                                                                
           IF WS-K665-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K665-STATUS ' WS-K665-STATUS              
              MOVE WS-K665-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           IF WS-K410-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K410-STATUS ' WS-K410-STATUS              
              MOVE WS-K410-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           IF WS-K853-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K853-STATUS ' WS-K853-STATUS              
              MOVE WS-K853-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           IF WS-K010-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K010-STATUS ' WS-K010-STATUS              
              MOVE WS-K010-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE.                          
                                                                                
           STRING WS-CURR-DD '/'                                                
                  WS-CURR-MM '/'                                                
                  WS-CURR-CC                                                    
                  WS-CURR-YY DELIMITED BY SIZE                                  
                             INTO L002-DATE.                                    
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------*                                                          
       200-READ-F740.                                                           
      *--------------*                                                          
                                                                                
           READ BP13F740 AT END                                                 
                MOVE 'Y' TO WS-F740-EOF                                         
                GO TO 200-EXIT.                                                 
                                                                                
           ADD   1     TO WS-READ-CNT-F740.                                     
           MOVE SPACES TO WS-FOUND.                                             
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       300-PROCESS-K665.                                                        
      *----------------*                                                        
                                                                                
           MOVE LOW-VALUES                 TO K853-NUM-NRIC.                    
           MOVE F740-NUM-REGN              TO K853-NUM-REGN.                    
                                                                                
           START BP13K853 KEY IS >= K853-KEY-FLD                                
                 INVALID KEY                                                    
                 DISPLAY 'READING ERROR, K853-STATUS ' WS-K853-STATUS           
                 MOVE WS-K853-STATUS       TO RETURN-CODE                       
                 PERFORM 999-CLOSE-FILES THRU 999-EXIT.                         
                                                                                
           MOVE 'N' TO WS-DEBAR-TAG.                                            
           PERFORM 310-PROCESS-K853   THRU 310-EXIT                             
                   UNTIL K853-NUM-REGN NOT = F740-NUM-REGN                      
                   OR    K853-KEY-FLD      = HIGH-VALUES.                       
                                                                                
           PERFORM 200-READ-F740  THRU 200-EXIT.                                
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       310-PROCESS-K853.                                                        
      *----------------*                                                        
                                                                                
            READ BP13K853 NEXT                                                  
                 AT END MOVE HIGH-VALUES TO K853-KEY-FLD                        
                 GO TO 310-EXIT.                                                
                                                                                
            ADD 1 TO WS-K853-READ                                               
            IF K853-NUM-REGN = F740-NUM-REGN                                    
               IF WS-DEBAR-TAG = 'N'                                            
                  PERFORM 400-GET-DEBARMENT-NO                                  
                  MOVE 'Y' TO WS-DEBAR-TAG                                      
               END-IF                                                           
               PERFORM 500-PREPARE-DEBARMENT-REC    THRU 500-EXIT               
            END-IF.                                                             
                                                                                
       310-EXIT.                                                                
            EXIT.                                                               
                                                                                
      *-------------------*                                                     
       400-GET-DEBARMENT-NO.                                                    
      *-------------------*                                                     
                                                                                
           MOVE  LOW-VALUES    TO WS-K665-KEY-FLD.                              
           START BP13K665 KEY IS >= WS-K665-KEY-FLD                             
                 INVALID KEY                                                    
                 DISPLAY 'READING ERROR, K665-STATUS ' WS-K665-STATUS           
                 MOVE WS-K665-STATUS       TO RETURN-CODE                       
                 PERFORM 999-CLOSE-FILES THRU 999-EXIT.                         
                                                                                
           PERFORM 410-READ-BP13K665 THRU 410-EXIT                              
                   UNTIL WS-K665-KEY-FLD = HIGH-VALUES.                         
                                                                                
           IF K665-KEY-FLD = SPACES OR LOW-VALUES                               
              MOVE 0                TO  K665-NUM-LAST-DEBREF-ID                 
           END-IF.                                                              
                                                                                
           ADD   1                  TO  K665-NUM-LAST-DEBREF-ID.                
           MOVE  'SL'               TO  K665-NUM-DEBREF-AO                      
                                        K665-NUM-LAST-DEBREF-AO.                
           MOVE  WS-CURR-DATE       TO  K665-DTE-ISSUE-DEBARMENT.               
      *    MOVE  '20020214'         TO  K665-DTE-ISSUE-DEBARMENT.               
           MOVE  WS-CURR-DATE(1:4)  TO  K665-NUM-LAST-DEBREF-YR.                
           MOVE  'P13C002'          TO  K665-NUM-USERID.                        
                                                                                
           IF WS-DEBARMENT-TAG = 'Y'                                            
              REWRITE WS-BP13K665-REC FROM BP13K665-REC                         
           ELSE                                                                 
              WRITE   WS-BP13K665-REC FROM BP13K665-REC.                        
                                                                                
           EVALUATE WS-K665-STATUS                                              
           WHEN 00                                                              
              MOVE  'Y'               TO  WS-DEBARMENT-TAG                      
              ADD   1                 TO  WS-K665-WRITE                         
                                                                                
           WHEN 22                                                              
              REWRITE WS-BP13K665-REC FROM BP13K665-REC                         
                      INVALID KEY                                               
                      DISPLAY 'REWRITING ERROR, K665-STATUS '                   
                      WS-K665-STATUS                                            
                      MOVE WS-K665-STATUS       TO RETURN-CODE                  
                      PERFORM 999-CLOSE-FILES THRU 999-EXIT                     
              END-REWRITE                                                       
              ADD   1                 TO  WS-K665-WRITE                         
              MOVE  'Y'               TO   WS-DEBARMENT-TAG                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'WRITING ERROR, K665-STATUS ' WS-K665-STATUS              
              MOVE WS-K665-STATUS     TO   RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT                             
           END-EVALUATE.                                                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       410-READ-BP13K665.                                                       
      *------------------*                                                      
                                                                                
           READ BP13K665 NEXT                                                   
                AT END MOVE HIGH-VALUES TO WS-K665-KEY-FLD                      
                GO TO 410-EXIT.                                                 
                                                                                
           ADD 1 TO WS-K665-READ.                                               
                                                                                
           IF WS-K665-KEY-FLD NOT = SPACES AND LOW-VALUES                       
              MOVE WS-BP13K665-REC TO BP13K665-REC.                             
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------*                                               
       500-PREPARE-DEBARMENT-REC.                                               
      *-------------------------*                                               
                                                                                
           MOVE SPACES TO P04K010-REC.                                          
           MOVE F740-NUM-REGN           TO K410-KEY-FLD.                        
           PERFORM 505-READ-BP13K410    THRU 505-EXIT.                          
           PERFORM 510-WRITE-BP04K010   THRU 510-EXIT.                          
           PERFORM 700-PRINT-L002       THRU 700-EXIT.                          
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       505-READ-BP13K410.                                                       
                                                                                
           READ  BP13K410 KEY IS K410-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K410-STATUS                                              
             WHEN  00                                                           
                    MOVE 'Y'   TO WS-K410-FOUND                                 
                    ADD   1    TO WS-K410-READ                                  
             WHEN  23                                                           
                    DISPLAY 'BP13K410 RECORD NOT FOUND  = '                     
                                 WS-K410-STATUS                                 
                    DISPLAY '    K410-KEY-FLD  = '   K410-KEY-FLD               
             WHEN  OTHER                                                        
                    DISPLAY 'ERROR READING BP13K410 = '                         
                                 WS-K410-STATUS                                 
                    DISPLAY '      K410-KEY-FLD     = '                         
                                                    K410-KEY-FLD                
           END-EVALUATE.                                                        
       505-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       510-WRITE-BP04K010.                                                      
      *------------------*                                                      
           IF WS-K410-FOUND = 'Y'                                               
              MOVE 'MB'                  TO K010-REASON-CODE                    
              MOVE '060'                 TO K010-PERIOD-DEBAR                   
              MOVE K410-DTE-KEY-ISSUE    TO K010-EFF-DATE                       
           ELSE                                                                 
              MOVE 'MA'                  TO K010-REASON-CODE                    
              MOVE '999'                 TO K010-PERIOD-DEBAR                   
              MOVE WS-CURR-DATE          TO K010-EFF-DATE                       
      *       MOVE '20020214'            TO K010-EFF-DATE                       
           END-IF.                                                              
           MOVE 'N'                      TO WS-K410-FOUND.                      
           MOVE K853-NUM-NRIC            TO K010-NUM-UINFIN.                    
           MOVE K853-NME-OCCP            TO K010-NAME.                          
           MOVE K665-NUM-LAST-DEBARMENT  TO K010-DEB-REF-NO.                    
           MOVE '1'                      TO K010-PROP-TYPE.                     
           MOVE ZEROS                    TO K010-AMT-OWING.                     
           MOVE 'BP13'                   TO K010-INTERFACE-SYS.                 
           MOVE WS-CURR-DATE             TO K010-INCLUSION-DATE                 
                                            K010-LUPDATE.                       
                                                                                
           WRITE P04K010-REC.                                                   
                                                                                
           EVALUATE WS-K010-STATUS                                              
           WHEN 00                                                              
                ADD 1 TO WS-K010-WRITE                                          
           WHEN 22                                                              
           WHEN 02                                                              
                DISPLAY 'DUPLICATE RECORD BP04K010: ' K010-PRIME-KEY            
           WHEN OTHER                                                           
                MOVE WS-K010-STATUS       TO RETURN-CODE                        
                DISPLAY 'ERROR IN WRITING BP04K010: ' K010-PRIME-KEY            
                DISPLAY F740-NUM-REGN                                           
                DISPLAY WS-K010-STATUS                                          
                PERFORM 999-CLOSE-FILES THRU 999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       510-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       700-PRINT-L002.                                                          
      *---------------*                                                         
           MOVE K665-NUM-LAST-DEBARMENT  TO L002-DEB-REF-NO.                    
           MOVE K010-REASON-CODE         TO L002-DEB-CODE.                      
           MOVE K853-NUM-NRIC            TO L002-NUM-NRIC.                      
           MOVE K853-NME-OCCP            TO L002-NME-OCCP.                      
           MOVE F740-NUM-REF             TO L002-NUM-REF.                       
           MOVE K010-PERIOD-DEBAR        TO L002-PERIOD.                        
                                                                                
           STRING K010-EFF-DATE(7:2) '-'                                        
                  K010-EFF-DATE(5:2) '-'                                        
                  K010-EFF-DATE(1:4) DELIMITED BY SIZE                          
                  INTO L002-EFF-DATE.                                           
                                                                                
           IF WS-LINE-CTR > 55                                                  
              ADD  1              TO   WS-PAGE-CTR                              
              MOVE 5              TO   WS-LINE-CTR                              
              MOVE WS-PAGE-CTR    TO   L002-PAGENO                              
              WRITE BP13L002-REC  FROM L002-PR-HEAD-01 AFTER PAGE               
              WRITE BP13L002-REC  FROM L002-PR-HEAD-02                          
              WRITE BP13L002-REC  FROM L002-PR-LINE    AFTER 2                  
              WRITE BP13L002-REC  FROM L002-PR-HEAD-04                          
              WRITE BP13L002-REC  FROM L002-PR-LINE                             
           END-IF.                                                              
                                                                                
           IF F740-NUM-REGN  NOT = WS-PREV-REGN                                 
              ADD  1                        TO WS-SEQ-NO                        
              MOVE WS-SEQ-NO                TO L002-SEQ-NO                      
              MOVE F740-NUM-REGN            TO L002-NUM-REGN                    
           ELSE                                                                 
              MOVE ZEROES                   TO L002-SEQ-NO                      
              MOVE SPACES                   TO L002-NUM-REGN                    
           END-IF                                                               
                                                                                
           WRITE BP13L002-REC  FROM L002-PR-DTL-01.                             
           ADD   1             TO   WS-LINE-CTR .                               
                                                                                
           MOVE   F740-NUM-REGN             TO WS-PREV-REGN.                    
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       999-CLOSE-FILES.                                                         
      *----------------*                                                        
                                                                                
           CLOSE BP13F740                                                       
                 BP13K665                                                       
                 BP13K410                                                       
                 BP13K853                                                       
                 BP04K010                                                       
                 BP13L002.                                                      
                                                                                
           IF WS-K665-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K665, STATUS ' WS-K665-STATUS          
              MOVE WS-K665-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K853-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K853, STATUS ' WS-K853-STATUS          
              MOVE WS-K853-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP04K010, STATUS ' WS-K010-STATUS          
              MOVE WS-K010-STATUS        TO RETURN-CODE.                        
                                                                                
           DISPLAY '*------------------------------------------------'.         
           DISPLAY 'DATE : ' L002-DATE  '   ' 'BP13C002'                        
           DISPLAY '*------------------------------------------------'.         
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF REC READ        BP13F740   = '                        
                    WS-READ-CNT-F740.                                           
           DISPLAY 'NO OF REC READ        BP13K665   = '                        
                    WS-K665-READ                                                
           DISPLAY 'NO OF REC READ        BP13K410   = '                        
                    WS-K410-READ                                                
           DISPLAY 'NO OF REC WRITTEN     BP13K665   = '                        
                    WS-K665-WRITE                                               
           DISPLAY 'NO OF REC READ        BP13K853   = '                        
                    WS-K853-READ                                                
           DISPLAY 'NO OF REC WRITTEN     BP04K010   = '                        
                    WS-K010-WRITE                                               
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
