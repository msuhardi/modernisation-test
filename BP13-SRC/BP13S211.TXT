 /*****************************************************************/            
 /*   SYSTEM OF COMMITMENT (BP13)                                 */            
 /*===============================================================*/            
 /* PROGRAM ID    :  BP13S211                                     */            
 /* AUTHOR        :  GV                                           */            
 /* DATE WRITTEN  :  05/08/1999                                   */            
 /* OBJECTIVE     :  SUMMARY REPORT ON ACCEPTED/UNACCEPTED FLATS  */            
 /*                                                               */            
 /* INPUT FILE    :  BM06F120                                     */            
 /*                  BP13K060                                     */            
 /*                  BM06F260                                     */            
 /* OUTPUT FILE   :  BP13L211                                     */            
 /*---------------------------------------------------------------*/            
 /* CHG-NO   DATE     BY   DESCRIPTION                            */            
 /* -------- -------- ---- ----------                             */            
 /* BP130712 19990805 GV   NEW PROGRAM                            */            
 /*****************************************************************/            
  DATA BM06F120;                                                                
      INFILE BM06F120 END=EOF;                                                  
      INPUT  @615 NUM_NT   $CHAR3.                                              
             @128 FLT_STAT $CHAR2.                                              
             @149 RP_CODE  $CHAR2.                                              
             @291 DTE_ALOC $CHAR8.                                              
             @299 DTE_ACPT $CHAR8.                                              
             @606 ROOM_TYP $CHAR2.;                                             
                                                                                
      LENGTH FLATSTAT $2.;                                                      
      LENGTH NUM 3;                                                             
                                                                                
      NUM=1; /* FACILITATES AS ANALYSIS VARIABLE DURING TABULATING */           
                                                                                
      IF _N_ = 1 THEN                                                           
        DO;                                                                     
           INFILE BM06F260;                                                     
           INPUT  @1  DTE_YY $CHAR4.      @5  DTE_MM $CHAR2.                    
                  @7  DTE_DD $CHAR2.;                                           
                                                                                
           LENGTH MONTH $2.;                                                    
           MONTH=DTE_MM;                                                        
        END;                                                                    
                                                                                
      SELECT (FLT_STAT);                                                        
        WHEN ('RP')                                                             
             DO;                                                                
               SELECT (RP_CODE);                                                
                 WHEN ('40')     FLATSTAT='BB';                                 
                 OTHERWISE       FLATSTAT='RP';                                 
               END;                                                             
             END;                                                               
        WHEN ('ST')              FLATSTAT='ST';                                 
        OTHERWISE                FLATSTAT='  ';                                 
      END;                                                                      
                                                                                
      LENGTH K060KEY  $5.;                                                      
      K060KEY='02'||NUM_NT;                                                     
                                                                                
      INFILE BP13K060 VSAM KEY=K060KEY;                                         
      RC=_IORC_;FBC=_FDBK_;                                                     
      IF RC=0 THEN                                                              
         DO;                                                                    
           INPUT @06 NEWTOWN $CHAR20.;                                          
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
         NEWTOWN='INVALID TOWN';                                                
         _ERROR_=0;                                                             
         END;                                                                   
                                                                                
      LENGTH ACPTED $1.;                                                        
      IF DTE_ACPT^>0 THEN                                                       
      DO;                                                                       
         ACPTED='B';                                                            
      END;                                                                      
      ELSE                                                                      
      DO;                                                                       
         ACPTED='A';                                                            
      END;                                                                      
                                                                                
      LENGTH MMALOC $2.;                                                        
      MMALOC=SUBSTR(DTE_ALOC,5,2);                                              
                                                                                
      RETAIN FLATSTAT ACPTED RC FBC NUM MMALOC MONTH;                           
                                                                                
      IF MMALOC=MONTH  AND  ROOM_TYP='03'  AND                                  
                     (FLATSTAT='RP' OR FLATSTAT='ST' OR FLATSTAT='BB');         
                                                                                
   /* PROC SORT; BY NEWTOWN; */                                                 
      PROC PRINT;                                                               
                                                                                
   OPTIONS NONUMBER DATE LS=100;                                                
   DATA S1;                                                                     
   SET   BM06F120 END=EOF; /*BY NEWTOWN;*/                                      
                                                                                
   PROC FORMAT;                                                                 
        VALUE $ACCEPT 'A'='NO: OF FLATS ACCEPTED'                               
                      'B'='NO: OF FLATS NOT SELECTED';                          
                                                                                
   PROC PRINTTO FILE=BP13L211;                                                  
                                                                                
   PROC TABULATE;                                                               
   TITLE1 "BP13L211     HDB3          SYSTEM OF COMMITMENT      ";              
   TITLE2 "NO OF ACCEPTED/UNACCEPTED FLATS OVER NEWTOWN BASED ON FLAT-STATUS";  
   /***************************/                                                
   /* VARIABLE CLASSIFICATION */                                                
   /***************************/                                                
                                                                                
   CLASS NEWTOWN ACPTED FLATSTAT;                                               
   VAR NUM;                                                                     
                                                                                
   FORMAT ACPTED $ACCEPT.;                                                      
                                                                                
   /********************/                                                       
   /* TABLE DEFINITION */                                                       
   /********************/                                                       
                                                                                
   TABLE NEWTOWN ALL='TOTAL',                                                   
     ALL='NO OF FLATS OFFERED'*((FLATSTAT=' ' ALL='TOTAL')*NUM=' '*F=5.)        
     ACPTED=' '*((FLATSTAT=' ' ALL='TOTAL')*NUM=' '*F=5.)/                      
                                     RTS=25 MISSTEXT='0';                       
                                                                                
   LABEL FLATSTAT=' '                                                           
           ACPTED=' ';                                                          
                                                                                
   KEYLABEL SUM=' '                                                             
              N=' ';                                                            
