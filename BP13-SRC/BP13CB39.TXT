       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB39.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  17/05/2017.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVES   : REMOVE DUPLICATE RECORD AND GET THE LATEST *            
      *                 DATE ACCEPTANCE                            *            
      *                                                            *            
      *  INPUT FILE   : BP13F213 - RECORDS TO BE PROCESSED         *            
      *  OUTPUT FILES : P13F213A - NO DUPLICATE RECORDS            *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG-REF# BY   DATE     DESCRIPTION                         *            
      * -------- ---- -------- ----------------------------------- *            
      * BP136800 SMR2 17052017 NEW PROGRAM                         *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F213 ASSIGN TO BP13F213.                                  
           SELECT P13F213A ASSIGN TO P13F213A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F213                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 150 CHARACTERS                                      
            LABEL RECORDS   ARE STANDARD.                                       
       COPY BP13F213.                                                           
                                                                                
       FD   P13F213A                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 150 CHARACTERS                                      
            LABEL RECORDS   ARE STANDARD.                                       
       01   P13F213A-REC                   PIC X(150).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-COUNTERS.                                                         
           05  WS-F213-READ                PIC 9(06)  VALUE ZEROES.             
           05  WS-F213-DUPL                PIC 9(06)  VALUE ZEROES.             
           05  WS-F213A-WRITE              PIC 9(06)  VALUE ZEROES.             
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F213-EOF                 PIC X(01)  VALUE 'N'.                
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-PREV-NRIC                PIC X(09)  VALUE SPACES.             
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           PERFORM 1000-INIT-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F213      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
             UNTIL WS-F213-EOF = 'Y'.                                           
           PERFORM 4000-CLOSE-ROUTINE      THRU 4000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-INIT-ROUTINE.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F213                                                 
                OUTPUT P13F213A.                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-BP13F213.                                                      
      *-------------------------------------------------------------            
           READ BP13F213                                                        
             AT END                                                             
                MOVE 'Y'                   TO WS-F213-EOF                       
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                           TO WS-F213-READ.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           IF (F213-NUM-NRIC = SPACES OR LOW-VALUES) OR                         
              (F213-NUM-NRIC NOT = WS-PREV-NRIC)                                
              MOVE F213-NUM-NRIC           TO WS-PREV-NRIC                      
              WRITE P13F213A-REC           FROM BP13F213-REC                    
              ADD 1                        TO WS-F213A-WRITE                    
           ELSE                                                                 
              ADD 1                        TO WS-F213-DUPL                      
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F213      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY '********** SUMMARY REPORT FOR BP13CB39 **********'.         
           DISPLAY ' NO OF REC READ FROM BP13F213           = '                 
                     WS-F213-READ.                                              
           DISPLAY ' NO OF REC WRITTEN IN P13F213A          = '                 
                     WS-F213A-WRITE.                                            
           DISPLAY ' NO OF REC W/ DUPLICATE FROM BP13F213   = '                 
                     WS-F213-DUPL.                                              
           DISPLAY '*************************************************'.         
                                                                                
           CLOSE  BP13F213                                                      
                  P13F213A.                                                     
                                                                                
           STOP RUN.                                                            
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
