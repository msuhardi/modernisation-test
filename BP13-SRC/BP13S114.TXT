 /*================================================================*/           
 /* SAS PROGRAM TO PRINT MONTHLY REPORT FOR TRANSACTION ON         */           
 /* REGN FEE, BOOKING FEE, COMMITMENT DEPOSIT FOR THE MONTH.       */           
 /* BP130130 16/07/96 LJL TO SORT BY TRANS-TYPE.                   */           
 /* BP130418 25/08/98 RHB TO CATER FOR Y2000.                      */           
 /* BP131039 16/07/01 SN  REMOVE NT                                */           
 /*================================================================*/           
  OPTIONS  NODATE NONUMBER;                                                     
                                                                                
     PROC FORMAT;                                                               
        PICTURE DOL LOW-<0 = '000,000,009.99'                                   
                             (PREFIX='-$' MULT=100.00000000001)                 
                    0-HIGH = '000,000,009.99'                                   
                             (PREFIX='$' MULT=100.00000000001);                 
                                                                                
  DATA BP13F127 ;                                                               
     INFILE BP13F127;                                                           
     INPUT  @01  TRANCDE  $CHAR2.      @05  ORIGNO   $CHAR8.                    
            @13  POSTDTE  YYMMDD8.     @25  NRIC     $CHAR9.                    
            @34  NAME     $CHAR32.     @66  RECPT    $CHAR14.                   
            @66  DOCNO    $CHAR10.     @81  AMTRE    ZD9.2                      
            @90  CRDR     $CHAR1.      @91  TRANDTE  YYMMDD8.                   
            @99  PAYTYPE  $CHAR3.;                                              
                                                                                
     FORMAT AMTRE DOL.;                                                         
                                                                                
     IF TRANCDE=12;                                                             
                                                                                
     PROC SORT; BY  PAYTYPE  TRANDTE DOCNO;                                     
                                                                                
     DATA _NULLS_;                                                              
     SET BP13F127 END=FINAL;                                                    
     BY PAYTYPE TRANDTE DOCNO;                                                  
     OPTIONS PAGESIZE=55 LS=132 NODATE;                                         
     FILE BP13L114   HEADER=HH NOTITLES;                                        
                                                                                
     PAYAMT+AMTRE;                                                              
     SUBAMT+AMTRE;                                                              
     TOTAMT+AMTRE;                                                              
     GRDTOT+AMTRE;                                                              
     N+1;                                                                       
     M+1;                                                                       
     P+1;                                                                       
     IF FIRST.PAYTYPE THEN PUT _PAGE_;                                          
     ELSE                                                                       
     IF FIRST.DOCNO THEN PUT _PAGE_;                                            
        L+1;                                                                    
        PUT @03  TRANDTE  DDMMYY10.     @26  ORIGNO                             
            @43  NAME                  @74  NRIC                                
            @100 AMTRE DOL.;                                                    
                                                                                
        IF L=50 THEN DO;                                                        
           PUT _PAGE_;                                                          
           L=0;                                                                 
           END;                                                                 
                                                                                
        IF LAST.TRANDTE THEN DO;                                                
           PUT //@03 'NUMBER OF CASES = ' @28 N                                 
           @61 'SUB-TOTAL FOR ' TRANDTE DDMMYY10.                               
           @100 SUBAMT DOL.//;                                                  
           SUBAMT=0;                                                            
           N=0;                                                                 
           END;                                                                 
                                                                                
        IF LAST.PAYTYPE THEN DO;                                                
           PUT //@03 'NUMBER OF CASES = ' @28 P                                 
           @61 'SUB-TOTAL FOR ' PAYTYPE                                         
           @100 PAYAMT DOL.//;                                                  
           PAYAMT=0;                                                            
           P=0;                                                                 
           END;                                                                 
                                                                                
        IF FINAL THEN DO;                                                       
           PUT //@03 'TOTAL NUMBER OF CASES = ' @28 M                           
           @61 'GRAND-TOTAL ' @100 GRDTOT DOL.;                                 
           END;                                                                 
        RETURN;                                                                 
                                                                                
  HH:                                                                           
     PAGE + 1;                                                                  
     CURRENT =DATE();                                                           
     YR=YEAR(POSTDTE);                                                          
     PUT @1   'BP13L114' @15 'HDBCAT 3'                                         
         @47  'S Y S T E M   O F   C O M M I T M E N T'                         
         @98  'DATE :' @105 CURRENT DDMMYY10.                                   
         @119 'PAGE :' @126 PAGE 3./;                                           
     PUT @41  'SOC - CPF WITHDRAWALS POSTED FOR '                               
         @74  POSTDTE WORDDATE9. @85 YR//;                                      
     PUT @1   'TRANSACTION TYPE : 'PAYTYPE//;                                   
     PUT @51  'JOURNAL NO ISSUED = ' RECPT//;                                   
     PUT @1   'EFFECTIVE DATE';                                                 
     PUT @1   'FROM CPF BOARD'                                                  
         @22  'ORIGINAL REGN-NO'                                                
         @47  'NAME'                                                            
         @75  'CPF NO '                                                         
         @107 'AMOUNT';                                                         
     PUT @1   '--------------'                                                  
         @22  '----------------'                                                
         @47  '----'                                                            
         @75  '------'                                                          
         @107 '------';                                                         
     RETURN;                                                                    
