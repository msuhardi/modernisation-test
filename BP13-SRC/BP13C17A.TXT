       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C172.                                                 
      *AUTHOR.        CKK7.                                                     
      *DATE-WRITTEN.  24/08/04.                                                 
      * ============================================================== *        
      * =           SOC - SYSTEM OF COMMITMENT (BP13)                = *        
      * ============================================================== *        
      *  OBJECTIVES : TO SEND EMAIL ALERT ON REQUEST FOR URGENT SOCA - *        
      *               APPOINTMENT BY APPLICANTS.                       *        
      *                                                                *        
      * -------------------------------------------------------------- *        
      *  INPUT FILES  - BP13F022                                       *        
      *               - BP13K800                                       *        
      *               - BM06K110                                       *        
      *               - BM06K100                                       *        
      *               - PIDB                                           *        
      *  I-O FILE     - BP13K022                                       *        
      *  OUTPUT FILES - P13MAIL1 (BP13.MAILOUT1.C17A.XXX)              *        
      *               - P13MAIL2 (BP13.MAILOUT2.C17A.XXX)              *        
      * ============================================================== *        
      * CHG REQ# DATE     BY   REMARKS                                 *        
      * -------- ----     --   -------                                 *        
      * BP135782 19/08/15 KAM4 NEW PROGRAM.                            *        
      * ============================================================== *        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3083.                                               
       OBJECT-COMPUTER. IBM-3083.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F022 ASSIGN       TO BP13F022.                            
                                                                                
           SELECT BP13K800 ASSIGN       TO BP13K800                             
                  ACCESS MODE           IS RANDOM                               
                  ORGANIZATION          IS INDEXED                              
                  RECORD KEY            IS K800-NUM-REGN                        
                  FILE STATUS           IS BP13K800-STATUS.                     
                                                                                
           SELECT BP13K022 ASSIGN       TO BP13K022                             
                  ACCESS MODE           IS RANDOM                               
                  ORGANIZATION          IS INDEXED                              
                  RECORD KEY            IS K022-KEY-FLD                         
                  FILE STATUS           IS BP13K022-STATUS.                     
                                                                                
           SELECT BM06K100 ASSIGN       TO BM06K100                             
                  ACCESS MODE           IS RANDOM                               
                  ORGANIZATION          IS INDEXED                              
                  RECORD KEY            IS K100-KEY-FLD                         
                  FILE STATUS           IS BM06K100-STATUS.                     
                                                                                
           SELECT BM06K110 ASSIGN       TO BM06K110                             
                  ACCESS MODE           IS RANDOM                               
                  ORGANIZATION          IS INDEXED                              
                  RECORD KEY            IS K110-KEY-FLD                         
                  FILE STATUS           IS BM06K110-STATUS.                     
                                                                                
           SELECT P13MAIL1 ASSIGN       TO P13MAIL1.                            
           SELECT P13MAIL2 ASSIGN       TO P13MAIL2.                            
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      ******************************************************************        
                                                                                
       FD  BP13F022                                                             
           RECORDING MODE  IS F                                                 
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13F022.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K022                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K022.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BM06K100                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K100.                                                           
                                                                                
       FD  P13MAIL1                                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           RECORDING  MODE  IS F.                                               
       01  MAIL-PRTREC                       PIC X(80).                         
                                                                                
       FD  P13MAIL2                                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           RECORDING  MODE  IS F.                                               
       01  MAIL-PRTREC2                      PIC X(80).                         
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  BP13K800-STATUS         PIC 9(2)    VALUE 0.                     
           05  BP13K022-STATUS         PIC 9(2)    VALUE 0.                     
           05  BM06K110-STATUS         PIC 9(2)    VALUE 0.                     
           05  BM06K100-STATUS         PIC 9(2)    VALUE 0.                     
           05  WS-F022-EOF             PIC X       VALUE 'N'.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-SNO                  PIC 9(5)    VALUE 0.                     
           05  WS-SUM-CNT              PIC 9(5)    VALUE 0.                     
           05  WS-BLANK-1SOCADTE       PIC 9(5)    VALUE 0.                     
           05  WS-F022-READ            PIC 9(8)    VALUE 0.                     
           05  WS-K800-NOTFND          PIC 9(8)    VALUE 0.                     
           05  WS-K022-NOTFND          PIC 9(8)    VALUE 0.                     
           05  WS-K110-NOTFND          PIC 9(8)    VALUE 0.                     
           05  WS-K100-NOTFND          PIC 9(8)    VALUE 0.                     
           05  WS-PIDB-NOTFND          PIC 9(8)    VALUE 0.                     
           05  WS-MAIL-WROTE           PIC 9(8)    VALUE 0.                     
           05  WS-K022-REWRITE         PIC 9(8)    VALUE 0.                     
           05  WS-LETTER-TYPE          PIC X(1)    VALUE SPACES.                
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-SYS-DATE             PIC X(08)   VALUE SPACES.                
           05  WS-SYSTEM-DATE          PIC X(08)   VALUE SPACES.                
           05  WS-PROCESS-REC          PIC X(01)   VALUE SPACES.                
           05  WS-RETCODE              PIC -ZZZZ.                               
           05  WS-PREV-OIC             PIC X(05)   VALUE SPACES.                
                                                                                
      *----------------------------------------------------------------*        
      *                 O U T P U T   L A Y O U T                      *        
      *----------------------------------------------------------------*        
       01  HEADER1.                                                             
      *----Production usage                                                     
           05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB1'.                 
           05  FILLER          PIC X(68)  VALUE SPACES.                         
                                                                                
      *  for testing usage                                                      
      *    05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB3'.                 
      *    05  FILLER          PIC X(68)  VALUE SPACES.                         
                                                                                
      *----for production usage                                                 
       01  HEADER2.                                                             
           05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
           05  SEND-ID         PIC X(04)  VALUE 'OPCP'.                         
           05  FILLER          PIC X(09)  VALUE '@SGPHDB1>'.                    
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
      *  for testing usage                                                      
      *    05  SEND-ID         PIC X(07)  VALUE 'CS93388'.                      
      *    05  FILLER          PIC X(09)  VALUE '@SGPHDB3>'.                    
      *    05  FILLER          PIC X(53)  VALUE SPACES.                         
                                                                                
       01  HEADER-DTL1-TO1.                                                     
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  TO-MAIL-ID-1    PIC X(33)  VALUE SPACES.                         
           05  FILLER          PIC X(38)  VALUE SPACES.                         
                                                                                
       01  HEADER-DTL2-CC1.                                                     
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  CC-MAIL-ID-1    PIC X(14)  VALUE 'TENG_Siew_Hong'.               
           05  FILLER          PIC X(12)  VALUE '@hdb.gov.sg>'.                 
                                                                                
       01  HEADER-DTL3.                                                         
           05  FILLER          PIC X(04)  VALUE 'DATA'.                         
           05  FILLER          PIC X(76)  VALUE SPACES.                         
                                                                                
       01  HEADER-DTL4.                                                         
           05  FILLER          PIC X(05)  VALUE 'FROM:'.                        
           05  FROM-MAIL-ID    PIC X(26)  VALUE                                 
               'SOC System - Email Alert'.                                      
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  HEADER-DTL5-TOA.                                                     
           05  FILLER          PIC X(04)  VALUE 'TO:<'.                         
           05  TO-MAIL-ID-A    PIC X(33)  VALUE SPACES.                         
           05  FILLER          PIC X(43)  VALUE SPACES.                         
                                                                                
       01  HEADER-DTL6-CCA.                                                     
           05  FILLER          PIC X(04)  VALUE 'CC:<'.                         
           05  CC-MAIL-ID-A    PIC X(14)  VALUE 'TENG_Siew_Hong'.               
           05  FILLER          PIC X(12)  VALUE '@hdb.gov.sg>'.                 
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  HEADER-DTL6-CCB.                                                     
           05  FILLER          PIC X(04)  VALUE 'CC:<'.                         
           05  CC-MAIL-ID-B    PIC X(22)  VALUE SPACES.                         
           05  FILLER          PIC X(54)  VALUE SPACES.                         
                                                                                
       01  HEADER-DTL7-SUBJECT-A.                                               
           05  FILLER          PIC X(10)  VALUE 'SUBJECT : '.                   
           05  SUBJECT-1       PIC X(58)  VALUE                                 
            'Flats are ready for SOCA. To follow up urgent SOCA cases.'.        
           05  FILLER          PIC X(12)  VALUE SPACES.                         
                                                                                
      ******************************************************************        
                                                                                
       01  BODY-DTL1-HEADER1.                                                   
           05 FILLER          PIC X(28)  VALUE                                  
              'Dear CRM'.                                                       
           05 FILLER          PIC X(52)  VALUE SPACES.                          
                                                                                
       01  BODY-DTL2-INTRO1-A.                                                  
           05 FILLER          PIC X(55)  VALUE                                  
              'Please follow up with applicants for urgent SOCA cases.'.        
           05 FILLER          PIC X(25)  VALUE SPACES.                          
                                                                                
       01  BODY-DTL3-TBL-HEADER1.                                               
           05 FILLER          PIC X(50)  VALUE                                  
              'S/NO  CSM    REGN NO   SCH ACC NO   SELECTION DATE'.             
           05 FILLER          PIC X(30)  VALUE SPACES.                          
                                                                                
       01  BODY-DTL3-TBL-HEADER2.                                               
           05 FILLER          PIC X(51)  VALUE                                  
              '****  ****   ********  ***********  ***************'.            
           05 FILLER          PIC X(29)  VALUE SPACES.                          
                                                                                
       01  BODY-DTL4-TBL-ROW1.                                                  
           05 BODY-SNO        PIC 9(3)   VALUE ZEROES.                          
           05 FILLER          PIC X(3)   VALUE SPACES.                          
           05 BODY-CSM        PIC X(5).                                         
           05 FILLER          PIC X(2)   VALUE SPACES.                          
           05 BODY-REGN       PIC X(8).                                         
           05 FILLER          PIC X(2)   VALUE SPACES.                          
           05 BODY-SCH-ACC    PIC X(11).                                        
           05 FILLER          PIC X(2)   VALUE SPACES.                          
           05 BODY-SOCADATE   PIC X(10).                                        
           05 FILLER          PIC X(34)  VALUE SPACES.                          
                                                                                
       01  BODY-DTL4-TBL-ROW2.                                                  
           05 FILLER          PIC X(5)   VALUE SPACES.                          
           05 BODY-ADDR       PIC X(75).                                        
                                                                                
       01  BODY-DTL2-END1-A.                                                    
           05 FILLER          PIC X(50)  VALUE                                  
              'Please forward cases to AT to schedule appointment'.             
           05 FILLER          PIC X(23)  VALUE                                  
              ' if cases are with you.'.                                        
           05 FILLER          PIC X(07)  VALUE SPACES.                          
                                                                                
       01  BODY-BLANK-LINE.                                                     
           05 FILLER          PIC X(80)  VALUE SPACES.                          
                                                                                
       01  FOOTER-DTL2-END.                                                     
           05 FILLER          PIC X      VALUE '.'.                             
                                                                                
                                                                                
      *-------------------------------------------------------------            
      * BP13C913 PARAMETERS                                                     
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH-ACC.                                              
              10 WS-LINK-NUM-SCH       PIC X(4).                                
              10 WS-LINK-NUM-ACC       PIC X(5).                                
       COPY P13COMM8.                                                           
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-CONTROL.                                                            
      *----------------------------------------------------------------*        
           PERFORM 1000-OPEN-FILES        THRU 1000-EXIT.                       
           PERFORM 2000-READ-BP13F022     THRU 2000-EXIT.                       
           PERFORM 3000-PROCESS           THRU 3000-EXIT                        
                   UNTIL WS-F022-EOF = 'Y'.                                     
                                                                                
                                                                                
           PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                       
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       1000-OPEN-FILES.                                                         
      *----------------------------------------------------------------*        
           OPEN INPUT  BP13F022                                                 
                       BP13K800                                                 
                       BM06K110                                                 
                       BM06K100                                                 
                I-O    BP13K022                                                 
                OUTPUT P13MAIL1                                                 
                       P13MAIL2.                                                
                                                                                
           IF BP13K800-STATUS NOT = ZEROES AND 97                               
              MOVE BP13K800-STATUS             TO RETURN-CODE                   
              DISPLAY 'OPEN BP13K800 FILE ERROR ' BP13K800-STATUS               
              PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           IF BP13K022-STATUS NOT = ZEROES AND 97                               
              MOVE BP13K022-STATUS             TO RETURN-CODE                   
              DISPLAY 'OPEN BP13K022 FILE ERROR ' BP13K022-STATUS               
              PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           IF BM06K110-STATUS NOT = ZEROES AND 97                               
              MOVE BM06K110-STATUS             TO RETURN-CODE                   
              DISPLAY 'OPEN BM06K110 FILE ERROR ' BM06K110-STATUS               
              PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           IF BM06K100-STATUS NOT = ZEROES AND 97                               
              MOVE BM06K100-STATUS             TO RETURN-CODE                   
              DISPLAY 'OPEN BM06K100 FILE ERROR ' BM06K100-STATUS               
              PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)     TO WS-SYSTEM-DATE.               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       2000-READ-BP13F022.                                                      
      *----------------------------------------------------------------*        
           READ BP13F022 AT END                                                 
           MOVE 'Y'                      TO WS-F022-EOF                         
           IF WS-F022-READ = ZERO                                               
              MOVE '04'                  TO RETURN-CODE                         
              DISPLAY 'INPUT FILE IS EMPTY!'                                    
           END-IF                                                               
           GO TO 2000-EXIT.                                                     
                                                                                
           ADD 1                         TO WS-F022-READ.                       
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3000-PROCESS.                                                            
      *----------------------------------------------------------------*        
           PERFORM 4000-CHECK-FF-FILES           THRU 4000-EXIT                 
           IF WS-PROCESS-REC = 'Y'                                              
                                                                                
              IF F022-NUM-OIC NOT = WS-PREV-OIC                                 
                 IF WS-SNO > ZEROES                                             
                    PERFORM 3200-WRITE-MAIL-TRAILER   THRU 3200-EXIT            
                 END-IF                                                         
                                                                                
                 PERFORM 3100-WRITE-EMAIL-HDR1   THRU 3100-EXIT                 
                                                                                
                 MOVE F022-NUM-OIC                 TO WS-PREV-OIC               
                 MOVE ZEROES                       TO WS-SNO                    
              END-IF                                                            
                                                                                
              MOVE SPACES                          TO BODY-DTL4-TBL-ROW1        
              INITIALIZE                              BODY-DTL4-TBL-ROW1        
              ADD 1                                TO WS-SNO                    
                                                                                
              MOVE WS-SNO                          TO BODY-SNO                  
              MOVE F022-NUM-OIC                    TO BODY-CSM                  
              MOVE F022-NUM-REGN                   TO BODY-REGN                 
              MOVE K800-NUM-SCH-ACC                TO BODY-SCH-ACC              
              STRING K110-DTE-ACCEPTANCE(7:2) '/'                               
                     K110-DTE-ACCEPTANCE(5:2) '/'                               
                     K110-DTE-ACCEPTANCE(1:4)                                   
                     DELIMITED BY SIZE INTO BODY-SOCADATE                       
              END-STRING                                                        
              WRITE MAIL-PRTREC FROM BODY-DTL4-TBL-ROW1                         
                                                                                
              STRING COMM8-NUM-BLK ', '                                         
                     COMM8-NME-STREET ', ' '#'                                  
                     COMM8-NUM-LEVEL '-'                                        
                     COMM8-NUM-UNIT-MAIN '. S('                                 
                     COMM8-NUM-POSTAL-CODE ')'                                  
                     DELIMITED BY SIZE INTO BODY-ADDR                           
              END-STRING                                                        
              WRITE MAIL-PRTREC FROM BODY-DTL4-TBL-ROW2                         
              ADD 1                                TO WS-MAIL-WROTE             
              PERFORM 5000-PREPARE-SUMMARY       THRU 5000-EXIT                 
              PERFORM 3500-UPDATE-BP13K022       THRU 3500-EXIT                 
           END-IF                                                               
                                                                                
           PERFORM 2000-READ-BP13F022            THRU 2000-EXIT.                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3100-WRITE-EMAIL-HDR1.                                                   
      *----------------------------------------------------------------*        
           WRITE MAIL-PRTREC FROM HEADER1.                                      
           WRITE MAIL-PRTREC FROM HEADER2.                                      
                                                                                
           MOVE SPACES         TO TO-MAIL-ID-1                                  
                                  TO-MAIL-ID-A                                  
           STRING F022-NUM-OIC '@' 'HDB.GOV.SG' '>'                             
                  DELIMITED BY SPACES INTO TO-MAIL-ID-1                         
           END-STRING.                                                          
           WRITE MAIL-PRTREC FROM HEADER-DTL1-TO1.                              
           WRITE MAIL-PRTREC FROM HEADER-DTL3.                                  
           WRITE MAIL-PRTREC FROM HEADER-DTL4.                                  
                                                                                
           MOVE TO-MAIL-ID-1   TO TO-MAIL-ID-A.                                 
           WRITE MAIL-PRTREC FROM HEADER-DTL5-TOA.                              
                                                                                
           WRITE MAIL-PRTREC FROM HEADER-DTL7-SUBJECT-A.                        
           WRITE MAIL-PRTREC FROM BODY-BLANK-LINE.                              
           WRITE MAIL-PRTREC FROM BODY-DTL1-HEADER1.                            
           WRITE MAIL-PRTREC FROM BODY-BLANK-LINE.                              
           WRITE MAIL-PRTREC FROM BODY-DTL2-INTRO1-A.                           
           WRITE MAIL-PRTREC FROM BODY-BLANK-LINE                               
                                                                                
           WRITE MAIL-PRTREC FROM BODY-DTL3-TBL-HEADER1.                        
           WRITE MAIL-PRTREC FROM BODY-DTL3-TBL-HEADER2.                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3200-WRITE-MAIL-TRAILER.                                                 
      *----------------------------------------------------------------*        
           WRITE MAIL-PRTREC FROM BODY-BLANK-LINE.                              
           WRITE MAIL-PRTREC FROM BODY-DTL2-END1-A.                             
           WRITE MAIL-PRTREC FROM FOOTER-DTL2-END.                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3500-UPDATE-BP13K022.                                                    
      *----------------------------------------------------------------*        
           MOVE F022-KEY-FLD                       TO K022-KEY-FLD              
           READ BP13K022.                                                       
           IF BP13K022-STATUS = 0                                               
              PERFORM 3600-REWRITE-BP13K022      THRU 3600-EXIT                 
           ELSE                                                                 
             IF BP13K022-STATUS = 23                                            
                ADD 1                              TO WS-K022-NOTFND            
                DISPLAY 'BP13K022 - REC NOT FOUND.'                             
                DISPLAY ' '                                                     
             ELSE                                                               
                DISPLAY 'BP13K022 - READ FILE ERROR.'                           
                DISPLAY 'KEY : ' F022-KEY-FLD 'ERR : ' BP13K022-STATUS          
                MOVE BP13K022-STATUS               TO RETURN-CODE               
                PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT                 
             END-IF                                                             
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3600-REWRITE-BP13K022.                                                   
      *----------------------------------------------------------------*        
           MOVE 'Y'                                TO K022-EMAIL-SENT.          
           MOVE 'BP13C17A'                         TO K022-NUM-USERID.          
           MOVE WS-SYSTEM-DATE                     TO K022-DTE-UPDATE.          
                                                                                
           REWRITE BP13K022-REC.                                                
                                                                                
           IF BP13K022-STATUS = 0                                               
              ADD 1                                TO WS-K022-REWRITE           
           ELSE                                                                 
              DISPLAY 'BP13K022 - REWRITE ERROR.'                               
              DISPLAY 'KEY : ' K022-KEY-FLD                                     
              MOVE BP13K022-STATUS                 TO RETURN-CODE               
              PERFORM 9000-CLOSE-FILES           THRU 9000-EXIT                 
           END-IF.                                                              
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4000-CHECK-FF-FILES.                                                     
      *----------------------------------------------------------------*        
           MOVE 'Y'                                TO WS-PROCESS-REC.           
           MOVE F022-NUM-REGN                      TO K800-NUM-REGN.            
           PERFORM 4100-READ-BP13K800            THRU 4100-EXIT.                
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4100-READ-BP13K800.                                                      
      *----------------------------------------------------------------*        
           READ BP13K800.                                                       
           IF BP13K800-STATUS = 0                                               
              PERFORM 4200-READ-BM06K110         THRU 4200-EXIT                 
           ELSE                                                                 
             IF BP13K800-STATUS = 23                                            
                MOVE 'N'                           TO WS-PROCESS-REC            
                ADD  1                             TO WS-K800-NOTFND            
             ELSE                                                               
                DISPLAY 'BP13K800 - READ FILE ERROR'                            
                DISPLAY 'KEY : ' K800-NUM-REGN 'ERR : ' BP13K800-STATUS         
                DISPLAY ' '                                                     
                MOVE BP13K800-STATUS               TO RETURN-CODE               
                PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT                 
             END-IF                                                             
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4200-READ-BM06K110.                                                      
      *----------------------------------------------------------------*        
           MOVE K800-NUM-SCH-ACC                   TO K110-SCH-ACC-NO           
           READ BM06K110.                                                       
           IF BM06K110-STATUS = 0                                               
              PERFORM 4300-READ-BM06K100         THRU 4300-EXIT                 
           ELSE                                                                 
             IF BM06K110-STATUS = 23                                            
                MOVE 'N'                           TO WS-PROCESS-REC            
                ADD  1                             TO WS-K110-NOTFND            
             ELSE                                                               
                DISPLAY 'BM06K110 - READ FILE ERROR.'                           
                DISPLAY 'KEY : ' K110-SCH-ACC-NO 'ERR: ' BM06K110-STATUS        
                DISPLAY ' '                                                     
                MOVE BM06K110-STATUS               TO RETURN-CODE               
                PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT                 
             END-IF                                                             
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4300-READ-BM06K100.                                                      
      *----------------------------------------------------------------*        
           MOVE K110-AIX1                          TO K100-KEY-FLD.             
                                                                                
           READ BM06K100.                                                       
                                                                                
           IF BM06K100-STATUS = 0                                               
              IF K100-DTE-1ST-SOCA = SPACES OR LOW-VALUES OR 0                  
                 DISPLAY 'FIRST SOCA DATE IS BLANK'                             
                 MOVE 'N'                          TO WS-PROCESS-REC            
                 ADD 1                             TO WS-BLANK-1SOCADTE         
              ELSE                                                              
                 DISPLAY 'FIRST SOCA DATE IS NOT BLANK'                         
                 DISPLAY 'TO READ PIDB USING P13C913'                           
                 DISPLAY ' '                                                    
                 MOVE K110-SCH-ACC-NO              TO WS-LINK-REC               
                 PERFORM 4400-READ-PIDB          THRU 4400-EXIT                 
              END-IF                                                            
           ELSE                                                                 
             IF BM06K100-STATUS = 23                                            
                MOVE 'N'                           TO WS-PROCESS-REC            
                ADD  1                             TO WS-K100-NOTFND            
             ELSE                                                               
                DISPLAY 'BM06K100 - READ FILE ERROR.'                           
                DISPLAY 'KEY : ' K100-KEY-FLD 'ERR : ' BM06K100-STATUS          
                DISPLAY ' '                                                     
                MOVE BM06K100-STATUS               TO RETURN-CODE               
                PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT                 
             END-IF                                                             
           END-IF.                                                              
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4400-READ-PIDB.                                                          
      *----------------------------------------------------------------*        
           CALL 'BP13C913' USING WS-LINK-REC,                                   
                                 BP13COMM8-REC.                                 
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
           WHEN +000                                                            
                DISPLAY 'READ PIDB SUCCEESSFUL.........'                        
                DISPLAY 'COMM8-CDE-SYSERR : ' COMM8-CDE-SYSERR                  
           WHEN +100                                                            
                MOVE SPACES                        TO BP13COMM8-REC             
                INITIALIZE                            BP13COMM8-REC             
                DISPLAY 'REC NOT FOUND IN PIDB, SCH: ' K110-SCH-ACC-NO          
                MOVE 'N'                           TO WS-PROCESS-REC            
                DISPLAY ' '                                                     
                ADD  1                             TO WS-PIDB-NOTFND            
           WHEN OTHER                                                           
                MOVE COMM8-CDE-SYSERR              TO WS-RETCODE                
                DISPLAY 'ERROR IN BP13C913 (PIDB), RETCODE: ' WS-RETCODE        
                PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT                 
           END-EVALUATE.                                                        
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       5000-PREPARE-SUMMARY.                                                    
      *----------------------------------------------------------------*        
           IF WS-SUM-CNT = 0                                                    
              PERFORM 5100-WRITE-SUM-HEADER      THRU 5100-EXIT                 
           END-IF.                                                              
                                                                                
           ADD   1                                 TO WS-SUM-CNT.               
           MOVE  WS-SUM-CNT                        TO BODY-SNO.                 
           WRITE MAIL-PRTREC2 FROM BODY-DTL4-TBL-ROW1.                          
           WRITE MAIL-PRTREC2 FROM BODY-DTL4-TBL-ROW2.                          
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       5100-WRITE-SUM-HEADER.                                                   
      *----------------------------------------------------------------*        
           WRITE MAIL-PRTREC2 FROM HEADER1.                                     
           WRITE MAIL-PRTREC2 FROM HEADER2.                                     
                                                                                
           MOVE SPACES             TO TO-MAIL-ID-1                              
                                      TO-MAIL-ID-A.                             
           STRING 'TENG_Siew_Hong' '@' 'HDB.GOV.SG' '>'                         
                  DELIMITED BY SPACES INTO TO-MAIL-ID-1                         
           END-STRING.                                                          
           WRITE MAIL-PRTREC2 FROM HEADER-DTL1-TO1.                             
           WRITE MAIL-PRTREC2 FROM HEADER-DTL3.                                 
           WRITE MAIL-PRTREC2 FROM HEADER-DTL4.                                 
                                                                                
           MOVE TO-MAIL-ID-1   TO TO-MAIL-ID-A.                                 
           WRITE MAIL-PRTREC2 FROM HEADER-DTL5-TOA.                             
                                                                                
           WRITE MAIL-PRTREC2 FROM HEADER-DTL7-SUBJECT-A.                       
           WRITE MAIL-PRTREC2 FROM BODY-BLANK-LINE.                             
           WRITE MAIL-PRTREC2 FROM BODY-DTL1-HEADER1.                           
           WRITE MAIL-PRTREC2 FROM BODY-BLANK-LINE.                             
           WRITE MAIL-PRTREC2 FROM BODY-DTL2-INTRO1-A.                          
           WRITE MAIL-PRTREC2 FROM BODY-BLANK-LINE.                             
                                                                                
           WRITE MAIL-PRTREC2 FROM BODY-DTL3-TBL-HEADER1.                       
           WRITE MAIL-PRTREC2 FROM BODY-DTL3-TBL-HEADER2.                       
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       9000-CLOSE-FILES.                                                        
      *----------------------------------------------------------------*        
           IF WS-SNO > 0                                                        
              PERFORM 3200-WRITE-MAIL-TRAILER    THRU 3200-EXIT                 
           END-IF.                                                              
                                                                                
           IF WS-SUM-CNT > 0                                                    
              WRITE MAIL-PRTREC2 FROM BODY-BLANK-LINE                           
              WRITE MAIL-PRTREC2 FROM BODY-DTL2-END1-A                          
              WRITE MAIL-PRTREC2 FROM FOOTER-DTL2-END                           
           END-IF.                                                              
                                                                                
           DISPLAY 'NO. OF REC READ   IN BP13F022 = ' WS-F022-READ.             
           DISPLAY 'NO. OF REC RWRITE IN BP13K022 = ' WS-K022-REWRITE.          
           DISPLAY 'NO. OF REC NOTFND IN BP13K022 = ' WS-K022-NOTFND.           
           DISPLAY 'NO. OF REC NOTFND IN BP13K800 = ' WS-K800-NOTFND.           
           DISPLAY 'NO. OF REC NOTFND IN BM06K110 = ' WS-K110-NOTFND.           
           DISPLAY 'NO. OF REC NOTFND IN BM06K100 = ' WS-K100-NOTFND.           
           DISPLAY 'NO. OF REC NOTFND IN PIDB TBL = ' WS-PIDB-NOTFND.           
           DISPLAY 'NO. OF 1ST SOCA DATE IS BLANK = ' WS-BLANK-1SOCADTE.        
           DISPLAY 'MAIL SUMMARY COUNT            = ' WS-SUM-CNT.               
                                                                                
           CLOSE  BP13F022                                                      
                  BP13K800                                                      
                  BM06K110                                                      
                  BM06K100                                                      
                  BP13K022                                                      
                  P13MAIL1                                                      
                  P13MAIL2.                                                     
                                                                                
           IF BP13K022-STATUS NOT = ZEROES                                      
              MOVE BP13K022-STATUS TO RETURN-CODE                               
              DISPLAY 'CLOSE BP13K022 FILE ERROR ' BP13K022-STATUS              
           END-IF.                                                              
                                                                                
           IF BP13K800-STATUS NOT = ZEROES                                      
              MOVE BP13K800-STATUS TO RETURN-CODE                               
              DISPLAY 'CLOSE BP13K800 FILE ERROR ' BP13K800-STATUS              
           END-IF.                                                              
                                                                                
           IF BM06K110-STATUS NOT = ZEROES                                      
              MOVE BM06K110-STATUS TO RETURN-CODE                               
              DISPLAY 'CLOSE BM06K110 FILE ERROR ' BM06K110-STATUS              
           END-IF.                                                              
                                                                                
           IF BM06K100-STATUS NOT = ZEROES                                      
              MOVE BM06K100-STATUS TO RETURN-CODE                               
              DISPLAY 'CLOSE BM06K100 FILE ERROR ' BM06K100-STATUS              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
