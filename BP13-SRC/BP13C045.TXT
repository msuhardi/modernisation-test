      *--------------------------------------------------------*                
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------*                
       PROGRAM-ID. BP13C045.                                                    
      *AUTHOR. SK.                                                              
      *DATE-WRITTEN.   10/07/2004.                                              
      *--------------------------------------------------------*                
      *  OBJECTIVE :                                           *                
      *    MATCH F730 AND F595 (TO FIND THE CACELLATION)       *                
      *                                                        *                
      *    IF MATCH, WRITE FROM BP13F730                       *                
      *    FOUND IN BP13F595 & NOT IN BP13F730,WRITE FROM F595 *                
      *    FOUND IN BP13F730 & NOT IN BP13F595,WRITE FROM F730 *                
      *                                                        *                
      * ): F730 - OUTREC INTO F800 FORMAT                      *                
      * ): F595 - OUTREC INTO F800 FORMAT                      *                
      *                                                        *                
      * INPUT  : P13F800A (BP13F730)                           *                
      *          P13F800B (BP13F595)                           *                
      *                                                        *                
      * OUTPUT : P13F800C                                      *                
      *-------------- --------- -------------------------------*                
      * CHG REQ# BY   ON        DESCRIPTION                    *                
      *-------------- --------- -------------------------------*                
      * BP132558 SK   10072004  NEW PGM                        *                
      *--------------------------------------------------------*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT P13F800A  ASSIGN TO P13F800A.                                 
           SELECT P13F800B  ASSIGN TO P13F800B.                                 
           SELECT P13F800C  ASSIGN TO P13F800C.                                 
                                                                                
      *--------------------------------------------------------*                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------*                
                                                                                
       FD   P13F800A                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13F800.                                                           
                                                                                
       FD   P13F800B                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13K800.                                                           
                                                                                
       FD   P13F800C                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       01   P13F800C-REC.                                                       
            05 FILLER              PIC X(2000).                                 
                                                                                
      *--------------------------------------------------------*                
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------*                
       01  WORK-AREAS.                                                          
           05 WS-READ-F800A             PIC 9(8)    VALUE 0.                    
           05 WS-READ-F800B             PIC 9(8)    VALUE 0.                    
           05 WS-MATCH                  PIC 9(8)    VALUE 0.                    
           05 WS-WRITE-F800C            PIC 9(8)    VALUE 0.                    
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY            PIC 9(4).                                 
           05  WS-SYS-MM              PIC 9(2).                                 
           05  WS-SYS-DD              PIC 9(2).                                 
                                                                                
       01  WS-EDITED-DATE             PIC X(10).                                
                                                                                
      *--------------------------------------------------------*                
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------*                
                                                                                
      *--------------------------------------------------------*                
       0000-MAIN.                                                               
      *--------------------------------------------------------*                
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-F800A    THRU 2000-EXIT.                           
           PERFORM 3000-READ-F800B    THRU 3000-EXIT.                           
                                                                                
           PERFORM 4000-COMPARE       THRU 4000-EXIT                            
                   UNTIL F800-NUM-REGN = HIGH-VALUES                            
                     AND K800-NUM-REGN = HIGH-VALUES.                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       1000-OPEN-FILES.                                                         
      *--------------------------------------------------------*                
                                                                                
           OPEN INPUT  P13F800A                                                 
                       P13F800B                                                 
           OUTPUT      P13F800C.                                                
                                                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATE.                      
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2000-READ-F800A.                                                         
      *--------------------------------------------------------*                
                                                                                
           READ P13F800A                                                        
                AT END                                                          
                   MOVE HIGH-VALUES TO F800-NUM-REGN                            
                NOT AT END                                                      
                   ADD 1 TO WS-READ-F800A                                       
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       3000-READ-F800B.                                                         
      *--------------------------------------------------------*                
                                                                                
           READ P13F800B                                                        
                AT END                                                          
                   MOVE HIGH-VALUES TO K800-NUM-REGN                            
                NOT AT END                                                      
                   ADD 1 TO WS-READ-F800B                                       
           END-READ.                                                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4000-COMPARE.                                                            
      *--------------------------------------------------------*                
      * F800 = F730                                            *                
      * K800 = F595                                            *                
      *--------------------------------------------------------*                
                                                                                
           IF F800-NUM-REGN  = K800-NUM-REGN                                    
              ADD 1 TO WS-MATCH                                                 
              WRITE P13F800C-REC FROM BP13F800-MASTER                           
              ADD 1 TO WS-WRITE-F800C                                           
              PERFORM 2000-READ-F800A  THRU 2000-EXIT                           
              PERFORM 3000-READ-F800B  THRU 3000-EXIT                           
           ELSE                                                                 
              IF F800-NUM-REGN > K800-NUM-REGN                                  
                 WRITE P13F800C-REC FROM BP13K800-MASTER                        
                 ADD 1 TO WS-WRITE-F800C                                        
                 PERFORM 3000-READ-F800B  THRU 3000-EXIT                        
              ELSE                                                              
                 WRITE P13F800C-REC FROM BP13F800-MASTER                        
                 ADD 1 TO WS-WRITE-F800C                                        
                 PERFORM 2000-READ-F800A  THRU 2000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       9999-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------*                
                                                                                
           DISPLAY '*********** BP13C045 **************'.                       
           DISPLAY SPACES.                                                      
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF RECORDS READ (P13F800A) :' WS-READ-F800A.            
           DISPLAY 'NO. OF RECORDS READ (P13F800B) :' WS-READ-F800B.            
           DISPLAY 'NO. OF RECORDS WRITE(P13F800C) :' WS-WRITE-F800C.           
           DISPLAY 'NO. OF RECORDS MATCH           :' WS-MATCH.                 
           DISPLAY '***********************************'.                       
                                                                                
           CLOSE       P13F800A                                                 
                       P13F800B                                                 
                       P13F800C.                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
