       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD23.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI                                       
       DATE-WRITTEN.  29/12/2008.                                               
      * ========================================================== *            
      * SYSTEM NAME (SYSTEM ID)                                    *            
      * ========================================================== *            
      * OBJECTIVE   : TO MATCH BP13FA40 AND BP13KB20 AND WRITE TO  *            
      *               BP13FB20 TEMPORARY FILE                      *            
      * ========================================================== *            
      * INPUT FILES  :  BP13FA40  -  SEQUENTIAL DATASET            *            
      *                 BP13KB20  -  VSAM DATASET                  *            
      * OUTPUT FILES :  BP13FB20  -  SEQUENTIAL DATASET            *            
      * ========================================================== *            
      * CHG-REQ# BY    DATE       DESCRIPTION                      *            
      * -------- ----  ---------- -------------------------------- *            
      * BP133427 PCL3  29/12/08   NEW PROGRAM                      *            
      * -------- ----  ---------- -------------------------------- *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FA40  ASSIGN       TO BP13FA40.                           
           SELECT BP13FB20  ASSIGN       TO BP13FB20.                           
           SELECT BP13KB20  ASSIGN       TO BP13KB20                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS KB20-KEY-FLD                        
                            FILE STATUS  IS WS-KB20-STATUS.                     
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------*           
       FD  BP13FA40                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13FA40.                                                           
                                                                                
       FD  BP13FB20                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 3300 CHARACTERS.                                     
       COPY BP13FB20.                                                           
                                                                                
       FD  BP13KB20                                                             
           RECORD CONTAINS 3300 CHARACTERS.                                     
       COPY BP13KB20.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------*           
       01  WS-KB20-STATUS               PIC 9(2)   VALUE 00.                    
       01  WS-FA40-EOF                  PIC X(1)   VALUE 'N'.                   
       01  WS-CNT                       PIC 9(6)   VALUE ZEROES.                
       01  WS-CNT-MATCH                 PIC 9(6)   VALUE ZEROES.                
       01  WS-CNT-UNMATCH               PIC 9(6)   VALUE ZEROES.                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------*           
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------*           
                                                                                
           MOVE 'N'                   TO   WS-FA40-EOF.                         
           MOVE ZEROES                TO   WS-CNT                               
                                           WS-CNT-MATCH                         
                                           WS-CNT-UNMATCH.                      
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
                                                                                
           PERFORM 2000-READ-BP13FA40 THRU 2000-EXIT                            
                   UNTIL WS-FA40-EOF = 'Y'.                                     
                                                                                
           PERFORM 3000-CLOSE-FILES   THRU 3000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------*           
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------*           
                                                                                
           OPEN INPUT  BP13FA40                                                 
                       BP13KB20                                                 
                OUTPUT BP13FB20.                                                
                                                                                
           IF WS-KB20-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13KB20: '  WS-KB20-STATUS                
              PERFORM 3000-CLOSE-FILES            THRU 3000-EXIT                
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------*           
       2000-READ-BP13FA40.                                                      
      *-------------------------------------------------------------*           
                                                                                
            READ BP13FA40                                                       
                 AT END MOVE 'Y'                   TO WS-FA40-EOF               
            END-READ.                                                           
                                                                                
            IF WS-FA40-EOF = 'N'                                                
               ADD 1                               TO WS-CNT                    
               PERFORM 2200-READ-KB20-WRITE-FB20 THRU 2200-EXIT                 
            END-IF.                                                             
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------*           
       2200-READ-KB20-WRITE-FB20.                                               
      *-------------------------------------------------------------*           
                                                                                
            MOVE FA40-REGN-NO               TO KB20-NUM-REGN.                   
            READ BP13KB20.                                                      
                                                                                
            EVALUATE WS-KB20-STATUS                                             
                 WHEN 00                                                        
                      ADD 1 TO WS-CNT-MATCH                                     
                      WRITE BP13FB20-REC FROM BP13KB20-REC                      
                 WHEN 23                                                        
                      ADD 1 TO WS-CNT-UNMATCH                                   
                 WHEN OTHER                                                     
                      DISPLAY 'ERROR READING BP13KB20: ' WS-KB20-STATUS         
                              ' REGN :' KB20-NUM-REGN                           
                      PERFORM 3000-CLOSE-FILES           THRU 3000-EXIT         
            END-EVALUATE.                                                       
                                                                                
       2200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------*           
       3000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------*           
                                                                                
           CLOSE BP13FA40                                                       
                 BP13FB20                                                       
                 BP13KB20.                                                      
                                                                                
           IF WS-KB20-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13KB20: '  WS-KB20-STATUS                
           END-IF.                                                              
                                                                                
           DISPLAY '*************** BP13CD23 ***************'.                  
           DISPLAY "TOTAL NO# READ IN BP13FA40 : " WS-CNT.                      
           DISPLAY "TOTAL NO# MATCHED : "          WS-CNT-MATCH.                
           DISPLAY "TOTAL NO# NOT FOUND IN BP13KB20 : " WS-CNT-UNMATCH.         
           DISPLAY '************ END OF BP13CD23 ***********'.                  
                                                                                
           STOP RUN.                                                            
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
