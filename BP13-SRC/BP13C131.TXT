      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
       PROGRAM-ID.    BP13C131.                                                 
      *AUTHOR.        EG1.                                                      
      *DATE-WRITTEN.  22/03/96.                                                 
                                                                                
      ******************************************************************        
      *  PROGRAM NAME : BP13C131                                       *        
      *  OBJECTIVE    : 1. TO UPDATE THE EXAPPLT RECORDS THAT          *        
      *                    WERE RELEASED THRU THE IDNET FOR CPF        *        
      *                    REFUND AND WAS SUCCESSFUL.                  *        
      *                                                                *        
      *  INPUT/OUTPUT FILES :                                          *        
      *  ====================                                          *        
      *  TYPE  FILE NAME                   DD NAME   ACCESS    KEYLEN  *        
      *  ----  ---------                   -------   ------    ------  *        
      *  I     AB02F003                    AB02F003   SEQ.             *        
      *  I     BP13K885                    BP13K885   KSDS             *        
      *  I     BP13K022                    BP13K022   KSDS             *        
      *  O     BP13FOUT                    BP13K885   SEQ.             *        
      *================================================================*        
      * MODIFICATION HISTORY:                                          *        
      * ---------------------                                          *        
      * CHG REQ. AUTHOR  DATE     DESCRIPTIONS                         *        
      * =======  ====== =======   =====================================*        
      * BP130103 EG1    22031996  TO CATER FOR SUB-TRANSFER MODULE     *        
      * BP130418 RHB    06061998  Y2K                                  *        
      * BP132481 MJ16   16022004  TO INCLUDE OIC IN THE REPORT         *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
      *================================================================*        
       CONFIGURATION SECTION.                                                   
      *================================================================*        
       SOURCE-COMPUTER.  IBM-4341.                                              
       OBJECT-COMPUTER.  IBM-4341.                                              
                                                                                
      *================================================================*        
       INPUT-OUTPUT SECTION.                                                    
      *================================================================*        
      *----------------------------------------------------------------*        
       FILE-CONTROL.                                                            
      *----------------------------------------------------------------*        
           SELECT AB02F003 ASSIGN TO AB02F003.                                  
                                                                                
           SELECT BP13K885 ASSIGN TO BP13K885                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS          IS DYNAMIC                                    
                  RECORD KEY      IS K885-KEY-FLD                               
                  FILE STATUS     IS K885-STATUS.                               
                                                                                
           SELECT BP13K022 ASSIGN TO BP13K022                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS          IS RANDOM                                     
                  RECORD KEY      IS K022-KEY-FLD                               
                  ALTERNATE KEY   IS K022-NUM-REGN                              
                  FILE STATUS     IS K022-STATUS.                               
                                                                                
           SELECT BP13L131 ASSIGN TO BP13L131.                                  
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
      *================================================================*        
       FILE SECTION.                                                            
      *================================================================*        
       FD   AB02F003                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 70 CHARACTERS                                       
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY AB02F003.                                                           
                                                                                
       FD   BP13K885                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K885.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP13L131                                                            
            LABEL RECORDS ARE OMITTED                                           
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE IS F.                                                
                                                                                
       01   L131-REC                    PIC X(132).                             
                                                                                
      *================================================================*        
       WORKING-STORAGE SECTION.                                                 
      *================================================================*        
      *----------------------------------------------------------------*        
      *    ERROR HANDLING VARIABLES                                    *        
      *----------------------------------------------------------------*        
       01 WS-ERR-MSG                     PIC X(80) VALUE SPACES.                
       01 WS-ERR-CDE                     PIC X(2)  VALUE SPACES.                
                                                                                
      *----------------------------------------------------------------*        
      *    FLAG VARIABLES                                              *        
      *----------------------------------------------------------------*        
       01 WS-FLAGS.                                                             
          05 WS-K885-VALID-FL            PIC X.                                 
                                                                                
      *----------------------------------------------------------------*        
      *    FILE STATUS                                                 *        
      *----------------------------------------------------------------*        
       01 WS-FILE-STATUS-VAR.                                                   
          05 K885-STATUS                 PIC X(2).                              
          05 K022-STATUS                 PIC X(2).                              
                                                                                
       01 WS-EOF-STATUS-VAR.                                                    
          05 WS-K885-EOF                 PIC X(1) VALUE 'N'.                    
          05 WS-F003-EOF                 PIC X(1) VALUE 'N'.                    
                                                                                
      *----------------------------------------------------------------*        
      *    WORKING STORAGE AREA FOR COUNTER                            *        
      *----------------------------------------------------------------*        
       01  WS-COUNTERS.                                                         
           05 WS-CNT-F003-READ           PIC 9(07) VALUE 0.                     
           05 WS-CNT-WRITE-FOUT          PIC 9(07) VALUE 0.                     
                                                                                
      *----------------------------------------------------------------*        
      *    WORKING STORAGE AREA FOR DATE VARIABLES                     *        
      *----------------------------------------------------------------*        
       01  WS-CURRENT-DATE.                                                     
           05 WS-CURR-CC                 PIC X(02) VALUE SPACES.                
           05 WS-CURR-YY                 PIC X(02) VALUE SPACES.                
           05 WS-CURR-MM                 PIC X(02) VALUE SPACES.                
           05 WS-CURR-DD                 PIC X(02) VALUE SPACES.                
                                                                                
      *----------------------------------------------------------------*        
      *    WORKING STORAGE AREA FOR REPORT VARIABLES                   *        
      *----------------------------------------------------------------*        
       01  WS-ERR-SEQ-CNT                PIC 9(5) VALUE 0.                      
       01  WS-LINE-CNT                   PIC 99   VALUE 0.                      
       01  WS-PAGE-CNT                   PIC 9(5) VALUE 0.                      
                                                                                
      *----------------------------------------------------------------*        
      *    WORKING STORAGE AREA FOR REPORT LAYOUT                      *        
      *----------------------------------------------------------------*        
       01  L131-RPT-HDR-1.                                                      
           05 L131-RPT-ID                PIC X(08) VALUE 'BP13L131'.            
           05 FILLER                     PIC X(06) VALUE SPACES.                
           05 FILLER                     PIC X(04) VALUE 'HDB3'.                
           05 FILLER                     PIC X(28) VALUE SPACES.                
           05 FILLER                     PIC X(39) VALUE                        
             'S Y S T E M   O F   C O M M I T M E N T'.                         
           05 FILLER                     PIC X(14) VALUE SPACES.                
           05 FILLER                     PIC X(07) VALUE 'DATE : '.             
           05 L131-PRINT-DATE            PIC X(10).                             
           05 FILLER                     PIC X(03) VALUE SPACES.                
           05 FILLER                     PIC X(07) VALUE 'PAGE : '.             
           05 L131-PAGE-CNT              PIC ZZZZ9.                             
                                                                                
       01  L131-RPT-HDR-2.                                                      
           05 FILLER                     PIC X(40) VALUE SPACES.                
           05 FILLER                     PIC X(51) VALUE                        
             '       ERROR LIST ON SOC REFUND CASE FOR           '.             
                                                                                
       01  L131-RPT-HDR-3.                                                      
           05 FILLER                     PIC X(49) VALUE SPACES.                
           05 FILLER                     PIC X(51) VALUE                        
             '      CANCELLATION CASES                  '.                      
                                                                                
       01  L131-RPT-HDR-4.                                                      
           05 FILLER                     PIC X(02) VALUE SPACES.                
           05 FILLER                     PIC X(03) VALUE 'NO.'.                 
           05 FILLER                     PIC X(06) VALUE SPACES.                
           05 FILLER                     PIC X(08) VALUE 'REGN NO.'.            
           05 FILLER                     PIC X(06) VALUE SPACES.                
           05 FILLER                     PIC X(09) VALUE 'CPF1 NO.'.            
           05 FILLER                     PIC X(03) VALUE SPACES.                
           05 FILLER                     PIC X(09) VALUE 'CPF2 NO.'.            
           05 FILLER                     PIC X(03) VALUE SPACES.                
           05 FILLER                     PIC X(09) VALUE 'CPF4 NO.'.            
           05 FILLER                     PIC X(03) VALUE SPACES.                
           05 FILLER                     PIC X(09) VALUE 'CPF4 NO.'.            
           05 FILLER                     PIC X(03) VALUE SPACES.                
           05 FILLER                     PIC X(11) VALUE 'EXAPPLT CPF'.         
           05 FILLER                     PIC X(03) VALUE SPACES.                
           05 FILLER                     PIC X(05) VALUE 'OIC  '.               
           05 FILLER                     PIC X(03) VALUE SPACES.                
           05 FILLER                     PIC X(41) VALUE 'REMARKS'.             
                                                                                
       01  L131-RPT-HDR-5.                                                      
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 FILLER                     PIC X(04) VALUE '===='.                
           05 FILLER                     PIC X(06) VALUE SPACES.                
           05 FILLER                     PIC X(08) VALUE '========'.            
           05 FILLER                     PIC X(06) VALUE SPACES.                
           05 FILLER                     PIC X(09) VALUE '========='.           
           05 FILLER                     PIC X(3) VALUE SPACES.                 
           05 FILLER                     PIC X(09) VALUE '========='.           
           05 FILLER                     PIC X(3) VALUE SPACES.                 
           05 FILLER                     PIC X(09) VALUE '========='.           
           05 FILLER                     PIC X(3) VALUE SPACES.                 
           05 FILLER                     PIC X(09) VALUE '========='.           
           05 FILLER                     PIC X(3) VALUE SPACES.                 
           05 FILLER                     PIC X(11) VALUE '========='.           
           05 FILLER                     PIC X(03) VALUE SPACES.                
           05 FILLER                     PIC X(05) VALUE '====='.               
           05 FILLER                     PIC X(03) VALUE SPACES.                
           05 FILLER                     PIC X(40) VALUE ALL '='.               
                                                                                
       01  L131-DETAIL-LINE.                                                    
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 L131-SEQ-NO                PIC ZZZ9.                              
           05 FILLER                     PIC X(06) VALUE SPACES.                
           05 L131-REGN-NO               PIC X(08).                             
           05 FILLER                     PIC X(06) VALUE SPACES.                
           05 L131-CPF-NO1               PIC X(09).                             
           05 FILLER                     PIC X(3) VALUE SPACES.                 
           05 L131-CPF-NO2               PIC X(09).                             
           05 FILLER                     PIC X(3) VALUE SPACES.                 
           05 L131-CPF-NO3               PIC X(09).                             
           05 FILLER                     PIC X(3) VALUE SPACES.                 
           05 L131-CPF-NO4               PIC X(09).                             
           05 FILLER                     PIC X(3) VALUE SPACES.                 
           05 L131-NRIC                  PIC X(09).                             
           05 FILLER                     PIC X(5) VALUE SPACES.                 
           05 L131-OIC                   PIC X(5).                              
           05 FILLER                     PIC X(03) VALUE SPACES.                
           05 L131-REMARKS               PIC X(40).                             
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *================================================================*        
       0000-MAIN-ROUTINE.                                                       
      *================================================================*        
                                                                                
           PERFORM 1000-INITIALIZATION      THRU 1000-EXIT.                     
           PERFORM 2000-PROCESS-ROUTINE     THRU 2000-EXIT                      
                   UNTIL WS-F003-EOF = 'Y'.                                     
           PERFORM 9999-CLOSE-FILES         THRU 9999-EXIT.                     
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       1000-INITIALIZATION.                                                     
      *================================================================*        
           OPEN INPUT  AB02F003                                                 
                       BP13K022                                                 
                I-O    BP13K885                                                 
                OUTPUT BP13L131.                                                
                                                                                
           IF K885-STATUS NOT EQUAL 00 AND 97                                   
              STRING 'BP13K885 OPENING ERROR (' K885-STATUS ')'                 
                     DELIMITED BY SIZE INTO WS-ERR-MSG                          
              PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                          
           END-IF.                                                              
                                                                                
           IF K022-STATUS NOT EQUAL 00 AND 97                                   
              STRING 'BP13K022 OPENING ERROR (' K022-STATUS ')'                 
                     DELIMITED BY SIZE INTO WS-ERR-MSG                          
              PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                          
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE.                       
                                                                                
           READ  AB02F003 AT END                                                
                 MOVE 'Y'          TO  WS-F003-EOF.                             
                                                                                
           IF WS-F003-EOF = 'Y'                                                 
              STRING 'NO INPUT F003 RECORDS FOUND'                              
                     DELIMITED BY SIZE INTO WS-ERR-MSG                          
              PERFORM 9999-CLOSE-FILES         THRU 9999-EXIT                   
           ELSE                                                                 
              ADD 1                TO WS-CNT-F003-READ                          
           END-IF.                                                              
           MOVE 60                 TO WS-LINE-CNT.                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       2000-PROCESS-ROUTINE.                                                    
      *================================================================*        
           PERFORM  3000-VALIDATE-RECS THRU 3000-EXIT.                          
                                                                                
           IF WS-K885-VALID-FL  NOT = 'Y'                                       
              PERFORM 6000-ERROR-LIST-PRINT     THRU 6000-EXIT.                 
                                                                                
           READ  AB02F003 AT END                                                
                 MOVE 'Y'        TO WS-F003-EOF                                 
                 GO  TO 2000-EXIT                                               
                 END-READ.                                                      
                                                                                
           ADD   1   TO  WS-CNT-F003-READ.                                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       3000-VALIDATE-RECS.                                                      
      *================================================================*        
           IF F003-CDE-TRANS NOT = 'W'                                          
              GO TO 3000-EXIT.                                                  
                                                                                
           MOVE SPACES                     TO WS-K885-VALID-FL                  
                                              WS-ERR-CDE.                       
           MOVE LOW-VALUES                 TO K885-KEY-FLD.                     
           MOVE F003-NUM-ORIG-REGN         TO K885-NUM-REGN.                    
           PERFORM  8100-START-BP13K885    THRU 8100-EXIT.                      
                                                                                
           PERFORM UNTIL WS-K885-EOF = 'Y' OR                                   
                         WS-K885-VALID-FL = 'N'                                 
              PERFORM  8150-READNEXT-BP13K885 THRU 8150-EXIT                    
              IF WS-K885-EOF NOT = 'Y'                                          
                 IF K885-CDE-VOUCHER  = 'Y'                                     
                    MOVE 'Y'                  TO WS-K885-VALID-FL               
                    PERFORM  3100-CHECK-SUMS  THRU 3100-EXIT                    
                 ELSE                                                           
                    MOVE 'N'                  TO WS-K885-VALID-FL               
                    MOVE '01'                 TO WS-ERR-CDE                     
                 END-IF                                                         
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       3100-CHECK-SUMS.                                                         
      *================================================================*        
           IF F003-NUM-CPF1-ACCT = K885-NUM-CPF                                 
              MOVE 'Y'                     TO WS-K885-VALID-FL                  
           ELSE                                                                 
           IF F003-NUM-CPF2-ACCT = K885-NUM-CPF                                 
              MOVE 'Y'                     TO WS-K885-VALID-FL                  
           ELSE                                                                 
           IF F003-NUM-CPF3-ACCT = K885-NUM-CPF                                 
              MOVE 'Y'                     TO WS-K885-VALID-FL                  
           ELSE                                                                 
           IF F003-NUM-CPF4-ACCT = K885-NUM-CPF                                 
              MOVE 'Y'                     TO WS-K885-VALID-FL                  
           ELSE                                                                 
              MOVE 'N'                     TO WS-K885-VALID-FL                  
              MOVE '02'                    TO WS-ERR-CDE                        
           END-IF.                                                              
                                                                                
           IF WS-K885-VALID-FL = 'Y'                                            
              PERFORM  4000-WRITE-FOUT THRU 4000-EXIT.                          
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       4000-WRITE-FOUT.                                                         
      *================================================================*        
           DISPLAY ' REGN' F003-NUM-ORIG-REGN                                   
                                                                                
           MOVE 'R' TO K885-CDE-VOUCHER.                                        
           REWRITE BP13K885-REC.                                                
           ADD   1                     TO   WS-CNT-WRITE-FOUT.                  
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       6000-ERROR-LIST-PRINT.                                                   
      *================================================================*        
           PERFORM 6100-FORMAT-ERR-DETAIL  THRU 6100-EXIT.                      
           IF WS-LINE-CNT > 46                                                  
              PERFORM 6050-WRITE-HDR   THRU 6050-EXIT                           
           END-IF.                                                              
                                                                                
           WRITE L131-REC      FROM  L131-DETAIL-LINE AFTER 2.                  
           ADD   2             TO    WS-LINE-CNT.                               
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       6050-WRITE-HDR.                                                          
      *================================================================*        
           MOVE 'BP13L131'     TO  L131-RPT-ID.                                 
           ADD  1              TO  WS-PAGE-CNT.                                 
           MOVE WS-PAGE-CNT    TO  L131-PAGE-CNT.                               
           STRING WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CC WS-CURR-YY           
                  DELIMITED BY SIZE INTO L131-PRINT-DATE.                       
                                                                                
           WRITE L131-REC    FROM L131-RPT-HDR-1   AFTER PAGE.                  
           WRITE L131-REC    FROM L131-RPT-HDR-2   AFTER 1.                     
           WRITE L131-REC    FROM L131-RPT-HDR-3   AFTER 1.                     
           WRITE L131-REC    FROM L131-RPT-HDR-4   AFTER 3.                     
           WRITE L131-REC    FROM L131-RPT-HDR-5   AFTER 1.                     
                                                                                
           MOVE 8               TO WS-LINE-CNT.                                 
                                                                                
       6050-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       6100-FORMAT-ERR-DETAIL.                                                  
      *================================================================*        
           ADD 1                   TO WS-ERR-SEQ-CNT.                           
           MOVE WS-ERR-SEQ-CNT     TO L131-SEQ-NO.                              
           MOVE F003-NUM-ORIG-REGN TO L131-REGN-NO.                             
           MOVE K885-NUM-CPF       TO L131-NRIC.                                
           MOVE F003-NUM-CPF1-ACCT TO L131-CPF-NO1.                             
           MOVE F003-NUM-CPF2-ACCT TO L131-CPF-NO2.                             
           MOVE F003-NUM-CPF3-ACCT TO L131-CPF-NO3.                             
           MOVE F003-NUM-CPF4-ACCT TO L131-CPF-NO4.                             
                                                                                
           PERFORM 8000-READ-BP13K022 THRU 8000-EXIT.                           
                                                                                
           IF K022-NUM-OIC NOT = SPACES AND LOW-VALUES                          
              MOVE K022-NUM-OIC TO L131-OIC                                     
           ELSE                                                                 
              MOVE SPACES       TO L131-OIC                                     
           END-IF.                                                              
                                                                                
           EVALUATE WS-ERR-CDE                                                  
           WHEN '01'                                                            
                MOVE 'INCOMPLETE CASE!'    TO L131-REMARKS                      
           WHEN '02'                                                            
                MOVE 'CPF NO NOT FOUND IN EXAPPLT FILE!'                        
                                           TO L131-REMARKS                      
           END-EVALUATE.                                                        
                                                                                
       6100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       8000-READ-BP13K022.                                                      
      *================================================================*        
           MOVE LOW-VALUES           TO K022-KEY-FLD.                           
           MOVE F003-NUM-ORIG-REGN   TO K022-NUM-REGN.                          
           MOVE SPACES               TO K022-NUM-OIC.                           
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE K022-STATUS                                                 
             WHEN '00'                                                          
             WHEN '02'                                                          
             WHEN '23'                                                          
             WHEN '10'                                                          
                  CONTINUE                                                      
                                                                                
             WHEN OTHER                                                         
               STRING 'BP13K022 READ ERROR CODE(' K022-STATUS ')'               
                      DELIMITED BY SIZE INTO WS-ERR-MSG                         
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
                                                                                
           END-EVALUATE.                                                        
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       8100-START-BP13K885.                                                     
      *================================================================*        
            DISPLAY 'KEY' K885-KEY-FLD                                          
            MOVE 'N'                 TO WS-K885-EOF.                            
            START BP13K885 KEY >= K885-KEY-FLD                                  
                  INVALID KEY  MOVE 'Y'    TO WS-K885-EOF                       
                               GO TO 8100-EXIT.                                 
                                                                                
            IF K885-STATUS NOT = '00'                                           
               STRING 'BP13K885 START ERROR CODE(' K885-STATUS ')'              
                      DELIMITED BY SIZE INTO WS-ERR-MSG                         
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
       8100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       8150-READNEXT-BP13K885.                                                  
      *================================================================*        
            DISPLAY '8150' K885-KEY-FLD                                         
            READ  BP13K885 NEXT                                                 
                  AT END   MOVE 'Y'        TO WS-K885-EOF                       
                           GO TO 8150-EXIT.                                     
                                                                                
            IF K885-STATUS NOT = '00'                                           
               STRING 'BP13K885 READ NEXT ERROR CODE(' K885-STATUS ')'          
                      DELIMITED BY SIZE INTO WS-ERR-MSG                         
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
            IF K885-NUM-REGN NOT = F003-NUM-ORIG-REGN                           
               DISPLAY 'NOTNFND'   F003-NUM-ORIG-REGN                           
                                   K885-NUM-REGN                                
               MOVE 'Y'            TO WS-K885-EOF                               
            END-IF.                                                             
                                                                                
       8150-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       9900-HANDLE-ABEND.                                                       
      *================================================================*        
            CLOSE   AB02F003                                                    
                    BP13K022                                                    
                    BP13K885                                                    
                    BP13L131.                                                   
                                                                                
            DISPLAY '***************************************'                   
            DISPLAY '*    BP13C131                         *'                   
            DISPLAY '***************************************'                   
            DISPLAY '* NO. OF F003 READ: ' WS-CNT-F003-READ.                    
            DISPLAY '* NO. OF VALID RECS: ' WS-CNT-WRITE-FOUT.                  
            DISPLAY '* NO. OF ERR CASES: ' WS-ERR-SEQ-CNT.                      
            DISPLAY '***************************************'                   
            DISPLAY '* ABEND DESC: ' WS-ERR-MSG.                                
            MOVE '4095'               TO RETURN-CODE.                           
                                                                                
            STOP RUN.                                                           
       9900-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       9999-CLOSE-FILES.                                                        
      *================================================================*        
            CLOSE   AB02F003                                                    
                    BP13K885                                                    
                    BP13L131.                                                   
                                                                                
            DISPLAY '***************************************'                   
            DISPLAY '*    BP13C131 CONTROL TOTAL           *'                   
            DISPLAY '***************************************'                   
            DISPLAY '* NO. OF F003 READ: ' WS-CNT-F003-READ.                    
            DISPLAY '* NO. OF VALID RECS: ' WS-CNT-WRITE-FOUT.                  
            DISPLAY '* NO. OF ERR CASES: ' WS-ERR-SEQ-CNT.                      
            DISPLAY '* ABEND DESC: ' WS-ERR-MSG.                                
            DISPLAY '***************************************'                   
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
