       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C637                                                  
      *AUTHOR.        LSB1.                                                     
      *DATE-WRITTEN.  08/09/1997.                                               
      * ====================================================== *                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      * ====================================================== *                
      *    OBJECTIVE   : TO EXTRACT THE WHOLE ESSENTAIL FAMILY *                
      *                  INFORMATION FOR CASES TAG AS PARENT   *                
      *                  CHILD CASES.                          *                
      *                                                        *                
      *    INPUT FILE  : BP13F558                              *                
      *                  P13F558A                              *                
      *   OUTPUT FILE  : P13F558O                              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY   DATE     DETAILS                          *                
      * -----------------------------                          *                
      * BP130377 LSB 970908 - NEW PGM                          *                
      * BP130445 SSS 980206 - EXPAND F558 RECL FROM 150 TO 400 *                
      * BP130897 AKT 000803 - EXPAND F558 RECL FROM 400 TO 600 *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
           SELECT P13F558A ASSIGN TO P13F558A.                                  
           SELECT P13F558O ASSIGN TO P13F558O.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F558                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F558.                                                           
                                                                                
       FD  P13F558A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       01  P13F558A-REC.                                                        
           05 F558A-NUM-REGN             PIC X(8).                              
           05 FILLER                     PIC X(592).                            
                                                                                
       FD  P13F558O                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       01  P13F558O-REC                   PIC X(600).                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-COUNTERS.                                                         
           05 WS-F558-READ                PIC 9(8)    VALUE ZEROS.              
           05 WS-F558A-READ               PIC 9(8)    VALUE ZEROS.              
           05 WS-F558O-WRITE              PIC 9(8)    VALUE ZEROS.              
           05 WS-MR                       PIC 9(8)    VALUE 0.                  
           05 WS-F558A-NMR                PIC 9(8)    VALUE 0.                  
           05 WS-F558-NMR                 PIC 9(8)    VALUE 0.                  
                                                                                
       01  WS-EOF-F558                    PIC 9.                                
           88 EOF-F558                    VALUE 1.                              
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN.                                                                
      *-------------------------------------------------------------            
           OPEN INPUT  P13F558A                                                 
                       BP13F558                                                 
                OUTPUT P13F558O.                                                
                                                                                
           PERFORM 100-READ-F558A-REC THRU 100-EXIT.                            
           PERFORM 110-READ-F558-REC THRU 110-EXIT.                             
           PERFORM 200-PROCESS-REC   THRU 200-EXIT                              
                   UNTIL F558A-NUM-REGN = HIGH-VALUE                            
                     AND F558-NUM-REGN = HIGH-VALUE.                            
           PERFORM 900-CLOSE-ROUTINE.                                           
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-READ-F558A-REC.                                                      
      *-------------------------------------------------------------            
           READ P13F558A AT END MOVE HIGH-VALUE TO F558A-NUM-REGN               
                GO TO 100-EXIT.                                                 
           ADD 1 TO WS-F558A-READ.                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       110-READ-F558-REC.                                                       
      *-------------------------------------------------------------            
           READ BP13F558 AT END MOVE HIGH-VALUE TO F558-NUM-REGN                
                GO TO 110-EXIT.                                                 
           ADD 1 TO WS-F558-READ.                                               
                                                                                
       110-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-PROCESS-REC.                                                         
      *-------------------------------------------------------------            
           IF F558A-NUM-REGN = F558-NUM-REGN                                    
               ADD 1 TO WS-MR                                                   
               PERFORM 300-WRITE-OUTPUT THRU 300-EXIT                           
                  UNTIL F558-NUM-REGN NOT = F558A-NUM-REGN                      
                     OR F558-NUM-REGN = HIGH-VALUES                             
               PERFORM 100-READ-F558A-REC THRU 100-EXIT                         
                                                                                
           ELSE                                                                 
               IF  F558A-NUM-REGN < F558-NUM-REGN                               
                   ADD 1 TO WS-F558A-NMR                                        
                   PERFORM 100-READ-F558A-REC THRU 100-EXIT                     
                                                                                
               ELSE                                                             
                   ADD 1 TO WS-F558-NMR                                         
                   PERFORM 110-READ-F558-REC THRU 110-EXIT.                     
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-WRITE-OUTPUT.                                                        
      *-------------------------------------------------------------            
           MOVE BP13F558-REC TO P13F558O-REC.                                   
           WRITE P13F558O-REC.                                                  
           ADD 1 TO WS-F558O-WRITE.                                             
                                                                                
           PERFORM 110-READ-F558-REC THRU 110-EXIT.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
      *-------------------------------------------------------------            
           DISPLAY '----------- BP13C637 CONTROL -----------'.                  
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS READ FROM F558A ' WS-F558A-READ.              
           DISPLAY 'NO OF RECORDS READ FROM F558 ' WS-F558-READ.                
           DISPLAY 'NO OF REORDS WRITE OUT       ' WS-F558O-WRITE.              
           DISPLAY 'NO OF RECORDS MATCH          ' WS-MR.                       
           DISPLAY 'NO OF RECORD NOT MATCH-F558  ' WS-F558-NMR.                 
           DISPLAY 'NO OF RECORD NOT MATCH-F558A ' WS-F558A-NMR.                
                                                                                
           CLOSE BP13F558                                                       
                 P13F558O                                                       
                 P13F558A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
