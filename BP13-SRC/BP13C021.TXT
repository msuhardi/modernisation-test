       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C021.                                                 
       AUTHOR.        SHARON DUMDUM.                                            
      *DATE-WRITTEN.  27/08/2004.                                               
      * =========================================================== *           
      *              SYSTEM OF COMMITMENT (BP13)                    *           
      * =========================================================== *           
      *   OBJECTIVE  :  TO GENERATE REPORT ON MCPS POSTAL CODE.     *           
      *                                                             *           
      *   INPUT      :  BP13F725 -- SEQUENTIAL FILE                 *           
      *                 BP13F730 -- REQ FILE                        *           
      *   OUTPUT     :  P13F730A                                    *           
      *                 BP13L021                                    *           
      *                                                             *           
      *   AMENDMENTS:                                               *           
      *    REF      DATE     BY          DESCRIPTIONS               *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP132583 27/08/2004 SD10 NEW PROGRAM                        *           
      * BP133320 22/05/2008 MSD1 OUTPUT NOT MATCH CASES INTO SEQFILE*           
      * BP136609 30/03/2017 SMR2 ADD NEW OUTPUT FOR RDB DIFF CASES  *           
      * BP136762 24/05/2017 SMR2 INITIALIZE RDB POSTAL              *           
      * BP137920 19/07/2019 AM25 ADD NEW OUTPUT FOR RDB SUBT CASES  *           
      * BP138264 30/03/2020 AM25 READ SUBT FROM ROFS INSTEAD OF RDB *           
      * BP138971 15/12/2021 KR13 CATER FOR NEW FIN SERIES 'M'       *           
      * BP139682 12/12/2023 AM25 NEW REPORT FILE IN CSV FORMAT      *           
      *          26/02/2024 AM25 REVERT CHANGES                     *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F725 ASSIGN TO BP13F725.                                  
           SELECT BP13F730 ASSIGN TO BP13F730.                                  
           SELECT P13F730A ASSIGN TO P13F730A.                                  
           SELECT P13F730B ASSIGN TO P13F730B.                                  
           SELECT P13F730C ASSIGN TO P13F730C.                                  
           SELECT BP13L021 ASSIGN TO BP13L021.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F725       BLOCK  CONTAINS 0 RECORDS                             
                          RECORD CONTAINS 211 CHARACTERS                        
                          LABEL RECORDS ARE STANDARD                            
                          RECORDING MODE IS F.                                  
       COPY BP13F725.                                                           
                                                                                
       FD  BP13F730       BLOCK  CONTAINS 0 RECORDS                             
                          RECORD CONTAINS 500 CHARACTERS                        
                          LABEL RECORDS ARE STANDARD                            
                          RECORDING MODE IS F.                                  
       COPY BP13F730.                                                           
                                                                                
       FD  P13F730A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  P13F730A-REC                   PIC X(500).                           
                                                                                
       FD  P13F730B                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  P13F730B-REC                   PIC X(500).                           
                                                                                
       FD  P13F730C                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  P13F730C-REC                   PIC X(500).                           
                                                                                
       FD   BP13L021                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE IS F.                                                
                                                                                
       01   BP13L021-REC           PIC X(132).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-F725-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-F730-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-L021-WRITE           PIC 9(8)    VALUE ZERO.                      
       01  WS-L021-MATCH           PIC 9(8)    VALUE ZERO.                      
       01  WS-L021-RDB             PIC 9(8)    VALUE ZERO.                      
       01  WS-L021-MHA             PIC 9(8)    VALUE ZERO.                      
       01  WS-L021-SUBT            PIC 9(8)    VALUE ZERO.                      
                                                                                
       01  WS-EOF-F725             PIC X       VALUE SPACE.                     
       01  WS-EOF-F730             PIC X       VALUE SPACE.                     
       01  WS-RDB-EOC              PIC X(1)    VALUE 'N'.                       
           88 END-OF-CURSOR                    VALUE 'Y'.                       
       01  WS-SUBT-EOC             PIC X(1)    VALUE 'N'.                       
           88 END-OF-CURSUBT                   VALUE 'Y'.                       
       01  WS-L021-LINECTR         PIC 9(2)    VALUE 60.                        
       01  WS-L021-PAGECTR         PIC 9(5)    VALUE ZEROES.                    
       01  WS-L021-SNO             PIC 9(5)    VALUE ZEROES.                    
       01  WS-CTR                  PIC 9(5)    VALUE ZEROES.                    
       01  WS-SQLCODE              PIC 9(4)    VALUE ZEROES.                    
       01  WS-RDB-POSTAL           PIC X(6)    VALUE SPACES.                    
       01  WS-SUBT-POSTAL          PIC X(6)    VALUE SPACES.                    
                                                                                
       01  WS-TIME-DATES.                                                       
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY.                                                 
                   15  WS-SYS-CC   PIC 9(2).                                    
                   15  WS-SYS-YY   PIC 9(2).                                    
               10  WS-SYS-MM       PIC 9(2).                                    
               10  WS-SYS-DD       PIC 9(2).                                    
           05  WS-EDITED-DATE.                                                  
               10  WS-EDITED-DD    PIC X(2)    VALUE SPACES.                    
               10  FILLER          PIC X       VALUE '/'.                       
               10  WS-EDITED-MM    PIC X(2)    VALUE SPACES.                    
               10  FILLER          PIC X       VALUE '/'.                       
               10  WS-EDITED-YY    PIC X(4)    VALUE SPACES.                    
                                                                                
      *--------------------------------------------------------------*          
      * COMMAREA FOR CALLING AV02C013 FOR FINDING NRIC NUMBER TYPE   *          
      *--------------------------------------------------------------*          
       01  WS-AV02-AREA.                                                        
           05  WS-AV02-NRIC        PIC X(9)    VALUE SPACES.                    
           05  WS-AV02-FLAG        PIC X(1)    VALUE SPACES.                    
           05  WS-AV02-NRIC-TYPE   PIC X(3)    VALUE SPACES.                    
                                                                                
      *============================================================*            
      *         BP13L021 -   R E P O R T    L A Y O U T            *            
      *============================================================*            
                                                                                
       01  L021-HEADER-01.                                                      
           05  FILLER              PIC X(08)     VALUE 'BP13L021'.              
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(25)     VALUE SPACES.                  
           05  FILLER              PIC X(41)     VALUE                          
               'S Y S T E M    O F    C O M M I T M E N T'.                     
           05  FILLER              PIC X(12)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE :'.                
           05  L021-RUN-DATE       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L021-PAGE-NO        PIC ZZZZ9.                                   
                                                                                
       01  L021-HEADER-02.                                                      
           05  FILLER              PIC X(46)     VALUE SPACES.                  
           05  FILLER              PIC X(55)     VALUE                          
               'REPORT ON MATCHING OF MCPS POSTAL CODE'.                        
                                                                                
       01  L021-HEADER-03.                                                      
           05  FILLER              PIC X(07)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'APPL'.                  
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'APPL'.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'MHA'.                   
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'RDB'.                   
                                                                                
       01  L021-HEADER-04.                                                      
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'SNO'.                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE 'REGNO'.                 
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'NRIC'.                  
           05  FILLER              PIC X(07)     VALUE SPACES.                  
           05  FILLER              PIC X(06)     VALUE 'POSTAL'.                
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(06)     VALUE 'POSTAL'.                
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(06)     VALUE 'POSTAL'.                
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'NAME'.                  
           05  FILLER              PIC X(64)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'REMARKS'.               
                                                                                
       01  L021-HEADER-05.                                                      
           05  FILLER              PIC X(131)    VALUE ALL '-'.                 
                                                                                
       01  L021-DETAILS.                                                        
           05  L021-SNO            PIC ZZZZ9.                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L021-REGNO          PIC X(08).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L021-NRIC           PIC X(09).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L021-F730-POSTAL    PIC X(06).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L021-F725-POSTAL    PIC X(06).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L021-RDB-POSTAL     PIC X(06).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L021-NAME           PIC X(66).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L021-REMARKS        PIC X(11).                                   
                                                                                
           EXEC SQL INCLUDE SQLCA     END-EXEC.                                 
           EXEC SQL INCLUDE P13IDNO   END-EXEC.                                 
           EXEC SQL INCLUDE P13CSID   END-EXEC.                                 
           EXEC SQL INCLUDE P13VINTF  END-EXEC.                                 
           EXEC SQL INCLUDE P13RSROL  END-EXEC.                                 
           EXEC SQL INCLUDE P13SBTNT  END-EXEC.                                 
           EXEC SQL INCLUDE P13SBLET  END-EXEC.                                 
                                                                                
           EXEC SQL DECLARE CUR_RDB CURSOR FOR                                  
             SELECT T3.NUM_POSTAL_CODE                                          
               FROM IDNTY_NO       T1                                           
                  , CSTMR_ID       T2                                           
                  , PIDB_INTERFACE T3                                           
                  , RSDNT_ROLE     T4                                           
              WHERE T1.NUM_CSTMR         = T2.NUM_CSTMR                         
                AND T2.NUM_CSTMR_SOURCE  = T4.NUM_CSTMR_SOURCE                  
                AND T3.NUM_SCHM          = T4.NUM_SCHEME                        
                AND T3.NUM_ACCNT         = T4.NUM_ACNT                          
                AND T4.DTE_ROLE_END      = '0001-01-01'                         
                AND T4.NUM_ROLE IN ('003','004','023','024','025')              
                AND T1.NUM_IDNTY_NO      = :DCLIDNTY-NO.NUM-IDNTY-NO            
                AND T1.NUM_IDNTY_NO_TYPE =                                      
                                         :DCLIDNTY-NO.NUM-IDNTY-NO-TYPE         
           END-EXEC.                                                            
                                                                                
           EXEC SQL DECLARE CUR_SUBT CURSOR FOR                                 
             SELECT T3.NUM_POSTAL_CODE                                          
               FROM SBTNT          T1                                           
                  , SUBLET         T2                                           
                  , PIDB_INTERFACE T3                                           
              WHERE T1.NUM_CASE_REF      = T2.NUM_CASE_REF                      
                AND T3.NUM_SCHM          = T2.NUM_SCHEME                        
                AND T3.NUM_ACCNT         = T2.NUM_ACNT                          
                AND T1.DTE_ROLE_END      = '0001-01-01'                         
                AND T1.DTE_ROLE_START    <= :DCLSBTNT.DTE-ROLE-START            
                AND T1.NUM_IDNTY_NO      = :DCLSBTNT.NUM-IDNTY-NO               
                AND T1.NUM_IDNTY_NO_TYPE = :DCLSBTNT.NUM-IDNTY-NO-TYPE          
           END-EXEC.                                                            
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-ROUTINE  THRU 1000-EXIT.                           
           PERFORM 3000-READ-F725     THRU 3000-EXIT.                           
           PERFORM 4000-READ-F730     THRU 4000-EXIT.                           
           PERFORM 2000-PROCESS       THRU 2000-EXIT                            
              UNTIL WS-EOF-F730 = 'Y' OR                                        
                    WS-EOF-F725 = 'Y'.                                          
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F725                                                 
                       BP13F730                                                 
                OUTPUT P13F730A                                                 
                       P13F730B                                                 
                       P13F730C                                                 
                       BP13L021.                                                
           MOVE 'N' TO WS-EOF-F725.                                             
           MOVE 'N' TO WS-EOF-F730.                                             
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-SYSTEM-DATE.                 
                                                                                
           MOVE   WS-SYS-DD           TO WS-EDITED-DD.                          
           MOVE   WS-SYS-MM           TO WS-EDITED-MM.                          
           MOVE   WS-SYS-CCYY         TO WS-EDITED-YY.                          
                                                                                
           MOVE   WS-EDITED-DATE      TO L021-RUN-DATE.                         
                                                                                
           STRING WS-SYS-CCYY '-'                                               
                  WS-SYS-MM   '-'                                               
                  WS-SYS-DD                                                     
              DELIMITED BY SIZE INTO DTE-ROLE-START OF DCLSBTNT.                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-PROCESS.                                                            
      *****************************************************************         
      *  MATCH REGN-NO AND NRIC AND POSTAL CDE                                  
           MOVE SPACES                     TO WS-RDB-POSTAL                     
                                              WS-SUBT-POSTAL.                   
           IF F730-NUM-REGN = F725-NUM-REGN                                     
              MOVE SPACES                  TO L021-DETAILS                      
              MOVE F730-NUM-REGN           TO L021-REGNO                        
              MOVE F725-NUM-NRIC           TO L021-NRIC                         
              MOVE F725-NME-PRINCIPAL      TO L021-NAME                         
              IF F725-NUM-NRIC = F730-NUM-RES-NRIC1 OR                          
                                 F730-NUM-RES-NRIC2                             
                 IF F725-CDE-POSTAL = F730-NUM-MCPS-POSTAL                      
                    PERFORM 6000-GET-NRIC-TYPE   THRU 6000-EXIT                 
                    PERFORM 8000-READ-SUBT-TABLE THRU 8000-EXIT                 
                    IF F725-CDE-POSTAL = WS-SUBT-POSTAL                         
                       WRITE P13F730C-REC     FROM BP13F730-REC                 
                       MOVE 'SUB-T'           TO L021-REMARKS                   
                       ADD 1                  TO WS-L021-SUBT                   
                       PERFORM 5000-PRINT-RPT THRU 5000-EXIT                    
                       PERFORM 3000-READ-F725 THRU 3000-EXIT                    
                       PERFORM 4000-READ-F730 THRU 4000-EXIT                    
                    ELSE                                                        
                       PERFORM 7000-READ-RDB-TABLE  THRU 7000-EXIT              
                       IF F725-CDE-POSTAL = WS-RDB-POSTAL                       
                          MOVE SPACES            TO L021-REMARKS                
                          ADD 1                  TO WS-L021-MATCH               
                          PERFORM 5000-PRINT-RPT THRU 5000-EXIT                 
                          PERFORM 3000-READ-F725 THRU 3000-EXIT                 
                          PERFORM 4000-READ-F730 THRU 4000-EXIT                 
                       ELSE                                                     
                          WRITE P13F730B-REC     FROM BP13F730-REC              
                          MOVE 'RDB DIFF'        TO L021-REMARKS                
                          ADD 1                  TO WS-L021-RDB                 
                          PERFORM 5000-PRINT-RPT THRU 5000-EXIT                 
                          PERFORM 3000-READ-F725 THRU 3000-EXIT                 
                       END-IF                                                   
                    END-IF                                                      
                 ELSE                                                           
                    WRITE P13F730A-REC     FROM BP13F730-REC                    
                    MOVE 'NOT MATCHED'     TO L021-REMARKS                      
                    ADD 1                  TO WS-L021-MHA                       
                    PERFORM 5000-PRINT-RPT THRU 5000-EXIT                       
                    PERFORM 3000-READ-F725 THRU 3000-EXIT                       
                 END-IF                                                         
              ELSE                                                              
                 PERFORM 3000-READ-F725    THRU 3000-EXIT                       
              END-IF                                                            
           ELSE                                                                 
              IF F730-NUM-REGN < F725-NUM-REGN                                  
                 PERFORM 4000-READ-F730    THRU 4000-EXIT                       
              ELSE                                                              
                 PERFORM 3000-READ-F725    THRU 3000-EXIT                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-READ-F725.                                                          
      *****************************************************************         
                                                                                
           READ BP13F725 AT END MOVE 'Y' TO WS-EOF-F725                         
                GO TO 3000-EXIT.                                                
            ADD 1 TO WS-F725-READ.                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4000-READ-F730.                                                          
      *****************************************************************         
                                                                                
           READ BP13F730 AT END MOVE 'Y' TO WS-EOF-F730                         
                GO TO 4000-EXIT.                                                
           ADD 1 TO WS-F730-READ.                                               
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       5000-PRINT-RPT.                                                          
      *****************************************************************         
                                                                                
           MOVE F730-NUM-MCPS-POSTAL TO L021-F730-POSTAL.                       
           MOVE F725-CDE-POSTAL      TO L021-F725-POSTAL.                       
           IF L021-REMARKS = 'SUB-T'                                            
              MOVE WS-SUBT-POSTAL    TO L021-RDB-POSTAL                         
           ELSE                                                                 
              MOVE WS-RDB-POSTAL     TO L021-RDB-POSTAL                         
           END-IF.                                                              
                                                                                
           ADD 1                     TO WS-L021-SNO.                            
           MOVE WS-L021-SNO          TO L021-SNO.                               
                                                                                
           IF WS-L021-LINECTR >= 55                                             
              ADD                  1 TO WS-L021-PAGECTR                         
              MOVE WS-L021-PAGECTR   TO L021-PAGE-NO                            
              WRITE BP13L021-REC   FROM L021-HEADER-01 AFTER PAGE               
              WRITE BP13L021-REC   FROM L021-HEADER-02                          
              WRITE BP13L021-REC   FROM L021-HEADER-03 AFTER 2                  
              WRITE BP13L021-REC   FROM L021-HEADER-04                          
              WRITE BP13L021-REC   FROM L021-HEADER-05                          
              WRITE BP13L021-REC   FROM L021-DETAILS                            
              MOVE                 8 TO WS-L021-LINECTR                         
           ELSE                                                                 
              ADD                  1 TO WS-L021-LINECTR                         
              WRITE BP13L021-REC   FROM L021-DETAILS                            
           END-IF.                                                              
           ADD 1 TO WS-L021-WRITE.                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       6000-GET-NRIC-TYPE.                                                      
      *****************************************************************         
                                                                                
           MOVE SPACES                  TO WS-AV02-AREA.                        
           MOVE F725-NUM-NRIC           TO WS-AV02-NRIC.                        
                                                                                
           IF WS-AV02-NRIC(1:1) = 'S' OR 'T' OR 'F' OR 'G' OR 'M'               
              CALL 'AV02C013' USING WS-AV02-NRIC,                               
                                    WS-AV02-FLAG,                               
                                    WS-AV02-NRIC-TYPE                           
              IF WS-AV02-NRIC-TYPE NOT = 'UIN' AND 'FIN'                        
                 MOVE 'XIN'             TO WS-AV02-NRIC-TYPE                    
              END-IF                                                            
           ELSE                                                                 
              MOVE 'XIN'                TO WS-AV02-NRIC-TYPE                    
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       7000-READ-RDB-TABLE.                                                     
      *****************************************************************         
                                                                                
           MOVE SPACES                  TO WS-RDB-POSTAL.                       
           MOVE 'N'                     TO WS-RDB-EOC.                          
           MOVE F725-NUM-NRIC           TO NUM-IDNTY-NO OF DCLIDNTY-NO.         
           MOVE WS-AV02-NRIC-TYPE       TO NUM-IDNTY-NO-TYPE                    
                                                        OF DCLIDNTY-NO.         
                                                                                
           PERFORM 7100-OPEN-CURSOR     THRU 7100-EXIT.                         
           PERFORM 7200-FETCH-CURSOR    THRU 7200-EXIT                          
             UNTIL END-OF-CURSOR.                                               
           PERFORM 7300-CLOSE-CURSOR    THRU 7300-EXIT.                         
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       7100-OPEN-CURSOR.                                                        
      *****************************************************************         
                                                                                
           EXEC SQL OPEN CUR_RDB   END-EXEC.                                    
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN ZEROES                                                          
                CONTINUE                                                        
                                                                                
           WHEN OTHER                                                           
                MOVE SQLCODE            TO WS-SQLCODE                           
                DISPLAY 'ERROR OPENING CUR_RDB'                                 
                DISPLAY 'SQLCODE : ' WS-SQLCODE                                 
                MOVE WS-SQLCODE         TO RETURN-CODE                          
                GO TO 9000-CLOSE-ROUTINE                                        
           END-EVALUATE.                                                        
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       7200-FETCH-CURSOR.                                                       
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
             FETCH CUR_RDB                                                      
              INTO :DCLPIDB-INTERFACE.NUM-POSTAL-CODE                           
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN ZEROES                                                          
                MOVE NUM-POSTAL-CODE    TO WS-RDB-POSTAL                        
                IF F725-CDE-POSTAL = WS-RDB-POSTAL                              
                   MOVE 'Y'             TO WS-RDB-EOC                           
                END-IF                                                          
                                                                                
           WHEN 100                                                             
                MOVE 'Y'                TO WS-RDB-EOC                           
                                                                                
           WHEN OTHER                                                           
                MOVE SQLCODE            TO WS-SQLCODE                           
                DISPLAY 'ERROR READING CUR_RDB'                                 
                DISPLAY 'SQLCODE : ' WS-SQLCODE                                 
                MOVE WS-SQLCODE         TO RETURN-CODE                          
                GO TO 9000-CLOSE-ROUTINE                                        
           END-EVALUATE.                                                        
                                                                                
       7200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       7300-CLOSE-CURSOR.                                                       
      *****************************************************************         
                                                                                
           EXEC SQL CLOSE CUR_RDB  END-EXEC.                                    
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN ZEROES                                                          
                CONTINUE                                                        
                                                                                
           WHEN OTHER                                                           
                MOVE SQLCODE            TO WS-SQLCODE                           
                DISPLAY 'ERROR CLOSING CUR_RDB'                                 
                DISPLAY 'SQLCODE : ' WS-SQLCODE                                 
                MOVE WS-SQLCODE         TO RETURN-CODE                          
                GO TO 9000-CLOSE-ROUTINE                                        
           END-EVALUATE.                                                        
                                                                                
       7300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       8000-READ-SUBT-TABLE.                                                    
      *****************************************************************         
                                                                                
           MOVE SPACES                  TO WS-SUBT-POSTAL.                      
           MOVE 'N'                     TO WS-SUBT-EOC.                         
           MOVE F725-NUM-NRIC           TO NUM-IDNTY-NO OF DCLSBTNT.            
           MOVE WS-AV02-NRIC-TYPE       TO NUM-IDNTY-NO-TYPE                    
                                                        OF DCLSBTNT.            
                                                                                
           PERFORM 8100-OPEN-CURSOR     THRU 8100-EXIT.                         
           PERFORM 8200-FETCH-CURSOR    THRU 8200-EXIT                          
             UNTIL END-OF-CURSUBT.                                              
           PERFORM 8300-CLOSE-CURSOR    THRU 8300-EXIT.                         
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       8100-OPEN-CURSOR.                                                        
      *****************************************************************         
                                                                                
           EXEC SQL OPEN CUR_SUBT  END-EXEC.                                    
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN ZEROES                                                          
                CONTINUE                                                        
                                                                                
           WHEN OTHER                                                           
                MOVE SQLCODE            TO WS-SQLCODE                           
                DISPLAY 'ERROR OPENING CUR_SUBT'                                
                DISPLAY 'SQLCODE : ' WS-SQLCODE                                 
                MOVE WS-SQLCODE         TO RETURN-CODE                          
                GO TO 9000-CLOSE-ROUTINE                                        
           END-EVALUATE.                                                        
                                                                                
       8100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       8200-FETCH-CURSOR.                                                       
      *****************************************************************         
                                                                                
           EXEC SQL                                                             
             FETCH CUR_SUBT                                                     
              INTO :DCLPIDB-INTERFACE.NUM-POSTAL-CODE                           
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN ZEROES                                                          
                MOVE NUM-POSTAL-CODE    TO WS-SUBT-POSTAL                       
                IF F725-CDE-POSTAL = WS-SUBT-POSTAL                             
                   MOVE 'Y'             TO WS-SUBT-EOC                          
                END-IF                                                          
                                                                                
           WHEN 100                                                             
                MOVE 'Y'                TO WS-SUBT-EOC                          
                                                                                
           WHEN OTHER                                                           
                MOVE SQLCODE            TO WS-SQLCODE                           
                DISPLAY 'ERROR READING CUR_SUBT'                                
                DISPLAY 'SQLCODE : ' WS-SQLCODE                                 
                MOVE WS-SQLCODE         TO RETURN-CODE                          
                GO TO 9000-CLOSE-ROUTINE                                        
           END-EVALUATE.                                                        
                                                                                
       8200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       8300-CLOSE-CURSOR.                                                       
      *****************************************************************         
                                                                                
           EXEC SQL CLOSE CUR_SUBT END-EXEC.                                    
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN ZEROES                                                          
                CONTINUE                                                        
                                                                                
           WHEN OTHER                                                           
                MOVE SQLCODE            TO WS-SQLCODE                           
                DISPLAY 'ERROR CLOSING CUR_SUBT'                                
                DISPLAY 'SQLCODE : ' WS-SQLCODE                                 
                MOVE WS-SQLCODE         TO RETURN-CODE                          
                GO TO 9000-CLOSE-ROUTINE                                        
           END-EVALUATE.                                                        
                                                                                
       8300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-ROUTINE.                                                      
      *****************************************************************         
                                                                                
           DISPLAY '*-------------- BP13C021 --------------*'.                  
           DISPLAY ' RECORDS READ IN BP13F725    : ' WS-F725-READ.              
           DISPLAY ' RECORDS READ IN BP13F730    : ' WS-F730-READ.              
           DISPLAY ' RECORDS WRITTEN IN BP13L021 : ' WS-L021-WRITE.             
           DISPLAY '   APPL, MHA & SUBT MATCHED  : ' WS-L021-SUBT.              
           DISPLAY '   APPL, MHA & RDB MATCHED   : ' WS-L021-MATCH.             
           DISPLAY '   APPL = MHA BUT RDB DIFF   : ' WS-L021-RDB.               
           DISPLAY '   APPL & MHA NOT MATCHED    : ' WS-L021-MHA.               
           DISPLAY '*--------------------------------------*'.                  
                                                                                
           CLOSE      BP13F725                                                  
                      BP13F730                                                  
                      P13F730A                                                  
                      P13F730B                                                  
                      P13F730C                                                  
                      BP13L021.                                                 
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
