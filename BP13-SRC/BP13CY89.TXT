      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CY89.                                                 
      *AUTHOR.        PAULO CAMIA LEGASPI.                                      
      *DATE-WRITTEN.  07/07/11.                                                 
      *****************************************************************         
      *OBJECTIVE - DETERMINE HARDLUCK AND FT-2T CASES.                *         
      *            OUTPUT BP13F585 FOR HARDLUCK CHECK.                *         
      *                                                               *         
      *     INPUT FILE : 1. BP13F595                                  *         
      *                  2. BP13F730                                  *         
      *                                                               *         
      *     OUTPUT FILE: 1. BP13F585                                  *         
      *                                                               *         
      *---------------------------------------------------------------*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------- ---- -----------------------              *         
      * BP134277 07/07/2011 PCL3 NEW PROGRAM.                         *         
      * BP134277 21/07/2011 JB8  TO RESET APPLICATION                 *         
      * BP134388 13/01/2012 ESA1 CATER FOR BOOK-STATUS 'LS' & 'WS'    *         
      * BP134598 17/07/2012 LSB1 CHANGE 2009 TO 2011                  *         
      * BP134780 08/01/2013 IMC1 REMOVE CUTOFF DATE HARDCODING        *         
      * BP135039 16/12/2013 IMC1 ADD NEW BOOK-STATUS                  *         
      * BP135190 04/12/2013 SMR2 EXPAND BP13K813 TO 1000              *         
      * BP135237 03/02/2014 LSB2 CUTOFF 201101, FT TRANS=03           *         
      * BP135237 10/03/2014 IMC1 ADD NEW SMS FND TAG                  *         
      * BP135340 15/05/2014 IMC1 REMOVE BP13K816,REPLACE WITH BP13K813*         
      * BP135635 20/01/2015 LSB1 FTS- TRANS-TYPE =04                  *         
      * BP138080 05/11/2019 SMP3 CATER FOR BOOK STATUS = 'HL'         *         
      * BP138080 11/11/2019 SMP3 POPULATE QUEUE NUMBER                *         
      * BP138080 28/01/2020 KR13 CATER FOR BOOK STATUS = 'HS'         *         
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F595  ASSIGN        TO BP13F595.                         
           SELECT  BP13F205  ASSIGN        TO BP13F205.                         
           SELECT  BP13F730  ASSIGN        TO BP13F730.                         
           SELECT  BP13F585  ASSIGN        TO BP13F585.                         
           SELECT  BP13LY89  ASSIGN        TO BP13LY89.                         
                                                                                
           SELECT  BP13K813  ASSIGN        TO BP13K813                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K813-KEY-FLD                      
                             FILE STATUS   IS K813-STATUS.                      
                                                                                
           SELECT  BP13K595  ASSIGN        TO BP13K595                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K595-KEY-FLD                      
                             FILE STATUS   IS K595-STATUS.                      
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F595                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F595.                                                           
                                                                                
       FD   BP13F730                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F730.                                                           
                                                                                
       FD   BP13F585                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F585.                                                           
                                                                                
       FD   BP13F205                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 80 CHARACTERS                                       
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F205.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13K595                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K595.                                                           
                                                                                
       FD  BP13LY89                                                             
           RECORDING MODE  IS  F.                                               
       01  BP13LY89-REC                     PIC X(132).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
                                                                                
       01  WS-BP13F585-REC             PIC X(2000) VALUE SPACES.                
                                                                                
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F595             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-F730             PIC X(1)  VALUE 'N'.                     
           05  K813-STATUS             PIC 9(2)  VALUE ZEROES.                  
           05  K595-STATUS             PIC 9(2)  VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-I                    PIC 9(02) VALUE ZEROES.                  
           05  WS-J                    PIC 9(02) VALUE ZEROES.                  
           05  WS-CTR                  PIC 9(02) VALUE ZEROES.                  
           05  WS-SUB                  PIC 9(02) VALUE ZEROES.                  
           05  WS-F595-READ            PIC 9(07) VALUE ZEROES.                  
           05  WS-F730-READ            PIC 9(07) VALUE ZEROES.                  
           05  WS-F585-WRITE           PIC 9(07) VALUE ZEROES.                  
           05  WS-LY89-WRITE           PIC 9(07) VALUE ZEROES.                  
           05  WS-K595-FND             PIC X(01) VALUE SPACES.                  
               88  K595-FND                      VALUE 'Y'.                     
           05  WS-CNT-FND              PIC 9(07) VALUE ZEROES.                  
           05  WS-CNT-NFND1            PIC 9(07) VALUE ZEROES.                  
           05  WS-CNT-NFND2            PIC 9(07) VALUE ZEROES.                  
           05  WS-CNT-BYPASS           PIC 9(07) VALUE ZEROES.                  
           05  CNT-LY89-PAGE           PIC 9(05)  VALUE ZEROS.                  
           05  CNT-LY89-SUM-SNO        PIC 9(05)  VALUE ZEROS.                  
           05  CNT-LY89-DTL-SNO        PIC 9(05)  VALUE ZEROS.                  
           05  CNT-LY89-PAGE-REC       PIC 9(05)  VALUE 60.                     
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-SYSTEM-DATE          PIC 9(08) VALUE ZEROES.                  
           05  WS-NUM-NRIC             PIC X(09) VALUE SPACES.                  
           05  WS-DTE-REQUEST          PIC X(08) VALUE SPACES.                  
           05  WS-DTE-LAST-QTR         PIC X(06) VALUE SPACES.                  
           05  WS-CDE-LAST-HH          PIC X(01) VALUE SPACES.                  
           05  WS-NUM-LAST-NRIC        PIC X(09) VALUE SPACES.                  
           05  WS-NUM-LAST-ALLO-CAT    PIC X(03) VALUE SPACES.                  
           05  WS-NUM-LAST-STATUS      PIC X(01) VALUE SPACES.                  
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-NOT-PURE-BTO              PIC X(01) VALUE 'N'.                
               88  NOT-PURE-BTO                       VALUE 'Y'.                
           05  WS-K813-BYPASS               PIC X(01) VALUE 'N'.                
           05  WS-BYPASS-REC                PIC X(01) VALUE 'N'.                
           05  WS-RESET-TAG                 PIC X(01) VALUE 'N'.                
                                                                                
       01  WS-NRICS.                                                            
           05  CNT-IC                       PIC 9(02) VALUE ZEROES.             
           05  WS-SAME-NRIC                 PIC X(01) VALUE 'Y'.                
           05  WS-HA-NRIC OCCURS 4 TIMES    PIC X(09) VALUE SPACES.             
           05  WS-NRIC OCCURS 4 TIMES       PIC X(09) VALUE SPACES.             
                                                                                
      *------------------------------------------------------------*            
      *        LAY-OUT FOR CONTROL LISTING                         *            
      *------------------------------------------------------------*            
       01  LY89-TITLE01.                                                        
           05  FILLER                   PIC X(14) VALUE 'BP13LY89'.             
           05  FILLER                   PIC X(8)  VALUE 'HDB3'.                 
           05  FILLER                   PIC X(18) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(18) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE: '.               
           05  LY89-DATE                PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(8)  VALUE ' PAGE: '.              
           05  LY89-PAGE                PIC ZZZZ9.                              
                                                                                
       01  LY89-TITLE02.                                                        
           05  FILLER                   PIC X(35) VALUE SPACES.                 
           05  FILLER                   PIC X(63) VALUE                         
               'REPORT FOR HARDLUCK/FT2T '.                                     
                                                                                
       01  LY89-TITLE03.                                                        
           05  FILLER                   PIC X(55)  VALUE                        
               '         BK TOT BE  BTO'.                                       
           05  FILLER                   PIC X(54)  VALUE                        
               'DATE   ALLOC BK SMS  ALLO SC   PURE MIXED INCOME NAME1'.        
                                                                                
       01  LY89-TITLE04.                                                        
           05  FILLER                   PIC X(132) VALUE                        
               '   S/NO TAG CNT CNT CNT REGN      NRIC1      NT  FT   BA        
      -        'LLOT   CAT  ST FND  TAG  TAG  BTO  MODE'.                       
                                                                                
       01  LY89-SUMMARY.                                                        
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LY89-SUM-SNO              PIC ZZZZ9 VALUE ZEROES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LY89-SUM-BK-TAG           PIC X(01) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LY89-SUM-TOT-CNT          PIC 99    VALUE ZEROES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LY89-SUM-BE-CNT           PIC 99    VALUE ZEROES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LY89-SUM-BTO-CNT          PIC 99    VALUE ZEROES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LY89-SUM-REGN             PIC X(08) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LY89-SUM-NRIC1            PIC X(09) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LY89-SUM-NT               PIC X(03) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LY89-SUM-FT               PIC X(02) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LY89-SUM-BALLOT           PIC X(07) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LY89-SUM-ALLO-CAT         PIC X(03) VALUE SPACES.                 
           05 FILLER                    PIC X(01) VALUE SPACES.                 
           05 LY89-SUM-SMS-TAG          PIC X(01) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LY89-SUM-BK-STATUS        PIC X(02) VALUE SPACES.                 
           05 FILLER                    PIC X(04) VALUE SPACES.                 
           05 LY89-SUM-ALLOC-TAG        PIC X(02) VALUE SPACES.                 
           05 FILLER                    PIC X(04) VALUE SPACES.                 
           05 LY89-SUM-SC               PIC X(01) VALUE SPACES.                 
           05 FILLER                    PIC X(04) VALUE SPACES.                 
           05 LY89-SUM-PURE-BTO         PIC X(01) VALUE SPACES.                 
           05 FILLER                    PIC X(04) VALUE SPACES.                 
           05 LY89-SUM-MIXED-MODE       PIC X(01) VALUE SPACES.                 
           05 FILLER                    PIC X(04) VALUE SPACES.                 
           05 LY89-SUM-INCOME           PIC Z(06) VALUE ZEROES.                 
           05 FILLER                    PIC X(01) VALUE SPACES.                 
           05 LY89-SUM-NAME1            PIC X(25) VALUE SPACES.                 
                                                                                
       01  LY89-DETAIL.                                                         
           05 FILLER                    PIC X(07) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 FILLER                    PIC X(01) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 FILLER                    PIC X(01) VALUE SPACES.                 
           05 LY89-DTL-SNO              PIC Z9    VALUE ZEROES.                 
           05 FILLER                    PIC X(01) VALUE ')'.                    
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LY89-DTL-REGN             PIC X(08) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LY89-DTL-NRIC1            PIC X(09) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LY89-DTL-NT               PIC X(03) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LY89-DTL-FT               PIC X(02) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LY89-DTL-BALLOT           PIC X(07) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LY89-DTL-ALLO-CAT         PIC X(03) VALUE SPACES.                 
           05 FILLER                    PIC X(04) VALUE SPACES.                 
           05 LY89-DTL-BK-STATUS        PIC X(02) VALUE SPACES.                 
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 1500-READ-BP13F205      THRU 1500-EXIT.                      
           PERFORM 2000-READ-BP13F595      THRU 2000-EXIT.                      
           PERFORM 2100-READ-BP13F730      THRU 2100-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F595  = 'Y' AND                                       
                   WS-EOF-F730 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F595                                                 
                       BP13F730                                                 
                       BP13F205                                                 
                       BP13K813                                                 
                       BP13K595                                                 
              OUTPUT   BP13F585                                                 
                       BP13LY89.                                                
                                                                                
           IF K813-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING ERROR K813 STATUS ' K813-STATUS                  
              MOVE K813-STATUS TO RETURN-CODE                                   
              PERFORM 9999-CLOSE-ROUTINE.                                       
                                                                                
           IF K595-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING ERROR K595 STATUS ' K595-STATUS                  
              MOVE K595-STATUS TO RETURN-CODE                                   
              PERFORM 9999-CLOSE-ROUTINE.                                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1500-READ-BP13F205.                                                      
      ******************************************************************        
                                                                                
           READ BP13F205           AT END                                       
                GO                 TO 9999-CLOSE-ROUTINE.                       
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F595.                                                      
      ******************************************************************        
                                                                                
           READ BP13F595           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F595                             
                MOVE HIGH-VALUES   TO   F595-NUM-NRIC1                          
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO   WS-F595-READ.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2100-READ-BP13F730.                                                      
      ******************************************************************        
                                                                                
           READ BP13F730          AT   END                                      
                MOVE 'Y'           TO   WS-EOF-F730                             
                MOVE HIGH-VALUES   TO   F730-NUM-NRIC1                          
                GO                 TO   2100-EXIT.                              
                                                                                
           ADD  1                  TO   WS-F730-READ.                           
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           IF F595-NUM-NRIC1 = F730-NUM-NRIC1                                   
              ADD 1                               TO WS-CNT-FND                 
              MOVE 'N'                            TO WS-NOT-PURE-BTO            
              MOVE ZEROES                         TO WS-CTR                     
              MOVE SPACES                         TO WS-DTE-REQUEST             
                                                     WS-DTE-LAST-QTR            
                                                     WS-CDE-LAST-HH             
                                                     WS-NUM-LAST-NRIC           
                                                   WS-NUM-LAST-ALLO-CAT         
                                                     WS-NUM-LAST-STATUS         
                                                                                
              MOVE SPACES                         TO BP13F585-REC               
              INITIALIZE                             BP13F585-REC               
                                                                                
              PERFORM 3001-KEEP-NRIC            THRU 3001-EXIT                  
                                                                                
              PERFORM 3600-CHECK-ALLO-CAT       THRU 3600-EXIT                  
              PERFORM 5100-OUTPUT-SUMMARY       THRU 5100-EXIT                  
                                                                                
              PERFORM UNTIL WS-EOF-F730 = 'Y'                                   
                         OR WS-CTR > 20                                         
                         OR WS-SAME-NRIC NOT = 'Y'                              
                 PERFORM 3500-PROCEED-SAME-NRIC THRU 3500-EXIT                  
                 PERFORM 2100-READ-BP13F730     THRU 2100-EXIT                  
                 PERFORM 3002-CHECK-SAME-NRIC   THRU 3002-EXIT                  
              END-PERFORM                                                       
                                                                                
              PERFORM 4000-WRITE-BP13F585       THRU 4000-EXIT                  
              PERFORM 2000-READ-BP13F595        THRU 2000-EXIT                  
           ELSE                                                                 
              IF F595-NUM-NRIC1 < F730-NUM-NRIC1                                
                 ADD 1                            TO WS-CNT-NFND1               
                 PERFORM 2000-READ-BP13F595     THRU 2000-EXIT                  
              ELSE                                                              
                  ADD 1                           TO WS-CNT-NFND2               
                  PERFORM 2100-READ-BP13F730    THRU 2100-EXIT                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3001-KEEP-NRIC.                                                          
      ******************************************************************        
                                                                                
           MOVE SPACES                         TO WS-NRICS.                     
           INITIALIZE                             WS-NRICS.                     
           MOVE 'Y'                            TO WS-SAME-NRIC                  
                                                                                
           MOVE F595-NUM-NRIC1                 TO WS-HA-NRIC(1).                
           MOVE F595-NUM-NRIC2                 TO WS-HA-NRIC(2).                
           MOVE F595-NUM-NRIC3                 TO WS-HA-NRIC(3).                
           MOVE F595-NUM-NRIC4                 TO WS-HA-NRIC(4).                
                                                                                
           PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > 4                      
              IF WS-HA-NRIC(WS-I)(1:2) NOT = SPACES AND LOW-VALUES              
                                          AND '##'  AND HIGH-VALUES             
                 IF WS-HA-NRIC(WS-I) NOT = WS-NRIC(1) AND WS-NRIC(2)            
                                       AND WS-NRIC(3) AND WS-NRIC(4)            
                    ADD 1                      TO CNT-IC                        
                    MOVE WS-HA-NRIC(WS-I)      TO WS-NRIC(CNT-IC)               
                 END-IF                                                         
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           MOVE F730-NUM-NRIC1                 TO WS-HA-NRIC(1).                
           MOVE F730-NUM-NRIC2                 TO WS-HA-NRIC(2).                
           MOVE F730-NUM-NRIC3                 TO WS-HA-NRIC(3).                
           MOVE F730-NUM-NRIC4                 TO WS-HA-NRIC(4).                
                                                                                
           PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > 4                      
              IF WS-HA-NRIC(WS-I)(1:2) NOT = SPACES AND LOW-VALUES              
                                          AND '##'  AND HIGH-VALUES             
                 IF WS-HA-NRIC(WS-I) NOT = WS-NRIC(1) AND WS-NRIC(2)            
                                       AND WS-NRIC(3) AND WS-NRIC(4)            
                    ADD 1                      TO CNT-IC                        
                    MOVE WS-HA-NRIC(WS-I)      TO WS-NRIC(CNT-IC)               
                 END-IF                                                         
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       3001-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3002-CHECK-SAME-NRIC.                                                    
      ******************************************************************        
                                                                                
           MOVE F730-NUM-NRIC1                 TO WS-HA-NRIC(1)                 
           MOVE F730-NUM-NRIC2                 TO WS-HA-NRIC(2)                 
           MOVE F730-NUM-NRIC3                 TO WS-HA-NRIC(3)                 
           MOVE F730-NUM-NRIC4                 TO WS-HA-NRIC(4)                 
                                                                                
           MOVE 'N'                            TO WS-SAME-NRIC                  
                                                                                
           PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > 4                      
              IF WS-HA-NRIC(WS-I)(1:2) NOT = SPACES AND LOW-VALUES              
                                          AND '##'  AND HIGH-VALUES             
                 IF WS-HA-NRIC(WS-I) = WS-NRIC(1) OR WS-NRIC(2)                 
                                    OR WS-NRIC(3) OR WS-NRIC(4)                 
                    MOVE 'Y'                   TO WS-SAME-NRIC                  
                    PERFORM VARYING WS-J FROM 1 BY 1 UNTIL WS-J > 4             
                       IF WS-HA-NRIC(WS-J)(1:2) NOT = SPACES AND '##'           
                                   AND LOW-VALUES AND HIGH-VALUES               
                          IF WS-HA-NRIC(WS-J) NOT = WS-NRIC(1) AND              
                             WS-NRIC(2) AND WS-NRIC(3) AND WS-NRIC(4)           
                             ADD 1                 TO CNT-IC                    
                             MOVE WS-HA-NRIC(WS-J) TO WS-NRIC(CNT-IC)           
                          END-IF                                                
                       END-IF                                                   
                    END-PERFORM                                                 
                 END-IF                                                         
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       3002-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3100-READ-BP13K813.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                          TO BP13K813-REC.                
           INITIALIZE                              BP13K813-REC.                
                                                                                
           MOVE 'N'                             TO WS-K813-BYPASS.              
           MOVE F730-CDE-NT1                    TO K813-NUM-ZONE.               
      *    MOVE F730-CDE-FLAT-TYPE              TO K813-NUM-FLAT-TYPE.          
      *    MOVE F730-DTE-BALLOT                 TO K813-DTE-BALLOT.             
                                                                                
           START BP13K813 KEY >= K813-KEY-FLD                                   
                                                                                
           EVALUATE K813-STATUS                                                 
               WHEN 00                                                          
                    READ BP13K813 NEXT RECORD                                   
                                                                                
                    IF K813-NUM-ZONE = F730-CDE-NT1                             
                       IF K813-NUM-HARDLUCK-BYPASS = 'Y' OR                     
                          K813-NUM-MATURE-EST-TAG  = 'Y'                        
                          MOVE 'Y'                 TO WS-K813-BYPASS            
                       END-IF                                                   
                    ELSE                                                        
                       DISPLAY ' NT1 NOT FND : ' F730-CDE-NT1                   
                               ' KEY : ' F730-KEY-FLD                           
                    END-IF                                                      
                                                                                
               WHEN 23                                                          
                    MOVE 'N'                    TO WS-K813-BYPASS               
                                                                                
               WHEN OTHER                                                       
                    MOVE K813-STATUS            TO RETURN-CODE                  
                    DISPLAY 'ERROR READING BP13K813: ' K813-STATUS              
                            ' KEY : ' K813-KEY-FLD                              
                    PERFORM  9999-CLOSE-ROUTINE THRU 9999-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       3200-PROCESS-BP13F585.                                                   
      ****************************************************************          
      ** RESET                                                                  
           MOVE 'N'                    TO WS-RESET-TAG                          
           IF F730-DTE-BALLOT >= '201101' AND                                   
              F730-DTE-BALLOT <  F205-DTE-REQUEST(1:6)                          
              IF (BP13F730-REC(476:2) NOT = SPACES AND LOW-VALUES) OR           
                 (F730-NUM-BOOK-STATUS = 'BF' OR 'LF' OR 'WF' OR                
                                         'LS' OR 'WS' OR 'LA' OR                
                                         'WA' OR 'LG' OR 'WG' OR 'HS')          
                 MOVE 'Y'              TO WS-RESET-TAG                          
              ELSE                                                              
                 IF F730-NUM-BOOK-STATUS = 'NC' OR 'NF' OR 'NV' OR              
                                           'NS' OR 'SC' OR 'SF' OR              
                                           'HL'                                 
                    CONTINUE                                                    
                 ELSE                                                           
                    IF ((BP13F730-REC(476:2) = SPACES OR LOW-VALUES) AND        
                        (BP13F730-REC(493:2) = 'FN' OR 'NI' OR 'SI' OR          
                                               'RQ' OR                          
                                               'RI' OR 'RG' OR 'NE'))           
                       MOVE 'Y'        TO WS-RESET-TAG                          
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-RESET-TAG = 'Y'                                                
              PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > 20                  
                 MOVE SPACES           TO F585-UNSUCC-REC(WS-I)                 
                 INITIALIZE               F585-UNSUCC-REC(WS-I)                 
                 MOVE ZEROES           TO WS-CTR                                
              END-PERFORM                                                       
              GO TO 3200-EXIT                                                   
           END-IF                                                               
                                                                                
           ADD  1                      TO WS-CTR.                               
                                                                                
           MOVE F730-NUM-REGN          TO F585-NUM-REGN(WS-CTR).                
      **                                                                        
      ** TO GET ORIG NRIC                                                       
           MOVE SPACES                 TO K595-KEY-FLD                          
           MOVE F730-NUM-REGN          TO K595-NUM-REGN-REF                     
           PERFORM 8001-READ-BP13K595  THRU 8001-EXIT                           
           IF K595-FND                                                          
              MOVE K595-NUM-NRIC1      TO F730-NUM-NRIC1                        
              MOVE K595-NUM-NRIC2      TO F730-NUM-NRIC2                        
              MOVE K595-NUM-NRIC3      TO F730-NUM-NRIC3                        
              MOVE K595-NUM-NRIC4      TO F730-NUM-NRIC4                        
           END-IF.                                                              
                                                                                
           IF (F730-NUM-NRIC1      = SPACES OR LOW-VALUES) OR                   
              (F730-NUM-NRIC1(1:1) = '#')                                       
              MOVE SPACES              TO F730-NUM-NRIC1                        
           END-IF.                                                              
                                                                                
           IF (F730-NUM-NRIC2      = SPACES OR LOW-VALUES) OR                   
              (F730-NUM-NRIC2(1:1) = '#')                                       
              MOVE SPACES              TO F730-NUM-NRIC2                        
           END-IF.                                                              
                                                                                
           IF (F730-NUM-NRIC3      = SPACES OR LOW-VALUES) OR                   
              (F730-NUM-NRIC3(1:1) = '#')                                       
              MOVE SPACES              TO F730-NUM-NRIC3                        
           END-IF.                                                              
                                                                                
           IF (F730-NUM-NRIC4      = SPACES OR LOW-VALUES) OR                   
              (F730-NUM-NRIC4(1:1) = '#')                                       
              MOVE SPACES              TO F730-NUM-NRIC4                        
           END-IF.                                                              
                                                                                
           IF F730-DTE-REQUEST NOT = SPACES AND LOW-VALUES AND ZEROES           
              IF WS-DTE-REQUEST = SPACES OR LOW-VALUES                          
                 MOVE F730-DTE-REQUEST TO WS-DTE-REQUEST                        
              END-IF                                                            
                                                                                
              IF F730-DTE-REQUEST > WS-DTE-REQUEST                              
                 MOVE F730-DTE-REQUEST        TO WS-DTE-REQUEST                 
                 MOVE F730-DTE-BALLOT         TO WS-DTE-LAST-QTR                
                 MOVE F730-CDE-HOUSEHOLD      TO WS-CDE-LAST-HH                 
                 MOVE F730-NUM-NRIC1          TO WS-NUM-LAST-NRIC               
                 MOVE F730-NUM-ALLO-CAT       TO WS-NUM-LAST-ALLO-CAT           
                 MOVE F730-CDE-REQUEST-STATUS TO WS-NUM-LAST-STATUS             
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE F730-NUM-NRIC1         TO F585-NUM-NRIC1(WS-CTR).               
           MOVE F730-NUM-NRIC2         TO F585-NUM-NRIC2(WS-CTR).               
           MOVE F730-NUM-NRIC3         TO F585-NUM-NRIC3(WS-CTR).               
           MOVE F730-NUM-NRIC4         TO F585-NUM-NRIC4(WS-CTR).               
                                                                                
           MOVE F730-DTE-BALLOT        TO F585-DTE-BALLOT(WS-CTR).              
           MOVE F730-NUM-ALLO-CAT      TO F585-NUM-ALLO-CAT(WS-CTR).            
           MOVE F730-CDE-NT1           TO F585-CDE-NT(WS-CTR).                  
           MOVE F730-CDE-FLAT-TYPE     TO F585-CDE-FLAT-TYPE(WS-CTR).           
           MOVE F730-CDE-HOUSEHOLD     TO F585-CDE-HOUSEHOLD(WS-CTR).           
           MOVE F730-NUM-MCPS          TO F585-CDE-MCPS-TAG(WS-CTR).            
           MOVE F730-CDE-REQUEST-STATUS                                         
                                  TO F585-CDE-REQUEST-STATUS(WS-CTR).           
           MOVE F730-DTE-REQUEST       TO F585-DTE-REQUEST(WS-CTR).             
           MOVE F730-NUM-ABORT         TO F585-NUM-ABORT(WS-CTR).               
                                                                                
           MOVE F730-NUM-BOOK-STATUS   TO F585-NUM-BOOK-STATUS(WS-CTR).         
           MOVE F730-NUM-NT-FT-QUEUE   TO F585-NUM-QUEUE(WS-CTR).               
           MOVE BP13F595-REC(461:1)    TO F585-CDE-BALLOT-HH(WS-CTR).           
           MOVE BP13F595-REC(467:1)    TO F585-CDE-BALLOT-HH(WS-CTR).           
                                                                                
           PERFORM 5200-OUTPUT-DETAIL  THRU 5200-EXIT.                          
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3300-CHECK-BYPASS.                                                       
      ****************************************************************          
                                                                                
           MOVE 'N'                         TO WS-BYPASS-REC.                   
                                                                                
           IF F730-NUM-ALLO-CAT = 'BTO'                                         
              IF (BP13F730-REC(476:9)  = SPACES AND                             
                  F730-NUM-BOOK-STATUS = SPACES AND                             
                 (BP13F730-REC(493:2)  = SPACES OR '00'))                       
                  OR                                                            
                                                                                
                 (BP13F730-REC(476:9)  = SPACES AND                             
                  BP13F730-REC(493:2)  = 'FN' AND                               
                  F730-NUM-BOOK-STATUS = SPACES AND                             
                  F730-DTE-BALLOT      > '200805')                              
                  OR                                                            
                                                                                
                 (F730-CDE-REQUEST-STATUS  = 'W' AND                            
                  F730-NUM-BOOK-STATUS     = SPACES)                            
                                                                                
                  IF (BP13F730-REC(476:9)  = SPACES OR LOW-VALUES) AND          
                     (BP13F730-REC(493:2)  = 'FN') AND                          
                     (F730-NUM-BOOK-STATUS = SPACES) AND                        
                     (F730-DTE-BALLOT     >= '200805')                          
                     MOVE 'Y'              TO WS-BYPASS-REC                     
                  ELSE                                                          
                     MOVE 'Y'              TO WS-BYPASS-REC                     
                  END-IF                                                        
              ELSE                                                              
                IF F730-NUM-BOOK-STATUS = 'NE' OR                               
                   BP13F730-REC(493:2)  = 'NE'                                  
                   MOVE 'Y'                TO WS-BYPASS-REC                     
                END-IF                                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3500-PROCEED-SAME-NRIC.                                                  
      ****************************************************************          
              IF F205-NUM-OPTION = 'YH'                                         
                 IF F730-CDE-HOUSEHOLD = 'G' OR                                 
                    F730-CDE-BALLOT-HOUSEHOLD = 'G'                             
                     GO TO 3500-EXIT                                            
              END-IF                                                            
                                                                                
              IF F205-NUM-OPTION = 'YJ' OR 'UG'                                 
                 IF F730-CDE-HOUSEHOLD = 'H' OR                                 
                    F730-CDE-BALLOT-HOUSEHOLD = 'H'                             
                     GO TO 3500-EXIT                                            
              END-IF                                                            
                                                                                
              IF (F730-NUM-BOOK-STATUS = 'BF' OR 'LF' OR 'WF' OR                
                                         'LS' OR 'WS' OR 'LA' OR                
                                         'WA' OR 'LG' OR 'WG' OR 'HS')          
                 PERFORM 3200-PROCESS-BP13F585  THRU 3200-EXIT                  
              END-IF.                                                           
                                                                                
           PERFORM 3100-READ-BP13K813           THRU 3100-EXIT.                 
                                                                                
           IF (F730-CDE-FLAT-TYPE    = '1A')  OR                                
              (F730-CDE-FLAT-TYPE    = 'SP')  OR                                
              (F730-NUM-ALLO-CAT NOT = 'BTO') OR                                
              (WS-K813-BYPASS        = 'Y')                                     
              ADD 1                               TO WS-CNT-BYPASS              
           ELSE                                                                 
              PERFORM 3300-CHECK-BYPASS         THRU 3300-EXIT                  
                                                                                
              IF WS-BYPASS-REC = 'Y'                                            
                 ADD 1                            TO WS-CNT-BYPASS              
              ELSE                                                              
                 PERFORM 3200-PROCESS-BP13F585  THRU 3200-EXIT                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       3600-CHECK-ALLO-CAT.                                                     
      **************************************************************            
                                                                                
           IF F730-DTE-BALLOT > '201101'                                        
              IF F730-NUM-ALLO-CAT NOT = 'BTO'                                  
                 MOVE 'Y'                 TO WS-NOT-PURE-BTO                    
              ELSE                                                              
                 PERFORM 3100-READ-BP13K813 THRU 3100-EXIT                      
                 IF K813-NUM-MATURE-EST-TAG = 'Y'                               
                    MOVE 'Y'              TO WS-NOT-PURE-BTO                    
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       3700-READ-BP13K813.                                                      
      **************************************************************            
                                                                                
           MOVE SPACES                              TO BP13K813-REC.            
           INITIALIZE                                  BP13K813-REC.            
                                                                                
           MOVE F730-CDE-NT1                        TO K813-NUM-ZONE.           
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE K813-STATUS                                                 
               WHEN 00                                                          
                    CONTINUE                                                    
               WHEN 23                                                          
                    MOVE SPACES              TO K813-CDE-NT                     
                                                K813-NUM-MATURE-EST-TAG         
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K813 , STATUS = '                
                            K813-STATUS                                         
                    GO TO 9999-CLOSE-ROUTINE                                    
           END-EVALUATE.                                                        
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-WRITE-BP13F585.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES              TO WS-BP13F585-REC.                         
           INITIALIZE                  WS-BP13F585-REC.                         
                                                                                
           MOVE F595-NUM-NRIC1      TO F585-NUM-NRIC.                           
                                                                                
           EVALUATE F205-NUM-OPTION                                             
               WHEN 'YH'                                                        
                    IF F595-CDE-ALLOC-SCH = 'FTS'                               
                       MOVE '4'        TO F585-NUM-TRANS                        
                    ELSE                                                        
                       MOVE '1'        TO F585-NUM-TRANS                        
                    END-IF                                                      
               WHEN 'YJ'                                                        
                    MOVE '2'        TO F585-NUM-TRANS                           
               WHEN 'UG'                                                        
                    MOVE '3'        TO F585-NUM-TRANS                           
           END-EVALUATE.                                                        
                                                                                
           MOVE BP13F595-REC(470:2) TO F585-NUM-UNSUCC-TOT.                     
           MOVE BP13F595-REC(465:2) TO F585-NUM-UNSUCC-BE.                      
           MOVE BP13F595-REC(467:2) TO F585-NUM-UNSUCC-BTO.                     
           MOVE BP13F595-REC(472:2) TO F585-NUM-UNSUCC-DBS.                     
           MOVE F595-NUM-REGN       TO F585-NUM-SUCC-REGN.                      
           MOVE F595-DTE-BALLOT     TO F585-DTE-SUCC-BALLOT.                    
           MOVE F595-NUM-ALLO-CAT   TO F585-NUM-SUCC-ALLO-CAT.                  
           MOVE F595-NUM-SMS-STATUS TO F585-NUM-EXIST-LESSEE.                   
           MOVE WS-DTE-LAST-QTR     TO F585-DTE-LAST-QTR.                       
           MOVE WS-CDE-LAST-HH      TO F585-CDE-LAST-HH.                        
           MOVE WS-NUM-LAST-NRIC    TO F585-NUM-LAST-NRIC.                      
           MOVE WS-NUM-LAST-ALLO-CAT TO F585-NUM-LAST-ALLO-CAT.                 
           MOVE WS-NUM-LAST-STATUS   TO F585-NUM-LAST-STATUS.                   
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO F585-DTE-UPDATE.                  
           MOVE 'BP13CY89'                 TO F585-NUM-USERID.                  
                                                                                
                                                                                
           MOVE BP13F585-REC      TO WS-BP13F585-REC.                           
                                                                                
                                                                                
           WRITE BP13F585-REC.                                                  
           ADD 1                  TO WS-F585-WRITE.                             
                                                                                
           MOVE WS-BP13F585-REC   TO BP13F585-REC.                              
                                                                                
           MOVE SPACES            TO WS-NUM-NRIC.                               
                                                                                
           PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > 4                      
              IF WS-NRIC(WS-I)(1:2) NOT = SPACES AND LOW-VALUES AND '##'        
                                      AND HIGH-VALUES                           
                 IF WS-NRIC(WS-I) NOT = F595-NUM-NRIC1                          
                    MOVE WS-NRIC(WS-I)        TO WS-BP13F585-REC(1:9)           
                    WRITE BP13F585-REC      FROM WS-BP13F585-REC                
                    ADD 1                     TO WS-F585-WRITE                  
                 END-IF                                                         
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       5000-OUTPUT-HEADER.                                                      
      **************************************************************            
                                                                                
           ADD 1                           TO CNT-LY89-PAGE                     
           MOVE CNT-LY89-PAGE              TO LY89-PAGE.                        
                                                                                
           MOVE SPACES                     TO BP13LY89-REC                      
           WRITE BP13LY89-REC AFTER PAGE.                                       
           WRITE BP13LY89-REC FROM LY89-TITLE01 AFTER 3.                        
           WRITE BP13LY89-REC FROM LY89-TITLE02.                                
           WRITE BP13LY89-REC FROM LY89-TITLE03 AFTER 2.                        
           WRITE BP13LY89-REC FROM LY89-TITLE04.                                
                                                                                
           MOVE 8                          TO CNT-LY89-PAGE-REC.                
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       5100-OUTPUT-SUMMARY.                                                     
      **************************************************************            
                                                                                
           ADD 1                           TO CNT-LY89-PAGE-REC.                
           IF CNT-LY89-PAGE-REC > 60                                            
              PERFORM 5000-OUTPUT-HEADER THRU 5000-EXIT                         
              ADD 1                        TO CNT-LY89-PAGE-REC                 
           END-IF.                                                              
                                                                                
           INITIALIZE                         LY89-SUMMARY.                     
           MOVE 0                          TO CNT-LY89-DTL-SNO.                 
           ADD 1                           TO CNT-LY89-SUM-SNO.                 
           MOVE CNT-LY89-SUM-SNO           TO LY89-SUM-SNO.                     
           MOVE F730-NUM-REGN              TO LY89-SUM-REGN.                    
           MOVE F730-NUM-NRIC1             TO LY89-SUM-NRIC1.                   
                                                                                
           PERFORM 3100-READ-BP13K813    THRU 3100-EXIT                         
           IF K813-CDE-NT NOT = SPACES                                          
              MOVE K813-CDE-NT             TO LY89-SUM-NT                       
           ELSE                                                                 
              MOVE F730-CDE-NT1            TO LY89-SUM-NT                       
           END-IF.                                                              
                                                                                
           MOVE F730-CDE-FLAT-TYPE         TO LY89-SUM-FT.                      
           STRING F730-DTE-BALLOT(1:4) '-' F730-DTE-BALLOT(5:2)                 
                  DELIMITED BY SIZE INTO LY89-SUM-BALLOT.                       
                                                                                
           MOVE F730-NUM-ALLO-CAT          TO LY89-SUM-ALLO-CAT.                
           MOVE F730-NUM-SMS-STATUS        TO LY89-SUM-SMS-TAG.                 
           MOVE BP13F730-REC(493:2)        TO LY89-SUM-ALLOC-TAG.               
           MOVE BP13F730-REC(469:1)        TO LY89-SUM-BK-TAG.                  
           MOVE BP13F595-REC(465:1)        TO LY89-SUM-BE-CNT.                  
           MOVE BP13F595-REC(467:1)        TO LY89-SUM-BTO-CNT.                 
           MOVE BP13F595-REC(470:1)        TO LY89-SUM-TOT-CNT.                 
           MOVE F730-NUM-BOOK-STATUS       TO LY89-SUM-BK-STATUS.               
           MOVE BP13F730-REC(243:1)        TO LY89-SUM-SC.                      
                                                                                
           IF NOT-PURE-BTO                                                      
              MOVE SPACE                   TO LY89-SUM-PURE-BTO                 
              MOVE 'Y'                     TO LY89-SUM-MIXED-MODE               
           ELSE                                                                 
              MOVE 'Y'                     TO LY89-SUM-PURE-BTO                 
              MOVE SPACE                   TO LY89-SUM-MIXED-MODE               
           END-IF.                                                              
                                                                                
           WRITE BP13LY89-REC            FROM LY89-SUMMARY.                     
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       5200-OUTPUT-DETAIL.                                                      
      **************************************************************            
                                                                                
           ADD 1                           TO CNT-LY89-PAGE-REC                 
           IF CNT-LY89-PAGE-REC > 60                                            
              PERFORM 5000-OUTPUT-HEADER THRU 5000-EXIT                         
              ADD 1                        TO CNT-LY89-PAGE-REC                 
           END-IF.                                                              
                                                                                
           INITIALIZE                         LY89-DETAIL.                      
           ADD 1                           TO CNT-LY89-DTL-SNO.                 
           MOVE CNT-LY89-DTL-SNO           TO LY89-DTL-SNO.                     
           MOVE F730-NUM-REGN              TO LY89-DTL-REGN.                    
           MOVE F730-NUM-NRIC1             TO LY89-DTL-NRIC1.                   
                                                                                
           PERFORM 3100-READ-BP13K813    THRU 3100-EXIT                         
           IF K813-CDE-NT NOT = SPACES                                          
              MOVE K813-CDE-NT             TO LY89-DTL-NT                       
           ELSE                                                                 
              MOVE F730-CDE-NT1            TO LY89-DTL-NT                       
           END-IF.                                                              
                                                                                
           MOVE F730-CDE-FLAT-TYPE         TO LY89-DTL-FT.                      
           STRING F730-DTE-BALLOT(1:4) '-' F730-DTE-BALLOT(5:2)                 
                  DELIMITED BY SIZE INTO LY89-DTL-BALLOT.                       
           MOVE F730-NUM-ALLO-CAT          TO LY89-DTL-ALLO-CAT.                
           MOVE F730-NUM-BOOK-STATUS       TO LY89-DTL-BK-STATUS.               
                                                                                
           WRITE BP13LY89-REC            FROM LY89-DETAIL.                      
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=======================*                                                 
       8001-READ-BP13K595.                                                      
      *=======================*                                                 
                                                                                
           MOVE 'N'                      TO WS-K595-FND                         
                                                                                
           START BP13K595 KEY >= K595-KEY-FLD                                   
                                                                                
           IF K595-STATUS = 00 OR 02                                            
              READ BP13K595 NEXT RECORD                                         
              IF K595-STATUS = 00 OR 02                                         
                 MOVE 'Y'                TO WS-K595-FND                         
              ELSE                                                              
                 IF K595-STATUS = 23 OR 10                                      
                    DISPLAY 'BP13K595 - RECORD NOT FND : ' K595-KEY-FLD         
                 ELSE                                                           
                    DISPLAY 'BP13K595 - ERROR READING NEXT : '                  
                            K595-STATUS                                         
                    GO TO 9999-CLOSE-ROUTINE                                    
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              DISPLAY 'BP13K595 - ERROR READING NEXT : '                        
                      K595-STATUS  ' KEY ' K595-KEY-FLD                         
      *       GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
       8001-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CY89'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F595 RECORDS READ ........ : '                
                    WS-F595-READ.                                               
           DISPLAY '(2) NO OF BP13F595 RECORDS READ ........ : '                
                    WS-F730-READ                                                
           DISPLAY '(3) NO OF NRIC MATCHED ................. : '                
                    WS-CNT-FND.                                                 
           DISPLAY '(4) NO OF F595 NOT FD IN F595........... : '                
                    WS-CNT-NFND1.                                               
           DISPLAY '    NO OF F595 NOT FD IN F730.......... : '                 
                    WS-CNT-NFND2.                                               
           DISPLAY '    NO OF RECS BYPASS (BTO/1A).......... : '                
                    WS-CNT-BYPASS.                                              
           DISPLAY '                                           '                
           DISPLAY '(5) NO OF BP13F585 WRITE ............... : '                
                    WS-F585-WRITE.                                              
           DISPLAY '(6) NO OF BP13LY89 WRITE ............... : '                
                    WS-LY89-WRITE.                                              
                                                                                
           CLOSE BP13F595                                                       
                 BP13F730                                                       
                 BP13F585                                                       
                 BP13K813                                                       
                 BP13K595                                                       
                 BP13LY89.                                                      
                                                                                
           IF K813-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSING ERROR K813 STATUS ' K813-STATUS                  
              MOVE K813-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           IF K595-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSING ERROR K595 STATUS ' K595-STATUS                  
              MOVE K595-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
