      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C11B.                                                 
      *AUTHOR.        ELGER DE GUZMAN.                                          
      *DATE-WRITTEN.  23/10/2013.                                               
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : THIS PROGRAM WILL UPDATE THE SUMMARY COUNT  *           
      *                 FILE USE BY PORTAL.                         *           
      *                 PROGRAM WILL UPDATE THE SUMMARY REGISTRATION*           
      *                 NUMBER LIST FOR SOCA SUMMARY PORTAL.        *           
      *               : FOR KPI AND SA/AO SIGNED CASES.             *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  &&BP13F80A -- TEMP FILE CONTAINS ALL OUTSTANDING CASES*           
      *                                                             *           
      *   OUTPUT FILES:                                             *           
      *   1.  BP13FB40 -- SUMMARY COUNT FILE                        *           
      *                                                             *           
      * REF      DATE        BY   DESCRIPTIONS                      *           
      * -------  --------    ---- ------------                      *           
      * BP135131 03/12/2013  ESD1 NEW PROGRAM.                      *           
      * BP135179 12/15/2014  ESD1 CATER TO RETURN 50 MONTHS         *           
      * BP138819 04/08/2021  ZDD1 CATER TO CCA 1F                   *           
      * =========================================================== *           
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
       CONFIGURATION SECTION.                                                   
      *------------------------*                                                
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *------------------------*                                                
       INPUT-OUTPUT SECTION.                                                    
      *------------------------*                                                
       FILE-CONTROL.                                                            
      *------------------------*                                                
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K110-KEY-FLD                       
                            FILE STATUS   IS BM06K110-STATUS.                   
                                                                                
           SELECT BP13F300 ASSIGN TO BP13F300.                                  
                                                                                
           SELECT BP13F80A ASSIGN TO BP13F80A.                                  
                                                                                
           SELECT BP13FB40 ASSIGN TO BP13FB40.                                  
                                                                                
           SELECT BP13L11B ASSIGN TO BP13L11B.                                  
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
      *------------------------*                                                
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD BP13F300                                                              
            RECORD CONTAINS  2000  CHARACTERS                                   
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F300.                                                   00150000
                                                                                
       FD BP13F80A                                                              
            RECORD CONTAINS  2000  CHARACTERS                                   
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                   00150000
                                                                                
       FD BP13FB40                                                              
            RECORD CONTAINS 2000  CHARACTERS                                    
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13FB40.                                                   00150000
                                                                                
       FD BP13L11B                                                              
            RECORD CONTAINS 132  CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       01 PRINT-REC          PIC X(132).                                00150000
                                                                                
      *-------------------------------------------------------------            
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  BM06K110-STATUS            PIC 9(2)       VALUE ZEROES.          
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-FB40-WRITE              PIC 9(5)       VALUE ZEROES.          
           05  WS-CNT-F80A-READ           PIC 9(5)       VALUE ZEROES.          
           05  WS-CNT-F300-READ           PIC 9(5)       VALUE ZEROES.          
           05  WS-K110-NOTFND             PIC 9(5)       VALUE ZEROES.          
           05  WS-SKIP-COUNT              PIC 9(5)       VALUE ZEROES.          
           05  WS-PAGE-CNT                PIC 9(5)       VALUE ZERO.            
           05  WS-LINE-CNT                PIC 9(5)       VALUE ZERO.            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F80A-EOF                PIC X          VALUE 'N'.             
           05  WS-F300-EOF                PIC X          VALUE 'N'.             
           05  WS-K110-REC-FND            PIC X          VALUE SPACES.          
           05  WS-SKIP-REC                PIC X          VALUE 'N'.             
           05  WS-ERROR-REPORT            PIC X          VALUE 'N'.             
                                                                                
       01  WS-DTE-CUR.                                                          
           05  WS-CC                      PIC 99.                               
           05  WS-YY                      PIC 99.                               
           05  WS-MM                      PIC 99.                               
           05  WS-DD                      PIC 99.                               
                                                                                
       01  WS-SAVE-AREA.                                                        
           05  WS-REC-TYPE                PIC X.                                
           05  WS-LOAN-TAG                PIC X.                                
           05  WS-MONTH-OUT               PIC 9(2) VALUE ZEROES.                
           05  WS-ROOM-TYPE               PIC X(2) VALUE SPACES.                
           05  WS-REGN                    PIC X(8) VALUE SPACES.                
           05  WS-DATE-TEMP               PIC X(8) VALUE SPACES.                
           05  WS-CNT-KPI-DCNT.                                                 
               10 WS-SA-KPI-D1MO            PIC 9(5) VALUE ZEROES.              
               10 WS-SA-KPI-D2MO            PIC 9(5) VALUE ZEROES.              
               10 WS-SA-KPI-D3MO            PIC 9(5) VALUE ZEROES.              
               10 WS-SA-KPI-D4MO            PIC 9(5) VALUE ZEROES.              
               10 WS-SA-KPI-D5MO            PIC 9(5) VALUE ZEROES.              
                                                                                
           05  WS-CNT-KPI-ECNT.                                                 
               10 WS-SA-KPI-E1MO            PIC 9(5) VALUE ZEROES.              
               10 WS-SA-KPI-E2MO            PIC 9(5) VALUE ZEROES.              
               10 WS-SA-KPI-E3MO            PIC 9(5) VALUE ZEROES.              
               10 WS-SA-KPI-E4MO            PIC 9(5) VALUE ZEROES.              
               10 WS-SA-KPI-E5MO            PIC 9(5) VALUE ZEROES.              
                                                                                
           05  WS-CNT-F300.                                                     
               10 WS-F300-SA               PIC 9(5) VALUE ZEROES.               
               10 WS-F300-2R               PIC 9(5) VALUE ZEROES.               
               10 WS-F300-3R               PIC 9(5) VALUE ZEROES.               
               10 WS-F300-4R               PIC 9(5) VALUE ZEROES.               
               10 WS-F300-5R               PIC 9(5) VALUE ZEROES.               
               10 WS-F300-ER               PIC 9(5) VALUE ZEROES.               
                                                                                
       01  WS-REM                    PIC 99.                                    
       01  WS-SNO                    PIC 9(6)   VALUE ZEROES.                   
       01  WS-REMARK                 PIC X(50)  VALUE SPACES.                   
       01  WS-YEAR-LEAP              PIC 9(4).                                  
       01  WS-LEASE-REM              PIC 9(2)   VALUE ZEROES.                   
       01  WS-EXP-DTE                PIC 9(4)   VALUE ZEROES.                   
       01  WS-TENURE                 PIC 9(4)   VALUE ZEROES.                   
       01  WS-MONTHS-OUT.                                                       
            05  WS-ACCPT-DTE             PIC X(8)   VALUE SPACES.               
            05  WS-ACCPT-DATE  REDEFINES WS-ACCPT-DTE.                          
                10  WS-ACCPT-YY           PIC 9(4).                             
                10  WS-ACCPT-MM           PIC 9(2).                             
                10  WS-ACCPT-DD           PIC 9(2).                             
            05  WS-TODAY-DTE              PIC X(8)   VALUE SPACES.              
            05  WS-TODAY-DATE REDEFINES WS-TODAY-DTE.                           
                10  WS-TODAY-YY             PIC 9(4).                           
                10  WS-TODAY-MM             PIC 9(2).                           
                10  WS-TODAY-DD             PIC 9(2).                           
            05  WS-1STLTR-DTE             PIC X(8)   VALUE SPACES.              
            05  WS-1STLTR-DATE REDEFINES WS-1STLTR-DTE.                         
                10  WS-1STLTR-YY            PIC 9(4).                           
                10  WS-1STLTR-MM            PIC 9(2).                           
                10  WS-1STLTR-DD            PIC 9(2).                           
                                                                                
       01  WS-DATE-DIFF.                                                        
           05  WS-DIFF-YY           PIC 9(4).                                   
           05  WS-DIFF-MM           PIC S9(2).                                  
           05  WS-DIFF-DD           PIC 9(2).                                   
                                                                                
       01  WS-VALID-MM                      PIC 99.                             
           88  WS-VALID-31-DAY-MONTH        VALUE 01 03 05 07 08 10 12.         
           88  WS-VALID-30-DAY-MONTH        VALUE 04 06 09 11.                  
           88  WS-VALID-FEB-MONTH           VALUE 02.                           
                                                                                
       01  L11B-ERR-HEAD-01.                                                    
           05  FILLER              PIC X(8)      VALUE 'BP13L11B'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE '        '.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                    'S Y S T E M   O F   '.                     
           05  FILLER              PIC X(19)     VALUE                          
                                    'C O M M I T M E N T'.                      
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  L11B-DATE           PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  L11B-PAGENO         PIC ZZZZ9     VALUE ZEROES.                  
                                                                                
       01  L11B-ERR-HEAD-02.                                                    
           05  FILLER              PIC X(39)     VALUE SPACES .                 
           05  FILLER              PIC X(93)     VALUE                          
               ' ERROR LIST OF OUTSTANDING CASES (SOCA - APPLICATION)'.         
                                                                                
       01  L11B-ERR-HEAD-03.                                                    
           05  FILLER              PIC X(28)                                    
                  VALUE '   S/NO     SCH-ACCNT NO    '.                         
           05  FILLER              PIC X(34)                                    
                  VALUE 'REGN NO       ERROR DESCRIPTION'.                      
           05  FILLER              PIC X(25)     VALUE SPACES.                  
                                                                                
       01  L11B-ERR-DETAILS.                                                    
           05  LL1A-SNO            PIC ZZZZZ9    VALUE ZEROES.                  
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  LL1A-NUM-SCHACCNT   PIC X(11).                                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  LL1A-REG-NO         PIC X(8).                                    
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  LL1A-REMARK         PIC X(50)     VALUE SPACES.                  
           05  FILLER              PIC X(3)      VALUE SPACES.                  
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
                                                                                
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           PERFORM 100-OPEN-FILES       THRU 100-EXIT.                          
           PERFORM 200-READ-BP13F80A    THRU 200-EXIT.                          
           PERFORM 300-PROCESS-KPI      THRU 300-EXIT                           
                   UNTIL WS-F80A-EOF = 'Y'.                                     
           PERFORM 400-WRITE-COUNT      THRU 400-EXIT.                          
                                                                                
           PERFORM 250-READ-BP13F300    THRU 250-EXIT.                          
           PERFORM 350-PROCESS-F300     THRU 350-EXIT                           
                   UNTIL WS-F300-EOF = 'Y'.                                     
           PERFORM 999-CLOSE-FILES      THRU 999-EXIT.                          
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN   INPUT  BM06K110                                               
                         BP13F80A                                               
                         BP13F300                                               
                  OUTPUT BP13FB40                                               
                         BP13L11B.                                              
                                                                                
           IF BM06K110-STATUS NOT = ZEROS   AND  97                             
               DISPLAY 'ERROR OPENING - BM06K110 : ' BM06K110-STATUS            
               MOVE BM06K110-STATUS TO RETURN-CODE                              
               PERFORM 999-CLOSE-FILES THRU 999-EXIT.                           
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-TODAY-DTE WS-DTE-CUR.               
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       200-READ-BP13F80A.                                                       
      *-------------------------------------------------------------            
           READ BP13F80A                                                        
                AT END                                                          
                MOVE HIGH-VALUES TO BP13F800-MASTER                             
                MOVE 'Y' TO  WS-F80A-EOF                                        
                GO TO 200-EXIT.                                                 
           ADD 1 TO WS-CNT-F80A-READ.                                           
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       250-READ-BP13F300.                                                       
      *-------------------------------------------------------------            
           READ BP13F300                                                        
                AT END                                                          
                MOVE HIGH-VALUES TO BP13F300-REC                                
                MOVE 'Y' TO  WS-F300-EOF                                        
                GO TO 250-EXIT.                                                 
           ADD 1 TO WS-CNT-F300-READ.                                           
                                                                                
       250-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       300-PROCESS-KPI.                                                         
      *-------------------------------------------------------------            
                                                                                
           MOVE 'N' TO WS-SKIP-REC                                              
                                                                                
           PERFORM 305-READ-BM06K110  THRU 305-EXIT                             
           PERFORM 500-VALIDATE-REC   THRU 500-EXIT                             
           IF WS-SKIP-REC = 'Y'                                                 
              ADD 1   TO  WS-SKIP-COUNT                                         
           ELSE                                                                 
              PERFORM 510-EVALUATE-DATA  THRU 510-EXIT                          
              PERFORM 520-SAVE-COUNT     THRU 520-EXIT                          
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-BP13F80A    THRU 200-EXIT.                          
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       305-READ-BM06K110.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                    TO K110-REC.                          
           MOVE F800-NUM-SCH-ACC          TO K110-KEY-FLD.                      
           READ BM06K110.                                                       
                                                                                
           EVALUATE BM06K110-STATUS                                             
              WHEN 00                                                           
              WHEN 02                                                           
                   CONTINUE                                                     
              WHEN 23                                                           
                   MOVE 'SCHEME ACCOUNT NOT FOUND ON FIS ' TO WS-REMARK         
                   ADD 1 TO WS-K110-NOTFND                                      
                   MOVE 'Y' TO WS-SKIP-REC                                      
                   PERFORM 800-ERROR-REPORT THRU 800-EXIT                       
              WHEN OTHER                                                        
                   MOVE BM06K110-STATUS    TO RETURN-CODE                       
                   DISPLAY 'ERROR READING BM06K110, STATUS = '                  
                                          BM06K110-STATUS                       
                   PERFORM 999-CLOSE-FILES THRU 999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       305-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       350-PROCESS-F300.                                                        
      *-------------------------------------------------------------            
                                                                                
           PERFORM 515-EVALUATE-F300 THRU 515-EXIT.                             
           PERFORM 550-SAVE-RMCNT    THRU 550-EXIT.                             
           PERFORM 250-READ-BP13F300 THRU 250-EXIT.                             
           IF WS-DATE-TEMP = SPACES OR LOW-VALUES OR ZEROES                     
              MOVE F300-DTE-SALE-AGRM TO WS-DATE-TEMP                           
           END-IF.                                                              
           IF WS-F300-EOF = 'Y'                                                 
              PERFORM 450-WRITE-SA-AO THRU 450-EXIT                             
           END-IF.                                                              
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       400-WRITE-COUNT.                                                         
      *-------------------------------------------------------------            
           MOVE SPACES TO BP13FB40-REC.                                         
           INITIALIZE BP13FB40-REC.                                             
           MOVE WS-TODAY-DTE       TO FB40-RUN-DATE                             
                                      FB40-DTE-UPDATE                           
           MOVE 'D'                TO FB40-NUM-RECTYPE                          
           MOVE ' '                TO FB40-NUM-LOAN-TAG                         
           MOVE WS-SA-KPI-D1MO     TO FB40-SA-COUNT(1)                          
           MOVE WS-SA-KPI-D2MO     TO FB40-2ROOM-COUNT(1)                       
           MOVE WS-SA-KPI-D3MO     TO FB40-3ROOM-COUNT(1)                       
           MOVE WS-SA-KPI-D4MO     TO FB40-4ROOM-COUNT(1)                       
           MOVE WS-SA-KPI-D5MO     TO FB40-5ROOM-COUNT(1)                       
           MOVE 'BP13C11B'         TO FB40-NUM-USERID                           
                                                                                
           WRITE BP13FB40-REC.                                                  
           ADD 1  TO WS-FB40-WRITE                                              
                                                                                
           MOVE SPACES TO BP13FB40-REC.                                         
           INITIALIZE BP13FB40-REC.                                             
           MOVE WS-TODAY-DTE       TO FB40-RUN-DATE                             
                                      FB40-DTE-UPDATE                           
           MOVE 'E'                TO FB40-NUM-RECTYPE                          
           MOVE ' '                TO FB40-NUM-LOAN-TAG                         
           MOVE WS-SA-KPI-E1MO     TO FB40-SA-COUNT(1)                          
           MOVE WS-SA-KPI-E2MO     TO FB40-2ROOM-COUNT(1)                       
           MOVE WS-SA-KPI-E3MO     TO FB40-3ROOM-COUNT(1)                       
           MOVE WS-SA-KPI-E4MO     TO FB40-4ROOM-COUNT(1)                       
           MOVE WS-SA-KPI-E5MO     TO FB40-5ROOM-COUNT(1)                       
           MOVE 'BP13C11B'         TO FB40-NUM-USERID                           
                                                                                
           WRITE BP13FB40-REC.                                                  
           ADD 1  TO WS-FB40-WRITE.                                             
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       450-WRITE-SA-AO.                                                         
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES TO BP13FB40-REC.                                         
           INITIALIZE BP13FB40-REC.                                             
           MOVE WS-DATE-TEMP        TO FB40-RUN-DATE                            
                                      FB40-DTE-UPDATE                           
           MOVE 'A'                TO FB40-NUM-RECTYPE                          
           MOVE ' '                TO FB40-NUM-LOAN-TAG                         
           MOVE WS-F300-SA         TO FB40-SA-COUNT(1)                          
           MOVE WS-F300-2R         TO FB40-2ROOM-COUNT(1)                       
           MOVE WS-F300-3R         TO FB40-3ROOM-COUNT(1)                       
           MOVE WS-F300-4R         TO FB40-4ROOM-COUNT(1)                       
           MOVE WS-F300-5R         TO FB40-5ROOM-COUNT(1)                       
           MOVE WS-F300-ER         TO FB40-EROOM-COUNT(1)                       
           MOVE 'BP13C11B'         TO FB40-NUM-USERID                           
                                                                                
           WRITE BP13FB40-REC.                                                  
           ADD 1  TO WS-FB40-WRITE                                              
                                                                                
               .                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       500-VALIDATE-REC.                                                        
      *-------------------------------------------------------------            
                                                                                
           IF F800-NUM-ALLO-CAT = 'BTO'                                         
              MOVE 'D'  TO WS-REC-TYPE                                          
           ELSE                                                                 
              MOVE 'E'  TO WS-REC-TYPE                                          
           END-IF                                                               
                                                                                
           IF (K110-DTE-ACCEPTANCE = SPACES OR LOW-VALUES OR ZEROES) AND        
              (WS-SKIP-REC NOT = 'Y')                                           
              STRING 'E777: INVALID ACCPT DTE = '                               
                      K110-DTE-ACCEPTANCE DELIMITED BY SIZE                     
                      INTO WS-REMARK                                            
              MOVE 'Y' TO WS-SKIP-REC                                           
              PERFORM 800-ERROR-REPORT THRU 800-EXIT                            
           ELSE                                                                 
              MOVE  K110-DTE-ACCEPTANCE   TO  WS-ACCPT-DTE                      
           END-IF.                                                              
                                                                                
           IF F800-DTE-1ST-TP-LETTER = SPACES OR LOW-VALUES OR ZEROES           
              STRING 'INVALID 1ST APPT LETTER DTE : '                           
                      F800-DTE-1ST-TP-LETTER DELIMITED BY SIZE                  
                      INTO WS-REMARK                                            
              PERFORM 800-ERROR-REPORT THRU 800-EXIT                            
              MOVE 'Y' TO WS-SKIP-REC                                           
           ELSE                                                                 
              MOVE F800-DTE-1ST-TP-LETTER TO WS-1STLTR-DTE                      
           END-IF.                                                              
                                                                                
           IF F800-NUM-SCH-ACC = SPACES OR LOW-VALUES OR ZEROES                 
              MOVE 'NO SCHEME ACCOUNT ' TO WS-REMARK                            
              MOVE 'Y' TO WS-SKIP-REC                                           
              PERFORM 800-ERROR-REPORT THRU 800-EXIT                            
           END-IF.                                                              
                                                                                
           IF F800-NUM-STATUS = 'C'                                             
              MOVE 'REGN IS CANCELLED ' TO WS-REMARK                            
              MOVE 'Y' TO WS-SKIP-REC                                           
              PERFORM 800-ERROR-REPORT THRU 800-EXIT                            
           END-IF.                                                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       510-EVALUATE-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
      ***COMPUTE MONTHS FROM ACCEPT DATE TO 1ST LETTER DATE                     
           MOVE WS-1STLTR-MM               TO WS-VALID-MM.                      
           COMPUTE WS-DIFF-YY = WS-1STLTR-YY - WS-ACCPT-YY                      
           COMPUTE WS-DIFF-MM = WS-1STLTR-MM - WS-ACCPT-MM                      
           COMPUTE WS-DIFF-DD = WS-1STLTR-DD - WS-ACCPT-DD                      
                                                                                
           IF (WS-1STLTR-MM = 02) AND                                           
              (WS-1STLTR-DD > 28)                                               
              DIVIDE 4 INTO WS-1STLTR-YY GIVING WS-YEAR-LEAP                    
                                        REMAINDER WS-REM                        
              IF WS-REM = 0                                                     
                 MOVE 29 TO WS-1STLTR-DD                                        
              ELSE                                                              
                 MOVE 28 TO WS-1STLTR-DD                                        
              END-IF                                                            
                                                                                
              IF WS-1STLTR-DD < WS-ACCPT-DD                                     
                 COMPUTE WS-DIFF-MM = WS-DIFF-MM - 1                            
              END-IF                                                            
           ELSE                                                                 
              IF WS-1STLTR-DD < WS-ACCPT-DD                                     
                 IF WS-VALID-30-DAY-MONTH AND                                   
                    WS-1STLTR-DD = 30 AND                                       
                    WS-ACCPT-DD = 31                                            
                    CONTINUE                                                    
                 ELSE                                                           
                    COMPUTE WS-DIFF-MM = WS-DIFF-MM - 1                         
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-DIFF-MM < 0                                                    
              COMPUTE WS-DIFF-MM = WS-DIFF-MM + 12                              
              COMPUTE WS-DIFF-YY = WS-DIFF-YY - 1                               
           END-IF.                                                              
                                                                                
           IF WS-DIFF-YY > 1                                                    
              COMPUTE WS-DIFF-MM =((12 * WS-DIFF-YY) + WS-DIFF-MM)              
           END-IF.                                                              
                                                                                
           IF WS-DIFF-MM > 4                                                    
              MOVE 05  TO  WS-MONTH-OUT                                         
           ELSE                                                                 
              MOVE WS-DIFF-MM  TO  WS-MONTH-OUT                                 
           END-IF                                                               
                                                                                
               .                                                                
       510-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       515-EVALUATE-F300.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF F300-NUM-FLAT-TYPE = '1A' OR '2A' OR '1F'                         
              MOVE 'SA'    TO   WS-ROOM-TYPE                                    
           ELSE                                                                 
              MOVE F300-NUM-FLAT-TYPE  TO WS-ROOM-TYPE                          
           END-IF.                                                              
                                                                                
           IF  F300-NUM-LOAN-TAG  = SPACES OR LOW-VALUES                        
               MOVE 'N'  TO WS-LOAN-TAG                                         
           ELSE                                                                 
              MOVE F300-NUM-LOAN-TAG  TO WS-LOAN-TAG                            
           END-IF.                                                              
                                                                                
           MOVE F300-CUR-REG       TO WS-REGN                                   
                                                                                
               .                                                                
       515-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       520-SAVE-COUNT.                                                          
      *-------------------------------------------------------------            
                                                                                
           IF WS-REC-TYPE  = 'D'                                                
              EVALUATE WS-MONTH-OUT                                             
                 WHEN 00                                                        
                   ADD 1  TO  WS-SKIP-COUNT                                     
                                                                                
                 WHEN 01                                                        
                   ADD  1  TO  WS-SA-KPI-D1MO                                   
                                                                                
                 WHEN 02                                                        
                   ADD  1  TO  WS-SA-KPI-D2MO                                   
                                                                                
                 WHEN 03                                                        
                   ADD  1  TO  WS-SA-KPI-D3MO                                   
                                                                                
                 WHEN 04                                                        
                   ADD  1  TO  WS-SA-KPI-D4MO                                   
                                                                                
                 WHEN 05                                                        
                   ADD  1  TO  WS-SA-KPI-D5MO                                   
                                                                                
              END-EVALUATE                                                      
           ELSE                                                                 
              EVALUATE WS-MONTH-OUT                                             
                 WHEN 00                                                        
                   ADD 1  TO  WS-SKIP-COUNT                                     
                                                                                
                 WHEN 01                                                        
                   ADD  1  TO  WS-SA-KPI-E1MO                                   
                                                                                
                 WHEN 02                                                        
                   ADD  1  TO  WS-SA-KPI-E2MO                                   
                                                                                
                 WHEN 03                                                        
                   ADD  1  TO  WS-SA-KPI-E3MO                                   
                                                                                
                 WHEN 04                                                        
                   ADD  1  TO  WS-SA-KPI-E4MO                                   
                                                                                
                 WHEN 05                                                        
                   ADD  1  TO  WS-SA-KPI-E5MO                                   
                                                                                
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
       520-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       550-SAVE-RMCNT.                                                          
      *-------------------------------------------------------------            
                                                                                
           EVALUATE TRUE                                                        
              WHEN WS-ROOM-TYPE = 'SA'                                          
                   ADD  1      TO   WS-F300-SA                                  
                                                                                
              WHEN WS-ROOM-TYPE(1:1)= '2'                                       
                   ADD  1      TO   WS-F300-2R                                  
                   MOVE '2 ' TO WS-ROOM-TYPE                                    
              WHEN WS-ROOM-TYPE(1:1)= '3'                                       
                   ADD  1      TO   WS-F300-3R                                  
                   MOVE '3 ' TO WS-ROOM-TYPE                                    
              WHEN WS-ROOM-TYPE(1:1)= '4'                                       
                   ADD  1      TO   WS-F300-4R                                  
                   MOVE '4 ' TO WS-ROOM-TYPE                                    
              WHEN WS-ROOM-TYPE(1:1)= '5'                                       
                   ADD  1      TO   WS-F300-5R                                  
                   MOVE '5 ' TO WS-ROOM-TYPE                                    
              WHEN WS-ROOM-TYPE(1:1)= 'E'                                       
                   ADD  1      TO   WS-F300-ER                                  
                   MOVE 'E ' TO WS-ROOM-TYPE                                    
              WHEN OTHER                                                        
                   MOVE 12                 TO RETURN-CODE                       
                   DISPLAY 'ROOM TYPE ERROR FOR: ' F800-NUM-REGN                
                   DISPLAY 'ROOM TYPE = ' WS-ROOM-TYPE                          
                   PERFORM 999-CLOSE-FILES THRU 999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       550-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       800-ERROR-REPORT.                                                        
      *-------------------------------------------------------------            
           IF WS-LINE-CNT > 45                                                  
              MOVE 'N' TO WS-ERROR-REPORT                                       
              MOVE ZEROES TO WS-LINE-CNT                                        
           END-IF.                                                              
                                                                                
           IF WS-ERROR-REPORT = 'Y'                                             
              CONTINUE                                                          
           ELSE                                                                 
              PERFORM 810-ERROR-HEADING THRU 810-EXIT                           
           END-IF.                                                              
                                                                                
           ADD  1   TO   WS-SNO WS-LINE-CNT                                     
           MOVE WS-SNO            TO LL1A-SNO                                   
           MOVE F800-NUM-SCH-ACC  TO LL1A-NUM-SCHACCNT                          
           MOVE F800-NUM-REGN     TO LL1A-REG-NO                                
           MOVE WS-REMARK         TO LL1A-REMARK                                
           WRITE PRINT-REC FROM L11B-ERR-DETAILS.                               
                                                                                
       800-EXIT.                                                                
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       810-ERROR-HEADING.                                                       
      *-------------------------------------------------------------            
                                                                                
            ADD 1               TO WS-PAGE-CNT.                                 
            MOVE WS-PAGE-CNT    TO L11B-PAGENO.                                 
            STRING WS-DD '/' WS-MM '/' WS-CC WS-YY                              
                   DELIMITED BY SIZE INTO L11B-DATE.                            
            MOVE SPACES         TO PRINT-REC                                    
            WRITE PRINT-REC FROM L11B-ERR-HEAD-01 AFTER PAGE.                   
            WRITE PRINT-REC FROM L11B-ERR-HEAD-02 AFTER 2.                      
            WRITE PRINT-REC FROM L11B-ERR-HEAD-03 AFTER 2.                      
            INSPECT PRINT-REC REPLACING ALL SPACES BY LOW-VALUES.               
            INSPECT PRINT-REC REPLACING CHARACTERS BY '-'.                      
            INSPECT PRINT-REC REPLACING ALL LOW-VALUES BY SPACES.               
            WRITE PRINT-REC .                                                   
            MOVE SPACES TO PRINT-REC.                                           
            WRITE PRINT-REC AFTER 2.                                            
            MOVE 'Y'  TO WS-ERROR-REPORT.                                       
                                                                                
       810-EXIT.                                                                
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       999-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
           DISPLAY '******  BP13C11A *************'.                            
           DISPLAY 'NO OF F800 READ      (F800) : ' WS-CNT-F80A-READ.           
           DISPLAY 'NO OF F300 READ      (F300) : ' WS-CNT-F300-READ.           
           DISPLAY 'NO OF K110 NTFOUND(SKIP)(K110) : ' WS-K110-NOTFND.          
           DISPLAY 'NO OF TOTAL REC SKIPPED     : ' WS-SKIP-COUNT.              
           DISPLAY 'NO OF FB40 WRITE     (FB40) : ' WS-FB40-WRITE.              
                                                                                
           CLOSE BM06K110                                                       
                 BP13F80A                                                       
                 BP13F300                                                       
                 BP13FB40                                                       
                 BP13L11B.                                                      
                                                                                
           IF BM06K110-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BM06K110-STATUS ' BM06K110-STATUS          
            MOVE BM06K110-STATUS TO RETURN-CODE.                                
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
            EXIT.                                                               
