      *------------------------------------------------------------*            
       IDENTIFICATION DIVISION.                                                 
      *------------------------------------------------------------*            
       PROGRAM-ID. BP13C432.                                                    
      *AUTHOR    .SK.                                                           
      *DATE-WRITTEN.  20021212.                                                 
      *-------------------------------------------------------------*           
      *SYSTEM NAME       :  SYSTEM OF COMMITMENT.                   *           
      *SYSTEM ID         :  BP13.                                   *           
      *OBJECTIVE         :  PRINT REQUEST OF CANCELLATION OF SALES  *           
      *                     APPLICATIONS                            *           
      *-------------------------------------------------------------*           
      *DATASETS USED                                                *           
      *   1. BP13F800                                               *           
      *   2. P13L432A -  PRINT FILE                                 *           
      *   3. P13L432B -  PRINT FILE                                 *           
      *-------------------------------------------------------------*           
      * CHG-NO   BY    ON    - DESCRIPTION                          *           
      * ---------------------------------                           *           
      * BP132313 CKK 230303  TO PRINT REPORT ON CANCELLATION        *           
      * BP132313 CKK 110403  DON'T PRINT REPORT IF BK-FEE-PAID IS   *           
      *                      ZEROES                                 *           
      * BP132313 CKK 010703  PRINT REPORT IF BK-FEE-PAID = ZEROES   *           
      * BP132408 SK  130903  TO PRINT OIC CODE                      *           
      *------------------------------------------------------------*            
       ENVIRONMENT DIVISION.                                                    
      *------------------------------------------------------------*            
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
           SELECT P13L432A ASSIGN TO P13L432A.                                  
           SELECT P13L432B ASSIGN TO P13L432B.                                  
                                                                                
           SELECT BP13K022 ASSIGN TO BP13K022                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K022-KEY-FLD                               
                  ALTERNATE KEY   IS K022-NUM-REGN                              
                  FILE STATUS     IS WS-K022-STATUS.                            
                                                                                
      *------------------------------------------------------------*            
       DATA DIVISION.                                                           
      *------------------------------------------------------------*            
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F800                                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           BLOCK CONTAINS 0 RECORDS                                             
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD  P13L432A                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01 P13L432A-REC     PIC X(132).                                          
                                                                                
       FD  P13L432B                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01 P13L432B-REC     PIC X(132).                                          
                                                                                
      *------------------------------------------------------------*            
       WORKING-STORAGE SECTION.                                                 
      *------------------------------------------------------------*            
       01  PRINT-HEAD-A.                                                        
           05 FILLER        PIC X(10) VALUE 'P13L432A'.                         
           05 FILLER        PIC X(02) VALUE SPACES.                             
           05 FILLER        PIC X(10) VALUE 'HDBCAT 3'.                         
           05 FILLER        PIC X(28) VALUE SPACES.                             
           05 FILLER        PIC X(30) VALUE 'SYSTEM OF COMMITMENT'.             
           05 FILLER        PIC X(10) VALUE SPACES.                             
           05 FILLER        PIC X(07) VALUE 'DATE : '.                          
           05 WS-DATE-P-A.                                                      
              10 WS-DD-P-A   PIC X(2).                                          
              10 FILLER      PIC X VALUE '/'.                                   
              10 WS-MM-P-A   PIC X(2).                                          
              10 FILLER      PIC X VALUE '/'.                                   
              10 WS-CCYY-P-A PIC X(4).                                          
           05 FILLER         PIC X(02) VALUE SPACES.                            
           05 FILLER         PIC X(07) VALUE 'PAGE :'.                          
           05 WS-PAGE-P-A    PIC ZZZZ9.                                         
                                                                                
       01  PRINT-HEAD-B.                                                        
           05 FILLER         PIC X(10) VALUE 'P13L432B'.                        
           05 FILLER         PIC X(02) VALUE SPACES.                            
           05 FILLER         PIC X(10) VALUE 'HDBCAT 3'.                        
           05 FILLER         PIC X(28) VALUE SPACES.                            
           05 FILLER         PIC X(30) VALUE 'SYSTEM OF COMMITMENT'.            
           05 FILLER         PIC X(10) VALUE SPACES.                            
           05 FILLER         PIC X(07) VALUE 'DATE : '.                         
           05 WS-DATE-P-B.                                                      
              10 WS-DD-P-B   PIC X(2).                                          
              10 FILLER      PIC X VALUE '/'.                                   
              10 WS-MM-P-B   PIC X(2).                                          
              10 FILLER      PIC X VALUE '/'.                                   
              10 WS-CCYY-P-B PIC X(4).                                          
           05 FILLER         PIC X(02) VALUE SPACES.                            
           05 FILLER         PIC X(07) VALUE 'PAGE :'.                          
           05 WS-PAGE-P-B    PIC ZZZZ9.                                         
                                                                                
       01  PRINT-HEAD1.                                                         
           05 FILLER         PIC X(20) VALUE SPACES.                            
           05 FILLER         PIC X(80) VALUE 'REQUEST ON CANCELLATION OF        
      -    ' SALES APPLICATION WITH FILE RECEIVED DATE'.                        
                                                                                
       01  PRINT-HEAD1B.                                                        
           05 FILLER         PIC X(20) VALUE SPACES.                            
           05 FILLER         PIC X(80) VALUE 'REQUEST ON CANCELLATION OF        
      -    ' SALES APPLICATION WITHOUT FILE RECEIVED DATE'.                     
                                                                                
       01  PRINT-HEAD2.                                                         
           05 FILLER        PIC X(10)  VALUE '   1'.                            
           05 FILLER        PIC X(05)  VALUE ' 2'.                              
           05 FILLER        PIC X(15)  VALUE '   3'.                            
           05 FILLER        PIC X(15)  VALUE '   4'.                            
           05 FILLER        PIC X(15)  VALUE '   5'.                            
           05 FILLER        PIC X(30)  VALUE '      6'.                         
           05 FILLER        PIC X(10)  VALUE ' 7'.                              
                                                                                
       01  PRINT-HEAD3.                                                         
           05 FILLER        PIC X(10)  VALUE 'REGN NO'.                         
           05 FILLER        PIC X(05)  VALUE 'ALLN'.                            
           05 FILLER        PIC X(15)  VALUE 'DTE REC'.                         
           05 FILLER        PIC X(14)  VALUE 'DTE '.                            
           05 FILLER        PIC X(15)  VALUE 'DTE OF REPLY'.                    
           05 FILLER        PIC X(30)  VALUE 'NO. DAYS FROM'.                   
           05 FILLER        PIC X(10)  VALUE ' OIC'.                            
                                                                                
       01  PRINT-HEAD4.                                                         
           05 FILLER         PIC X(10) VALUE SPACES.                            
           05 FILLER         PIC X(05) VALUE 'MDE'.                             
           05 FILLER         PIC X(15) VALUE SPACES.                            
           05 FILLER         PIC X(14) VALUE 'CANCELLED'.                       
           05 FILLER         PIC X(15) VALUE SPACES.                            
           05 FILLER         PIC X(30) VALUE '(5) TO (3)'.                      
                                                                                
       01  PRINT-LINE.                                                          
           05 WS-LINE          PIC X(132) VALUE ALL '-'.                        
                                                                                
       01  WS-BLANK.                                                            
           05 WS-BLANK-LINE    PIC X(132) VALUE SPACE.                          
                                                                                
       01  PRINT-DETAIL.                                                        
           05 WS-REGN-P        PIC X(8).                                        
           05 FILLER           PIC X(2) VALUE SPACES.                           
           05 WS-ALL-MDE-P     PIC X(3).                                        
           05 FILLER           PIC X(2) VALUE SPACES.                           
           05 WS-DTE-REQ-P.                                                     
              10 WS-RQDD-P     PIC X(2).                                        
              10 WS-RQ-SEP1    PIC X VALUE SPACES.                              
              10 WS-RQMM-P     PIC X(2).                                        
              10 WS-RQ-SEP2    PIC X VALUE SPACES.                              
              10 WS-RQCCYY-P   PIC X(4).                                        
           05 FILLER           PIC X(5) VALUE SPACES.                           
           05 WS-DTE-CAN-P.                                                     
              10 WS-DCDD-P     PIC X(2).                                        
              10 WS-DC-SEP1    PIC X VALUE SPACES.                              
              10 WS-DCMM-P     PIC X(2).                                        
              10 WS-DC-SEP2    PIC X VALUE SPACES.                              
              10 WS-DCCCYY-P   PIC X(4).                                        
           05 FILLER           PIC X(5) VALUE SPACES.                           
           05 WS-DTE-LETTER-P.                                                  
              10 WS-DLDD-P     PIC X(2).                                        
              10 WS-DL-SEP1    PIC X VALUE SPACES.                              
              10 WS-DLMM-P     PIC X(2).                                        
              10 WS-DL-SEP2    PIC X VALUE SPACES.                              
              10 WS-DLCCYY-P   PIC X(4).                                        
           05 FILLER           PIC X(5) VALUE SPACES.                           
           05 WS-DAYS-P        PIC Z(4).                                        
           05 FILLER           PIC X(26) VALUE SPACES.                          
           05 WS-OIC-CODE      PIC X(5).                                        
                                                                                
       01 WS-BP13F800-EOF      PIC X(1) VALUE 'N'.                              
       01 WS-NO-OF-DAYS        PIC S9(4) VALUE 0.                               
       01 WS-DATE-VAL1         PIC 9(8) VALUE 0.                                
       01 WS-DATE-VAL2         PIC 9(8) VALUE 0.                                
       01 WS-DATE-VAL3         PIC 9(8) VALUE 0.                                
       01 WS-DATE-VAL4         PIC 9(8) VALUE 0.                                
       01 WS-PAGE-NO1          PIC 9(5) VALUE 0.                                
       01 WS-PAGE-NO2          PIC 9(5) VALUE 0.                                
                                                                                
       01 WS-LINE-COUNT1       PIC 9(2) VALUE 0.                                
       01 WS-LINE-COUNT2       PIC 9(2) VALUE 0.                                
                                                                                
       01 WS-K022-FOUND        PIC X(1)  VALUE 'N'.                             
       01 WS-K022-EOF          PIC X(1)  VALUE 'N'.                             
       01 WS-K022-STATUS       PIC 9(2)  VALUE 0.                               
                                                                                
       01  WS-NDATE            PIC 9(08) VALUE ZEROES.                          
       01  WS-XDATE REDEFINES  WS-NDATE                                         
                               PIC X(08).                                       
                                                                                
       01 WS-CDATE             PIC 9(08) VALUE ZEROES.                          
       01 WS-SYS-DATE REDEFINES  WS-CDATE.                                      
          05 WS-SYS-CCYY       PIC X(4).                                        
          05 WS-SYS-MM         PIC X(2).                                        
          05 WS-SYS-DD         PIC X(2).                                        
                                                                                
       01  WS-CUR-DATE         PIC 9(6).                                        
       01  WS-PRINT-DATE.                                                       
           05 WS-DD            PIC X(2).                                        
           05 FILLER           PIC X(1) VALUE '/'.                              
           05 WS-MM            PIC X(2).                                        
           05 FILLER           PIC X(1) VALUE '/'.                              
           05 WS-YY            PIC X(2).                                        
                                                                                
      *--DISPLAY VARIABLES                                                      
       01 WS-READ-F800         PIC 9(08) VALUE 0.                               
       01 WS-PRINT1            PIC 9(08) VALUE 0.                               
       01 WS-PRINT2            PIC 9(08) VALUE 0.                               
                                                                                
      *------------------------------------------------------------*            
       PROCEDURE DIVISION.                                                      
      *------------------------------------------------------------*            
                                                                                
       0000-CONTROL.                                                            
           MOVE FUNCTION CURRENT-DATE TO  WS-SYS-DATE.                          
           COMPUTE WS-DATE-VAL3 = FUNCTION INTEGER-OF-DATE(WS-CDATE).           
                                                                                
           MOVE SPACES TO P13L432A-REC P13L432B-REC.                            
           INITIALIZE     P13L432A-REC P13L432B-REC.                            
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 1500-PRINT-HEADINGS THRU 1500-EXIT.                          
           PERFORM 1600-PRINT-HEADINGS THRU 1600-EXIT.                          
           PERFORM 1100-READ-BP13F800  THRU 1100-EXIT                           
                   UNTIL WS-BP13F800-EOF = 'Y'.                                 
           PERFORM 8000-DISPLAY        THRU 8000-EXIT.                          
           PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       1000-OPEN-FILES.                                                         
      *------------------------------------------------------------*            
                                                                                
           OPEN INPUT BP13F800 BP13K022                                         
                OUTPUT P13L432A P13L432B.                                       
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K022 OPEN ERROR, STATUS IS ' WS-K022-STATUS          
              PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       1100-READ-BP13F800.                                                      
      *------------------------------------------------------------*            
                                                                                
           READ BP13F800                                                        
                AT END MOVE 'Y' TO WS-BP13F800-EOF                              
                GO TO 1100-EXIT.                                                
           PERFORM 2000-MAIN-ROUTINE THRU 2000-EXIT.                            
           ADD 1 TO WS-READ-F800.                                               
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       1500-PRINT-HEADINGS.                                                     
      *------------------------------------------------------------*            
           ADD 1 TO WS-PAGE-NO1.                                                
                                                                                
           MOVE WS-PAGE-NO1 TO WS-PAGE-P-A.                                     
           MOVE WS-SYS-DD   TO WS-DD-P-A.                                       
           MOVE WS-SYS-MM   TO WS-MM-P-A.                                       
           MOVE WS-SYS-CCYY TO WS-CCYY-P-A.                                     
                                                                                
           WRITE P13L432A-REC FROM PRINT-HEAD-A AFTER ADVANCING 1 LINE.         
           WRITE P13L432A-REC FROM PRINT-HEAD1  AFTER ADVANCING 1 LINE.         
           WRITE P13L432A-REC FROM PRINT-HEAD2  AFTER ADVANCING 2 LINE.         
           WRITE P13L432A-REC FROM PRINT-HEAD3  AFTER ADVANCING 1 LINE.         
           WRITE P13L432A-REC FROM PRINT-HEAD4  AFTER ADVANCING 1 LINE.         
           WRITE P13L432A-REC FROM PRINT-LINE.                                  
                                                                                
           MOVE 8 TO WS-LINE-COUNT1.                                            
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       1600-PRINT-HEADINGS.                                                     
      *------------------------------------------------------------*            
           ADD 1 TO WS-PAGE-NO2.                                                
           MOVE WS-PAGE-NO2 TO WS-PAGE-P-B.                                     
           MOVE WS-SYS-DD   TO WS-DD-P-B.                                       
           MOVE WS-SYS-MM   TO WS-MM-P-B.                                       
           MOVE WS-SYS-CCYY TO WS-CCYY-P-B.                                     
                                                                                
           WRITE P13L432B-REC FROM PRINT-HEAD-B AFTER ADVANCING 1 LINE.         
           WRITE P13L432B-REC FROM PRINT-HEAD1B AFTER ADVANCING 1 LINE.         
           WRITE P13L432B-REC FROM PRINT-HEAD2  AFTER ADVANCING 2 LINE.         
           WRITE P13L432B-REC FROM PRINT-HEAD3  AFTER ADVANCING 1 LINE.         
           WRITE P13L432B-REC FROM PRINT-HEAD4  AFTER ADVANCING 1 LINE.         
           WRITE P13L432B-REC FROM PRINT-LINE.                                  
                                                                                
           MOVE 8 TO WS-LINE-COUNT2.                                            
                                                                                
       1600-EXIT.                                                               
           EXIT.                                                                
      *------------------------------------------------------------*            
       2000-MAIN-ROUTINE.                                                       
      *------------------------------------------------------------*            
           MOVE ZEROS TO WS-DATE-VAL1 WS-DATE-VAL2.                             
                                                                                
           IF F800-DTE-LETTER-RECV-CAN IS NUMERIC AND                           
              F800-DTE-LETTER-RECV-CAN NOT = ZEROES                             
             MOVE F800-DTE-LETTER-RECV-CAN  TO WS-XDATE                         
             COMPUTE WS-DATE-VAL1 = FUNCTION INTEGER-OF-DATE(WS-NDATE)          
           ELSE                                                                 
              IF F800-DTE-LETTER-RECV-CAN = SPACES OR                           
                 F800-DTE-LETTER-RECV-CAN = ZEROES OR                           
                 F800-DTE-LETTER-RECV-CAN = '   0 0 0'                          
                 MOVE WS-SYS-DATE            TO WS-XDATE                        
             COMPUTE WS-DATE-VAL1 = FUNCTION INTEGER-OF-DATE(WS-NDATE)          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F800-DTE-REQ-CANCEL IS NUMERIC AND                                
              F800-DTE-REQ-CANCEL NOT = ZEROES                                  
              MOVE F800-DTE-REQ-CANCEL  TO WS-XDATE                             
              COMPUTE WS-DATE-VAL2 = FUNCTION INTEGER-OF-DATE(WS-NDATE)         
           END-IF.                                                              
                                                                                
           IF WS-DATE-VAL2 NOT = ZEROES                                         
              COMPUTE WS-NO-OF-DAYS = WS-DATE-VAL1 - WS-DATE-VAL2               
           ELSE                                                                 
              MOVE ZEROES TO WS-NO-OF-DAYS.                                     
                                                                                
      *    IF F800-AMT-BK-PAID = ZEROES                                         
      *       MOVE ZEROES TO WS-NO-OF-DAYS.                                     
                                                                                
           IF WS-NO-OF-DAYS > +14                                               
              MOVE F800-NUM-REGN                 TO WS-REGN-P                   
              MOVE F800-NUM-ALLO-CAT             TO WS-ALL-MDE-P                
                                                                                
              IF F800-DTE-REQ-CANCEL IS NUMERIC                                 
                 MOVE F800-DTE-REQ-CANCEL(1:4)   TO WS-RQCCYY-P                 
                 MOVE '/'                        TO WS-RQ-SEP1                  
                 MOVE F800-DTE-REQ-CANCEL(5:2)   TO WS-RQMM-P                   
                 MOVE '/'                        TO WS-RQ-SEP2                  
                 MOVE F800-DTE-REQ-CANCEL(7:2)   TO WS-RQDD-P                   
              ELSE                                                              
                 MOVE SPACES                     TO WS-DTE-REQ-P                
              END-IF                                                            
                                                                                
              IF F800-DTE-CANCEL IS NUMERIC                                     
                 MOVE F800-DTE-CANCEL(1:4)       TO WS-DCCCYY-P                 
                 MOVE '/'                        TO WS-DC-SEP1                  
                 MOVE F800-DTE-CANCEL(5:2)       TO WS-DCMM-P                   
                 MOVE '/'                        TO WS-DC-SEP2                  
                 MOVE F800-DTE-CANCEL(7:2)       TO WS-DCDD-P                   
              ELSE                                                              
                 MOVE SPACES                     TO WS-DTE-CAN-P                
              END-IF                                                            
                                                                                
              MOVE WS-NO-OF-DAYS                 TO WS-DAYS-P                   
                                                                                
              IF F800-DTE-LETTER-RECV-CAN IS NUMERIC                            
                 MOVE F800-DTE-LETTER-RECV-CAN(1:4) TO WS-DLCCYY-P              
                 MOVE '/'                           TO WS-DL-SEP1               
                 MOVE F800-DTE-LETTER-RECV-CAN(5:2) TO WS-DLMM-P                
                 MOVE '/'                           TO WS-DL-SEP2               
                 MOVE F800-DTE-LETTER-RECV-CAN(7:2) TO WS-DLDD-P                
              ELSE                                                              
                 MOVE SPACES                        TO WS-DTE-LETTER-P          
              END-IF                                                            
                                                                                
              PERFORM 4000-READ-K022                THRU 4000-EXIT              
                                                                                
              IF WS-K022-FOUND = 'Y'                                            
                 MOVE K022-NUM-OIC    TO WS-OIC-CODE                            
              ELSE                                                              
                 MOVE SPACES          TO WS-OIC-CODE                            
              END-IF                                                            
                                                                                
              IF F800-DTE-REQ-CANCEL NOT = ZEROES AND SPACES AND                
                                           LOW-VALUES                           
                IF F800-DTE-FILE-RECEIVED IS NUMERIC                            
                   IF (F800-DTE-LETTER-RECV-CAN NOT = ZEROS AND SPACES          
                                                      AND LOW-VALUES            
                                                      AND '   0 0 0')           
                      MOVE F800-DTE-LETTER-RECV-CAN TO WS-XDATE                 
                                                                                
                      COMPUTE WS-DATE-VAL4 =                                    
                                  FUNCTION INTEGER-OF-DATE(WS-NDATE)            
                   ELSE                                                         
                      MOVE 0 TO WS-DATE-VAL4                                    
                   END-IF                                                       
                                                                                
                   COMPUTE WS-NO-OF-DAYS = WS-DATE-VAL3 - WS-DATE-VAL4          
                                                                                
                   IF WS-NO-OF-DAYS <= 7                                        
                      PERFORM 3000-PRINT            THRU 3000-EXIT              
                   ELSE                                                         
                      IF (F800-DTE-LETTER-RECV-CAN = ZEROS OR SPACES            
                                                      OR LOW-VALUES             
                                                      OR '   0 0 0')            
                         PERFORM 3000-PRINT         THRU 3000-EXIT              
                      END-IF                                                    
                   END-IF                                                       
                ELSE                                                            
                   PERFORM 3500-PRINT               THRU 3500-EXIT              
                END-IF                                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3000-PRINT.                                                              
      *------------------------------------------------------------*            
           IF WS-LINE-COUNT1 > 56                                               
              WRITE P13L432A-REC FROM PRINT-LINE AFTER                          
                                      ADVANCING 1 LINE                          
              WRITE P13L432A-REC FROM WS-BLANK AFTER ADVANCING PAGE             
              PERFORM 1500-PRINT-HEADINGS THRU 1500-EXIT                        
           END-IF.                                                              
                                                                                
           WRITE P13L432A-REC FROM PRINT-DETAIL AFTER                           
                                   ADVANCING 1 LINE.                            
           ADD 1 TO WS-LINE-COUNT1.                                             
           ADD 1 TO WS-PRINT1.                                                  
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       3500-PRINT.                                                              
      *------------------------------------------------------------*            
           IF WS-LINE-COUNT2 > 56                                               
              WRITE P13L432B-REC FROM PRINT-LINE AFTER                          
                                      ADVANCING 1 LINE                          
              WRITE P13L432B-REC FROM WS-BLANK AFTER ADVANCING PAGE             
              PERFORM 1600-PRINT-HEADINGS THRU 1600-EXIT                        
           END-IF.                                                              
           WRITE P13L432B-REC FROM PRINT-DETAIL AFTER                           
                                   ADVANCING 1 LINE.                            
           ADD 1 TO WS-LINE-COUNT2.                                             
           ADD 1 TO WS-PRINT2.                                                  
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       4000-READ-K022.                                                          
      *------------------------------------------------------------*            
                                                                                
           MOVE LOW-VALUES      TO K022-NUM-REGN.                               
           MOVE F800-NUM-REGN   TO K022-NUM-REGN.                               
                                                                                
           START BP13K022 KEY >= K022-NUM-REGN.                                 
                                                                                
           IF WS-K022-STATUS = 00 OR 02                                         
              READ BP13K022 NEXT RECORD                                         
              IF WS-K022-STATUS = 00 OR 02                                      
                 IF K022-NUM-REGN = F800-NUM-REGN                               
                    MOVE 'Y' TO WS-K022-FOUND                                   
                 ELSE                                                           
                    MOVE 'N' TO WS-K022-FOUND                                   
                 END-IF                                                         
              ELSE                                                              
                 IF WS-K022-STATUS = 23                                         
                    MOVE 'N' TO WS-K022-FOUND                                   
                 ELSE                                                           
                    DISPLAY 'ERROR IN READ OF BP13K022. STATUS IS '             
                            WS-K022-STATUS                                      
                    PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                  
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
           IF WS-K022-STATUS = 23                                               
              MOVE 'N' TO WS-K022-FOUND                                         
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'ERROR WITH START READ OF BP13K022. STATUS IS '           
                       WS-K022-STATUS                                           
              PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       8000-DISPLAY.                                                            
      *------------------------------------------------------------*            
           IF WS-LINE-COUNT1 <= 55                                              
              WRITE P13L432A-REC FROM PRINT-LINE                                
           END-IF.                                                              
                                                                                
           IF WS-LINE-COUNT2 <= 55                                              
              WRITE P13L432B-REC FROM PRINT-LINE                                
           END-IF.                                                              
                                                                                
           ACCEPT WS-CUR-DATE   FROM DATE.                                      
           MOVE WS-CUR-DATE(1:2) TO WS-YY.                                      
           MOVE WS-CUR-DATE(3:2) TO WS-MM.                                      
           MOVE WS-CUR-DATE(5:2) TO WS-DD.                                      
                                                                                
           DISPLAY '********************  '.                                    
           DISPLAY '                      '.                                    
           DISPLAY 'PROGRAM-ID : BP13C432 '.                                    
           DISPLAY 'OUTPUT     : P13L432A '.                                    
           DISPLAY '             P13L432B '.                                    
           DISPLAY 'CURR-DATE  : ' WS-PRINT-DATE.                               
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS READ    :'                                    
           DISPLAY '   BP13F800           : ' WS-READ-F800.                     
           DISPLAY 'NO OF RECORDS PRINTED :'                                    
           DISPLAY '    P13L432A          : ' WS-PRINT1.                        
           DISPLAY '    P13L432B          : ' WS-PRINT2.                        
           DISPLAY '********************  '.                                    
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       9000-CLOSE-FILES.                                                        
      *------------------------------------------------------------*            
                                                                                
           CLOSE BP13F800                                                       
                 P13L432A P13L432B.                                             
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
