      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CC20.                                                 
       AUTHOR.        ABH.                                                      
       DATE-WRITTEN.  25/05/06.                                                 
      *****************************************************************         
      * OBJECTIVE - TO GENERATE REPORT ON PROGRESS PAYMENT AFTER                
      *                REFLECTING INTEREST, LOAN AND INSTALMENT.                
      *                                                                         
      *     INPUT FILE :                                                        
      *                  1. BP13FD38                                            
      *                  2. SY02F001                                            
      *                                                                         
      *     OUTPUT FILE:                                                        
      *                  1. NIL                                                 
      *                                                                         
      *     LISTING:                                                            
      *                  1. BP13LC20 (REPORT)                                   
      *                                                                         
      *                                                                         
      *----------------------------------------------------------------         
      * REF NO    DATE      BY   AMENDMENTS/ENHANCEMENTS                        
      * -------   --------  ---  ------------------------                       
      * BP132772  05252006  BA3  NEW PROGRAM                                    
      * BP132888  07072006  BA2                                                 
      * BP132914  09192006  BA3  CHANGE REPORT HEADING                          
      * BP132888  23012007  BA3  INCREASE SEQUENCE NUMBER INTO 5 BYTES          
      * BP133173  01112007  BA3  INCLUDE NUM-LOAN-SCH INTO THE REPORT           
      * BP133209  31012008  ED1  ADD BALCR FIELDS TO TAG NEGATIVE AMTS          
      * BP133572  06042009  JB8  REMOVE CHECKING OF CURRENT DATE                
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13FD38  ASSIGN  TO   BP13FD38.                             
           SELECT  SY02F001  ASSIGN  TO   SY02F001.                             
           SELECT  BP13LC20  ASSIGN  TO   BP13LC20.                             
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BP13FD38                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 400 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13FD38.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13LC20                    BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 132 CHARACTERS           
                                       LABEL RECORDS ARE OMITTED                
                                       RECORDING MODE IS F.                     
       01  PRINT-REC                   PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FD38-EOF                 PIC X     VALUE 'N'.                     
                                                                                
       01  WS-NEW-BAL                  PIC 9(7)V99.                             
       01  WS-COUNTERS                           VALUE ZEROES.                  
           05  WS-CNT-FD38             PIC 9(7).                                
           05  WS-PAGE-CNT             PIC 9(5).                                
           05  WS-LINE-CNT             PIC 9(2).                                
           05  CNT-CASES               PIC 9(5).                                
                                                                                
       01  WS-CURR-DATE.                                                        
           05  WS-CURR-CCYY            PIC X(4)  VALUE SPACES.                  
           05  WS-CURR-MM              PIC X(2)  VALUE SPACES.                  
           05  WS-CURR-DD              PIC X(2)  VALUE SPACES.                  
                                                                                
       01  WS-DUE-DATE.                                                         
           05  WS-KD38-DTE-DUE                PIC  X(08).                       
           05  WS-NUM-KD38-DTE-DUE  REDEFINES WS-KD38-DTE-DUE.                  
               10  WS-KD38-DTE-DUE-CCYY       PIC 9(04).                        
               10  WS-KD38-DTE-DUE-MM         PIC 9(02).                        
               10  WS-KD38-DTE-DUE-DD         PIC 9(02).                        
                                                                                
       01  LC20-PR-HEAD-01.                                                     
           05  FILLER                  PIC X(8)  VALUE 'BP13LC20'.              
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(8)  VALUE 'HDBCAT 3'.              
           05  FILLER                  PIC X(24) VALUE SPACES.                  
           05  FILLER                  PIC X(20) VALUE                          
                                       'S Y S T E M   O F   '.                  
           05  FILLER                  PIC X(19) VALUE                          
                                       'C O M M I T M E N T'.                   
           05  FILLER                  PIC X(15) VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'DATE :'.                
           05  LC20-DATE               PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'PAGE : '.               
           05  LC20-PAGENO             PIC ZZZZ9 .                              
                                                                                
       01  LC20-PR-HEAD-02.                                                     
           05  FILLER                  PIC X(34) VALUE SPACES.                  
           05  FILLER                  PIC X(50) VALUE                          
               'REPORT ON NEW LOAN EFFECTIVE, COMPUTE REVISE INSTA'.            
           05  FILLER                  PIC X(05) VALUE                          
               'LMENT'.                                                         
                                                                                
       01  LC20-PR-HEAD-03.                                                     
           05  FILLER                  PIC X(6)  VALUE 'S/NO'.                  
           05  FILLER                  PIC X(30) VALUE SPACES.                  
           05  FILLER                  PIC X(06) VALUE 'AS AT '.                
           05  LC20-HEAD-DTE           PIC X(07) VALUE SPACES.                  
           05  FILLER                  PIC X(16) VALUE SPACES.                  
           05  FILLER                  PIC X(06) VALUE 'AS AT '.                
           05  LC20-HEAD-DTE1          PIC X(07) VALUE SPACES.                  
           05  FILLER                  PIC X(13) VALUE SPACES.                  
                                                                                
           05  FILLER                  PIC X(10) VALUE                          
                                      'OLD INSTAL'.                             
           05  FILLER                  PIC X(07) VALUE SPACES.                  
           05  FILLER                  PIC X(10) VALUE                          
                                      'NEW INSTAL'.                             
                                                                                
       01  LC20-PR-DETAILS-01.                                                  
           05  LC20-SNO                PIC ZZZZ9 VALUE ZEROES.                  
           05  FILLER                  PIC X(2)  VALUE SPACES.                  
           05  FILLER                  PIC X(14) VALUE                          
                                             'REGN         :'.                  
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  LC20-REG-NO             PIC X(8)  VALUE SPACES.                  
           05  FILLER                  PIC X(6)  VALUE SPACES.                  
           05  FILLER                  PIC X(15) VALUE                          
                                                'EXIST LOAN BAL:'.              
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  LC20-EXIST-LOAN-BAL     PIC $$$$$$$$9.99 VALUE ZEROES.           
           05  LC20-EXIST-LOAN-BALCR   PIC X(2)  VALUE SPACES.                  
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  FILLER                  PIC X(9)  VALUE 'LOAN-BAL:'.             
           05  FILLER                  PIC X(2)  VALUE SPACES.                  
           05  LC20-LOAN-BAL           PIC $$$$$$9.99  VALUE ZEROES.            
           05  LC20-LOAN-BALCR         PIC X(2)  VALUE SPACES.                  
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE 'CASH:'.                 
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  LC20-OLD-INST-CASH      PIC $$$$$$9.99  VALUE ZEROES.            
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE 'CASH:'.                 
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  LC20-NEW-INST-CASH      PIC $$$$$$9.99  VALUE ZEROES.            
                                                                                
       01  LC20-PR-DETAILS-02.                                                  
           05  FILLER                  PIC X(7)  VALUE SPACES.                  
           05  FILLER                  PIC X(14) VALUE                          
                                       'SCHACCT      :'.                        
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  LC20-SCHACCT            PIC X(13) VALUE SPACES.                  
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  FILLER                  PIC X(15) VALUE                          
                                                'ORIG-INTEREST :'.              
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  LC20-ORIG-INT1          PIC $$$$$9.99   VALUE ZEROES.            
           05  FILLER                  PIC X(3)  VALUE SPACES.                  
           05  FILLER                  PIC X(9)  VALUE 'ORIG-INT:'.             
           05  FILLER                  PIC X(3)  VALUE SPACES.                  
           05  LC20-ORIG-INT2          PIC $$$$$9.99  VALUE ZEROES.             
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE 'CPF1:'.                 
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  LC20-OLD-INST-CPF1      PIC $$$$$$9.99  VALUE ZEROES.            
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE 'CPF1:'.                 
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  LC20-NEW-INST-CPF1      PIC $$$$$$9.99  VALUE ZEROES.            
                                                                                
       01  LC20-PR-DETAILS-03.                                                  
           05  FILLER                  PIC X(7)  VALUE SPACES.                  
           05  FILLER                  PIC X(14) VALUE                          
                                            'SCHEDULE     :'.                   
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  LC20-SCHEDULE           PIC X(2)  VALUE SPACES.                  
           05  FILLER                  PIC X(12) VALUE SPACES.                  
           05  FILLER                  PIC X(15) VALUE                          
                                                'ORIG-INSTAL   :'.              
           05  FILLER                  PIC X(3)  VALUE SPACES.                  
           05  LC20-ORIG-INS1          PIC $$$$$$9.99  VALUE ZEROES.            
           05  FILLER                  PIC X(3)  VALUE SPACES.                  
           05  FILLER                  PIC X(9)  VALUE 'ORIG-INS:'.             
           05  FILLER                  PIC X(2)  VALUE SPACES.                  
           05  LC20-ORIG-INS2          PIC $$$$$$9.99   VALUE ZEROES.           
           05  FILLER                  PIC X(3)  VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE 'CPF2:'.                 
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  LC20-OLD-INST-CPF2      PIC $$$$$$9.99  VALUE ZEROES.            
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE 'CPF2:'.                 
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  LC20-NEW-INST-CPF2      PIC $$$$$$9.99  VALUE ZEROES.            
                                                                                
       01  LC20-PR-DETAILS-04.                                                  
           05  FILLER                  PIC X(7)  VALUE SPACES.                  
           05  FILLER                  PIC X(14) VALUE                          
                                      'DUE DATE     :'.                         
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  LC20-DUE-DATE           PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(15) VALUE                          
                                                'NEW LOAN      :'.              
           05  FILLER                  PIC X(2)  VALUE SPACES.                  
           05  LC20-NEW-LOAN           PIC $$$$$$$9.99 VALUE ZEROES.            
           05  FILLER                  PIC X(27) VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE 'CPF3:'.                 
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  LC20-OLD-INST-CPF3      PIC $$$$$$9.99  VALUE ZEROES.            
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE 'CPF3:'.                 
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  LC20-NEW-INST-CPF3      PIC $$$$$$9.99  VALUE ZEROES.            
                                                                                
       01  LC20-PR-DETAILS-05.                                                  
           05  FILLER                  PIC X(07) VALUE SPACES.                  
           05  FILLER                  PIC X(14) VALUE                          
                                         'BAL REPAY    :'.                      
           05  FILLER                  PIC X(1) VALUE SPACES.                   
           05  LC20-LOAN-REPAY         PIC Z9.9(6) VALUE ZEROES.                
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  FILLER                  PIC X(15) VALUE                          
                                                'NEW INTEREST  :'.              
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  LC20-NEW-INT            PIC $$$$$9.99   VALUE ZEROES.            
           05  FILLER                  PIC X(27) VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE 'CPF4:'.                 
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  LC20-OLD-INST-CPF4      PIC $$$$$$9.99  VALUE ZEROES.            
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE 'CPF4:'.                 
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  LC20-NEW-INST-CPF4      PIC $$$$$$9.99 VALUE ZEROES.             
                                                                                
       01  LC20-PR-DETAILS-06.                                                  
           05  FILLER                  PIC X(07) VALUE SPACES.                  
           05  FILLER                  PIC X(14) VALUE 'LOAN EXP DATE:'.        
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  LC20-LOAN-EXP-DTE       PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(04) VALUE SPACES.                  
           05  FILLER                  PIC X(15) VALUE                          
                                                'NEW LOAN BAL  :'.              
           05  FILLER                  PIC X(2)  VALUE SPACES.                  
           05  LC20-NEW-LOAN-BAL       PIC $$$$$$$9.99 VALUE ZEROES.            
           05  LC20-NEW-LOAN-BALCR     PIC X(2)  VALUE SPACES.                  
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  FILLER                  PIC X(9)  VALUE 'NEW BAL :'.             
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  LC20-NEW-BAL            PIC $$$$$$$9.99 VALUE ZEROES.            
           05  FILLER                  PIC X(3)  VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE 'TOT :'.                 
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  LC20-OLD-INST-TOT       PIC $$$$$$9.99  VALUE ZEROES.            
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE 'TOT :'.                 
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  LC20-NEW-INST-TOT       PIC $$$$$$9.99  VALUE ZEROES.            
                                                                                
       01  LC20-PR-DETAILS-07.                                                  
           05  FILLER                  PIC X(07) VALUE SPACES.                  
           05  FILLER                  PIC X(14) VALUE                          
                                          'SCH/INT RATE :'.                     
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  LC20-LOAN-SCHEME        PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE '/'.                     
           05  LC20-INT-RATE           PIC 99.99 VALUE ZEROES.                  
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  FILLER                  PIC X(15) VALUE                          
                                                'ARR-INSTAL    :'.              
           05  FILLER                  PIC X(3)  VALUE SPACES.                  
           05  LC20-ARR-INSTAL         PIC $$$$$$9.99  VALUE ZEROES.            
           05  FILLER                  PIC X(27) VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE 'EFF :'.                 
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  LC20-OLD-EFF-DATE       PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE 'EFF :'.                 
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  LC20-NEW-EFF-DATE       PIC X(10) VALUE SPACES.                  
                                                                                
       01  LC20-PR-DETAILS-08.                                                  
           05  FILLER                  PIC X(07) VALUE SPACES.                  
           05  FILLER                  PIC X(14) VALUE                          
                                                'DAY COMP     :'.               
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  LC20-DAY-COMP           PIC XXX   VALUE SPACES.                  
                                                                                
       01  LC20-PR-DETAILS-09.                                                  
           05  FILLER                  PIC X(07) VALUE SPACES.                  
           05  FILLER                  PIC X(14) VALUE                          
                                                'INT AMT      :'.               
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  LC20-INT-AMT            PIC $$$$$9.99   VALUE ZEROES.            
                                                                                
       01  LC20-PR-DETAILS-10.                                                  
           05  FILLER                  PIC X(07) VALUE SPACES.                  
           05  FILLER                  PIC X(14) VALUE                          
                                               'EFF INT      :'.                
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
           05  LC20-EFF-INT            PIC X(10) VALUE SPACES.                  
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN-PARA.                                                          
      ******************************************************************        
                                                                                
           PERFORM 1000-START-UP           THRU  1000-EXIT.                     
                                                                                
           PERFORM 1500-READ-SY02F001      THRU  1500-EXIT.                     
                                                                                
           PERFORM 2000-READ-FD38          THRU  2000-EXIT.                     
                                                                                
           PERFORM 3000-PROCESS-RECORD     THRU  3000-EXIT                      
                   UNTIL WS-FD38-EOF       =     'Y'.                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1000-START-UP.                                                           
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13FD38                                                 
                       SY02F001                                                 
                OUTPUT BP13LC20.                                                
                                                                                
           MOVE 60                    TO WS-LINE-CNT.                           
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1500-READ-SY02F001.                                                      
      ******************************************************************        
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-CURRENT      TO WS-CURR-DATE.                          
                                                                                
           STRING WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CCYY                    
                  DELIMITED BY SIZE INTO LC20-DATE.                             
                                                                                
       1500-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2000-READ-FD38.                                                          
      ******************************************************************        
                                                                                
           READ BP13FD38           AT   END                                     
                MOVE 'Y'           TO   WS-FD38-EOF                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO   WS-CNT-FD38.                            
                                                                                
       2000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORD.                                                     
      ******************************************************************        
                                                                                
           PERFORM 4000-PRINT-DETAILS      THRU 4000-EXIT.                      
                                                                                
           PERFORM 2000-READ-FD38          THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       4000-PRINT-DETAILS.                                                      
      ******************************************************************        
           IF FD38-DTE-DUE NOT = SPACES AND LOW-VALUES AND ZEROES               
              STRING FD38-DTE-DUE (7:2) '/'                                     
                     FD38-DTE-DUE (5:2) '/'                                     
                     FD38-DTE-DUE (1:4)                                         
                     DELIMITED BY SIZE    INTO LC20-DUE-DATE                    
              MOVE FD38-DTE-DUE             TO WS-KD38-DTE-DUE                  
              STRING FD38-DTE-DUE (5:2) '/'                                     
                     FD38-DTE-DUE (1:4)                                         
                     DELIMITED BY SIZE    INTO LC20-HEAD-DTE                    
              MOVE FD38-DTE-DUE             TO WS-KD38-DTE-DUE                  
           ELSE                                                                 
              MOVE SPACES                   TO LC20-DUE-DATE                    
                                               LC20-HEAD-DTE                    
              MOVE ZEROES                   TO WS-KD38-DTE-DUE                  
           END-IF.                                                              
                                                                                
           IF WS-NUM-KD38-DTE-DUE NOT = ZEROES                                  
              COMPUTE WS-KD38-DTE-DUE-MM = WS-KD38-DTE-DUE-MM + 1               
              IF WS-KD38-DTE-DUE-MM > 12                                        
                 MOVE 01                    TO WS-KD38-DTE-DUE-MM               
                 ADD +1                     TO WS-KD38-DTE-DUE-CCYY             
              END-IF                                                            
              STRING WS-KD38-DTE-DUE-MM   '/'                                   
                     WS-KD38-DTE-DUE-CCYY                                       
              DELIMITED BY SIZE             INTO LC20-HEAD-DTE1                 
           ELSE                                                                 
              MOVE SPACES                   TO   LC20-HEAD-DTE1                 
           END-IF.                                                              
                                                                                
      *    IF WS-LINE-CNT > 50                                                  
           IF WS-LINE-CNT > 08                                                  
              PERFORM 5000-PRINT-HEADING THRU 5000-EXIT                         
           END-IF.                                                              
                                                                                
           ADD   1                       TO CNT-CASES.                          
                                                                                
           MOVE CNT-CASES                TO LC20-SNO.                           
           MOVE FD38-NUM-REGN            TO LC20-REG-NO.                        
           STRING FD38-NUM-SCH-ACC (1:4) '-'                                    
                  FD38-NUM-SCH-ACC (5:4) '-'                                    
                  FD38-NUM-SCH-ACC (09:3)                                       
                  DELIMITED BY SIZE    INTO LC20-SCHACCT.                       
           MOVE FD38-NUM-SCHEDULE        TO LC20-SCHEDULE.                      
                                                                                
           MOVE LC20-DUE-DATE(4:7)       TO  LC20-HEAD-DTE.                     
           IF FD38-AMT-ARR-INSTAL IS NOT NUMERIC                                
              MOVE 0                     TO FD38-AMT-ARR-INSTAL.                
                                                                                
           IF FD38-AMT-OPEN-BAL1 IS NOT NUMERIC                                 
              MOVE 0 TO FD38-AMT-OPEN-BAL1.                                     
           IF FD38-AMT-ARR-INSTAL IS NOT NUMERIC                                
              MOVE 0 TO FD38-AMT-ARR-INSTAL.                                    
           IF FD38-AMT-INSTAL-ORIG1 IS NOT NUMERIC                              
              MOVE 0 TO FD38-AMT-INSTAL-ORIG1.                                  
           IF FD38-AMT-INTEREST-ORIG1 IS NOT NUMERIC                            
              MOVE 0 TO FD38-AMT-INTEREST-ORIG1.                                
           IF FD38-AMT-INTEREST-NEWLOAN IS NOT NUMERIC                          
              MOVE 0 TO FD38-AMT-INTEREST-NEWLOAN.                              
           IF FD38-NUM-LOAN-REPYMT-BALYR IS NOT NUMERIC                         
              MOVE 0 TO FD38-NUM-LOAN-REPYMT-BALYR.                             
           IF FD38-AMT-OPEN-BAL2 IS NOT NUMERIC                                 
              MOVE 0 TO FD38-AMT-OPEN-BAL2.                                     
           MOVE FD38-AMT-OPEN-BAL1       TO LC20-EXIST-LOAN-BAL.                
           IF FD38-AMT-OPEN-BAL1 < 0                                            
              MOVE 'CR' TO LC20-EXIST-LOAN-BALCR                                
           ELSE                                                                 
              MOVE '  ' TO LC20-EXIST-LOAN-BALCR                                
           END-IF.                                                              
                                                                                
           MOVE FD38-AMT-ARR-INSTAL      TO LC20-ARR-INSTAL.                    
           MOVE FD38-AMT-INSTAL-ORIG1    TO LC20-ORIG-INS1.                     
           MOVE FD38-AMT-INTEREST-ORIG1  TO LC20-ORIG-INT1.                     
           MOVE FD38-AMT-LOAN-NEW        TO LC20-NEW-LOAN.                      
           MOVE FD38-AMT-OPEN-BAL2       TO LC20-LOAN-BAL                       
                                            LC20-NEW-LOAN-BAL.                  
           IF FD38-AMT-OPEN-BAL2 < 0                                            
              MOVE 'CR'                  TO LC20-LOAN-BALCR                     
                                            LC20-NEW-LOAN-BALCR                 
           ELSE                                                                 
              MOVE '  '                  TO LC20-LOAN-BALCR                     
                                            LC20-NEW-LOAN-BALCR                 
           END-IF.                                                              
                                                                                
           MOVE FD38-AMT-INTEREST-NEWLOAN TO LC20-NEW-INT.                      
                                                                                
           IF FD38-AMT-INSTAL-ORIG2 NOT NUMERIC                                 
              MOVE 0 TO FD38-AMT-INSTAL-ORIG2.                                  
           IF FD38-AMT-INTEREST-ORIG2 NOT NUMERIC                               
              MOVE 0 TO FD38-AMT-INTEREST-ORIG2.                                
           IF FD38-AMT-INTEREST-NEWLOAN NOT NUMERIC                             
              MOVE 0 TO FD38-AMT-INTEREST-NEWLOAN.                              
           IF FD38-AMT-LOAN-TOT-DUEDTE IS NOT NUMERIC                           
              MOVE 0 TO FD38-AMT-LOAN-TOT-DUEDTE.                               
           MOVE FD38-AMT-INSTAL-ORIG2    TO LC20-ORIG-INS2.                     
           MOVE FD38-AMT-INTEREST-ORIG2  TO LC20-ORIG-INT2.                     
           MOVE FD38-AMT-LOAN-TOT-DUEDTE TO LC20-NEW-BAL.                       
                                                                                
           MOVE FD38-NUM-LOAN-SCH        TO LC20-LOAN-SCHEME.                   
           MOVE FD38-CDE-INTEREST-RATE   TO LC20-INT-RATE.                      
           MOVE FD38-NUM-INTEREST-DAY    TO LC20-DAY-COMP.                      
           MOVE FD38-AMT-INTEREST-NEWLOAN TO LC20-INT-AMT.                      
           IF FD38-DTE-INTEREST NOT = SPACES AND ZEROES AND LOW-VALUES          
              STRING FD38-DTE-INTEREST(7:2) '/'                                 
                     FD38-DTE-INTEREST(5:2) '/'                                 
                     FD38-DTE-INTEREST(1:4)                                     
                     DELIMITED BY SIZE      INTO LC20-EFF-INT                   
           ELSE                                                                 
              MOVE SPACES                     TO LC20-EFF-INT                   
           END-IF.                                                              
                                                                                
           IF FD38-AMT-INSTAL-OLD-CASH IS NOT NUMERIC                           
              MOVE 0 TO FD38-AMT-INSTAL-OLD-CASH.                               
           IF FD38-AMT-INSTAL-OLD-CPF1 IS NOT NUMERIC                           
              MOVE 0 TO FD38-AMT-INSTAL-OLD-CPF1.                               
           IF FD38-AMT-INSTAL-OLD-CPF2 IS NOT NUMERIC                           
              MOVE 0 TO FD38-AMT-INSTAL-OLD-CPF2.                               
           IF FD38-AMT-INSTAL-OLD-CPF3 IS NOT NUMERIC                           
              MOVE 0 TO FD38-AMT-INSTAL-OLD-CPF3.                               
           IF FD38-AMT-INSTAL-OLD-CPF4 IS NOT NUMERIC                           
              MOVE 0 TO FD38-AMT-INSTAL-OLD-CPF4.                               
           MOVE FD38-AMT-INSTAL-OLD-CASH TO LC20-OLD-INST-CASH.                 
           MOVE FD38-AMT-INSTAL-OLD-CPF1 TO LC20-OLD-INST-CPF1.                 
           MOVE FD38-AMT-INSTAL-OLD-CPF2 TO LC20-OLD-INST-CPF2.                 
           MOVE FD38-AMT-INSTAL-OLD-CPF3 TO LC20-OLD-INST-CPF3.                 
           MOVE FD38-AMT-INSTAL-OLD-CPF4 TO LC20-OLD-INST-CPF4.                 
           MOVE FD38-AMT-INSTAL-OLD-TOT  TO LC20-OLD-INST-TOT.                  
      *?   MOVE                          TO LC20-OLD-EFF-DATE.                  
                                                                                
           IF FD38-AMT-INSTAL-NEW-CASH IS NOT NUMERIC                           
              MOVE 0 TO FD38-AMT-INSTAL-NEW-CASH.                               
           MOVE FD38-AMT-INSTAL-NEW-CASH TO LC20-NEW-INST-CASH.                 
           MOVE FD38-AMT-INSTAL-NEW-CPF1 TO LC20-NEW-INST-CPF1.                 
           MOVE FD38-AMT-INSTAL-NEW-CPF2 TO LC20-NEW-INST-CPF2.                 
           MOVE FD38-AMT-INSTAL-NEW-CPF3 TO LC20-NEW-INST-CPF3.                 
           MOVE FD38-AMT-INSTAL-NEW-CPF4 TO LC20-NEW-INST-CPF4.                 
           MOVE FD38-AMT-INSTAL-NEW-TOT  TO LC20-NEW-INST-TOT.                  
           MOVE FD38-NUM-LOAN-REPYMT-BALYR TO LC20-LOAN-REPAY.                  
           STRING FD38-DTE-INSTAL-NEW(7:2) '/'                                  
                  FD38-DTE-INSTAL-NEW(5:2) '/'                                  
                  FD38-DTE-INSTAL-NEW(1:4)                                      
                  DELIMITED BY SIZE      INTO LC20-NEW-EFF-DATE.                
                                                                                
           IF FD38-DTE-LOAN-EXPIRY NOT = SPACES AND LOW-VALUES                  
                                         AND ZEROES                             
              STRING FD38-DTE-LOAN-EXPIRY(7:2) '/'                              
                     FD38-DTE-LOAN-EXPIRY(5:2) '/'                              
                     FD38-DTE-LOAN-EXPIRY(1:4)                                  
                     DELIMITED BY SIZE      INTO LC20-LOAN-EXP-DTE              
           ELSE                                                                 
              MOVE   SPACES                   TO LC20-LOAN-EXP-DTE              
           END-IF.                                                              
                                                                                
           WRITE PRINT-REC             FROM LC20-PR-DETAILS-01.                 
           WRITE PRINT-REC             FROM LC20-PR-DETAILS-02.                 
           WRITE PRINT-REC             FROM LC20-PR-DETAILS-03.                 
           WRITE PRINT-REC             FROM LC20-PR-DETAILS-04.                 
           WRITE PRINT-REC             FROM LC20-PR-DETAILS-05.                 
           WRITE PRINT-REC             FROM LC20-PR-DETAILS-06.                 
           WRITE PRINT-REC             FROM LC20-PR-DETAILS-07.                 
           WRITE PRINT-REC             FROM LC20-PR-DETAILS-08.                 
           WRITE PRINT-REC             FROM LC20-PR-DETAILS-09.                 
           WRITE PRINT-REC             FROM LC20-PR-DETAILS-10.                 
           MOVE  SPACES                TO   PRINT-REC.                          
           WRITE PRINT-REC.                                                     
                                                                                
           ADD 1                         TO WS-LINE-CNT.                        
                                                                                
                                                                                
       4000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       5000-PRINT-HEADING.                                                      
      ******************************************************************        
                                                                                
           ADD 1               TO WS-PAGE-CNT.                                  
           MOVE WS-PAGE-CNT    TO LC20-PAGENO.                                  
           MOVE SPACES         TO PRINT-REC                                     
           WRITE PRINT-REC FROM LC20-PR-HEAD-01 AFTER PAGE.                     
           WRITE PRINT-REC FROM LC20-PR-HEAD-02 AFTER 2.                        
           WRITE PRINT-REC FROM LC20-PR-HEAD-03 AFTER 2.                        
           MOVE SPACES TO PRINT-REC.                                            
           WRITE PRINT-REC AFTER 1.                                             
           MOVE 6 TO WS-LINE-CNT.                                               
                                                                                
       5000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
                                                                                
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13CC20'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY 'NO OF BP13FD38 RECORDS READ..........: '                    
                    WS-CNT-FD38.                                                
           DISPLAY 'NO OF BP13LC20 RECORDS WRITTEN.......: '                    
                    CNT-CASES.                                                  
                                                                                
           CLOSE BP13FD38                                                       
                 SY02F001                                                       
                 BP13LC20.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
