       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB34.                                                 
       AUTHOR.        EDEN ROSE DINGAL.                                         
       DATE-WRITTEN.  29/08/20007.                                              
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : BASED ON TRANSACTION FILE, OUTPUT THE *                
      *                  DAILY ACCEPT/REJECT  CASES.           *                
      *                  IF RECORD WITH ACCEPT/REJECT ON THE   *                
      *                  SAME DAY, NO OUTPUT.                  *                
      *                                                        *                
      *    INPUT  FILE  : BP13F310  - ALL CASES                *                
      *    OUTPUT FILES : BP13K580  - REJ'D & ACPT'D CASES     *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO   BY   DATE     DETAILS                         *                
      * BP133111 ED1 290807    NEW PROGRAM                     *                
      * BP133111 ED1 190907    GET NT & FLAT-TYP FROM F310. IF *                
      *                        EMPTY, GET FROM BP13K800        *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F310 ASSIGN TO BP13F310.                                  
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE IS DYNAMIC                                          
                RECORD KEY IS K800-NUM-REGN                                     
                FILE STATUS IS K800-STATUS.                                     
                                                                                
           SELECT BP13K580 ASSIGN TO BP13K580                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE IS DYNAMIC                                          
                RECORD KEY IS K580-KEY-FLD                                      
                FILE STATUS IS K580-STATUS.                                     
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K580                                                             
           RECORD CONTAINS 200  CHARACTERS.                                     
       COPY BP13K580.                                                           
                                                                                
       FD   BP13F310                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 150 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F310.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F310-READ                PIC 9(7)    VALUE ZEROS.              
           05 WS-K580-WRITE               PIC 9(7)    VALUE ZEROS.              
                                                                                
       01  K800-STATUS                    PIC 99      VALUE 0.                  
       01  K580-STATUS                    PIC 99      VALUE 0.                  
       01  WS-EOF-DUPREC                  PIC X       VALUE 'N'.                
       01  WS-EOF-F310                    PIC X       VALUE 'N'.                
       01  WS-NEW-CASE                    PIC X       VALUE 'N'.                
       01  WS-DATE                        PIC X(8)    VALUE SPACES.             
       01  WS-PREV-SCH-ACC                PIC X(11)   VALUE SPACES.             
       01  WS-CURR-F310-REC               PIC X(150)  VALUE SPACES.             
       01  WS-PREV-F310-REC               PIC X(150)  VALUE SPACES.             
       01  WS-PREV-F310-REC-REJ           PIC X(150)  VALUE SPACES.             
       01  WS-PREV-DTE                    PIC X(8)    VALUE SPACES.             
       01  WS-PREV-REGN                   PIC X(8)    VALUE SPACES.             
       01  WS-CNT                         PIC 9(2)    VALUE ZEROS.              
       01  WS-CTR                         PIC 9(6)    VALUE ZEROS.              
       01  WS-CTR-CHAR REDEFINES WS-CTR.                                        
           05 WS-CTR-X                    PIC ZZZZZ9.                           
       01  WS-ACCEPT-FND                  PIC X       VALUE 'N'.                
       01  WS-OUTPUT                      PIC X       VALUE 'N'.                
                                                                                
       01  WS-NEW-KEY.                                                          
           05  WS-NEW-REGN                PIC X(8)    VALUE SPACES.             
           05  WS-NEW-DATE                PIC X(8)    VALUE SPACES.             
                                                                                
       01  WS-PRE-KEY.                                                          
           05  WS-PRE-REGN                PIC X(8)    VALUE SPACES.             
           05  WS-PRE-DATE                PIC X(8)    VALUE SPACES.             
                                                                                
       01  WS-K800-NRIC.                                                        
           05  WS-HEX                   PIC X(2)    VALUE '##'.                 
           05  WS-NRIC                  PIC X(6)    VALUE SPACES.               
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************                                                        
       000-MAIN-ROUTINE.                                                        
      ******************                                                        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F310      THRU 200-EXIT.                            
           PERFORM 300-PROCESS-RTN    THRU 300-EXIT                             
             UNTIL WS-EOF-F310 = 'Y'.                                           
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ********************                                                      
       100-INITIALIZATION.                                                      
      ********************                                                      
            OPEN INPUT  BP13F310                                                
                        BP13K800                                                
                 I-O    BP13K580.                                               
                                                                                
                                                                                
            IF K800-STATUS NOT = 00 AND 97                                      
               DISPLAY 'BP13K800 - OPEN ERROR, STATUS: ' K800-STATUS            
            END-IF.                                                             
                                                                                
            IF K580-STATUS NOT = 00 AND 97                                      
               DISPLAY 'BP13K580 - OPEN ERROR, STATUS: ' K580-STATUS            
            END-IF.                                                             
                                                                                
            MOVE ZEROS                 TO WS-COUNTERS.                          
                                                                                
            MOVE FUNCTION CURRENT-DATE TO WS-DATE.                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *****************                                                         
       200-READ-F310.                                                           
      *****************                                                         
            READ BP13F310 AT END                                                
                 MOVE 'Y'             TO WS-EOF-F310                            
                 MOVE HIGH-VALUES     TO F310-REGN-NO                           
                 GO TO 200-EXIT.                                                
                                                                                
            ADD  1                    TO WS-F310-READ.                          
            MOVE F310-REGN-NO         TO WS-NEW-REGN.                           
            MOVE F310-DTE-TRANS       TO WS-NEW-DATE.                           
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *****************                                                         
       300-PROCESS-RTN.                                                         
      *****************                                                         
            MOVE F310-SCH-ACC       TO WS-PREV-SCH-ACC.                         
            MOVE 'N'                TO WS-OUTPUT.                               
            MOVE 'N'                TO WS-ACCEPT-FND.                           
            MOVE SPACES             TO WS-CURR-F310-REC.                        
            MOVE SPACES             TO WS-PREV-F310-REC.                        
            MOVE SPACES             TO WS-PREV-F310-REC-REJ.                    
            MOVE WS-NEW-KEY         TO WS-PRE-KEY.                              
                                                                                
            PERFORM 400-MATCH-RECORD     THRU 400-EXIT UNTIL                    
               WS-NEW-KEY     NOT = WS-PRE-KEY OR                               
               WS-EOF-F310        = 'Y'.                                        
                                                                                
            IF WS-ACCEPT-FND = 'Y'                                              
                                                                                
      * WRITE ACCEPTED CASES TO K580                                            
      *---------------------------------------                                  
               MOVE WS-PREV-F310-REC      TO   BP13F310-TRANS-REC               
               PERFORM 500-WRITE-RECORD   THRU 500-EXIT                         
                                                                                
            END-IF.                                                             
                                                                                
            IF WS-OUTPUT     = 'Y'                                              
                                                                                
      * WRITE REJECTED CASES TO K580                                            
      *------------------------------                                           
               MOVE WS-PREV-F310-REC-REJ  TO   BP13F310-TRANS-REC               
               PERFORM 500-WRITE-RECORD   THRU 500-EXIT                         
                                                                                
            END-IF.                                                             
                                                                                
            MOVE WS-CURR-F310-REC      TO   BP13F310-TRANS-REC.                 
                                                                                
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************                                                        
       400-MATCH-RECORD.                                                        
      ******************                                                        
                                                                                
            IF WS-NEW-KEY = WS-PRE-KEY                                          
               IF F310-CDE-TRANS(1:1) = 'A' AND                                 
                  F310-SCH-ACC        = WS-PREV-SCH-ACC                         
                  MOVE 'Y' TO WS-ACCEPT-FND                                     
                  MOVE BP13F310-TRANS-REC TO WS-PREV-F310-REC                   
               ELSE                                                             
                                                                                
               IF F310-CDE-TRANS(1:1) = 'R'                                     
                  IF F310-SCH-ACC  = WS-PREV-SCH-ACC AND                        
                     WS-ACCEPT-FND = 'Y'                                        
                     MOVE 'N'  TO WS-OUTPUT                                     
                     MOVE 'N'  TO WS-ACCEPT-FND                                 
                  ELSE                                                          
                     MOVE 'Y'  TO WS-OUTPUT                                     
                     MOVE BP13F310-TRANS-REC TO WS-PREV-F310-REC-REJ            
                  END-IF                                                        
                                                                                
               END-IF                                                           
            END-IF.                                                             
                                                                                
            PERFORM 200-READ-F310      THRU 200-EXIT.                           
            MOVE BP13F310-TRANS-REC    TO   WS-CURR-F310-REC.                   
                                                                                
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *******************                                                       
       410-READ-BP13K800.                                                       
      *******************                                                       
            MOVE SPACES        TO BP13K800-MASTER.                              
            MOVE F310-REGN-NO  TO K800-NUM-REGN.                                
                                                                                
            READ BP13K800.                                                      
             EVALUATE K800-STATUS                                               
             WHEN 00                                                            
                  CONTINUE                                                      
             WHEN 23                                                            
                  DISPLAY 'BP13K800 REC NOTFND,KEY ' K800-NUM-REGN              
                  MOVE SPACES TO BP13K800-MASTER                                
             WHEN OTHER                                                         
                  DISPLAY 'BP13K800 - ERROR READING, STAT: '                    
                           K800-STATUS  ',KEY ' K800-NUM-REGN                   
                  MOVE SPACES TO BP13K800-MASTER                                
             END-EVALUATE.                                                      
                                                                                
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************                                                        
       500-WRITE-RECORD.                                                        
      ******************                                                        
            PERFORM 410-READ-BP13K800  THRU 410-EXIT.                           
            MOVE F310-REGN-NO         TO K580-NUM-REGN.                         
            MOVE F310-SCH-ACC         TO K580-SCH-ACC.                          
            MOVE F310-DTE-TRANS       TO K580-DTE-ACC-REJ.                      
            MOVE F310-CDE-TRANS       TO K580-NUM-ACC-REJ.                      
            MOVE K800-NUM-NRIC1       TO K580-NUM-NRIC1.                        
            MOVE K800-NUM-NRIC2       TO K580-NUM-NRIC2.                        
            MOVE K800-NUM-NRIC3       TO K580-NUM-NRIC3.                        
            MOVE K800-NUM-NRIC4       TO K580-NUM-NRIC4.                        
                                                                                
            MOVE K800-NUM-NRIC1       TO K580-NUM-NRIC1.                        
                                                                                
            IF K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES AND ZEROS             
               MOVE K800-NUM-NRIC2    TO K580-NUM-NRIC2                         
            ELSE                                                                
               MOVE K800-NUM-NRIC1(2:6) TO WS-NRIC                              
               MOVE WS-K800-NRIC        TO K580-NUM-NRIC2                       
            END-IF.                                                             
                                                                                
            IF K800-NUM-NRIC3 NOT = SPACES AND LOW-VALUES AND ZEROS             
               MOVE K800-NUM-NRIC3    TO K580-NUM-NRIC3                         
            ELSE                                                                
               IF K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES AND ZEROS          
                  MOVE K800-NUM-NRIC2(2:6) TO WS-NRIC                           
               ELSE                                                             
                  MOVE K800-NUM-NRIC1(2:6) TO WS-NRIC                           
               END-IF                                                           
               MOVE WS-K800-NRIC   TO K580-NUM-NRIC3                            
            END-IF.                                                             
                                                                                
            IF K800-NUM-NRIC4 NOT = SPACES AND LOW-VALUES AND ZEROS             
               MOVE K800-NUM-NRIC4    TO K580-NUM-NRIC4                         
            ELSE                                                                
               IF K800-NUM-NRIC3 NOT = SPACES AND LOW-VALUES AND ZEROS          
                  MOVE K800-NUM-NRIC3(2:6) TO WS-NRIC                           
               ELSE                                                             
                  IF K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES AND ZEROS       
                     MOVE K800-NUM-NRIC2(2:6) TO WS-NRIC                        
                  ELSE                                                          
                     MOVE K800-NUM-NRIC1(2:6) TO WS-NRIC                        
                  END-IF                                                        
               END-IF                                                           
               MOVE WS-K800-NRIC   TO K580-NUM-NRIC4                            
            END-IF.                                                             
                                                                                
                                                                                
            MOVE K800-DTE-REGN        TO K580-DTE-REGN.                         
            MOVE K800-DTE-BALLOT      TO K580-DTE-BALLOT.                       
            MOVE K800-NUM-HOUSEHOLD   TO K580-NUM-HOUSEHOLD.                    
            MOVE K800-NUM-ALLO-SCHEME TO K580-NUM-ALLOC-SCHEME.                 
            MOVE K800-NUM-ALLO-CAT    TO K580-NUM-ALLO-CAT.                     
            MOVE K800-NUM-ALLOC-TAG   TO K580-NUM-ALLOC-TAG.                    
            MOVE WS-DATE              TO K580-DTE-UPDATE.                       
            MOVE 'BP13CB35'           TO K580-NUM-USERID.                       
                                                                                
            IF F310-CDE-NT NOT = SPACES AND LOW-VALUES AND ZEROS                
               MOVE F310-CDE-NT       TO K580-NUM-NT                            
            ELSE                                                                
               MOVE K800-NUM-NEW-TOWN TO K580-NUM-NT                            
            END-IF.                                                             
                                                                                
            IF F310-CDE-FLAT-TYPE NOT = SPACES AND LOW-VALUES AND ZEROS         
               MOVE F310-CDE-FLAT-TYPE TO K580-NUM-FLAT-TYPE                    
            ELSE                                                                
               MOVE K800-NUM-FLAT-TYPE TO K580-NUM-FLAT-TYPE                    
            END-IF.                                                             
                                                                                
            WRITE BP13K580-REC.                                                 
            EVALUATE K580-STATUS                                                
            WHEN 00                                                             
            WHEN 02                                                             
              ADD 1 TO WS-K580-WRITE                                            
            WHEN 22                                                             
              ADD 1 TO WS-K580-WRITE                                            
                 DISPLAY 'BP13K580 - DUPLICATE, STATUS: '                       
                         K580-STATUS ',KEY IS ' K580-KEY-FLD                    
                                                                                
            WHEN OTHER                                                          
                 DISPLAY 'BP13K580 - WRITE ERROR, STATUS: '                     
                         K580-STATUS ',KEY IS ' K580-KEY-FLD                    
            END-EVALUATE.                                                       
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
       999-CLOSE-FILES.                                                         
           CLOSE BP13F310                                                       
                 BP13K800                                                       
                 BP13K580.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13CB34 ================*'.              
           DISPLAY '  F310 RECORDS READ    = ' WS-F310-READ.                    
           DISPLAY '  K580 RECORDS WRITTEN = ' WS-K580-WRITE.                   
           DISPLAY '*==========================================*'.              
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K800 - CLOSING ERROR,STAT: ' K800-STATUS             
           END-IF.                                                              
                                                                                
           IF K580-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K580 - CLOSING ERROR,STAT: ' K580-STATUS             
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
        999-EXIT.                                                               
            EXIT.                                                               
                                                                                
