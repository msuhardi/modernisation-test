      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13CD83.                                                 
       AUTHOR.        IRVIN CHING.                                              
       DATE-WRITTEN.  AUG 28, 2013.                                             
      *===============================================================*         
      * OBJECTIVE  :  DETERMINE STEP UP ELIGIBILITY                   *         
      *            :  1) SYSTEM TO READ SMS FOUND AS DIRECT PURCHASE  *         
      *            :     2-ROOM FLAT IN NON-MATURE ESTATE             *         
      *            :     NOW APPLY FOR 3-R00M FLAT IN NON-MATURE EST  *         
      *            :     TAG STEP-UP GRANT = Y                        *         
      *            :  2) SYSTEM TO READ RSL FOUND AS DIRECT PURCHASE  *         
      *            :     2-ROOM FLAT IN NON-MATURE ESTATE             *         
      *            :     NOW APPLY FOR 3-R00M FLAT IN NON-MATURE EST  *         
      *            :     CHECK RESALE APPROVAL DATE WITH DTE-APPLICATN*         
      *            :     TAG STEP-UP GRANT = P.                       *         
      *            :     POLICY IS VALID PERIOD IS 6 MONTH FROM       *         
      *            :     DATE-BOOKING, SELECTION TEAM TO VERIFY THE   *         
      *            :     THE CUT-OFF DATE AND UPDATE                  *         
      *            :     STEP-UP GRANT = Y IF IS WITHIN THE 6 MTH     *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :                                                  *         
      *               1. BP13F730                                     *         
      *               2. BP13K558                                     *         
      *                                                               *         
      * OUTPUT FILE:  1. BP13LD83                                     *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP135059 28/08/2013 IMC1 NEW PROGRAM                          *         
      * BP135190 04/12/2013 SMR2 EXPAND BP13K813 TO 1000              *         
      * BP135340 06/05/2014 SMR2 CATER FOR MODL-DESG-SMS              *         
      *                          USE BP13K800 INSTEAD OF BP13K730     *         
      * BP135458 26/08/2014 PCL4 TO INITIALIZE NRIC WHEN = ##         *         
      * BP135513 29/10/2014 SMR2 APPLICABLE FOR PURE SECOND TIMER HH  *         
      * BP135838 01/07/2015 ESA1 TO CATER FOR MODL-DESIGN = "92"      *         
      * BP135758 16/07/2015 SMR2 REMOVE MODL-DESG-SMS IN RSL          *         
      * BP136838 16/11/2017 FNP1 DO CHECK FOR FLAT TYPE FOR ROF TO    *         
      *                          DETERMINE STEPUP                     *         
      * BP137786 18/04/2019  KV6 CHECK FOR FLAT TYPE 'OBF'            *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F730  ASSIGN        TO BP13F730.                         
           SELECT  BP13LD83  ASSIGN        TO BP13LD83.                         
                                                                                
           SELECT  BP13K558  ASSIGN        TO BP13K558                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K558-KEY-FLD                      
                             FILE STATUS   IS WS-K558-STATUS.                   
                                                                                
           SELECT  BP13K800  ASSIGN        TO BP13K800                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K800-NUM-REGN                     
                             FILE STATUS   IS WS-K800-STATUS.                   
                                                                                
           SELECT  BP13K80A  ASSIGN        TO BP13K80A                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K80A-KEY-FLD                      
                             FILE STATUS   IS WS-K80A-STATUS.                   
                                                                                
           SELECT  BP13K813  ASSIGN        TO BP13K813                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K813-KEY-FLD                      
                             FILE STATUS   IS WS-K813-STATUS.                   
                                                                                
           SELECT   BP13K825 ASSIGN        TO BP13K825                          
                             ORGANIZATION  IS INDEXED                           
                             ACCESS MODE   IS DYNAMIC                           
                             RECORD KEY    IS K825-KEY-FLD                      
                             FILE STATUS   IS WS-K825-STATUS.                   
                                                                                
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F730                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F730.                                                           
                                                                                
       FD   BP13K558                                                            
            RECORD CONTAINS 600 CHARACTERS.                                     
       COPY BP13K558.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K80A                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K80A.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13K825                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K825.                                                           
                                                                                
       FD   BP13LD83                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 133 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01 BP13LD83-REC        PIC X(133).                                       
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
                                                                                
       COPY BP13F23A.                                                           
                                                                                
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F730             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-K558             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-K80A             PIC X(1)  VALUE 'N'.                     
           05  WS-K825-FND             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-K825             PIC X(1)  VALUE 'N'.                     
           05  WS-K800-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K558-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K80A-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K813-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K825-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  CNT-K825-READ           PIC 9(7)  VALUE ZEROES.                  
           05  WS-F730-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-LD83-WRITE           PIC 9(7)  VALUE ZEROES.                  
           05  WS-K558-FND             PIC 9(7)  VALUE ZEROES.                  
           05  WS-K558-NFND            PIC 9(7)  VALUE ZEROES.                  
           05  WS-K800-UPDATE          PIC 9(7)  VALUE ZEROES.                  
           05  WS-SPOUSE-NOTFD         PIC 9(7)  VALUE ZEROES.                  
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-HH-INCOME            PIC 9(5)  VALUE ZEROES.                  
           05  WS-K558-FOUND           PIC X(1)  VALUE 'N'.                     
           05  WS-K558-OKAY            PIC X(1)  VALUE 'N'.                     
           05  WS-K80A-NRIC            PIC X(09) VALUE SPACES.                  
           05  WS-MATURE               PIC X(08) VALUE SPACES.                  
           05  WS-MATURE-REQ           PIC X(08) VALUE SPACES.                  
           05  WS-NUM-REGN             PIC X(08) VALUE SPACES.                  
           05  WS-NUM-HDB-REF          PIC X(11) VALUE SPACES.                  
           05  WS-NUM-SCH-ACC-SMS      PIC X(11) VALUE SPACES.                  
           05  WS-NUM-SCH-ACC-RSL      PIC X(11) VALUE SPACES.                  
           05  WS-FILE-STATUS-SMS      PIC X(2)  VALUE SPACES.                  
           05  WS-FILE-STATUS-RSL      PIC X(3)  VALUE SPACES.                  
           05  WS-SALES-TYPE-SMS       PIC X(2)  VALUE SPACES.                  
           05  WS-NEW-TOWN-SMS         PIC X(3)  VALUE SPACES.                  
           05  WS-NEW-TOWN-RSL         PIC X(3)  VALUE SPACES.                  
           05  WS-CURR-DATE.                                                    
               10 WS-CURR-CCYY         PIC 9(04).                               
               10 WS-CURR-MM           PIC 9(02).                               
               10 WS-CURR-DD           PIC 9(02).                               
           05  WS-APPR-DATE.                                                    
               10 WS-APPR-CCYY         PIC 9(04).                               
               10 WS-APPR-MM           PIC 9(02).                               
               10 WS-APPR-DD           PIC 9(02).                               
           05  WS-COMP-APPROVAL        PIC 9(06) VALUE ZEROES.                  
           05  WS-COMP-CURRENT         PIC 9(06) VALUE ZEROES.                  
           05  WS-COMP-DIFF            PIC 9(06) VALUE ZEROES.                  
      *---------------*                                                         
      * REPORT LAYOUT                                                           
      *---------------*                                                         
       01  BP13LD83-HEADER.                                                     
           05  FILLER                   PIC X(08) VALUE 'REGN'.                 
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  FILLER                   PIC X(09) VALUE 'NRIC1'.                
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  FILLER                   PIC X(09) VALUE 'NRIC2'.                
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  FILLER                   PIC X(09) VALUE 'NRIC3'.                
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  FILLER                   PIC X(09) VALUE 'NRIC4'.                
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  FILLER                   PIC X(03) VALUE 'CAT'.                  
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  FILLER                   PIC X(03) VALUE 'NT'.                   
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  FILLER                   PIC X(02) VALUE 'FT'.                   
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  FILLER                   PIC X(03) VALUE 'HH '.                  
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  FILLER                   PIC X(03) VALUE 'BHH'.                  
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  FILLER                   PIC X(05) VALUE 'HHINC'.                
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  FILLER                   PIC X(03) VALUE 'ALO'.                  
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  FILLER                   PIC X(11) VALUE 'SMS SCH'.              
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  FILLER                   PIC X(02) VALUE 'FT'.                   
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  FILLER                   PIC X(03) VALUE 'NT'.                   
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  FILLER                   PIC X(02) VALUE 'ST'.                   
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  FILLER                   PIC X(11) VALUE 'RSL SCH'.              
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  FILLER                   PIC X(02) VALUE 'FT'.                   
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  FILLER                   PIC X(03) VALUE 'NT'.                   
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  FILLER                   PIC X(03) VALUE 'FS'.                   
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  FILLER                   PIC X(08) VALUE 'APPROVAL'.             
                                                                                
       01  BP13LD83-DETAIL.                                                     
           05  LD83-NUM-REGN            PIC X(08) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  LD83-NUM-NRIC1           PIC X(09) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  LD83-NUM-NRIC2           PIC X(09) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  LD83-NUM-NRIC3           PIC X(09) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  LD83-NUM-NRIC4           PIC X(09) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  LD83-NUM-CATEGORY        PIC X(03) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  LD83-NUM-NEW-TOWN        PIC X(03) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  LD83-NUM-FLAT-TYPE       PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  LD83-NUM-HOUSEHOLD       PIC X(03) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  LD83-NUM-BALLOT-HH       PIC X(03) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  LD83-NUM-HH-INCOME       PIC X(05) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  LD83-NUM-ALLO-SCH        PIC X(03) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  LD83-NUM-SCH-ACC-SMS     PIC X(11) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  LD83-NUM-SMS-FT          PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  LD83-NUM-NEW-TOWN-SMS    PIC X(03) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  LD83-NUM-SALES-TYPE-SMS  PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  LD83-NUM-SCH-ACC-RSL     PIC X(11) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  LD83-NUM-RSL-FT          PIC X(02) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  LD83-NUM-NEW-TOWN-RSL    PIC X(03) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  LD83-NUM-FILE-STATUS-RSL PIC X(03) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  LD83-DTE-APPROVAL        PIC X(08) VALUE SPACES.                 
      *    05  LD83-NUM-PPO-FLAT        PIC X(03) VALUE SPACES.                 
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F730      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F730 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13K813                                                 
                       BP13K80A                                                 
                       BP13K558                                                 
                       BP13K825                                                 
                I-O    BP13K800                                                 
                OUTPUT BP13LD83.                                                
                                                                                
           IF WS-K558-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K558 - ERROR OPENING : ' WS-K558-STATUS              
              MOVE WS-K558-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE         THRU 9999-EXIT                
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR OPENING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE         THRU 9999-EXIT                
           END-IF.                                                              
                                                                                
           IF WS-K80A-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K80A - ERROR OPENING : ' WS-K80A-STATUS              
              MOVE WS-K80A-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE         THRU 9999-EXIT                
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K813 - ERROR OPENING : ' WS-K813-STATUS              
              MOVE WS-K813-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE         THRU 9999-EXIT                
           END-IF.                                                              
                                                                                
           IF WS-K825-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K825 - ERROR OPENING : ' WS-K825-STATUS              
              MOVE WS-K825-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE         THRU 9999-EXIT                
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-CURR-DATE.                     
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F730.                                                      
      ******************************************************************        
                                                                                
           READ BP13F730           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F730                             
                GO                 TO   2000-EXIT.                              
                                                                                
           MOVE F730-NUM-REGN        TO WS-NUM-REGN.                            
           ADD  1                    TO WS-F730-READ.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
           MOVE ZEROES           TO WS-HH-INCOME                                
                                    WS-COMP-APPROVAL                            
                                    WS-COMP-CURRENT                             
                                    WS-COMP-DIFF.                               
           MOVE 'N'              TO WS-K558-FOUND                               
                                    WS-MATURE                                   
                                    WS-MATURE-REQ.                              
           MOVE SPACES           TO WS-NUM-HDB-REF     WS-NEW-TOWN-SMS          
                                    WS-NUM-SCH-ACC-SMS WS-NEW-TOWN-RSL          
                                    WS-SALES-TYPE-SMS                           
                                    WS-NUM-SCH-ACC-RSL                          
                                    WS-FILE-STATUS-SMS                          
                                    WS-FILE-STATUS-RSL.                         
                                                                                
           PERFORM 5700-READ-BP13K813-REQ         THRU 5700-EXIT.               
           PERFORM 4000-STARTBR-BP13K558          THRU 4000-EXIT.               
                                                                                
           PERFORM 4200-START-BP13K825            THRU 4200-EXIT                
           IF WS-K825-FND = 'Y'                                                 
              PERFORM 4250-READNEXT-BP13K825      THRU 4250-EXIT                
                UNTIL WS-EOF-K825 = 'Y'                                         
           END-IF.                                                              
                                                                                
           IF F730-NUM-STEPUP = 'Y' OR 'P'                                      
              PERFORM 4300-READ-BP13K800          THRU 4300-EXIT                
              PERFORM 6000-WRITE-BP13LD83         THRU 6000-EXIT                
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F730             THRU 2000-EXIT.               
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-STARTBR-BP13K558.                                                   
      ******************************************************************        
                                                                                
           MOVE SPACES          TO  BP13K558-REC.                               
           INITIALIZE               BP13K558-REC.                               
                                                                                
           MOVE F730-NUM-REGN     TO  K558-NUM-REGN.                            
                                                                                
           MOVE 'N'               TO  WS-EOF-K558.                              
                                                                                
           START BP13K558 KEY >= K558-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K558-STATUS                                              
           WHEN 00                                                              
                PERFORM 4100-READNEXT-BP13K558 THRU 4100-EXIT                   
                  UNTIL K558-NUM-REGN NOT = F730-NUM-REGN                       
                     OR WS-EOF-K558 = 'Y'                                       
                     OR F730-NUM-STEPUP = 'Y'                                   
           WHEN 10                                                              
           WHEN 23                                                              
                DISPLAY 'BP13K558 RECORD NOT FOUND  = ' K558-KEY-FLD            
           WHEN OTHER                                                           
                DISPLAY 'ERROR START BP13K558, STATUS  '                        
                        WS-K558-STATUS                                          
                        ',  REGN ' K558-NUM-REGN                                
                MOVE WS-K558-STATUS             TO RETURN-CODE                  
                PERFORM 9999-CLOSE-ROUTINE    THRU 9999-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4100-READNEXT-BP13K558.                                                  
      ******************************************************************        
                                                                                
           READ BP13K558 NEXT RECORD                                            
                AT END MOVE 'Y' TO WS-EOF-K558.                                 
                                                                                
           EVALUATE WS-K558-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
                IF K558-NUM-REGN = F730-NUM-REGN                                
                   PERFORM 4150-PROCESS-K558 THRU 4150-EXIT                     
                ELSE                                                            
                   MOVE 'Y'                  TO WS-EOF-K558                     
                END-IF                                                          
           WHEN 10                                                              
           WHEN 23                                                              
                MOVE 'Y'                                 TO WS-EOF-K558         
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K558, STATUS '                       
                        WS-K558-STATUS ',  REGN ' K558-NUM-REGN                 
                MOVE WS-K558-STATUS               TO RETURN-CODE                
                PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4150-PROCESS-K558.                                                       
      ******************************************************************        
                                                                                
           IF K558-NUM-HDB-REF-SMS NOT = SPACES AND LOW-VALUES                  
              MOVE K558-NUM-HDB-REF-SMS      TO WS-NUM-HDB-REF                  
                                                WS-NUM-SCH-ACC-SMS              
              MOVE K558-CDE-SALES-TYPE-SMS   TO WS-SALES-TYPE-SMS               
                                                                                
              PERFORM 5200-READ-BP13K80A     THRU 5200-EXIT                     
              MOVE K80A-NUM-TOWN             TO WS-NEW-TOWN-SMS                 
              MOVE K558-CDE-FILE-STATUS-SMS  TO WS-FILE-STATUS-SMS              
                                                                                
              IF F730-NUM-SMS-FT    = '02' AND                                  
                 F730-CDE-HOUSEHOLD = 'G' AND                                   
                (F730-NUM-FT2T NOT  = 'Y')                                      
                 IF K558-CDE-FILE-STATUS-SMS NOT = 'OM' AND                     
                   (K558-CDE-MODL-DESG-SMS   NOT = '80' AND '89' AND            
                                                   '92') AND                    
                    WS-MATURE-REQ = 'N' AND                                     
                    WS-MATURE     = 'N'                                         
                    IF F730-NUM-ALLO-CAT = 'ROF' OR 'OBF'                       
                       MOVE 'P'                  TO F730-NUM-STEPUP             
                    ELSE                                                        
                       IF (F730-CDE-FLAT-TYPE = '03' OR '3 ')                   
                          MOVE 'Y'                  TO F730-NUM-STEPUP          
                       ELSE                                                     
                          MOVE 'N'                  TO F730-NUM-STEPUP          
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF K558-NUM-HDB-REF-RSL NOT = SPACES AND LOW-VALUES                  
              MOVE K558-NUM-HDB-REF-RSL      TO WS-NUM-HDB-REF                  
                                                WS-NUM-SCH-ACC-RSL              
              PERFORM 5200-READ-BP13K80A     THRU 5200-EXIT                     
              MOVE K80A-NUM-TOWN             TO WS-NEW-TOWN-RSL                 
              MOVE K558-CDE-FILE-STATUS-RSL  TO WS-FILE-STATUS-RSL              
                                                                                
              PERFORM 4160-COMPUTE-RESALE-DTE THRU 4160-EXIT                    
              PERFORM 4170-COMPUTE-DIFF       THRU 4170-EXIT                    
                                                                                
              IF F730-NUM-RSL-FT    = '02' AND                                  
                 F730-CDE-HOUSEHOLD = 'G' AND                                   
                (F730-NUM-FT2T NOT  = 'Y')                                      
                IF K558-CDE-FILE-STATUS-RSL NOT = 'OM' AND                      
                   WS-MATURE-REQ = 'N' AND                                      
                   WS-MATURE = 'N' AND                                          
                   WS-COMP-DIFF <= 6 AND                                        
                  ((F730-NUM-ALLO-CAT = 'ROF' OR 'OBF') OR                      
                   (F730-CDE-FLAT-TYPE = '03' OR '3 '))                         
                   MOVE 'P'                  TO F730-NUM-STEPUP                 
                ELSE                                                            
                   IF (F730-CDE-FLAT-TYPE = '03' OR '3 ')                       
                      MOVE 'N'                  TO F730-NUM-STEPUP              
                   END-IF                                                       
                END-IF                                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       4160-COMPUTE-RESALE-DTE.                                                 
      *===============================================================*         
                                                                                
           IF K558-DTE-RESALE-EFF NOT NUMERIC                                   
              MOVE ZEROES             TO K558-DTE-RESALE-EFF                    
           END-IF.                                                              
                                                                                
           IF F730-DTE-REQUEST    NOT NUMERIC                                   
              MOVE ZEROES             TO F730-DTE-REQUEST                       
           END-IF.                                                              
                                                                                
       4160-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       4170-COMPUTE-DIFF.                                                       
      *===============================================================*         
                                                                                
           MOVE K558-DTE-APPROVAL-RSL      TO WS-APPR-DATE.                     
           COMPUTE WS-COMP-APPROVAL = (WS-APPR-CCYY * 12) + WS-APPR-MM.         
           COMPUTE WS-COMP-CURRENT  = (WS-CURR-CCYY * 12) + WS-CURR-MM.         
           COMPUTE WS-COMP-DIFF = WS-COMP-CURRENT - WS-COMP-APPROVAL.           
                                                                                
       4170-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       4200-START-BP13K825.                                                     
      *===============================================================*         
                                                                                
           MOVE SPACES                            TO BP13K825-REC               
           INITIALIZE                                BP13K825-REC.              
           MOVE 'N'                               TO WS-EOF-K825                
                                                     WS-K825-FND.               
                                                                                
           MOVE F730-NUM-REGN                     TO K825-NUM-REGN              
                                                                                
           START BP13K825 KEY >= K825-KEY-FLD.                                  
                                                                                
           IF WS-K825-STATUS = 00 OR 02                                         
              MOVE 'Y'                            TO WS-K825-FND                
           ELSE                                                                 
              IF WS-K825-STATUS = 23 OR 10                                      
                 MOVE 'N'                         TO WS-K825-FND                
              ELSE                                                              
                 DISPLAY 'BP13K825 - ERROR START : ' WS-K825-STATUS             
                 PERFORM 9999-CLOSE-ROUTINE       THRU 9999-EXIT                
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       4250-READNEXT-BP13K825.                                                  
      *===============================================================*         
                                                                                
           READ BP13K825 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'    TO WS-EOF-K825                                      
                GO          TO 4250-EXIT                                        
           END-READ.                                                            
                                                                                
           IF WS-K825-STATUS = 00 OR 02                                         
              IF K825-NUM-REGN = F730-NUM-REGN                                  
                 MOVE 'Y'                         TO WS-K825-FND                
                                                                                
                 IF K825-AMT-INCOME IS NUMERIC                                  
                    ADD K825-AMT-INCOME           TO WS-HH-INCOME               
                 END-IF                                                         
                                                                                
              ELSE                                                              
                 MOVE 'Y'                         TO WS-EOF-K825                
              END-IF                                                            
           ELSE                                                                 
              IF WS-K825-STATUS = 23 OR 10                                      
                 MOVE 'Y'                         TO WS-EOF-K825                
              ELSE                                                              
                 DISPLAY 'BP13K825 - ERROR READ NEXT : ' WS-K825-STATUS         
                 PERFORM 9999-CLOSE-ROUTINE       THRU 9999-EXIT                
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4250-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4300-READ-BP13K800.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                      TO BP13K800-MASTER.                 
           INITIALIZE                          BP13K800-MASTER.                 
                                                                                
           MOVE F730-NUM-REGN               TO K800-NUM-REGN.                   
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN ZEROES                                                          
              PERFORM 4400-UPDATE-BP13K800  THRU 4400-EXIT                      
                                                                                
           WHEN 23                                                              
              DISPLAY 'NUM-REGN NOT FOUND IN K800 : ' F730-NUM-REGN             
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K800   : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS           TO RETURN-CODE                      
              PERFORM 9999-CLOSE-ROUTINE    THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4400-UPDATE-BP13K800.                                                    
      ******************************************************************        
                                                                                
           MOVE F730-NUM-STEPUP             TO K800-NUM-STEPUP.                 
                                                                                
           REWRITE BP13K800-MASTER.                                             
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                         TO WS-K800-UPDATE                   
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR WRITING TO BP13K800. STATUS IS - '                 
                      WS-K800-STATUS                                            
              MOVE WS-K800-STATUS           TO RETURN-CODE                      
              PERFORM 9999-CLOSE-ROUTINE    THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5200-READ-BP13K80A.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                             TO  BP13K80A-REC.            
           INITIALIZE                                  BP13K80A-REC.            
                                                                                
           MOVE WS-NUM-HDB-REF                    TO  K80A-NUM-SCH-ACC.         
                                                                                
           READ BP13K80A.                                                       
                                                                                
           EVALUATE WS-K80A-STATUS                                              
           WHEN 00                                                              
                PERFORM 5500-DECODE-NT      THRU 5500-EXIT                      
                                                                                
           WHEN 23                                                              
                DISPLAY 'NOT FOUND BP13K80A: ' K80A-KEY-FLD                     
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'READ ERROR BP13K80A, STATUS  '                         
                        WS-K80A-STATUS                                          
                        ',  SCHACC ' K80A-NUM-SCH-ACC                           
                MOVE WS-K80A-STATUS             TO RETURN-CODE                  
                PERFORM 9999-CLOSE-ROUTINE    THRU 9999-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       5500-DECODE-NT.                                                          
      *===============================================================*         
                                                                                
      *    MOVE SPACES                         TO BP13K813-REC.                 
      *    INITIALIZE                             BP13K813-REC.                 
      *                                                                         
      *    PERFORM 5600-READ-BP13K813        THRU 5600-EXIT.                    
                                                                                
           IF K80A-NUM-TOWN = 'SB' OR 'WL' OR 'YS' OR 'BB' OR 'BP' OR           
                             'CCK' OR 'JW' OR 'JE' OR 'SK' OR 'PG' OR           
                              'HG'                                              
              MOVE 'N'          TO WS-MATURE                                    
           ELSE                                                                 
              MOVE 'Y'          TO WS-MATURE                                    
           END-IF.                                                              
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       5600-READ-BP13K813.                                                      
      *===============================================================*         
                                                                                
           MOVE SPACES                          TO BP13K813-REC.                
           INITIALIZE                              BP13K813-REC.                
                                                                                
           MOVE K80A-NUM-TOWN                   TO K813-NUM-ZONE.               
           MOVE K80A-NUM-FLAT-TYPE              TO K813-NUM-FLAT-TYPE.          
           MOVE F730-DTE-BALLOT                 TO K813-DTE-BALLOT.             
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
               WHEN 00                                                          
                    MOVE K813-NUM-MATURE-EST-TAG TO WS-MATURE                   
                                                                                
               WHEN 23                                                          
                    DISPLAY 'NOT FOUND BP13K813: ' K813-KEY-FLD                 
                                                                                
               WHEN OTHER                                                       
                    MOVE WS-K813-STATUS               TO RETURN-CODE            
                    DISPLAY 'ERROR READING BP13K813: ' WS-K813-STATUS           
                            ' KEY : ' K813-KEY-FLD                              
                    PERFORM  9999-CLOSE-ROUTINE THRU 9999-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       5600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       5700-READ-BP13K813-REQ.                                                  
      *===============================================================*         
                                                                                
           MOVE SPACES                          TO BP13K813-REC.                
           INITIALIZE                              BP13K813-REC.                
                                                                                
           MOVE F730-CDE-NT1                    TO K813-NUM-ZONE.               
           MOVE F730-CDE-FLAT-TYPE              TO K813-NUM-FLAT-TYPE.          
           MOVE F730-DTE-BALLOT                 TO K813-DTE-BALLOT.             
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
               WHEN 00                                                          
                    MOVE K813-NUM-MATURE-EST-TAG TO WS-MATURE-REQ               
                                                                                
               WHEN 23                                                          
                    DISPLAY 'NOT FOUND BP13K813: ' K813-KEY-FLD                 
                                                                                
               WHEN OTHER                                                       
                    MOVE WS-K813-STATUS               TO RETURN-CODE            
                    DISPLAY 'ERROR READING BP13K813: ' WS-K813-STATUS           
                            ' KEY : ' K813-KEY-FLD                              
                    PERFORM  9999-CLOSE-ROUTINE THRU 9999-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       5700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6000-WRITE-BP13LD83.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                   TO BP13LD83-REC                        
                                            BP13LD83-DETAIL.                    
           INITIALIZE                       BP13LD83-REC                        
                                            BP13LD83-DETAIL.                    
                                                                                
           MOVE F730-NUM-REGN            TO LD83-NUM-REGN.                      
                                                                                
           IF F730-NUM-NRIC1(1:2) = '##'                                        
              MOVE SPACES                TO F730-NUM-NRIC1                      
           END-IF.                                                              
                                                                                
           IF F730-NUM-NRIC2(1:2) = '##'                                        
              MOVE SPACES                TO F730-NUM-NRIC2                      
           END-IF.                                                              
                                                                                
           IF F730-NUM-NRIC3(1:2) = '##'                                        
              MOVE SPACES                TO F730-NUM-NRIC3                      
           END-IF.                                                              
                                                                                
           IF F730-NUM-NRIC4(1:2) = '##'                                        
              MOVE SPACES                TO F730-NUM-NRIC4                      
           END-IF.                                                              
                                                                                
           MOVE F730-NUM-NRIC1           TO LD83-NUM-NRIC1.                     
           MOVE F730-NUM-NRIC2           TO LD83-NUM-NRIC2.                     
           MOVE F730-NUM-NRIC3           TO LD83-NUM-NRIC3.                     
           MOVE F730-NUM-NRIC4           TO LD83-NUM-NRIC4.                     
           MOVE F730-CDE-CATEGORY        TO LD83-NUM-CATEGORY.                  
           MOVE F730-CDE-FLAT-TYPE       TO LD83-NUM-FLAT-TYPE.                 
           MOVE F730-CDE-NT1             TO LD83-NUM-NEW-TOWN.                  
           MOVE WS-HH-INCOME             TO LD83-NUM-HH-INCOME.                 
           MOVE F730-CDE-HOUSEHOLD       TO LD83-NUM-HOUSEHOLD.                 
           MOVE F730-CDE-ALLOC-SCH       TO LD83-NUM-ALLO-SCH.                  
           MOVE F730-CDE-BALLOT-HOUSEHOLD      TO LD83-NUM-BALLOT-HH.           
           MOVE WS-NUM-SCH-ACC-SMS       TO LD83-NUM-SCH-ACC-SMS.               
           MOVE F730-NUM-SMS-FT          TO LD83-NUM-SMS-FT.                    
           MOVE WS-NEW-TOWN-SMS          TO LD83-NUM-NEW-TOWN-SMS.              
           MOVE WS-SALES-TYPE-SMS        TO LD83-NUM-SALES-TYPE-SMS.            
           MOVE WS-NUM-SCH-ACC-RSL       TO LD83-NUM-SCH-ACC-RSL.               
           MOVE F730-NUM-RSL-FT          TO LD83-NUM-RSL-FT.                    
           MOVE WS-FILE-STATUS-RSL       TO LD83-NUM-FILE-STATUS-RSL.           
           MOVE WS-NEW-TOWN-RSL          TO LD83-NUM-NEW-TOWN-RSL.              
           MOVE WS-APPR-DATE             TO LD83-DTE-APPROVAL.                  
                                                                                
           IF WS-LD83-WRITE = ZEROES                                            
              WRITE BP13LD83-REC       FROM BP13LD83-HEADER                     
           END-IF.                                                              
                                                                                
           WRITE BP13LD83-REC          FROM BP13LD83-DETAIL.                    
           ADD 1                         TO WS-LD83-WRITE.                      
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CD83'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY ' (1) NO OF BP13F730 RECORDS READ............. : '           
                    WS-F730-READ.                                               
           DISPLAY ' (2) NO OF BP13LD83 RECORDS WRITTEN.......... : '           
                    WS-LD83-WRITE.                                              
           DISPLAY ' (3) NO OF BP13K800 RECS UPDATED............. : '           
                    WS-K800-UPDATE.                                             
           DISPLAY ' (4) NO OF BP13K558 RECS FOUND............... : '           
                    WS-K558-FND.                                                
           DISPLAY ' (5) NO OF BP13K558 RECS NOT FOUND........... : '           
                    WS-K558-NFND.                                               
                                                                                
           CLOSE    BP13F730                                                    
                    BP13LD83                                                    
                    BP13K558                                                    
                    BP13K800                                                    
                    BP13K825                                                    
                    BP13K80A.                                                   
                                                                                
           IF WS-K558-STATUS  NOT = 00 AND 97                                   
              DISPLAY 'BP13K558 - ERROR CLOSING : ' WS-K558-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K80A-STATUS  NOT = 00 AND 97                                   
              DISPLAY 'BP13K80A - ERROR CLOSING : ' WS-K80A-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS  NOT = 00 AND 97                                   
              DISPLAY 'BP13K800 - ERROR CLOSING : ' WS-K800-STATUS              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
