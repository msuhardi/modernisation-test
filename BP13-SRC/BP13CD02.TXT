       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD02.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  12/08/2015.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE    :  UPDATE PORT-ELIG-TAG = 'S' BACK TO 'P'     *            
      *                 BEFORE TRANSFERRING TO SOC MASTER          *            
      *                                                            *            
      * INPUT FILE   :  BP13F740  -  PORT-ELIG-TAG = 'S'           *            
      * OUTPUT FILES :  BP13F520  -  PROCESSED CASES               *            
      *                 BP13LD02  -  GENERATED REPORT              *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP136096  SMR2  12082015  NEW PROGRAM                      *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740  ASSIGN       TO BP13F740.                           
           SELECT BP13F520  ASSIGN       TO BP13F520.                           
           SELECT BP13LD02  ASSIGN       TO BP13LD02.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F740                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE   IS F                                               
            LABEL RECORD     IS STANDARD.                                       
       COPY BP13F740.                                                           
                                                                                
       FD   BP13F520                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE   IS F                                               
            LABEL RECORD     IS STANDARD.                                       
       COPY BP13F520.                                                           
                                                                                
       FD   BP13LD02                                                            
            RECORDING MODE   IS F                                               
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   BP13LD02-REC               PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-COUNTERS.                                                         
           05  WS-F740-READ             PIC 9(06)  VALUE ZEROES.                
           05  WS-F740-NOTS             PIC 9(06)  VALUE ZEROES.                
           05  WS-F520-WRITE            PIC 9(06)  VALUE ZEROES.                
           05  WS-LD02-WRITE            PIC 9(06)  VALUE ZEROES.                
           05  WS-LD02-SNO              PIC 9(05)  VALUE ZEROES.                
           05  WS-LD02-PAGE             PIC 9(05)  VALUE ZEROES.                
           05  WS-LD02-LINE             PIC 9(02)  VALUE 60.                    
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F740-EOF              PIC X(01)  VALUE 'N'.                   
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-DATE                  PIC X(08)  VALUE SPACES.                
           05  WS-DATE-EDIT             PIC X(10)  VALUE SPACES.                
                                                                                
      **************************************************************            
      *            BP13LD02 - R E P O R T   L A Y O U T            *            
      **************************************************************            
       01  LD02-HEADING-01.                                                     
           05  FILLER              PIC X(08)     VALUE 'BP13LD02'.              
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDB3'.                  
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(13)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE :'.                
           05  LD02-RUN-DATE       PIC X(10).                                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LD02-PAGE-NO        PIC ZZZZ9 .                                  
                                                                                
       01  LD02-HEADING-02.                                                     
           05  FILLER              PIC X(48)     VALUE SPACES.                  
           05  FILLER              PIC X(36)     VALUE                          
               'LIST OF PORT CASES UPDATED BACK TO P'.                          
                                                                                
       01  LD02-HEADING-03.                                                     
           05  FILLER              PIC X(55)     VALUE SPACES.                  
           05  FILLER              PIC X(12)     VALUE                          
               'OLD      NEW'.                                                  
                                                                                
       01  LD02-HEADING-04.                                                     
           05  FILLER              PIC X(48)     VALUE                          
               '     S/N     REGNO        NRIC          NT      '.              
           05  FILLER              PIC X(20)     VALUE                          
               'FT     PORT     PORT'.                                          
                                                                                
       01  LD02-DETAILS.                                                        
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LD02-S-NO           PIC ZZZZ9     VALUE ZEROES.                  
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LD02-REGN           PIC X(08)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LD02-NRIC           PIC X(09)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LD02-NT             PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LD02-FT             PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LD02-PORT-OLD       PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE SPACES.                  
           05  LD02-PORT-NEW       PIC X(01)     VALUE SPACES.                  
                                                                                
       01  LD02-DASH.                                                           
           05  FILLER              PIC X(132)    VALUE ALL '-'.                 
                                                                                
       01  LD02-BLANK.                                                          
           05  FILLER              PIC X(132)    VALUE SPACES.                  
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F740       THRU 2000-EXIT                      
             UNTIL WS-F740-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F740                                                 
                OUTPUT BP13F520                                                 
                       BP13LD02.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE       TO WS-DATE.                         
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO WS-DATE-EDIT.                          
           MOVE WS-DATE-EDIT                TO LD02-RUN-DATE.                   
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-BP13F740.                                                      
      *-------------------------------------------------------------            
           READ BP13F740                                                        
             AT END                                                             
                MOVE 'Y'                    TO WS-F740-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F740-READ.                    
           PERFORM 3000-PROCESS-BP13F740    THRU 3000-EXIT.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-PROCESS-BP13F740.                                                   
      *-------------------------------------------------------------            
           MOVE BP13F740-REC                TO BP13F520-REC.                    
                                                                                
           IF F740-NUM-PORT-ELIG-TAG = 'S'                                      
              MOVE 'P'                      TO F520-NUM-PORT-ELIG-TAG           
              PERFORM 4000-WRITE-BP13LD02   THRU 4000-EXIT                      
           ELSE                                                                 
              ADD 1                         TO WS-F740-NOTS                     
           END-IF.                                                              
                                                                                
           WRITE BP13F520-REC.                                                  
           ADD 1                            TO WS-F520-WRITE.                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-WRITE-BP13LD02.                                                     
      *-------------------------------------------------------------            
           IF WS-LD02-LINE > 55                                                 
              PERFORM 4100-WRITE-HEAD-LD02  THRU 4100-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 4200-WRITE-DET-LD02      THRU 4200-EXIT.                     
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4100-WRITE-HEAD-LD02.                                                    
      *-------------------------------------------------------------            
           ADD 1                       TO WS-LD02-PAGE.                         
           MOVE WS-LD02-PAGE           TO LD02-PAGE-NO.                         
                                                                                
           WRITE BP13LD02-REC          FROM LD02-HEADING-01 AFTER PAGE.         
           WRITE BP13LD02-REC          FROM LD02-HEADING-02.                    
           WRITE BP13LD02-REC          FROM LD02-BLANK.                         
           WRITE BP13LD02-REC          FROM LD02-HEADING-03.                    
           WRITE BP13LD02-REC          FROM LD02-HEADING-04.                    
           WRITE BP13LD02-REC          FROM LD02-DASH.                          
                                                                                
           MOVE 6                      TO WS-LD02-LINE.                         
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4200-WRITE-DET-LD02.                                                     
      *-------------------------------------------------------------            
           MOVE SPACES                      TO LD02-DETAILS.                    
           INITIALIZE                          LD02-DETAILS.                    
                                                                                
           ADD 1                            TO WS-LD02-SNO                      
                                               WS-LD02-WRITE                    
                                               WS-LD02-LINE.                    
                                                                                
           MOVE WS-LD02-SNO                 TO LD02-S-NO.                       
           MOVE F520-NUM-REGN               TO LD02-REGN.                       
           MOVE F520-NUM-NRIC1              TO LD02-NRIC.                       
           MOVE F520-CDE-NT1                TO LD02-NT.                         
           MOVE F520-CDE-FLAT-TYPE          TO LD02-FT.                         
           MOVE F740-NUM-PORT-ELIG-TAG      TO LD02-PORT-OLD.                   
           MOVE F520-NUM-PORT-ELIG-TAG      TO LD02-PORT-NEW.                   
           WRITE BP13LD02-REC               FROM LD02-DETAILS.                  
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY '  '.                                                        
           DISPLAY '    BP13CD02 CONTROL TOTAL    : ' WS-DATE-EDIT.             
           DISPLAY '  '.                                                        
           DISPLAY ' 1. NO OF BP13F740 READ       : ' WS-F740-READ.             
           DISPLAY ' 2. NO OF BP13F740 PORT NOT S : ' WS-F740-NOTS.             
           DISPLAY ' 3. NO OF BP13F520 WRITTEN    : ' WS-F520-WRITE.            
           DISPLAY ' 4. NO OF BP13LD02 WRITTEN    : ' WS-LD02-WRITE.            
           DISPLAY '  '.                                                        
           DISPLAY ' CHECK '.                                                   
           DISPLAY '       1.  =   2.  +  4. '.                                 
           DISPLAY '       1.  =   3. '.                                        
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13F740                                                       
                 BP13F520                                                       
                 BP13LD02.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
