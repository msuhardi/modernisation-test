       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF97.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  25/09/2013.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE    : REPORT FOR SMS & RSL WHERE NUM-SELLER-RSL   *            
      *                IS 'S' OR 'P'                               *            
      *              : GENERATE REPORT WHEN,                       *            
      *                1. K558-NUM-HDB-REF-SMS IS FOUND &          *            
      *                   K558-CDE-FILE-STATUS-SMS IS 'OM'         *            
      *                2. K558-NUM-HDB-REF-SMS IS NOT FOUND        *            
      *                3. K558-NUM-HDB-REF-RSL IS FOUND &          *            
      *                   K558-CDE-FILE-STATUS-RSL IS NOT 'OM'     *            
      *                                                            *            
      * INPUT  FILES : BP13F730 - CASES PER REGN                   *            
      *                BP13K558 - BP13.K558.UPGOCCP                *            
      * OUTPUT FILE  : BP13LF97 - GENERATED REPORT                 *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP135148  SMR2  25092013  NEW PROGRAM                      *            
      * BP137132  KR13  30072018  ADDED PAGE BREAK FOR REPORT      *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN       TO BP13F730.                            
                                                                                
           SELECT BP13K558 ASSIGN       TO BP13K558                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K558-KEY-FLD                         
                           FILE STATUS  IS WS-K558-STATUS.                      
                                                                                
           SELECT BP13LF97 ASSIGN       TO BP13LF97.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F730                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  500 CHARACTERS                                     
            RECORDING MODE    IS F                                              
            LABEL RECORD      IS STANDARD.                                      
       COPY BP13F730.                                                           
                                                                                
       FD   BP13K558                                                            
            RECORD CONTAINS  600 CHARACTERS.                                    
       COPY BP13K558.                                                           
                                                                                
       FD   BP13LF97                                                            
            RECORDING MODE    IS F                                              
            RECORD CONTAINS  132 CHARACTERS.                                    
       01   BP13LF97-REC                   PIC X(132).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K558-STATUS              PIC 9(02)   VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F730-READ                PIC 9(05)   VALUE ZEROES.            
           05  WS-LF97-SNO                 PIC 9(05)   VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F730-EOF                 PIC X(01)   VALUE 'N'.               
           05  WS-K558-EOF                 PIC X(01)   VALUE 'N'.               
           05  WS-K558-FND                 PIC X(01)   VALUE 'N'.               
           05  WS-OCCP-FND                 PIC X(01)   VALUE 'N'.               
           05  WS-LF97-HDR                 PIC X(01)   VALUE 'N'.               
           05  WS-F730-ELIG                PIC X(01)   VALUE 'N'.               
           05  WS-CHECK-ELIG               PIC X(01)   VALUE 'N'.               
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-CUR-DATE                 PIC X(08)   VALUE SPACES.            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NRIC                     PIC X(09)   VALUE SPACES.            
           05  WS-HA-CDE-STAT              PIC X(01)   VALUE SPACES.            
           05  WS-SP-CDE-STAT              PIC X(01)   VALUE SPACES.            
                                                                                
       01 WS-REPORT-VARS.                                                       
          05 LF97-HEADER1.                                                      
             10 LF97-RPTNAME      PIC X(08)  VALUE SPACES.                      
             10 FILLER            PIC X(05)  VALUE SPACES.                      
             10 FILLER            PIC X(08)  VALUE 'HDBCAT 3'.                  
             10 FILLER            PIC X(39)  VALUE SPACES.                      
             10 FILLER            PIC X(20)  VALUE                              
                'SYSTEM OF COMMITMENT'.                                         
             10 FILLER            PIC X(33)  VALUE SPACES.                      
             10 FILLER            PIC X(07)  VALUE 'DATE : '.                   
             10 LF97-DATE         PIC X(10)  VALUE SPACES.                      
          05 LF97-HEADER2.                                                      
             10 FILLER            PIC X(49)  VALUE SPACES.                      
             10 FILLER            PIC X(42)  VALUE                              
                'REPORT FOR SMS, RSL, TRF, MAM & SLS SCHEME'.                   
          05 LF97-HEADER3.                                                      
             10 FILLER            PIC X(30)  VALUE SPACES.                      
             10 FILLER            PIC X(03)  VALUE 'SMS'.                       
             10 FILLER            PIC X(21)  VALUE SPACES.                      
             10 FILLER            PIC X(03)  VALUE 'RSL'.                       
             10 FILLER            PIC X(26)  VALUE SPACES.                      
             10 FILLER            PIC X(03)  VALUE 'TRF'.                       
             10 FILLER            PIC X(12)  VALUE SPACES.                      
             10 FILLER            PIC X(03)  VALUE 'MAM'.                       
             10 FILLER            PIC X(12)  VALUE SPACES.                      
             10 FILLER            PIC X(03)  VALUE 'SLS'.                       
          05 LF97-HEADER4.                                                      
             10 FILLER            PIC X(52)  VALUE                              
                ' S/NO REG NO   NRIC       REL HDB REF     FS FT SOLD'.         
             10 FILLER            PIC X(53)  VALUE                              
                '  HDB REF     FS  APPROVAL SLR HDB REF     ST HDB REF'.        
             10 FILLER            PIC X(22)  VALUE                              
                '     ST HDB REF     ST'.                                       
          05 LF97-BLANK.                                                        
             10 FILLER            PIC X(132) VALUE SPACES.                      
          05 LF97-RECORD.                                                       
             10 LF97-SNO          PIC X(05)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF97-REGN         PIC X(08)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF97-DETAIL       PIC X(117) VALUE SPACES.                      
          05 LF97-DET-REC.                                                      
             10 LF97-DET-NRIC     PIC X(09)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF97-DET-APPLN    PIC X(01)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF97-DET-REL      PIC X(02)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF97-SMS-HDBREF   PIC X(11)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF97-SMS-FS       PIC X(02)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF97-SMS-DATE     PIC X(08)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF97-RSL-HDBREF   PIC X(11)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF97-RSL-FS       PIC X(03)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF97-RSL-DATE     PIC X(08)  VALUE SPACES.                      
             10 FILLER            PIC X(02)  VALUE SPACES.                      
             10 LF97-RSL-SELLER   PIC X(01)  VALUE SPACES.                      
             10 FILLER            PIC X(02)  VALUE SPACES.                      
             10 LF97-TRF-HDBREF   PIC X(11)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF97-TRF-ST       PIC X(02)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF97-MAM-HDBREF   PIC X(11)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF97-MAM-ST       PIC X(02)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF97-SLS-HDBREF   PIC X(11)  VALUE SPACES.                      
             10 FILLER            PIC X(01)  VALUE SPACES.                      
             10 LF97-SLS-ST       PIC X(02)  VALUE SPACES.                      
             10 FILLER            PIC X(05)  VALUE SPACES.                      
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ********************                                                      
       0000-MAIN-ROUTINE.                                                       
      ********************                                                      
                                                                                
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F730      THRU 2000-EXIT                       
             UNTIL WS-F730-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       1000-OPEN-FILES.                                                         
      ******************                                                        
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13K558                                                 
                OUTPUT BP13LF97.                                                
                                                                                
           IF WS-K558-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K558 FILE ERROR ' WS-K558-STATUS             
              MOVE WS-K558-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-CUR-DATE.                      
           STRING WS-CUR-DATE(7:2), '/',                                        
                  WS-CUR-DATE(5:2), '/',                                        
                  WS-CUR-DATE(1:4) DELIMITED BY SIZE                            
             INTO LF97-DATE.                                                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       2000-READ-BP13F730.                                                      
      *********************                                                     
                                                                                
           READ BP13F730 AT END                                                 
                MOVE 'Y'                   TO WS-F730-EOF                       
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                           TO WS-F730-READ.                     
           PERFORM 3000-VALIDATE-ELIG      THRU 3000-EXIT.                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       3000-VALIDATE-ELIG.                                                      
      *********************                                                     
                                                                                
           MOVE SPACES                     TO WS-HA-CDE-STAT                    
                                              WS-SP-CDE-STAT.                   
           MOVE 'N'                        TO WS-K558-FND                       
                                              WS-F730-ELIG.                     
           MOVE 'Y'                        TO WS-CHECK-ELIG.                    
                                                                                
           PERFORM 6000-STARTBR-K558       THRU 6000-EXIT.                      
                                                                                
           IF ((WS-F730-ELIG = 'Y') AND                                         
              ((WS-HA-CDE-STAT = 'S' AND WS-SP-CDE-STAT = 'P') OR               
               (WS-HA-CDE-STAT = 'P' AND WS-SP-CDE-STAT = 'S')))                
              PERFORM 4000-PROCESS-REPORT  THRU 4000-EXIT                       
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       4000-PROCESS-REPORT.                                                     
      **********************                                                    
                                                                                
           MOVE 'N'                        TO WS-K558-FND                       
                                              WS-CHECK-ELIG.                    
                                                                                
           MOVE '1'                        TO LF97-DET-APPLN.                   
           MOVE F730-NUM-NRIC1             TO WS-NRIC.                          
           PERFORM 5000-READ-BP13K558      THRU 5000-EXIT.                      
           IF WS-K558-FND = 'Y'                                                 
              ADD 1                        TO WS-LF97-SNO                       
              PERFORM 8000-WRITE-BP13LF97  THRU 8000-EXIT                       
           END-IF.                                                              
           PERFORM 5200-CHECK-OTHER-HA     THRU 5200-EXIT.                      
           PERFORM 6000-STARTBR-K558       THRU 6000-EXIT.                      
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       5000-READ-BP13K558.                                                      
      *********************                                                     
                                                                                
           MOVE SPACES                     TO BP13K558-REC.                     
           INITIALIZE                         BP13K558-REC.                     
                                                                                
           MOVE F730-NUM-REGN              TO K558-NUM-REGN.                    
           MOVE WS-NRIC                    TO K558-NUM-NRIC.                    
                                                                                
           READ BP13K558.                                                       
                                                                                
           EVALUATE WS-K558-STATUS                                              
           WHEN ZEROES                                                          
              MOVE 'Y'                     TO WS-K558-FND                       
              PERFORM 5100-MOVE-DETAILS    THRU 5100-EXIT                       
                                                                                
           WHEN 23                                                              
              MOVE 'N'                     TO WS-K558-FND                       
              DISPLAY 'RECORD NOT FOUND IN BP13K558 : ' K558-KEY-FLD            
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING ON BP13K558'                               
              MOVE WS-K558-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       5100-MOVE-DETAILS.                                                       
      ********************                                                      
                                                                                
           MOVE K558-NUM-NRIC              TO LF97-DET-NRIC.                    
           MOVE K558-CDE-RELATIONSHIP      TO LF97-DET-REL.                     
           MOVE K558-NUM-HDB-REF-SMS       TO LF97-SMS-HDBREF.                  
           MOVE K558-CDE-FILE-STATUS-SMS   TO LF97-SMS-FS.                      
           MOVE K558-DTE-FLAT-SOLD-SMS     TO LF97-SMS-DATE.                    
           MOVE K558-NUM-HDB-REF-RSL       TO LF97-RSL-HDBREF.                  
           MOVE K558-CDE-FILE-STATUS-RSL   TO LF97-RSL-FS.                      
           MOVE K558-DTE-APPROVAL-RSL      TO LF97-RSL-DATE.                    
           MOVE K558-NUM-SELLER-RSL        TO LF97-RSL-SELLER.                  
           MOVE K558-NUM-HDB-REF-TR        TO LF97-TRF-HDBREF.                  
           MOVE K558-NUM-SALES-TYPE-TR     TO LF97-TRF-ST.                      
           MOVE K558-NUM-HDB-REF-MAM       TO LF97-MAM-HDBREF.                  
           MOVE K558-NUM-SALES-TYPE-MAM    TO LF97-MAM-ST.                      
           MOVE K558-NUM-HDB-REF-SLS       TO LF97-SLS-HDBREF.                  
           MOVE K558-NUM-SALES-TYPE-SLS    TO LF97-SLS-ST.                      
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       5200-CHECK-OTHER-HA.                                                     
      **********************                                                    
                                                                                
           IF F730-NUM-NRIC2 NOT = SPACES AND LOW-VALUES                        
              IF F730-NUM-NRIC2(1:1) NOT = '#'                                  
                 MOVE '2'                  TO LF97-DET-APPLN                    
                 MOVE F730-NUM-NRIC2       TO WS-NRIC                           
                 PERFORM 5000-READ-BP13K558   THRU 5000-EXIT                    
                 IF WS-K558-FND = 'Y'                                           
                    PERFORM 8000-WRITE-BP13LF97  THRU 8000-EXIT                 
                 END-IF                                                         
              ELSE                                                              
                 GO TO 5200-EXIT                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F730-NUM-NRIC3 NOT = SPACES AND LOW-VALUES                        
              IF F730-NUM-NRIC3(1:1) NOT = '#'                                  
                 MOVE '3'                  TO LF97-DET-APPLN                    
                 MOVE F730-NUM-NRIC3       TO WS-NRIC                           
                 PERFORM 5000-READ-BP13K558   THRU 5000-EXIT                    
                 IF WS-K558-FND = 'Y'                                           
                    PERFORM 8000-WRITE-BP13LF97  THRU 8000-EXIT                 
                 END-IF                                                         
              ELSE                                                              
                 GO TO 5200-EXIT                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F730-NUM-NRIC4 NOT = SPACES AND LOW-VALUES                        
              IF F730-NUM-NRIC4(1:1) NOT = '#'                                  
                 MOVE '4'                  TO LF97-DET-APPLN                    
                 MOVE F730-NUM-NRIC4       TO WS-NRIC                           
                 PERFORM 5000-READ-BP13K558   THRU 5000-EXIT                    
                 IF WS-K558-FND = 'Y'                                           
                    PERFORM 8000-WRITE-BP13LF97  THRU 8000-EXIT                 
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       6000-STARTBR-K558.                                                       
      ********************                                                      
                                                                                
           MOVE SPACES                     TO BP13K558-REC.                     
           INITIALIZE                         BP13K558-REC.                     
                                                                                
           MOVE 'N'                        TO WS-K558-EOF                       
                                              WS-OCCP-FND.                      
                                                                                
           MOVE F730-NUM-REGN              TO K558-NUM-REGN.                    
                                                                                
           START BP13K558 KEY >= K558-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K558-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              PERFORM 6100-READNEXT-K558   THRU 6100-EXIT                       
              IF WS-CHECK-ELIG = 'Y'                                            
                 PERFORM 6300-CHECK-K558   THRU 6300-EXIT                       
                   UNTIL WS-K558-EOF = 'Y'                                      
              ELSE                                                              
                 PERFORM 6200-PROCESS-K558 THRU 6200-EXIT                       
                   UNTIL WS-K558-EOF = 'Y'                                      
              END-IF                                                            
                                                                                
           WHEN 23                                                              
           WHEN 10                                                              
              CONTINUE                                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'START ERROR BP13K558 : ' WS-K558-STATUS                  
                      ' REGN : ' K558-NUM-REGN                                  
              MOVE WS-K558-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       6100-READNEXT-K558.                                                      
      *********************                                                     
                                                                                
           READ BP13K558 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K558-STATUS                                              
           WHEN ZEROES                                                          
              CONTINUE                                                          
                                                                                
           WHEN 23                                                              
           WHEN 10                                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K558 : ' K558-KEY-FLD            
              MOVE 'Y'                     TO WS-K558-EOF                       
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING ON BP13K558'                               
              MOVE WS-K558-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       6200-PROCESS-K558.                                                       
      ********************                                                      
                                                                                
           IF F730-NUM-REGN = K558-NUM-REGN                                     
              IF K558-NUM-NRIC NOT = F730-NUM-NRIC1 AND                         
                                     F730-NUM-NRIC2 AND                         
                                     F730-NUM-NRIC3 AND                         
                                     F730-NUM-NRIC4                             
                 IF WS-OCCP-FND = 'N'                                           
                    MOVE 'O'               TO LF97-DET-APPLN                    
                    MOVE 'Y'               TO WS-OCCP-FND                       
                 ELSE                                                           
                    MOVE SPACES            TO LF97-DET-APPLN                    
                 END-IF                                                         
                 PERFORM 5100-MOVE-DETAILS    THRU 5100-EXIT                    
                 PERFORM 8000-WRITE-BP13LF97  THRU 8000-EXIT                    
              END-IF                                                            
           ELSE                                                                 
              MOVE 'Y'                     TO WS-K558-EOF                       
           END-IF.                                                              
                                                                                
           PERFORM 6100-READNEXT-K558      THRU 6100-EXIT.                      
                                                                                
       6200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       6300-CHECK-K558.                                                         
      ******************                                                        
                                                                                
           IF F730-NUM-REGN = K558-NUM-REGN                                     
              IF K558-NUM-HDB-REF-SMS NOT = SPACES AND LOW-VALUES               
                 IF K558-CDE-FILE-STATUS-SMS = 'OM'                             
                    MOVE 'Y'               TO WS-F730-ELIG                      
                 END-IF                                                         
              ELSE                                                              
                 MOVE 'Y'                  TO WS-F730-ELIG                      
              END-IF                                                            
              IF K558-NUM-HDB-REF-RSL NOT = SPACES AND LOW-VALUES               
                 IF K558-CDE-FILE-STATUS-RSL NOT = 'OM '                        
                    MOVE 'Y'               TO WS-F730-ELIG                      
                 END-IF                                                         
              END-IF                                                            
              IF WS-F730-ELIG = 'Y'                                             
                 IF K558-CDE-RELATIONSHIP = '00'                                
                    MOVE K558-NUM-SELLER-RSL  TO WS-HA-CDE-STAT                 
                 END-IF                                                         
                 IF K558-CDE-RELATIONSHIP = '20'                                
                    MOVE K558-NUM-SELLER-RSL  TO WS-SP-CDE-STAT                 
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              MOVE 'Y'                     TO WS-K558-EOF                       
           END-IF.                                                              
                                                                                
           PERFORM 6100-READNEXT-K558      THRU 6100-EXIT.                      
                                                                                
       6300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       8000-WRITE-BP13LF97.                                                     
      **********************                                                    
                                                                                
           MOVE SPACES                     TO BP13LF97-REC.                     
                                                                                
           IF WS-LF97-HDR = 'N'                                                 
              MOVE 'Y'                     TO WS-LF97-HDR                       
              MOVE 'BP13LF97'              TO LF97-RPTNAME                      
              WRITE BP13LF97-REC           AFTER PAGE                           
              WRITE BP13LF97-REC           FROM LF97-HEADER1                    
              WRITE BP13LF97-REC           FROM LF97-HEADER2                    
              WRITE BP13LF97-REC           FROM LF97-BLANK                      
              WRITE BP13LF97-REC           FROM LF97-BLANK                      
              WRITE BP13LF97-REC           FROM LF97-HEADER3                    
              WRITE BP13LF97-REC           FROM LF97-HEADER4                    
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO LF97-RECORD.                      
           IF LF97-DET-APPLN = '1'                                              
              MOVE WS-LF97-SNO             TO LF97-SNO                          
              MOVE F730-NUM-REGN           TO LF97-REGN                         
              WRITE BP13LF97-REC           FROM LF97-BLANK                      
           ELSE                                                                 
              MOVE SPACES                  TO LF97-SNO                          
                                              LF97-REGN                         
           END-IF.                                                              
           MOVE LF97-DET-REC               TO LF97-DETAIL.                      
           WRITE BP13LF97-REC              FROM LF97-RECORD.                    
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *******************                                                       
       9000-CLOSE-FILES.                                                        
      *******************                                                       
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13CF97 ================*'.              
           DISPLAY '  NO OF F730 RECORDS READ     = ' WS-F730-READ.             
           DISPLAY '  NO OF LF97 RECORDS WRITTEN  = ' WS-LF97-SNO.              
           DISPLAY '*==========================================*'.              
                                                                                
           CLOSE BP13F730                                                       
                 BP13K558                                                       
                 BP13LF97.                                                      
                                                                                
           IF WS-K558-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSING BP13K558 FILE ERROR ' WS-K558-STATUS             
              MOVE WS-K558-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
        9000-EXIT.                                                              
            EXIT.                                                               
                                                                                
