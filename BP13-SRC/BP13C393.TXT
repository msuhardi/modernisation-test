       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C393.                                                 
      *AUTHOR.        N.SURESH.                                                 
      *DATE-WRITTEN.  14/04/2001.                                               
      * ====================================================== *                
      *             SYSTEM OF COMMITMENT (BP13)                *                
      * ====================================================== *                
      *    OBJECTIVE   : TO CREATE FILE BP13F690 FROM SALES    *                
      *                  ORDER TYPE 13 FOR STUDIO APARTMENT ON *                
      *                  AMORTISATION OF DEFER-INCOME.         *                
      *--------------------------------------------------------*                
      * CHG-NO  BY    DATE    DETAILS                          *                
      * ------  --- --------  -------                          *                
      * BP131000 SN   14042001  NEW PGM                        *                
      * BP132292 SK   07022002  CHANGE F690-DTE-SURRENDER TO   *                
      *                         F690-DTE-SURRENDER-REQUEST     *                
      * BP135903 FNP  20102015  CATER FOR FLEXI/LBS            *                
      * BP136220 FNP  15082016  SUBTRACT PREMIUMS FROM CURR    *                
      *                         SELLING PRICE TO GET DEFER INCOME               
      *                         EXPAND F690 TO 600 BYTES       *                
      *                         ADD GRANT AND PREMIUM FIELDS   *                
      * BP137219 FNP  19022018  CATER FOR FSG GRANT            *                
      * BP137363 FNP  13082018  REPLACE BP13KD05 WITH READ FROM TABLE           
      *                         BY CALLING BP13CB4A SUBROUTINE *                
      * BP137471 MRN1 25092018  INCLUDE GRANT AMT FROM EX TABLE*                
      *                         IN THE AMOUNT COMPUTATION      *                
      * BP137700 MRN1 25032019  FIXED ISSUE OF RETRIEVING GRANT*                
      * BP137804 MRN1 02042019  INITIALIZE NUM-NEXT-GRANT      *                
      * BP138083 KR13 18012020  ADDED EHG                      *                
      * BP138560 FP6  29122020  ADDED LHG                      *                
      * BP138944 FP6  14102021  EXPAND F690 FROM 600 TO 800    *                
      *                         ADDED SUG                      *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F120 ASSIGN       TO BP13F120.                            
                                                                                
           SELECT BP13F690 ASSIGN       TO BP13F690.                            
                                                                                
           SELECT BM06K110 ASSIGN       TO BM06K110                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K110-KEY-FLD                         
                           FILE STATUS  IS K110-STATUS.                         
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F120                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORDING MODE F                                                     
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F120.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BP13F690                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORDING MODE F                                                     
           RECORD CONTAINS 800 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F690.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
                                                                                
       01  WS-CURR-TIME.                                                        
           05  WS-HH                      PIC X(2)   VALUE SPACES.              
           05  WS-MM                      PIC X(2)   VALUE SPACES.              
           05  WS-SS                      PIC X(2)   VALUE SPACES.              
           05  WS-MS                      PIC X(2)   VALUE SPACES.              
                                                                                
       01  WS-TIME.                                                             
           05  WS-HH                      PIC X(2)   VALUE SPACES.              
           05  WS-MM                      PIC X(2)   VALUE SPACES.              
           05  WS-SS                      PIC X(2)   VALUE SPACES.              
                                                                                
       01  WS-EDITED-TIME.                                                      
           05  WS-SYS-TIME                PIC X(6)   VALUE SPACES.              
           05  WS-MICRO-SEC               PIC 9      VALUE 0.                   
                                                                                
       01  WS-ADJUST-TIME.                                                      
           05  WS-ADJ-TIME                PIC X(4)   VALUE SPACES.              
           05  WS-ADJ-MICRO-SEC           PIC 999    VALUE 0.                   
                                                                                
       01  WS-SEC-COUNT                   PIC 9      VALUE 0.                   
                                                                                
       01  WS-FILE-STATUS.                                                      
           05 K110-STATUS                 PIC 99     VALUE ZEROS.               
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F120-READ                PIC 9(5)   VALUE ZEROS.               
           05 WS-K110-READ                PIC 9(5)   VALUE ZEROS.               
           05 WS-F690-WRITE               PIC 9(5)   VALUE ZEROS.               
           05 WS-AMT-PREM                 PIC 9(5)   VALUE ZEROS.               
                                                                                
       01  WS-SYS-DATE.                                                         
           05 WS-SYS-CC                PIC 99.                                  
           05 WS-SYS-YY                PIC 99.                                  
           05 WS-SYS-MM                PIC 99.                                  
           05 WS-SYS-DD                PIC 99.                                  
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-AMT-AHG-GRANT-TOT     PIC 9(06)V99 VALUE ZEROES.               
           05 WS-AMT-EHG-GRANT-TOT     PIC 9(06)V99 VALUE ZEROES.               
           05 WS-AMT-LHG-GRANT-TOT     PIC 9(06)V99 VALUE ZEROES.               
           05 WS-AMT-SHG-GRANT-TOT     PIC 9(06)V99 VALUE ZEROES.               
           05 WS-AMT-REG-GRANT-TOT     PIC 9(06)V99 VALUE ZEROES.               
           05 WS-AMT-SRG-GRANT-TOT     PIC 9(06)V99 VALUE ZEROES.               
           05 WS-AMT-FSG-GRANT-TOT     PIC 9(06)V99 VALUE ZEROES.               
           05 WS-AMT-SUG-GRANT-TOT     PIC 9(06)V99 VALUE ZEROES.               
           05 WS-CTR                   PIC 9(02)    VALUE ZEROES.               
           05 WS-CNT                   PIC 9(02)    VALUE ZEROES.               
           05 WS-NO-MORE-ROWS          PIC X(1)  VALUE 'N'.                     
           05 WS-NRIC-CTR              PIC 9(02)    VALUE ZEROES.               
           05 WS-TEMP-STORAGE          OCCURS 10 TIMES.                         
              10 WS-NRIC               PIC X(09)    VALUE SPACES.               
              10 WS-AHG-GRANT          PIC 9(06)V99 VALUE ZEROES.               
              10 WS-AHG-RECOVER        PIC 9(06)V99 VALUE ZEROES.               
              10 WS-EHG-GRANT          PIC 9(06)V99 VALUE ZEROES.               
              10 WS-EHG-RECOVER        PIC 9(06)V99 VALUE ZEROES.               
              10 WS-LHG-GRANT          PIC 9(06)V99 VALUE ZEROES.               
              10 WS-LHG-RECOVER        PIC 9(06)V99 VALUE ZEROES.               
              10 WS-SHG-GRANT          PIC 9(06)V99 VALUE ZEROES.               
              10 WS-SHG-RECOVER        PIC 9(06)V99 VALUE ZEROES.               
              10 WS-REG-GRANT          PIC 9(06)V99 VALUE ZEROES.               
              10 WS-REG-RECOVER        PIC 9(06)V99 VALUE ZEROES.               
              10 WS-SRG-GRANT          PIC 9(06)V99 VALUE ZEROES.               
              10 WS-SRG-RECOVER        PIC 9(06)V99 VALUE ZEROES.               
              10 WS-FSG-GRANT          PIC 9(06)V99 VALUE ZEROES.               
              10 WS-FSG-RECOVER        PIC 9(06)V99 VALUE ZEROES.               
              10 WS-SUG-GRANT          PIC 9(06)V99 VALUE ZEROES.               
              10 WS-SUG-RECOVER        PIC 9(06)V99 VALUE ZEROES.               
                                                                                
       01  WS-PERSON.                                                           
           05  WS-PERSON-DTL               OCCURS 10 TIMES.                     
               10 WS-NUM-NRIC              PIC X(09).                           
               10 WS-AMT-AHG-GRANT         PIC 9(06)V99 VALUE ZEROES.           
               10 WS-AMT-EHG-GRANT         PIC 9(06)V99 VALUE ZEROES.           
               10 WS-AMT-LHG-GRANT         PIC 9(06)V99 VALUE ZEROES.           
               10 WS-AMT-SHG-GRANT         PIC 9(06)V99 VALUE ZEROES.           
               10 WS-AMT-REG-GRANT         PIC 9(06)V99 VALUE ZEROES.           
               10 WS-AMT-SRG-GRANT         PIC 9(06)V99 VALUE ZEROES.           
               10 WS-AMT-FSG-GRANT         PIC 9(06)V99 VALUE ZEROES.           
               10 WS-AMT-SUG-GRANT         PIC 9(06)V99 VALUE ZEROES.           
               10 WS-AMT-AHG-RCVRY         PIC 9(06)V99 VALUE ZEROES.           
               10 WS-AMT-EHG-RCVRY         PIC 9(06)V99 VALUE ZEROES.           
               10 WS-AMT-LHG-RCVRY         PIC 9(06)V99 VALUE ZEROES.           
               10 WS-AMT-SHG-RCVRY         PIC 9(06)V99 VALUE ZEROES.           
               10 WS-AMT-REG-RCVRY         PIC 9(06)V99 VALUE ZEROES.           
               10 WS-AMT-SRG-RCVRY         PIC 9(06)V99 VALUE ZEROES.           
               10 WS-AMT-FSG-RCVRY         PIC 9(06)V99 VALUE ZEROES.           
               10 WS-AMT-SUG-RCVRY         PIC 9(06)V99 VALUE ZEROES.           
                                                                                
       01  WS-CB4A-VARS.                                                        
           05 WS-GRANT-FND            PIC X(01) VALUE 'N'.                      
           05 WS-G                    PIC 9(02) VALUE ZEROES.                   
           05 WS-P                    PIC 9(02) VALUE ZEROES.                   
           05 WS-PRSN                 PIC 9(02) VALUE ZEROES.                   
           05 WS-STORED               PIC X(01) VALUE 'N'.                      
           05 WS-NUM-NEXT-NUM-GRANT   PIC X(06) VALUE SPACES.                   
                                                                                
                                                                                
       01  WS-F120-EOF                   PIC X VALUE 'N'.                       
       01  WS-K110-EOF                   PIC X VALUE 'N'.                       
       01  WS-F690-EOF                   PIC X VALUE 'N'.                       
                                                                                
       01  LINK-REC.                                                            
           05  LINK-NUM-SCH-ACC.                                                
               10  LINK-NUM-SCH             PIC X(4).                           
               10  LINK-NUM-ACC             PIC X(5).                           
                                                                                
       COPY P13COMM8.                                                           
       COPY P13COM73.                                                           
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       000-MAIN-ROUTINE.                                                        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F120      THRU 200-EXIT                             
           PERFORM 300-PROCESS-SA     THRU 300-EXIT                             
                   UNTIL WS-F120-EOF = 'Y'.                                     
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-INITIALIZATION.                                                      
                                                                                
           OPEN INPUT  BP13F120                                                 
                       BM06K110                                                 
                OUTPUT BP13F690.                                                
                                                                                
           IF K110-STATUS NOT = 00 AND 97                                       
              MOVE K110-STATUS          TO RETURN-CODE                          
              DISPLAY 'ERROR OPENING BM06K110 FILE ' K110-STATUS                
              GO TO 999-CLOSE-FILES.                                            
                                                                                
              MOVE FUNCTION CURRENT-DATE(1:8) TO WS-SYS-DATE.                   
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-F120.                                                           
           READ BP13F120 NEXT AT END   MOVE 'Y' TO WS-F120-EOF                  
                GO TO 200-EXIT.                                                 
           ADD  1                       TO WS-F120-READ.                        
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PROCESS-SA.                                                          
                                                                                
           PERFORM 305-READ-K110 THRU 305-EXIT.                                 
           IF K110-STATUS = 00                                                  
                                                                                
              PERFORM 350-GET-PIDB-INFO  THRU 350-EXIT                          
             IF (F120-CDE-TRANS-TYPE = '29') AND                                
                (F120-CDE-PAYMENT-TYPE = 'SA' OR 'VRL')                         
                  PERFORM 400-WRITE-F690     THRU 400-EXIT                      
             END-IF                                                             
           END-IF.                                                              
                                                                                
              PERFORM 200-READ-F120      THRU 200-EXIT.                         
       300-EXIT.                                                                
            EXIT.                                                               
                                                                                
       305-READ-K110.                                                           
           MOVE F120-NUM-SCH-ACCT       TO K110-SCH-ACC-NO.                     
                                                                                
           READ BM06K110 KEY IS K110-KEY-FLD.                                   
                                                                                
           IF K110-STATUS = 00                                                  
              ADD   1                      TO WS-K110-READ                      
           ELSE                                                                 
           IF K110-STATUS = 23                                                  
              MOVE K110-STATUS          TO RETURN-CODE                          
              DISPLAY 'REC NOT FOUND IN BM06K110, ' K110-SCH-ACC-NO             
           ELSE                                                                 
              MOVE K110-STATUS          TO RETURN-CODE                          
              DISPLAY 'ERROR READING BM06K110, ' K110-SCH-ACC-NO                
           END-IF.                                                              
       305-EXIT.                                                                
            EXIT.                                                               
                                                                                
                                                                                
       350-GET-PIDB-INFO.                                                       
              MOVE F120-NUM-SCH-ACCT       TO LINK-NUM-SCH-ACC.                 
              CALL 'BP13C913' USING LINK-REC,                                   
                                 BP13COMM8-REC.                                 
              IF COMM8-CDE-SYSERR NOT = 0                                       
                 MOVE COMM8-CDE-SYSERR     TO RETURN-CODE                       
                 DISPLAY 'ERROR READING PIDB ' COMM8-CDE-SYSERR                 
                 MOVE SPACES TO   COMM8-NUM-LEVEL                               
                                  COMM8-NUM-UNIT-MAIN                           
                                  COMM8-NUM-UNIT-SUB                            
                                  COMM8-NME-STREET                              
                                  COMM8-NUM-POSTAL-CODE                         
                                  COMM8-CDE-CLS-DESG                            
                                  COMM8-CDE-TYP-DESG                            
                                  COMM8-CDE-MODL-DESG                           
                                  COMM8-CDE-CONFIG-DESG                         
                                  COMM8-CDE-CLS-ACTLUSE                         
                                  COMM8-CDE-TYP-ACTLUSE                         
              END-IF.                                                           
                                                                                
       350-EXIT.                                                                
            EXIT.                                                               
                                                                                
       400-WRITE-F690.                                                          
           MOVE SPACES  TO F690-REC.                                            
           INITIALIZE      F690-REC.                                            
                                                                                
      *    MOVE  F120-AMT-RECEIPT        TO F690-AMT-DEFER-INCOME.              
           MOVE  F120-CDE-VALID          TO F690-NUM-AMORTISE-TRANS.            
           MOVE  F120-NUM-PP-SCHEDULE    TO F690-NUM-LEASE-TENURE.              
           MOVE  K110-DTE-KEY-ISSUED     TO F690-DTE-KEY-ISSUE.                 
           MOVE  K110-CURR-SELLING-PRICE TO F690-AMT-SELLING-PRICE.             
           MOVE  K110-SCH-ACC-NO      TO F690-NUM-SCH-ACC.                      
           MOVE  K110-REGN-NO         TO F690-NUM-REGN.                         
           MOVE  K110-ESTATE          TO F690-NUM-ESTATE.                       
           MOVE  K110-NEIGHBOURHOOD   TO F690-NUM-NEIGHBOURHOOD.                
           MOVE  K110-CONTRACT-NO     TO F690-NUM-CONTRACT.                     
           MOVE  K110-BLK-NO          TO F690-NUM-BLOCK.                        
           IF K110-AMT-SINGLES-PREMIUM NOT NUMERIC                              
              MOVE ZEROES             TO K110-AMT-SINGLES-PREMIUM               
           END-IF.                                                              
           IF K110-AMT-SC-SPR-PREMIUM NOT NUMERIC                               
              MOVE ZEROES             TO K110-AMT-SC-SPR-PREMIUM                
           END-IF.                                                              
           IF K110-AMT-ELDERLY-PREMIUM NOT NUMERIC                              
              MOVE ZEROES             TO K110-AMT-ELDERLY-PREMIUM               
           END-IF.                                                              
           COMPUTE WS-AMT-PREM = K110-AMT-SINGLES-PREMIUM +                     
                                 K110-AMT-SC-SPR-PREMIUM +                      
                                 K110-AMT-ELDERLY-PREMIUM.                      
                                                                                
           COMPUTE F690-AMT-DEFER-INCOME = K110-CURR-SELLING-PRICE -            
                                           WS-AMT-PREM.                         
                                                                                
           MOVE  K110-AMT-SINGLES-PREMIUM TO F690-AMT-SINGLES-PREMIUM.          
           MOVE  K110-AMT-SC-SPR-PREMIUM  TO F690-AMT-SC-SPR-PREMIUM.           
           MOVE  K110-AMT-ELDERLY-PREMIUM TO F690-AMT-ELDERLY-PREMIUM.          
           MOVE COMM8-NUM-LEVEL       TO F690-NUM-HSELVL.                       
           MOVE COMM8-NUM-UNIT-MAIN   TO F690-NUM-HSEMAIN.                      
           MOVE COMM8-NUM-UNIT-SUB    TO F690-NUM-HSESUB.                       
           MOVE COMM8-NME-STREET      TO F690-NME-STREET.                       
           MOVE COMM8-NUM-POSTAL-CODE TO F690-NUM-POSTAL.                       
           MOVE COMM8-CDE-CLS-DESG    TO F690-CDE-CLS-DESG.                     
           MOVE COMM8-CDE-TYP-DESG    TO F690-CDE-TYP-DESG.                     
           MOVE COMM8-CDE-MODL-DESG   TO F690-CDE-MODL-DESG.                    
           MOVE COMM8-CDE-CONFIG-DESG TO F690-CDE-CONFIG-DESG.                  
           MOVE COMM8-CDE-CLS-ACTLUSE TO F690-CDE-CLS-ACTUSE.                   
           MOVE COMM8-CDE-TYP-ACTLUSE TO F690-CDE-TYP-ACTUSE.                   
                                                                                
           MOVE ZEROES                TO F690-AMT-AMORT-INCOME-MTH.             
           MOVE ZEROES                TO F690-AMT-AMORT-INCOME-ACCUM.           
           MOVE ZEROES                TO F690-AMT-AMORT-INCOME-LAST.            
           MOVE ZEROES                TO F690-AMT-SURRENDER-BAL.                
           MOVE SPACES                TO F690-DTE-SURRENDER-REQUEST.            
           MOVE ZEROES                TO F690-NUM-AMORT.                        
           MOVE ZEROES                TO F690-AMT-COMPENSATION.                 
           MOVE ZEROES                TO F690-AMT-SURRENDER-COMPUTED.           
           MOVE ZEROES                TO F690-AMT-AMORT-SGP-MTH                 
                                         F690-AMT-AMORT-SGP-ACCUM               
                                         F690-AMT-AMORT-SGP-LAST                
                                         F690-NUM-AMORT-SGP                     
                                         F690-AMT-AMORT-SPR-MTH                 
                                         F690-AMT-AMORT-SPR-ACCUM               
                                         F690-AMT-AMORT-SPR-LAST                
                                         F690-NUM-AMORT-SPR                     
                                         F690-AMT-AMORT-ELD-MTH                 
                                         F690-AMT-AMORT-ELD-ACCUM               
                                         F690-AMT-AMORT-ELD-LAST                
                                         F690-NUM-AMORT-ELD                     
                                         F690-AMT-AMORT-AHG-MTH                 
                                         F690-AMT-AMORT-AHG-ACCUM               
                                         F690-AMT-AMORT-AHG-LAST                
                                         F690-NUM-AMORT-AHG                     
                                         F690-AMT-AMORT-EHG-MTH                 
                                         F690-AMT-AMORT-EHG-ACCUM               
                                         F690-AMT-AMORT-EHG-LAST                
                                         F690-NUM-AMORT-EHG                     
                                         F690-AMT-AMORT-LHG-MTH                 
                                         F690-AMT-AMORT-LHG-ACCUM               
                                         F690-AMT-AMORT-LHG-LAST                
                                         F690-NUM-AMORT-LHG                     
                                         F690-AMT-AMORT-SHG-MTH                 
                                         F690-AMT-AMORT-SHG-ACCUM               
                                         F690-AMT-AMORT-SHG-LAST                
                                         F690-NUM-AMORT-SHG                     
                                         F690-AMT-AMORT-SRG-MTH                 
                                         F690-AMT-AMORT-SRG-ACCUM               
                                         F690-AMT-AMORT-SRG-LAST                
                                         F690-NUM-AMORT-SRG                     
                                         F690-AMT-AMORT-REG-MTH                 
                                         F690-AMT-AMORT-REG-ACCUM               
                                         F690-AMT-AMORT-REG-LAST                
                                         F690-NUM-AMORT-REG                     
                                         F690-AMT-AMORT-SUG-MTH                 
                                         F690-AMT-AMORT-SUG-ACCUM               
                                         F690-AMT-AMORT-SUG-LAST                
                                         F690-NUM-AMORT-SUG                     
                                         F690-AMT-AHG-GRANT                     
                                         F690-AMT-EHG-GRANT                     
                                         F690-AMT-LHG-GRANT                     
                                         F690-AMT-SHG-GRANT                     
                                         F690-AMT-SRG-GRANT                     
                                         F690-AMT-REG-GRANT                     
                                         F690-AMT-FSG-GRANT                     
                                         F690-AMT-SUG-GRANT                     
                                         F690-AMT-AMORT-FSG-MTH                 
                                         F690-AMT-AMORT-FSG-ACCUM               
                                         F690-AMT-AMORT-FSG-LAST                
                                         F690-NUM-AMORT-FSG.                    
                                                                                
           PERFORM 450-RETRIEVE-GRANT-ASMNT THRU 450-EXIT.                      
                                                                                
           IF WS-GRANT-FND = 'Y'                                                
              PERFORM VARYING WS-CTR FROM 1 BY 1                                
                                    UNTIL WS-CTR > WS-NRIC-CTR                  
                 PERFORM 500-COMPUTE-GRANT THRU 500-EXIT                        
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
           WRITE F690-REC.                                                      
                                                                                
           ADD 1 TO WS-F690-WRITE.                                              
       400-EXIT.                                                                
            EXIT.                                                               
                                                                                
      ******************************************************************        
       450-RETRIEVE-GRANT-ASMNT.                                                
      ******************************************************************        
                                                                                
           INITIALIZE COM73-COMMAREA.                                           
           MOVE SPACES TO WS-NUM-NEXT-NUM-GRANT.                                
                                                                                
           PERFORM VARYING WS-CNT FROM 1 BY 1 UNTIL WS-CNT > 10                 
              INITIALIZE WS-PERSON-DTL(WS-CNT)                                  
              INITIALIZE WS-TEMP-STORAGE(WS-CNT)                                
           END-PERFORM.                                                         
                                                                                
           PERFORM 460-CALL-BP13CB4A THRU 460-EXIT.                             
           MOVE ZEROES TO WS-CNT.                                               
                                                                                
           PERFORM 470-PROCESS-GRANT-ASMNT THRU 470-EXIT                        
                   VARYING WS-G FROM 1 BY 1                                     
                   UNTIL WS-G > 5 OR                                            
                      COM73-NUM-GRANT-TYPE(WS-G) = SPACES OR LOW-VALUES.        
                                                                                
           PERFORM UNTIL COM73-NUM-NEXT-NUM-GRANT = SPACES OR LOW-VALUES        
              MOVE COM73-NUM-NEXT-NUM-GRANT TO WS-NUM-NEXT-NUM-GRANT            
              PERFORM 460-CALL-BP13CB4A THRU 460-EXIT                           
              PERFORM 470-PROCESS-GRANT-ASMNT THRU 470-EXIT                     
                      VARYING WS-G FROM 1 BY 1                                  
                      UNTIL WS-G > 5 OR                                         
                      COM73-NUM-GRANT-TYPE(WS-G) = SPACES OR LOW-VALUES         
           END-PERFORM.                                                         
                                                                                
           MOVE ZEROES                  TO WS-NRIC-CTR                          
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1  UNTIL WS-CTR > 10                
               MOVE ZEROES TO         WS-AHG-GRANT(WS-CTR)                      
                                      WS-AHG-RECOVER(WS-CTR)                    
                                      WS-EHG-GRANT(WS-CTR)                      
                                      WS-EHG-GRANT(WS-CTR)                      
                                      WS-LHG-RECOVER(WS-CTR)                    
                                      WS-LHG-RECOVER(WS-CTR)                    
                                      WS-SHG-GRANT(WS-CTR)                      
                                      WS-SHG-RECOVER(WS-CTR)                    
                                      WS-REG-GRANT(WS-CTR)                      
                                      WS-REG-RECOVER(WS-CTR)                    
                                      WS-SRG-GRANT(WS-CTR)                      
                                      WS-SRG-RECOVER(WS-CTR)                    
                                      WS-FSG-GRANT(WS-CTR)                      
                                      WS-FSG-RECOVER(WS-CTR)                    
                                      WS-SUG-RECOVER(WS-CTR)                    
                                      WS-SUG-RECOVER(WS-CTR)                    
               MOVE SPACES TO         WS-NRIC(WS-CTR)                           
           END-PERFORM.                                                         
                                                                                
           MOVE 'N' TO WS-NO-MORE-ROWS.                                         
                                                                                
           PERFORM 480-PROCESS-GRANT THRU 480-EXIT                              
                VARYING WS-CNT FROM 1 BY 1                                      
                UNTIL WS-CNT > 10 OR WS-NO-MORE-ROWS = 'Y'.                     
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       460-CALL-BP13CB4A.                                                       
      ******************************************************************        
           INITIALIZE COM73-COMMAREA.                                           
                                                                                
           MOVE 'A'                       TO COM73-NUM-CALL-ACTION.             
           MOVE F690-NUM-REGN             TO COM73-NUM-RGSTRN-INPUT.            
           MOVE WS-NUM-NEXT-NUM-GRANT     TO COM73-NUM-NEXT-NUM-GRANT.          
                                                                                
           CALL 'BP13CB4A' USING COM73-COMMAREA.                                
                                                                                
           IF COM73-CDE-ERROR = SPACES OR ZEROES                                
              NEXT SENTENCE                                                     
           ELSE                                                                 
              MOVE SPACES                TO COM73-COMMAREA                      
              INITIALIZE                    COM73-COMMAREA                      
           END-IF.                                                              
                                                                                
       460-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       470-PROCESS-GRANT-ASMNT.                                                 
      ******************************************************************        
                                                                                
           MOVE 0 TO WS-CNT.                                                    
                                                                                
            PERFORM VARYING WS-P FROM 1 BY 1 UNTIL WS-P > 10                    
              IF (COM73-IDNTY-NO(WS-G WS-P) NOT = SPACES AND LOW-VALUES)        
                 MOVE 'N' TO WS-STORED                                          
                 PERFORM VARYING WS-PRSN FROM 1 BY 1 UNTIL WS-PRSN > 4          
                                                OR WS-STORED = 'Y'              
                  IF WS-NRIC(WS-PRSN) = SPACES OR                               
                     WS-NRIC(WS-PRSN) = COM73-IDNTY-NO(WS-G WS-P)               
                     MOVE COM73-IDNTY-NO(WS-G WS-P)                             
                                              TO WS-NUM-NRIC(WS-PRSN)           
                     MOVE 'Y' TO WS-STORED                                      
                     EVALUATE COM73-NUM-GRANT-TYPE(WS-G)                        
                        WHEN 'AHG   '                                           
                           ADD COM73-AMT-GRANT(WS-G WS-P)                       
                                         TO WS-AMT-AHG-GRANT(WS-PRSN)           
                           ADD COM73-AMT-EX-GRANT(WS-G WS-P)                    
                                         TO WS-AMT-AHG-GRANT(WS-PRSN)           
                           ADD COM73-AMT-RCVRY(WS-G WS-P)                       
                                         TO WS-AMT-AHG-RCVRY(WS-PRSN)           
                        WHEN 'EHG   '                                           
                           ADD COM73-AMT-GRANT(WS-G WS-P)                       
                                         TO WS-AMT-EHG-GRANT(WS-PRSN)           
                           ADD COM73-AMT-EX-GRANT(WS-G WS-P)                    
                                         TO WS-AMT-EHG-GRANT(WS-PRSN)           
                           ADD COM73-AMT-RCVRY(WS-G WS-P)                       
                                         TO WS-AMT-EHG-RCVRY(WS-PRSN)           
                        WHEN 'LHG   '                                           
                           ADD COM73-AMT-GRANT(WS-G WS-P)                       
                                         TO WS-AMT-LHG-GRANT(WS-PRSN)           
                           ADD COM73-AMT-EX-GRANT(WS-G WS-P)                    
                                         TO WS-AMT-LHG-GRANT(WS-PRSN)           
                           ADD COM73-AMT-RCVRY(WS-G WS-P)                       
                                         TO WS-AMT-LHG-RCVRY(WS-PRSN)           
                        WHEN 'SHG   '                                           
                           ADD COM73-AMT-GRANT(WS-G WS-P)                       
                                         TO WS-AMT-SHG-GRANT(WS-PRSN)           
                           ADD COM73-AMT-EX-GRANT(WS-G WS-P)                    
                                         TO WS-AMT-SHG-GRANT(WS-PRSN)           
                           ADD COM73-AMT-RCVRY(WS-G WS-P)                       
                                         TO WS-AMT-SHG-RCVRY(WS-PRSN)           
                        WHEN 'REG   '                                           
                           ADD COM73-AMT-GRANT(WS-G WS-P)                       
                                         TO WS-AMT-REG-GRANT(WS-PRSN)           
                           ADD COM73-AMT-EX-GRANT(WS-G WS-P)                    
                                         TO WS-AMT-REG-GRANT(WS-PRSN)           
                           ADD COM73-AMT-RCVRY(WS-G WS-P)                       
                                         TO WS-AMT-REG-RCVRY(WS-PRSN)           
                        WHEN 'SRG   '                                           
                           ADD COM73-AMT-GRANT(WS-G WS-P)                       
                                         TO WS-AMT-SRG-GRANT(WS-PRSN)           
                           ADD COM73-AMT-EX-GRANT(WS-G WS-P)                    
                                         TO WS-AMT-SRG-GRANT(WS-PRSN)           
                           ADD COM73-AMT-RCVRY(WS-G WS-P)                       
                                         TO WS-AMT-SRG-RCVRY(WS-PRSN)           
                        WHEN 'FSG   '                                           
                           ADD COM73-AMT-GRANT(WS-G WS-P)                       
                                         TO WS-AMT-FSG-GRANT(WS-PRSN)           
                           ADD COM73-AMT-EX-GRANT(WS-G WS-P)                    
                                         TO WS-AMT-FSG-GRANT(WS-PRSN)           
                           ADD COM73-AMT-RCVRY(WS-G WS-P)                       
                                         TO WS-AMT-FSG-RCVRY(WS-PRSN)           
                        WHEN 'SUG   '                                           
                           ADD COM73-AMT-GRANT(WS-G WS-P)                       
                                         TO WS-AMT-SUG-GRANT(WS-PRSN)           
                           ADD COM73-AMT-EX-GRANT(WS-G WS-P)                    
                                         TO WS-AMT-SUG-GRANT(WS-PRSN)           
                           ADD COM73-AMT-RCVRY(WS-G WS-P)                       
                                         TO WS-AMT-SUG-RCVRY(WS-PRSN)           
                     END-EVALUATE                                               
                  END-IF                                                        
                 END-PERFORM                                                    
              END-IF                                                            
            END-PERFORM.                                                        
                                                                                
       470-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       480-PROCESS-GRANT.                                                       
      ******************************************************************        
           IF WS-NUM-NRIC(WS-CNT) = SPACES OR LOW-VALUES                        
              MOVE 'Y' TO WS-NO-MORE-ROWS                                       
              GO TO 480-EXIT                                                    
           END-IF.                                                              
                                                                                
           MOVE 'Y' TO WS-GRANT-FND                                             
           ADD 1 TO WS-NRIC-CTR                                                 
           PERFORM 490-CHECK-NON-NUMERIC  THRU 490-EXIT                         
                                                                                
           MOVE WS-NUM-NRIC(WS-CNT)  TO WS-NRIC(WS-NRIC-CTR).                   
                                                                                
           MOVE WS-AMT-AHG-GRANT(WS-CNT) TO WS-AHG-GRANT(WS-NRIC-CTR).          
           MOVE WS-AMT-AHG-RCVRY(WS-CNT) TO WS-AHG-RECOVER(WS-NRIC-CTR).        
                                                                                
           MOVE WS-AMT-EHG-GRANT(WS-CNT) TO WS-EHG-GRANT(WS-NRIC-CTR).          
           MOVE WS-AMT-EHG-RCVRY(WS-CNT) TO WS-EHG-RECOVER(WS-NRIC-CTR).        
                                                                                
           MOVE WS-AMT-LHG-GRANT(WS-CNT) TO WS-LHG-GRANT(WS-NRIC-CTR).          
           MOVE WS-AMT-LHG-RCVRY(WS-CNT) TO WS-LHG-RECOVER(WS-NRIC-CTR).        
                                                                                
           MOVE WS-AMT-SHG-GRANT(WS-CNT) TO WS-SHG-GRANT(WS-NRIC-CTR).          
           MOVE WS-AMT-SHG-RCVRY(WS-CNT) TO WS-SHG-RECOVER(WS-NRIC-CTR).        
                                                                                
           MOVE WS-AMT-REG-GRANT(WS-CNT) TO WS-REG-GRANT(WS-NRIC-CTR).          
           MOVE WS-AMT-REG-RCVRY(WS-CNT) TO WS-REG-RECOVER(WS-NRIC-CTR).        
                                                                                
           MOVE WS-AMT-SRG-GRANT(WS-CNT) TO WS-SRG-GRANT(WS-NRIC-CTR).          
           MOVE WS-AMT-SRG-RCVRY(WS-CNT) TO WS-SRG-RECOVER(WS-NRIC-CTR).        
                                                                                
           MOVE WS-AMT-FSG-GRANT(WS-CNT) TO WS-FSG-GRANT(WS-NRIC-CTR).          
           MOVE WS-AMT-FSG-RCVRY(WS-CNT) TO WS-FSG-RECOVER(WS-NRIC-CTR).        
                                                                                
           MOVE WS-AMT-SUG-GRANT(WS-CNT) TO WS-SUG-GRANT(WS-NRIC-CTR).          
           MOVE WS-AMT-SUG-RCVRY(WS-CNT) TO WS-SUG-RECOVER(WS-NRIC-CTR).        
                                                                                
       480-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       490-CHECK-NON-NUMERIC.                                                   
      ******************************************************************        
           IF WS-AMT-AHG-GRANT(WS-CNT) IS NOT NUMERIC                           
              MOVE ZEROES TO WS-AMT-AHG-GRANT(WS-CNT)                           
           END-IF.                                                              
                                                                                
           IF WS-AMT-AHG-RCVRY(WS-CNT) IS NOT NUMERIC                           
              MOVE ZEROES TO WS-AMT-AHG-RCVRY(WS-CNT)                           
           END-IF.                                                              
                                                                                
           IF WS-AMT-EHG-GRANT(WS-CNT) IS NOT NUMERIC                           
              MOVE ZEROES TO WS-AMT-EHG-GRANT(WS-CNT)                           
           END-IF.                                                              
                                                                                
           IF WS-AMT-EHG-RCVRY(WS-CNT) IS NOT NUMERIC                           
              MOVE ZEROES TO WS-AMT-EHG-RCVRY(WS-CNT)                           
           END-IF.                                                              
                                                                                
           IF WS-AMT-LHG-GRANT(WS-CNT) IS NOT NUMERIC                           
              MOVE ZEROES TO WS-AMT-LHG-GRANT(WS-CNT)                           
           END-IF.                                                              
                                                                                
           IF WS-AMT-LHG-RCVRY(WS-CNT) IS NOT NUMERIC                           
              MOVE ZEROES TO WS-AMT-LHG-RCVRY(WS-CNT)                           
           END-IF.                                                              
                                                                                
           IF WS-AMT-SHG-GRANT(WS-CNT) IS NOT NUMERIC                           
              MOVE ZEROES TO WS-AMT-SHG-GRANT(WS-CNT)                           
           END-IF.                                                              
                                                                                
           IF WS-AMT-SHG-RCVRY(WS-CNT) IS NOT NUMERIC                           
              MOVE ZEROES TO WS-AMT-SHG-RCVRY(WS-CNT)                           
           END-IF.                                                              
                                                                                
           IF WS-AMT-REG-GRANT(WS-CNT) IS NOT NUMERIC                           
              MOVE ZEROES TO WS-AMT-REG-GRANT(WS-CNT)                           
           END-IF.                                                              
                                                                                
           IF WS-AMT-REG-RCVRY(WS-CNT) IS NOT NUMERIC                           
              MOVE ZEROES TO WS-AMT-REG-RCVRY(WS-CNT)                           
           END-IF.                                                              
                                                                                
           IF WS-AMT-SRG-GRANT(WS-CNT) IS NOT NUMERIC                           
              MOVE ZEROES TO WS-AMT-SRG-GRANT(WS-CNT)                           
           END-IF.                                                              
                                                                                
           IF WS-AMT-SRG-RCVRY(WS-CNT) IS NOT NUMERIC                           
              MOVE ZEROES TO WS-AMT-SRG-RCVRY(WS-CNT)                           
           END-IF.                                                              
                                                                                
           IF WS-AMT-FSG-GRANT(WS-CNT) IS NOT NUMERIC                           
              MOVE ZEROES TO WS-AMT-FSG-GRANT(WS-CNT)                           
           END-IF.                                                              
                                                                                
           IF WS-AMT-FSG-RCVRY(WS-CNT) IS NOT NUMERIC                           
              MOVE ZEROES TO WS-AMT-FSG-RCVRY(WS-CNT)                           
           END-IF.                                                              
                                                                                
           IF WS-AMT-SUG-GRANT(WS-CNT) IS NOT NUMERIC                           
              MOVE ZEROES TO WS-AMT-SUG-GRANT(WS-CNT)                           
           END-IF.                                                              
                                                                                
           IF WS-AMT-SUG-RCVRY(WS-CNT) IS NOT NUMERIC                           
              MOVE ZEROES TO WS-AMT-SUG-RCVRY(WS-CNT)                           
           END-IF.                                                              
                                                                                
       490-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       500-COMPUTE-GRANT.                                                       
      ******************************************************************        
           MOVE ZEROES            TO WS-AMT-AHG-GRANT-TOT                       
                                     WS-AMT-EHG-GRANT-TOT                       
                                     WS-AMT-LHG-GRANT-TOT                       
                                     WS-AMT-SHG-GRANT-TOT                       
                                     WS-AMT-REG-GRANT-TOT                       
                                     WS-AMT-SRG-GRANT-TOT                       
                                     WS-AMT-FSG-GRANT-TOT                       
                                     WS-AMT-SUG-GRANT-TOT.                      
                                                                                
           IF WS-AHG-GRANT(WS-CTR) NOT = ZEROES                                 
              COMPUTE WS-AMT-AHG-GRANT-TOT = WS-AHG-GRANT(WS-CTR) -             
                                             WS-AHG-RECOVER(WS-CTR)             
           ELSE                                                                 
              MOVE ZEROES TO WS-AMT-AHG-GRANT-TOT                               
           END-IF.                                                              
                                                                                
           IF WS-EHG-GRANT(WS-CTR) NOT = ZEROES                                 
              COMPUTE WS-AMT-EHG-GRANT-TOT = WS-EHG-GRANT(WS-CTR) -             
                                             WS-EHG-RECOVER(WS-CTR)             
           ELSE                                                                 
              MOVE ZEROES TO WS-AMT-EHG-GRANT-TOT                               
           END-IF.                                                              
                                                                                
           IF WS-LHG-GRANT(WS-CTR) NOT = ZEROES                                 
              COMPUTE WS-AMT-LHG-GRANT-TOT = WS-LHG-GRANT(WS-CTR) -             
                                             WS-LHG-RECOVER(WS-CTR)             
           ELSE                                                                 
              MOVE ZEROES TO WS-AMT-LHG-GRANT-TOT                               
           END-IF.                                                              
                                                                                
           IF WS-SHG-GRANT(WS-CTR) NOT = ZEROES                                 
              COMPUTE WS-AMT-SHG-GRANT-TOT = WS-SHG-GRANT(WS-CTR) -             
                                             WS-SHG-RECOVER(WS-CTR)             
           ELSE                                                                 
              MOVE ZEROES TO WS-AMT-SHG-GRANT-TOT                               
           END-IF.                                                              
                                                                                
           IF WS-REG-GRANT(WS-CTR) NOT = ZEROES                                 
              COMPUTE WS-AMT-REG-GRANT-TOT = WS-REG-GRANT(WS-CTR) -             
                                             WS-REG-RECOVER(WS-CTR)             
           ELSE                                                                 
              MOVE ZEROES TO WS-AMT-REG-GRANT-TOT                               
           END-IF.                                                              
                                                                                
           IF WS-SRG-GRANT(WS-CTR) NOT = ZEROES                                 
              COMPUTE WS-AMT-SRG-GRANT-TOT = WS-SRG-GRANT(WS-CTR) -             
                                             WS-SRG-RECOVER(WS-CTR)             
           ELSE                                                                 
              MOVE ZEROES TO WS-AMT-SRG-GRANT-TOT                               
           END-IF.                                                              
                                                                                
           IF WS-FSG-GRANT(WS-CTR) NOT = ZEROES                                 
              COMPUTE WS-AMT-FSG-GRANT-TOT = WS-FSG-GRANT(WS-CTR) -             
                                             WS-FSG-RECOVER(WS-CTR)             
           ELSE                                                                 
              MOVE ZEROES TO WS-AMT-FSG-GRANT-TOT                               
           END-IF.                                                              
                                                                                
           IF WS-SUG-GRANT(WS-CTR) NOT = ZEROES                                 
              COMPUTE WS-AMT-SUG-GRANT-TOT = WS-SUG-GRANT(WS-CTR) -             
                                             WS-SUG-RECOVER(WS-CTR)             
           ELSE                                                                 
              MOVE ZEROES TO WS-AMT-SUG-GRANT-TOT                               
           END-IF.                                                              
                                                                                
           ADD WS-AMT-AHG-GRANT-TOT   TO F690-AMT-AHG-GRANT.                    
           ADD WS-AMT-EHG-GRANT-TOT   TO F690-AMT-EHG-GRANT.                    
           ADD WS-AMT-LHG-GRANT-TOT   TO F690-AMT-LHG-GRANT.                    
           ADD WS-AMT-SHG-GRANT-TOT   TO F690-AMT-SHG-GRANT.                    
           ADD WS-AMT-REG-GRANT-TOT   TO F690-AMT-REG-GRANT.                    
           ADD WS-AMT-SRG-GRANT-TOT   TO F690-AMT-SRG-GRANT.                    
           ADD WS-AMT-FSG-GRANT-TOT   TO F690-AMT-FSG-GRANT.                    
           ADD WS-AMT-SUG-GRANT-TOT   TO F690-AMT-SUG-GRANT.                    
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       999-CLOSE-FILES.                                                         
                                                                                
           CLOSE BP13F120                                                       
                 BM06K110                                                       
                 BP13F690.                                                      
                                                                                
           IF K110-STATUS NOT = 00 AND 97                                       
              MOVE K110-STATUS          TO RETURN-CODE                          
              DISPLAY 'ERROR CLOSING BM06K110 FILE ' K110-STATUS.               
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13C393 ================*'.              
           DISPLAY '    NO. OF F120  READ           = ' WS-F120-READ.           
           DISPLAY '    NO. OF K110  READ           = ' WS-K110-READ.           
           DISPLAY '    NO. OF F690  WRITE          = ' WS-F690-WRITE.          
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
        999-EXIT.                                                               
            EXIT.                                                               
