/*********************************************************************/         
/*   SYSTEM OF COMMITMENT (BP13)                                     */         
/*===================================================================*/         
/* PROGRAM ID       :  BP13S004                                      */         
/*                                                                   */         
/* AUTHOR           :  PJR                                           */         
/*                                                                   */         
/* DATE WRITTEN     :  21/02/96                                      */         
/*                                                                   */         
/* OBJECTIVE        :  TO PRINT REPORT ON UPDATION OF SECONDARY PANEL*/         
/*                     (MONTH WISE)                                  */         
/*                                                                   */         
/* INPUT FILE       :  BP13K878(BP13.K878.SECPANEL.LOG)              */         
/*                                                                   */         
/*                                                                   */         
/* OUTPUT FILE      :  BP13L004                                      */         
/*-------------------------------------------------------------------*/         
/*-------------------------------------------------------------------*/         
/* REVISION HISTORY :                                                */         
/*  DATE CHGE     CHGE REQ NO     BY   DESCRIPTION                   */         
/*  =========     ===========     ==   ===========                   */         
/*  04/03/97      BP130240        PJR  NEW REPORT.                   */         
/*  30/07/01      BP130866        LMS  NEW TEAM.                     */         
/*  03/01/11      BP133869        PCL3 TO CHANGE TO MONTHLY REPORT.  */         
/*********************************************************************/         
%GLOBAL CURRDTE;                                                                
PROC FORMAT;                                                                    
     VALUE $TEAM  '1'='CUS SERV MGER'                                           
                  '2'='REGN & SELN  '                                           
                  '3'='AGREEMENT    '                                           
                  '4'='OTHER UNITS  '                                           
                  '5'='FINANCE DEPT '                                           
                  '6'='PPU          '                                           
                  '7'='ADMIN/SYS SUP'                                           
                  '8'='EC TEAM      '                                           
                  '9'='CUS SERV CTRE'                                           
                  'A'='EN-BLOC RDVMT'                                           
                  'B'='CRDT OPS UNIT'                                           
                  'C'='MGT ACC & BDG'                                           
                  'D'='OTHER UNITS  ';                                          
DATA  SY02F001;                                                                 
     INFILE SY02F001;                                                           
     INPUT @1  CURRCY  $CHAR4.                                                  
           @1    CURRCC  $CHAR2.                                                
           @3    CURRYY  $CHAR2.                                                
           @5    CURRMM  $CHAR2.                                                
           @7    CURRDD  $CHAR2.;                                               
                                                                                
    CALL SYMPUT('CURRDTE',PUT(MDY(CURRMM,01,CURRYY),MONNAME3.)                  
                ||' '||PUT(MDY(CURRMM,01,CURRYY),YEAR4.));                      
                                                                                
                                                                                
OPTIONS NONUMBER NODATE;                                                        
DATA BP13F878;                                                                  
 INFILE BP13F878;                                                               
 INPUT @1    DTEUPDAT  $CHAR8.                                                  
       @19   USERID    $CHAR8.                                                  
       @41   STAFIDBF  $CHAR8.                                                  
       @49   TEAMIDBF  $CHAR1.                                                  
       @50   PANLIDBF  $CHAR4.                                                  
       @54   ACCESSBF  $CHAR1.                                                  
       @71   STAFIDAF  $CHAR8.                                                  
       @79   TEAMIDAF  $CHAR1.                                                  
       @80   PANLIDAF  $CHAR4.                                                  
       @84   ACCESSAF  $CHAR1.;                                                 
                                                                                
       IF STAFIDBF ^= '        ' THEN                                           
          STAFFID=STAFIDBF;                                                     
       ELSE                                                                     
          STAFFID=STAFIDAF;                                                     
                                                                                
  LENGTH K010_KEY $8.;                                                          
  K010_KEY=STAFFID;                                                             
  INFILE SY08K010  VSAM KEY=K010_KEY;                                           
  INPUT @;                                                                      
  RC=_IORC_; FBC=_FDBK_;                                                        
  IF RC = 0 THEN                                                                
     INPUT @12 NAME  $CHAR20.;                                                  
  PROC SORT; BY STAFFID;                                                        
  DATA _NULLS_;                                                                 
SET BP13F878 END=EOF;                                                           
BY  STAFFID;                                                                    
OPTIONS PAGESIZE=58 LINESIZE=132 MISSING=' ';                                   
FILE BP13L004 HEADER=HH NOTITLES;                                               
                                                                                
IF _N_ = 1 THEN                                                                 
   DO;                                                                          
   SET SY02F001;                                                                
   END;                                                                         
                                                                                
     IF DTEUPDAT ^= '      ' THEN                                               
      UPDTDTE = SUBSTR(DTEUPDAT,7,2)||'/'||SUBSTR(DTEUPDAT,5,2)||'/'||          
               SUBSTR(DTEUPDAT,1,4);                                            
     ELSE                                                                       
      UPDTDTE = '     ';                                                        
                                                                                
      IF TEAMIDBF ^= ' ' THEN                                                   
         TEAM_NME = PUT(TEAMIDBF,TEAM.);                                        
      ELSE                                                                      
         TEAM_NME = PUT(TEAMIDAF,TEAM.);                                        
                                                                                
     CTR+1;                                                                     
            PUT  @1   CTR                                                       
                 @8   STAFFID     $CHAR8.                                       
                 @17  NAME        $CHAR20.                                      
                 @38  TEAM_NME    $CHAR14.                                      
                 @57  PANLIDBF    $CHAR4.                                       
                 @67  ACCESSBF    $CHAR1.                                       
                 @77  PANLIDAF    $CHAR4.                                       
                 @87  ACCESSAF    $CHAR1.                                       
                 @97  UPDTDTE     $CHAR10.                                      
                 @117 USERID      $CHAR8.;                                      
    RETURN;                                                                     
HH:                                                                             
   PAGE+1;                                                                      
   RUNDATE=TODAY();                                                             
PUT @1   'BP13L004'                                                             
    @16  'HDB3'                                                                 
    @48  'S Y S T E M   O F   C O M M I T M E N T'                              
    @100 'DATE: ' RUNDATE DDMMYY10.                                             
    @118 'PAGE: ' PAGE;                                                         
PUT @25  'DETAILED REPORT ON UPDATING OF SECOND-LEVEL'                          
    @69  'SECURITY PROFILE IN THE MONTH OF ' "&CURRDTE"                         
    @118 'USER: SU' //;                                                         
   PUT @57 '(BEFORE UPDATE)'                                                    
       @77  '(AFTER UPDATE)';                                                   
   PUT @1  'S/N'                                                                
       @8  'USER-ID'                                                            
       @17 'NAME'                                                               
       @38 'TEAM'                                                               
       @57 'PANEL-ID'                                                           
       @67 'ACCESS'                                                             
       @77 'PANEL-ID'                                                           
       @87 'ACCESS'                                                             
       @97  'UPDATE DATE'                                                       
       @117 'STAFF-ID';                                                         
   PUT @1  '----'                                                               
       @8  '-------'                                                            
       @17 '----'                                                               
       @38 '------'                                                             
       @57 '--------'                                                           
       @67 '------'                                                             
       @77 '--------'                                                           
       @87 '------'                                                             
       @97 '-----------'                                                        
       @117 '--------';                                                         
                                                                                
