       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C76F.                                                 
       AUTHOR.        KARL MAGALONA CABUG.                                      
       DATE-WRITTEN.  20 NOVEMBER 2013.                                         
      *===============================================================*         
      *   OBJECTIVE: TO UPDATE SBF DEMAND/LIMIT FILE                  *         
      *                                                               *         
      *===============================================================*         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *       1. BP13F76F - SEQUENTIAL INPUT GENERATED FROM BP13K76F  *         
      *       2. BP13K769 - BE DEMAND/SUPPLY FILE                     *         
      *                                                               *         
      *     I-O :                                                     *         
      *       1. BP13K76F - SBF DEMAND/LIMIT FILE                     *         
      *                                                               *         
      *===============================================================*         
      * CHG REF   BY     DATE     DESCRIPTION                         *         
      * -------- ---- ---------- -----------------------              *         
      * BP134155 KAM4 20/11/2013 NEW PROGRAM.                         *         
      * BP136768 CCC5 17/05/2017 UPDATE K769 TO LEN 200               *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F76F   ASSIGN  TO BP13F76F.                              
                                                                                
           SELECT  BP13K769   ASSIGN  TO BP13K769                               
                        ORGANIZATION  IS INDEXED                                
                         ACCESS MODE  IS RANDOM                                 
                          RECORD KEY  IS K769-KEY-FLD                           
                         FILE STATUS  IS WS-K769-STATUS.                        
                                                                                
           SELECT  BP13K76F   ASSIGN  TO BP13K76F                               
                        ORGANIZATION  IS INDEXED                                
                         ACCESS MODE  IS RANDOM                                 
                          RECORD KEY  IS K76F-KEY-FLD                           
                         FILE STATUS  IS WS-K76F-STATUS.                        
                                                                                
      **********************                                                    
       DATA DIVISION.                                                           
      **********************                                                    
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F76F                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F76F.                                                           
                                                                                
       FD  BP13K769                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
       COPY BP13K769.                                                           
                                                                                
       FD  BP13K76F                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K76F.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-SWITCHES.                                                         
           05 WS-F76F-EOF                PIC X(01) VALUE 'N'.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F76F-READ               PIC 9(07) VALUE ZEROES.                
           05 WS-K769-NOTFND             PIC 9(07) VALUE ZEROES.                
           05 WS-K76F-REWRITE            PIC 9(07) VALUE ZEROES.                
                                                                                
       01  WS-OTHER-VARIABLES.                                                  
           05 WS-SYSTEM-DATE             PIC 9(08) VALUE ZEROES.                
           05 WS-K769-STATUS             PIC 9(02) VALUE ZEROES.                
           05 WS-K76F-STATUS             PIC 9(02) VALUE ZEROES.                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       0000-MAIN.                                                               
                                                                                
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 1500-READ-BP13F76F      THRU 1500-EXIT.                      
           PERFORM 2000-MAIN-PROCESS       THRU 2000-EXIT                       
             UNTIL WS-F76F-EOF = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       1000-OPEN-FILES.                                                         
      *----------------------------------------------------------------*        
                                                                                
           OPEN INPUT  BP13F76F                                                 
                       BP13K769                                                 
                I-O    BP13K76F.                                                
                                                                                
           IF WS-K769-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K769 - OPEN ERROR, STATUS: ' WS-K769-STATUS          
              MOVE WS-K769-STATUS                       TO RETURN-CODE          
              PERFORM 9999-CLOSE-ROUTINE           THRU 9999-EXIT               
           END-IF.                                                              
                                                                                
           IF WS-K76F-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K76F - OPEN ERROR, STATUS: ' WS-K76F-STATUS          
              MOVE WS-K76F-STATUS                       TO RETURN-CODE          
              PERFORM 9999-CLOSE-ROUTINE           THRU 9999-EXIT               
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       1500-READ-BP13F76F.                                                      
      *----------------------------------------------------------------*        
                                                                                
           READ BP13F76F                                                        
             AT END                                                             
                MOVE 'Y'           TO WS-F76F-EOF                               
                GO   TO 1500-EXIT.                                              
                                                                                
           ADD  1    TO WS-F76F-READ.                                           
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       2000-MAIN-PROCESS.                                                       
      *----------------------------------------------------------------*        
           MOVE SPACES                            TO K769-KEY-FLD.              
           MOVE F76F-NUM-CHOICE                   TO K769-NUM-CHOICE.           
           MOVE F76F-NUM-FT                       TO K769-NUM-FT.               
           PERFORM 2500-READ-BP13K769           THRU 2500-EXIT.                 
           PERFORM 1500-READ-BP13F76F           THRU 1500-EXIT.                 
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       2500-READ-BP13K769.                                                      
      *----------------------------------------------------------------*        
           READ BP13K769.                                                       
                                                                                
           IF WS-K769-STATUS = 00                                               
              IF K769-DTE-BALLOT = F76F-DTE-BALLOT                              
                 MOVE K769-NUM-1ST-TIMER          TO F76F-NUM-DEMAND            
                                                                                
                 IF K769-NUM-1ST-TIMER >=  F76F-NUM-DEMAND-TRIGGER              
                    MOVE 'Y'                      TO F76F-NUM-TRIGGER           
                 END-IF                                                         
                                                                                
                 PERFORM 2800-REWRITE-BP13K76F  THRU 2800-EXIT                  
              ELSE                                                              
                 ADD  1     TO WS-K769-NOTFND                                   
              END-IF                                                            
           ELSE                                                                 
             IF WS-K769-STATUS = 23                                             
                ADD  1     TO WS-K769-NOTFND                                    
             ELSE                                                               
                DISPLAY 'BP13K769 - READ ERROR, STAT:' WS-K769-STATUS           
                PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT                  
             END-IF                                                             
           END-IF.                                                              
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       2800-REWRITE-BP13K76F.                                                   
      *----------------------------------------------------------------*        
           MOVE SPACES                            TO BP13K76F-REC.              
           INITIALIZE                                BP13K76F-REC.              
           MOVE BP13F76F-REC                      TO BP13K76F-REC.              
                                                                                
           REWRITE BP13K76F-REC.                                                
                                                                                
           IF WS-K76F-STATUS = 00                                               
              ADD  1                              TO   WS-K76F-REWRITE          
           ELSE                                                                 
              DISPLAY 'ERROR UPDATING BP13K76F, STAT = ' WS-K76F-STATUS         
              PERFORM 9999-CLOSE-ROUTINE        THRU 9999-EXIT                  
           END-IF.                                                              
                                                                                
       2800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C76F'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F76F READ   : ' WS-F76F-READ.                 
           DISPLAY '(2) NO OF BP13F769 NOTFND : ' WS-K769-NOTFND.               
           DISPLAY '(3) NO OF BP13K76F UPDATED: ' WS-K76F-REWRITE.              
                                                                                
                                                                                
           CLOSE BP13F76F                                                       
                 BP13K769                                                       
                 BP13K76F.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
