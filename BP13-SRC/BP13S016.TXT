  /*******************************************************************/         
  /*                    SYSTEM OF COMMITMENT    (BP13)               */         
  /*=================================================================*/         
  /*                                                                 */         
  /* OBJECTIVES :                                                    */         
  /*     TO PRINT REPORT ON BE        - ALLOCATION OF FLATS BETWEEN  */         
  /*     MCPS AND NON MCPS.                                          */         
  /*-----------------------------------------------------------------*/         
  /* MODIFICATIONS                                                   */         
  /* =============                                                   */         
  /* CHGEREQ    DATE    BY  DESCRIPTION                              */         
  /* -------- -------- ---- ---------------------------------------- */         
  /* BP132360 17062003 ANC1 NEW PROGRAM                              */         
  /* BP132596 24092004 SD10 CHANGE BP13K760 TO BP13K012              */         
  /* BP134769 19032013 ZDD1 CHAHNGE PUNGGOL 21 TO PUNGGOL            */         
  /* BP135173 25042014 YCH2 CHGE DATA MERGE TO DATA MERGE1           */         
  /*******************************************************************/         
  PROC FORMAT;                                                                  
                                                                                
   VALUE $NTFMT  "AMK"="ANG MO KIO       "                                      
                 "BB "="BUKIT BATOK      "                                      
                 "BD "="BEDOK            "                                      
                 "BH "="BISHAN           "                                      
                 "BM "="BUKIT MERAH      "                                      
                 "BP "="BUKIT PANJANG    "                                      
                 "BT "="BUKIT TIMAH      "                                      
                 "CCK"="CHOA CHU KANG    "                                      
                 "CL "="CLEMENTI         "                                      
                 "CT "="CENTRAL          "                                      
                 "CTV"="CHESTERVALE      "                                      
                 "GL "="GEYLANG          "                                      
                 "HG "="HOUGANG          "                                      
                 "JE "="JURONG EAST      "                                      
                 "JK "="JALAN KAYU       "                                      
                 "JW "="JURONG WEST      "                                      
                 "KK "="KANGKAR          "                                      
                 "KWN"="KALLANG/WHAMPOA  "                                      
                 "LCK"="LIM CHU KANG     "                                      
                 "MD "="MANDAI           "                                      
                 "ME "="MAT EST (OTB)    "                                      
                 "MP "="MARINE PARADE    "                                      
                 "PG "="PUNGGOL          "                                      
                 "PGL"="PUNGGOL          "                                      
                 "PR "="PASIR RIS        "                                      
                 "PRC"="PASIR RIS/CHANGI "                                      
                 "QT "="QUEENSTOWN       "                                      
                 "RE "="GEYLANG          "                                      
                 "SB "="SEMBAWANG        "                                      
                 "SGN"="SERANGOON        "                                      
                 "SK "="SENGKANG         "                                      
                 "SP "="SIMPANG          "                                      
                 "STR"="SELETAR          "                                      
                 "TAP"="TAMPINES         "                                      
                 "TP "="TOA PAYOH        "                                      
                 "WIS"="WALK IN SELECTION"                                      
                 "WL "="WOODLANDS        "                                      
                 "YS "="YISHUN           "                                      
                 "ZN "="NON MAT (OTB)    "                                      
                 "00 "="OTHERS (FOR BTO) "                                      
                 ;                                                              
                                                                                
   VALUE $RMFMT  "1" = "1-RM"                                                   
                 "2" = "2-RM"                                                   
                 "3" = "3-RM"                                                   
                 "4" = "4-RM"                                                   
                 "5" = "5-RM"                                                   
                 "6" = "EXEC";                                                  
                                                                                
   %GLOBAL DATE;                                                                
   OPTIONS NODATE;                                                              
                                                                                
   DATA BP13K010;                                                               
   INFILE BP13K010 END=EOF;                                                     
   INPUT  @1     NT       $CHAR3.                                               
          @4     FT       $CHAR2.                                               
          @6     SUPPLY   5.                                                    
          ;                                                                     
                                                                                
   IF _N_ = 1 THEN                                                              
      DO;                                                                       
        CUR_DTE=PUT(TODAY(),DDMMYY10.);                                         
        CALL SYMPUT('DATE',CUR_DTE);                                            
      END;                                                                      
                                                                                
   IF NT ^= " ";                                                                
                                                                                
   IF FT="1" THEN FTCDE="1";                                                    
   IF FT="2" THEN FTCDE="2";                                                    
   IF FT="3" THEN FTCDE="3";                                                    
   IF FT="4" THEN FTCDE="4";                                                    
   IF FT="5" THEN FTCDE="5";                                                    
   IF FT="E" OR FT="6" THEN FTCDE="6";                                          
                                                                                
   DATA BP13K012;                                                               
     INFILE BP13K012 END=EOF;                                                   
     INPUT @1   NT       $CHAR3.                                                
           @4   FT       $CHAR2.                                                
           @11   FTMCPH   5.                                                    
           @32   RGMCPH1  5.                                                    
           @37   RGMCPH2  5.                                                    
           @42   RGMCPH3  5.                                                    
           @48   FTMCPG   5.                                                    
           @69   RGMCPG1  5.                                                    
           @74   RGMCPG2  5.                                                    
           @79   RGMCPG3  5.                                                    
           @85   FTNMCPH  5.                                                    
           @106  RGNMCPH1 5.                                                    
           @111  RGNMCPH2 5.                                                    
           @116  RGNMCPH3 5.                                                    
           @122  FTNMCPG  5.                                                    
           @143  RGNMCPG1 5.                                                    
           @148  RGNMCPG2 5.                                                    
           @153  RGNMCPG3 5.                                                    
           @270  APPMCPH  5.                                                    
           @275  APPMCPG  5.                                                    
           @280  APPNMCPH 5.                                                    
           @285  APPNMCPG 5.                                                    
           ;                                                                    
                                                                                
   IF NT ^= " ";                                                                
                                                                                
   PROC SORT DATA=BP13K010; BY NT FT;                                           
   PROC SORT DATA=BP13K012; BY NT FT;                                           
                                                                                
   DATA MERGE1;                                                                 
   MERGE BP13K010(IN=K010) BP13K012(IN=K012); BY NT FT;                         
   IF K010 AND K012;                                                            
                                                                                
                                                                                
   PROC SORT DATA=MERGE1; BY NT FT;                                             
   RUN;                                                                         
                                                                                
   DATA _NULL_;                                                                 
      SET MERGE1 END=EOF; BY NT FT;                                             
                                                                                
   RETAIN BEFC 0   BEFD 0   AFTC 0   AFTD 0   REGMCPH  0                        
          BEFG 0   BEFH 0   AFTG 0   AFTH 0   REGMCPG  0                        
          BEFK 0   BEFL 0   AFTK 0   AFTL 0   REGNMCPH 0                        
          BEFP 0            AFTP 0            REGNMCPG 0                        
          ;                                                                     
                                                                                
   FILE BP13L016 N=PS LINESLEFT=L PS=60 LS=132 HEADER=HH NOTITLES;              
                                                                                
   IF FIRST.NT THEN                                                             
   DO;                                                                          
      NTCDE  = PUT(NT,$NTFMT.);                                                 
      NT_CTR = 0;                                                               
   END;                                                                         
                                                                                
   NT_CTR + 1;                                                                  
                                                                                
   IF FIRST.FT THEN                                                             
   DO;                                                                          
      FTTYP  = PUT(FTCDE,$RMFMT.);                                              
      FT_CTR = 0;                                                               
      BEFC = 0;                                                                 
      BEFG = 0;                                                                 
      BEFK = 0;                                                                 
      BEFP = 0;                                                                 
      BEFD = 0;                                                                 
      BEFH = 0;                                                                 
      BEFL = 0;                                                                 
      AFTC = 0;                                                                 
      AFTG = 0;                                                                 
      AFTK = 0;                                                                 
      AFTP = 0;                                                                 
      AFTD = 0;                                                                 
      AFTH = 0;                                                                 
      AFTL = 0;                                                                 
      REGMCPH  = 0;                                                             
      REGMCPG  = 0;                                                             
      REGNMCPH = 0;                                                             
      REGNMCPG = 0;                                                             
                                                                                
   END;                                                                         
                                                                                
   FT_CTR + 1;                                                                  
                                                                                
   IF LAST.FT THEN                                                              
   DO;                                                                          
      IF FT_CTR > 0 THEN                                                        
      DO;                                                                       
         REGMCPH = RGMCPH1 + RGMCPH2 + RGMCPH3;                                 
         REGMCPG = RGMCPG1 + RGMCPG2 + RGMCPG3;                                 
         REGNMCPH = RGNMCPH1 + RGNMCPH2 + RGNMCPH3;                             
         REGNMCPG = RGNMCPG1 + RGNMCPG2 + RGNMCPG3;                             
         IF APPMCPH > 0 THEN DO;                                                
            BEFC = FTMCPH / APPMCPH;                                            
         END;                                                                   
         IF APPMCPG > 0 THEN DO;                                                
            BEFG = FTMCPG / APPMCPG;                                            
         END;                                                                   
         IF APPNMCPH > 0 THEN DO;                                               
            BEFK = FTNMCPH / APPNMCPH;                                          
         END;                                                                   
         IF APPNMCPG > 0 THEN DO;                                               
            BEFP = FTNMCPG / APPNMCPG;                                          
         END;                                                                   
                                                                                
         IF BEFP > 0 THEN DO;                                                   
            BEFD = BEFC / BEFP;                                                 
            BEFH = BEFG / BEFP;                                                 
            BEFL = BEFK / BEFP;                                                 
         END;                                                                   
                                                                                
         IF APPMCPH > 0 THEN DO;                                                
            AFTC = REGMCPH / APPMCPH;                                           
         END;                                                                   
         IF APPMCPG > 0 THEN DO;                                                
            AFTG = REGMCPG / APPMCPG;                                           
         END;                                                                   
         IF APPNMCPH > 0 THEN DO;                                               
            AFTK = REGNMCPH / APPNMCPH;                                         
         END;                                                                   
         IF APPNMCPG > 0 THEN DO;                                               
            AFTP = REGNMCPG / APPNMCPG;                                         
         END;                                                                   
                                                                                
         IF AFTP > 0 THEN DO;                                                   
            AFTD = AFTC / AFTP;                                                 
            AFTH = AFTG / AFTP;                                                 
            AFTL = AFTK / AFTP;                                                 
         END;                                                                   
      END;                                                                      
                                                                                
      PUT @1   NTCDE     $CHAR20.                                               
          @22  FTTYP     $CHAR4.                                                
          @27  SUPPLY    4.                                                     
          @32  'BEFORE BALLOTING'                                               
          @49  APPMCPH   4.                                                     
          @54  FTMCPH    4.                                                     
          @59  BEFC      5.3                                                    
          @65  BEFD      5.1                                                    
          @71  APPMCPG   4.                                                     
          @76  FTMCPG    4.                                                     
          @81  BEFG      5.3                                                    
          @87  BEFH      5.1                                                    
          @93  APPNMCPH  4.                                                     
          @98  FTNMCPH   4.                                                     
          @103 BEFK      5.3                                                    
          @109 BEFL      5.1                                                    
          @115 APPNMCPG  4.                                                     
          @120 FTNMCPG   4.                                                     
          @125 BEFP      5.3                                                    
          ;                                                                     
                                                                                
      PUT @32  'AFTER  BALLOTING'                                               
          @49  APPMCPH   4.                                                     
          @54  REGMCPH   4.                                                     
          @59  AFTC      5.3                                                    
          @65  AFTD      5.1                                                    
          @71  APPMCPG   4.                                                     
          @76  REGMCPG   4.                                                     
          @81  AFTG      5.3                                                    
          @87  AFTH      5.1                                                    
          @93  APPNMCPH  4.                                                     
          @98  REGNMCPH  4.                                                     
          @103 AFTK      5.3                                                    
          @109 AFTL      5.1                                                    
          @115 APPNMCPG  4.                                                     
          @120 REGNMCPG  4.                                                     
          @125 AFTP      5.3                                                    
          ;                                                                     
                                                                                
   END;                                                                         
                                                                                
   IF LAST.NT THEN                                                              
   DO;                                                                          
     PUT @1   '--------------------'                                            
         @22  '----'                                                            
         @27  '----'                                                            
         @32  '----------------'                                                
         @49  '----'                                                            
         @54  '----'                                                            
         @59  '-----'                                                           
         @65  '-----'                                                           
         @71  '----'                                                            
         @76  '----'                                                            
         @81  '-----'                                                           
         @87  '-----'                                                           
         @93  '----'                                                            
         @98  '----'                                                            
         @103 '-----'                                                           
         @109 '-----'                                                           
         @115 '----'                                                            
         @120 '----'                                                            
         @125 '-----'                                                           
         ;                                                                      
                                                                                
   END;                                                                         
                                                                                
   RETURN;                                                                      
                                                                                
   HH:                                                                          
     FORMAT NTCDE  $NTFMT.                                                      
            FTTYP  $RMFMT.;                                                     
     PAGE+1;                                                                    
     CUR_DATE=TODAY();                                                          
                                                                                
     PUT @001 'BP13L016'                                                        
         @040 'S Y S T E M   O F   C O M M I T M E N T'                         
         @104 'DATE : ' CUR_DATE DDMMYY10.                                      
         @123 'PAGE : ' PAGE;                                                   
                                                                                
     PUT @021 'REPORT ON BALLOTING EXERCISE-ALLOCATION'                         
         @061 'OF FLATS BETWEEN MCPS & NON MCPS';                               
                                                                                
     PUT //;                                                                    
                                                                                
     PUT @52   '(1) MCPS'                                                       
         @74   '(2) MCPS'                                                       
         @96   '(3) NON-MCPS'                                                   
         @115  '(4) NON-MCPS'                                                   
         ;                                                                      
                                                                                
     PUT @52   '(FIRST-TIMER)'                                                  
         @74   '(SECOND-TIMER)'                                                 
         @96   '(FIRST-TIMER)'                                                  
         @115  '(SECOND-TIMER)'                                                 
         ;                                                                      
                                                                                
     PUT @22   'FLAT'                                                           
         @27   'NO OF'                                                          
         @34   'ALLOCATION'                                                     
         @49   '  A  '                                                          
         @54   '  B  '                                                          
         @59   '  C  '                                                          
         @65   '  D  '                                                          
         @71   '  E  '                                                          
         @76   '  F  '                                                          
         @81   '  G  '                                                          
         @87   '  H  '                                                          
         @93   '  I  '                                                          
         @98   '  J  '                                                          
         @103  '  K  '                                                          
         @109  '  L  '                                                          
         @115  '  M  '                                                          
         @120  '  N  '                                                          
         @125  '  P  '                                                          
         ;                                                                      
                                                                                
                                                                                
     PUT @5    'ESTATE'                                                         
         @22   'TYPE'                                                           
         @27   'UNIT'                                                           
         @34   'PROPORTION'                                                     
         @49   'APPL'                                                           
         @54   'ALLO'                                                           
         @60   'B/A'                                                            
         @66   'C/P'                                                            
         @71   'APPL'                                                           
         @76   'ALLO'                                                           
         @82   'F/E'                                                            
         @88   'G/P'                                                            
         @93   'APPL'                                                           
         @98   'ALLO'                                                           
         @104  'J/I'                                                            
         @110  'K/P'                                                            
         @115  'APPL'                                                           
         @120  'ALLO'                                                           
         @126  'N/M'                                                            
         ;                                                                      
                                                                                
     PUT @1   '--------------------'                                            
         @22  '----'                                                            
         @27  '----'                                                            
         @32  '----------------'                                                
         @49  '----'                                                            
         @54  '----'                                                            
         @59  '-----'                                                           
         @65  '-----'                                                           
         @71  '----'                                                            
         @76  '----'                                                            
         @81  '-----'                                                           
         @87  '-----'                                                           
         @93  '----'                                                            
         @98  '----'                                                            
         @103 '-----'                                                           
         @109 '-----'                                                           
         @115 '----'                                                            
         @120 '----'                                                            
         @125 '-----'                                                           
         ;                                                                      
                                                                                
   RETURN;                                                                      
                                                                                
