      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13CH3A.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  23 SEP 2016.                                              
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : COMPUTE K249 FOR ELDERLY SA                 *           
      *                 CREATE NEW K249 AS INPUT FOR CH03           *           
      *                 (FOR SBF - SALE OF BALANCE FLATS)           *           
      *   INPUT FILES :                                             *           
      *   1.  BP13F249                                              *           
      *                                                             *           
      *   INPUT/OUTPUT:                                             *           
      *   1.  BP13K249                                              *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP136467 23/09/2016 FNP1 NEW PROGRAM                        *           
      * BP139545 31/10/2023 SC54 POLICY CHANGE STARTING FROM 202310 *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F249 ASSIGN       TO BP13F249.                            
                                                                                
           SELECT BP13K249 ASSIGN       TO BP13K249                             
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K249-KEY-FLD                         
                           FILE STATUS  IS WS-K249-STATUS                       
                           ACCESS MODE  IS DYNAMIC.                             
                                                                                
           SELECT P13K249A ASSIGN       TO P13K249A                             
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS WS-K249-KEY-FLD                      
                           FILE STATUS  IS WS-K249A-STATUS                      
                           ACCESS MODE  IS DYNAMIC.                             
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F249                                                            
            RECORD CONTAINS 450 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13F249.                                                           
                                                                                
       FD   BP13K249                                                            
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K249.                                                           
                                                                                
       FD   P13K249A                                                            
            RECORD CONTAINS 450 CHARACTERS.                                     
       01   P13K249A-REC.                                                       
           05  WS-K249-KEY-FLD.                                                 
               10  WS-K249-NUM-NEW-TOWN PIC X(20).                              
               10  WS-K249-NUM-FLAT-TYPE PIC X(09).                             
               10  WS-K249-NUM-SELECTION PIC X(03).                             
               10  WS-K249-DTE-BALLOT   PIC X(06).                              
               10  WS-K249-NUM-BONUS    PIC X(01).                              
           05  FILLER                   PIC X(01).                              
           05  WS-K249-NUM-TOTAL        PIC 9(04).                              
           05  WS-K249-NUM-AVAIL-CAT1   PIC 9(04).                              
           05  WS-K249-NUM-AVAIL-CAT2   PIC 9(04).                              
           05  WS-K249-NUM-AVAIL-CAT3   PIC 9(04).                              
           05  WS-K249-NUM-AVAIL-TOTAL  PIC 9(04).                              
           05  WS-K249-FILE-ORIG        PIC X(08).                              
           05  WS-K249-CAT1-AVAIL       PIC X(01).                              
           05  WS-K249-CAT2-AVAIL       PIC X(01).                              
           05  WS-K249-CAT3-AVAIL       PIC X(01).                              
           05  WS-K249-NUM-LIMIT        PIC 9(03).                              
           05  WS-K249-PANEL-ID         PIC X(04).                              
           05  WS-K249-DTE-UPDATE       PIC X(08).                              
           05  WS-K249-TME-UPDATE       PIC X(07).                              
           05  WS-K249-NUM-USERID       PIC X(08).                              
           05  WS-K249-ACCEPTANCE-INFO.                                         
               10  WS-K249-NUM-UNIT-CAT1    PIC 9(04).                          
               10  WS-K249-NUM-UNIT-CAT2    PIC 9(04).                          
               10  WS-K249-NUM-UNIT-CAT3    PIC 9(04).                          
               10  WS-K249-DTE-UPDATE-ACCEPT PIC X(08).                         
               10  WS-K249-TME-UPDATE-ACCEPT PIC X(07).                         
               10  WS-K249-NUM-USERID-ACCEPT PIC X(08).                         
               10  WS-K249-NUM-PANEL-ACCEPT PIC X(04).                          
           05  WS-K249-ALT-KEY1.                                                
               10  WS-K249-NUM-SELECTION1 PIC X(03).                            
               10  WS-K249-DTE-BALLOT1  PIC X(06).                              
               10  WS-K249-NUM-NEW-TOWN1 PIC X(20).                             
               10  WS-K249-NUM-FLAT-TYPE1 PIC X(09).                            
           05  WS-K249-NUM-ZONE         PIC X(10).                              
           05  WS-K249-CDE-NT           PIC X(3).                               
           05  FILLER                   PIC X(39).                              
           05  WS-K249-NUM-AVAIL-SPR    PIC 9(04).                              
           05  WS-K249-SPR-AVAIL        PIC X(01).                              
           05  WS-K249-NUM-UNIT-SPR     PIC 9(04).                              
           05  WS-K249-NUM-BTO-ZONE     PIC X(3).                               
           05  WS-K249-NUM-TOTAL-GEN3   PIC 9(04).                              
           05  WS-K249-NUM-UNIT-CAT1-NOGEN3 PIC 9(04).                          
           05  WS-K249-NUM-UNIT-CAT2-NOGEN3 PIC 9(04).                          
           05  WS-K249-NUM-UNIT-CAT3-NOGEN3 PIC 9(04).                          
           05  WS-K249-NME-PROJECT      PIC X(180).                             
           05  FILLER                   PIC X(12).                              
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY         PIC 9999.                                    
           05  WS-CUR-MM           PIC 99.                                      
           05  WS-CUR-DD           PIC 99.                                      
                                                                                
       01  WS-K249-STATUS           PIC 9(02)  VALUE ZEROES.                    
       01  WS-K249A-STATUS          PIC 9(02)  VALUE ZEROES.                    
       01  WS-K249-FND              PIC X      VALUE SPACES.                    
       01  WS-F249-EOF              PIC X      VALUE SPACES.                    
       01  WS-F249-READ-CNT         PIC 9(08)  VALUE ZEROES.                    
       01  WS-K249-WRITE-CNT        PIC 9(08)  VALUE ZEROES.                    
       01  WS-K249-2R-CNT           PIC 9(08)  VALUE ZEROES.                    
       01  WS-F249-SA-CNT           PIC 9(08)  VALUE ZEROES.                    
       01  WS-AVAIL-TOT             PIC 9(04)  VALUE ZEROES.                    
       01  WS-AVAIL-TOTAL           PIC 9(04)  VALUE ZEROES.                    
       01  WS-AVAIL-CAT             PIC 9(03)  VALUE ZEROES.                    
       01  WS-AVAIL-CAT1            PIC 9(03)  VALUE ZEROES.                    
       01  WS-AVAIL-CAT2            PIC 9(03)  VALUE ZEROES.                    
       01  WS-AVAIL-CAT3            PIC 9(03)  VALUE ZEROES.                    
       01  WS-NUM-TOTAL             PIC 9(04)  VALUE ZEROES.                    
       01  WS-NUM-TOTAL-GEN3        PIC 9(04)  VALUE ZEROES.                    
       01  WS-NUM-UNIT-CAT1         PIC 9(04)  VALUE ZEROES.                    
       01  WS-NUM-UNIT-CAT2         PIC 9(04)  VALUE ZEROES.                    
       01  WS-NUM-UNIT-CAT3         PIC 9(04)  VALUE ZEROES.                    
       01  WS-NUM-LIMIT             PIC 9(03)V99 VALUE ZEROES.                  
       01  WS-NUM-LIMIT-REDEF REDEFINES WS-NUM-LIMIT.                           
           05  WS-NUM-LIMIT-UNIT    PIC 9(03).                                  
           05  WS-NUM-LIMIT-DECIMAL PIC 9(02).                                  
                                                                                
       01  WS-SA-VARIABLES.                                                     
          05  WS-AVAIL-CAT1-SA         PIC 9(03)  VALUE ZEROES.                 
          05  WS-AVAIL-CAT2-SA         PIC 9(03)  VALUE ZEROES.                 
          05  WS-AVAIL-CAT3-SA         PIC 9(03)  VALUE ZEROES.                 
          05  WS-NUM-TOTAL-GEN3-SA     PIC 9(04)  VALUE ZEROES.                 
          05  WS-NUM-UNIT-CAT1-SA      PIC 9(04)  VALUE ZEROES.                 
          05  WS-NUM-UNIT-CAT2-SA      PIC 9(04)  VALUE ZEROES.                 
          05  WS-NUM-UNIT-CAT3-SA      PIC 9(04)  VALUE ZEROES.                 
          05  WS-UNIT-CAT1-NOGEN3-SA   PIC 9(04)  VALUE ZEROES.                 
          05  WS-UNIT-CAT2-NOGEN3-SA   PIC 9(04)  VALUE ZEROES.                 
          05  WS-UNIT-CAT3-NOGEN3-SA   PIC 9(04)  VALUE ZEROES.                 
          05  WS-NUM-LIMIT-SA          PIC 9(03)  VALUE ZEROES.                 
          05  WS-NUM-TOTAL-SA          PIC 9(04)  VALUE ZEROES.                 
                                                                                
       01  WS-2R-VARIABLES.                                                     
          05  WS-AVAIL-CAT1-2R         PIC 9(03)  VALUE ZEROES.                 
          05  WS-AVAIL-CAT2-2R         PIC 9(03)  VALUE ZEROES.                 
          05  WS-AVAIL-CAT3-2R         PIC 9(03)  VALUE ZEROES.                 
          05  WS-NUM-TOTAL-GEN3-2R     PIC 9(04)  VALUE ZEROES.                 
          05  WS-NUM-UNIT-CAT1-2R      PIC 9(04)  VALUE ZEROES.                 
          05  WS-NUM-UNIT-CAT2-2R      PIC 9(04)  VALUE ZEROES.                 
          05  WS-NUM-UNIT-CAT3-2R      PIC 9(04)  VALUE ZEROES.                 
          05  WS-UNIT-CAT1-NOGEN3-2R   PIC 9(04)  VALUE ZEROES.                 
          05  WS-UNIT-CAT2-NOGEN3-2R   PIC 9(04)  VALUE ZEROES.                 
          05  WS-UNIT-CAT3-NOGEN3-2R   PIC 9(04)  VALUE ZEROES.                 
          05  WS-NUM-LIMIT-2R          PIC 9(03)  VALUE ZEROES.                 
          05  WS-NUM-TOTAL-2R          PIC 9(04)  VALUE ZEROES.                 
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-PRE-PROCESS      THRU 1000-EXIT.                        
           PERFORM 2000-READ-F249        THRU 2000-EXIT.                        
           PERFORM 3000-PROCESS-REC      THRU 3000-EXIT                         
                   UNTIL WS-F249-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-PRE-PROCESS.                                                        
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13F249                                                 
                       BP13K249                                                 
                I-O    P13K249A.                                                
                                                                                
           IF WS-K249-STATUS NOT EQUAL ZEROES AND 97                            
              DISPLAY 'BP13K249 - OPEN ERROR: ' WS-K249-STATUS                  
              MOVE WS-K249-STATUS     TO   RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF WS-K249A-STATUS NOT EQUAL ZEROES AND 97                           
              DISPLAY 'P13K249A- OPEN ERROR: ' WS-K249A-STATUS                  
              MOVE WS-K249A-STATUS     TO   RETURN-CODE                         
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-DATE.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-F249.                                                          
      *---------------*                                                         
                                                                                
           READ BP13F249 AT END                                                 
                MOVE 'Y' TO WS-F249-EOF                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F249-READ-CNT.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
           MOVE ZEROES          TO WS-2R-VARIABLES                              
                                   WS-SA-VARIABLES                              
                                   WS-NUM-LIMIT.                                
                                                                                
           IF F249-NUM-SELECTION = 'SBF' AND                                    
              F249-NUM-FLAT-TYPE = 'SA       '                                  
              ADD 1 TO WS-F249-SA-CNT                                           
              PERFORM 3200-MOVE-F249-DTLS   THRU 3200-EXIT                      
              MOVE SPACES        TO K249-KEY-FLD                                
              MOVE F249-KEY-FLD  TO K249-KEY-FLD                                
              MOVE '2-ROOM   '   TO K249-NUM-FLAT-TYPE                          
              PERFORM 3500-READ-BP13K249       THRU 3500-EXIT                   
              IF WS-K249-FND = 'Y'                                              
                 PERFORM 4000-ADD-UNIT         THRU 4000-EXIT                   
              ELSE                                                              
                 MOVE BP13F249-REC      TO P13K249A-REC                         
                 MOVE '2-ROOM   '       TO WS-K249-NUM-FLAT-TYPE                
                 PERFORM 3600-WRITE-P13K249A      THRU 3600-EXIT                
              END-IF                                                            
              MOVE BP13F249-REC      TO P13K249A-REC                            
              PERFORM 3600-WRITE-P13K249A      THRU 3600-EXIT                   
           ELSE                                                                 
              IF F249-NUM-SELECTION = 'SBF' AND                                 
                 F249-NUM-FLAT-TYPE = '2-ROOM   '                               
                 MOVE BP13F249-REC      TO P13K249A-REC                         
                 PERFORM 3600-WRITE-P13K249A      THRU 3600-EXIT                
                                                                                
                 MOVE BP13F249-REC      TO P13K249A-REC                         
                 MOVE 'SA       '       TO WS-K249-NUM-FLAT-TYPE                
                 PERFORM 3600-WRITE-P13K249A      THRU 3600-EXIT                
              ELSE                                                              
                 MOVE BP13F249-REC      TO P13K249A-REC                         
                 PERFORM 3600-WRITE-P13K249A      THRU 3600-EXIT                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F249              THRU 2000-EXIT.                  
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3200-MOVE-F249-DTLS.                                                     
      *---------------------*                                                   
           MOVE F249-NUM-AVAIL-CAT1  TO WS-AVAIL-CAT1-SA.                       
           MOVE F249-NUM-AVAIL-CAT2  TO WS-AVAIL-CAT2-SA.                       
           MOVE F249-NUM-AVAIL-CAT3  TO WS-AVAIL-CAT3-SA.                       
           MOVE F249-NUM-LIMIT       TO WS-NUM-LIMIT-SA.                        
           MOVE F249-NUM-UNIT-CAT1   TO WS-NUM-UNIT-CAT1-SA.                    
           MOVE F249-NUM-UNIT-CAT2   TO WS-NUM-UNIT-CAT2-SA.                    
           MOVE F249-NUM-UNIT-CAT3   TO WS-NUM-UNIT-CAT3-SA.                    
           MOVE F249-NUM-TOTAL-GEN3  TO WS-NUM-TOTAL-GEN3-SA.                   
           MOVE F249-NUM-UNIT-CAT1-NOGEN3 TO WS-UNIT-CAT1-NOGEN3-SA.            
           MOVE F249-NUM-UNIT-CAT2-NOGEN3 TO WS-UNIT-CAT2-NOGEN3-SA.            
           MOVE F249-NUM-UNIT-CAT3-NOGEN3 TO WS-UNIT-CAT3-NOGEN3-SA.            
           MOVE F249-NUM-TOTAL       TO WS-NUM-TOTAL-SA.                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3500-READ-BP13K249.                                                      
      *---------------------*                                                   
           READ BP13K249.                                                       
                                                                                
           IF WS-K249-STATUS = 00                                               
              ADD 1 TO WS-K249-2R-CNT                                           
              MOVE 'Y' TO WS-K249-FND                                           
              PERFORM 3700-MOVE-K249-DTLS  THRU 3700-EXIT                       
           ELSE                                                                 
              IF WS-K249-STATUS = 23                                            
                 MOVE 'N' TO WS-K249-FND                                        
                 DISPLAY 'RECORD NOT FOUND IN BP13K249, KEY: '                  
                       K249-KEY-FLD                                             
              ELSE                                                              
                 DISPLAY 'ERROR WRITING BP13K249, KEY: '                        
                       K249-KEY-FLD ' , STATUS: ' WS-K249-STATUS                
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3600-WRITE-P13K249A.                                                     
      *---------------------*                                                   
                                                                                
           WRITE P13K249A-REC.                                                  
                                                                                
           IF WS-K249A-STATUS = 00 OR 02                                        
              ADD 1 TO WS-K249-WRITE-CNT                                        
           ELSE                                                                 
              DISPLAY 'ERROR WRITING BP13K249, KEY: '                           
                    WS-K249-KEY-FLD ' , STATUS: ' WS-K249A-STATUS               
           END-IF.                                                              
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3700-MOVE-K249-DTLS.                                                     
      *-----------------*                                                       
           MOVE K249-NUM-AVAIL-CAT1  TO WS-AVAIL-CAT1-2R.                       
           MOVE K249-NUM-AVAIL-CAT2  TO WS-AVAIL-CAT2-2R.                       
           MOVE K249-NUM-AVAIL-CAT3  TO WS-AVAIL-CAT3-2R.                       
           MOVE K249-NUM-LIMIT       TO WS-NUM-LIMIT-2R.                        
           MOVE K249-NUM-UNIT-CAT1   TO WS-NUM-UNIT-CAT1-2R.                    
           MOVE K249-NUM-UNIT-CAT2   TO WS-NUM-UNIT-CAT2-2R.                    
           MOVE K249-NUM-UNIT-CAT3   TO WS-NUM-UNIT-CAT3-2R.                    
           MOVE K249-NUM-TOTAL-GEN3  TO WS-NUM-TOTAL-GEN3-2R.                   
           MOVE K249-NUM-UNIT-CAT1-NOGEN3 TO WS-UNIT-CAT1-NOGEN3-2R.            
           MOVE K249-NUM-UNIT-CAT2-NOGEN3 TO WS-UNIT-CAT2-NOGEN3-2R.            
           MOVE K249-NUM-UNIT-CAT3-NOGEN3 TO WS-UNIT-CAT3-NOGEN3-2R.            
           MOVE K249-NUM-TOTAL       TO WS-NUM-TOTAL-2R.                        
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       4000-ADD-UNIT.                                                           
      *-----------------*                                                       
           COMPUTE F249-NUM-AVAIL-CAT1 = WS-AVAIL-CAT1-2R +                     
                                         WS-AVAIL-CAT1-SA.                      
                                                                                
           COMPUTE F249-NUM-AVAIL-CAT2 = WS-AVAIL-CAT2-2R +                     
                                         WS-AVAIL-CAT2-SA.                      
                                                                                
           COMPUTE F249-NUM-AVAIL-CAT3 = WS-AVAIL-CAT3-2R +                     
                                         WS-AVAIL-CAT3-SA.                      
                                                                                
           COMPUTE F249-NUM-TOTAL = WS-NUM-TOTAL-2R +                           
                                    WS-NUM-TOTAL-SA.                            
                                                                                
           COMPUTE F249-NUM-TOTAL-GEN3 = WS-NUM-TOTAL-GEN3-2R +                 
                                         WS-NUM-TOTAL-GEN3-SA.                  
                                                                                
           COMPUTE F249-NUM-UNIT-CAT1 = WS-NUM-UNIT-CAT1-2R +                   
                                        WS-NUM-UNIT-CAT1-SA.                    
                                                                                
           COMPUTE F249-NUM-UNIT-CAT2 = WS-NUM-UNIT-CAT2-2R +                   
                                        WS-NUM-UNIT-CAT2-SA.                    
                                                                                
           COMPUTE F249-NUM-UNIT-CAT3 = WS-NUM-UNIT-CAT3-2R +                   
                                        WS-NUM-UNIT-CAT3-SA.                    
                                                                                
           COMPUTE F249-NUM-UNIT-CAT1-NOGEN3 = WS-UNIT-CAT1-NOGEN3-2R +         
                                               WS-UNIT-CAT1-NOGEN3-SA.          
                                                                                
           COMPUTE F249-NUM-UNIT-CAT2-NOGEN3 = WS-UNIT-CAT2-NOGEN3-2R +         
                                               WS-UNIT-CAT2-NOGEN3-SA.          
                                                                                
           COMPUTE F249-NUM-UNIT-CAT3-NOGEN3 = WS-UNIT-CAT3-NOGEN3-2R +         
                                               WS-UNIT-CAT3-NOGEN3-SA.          
                                                                                
      ** CR - BP139545 START                                                    
           IF F249-DTE-BALLOT < '202310'                                        
                                                                                
              COMPUTE WS-NUM-LIMIT =                                            
                    ((F249-NUM-TOTAL  - F249-NUM-TOTAL-GEN3) * 0.05)            
                                                                                
              IF WS-NUM-LIMIT-DECIMAL > 0                                       
                 ADD 1                  TO   WS-NUM-LIMIT                       
              END-IF                                                            
                                                                                
              IF WS-NUM-LIMIT < 10                                              
                 MOVE 10                TO   F249-NUM-LIMIT                     
              ELSE                                                              
                 MOVE WS-NUM-LIMIT      TO   F249-NUM-LIMIT                     
              END-IF                                                            
                                                                                
           ELSE                                                                 
                                                                                
      *NOTE: IN BP13CH03, IF NUMBER OF FLAT IS >= TO LIMIT IT IS                
      * CONSIDER AS WITHIN LIMIT, HENCE, IN ORDER TO WAIVE,                     
      * WHEN THE NUMBER OF FLAT IS < = 5, THIS PROGRAM                          
      * WILL NEED TO SET THE LIMIT TO 6.                                        
                                                                                
              MOVE 6 TO F249-NUM-LIMIT                                          
                                                                                
           END-IF.                                                              
      ** CR - BP139545 END.                                                     
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13CH3A  *************'.                           
           DISPLAY 'RECS READ    FROM BP13F249 : ' WS-F249-READ-CNT.            
           DISPLAY 'SA RECS READ FROM BP13F249 : ' WS-F249-SA-CNT.              
           DISPLAY 'RECS READ    FROM BP13K249 : ' WS-K249-2R-CNT.              
           DISPLAY '                              '.                            
           DISPLAY 'RECS WRITTEN   IN BP13K249 : ' WS-K249-WRITE-CNT.           
                                                                                
           CLOSE BP13F249                                                       
                 BP13K249                                                       
                 P13K249A.                                                      
                                                                                
           IF WS-K249-STATUS NOT EQUAL 00                                       
              DISPLAY 'BP13K249 - CLOSE ERROR: ' WS-K249-STATUS                 
              MOVE WS-K249-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K249A-STATUS NOT EQUAL 00                                      
              DISPLAY 'P13K249A - CLOSE ERROR: ' WS-K249A-STATUS                
              MOVE WS-K249A-STATUS        TO RETURN-CODE                        
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
