       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C23X.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  08 SEPTEMBER 2012.                                        
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *  OBJECTIVE : 1. THIS IS A PROGRAM TO GET THE LOWEST        *            
      *                 AVAILABLE FLAT PER ESTATE                  *            
      *                                                            *            
      *  INPUT     :  BP13F230                                     *            
      *  I-O       :  BP13K23F                                     *            
      * ---------------------------------------------------------- *            
      * CHGE NO  BY   DATE    DESCRIPTION                          *            
      * -------- ---- ------  -----------                          *            
      * BP134653 ESA1 080912  NEW PROGRAM                          *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F230  ASSIGN       TO BP13F230.                   00470000
                                                                                
           SELECT BP13F23F  ASSIGN       TO BP13F23F.                   00470000
                                                                                
           SELECT BP13K23F  ASSIGN       TO BP13K23F                    00470000
                            ORGANIZATION IS INDEXED                     00490000
                            ACCESS MODE  IS RANDOM                      00480000
                            RECORD KEY   IS K23F-KEY-FLD                00500000
                            FILE STATUS  IS WS-K23F-STATUS.             00510000
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F230                                                            
            RECORDING MODE  F                                                   
            LABEL RECORDS   ARE STANDARD                                        
            BLOCK CONTAINS  0   RECORDS                                         
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K230.                                                           
                                                                                
       FD   BP13F23F                                                    00640000
            RECORDING MODE  F                                                   
            LABEL RECORDS   ARE STANDARD                                        
            BLOCK CONTAINS  0   RECORDS                                         
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13F23F.                                                   00680000
                                                                                
       FD   BP13K23F                                                    00640000
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13K23F.                                                   00680000
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-INDICATORS.                                                       
           05  WS-F230-EOF-IND              PIC X     VALUE 'N'.                
           05  WS-F23F-EOF-IND              PIC X     VALUE 'N'.                
           05  WS-K23F-STATUS               PIC 9(02) VALUE ZEROES.             
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-TEMP-PRICE                PIC 9(08) VALUE ZEROES.             
           05  WS-F230-CURR-SELLING-PRICE   PIC 9(08) VALUE ZEROES.             
           05  WS-F23F-NUM-CATEGORY         PIC X     VALUE SPACE.              
           05  WS-F230-NUM-LEVEL            PIC X(02) VALUE SPACE.              
           05  WS-F230-NUM-UNIT             PIC X(05) VALUE SPACE.              
           05  WS-F230-NUM-BLK              PIC X(05) VALUE SPACE.              
           05  WS-PREV-KEY-FLD              PIC X(42) VALUE SPACE.              
           05  WS-PREV-K23F-REC             PIC X(100) VALUE SPACES.            
                                                                                
       01  WS-COUNTERS-POINTERS.                                                
           05  WS-CNT-READ-F230             PIC 9(6)  VALUE ZEROS.              
           05  WS-CNT-READ-F23F             PIC 9(6)  VALUE ZEROS.              
           05  WS-CNT-WRITE-K23F            PIC 9(6)  VALUE ZEROS.              
           05  WS-CTR                       PIC 9(2)  VALUE ZEROS.              
           05  WS-CTR2                      PIC 9(2)  VALUE ZEROS.              
                                                                                
           05  WS-UNIT-DETAILS  OCCURS 50 TIMES                                 
                                        INDEXED BY WS-INDEX.                    
               10 WS-BLK                PIC X(05).                              
               10 WS-LEVEL              PIC X(02).                              
               10 WS-UNIT               PIC X(05).                              
               10 WS-SELLING-PRICE      PIC 9(08).                              
                                                                                
       01  WS-F230-KEY-FLD.                                                     
               10  WS-F230-NUM-SELECTION      PIC X(03).                        
               10  WS-F230-NUM-NEW-TOWN       PIC X(20).                        
               10  WS-F230-DTE-BALLOT         PIC X(06).                        
               10  WS-F230-NUM-FLAT-TYPE      PIC X(09).                        
               10  WS-F230-NUM-DESIGN         PIC X(04).                        
                                                                                
       01  WS-F23F-KEY-FLD.                                                     
               10  WS-F23F-NUM-SELECTION      PIC X(03).                        
               10  WS-F23F-NUM-NEW-TOWN       PIC X(20).                        
               10  WS-F23F-DTE-BALLOT         PIC X(06).                        
               10  WS-F23F-NUM-FLAT-TYPE      PIC X(09).                        
               10  WS-F23F-NUM-DESIGN         PIC X(04).                        
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           PERFORM  100-OPEN-ROUTINE        THRU    100-EXIT.                   
           PERFORM  200-READ-BP13F23F       THRU    200-EXIT                    
           PERFORM  300-READ-BP13F230       THRU    300-EXIT                    
           PERFORM  400-PROCESS-RECORDS     THRU    400-EXIT                    
            UNTIL WS-F23F-EOF-IND  = 'Y' OR WS-F230-EOF-IND  = 'Y'              
           PERFORM  700-DISPLAY-STATISTICS  THRU    700-EXIT.                   
           PERFORM  900-CLOSE-ROUTINE       THRU    900-EXIT.                   
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F23F                                                 
                       BP13F230                                                 
                I-O    BP13K23F.                                                
                                                                                
           IF WS-K23F-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K23F, STATUS = ' WS-K23F-STATUS        
              MOVE     WS-K23F-STATUS    TO    RETURN-CODE                      
              PERFORM  900-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       200-READ-BP13F23F.                                                       
      *-------------------------------------------------------------            
           READ BP13F23F                                                        
                AT END MOVE 'Y' TO WS-F23F-EOF-IND                              
                GO TO 200-EXIT.                                                 
                                                                                
           ADD  1                  TO WS-CNT-READ-F23F.                         
                                                                                
           MOVE F23F-NUM-SELECTION  TO WS-F23F-NUM-SELECTION.                   
           MOVE F23F-NUM-NEW-TOWN   TO WS-F23F-NUM-NEW-TOWN.                    
           MOVE F23F-DTE-BALLOT     TO WS-F23F-DTE-BALLOT.                      
           MOVE F23F-NUM-FLAT-TYPE  TO WS-F23F-NUM-FLAT-TYPE                    
           MOVE F23F-NUM-DESIGN     TO WS-F23F-NUM-DESIGN.                      
           MOVE F23F-NUM-CATEGORY   TO WS-F23F-NUM-CATEGORY.                    
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       300-READ-BP13F230.                                                       
      *-------------------------------------------------------------            
           READ BP13F230                                                        
                AT END MOVE 'Y' TO WS-F230-EOF-IND                              
                GO TO 300-EXIT.                                                 
                                                                                
           ADD  1                  TO WS-CNT-READ-F230.                         
                                                                                
           MOVE K230-NUM-SELECTION2 TO WS-F230-NUM-SELECTION.                   
           MOVE K230-NUM-NEW-TOWN2  TO WS-F230-NUM-NEW-TOWN.                    
           MOVE K230-DTE-BALLOT     TO WS-F230-DTE-BALLOT.                      
           MOVE K230-NUM-FLAT-TYPE2 TO WS-F230-NUM-FLAT-TYPE.                   
           MOVE K230-NUM-DESIGN     TO WS-F230-NUM-DESIGN.                      
           MOVE K230-CURR-SELLING-PRICE                                         
                                    TO WS-F230-CURR-SELLING-PRICE.              
           MOVE K230-NUM-BLK        TO WS-F230-NUM-BLK.                         
           MOVE K230-NUM-LEVEL      TO WS-F230-NUM-LEVEL.                       
           MOVE K230-NUM-UNIT       TO WS-F230-NUM-UNIT.                        
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       400-PROCESS-RECORDS.                                                     
      *-------------------------------------------------------------            
           IF WS-F23F-KEY-FLD = WS-F230-KEY-FLD                                 
              IF WS-TEMP-PRICE = ZEROES OR LOW-VALUES                           
                 MOVE WS-F230-CURR-SELLING-PRICE TO WS-TEMP-PRICE               
              END-IF                                                            
              IF WS-F230-CURR-SELLING-PRICE = WS-TEMP-PRICE                     
                 IF WS-CTR < 50                                                 
                    ADD 1    TO WS-CTR                                          
                    MOVE WS-F230-NUM-BLK       TO WS-BLK(WS-CTR)                
                    MOVE WS-F230-NUM-LEVEL     TO WS-LEVEL(WS-CTR)              
                    MOVE WS-F230-NUM-UNIT      TO WS-UNIT(WS-CTR)               
                    MOVE WS-TEMP-PRICE       TO WS-SELLING-PRICE(WS-CTR)        
                 END-IF                                                         
                 PERFORM 500-READ-BP13K23F  THRU 500-EXIT                       
              END-IF                                                            
              PERFORM 300-READ-BP13F230   THRU 300-EXIT                         
           ELSE                                                                 
             MOVE ZEROES   TO WS-TEMP-PRICE  WS-CTR                             
             IF WS-F23F-KEY-FLD > WS-F230-KEY-FLD                               
                PERFORM 300-READ-BP13F230   THRU 300-EXIT                       
             ELSE                                                               
               IF WS-F23F-KEY-FLD < WS-F230-KEY-FLD                             
                  MOVE WS-F23F-KEY-FLD  TO WS-PREV-KEY-FLD                      
                  PERFORM 200-READ-BP13F23F THRU 200-EXIT                       
                  IF WS-PREV-KEY-FLD = WS-F23F-KEY-FLD(1:42)                    
                     PERFORM VARYING WS-CTR2 FROM 1 BY 1 UNTIL                  
                         WS-CTR2 > 50                                           
                      IF WS-BLK(WS-CTR2) NOT = SPACES AND LOW-VALUES            
                       MOVE WS-F23F-NUM-SELECTION TO K23F-NUM-SELECTION         
                       MOVE WS-F23F-NUM-NEW-TOWN  TO K23F-NUM-NEW-TOWN          
                       MOVE WS-F23F-DTE-BALLOT    TO K23F-DTE-BALLOT            
                       MOVE WS-F23F-NUM-FLAT-TYPE TO K23F-NUM-FLAT-TYPE         
                       MOVE WS-F23F-NUM-DESIGN    TO K23F-NUM-DESIGN            
                       MOVE WS-F23F-NUM-CATEGORY  TO K23F-NUM-CATEGORY          
                       MOVE WS-BLK(WS-CTR2)        TO K23F-NUM-BLK              
                       MOVE WS-LEVEL(WS-CTR2)      TO K23F-NUM-LEVEL            
                       MOVE WS-UNIT(WS-CTR2)       TO K23F-NUM-UNIT             
                       MOVE WS-SELLING-PRICE(WS-CTR2) TO                        
                                         K23F-CURR-SELLING-PRICE                
                       WRITE   BP13K23F-REC                                     
                      ELSE                                                      
                        MOVE 51   TO WS-CTR2                                    
                      END-IF                                                    
                     END-PERFORM                                                
                  ELSE                                                          
                     PERFORM VARYING WS-CTR2 FROM 1 BY 1 UNTIL                  
                             WS-CTR2 > 50                                       
                       MOVE SPACES   TO  WS-BLK(WS-CTR2)                        
                       MOVE SPACES   TO  WS-LEVEL(WS-CTR2)                      
                       MOVE SPACES   TO  WS-UNIT(WS-CTR2)                       
                       MOVE ZEROES   TO  WS-SELLING-PRICE(WS-CTR2)              
                     END-PERFORM                                                
                     MOVE ZEROES     TO  WS-CTR                                 
                  END-IF                                                        
               END-IF                                                           
             END-IF                                                             
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       500-READ-BP13K23F.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES                TO BP13K23F-REC.                          
           MOVE WS-F230-NUM-SELECTION TO K23F-NUM-SELECTION.                    
           MOVE WS-F230-NUM-NEW-TOWN  TO K23F-NUM-NEW-TOWN.                     
           MOVE WS-F230-DTE-BALLOT    TO K23F-DTE-BALLOT.                       
           MOVE WS-F230-NUM-FLAT-TYPE TO K23F-NUM-FLAT-TYPE.                    
           MOVE WS-F230-NUM-DESIGN    TO K23F-NUM-DESIGN.                       
           MOVE WS-F23F-NUM-CATEGORY  TO K23F-NUM-CATEGORY.                     
           MOVE WS-F230-NUM-BLK       TO K23F-NUM-BLK.                          
           MOVE WS-F230-NUM-LEVEL     TO K23F-NUM-LEVEL.                        
           MOVE WS-F230-NUM-UNIT      TO K23F-NUM-UNIT.                         
                                                                                
           READ BP13K23F.                                                       
                                                                                
           EVALUATE WS-K23F-STATUS                                              
             WHEN 00                                                            
                PERFORM 600-WRITE-BP13K23F   THRU 600-EXIT                      
             WHEN 23                                                            
                PERFORM 600-WRITE-BP13K23F   THRU 600-EXIT                      
             WHEN OTHER                                                         
                DISPLAY 'BP13K23F - READ ERROR: ' WS-K23F-STATUS                
           END-EVALUATE.                                                        
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       600-WRITE-BP13K23F.                                                      
      *-------------------------------------------------------------            
           MOVE WS-F230-NUM-SELECTION TO K23F-NUM-SELECTION.                    
           MOVE WS-F230-NUM-NEW-TOWN  TO K23F-NUM-NEW-TOWN.                     
           MOVE WS-F230-DTE-BALLOT    TO K23F-DTE-BALLOT.                       
           MOVE WS-F230-NUM-FLAT-TYPE TO K23F-NUM-FLAT-TYPE.                    
           MOVE WS-F230-NUM-DESIGN    TO K23F-NUM-DESIGN.                       
           MOVE WS-F23F-NUM-CATEGORY  TO K23F-NUM-CATEGORY.                     
           MOVE WS-F230-NUM-BLK       TO K23F-NUM-BLK.                          
           MOVE WS-F230-NUM-LEVEL     TO K23F-NUM-LEVEL.                        
           MOVE WS-F230-NUM-UNIT      TO K23F-NUM-UNIT.                         
           MOVE WS-F230-CURR-SELLING-PRICE                                      
                                      TO K23F-CURR-SELLING-PRICE.               
           MOVE BP13K23F-REC TO WS-PREV-K23F-REC.                               
           WRITE   BP13K23F-REC.                                                
                                                                                
           EVALUATE WS-K23F-STATUS                                              
             WHEN 00                                                            
               ADD     1        TO  WS-CNT-WRITE-K23F                           
             WHEN 22                                                            
               REWRITE   BP13K23F-REC                                           
             WHEN OTHER                                                         
               DISPLAY 'ERROR WRITE BP13K23F, STAT = ' WS-K23F-STATUS           
               MOVE     WS-K23F-STATUS         TO  RETURN-CODE                  
               PERFORM 700-DISPLAY-STATISTICS  THRU 700-EXIT                    
               PERFORM  900-CLOSE-ROUTINE      THRU 900-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       700-DISPLAY-STATISTICS.                                                  
      *-------------------------------------------------------------            
           DISPLAY '  '.                                                        
           DISPLAY '*--------- BP13C23X CONTROL LIST ---------*'.               
           DISPLAY 'BP13F230 READ    RECORDS  = ' WS-CNT-READ-F230.             
           DISPLAY 'BP13F23F READ    RECORDS  = ' WS-CNT-READ-F23F.             
           DISPLAY 'BP13K23F RECORDS WRITTEN  = ' WS-CNT-WRITE-K23F.            
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       900-CLOSE-ROUTINE.                                                       
      *-------------------------------------------------------------            
           CLOSE BP13F23F                                                       
                 BP13F230                                                       
                 BP13K23F.                                                      
                                                                                
           IF WS-K23F-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K23F, STATUS = ' WS-K23F-STATUS        
              MOVE     WS-K23F-STATUS    TO    RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
