      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13CD55.                                                 
       AUTHOR.        JIANG BO.                                                 
       DATE-WRITTEN.  21/11/08.                                                 
                                                                                
      *=========================================================*               
      *    SYSTEM NAME  :  SYSTEM OF COMMITMENT                 *               
      *    SYSTEM ID    :  BP13                                 *               
      *                                                         *               
      *    OBJECTIVE    :  FILE TO BE SENT TO CPF FOR DBSS CASES*               
      *                                                         *               
      *     INPUT  FILE :                                       *               
      *        1.  BP13FD00                                     *               
      *        2.  BM06F120 (BO-CODE)                           *               
      *        3.  BP13K800 (LOAN-TAG)                          *               
      *        4.  BP13K825 (NUM-CPF)                           *               
      *     OUTPUT FILE  :                                      *               
      *        1.  BP13F605                                     *               
      *                                                         *               
      * CHG REQ   DDMMYY  BY    DESCRIPTION                     *               
      * ========  ======  ====  ===========================     *               
      * BP133505  211108  JB8   NEW PROGRAM                     *               
      *=========================================================*               
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FD00 ASSIGN       TO BP13FD00.                            
           SELECT BM06F120 ASSIGN       TO BM06F120.                            
           SELECT SY02F001 ASSIGN       TO SY02F001.                            
           SELECT BP13K800 ASSIGN       TO BP13K800                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K800-NUM-REGN                        
                           FILE STATUS  IS WS-K800-STATUS.                      
           SELECT BP13K825 ASSIGN       TO BP13K825                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K825-KEY-FLD                         
                           FILE STATUS  IS WS-K825-STATUS.                      
           SELECT BP13F605 ASSIGN       TO BP13F605.                            
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13FD00                                                            
            RECORDING MODE  IS   F                                              
            BLOCK  CONTAINS 0    RECORDS                                        
            RECORD CONTAINS 1600 CHARACTERS                                     
            LABEL  RECORDS  ARE  STANDARD.                                      
       COPY BP13FD00.                                                           
                                                                                
       FD   BM06F120                                                            
            RECORDING MODE  IS   F                                              
            BLOCK  CONTAINS 0    RECORDS                                        
            RECORD CONTAINS 700  CHARACTERS                                     
            LABEL  RECORDS  ARE  STANDARD.                                      
       COPY BM06F120.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K825                                                            
            RECORD CONTAINS 200  CHARACTERS.                                    
       COPY BP13K825.                                                           
                                                                                
       FD   BP13F605                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13F605.                                                           
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  WS-K800-STATUS               PIC 99    VALUE ZEROES.                 
       01  WS-K825-STATUS               PIC 99    VALUE ZEROES.                 
       01  WS-FD00-READ                 PIC 9(07) VALUE ZEROES.                 
       01  WS-FD00-FND                  PIC 9(07) VALUE ZEROES.                 
       01  WS-F120-NOTFND               PIC 9(07) VALUE ZEROES.                 
       01  WS-K800-NOTFND               PIC 9(07) VALUE ZEROES.                 
       01  WS-K825-NOTFND               PIC 9(07) VALUE ZEROES.                 
       01  WS-F120-READ                 PIC 9(07) VALUE ZEROES.                 
       01  WS-F605-WRITE                PIC 9(07) VALUE ZEROES.                 
       01  WS-EOF-FD00                  PIC X(01) VALUE 'N'.                    
       01  WS-EOF-F120                  PIC X(01) VALUE 'N'.                    
       01  WS-TOT-AMT-LOAN-GRANTED      PIC 9(08)V99 VALUE ZEROES.              
       01  WS-TOT-AMT-LOAN-BAL          PIC 9(08)V99 VALUE ZEROES.              
       01  WS-TOT-HDB-CASES             PIC 9(08) VALUE ZEROES.                 
       01  WS-TOT-BANK-CASES            PIC 9(08) VALUE ZEROES.                 
       01  WS-TOT-OTHER                 PIC 9(08) VALUE ZEROES.                 
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
      **********************                                                    
       0000-MAIN.                                                               
      **********************                                                    
           PERFORM  1000-OPENING-ROUTINE    THRU  1000-EXIT.                    
                                                                                
           PERFORM  5100-OUTPUT-HEADER      THRU  5100-EXIT.                    
                                                                                
           PERFORM  2000-READ-BP13FD00      THRU  2000-EXIT.                    
           PERFORM  2500-READ-BM06F120      THRU  2500-EXIT.                    
                                                                                
           PERFORM  3000-PROCESS-RECORDS    THRU  3000-EXIT                     
             UNTIL  WS-EOF-FD00 = 'Y'.                                          
                                                                                
           PERFORM  5200-OUTPUT-TRAILER     THRU  5200-EXIT.                    
                                                                                
           PERFORM  9000-CLOSE-ROUTINE      THRU  9000-EXIT.                    
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       1000-OPENING-ROUTINE.                                                    
      **********************                                                    
           OPEN INPUT  BP13FD00                                                 
                       SY02F001                                                 
                       BM06F120                                                 
                       BP13K800                                                 
                       BP13K825                                                 
                OUTPUT BP13F605.                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K800 , '                               
                      'STATUS = ' WS-K800-STATUS                                
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           IF WS-K825-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K825 , '                               
                      'STATUS = ' WS-K825-STATUS                                
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       2000-READ-BP13FD00.                                                      
      **********************                                                    
                                                                                
           READ BP13FD00 AT END                                                 
                         MOVE 'Y'          TO WS-EOF-FD00                       
                         MOVE HIGH-VALUES  TO FD00-NUM-REGN                     
                         GO TO 2000-EXIT.                                       
                                                                                
           ADD 1 TO WS-FD00-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       2500-READ-BM06F120.                                                      
      **********************                                                    
                                                                                
           READ BM06F120 AT END                                                 
                         MOVE 'Y'          TO WS-EOF-F120                       
                         MOVE HIGH-VALUES  TO F120-REGN-NO                      
                         GO TO 2500-EXIT.                                       
                                                                                
           ADD 1 TO WS-F120-READ.                                               
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       3000-PROCESS-RECORDS.                                                    
      **********************                                                    
                                                                                
           IF FD00-NUM-REGN = F120-REGN-NO                                      
                                                                                
              PERFORM 4000-READ-BP13K800      THRU 4000-EXIT                    
              PERFORM 4500-READ-BP13K825      THRU 4500-EXIT                    
              PERFORM 5000-OUTPUT-BP13F605    THRU 5000-EXIT                    
                                                                                
              PERFORM 2000-READ-BP13FD00      THRU 2000-EXIT                    
              PERFORM 2500-READ-BM06F120      THRU 2500-EXIT                    
           ELSE                                                                 
              IF FD00-NUM-REGN < F120-REGN-NO                                   
                 ADD 1 TO WS-F120-NOTFND                                        
                 DISPLAY 'BM06F120 - REGN NOT FND : ' FD00-NUM-REGN             
                 PERFORM 2000-READ-BP13FD00   THRU 2000-EXIT                    
              ELSE                                                              
                 PERFORM 2500-READ-BM06F120   THRU 2500-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       4000-READ-BP13K800.                                                      
      **********************                                                    
                                                                                
           MOVE SPACES                     TO BP13K800-MASTER.                  
           INITIALIZE                         BP13K800-MASTER.                  
                                                                                
           MOVE FD00-NUM-REGN              TO K800-NUM-REGN.                    
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-K800-STATUS = 23                                            
                 MOVE SPACES               TO K800-NUM-LOAN-TAG                 
                                              K800-NUM-ALLO-CAT                 
                 ADD 1 TO WS-K800-NOTFND                                        
                 DISPLAY 'BP13K800 - REGN NOT FND : ' FD00-NUM-REGN             
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13K800'                                  
                         ',STATUS = ' WS-K800-STATUS                            
                         ',REGN = ' FD00-NUM-REGN                               
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       4500-READ-BP13K825.                                                      
      **********************                                                    
                                                                                
           MOVE SPACES                     TO BP13K825-REC.                     
           INITIALIZE                         BP13K825-REC.                     
                                                                                
           MOVE FD00-NUM-REGN              TO K825-NUM-REGN.                    
           MOVE K800-NUM-NRIC1             TO K825-NUM-NRIC.                    
                                                                                
           READ BP13K825.                                                       
                                                                                
           IF WS-K825-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-K825-STATUS = 23                                            
                 MOVE SPACES               TO K825-NUM-CPF                      
                 ADD 1 TO WS-K825-NOTFND                                        
                 DISPLAY 'BP13K825 - RECORD NOT FND : ' K825-KEY-FLD            
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13K825'                                  
                         ',STATUS = ' WS-K825-STATUS                            
                         ',KEY = ' K825-KEY-FLD                                 
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       5000-OUTPUT-BP13F605.                                                    
      ************************                                                  
                                                                                
           MOVE SPACES                      TO BP13F605-DETAIL.                 
           INITIALIZE                          BP13F605-DETAIL.                 
                                                                                
           MOVE K800-NUM-LOAN-TAG           TO F605-CDE-LOAN-TYPE.              
           MOVE F120-BO-CODE                TO F605-CDE-BO.                     
           STRING '0' FD00-NUM-SCH-ACCNT                                        
              DELIMITED BY SIZE INTO F605-NUM-HOUSE-REF.                        
           MOVE K825-NUM-CPF                TO F605-NUM-CPF-ACCOUNT.            
           MOVE FD00-CDE-LOAN-SCH           TO F605-CDE-LOAN-SCH.               
           MOVE 2.60                        TO F605-NUM-MORTGAGE-RATE.          
                                                                                
           IF FD00-AMT-LOAN-GRANTED NOT NUMERIC                                 
              MOVE ZEROES                   TO FD00-AMT-LOAN-GRANTED            
           END-IF.                                                              
           MOVE FD00-AMT-LOAN-GRANTED       TO F605-AMT-LOAN-GRANTED            
                                                                                
           IF FD00-DTE-LOAN-COMM NOT NUMERIC                                    
              MOVE ZEROES                   TO FD00-DTE-LOAN-COMM               
           END-IF.                                                              
           MOVE FD00-DTE-LOAN-COMM          TO F605-DTE-LOAN-COMM               
                                                                                
           IF FD00-AMT-LOAN-BAL NOT NUMERIC                                     
              MOVE ZEROES                   TO FD00-AMT-LOAN-BAL                
           END-IF.                                                              
           MOVE FD00-AMT-LOAN-BAL           TO F605-AMT-LOAN-BAL                
                                                                                
           IF FD00-AMT-INSTAL NOT NUMERIC                                       
              MOVE ZEROES                   TO FD00-AMT-INSTAL                  
           END-IF.                                                              
           MOVE FD00-AMT-INSTAL             TO F605-AMT-INSTAL                  
                                                                                
           IF FD00-NUM-LOAN-REPYMT-PERIOD NOT NUMERIC                           
              MOVE ZEROES                TO FD00-NUM-LOAN-REPYMT-PERIOD         
           END-IF.                                                              
           MOVE FD00-NUM-LOAN-REPYMT-PERIOD TO F605-NUM-TERM-LOAN.              
                                                                                
           COMPUTE WS-TOT-AMT-LOAN-GRANTED = WS-TOT-AMT-LOAN-GRANTED +          
                                             FD00-AMT-LOAN-GRANTED.             
           COMPUTE WS-TOT-AMT-LOAN-BAL     = WS-TOT-AMT-LOAN-BAL +              
                                             FD00-AMT-LOAN-BAL.                 
           IF K800-NUM-LOAN-TAG = 'H'                                           
              ADD 1 TO WS-TOT-HDB-CASES                                         
           ELSE                                                                 
              IF K800-NUM-LOAN-TAG = 'B'                                        
                 ADD 1 TO WS-TOT-BANK-CASES                                     
              ELSE                                                              
                 ADD 1 TO WS-TOT-OTHER                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           WRITE BP13F605-DETAIL.                                               
           ADD 1 TO WS-F605-WRITE.                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       5100-OUTPUT-HEADER.                                                      
      ************************                                                  
                                                                                
           MOVE SPACES                   TO BP13F605-HEADER.                    
           INITIALIZE                       BP13F605-HEADER.                    
                                                                                
           MOVE F001-DTE-CURRENT         TO F605-DTE-EFFECTIVE                  
                                            F605-DTE-RUN.                       
                                                                                
           WRITE BP13F605-HEADER.                                               
           ADD 1 TO WS-F605-WRITE.                                              
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************                                                  
       5200-OUTPUT-TRAILER.                                                     
      ************************                                                  
                                                                                
           MOVE SPACES                   TO BP13F605-TRAILER.                   
           INITIALIZE                       BP13F605-TRAILER.                   
                                                                                
           MOVE ALL '9'                  TO F605-FILLER                         
           MOVE WS-TOT-AMT-LOAN-GRANTED  TO F605-AMT-TOT-LOAN.                  
           MOVE WS-TOT-AMT-LOAN-BAL      TO F605-AMT-TOT-OS-LOAN.               
           MOVE WS-TOT-HDB-CASES         TO F605-NUM-HDB-CASES.                 
           MOVE WS-TOT-BANK-CASES        TO F605-NUM-BANK-CASES.                
                                                                                
           WRITE BP13F605-TRAILER.                                              
           ADD 1 TO WS-F605-WRITE.                                              
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       9000-CLOSE-ROUTINE.                                                      
      ************************                                                  
                                                                                
           DISPLAY '************** CONTROL   TOTALS **************'.            
           DISPLAY 'PROGRAM-ID : BP13CD55'.                                     
           DISPLAY 'RUN DATE   : ' F001-DTE-CURRENT(7:2) '/'                    
                                   F001-DTE-CURRENT(5:2) '/'                    
                                   F001-DTE-CURRENT(1:4).                       
           DISPLAY '----------------------------------------------'.            
           DISPLAY 'NO OF BP13FD00 RECORDS READ....... : '                      
                    WS-FD00-READ.                                               
           DISPLAY 'NO OF BM06F120 RECORDS READ....... : '                      
                    WS-F120-READ.                                               
           DISPLAY 'NO OF CASES NOT HDB/BANK ......... : '                      
                    WS-TOT-OTHER.                                               
           DISPLAY 'NO OF BM06F120 RECORDS NOT FOUND.. : '                      
                    WS-F120-NOTFND.                                             
           DISPLAY 'NO OF BP13K800 RECORDS NOT FOUND.. : '                      
                    WS-K800-NOTFND.                                             
           DISPLAY 'NO OF BP13K825 RECORDS NOT FOUND.. : '                      
                    WS-K825-NOTFND.                                             
           DISPLAY 'NO OF BP13F605 RECORDS WRITE...... : '                      
                    WS-F605-WRITE.                                              
                                                                                
           CLOSE BP13FD00                                                       
                 BM06F120                                                       
                 SY02F001                                                       
                 BP13K800                                                       
                 BP13K825                                                       
                 BP13F605.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00                                           
              DISPLAY 'ERROR CLOSING BP13K800 ,'                                
                      'STATUS = ' WS-K800-STATUS                                
           END-IF.                                                              
                                                                                
           IF WS-K825-STATUS NOT = 00                                           
              DISPLAY 'ERROR CLOSING BP13K825 ,'                                
                      'STATUS = ' WS-K825-STATUS                                
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
