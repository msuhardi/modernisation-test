       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C98A.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  19/01/16.                                                 
      *===============================================================*         
      *               SYSTEM OF COMMITMENT (BP13) *                   *         
      *===============================================================*         
      *                                                               *         
      *   OBJECTIVE : FOR NEW APPLN THRU ORSA/INTERNET AF SOC CREATED *         
      *               THIS PROGRAM IS USED TO CREATE THE TRANSACTION  *         
      *               ID (AR) AND CREATE MARRIAGE RECORD (AM) , IF    *         
      *               CSTMR SOURCE IS EQUAL TO SPACES.                *         
      *1. FILE DETAILS :                                              *         
      *    INPUT   :  BP13F740                                        *         
      *               BP13K820  ( OCCP FILE)                          *         
      *    OUTPUT     BP13F915  ( TRANSACTION FILE)                   *         
      *                                                               *         
      *===============================================================*         
      * CHG-NO    CHG-DATE     BY   DESCRIPTION                       *         
      * ------    --------     --   -----------                       *         
      * BP135928  19/01/2016   FNP1 NEW PROGRAM (PATTERNED AFTER      *         
      *                             BP13C989 AND BP13C987             *         
      * BP137228  26/03/2018   JF7  REMOVE WRITE OF 'TE' RECORDS      *         
      * BP137228  28/03/2018   JF7  MODIFY COND FOR SPOUSE CHECK      *         
      *****************************************************************         
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
      *----------------------*                                                  
       CONFIGURATION SECTION.                                                   
      *----------------------*                                                  
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *----------------------*                                                  
       INPUT-OUTPUT SECTION.                                                    
      *----------------------*                                                  
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740   ASSIGN TO BP13F740.                                
                                                                                
           SELECT BP13K820   ASSIGN       TO BP13K820                           
                             ORGANIZATION IS INDEXED                            
                             ACCESS MODE  IS DYNAMIC                            
                             RECORD KEY   IS K820-KEY-FLD                       
                             FILE STATUS  IS WS-K820-STATUS.                    
                                                                                
           SELECT BP13F915   ASSIGN TO BP13F915.                                
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
      *-------------*                                                           
       FILE SECTION.                                                            
      *-------------*                                                           
                                                                                
       FD  BP13F740                                                             
           RECORD    CONTAINS 500  CHARACTERS                                   
           BLOCK     CONTAINS   0  RECORDS                                      
           LABEL     RECORDS  ARE  STANDARD                                     
           RECORDING MODE     IS   F.                                           
       COPY BP13F740.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BP13F915                                                             
           RECORD    CONTAINS 800 CHARACTERS                                    
           BLOCK     CONTAINS   0 RECORDS                                       
           LABEL     RECORDS  ARE STANDARD                                      
           RECORDING MODE     IS  F.                                            
       COPY BP13F915.                                                           
                                                                                
      *-------------------------*                                               
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
       01  FILE-STATUS.                                                         
           03  WS-K820-STATUS          PIC 9(02)         VALUE ZERO.            
                                                                                
       01  WS-SWITCHES.                                                         
           03  F740-EOF-IND            PIC X             VALUE SPACES.          
               88 F740-EOF             VALUE 'Y'.                               
           03  WS-K820-EOF-SW           PIC X(01)  VALUE 'N'.                   
               88  WS-K820-EOF          VALUE 'Y'.                              
                                                                                
       01  WS-ACCUMULATORS.                                                     
           03  WS-NO-READ-F740         PIC 9(10)         VALUE ZERO.            
           03  WS-NO-READ-K820         PIC 9(10)         VALUE ZERO.            
           03  WS-AR-WRT-F915          PIC 9(10)         VALUE ZERO.            
           03  WS-AM-WRT-F915          PIC 9(10)         VALUE ZERO.            
           03  WS-CANCEL               PIC 9(10)         VALUE ZERO.            
           03  WS-CUSTID               PIC 9(10)         VALUE ZERO.            
           03  WS-NO-DETAIL            PIC 9(10)         VALUE ZERO.            
           03  WS-WITH-SPOUSE          PIC 9(10)         VALUE ZERO.            
           03  WS-NO-SP-NAME           PIC 9(10)         VALUE ZERO.            
           03  WS-EDT-NO-READ-K820     PIC Z,ZZZ,ZZZ,ZZ9 VALUE ZERO.            
           03  WS-EDT-NO-READ-F740     PIC Z,ZZZ,ZZZ,ZZ9 VALUE ZERO.            
           03  WS-EDT-AR-WRT-F915      PIC Z,ZZZ,ZZZ,ZZ9 VALUE ZERO.            
           03  WS-EDT-AM-WRT-F915      PIC Z,ZZZ,ZZZ,ZZ9 VALUE ZERO.            
           03  WS-EDT-CANCEL           PIC Z,ZZZ,ZZZ,ZZ9 VALUE ZERO.            
           03  WS-EDT-CUSTID           PIC Z,ZZZ,ZZZ,ZZ9 VALUE ZERO.            
           03  WS-EDT-NO-DETAIL        PIC Z,ZZZ,ZZZ,ZZ9 VALUE ZERO.            
           03  WS-EDT-WITH-SPOUSE      PIC Z,ZZZ,ZZZ,ZZ9 VALUE ZERO.            
           03  WS-EDT-NO-SP-NAME       PIC Z,ZZZ,ZZZ,ZZ9 VALUE ZERO.            
                                                                                
       01  WS-TEMP.                                                             
           03  WS-NUM-K820-NRIC1       PIC X(9)          VALUE SPACES.          
           03  WS-NUM-K820-NRIC2       PIC X(9)          VALUE SPACES.          
           03  WS-NUM-K820-NRIC3       PIC X(9)          VALUE SPACES.          
           03  WS-NUM-K820-NRIC4       PIC X(9)          VALUE SPACES.          
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           03  WS-YY                    PIC 9(4).                               
           03  WS-MM                    PIC 9(2).                               
           03  WS-DD                    PIC 9(2).                               
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *----------------*                                                        
       000-MAIN-PROCESS.                                                        
      *----------------*                                                        
                                                                                
             PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                   
             PERFORM 2000-READ-BP13F740       THRU 2000-EXIT.                   
             PERFORM 3000-PROCESS-ROUTINE     THRU 3000-EXIT                    
               UNTIL F740-EOF.                                                  
             PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                   
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-ROUTINE.                                                       
      *----------------*                                                        
           OPEN       INPUT   BP13F740                                          
                              BP13K820                                          
                      OUTPUT  BP13F915.                                         
                                                                                
           IF WS-K820-STATUS NOT = ZEROS AND '97'                               
              DISPLAY ' ERROR OPENING BP13K820  FILE, STATUS '                  
                                                  WS-K820-STATUS                
              MOVE WS-K820-STATUS            TO   RETURN-CODE                   
              PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)   TO  WS-SYSTEM-DATE.                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       2000-READ-BP13F740.                                                      
      *-----------------*                                                       
                                                                                
           READ BP13F740 AT END  MOVE 'Y' TO F740-EOF-IND                       
                         GO TO 2000-EXIT.                                       
                                                                                
           ADD 1 TO WS-NO-READ-F740.                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3000-PROCESS-ROUTINE.                                                    
      *-------------------*                                                     
                                                                                
           PERFORM 4100-START-BP13K820      THRU 4100-EXIT.                     
                                                                                
           PERFORM 4300-BROWSE-BP13K820   THRU 4300-EXIT                        
                 UNTIL WS-K820-EOF OR                                           
                       (K820-NUM-REGN NOT = F740-NUM-REGN).                     
                                                                                
                                                                                
           PERFORM 2000-READ-BP13F740       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       4100-START-BP13K820.                                                     
      *------------------*                                                      
                                                                                
           MOVE 'N'           TO  WS-K820-EOF-SW.                               
           MOVE F740-NUM-REGN TO  K820-NUM-REGN.                                
           MOVE SPACES        TO  K820-NUM-NRIC.                                
                                                                                
           START BP13K820 KEY >= K820-KEY-FLD                                   
           IF WS-K820-STATUS = ZEROS OR 02                                      
              PERFORM 4200-READ-BP13K820   THRU 4200-EXIT                       
           ELSE                                                                 
              DISPLAY 'START BROWSE ERROR, BP13K820 STATUS IS '                 
                      WS-K820-STATUS                                            
              MOVE WS-K820-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       4200-READ-BP13K820.                                                      
      *------------------*                                                      
                                                                                
           READ BP13K820 NEXT AT END                                            
                              MOVE 'Y'      TO WS-K820-EOF-SW                   
                              GO TO 4200-EXIT.                                  
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4300-BROWSE-BP13K820.                                                    
      *-------------------*                                                     
                                                                                
           IF F740-NUM-REGN  = K820-NUM-REGN                                    
              ADD  1       TO  WS-NO-READ-K820                                  
              IF K820-NUM-CSTMR-SOURCE = SPACES                                 
                 PERFORM 4500-AR-WRITE-F915    THRU  4500-EXIT                  
                 IF (K820-NUM-NRIC-SPOUSE NOT = SPACES) AND                     
                    (K820-NUM-MARITAL-STATUS  = 'M' OR '2')                     
                    ADD   1    TO   WS-WITH-SPOUSE                              
                    PERFORM 4600-AM-WRITE-F915 THRU  4600-EXIT                  
                 END-IF                                                         
              ELSE                                                              
                 ADD  1  TO  WS-CUSTID                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 4200-READ-BP13K820  THRU 4200-EXIT.                          
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------*                                                       
       4500-AR-WRITE-F915.                                                      
      *-----------------*                                                       
                                                                                
           INITIALIZE  BP13F915-REC.                                            
           MOVE SPACES                   TO   BP13F915-REC.                     
                                                                                
           IF  K820-NUM-NATIONALITY      =    SPACES AND                        
               K820-NUM-CITIZENSHIP      =    SPACES AND                        
               K820-NUM-MARITAL-STATUS   =    SPACES AND                        
               K820-NUM-SEX              =    SPACES AND                        
               K820-NUM-ETHNIC           =    SPACES AND                        
               K820-DTE-BIRTH            =    SPACES                            
               ADD  1  TO  WS-NO-DETAIL                                         
               GO TO 4500-EXIT                                                  
           END-IF.                                                              
                                                                                
           MOVE  'AR'                    TO   F915-TXN-TYPE.                    
           MOVE  'P'                     TO   F915-CSTMR-TYPE.                  
           MOVE  'BP13'                  TO   F915-NUM-UPDATE-SOURCE.           
           MOVE  'BP13C98A'              TO   F915-NUM-UPDATE-USERID.           
                                                                                
           MOVE  K820-NUM-NRIC           TO   F915-NUM-IDNTY-NO.                
                                                                                
           MOVE  K820-NME-OCCP           TO   F915-NME-PERSON.                  
           MOVE  K820-NUM-NATIONALITY    TO   F915-NUM-NTNLTY.                  
           MOVE  K820-NUM-CITIZENSHIP    TO   F915-NUM-PR-STATUS                
           MOVE  K820-NUM-MARITAL-STATUS TO   F915-NUM-MRTL-STATUS.             
           MOVE  K820-NUM-SEX            TO   F915-NUM-SEX.                     
           MOVE  K820-NUM-ETHNIC         TO   F915-NUM-ETHNIC-GROUP.            
                                                                                
            STRING K820-DTE-BIRTH(1:4) '-' K820-DTE-BIRTH(5:2) '-'              
                   K820-DTE-BIRTH(7:2) DELIMITED BY SIZE                        
                   INTO F915-DTE-BIRTH.                                         
                                                                                
           PERFORM 4800-CONVERT-CUSTOMER-ROLE    THRU 4800-EXIT.                
                                                                                
           MOVE    K820-NUM-REGN          TO                                    
                                          F915-NUM-BSNS-REF.                    
           MOVE    '0001-01-01'           TO                                    
                                          F915-DTE-ACPTNC.                      
                                                                                
           STRING F740-DTE-REQUEST(1:4) '-' F740-DTE-REQUEST(5:2) '-'           
                  F740-DTE-REQUEST(7:2) DELIMITED BY SIZE                       
                  INTO  F915-DTE-ROLE-START.                                    
                                                                                
           WRITE BP13F915-REC.                                                  
           ADD   1    TO WS-AR-WRT-F915.                                        
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------*                                                      
       4600-AM-WRITE-F915.                                                      
      *------------------*                                                      
                                                                                
              IF   K820-NME-SPOUSE  =   SPACES                                  
                   ADD   1    TO   WS-NO-SP-NAME                                
                   GO TO 4600-EXIT                                              
              END-IF.                                                           
                                                                                
              MOVE SPACES                      TO BP13F915-REC.                 
              MOVE 'AM'                        TO F915-TXN-TYPE.                
              MOVE 'BP13'                      TO                               
                                               F915-NUM-UPDATE-SOURCE.          
              MOVE 'BP13C98A'                  TO                               
                                               F915-NUM-UPDATE-USERID.          
                                                                                
              MOVE K820-NUM-REGN               TO F915-NUM-REGN.                
                                                                                
      ****  MOVE PERSON'S DETAIL    TO F915 PERSON'S DETAIL  ****               
                                                                                
              MOVE K820-NUM-NRIC               TO F915-NUM-IDNTY-NO-PSN.        
              MOVE K820-NME-OCCP               TO F915-NME-PSN.                 
                                                                                
      ****  MOVE SPOUSE'S DETAIL    TO F915 SPOUSE'S DETAIL  ****               
                                                                                
              MOVE K820-NUM-NRIC-SPOUSE        TO F915-NUM-IDNTY-NO-SP.         
              MOVE K820-NME-SPOUSE             TO F915-NME-SP.                  
                                                                                
              WRITE BP13F915-REC.                                               
              ADD   1    TO   WS-AM-WRT-F915.                                   
                                                                                
       4600-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------*                                              
       4800-CONVERT-CUSTOMER-ROLE.                                              
      *--------------------------*                                              
                                                                                
            IF K820-NUM-NRIC        =   F740-NUM-NRIC1                          
               MOVE '011'           TO  F915-NUM-ROLE                           
            ELSE                                                                
              IF ( K820-NUM-NRIC    =   F740-NUM-NRIC2 ) OR                     
                 ( K820-NUM-NRIC    =   F740-NUM-NRIC3 ) OR                     
                 ( K820-NUM-NRIC    =   F740-NUM-NRIC4 )                        
                  MOVE '012'        TO  F915-NUM-ROLE                           
              ELSE                                                              
                  MOVE '013'        TO  F915-NUM-ROLE                           
              END-IF                                                            
            END-IF.                                                             
                                                                                
       4800-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------*                                                      
       9000-CLOSE-ROUTINE.                                                      
      *------------------*                                                      
                                                                                
           CLOSE BP13F740                                                       
                 BP13K820                                                       
                 BP13F915.                                                      
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY ' ERROR CLOSING BP13K820  FILE, STATUS '                  
              WS-K820-STATUS.                                                   
                                                                                
           MOVE  WS-NO-READ-K820       TO   WS-EDT-NO-READ-K820                 
           MOVE  WS-NO-READ-F740       TO   WS-EDT-NO-READ-F740                 
           MOVE  WS-AR-WRT-F915        TO   WS-EDT-AR-WRT-F915                  
           MOVE  WS-AM-WRT-F915        TO   WS-EDT-AM-WRT-F915                  
           MOVE  WS-NO-READ-K820       TO   WS-EDT-NO-READ-K820                 
                                                                                
           MOVE  WS-CANCEL             TO   WS-EDT-CANCEL.                      
           MOVE  WS-CUSTID             TO   WS-EDT-CUSTID.                      
           MOVE  WS-NO-DETAIL          TO   WS-EDT-NO-DETAIL.                   
           MOVE  WS-WITH-SPOUSE        TO   WS-EDT-WITH-SPOUSE.                 
           MOVE  WS-NO-SP-NAME         TO   WS-EDT-NO-SP-NAME.                  
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY " BP13C98A  CONTROL LIST  :  ".                              
           DISPLAY "----------------------------".                              
           DISPLAY SPACES.                                                      
           DISPLAY "NO. OF REC READ FROM BP13F740 :" WS-EDT-NO-READ-F740        
           DISPLAY "NO. OF REC READ FROM BP13K820 :" WS-EDT-NO-READ-K820        
           DISPLAY SPACES.                                                      
           DISPLAY "NO. OF REC WRITE TO  BP13F915 }" WS-EDT-AR-WRT-F915         
           DISPLAY " CDB TABLE   ( 'AR' TRANS )   }"                            
           DISPLAY SPACES.                                                      
           DISPLAY "NO. OF REC CANCELLED APPL     :" WS-EDT-CANCEL.             
           DISPLAY "NO. OF REC BYPASS WITH CUSTID :" WS-EDT-CUSTID.             
           DISPLAY "NO. OF REC BYPASS NO DETAIL   :" WS-EDT-NO-DETAIL.          
           DISPLAY "NO. OF REC WITH SPOUSE-NRIC   :" WS-EDT-WITH-SPOUSE.        
           DISPLAY "NO. OF REC SPOUSE-NAME=BLANK  :" WS-EDT-NO-SP-NAME.         
           DISPLAY SPACES.                                                      
           DISPLAY "NO. OF REC WRITE TO  BP13F915 }" WS-EDT-AM-WRT-F915         
           DISPLAY " MARRIED FILE ('AM' TRANS )   }"                            
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
