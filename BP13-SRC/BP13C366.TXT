      *----------------------------------------------------------------*        
       IDENTIFICATION DIVISION.                                                 
      *----------------------------------------------------------------*        
       PROGRAM-ID.    BP13C366.                                                 
       AUTHOR.        SK.                                                       
       DATE-WRITTEN.  11/02/2003.                                               
                                                                                
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                   *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  1. TP DATE PROCESSED FOR UNIT NOT HAS BEEN    *        
      *                     SURRENDERED.                               *        
      *   INPUT FILES :  1.  BP13F690  -  AMORTISE DEFER INCOME        *        
      *                      BM06F203  -  FIS/SLS INTERFACE FILE       *        
      *                                                                *        
      *   LISTINGS    :  1.  CONTROL LIST   -   BP13L366               *        
      *                                                                *        
      *----------------------------------------------------------------*        
      *                                                                *        
      *   CHGE-NO   BY    DATE      DESCRIPTION                        *        
      *   --------  ---  ---------- -----------                        *        
      *   BP132292  SK   11/02/2003 TP DATE PROCESSED NOT SURRENDERED  *        
      *   BP136398  FNP1 17/08/2016 EXPAND F690 FROM 250 TO 600        *        
      *   BP138944  FP6  13/10/2021 EXPAND F690 FROM 600 TO 800        *        
      ******************************************************************        
                                                                                
      *----------------------------------------------------------------*        
       ENVIRONMENT DIVISION.                                                    
      *----------------------------------------------------------------*        
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F690 ASSIGN TO BP13F690.                                  
           SELECT BP13L366 ASSIGN TO BP13L366.                                  
                                                                                
      *----------------------------------------------------------------*        
       DATA DIVISION.                                                           
      *----------------------------------------------------------------*        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F690                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 800 CHARACTERS.                                      
                                                                                
       COPY BP13F690.                                                           
                                                                                
       FD  BP13L366                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS  132 CHARACTERS.                                     
                                                                                
       01  BP13L366-REC         PIC X(132).                                     
                                                                                
      *----------------------------------------------------------------*        
       WORKING-STORAGE SECTION.                                                 
      *----------------------------------------------------------------*        
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-CNT-F690-READ          PIC 9(8)    VALUE 0.                    
                                                                                
       01  WS-SWITCHES.                                                         
           05 WS-F690-EOF               PIC X(1)    VALUE 'N'.                  
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-PREV-SCH-ACC           PIC X(11)   VALUE SPACES.               
           05 WS-PREV-TP-DATE           PIC X(8)    VALUE SPACES.               
           05 WS-CNT-LINE-ON-PAGE       PIC 9(8)    VALUE 66.                   
           05 WS-CNT-PRINT-L366         PIC 9(8)    VALUE 1.                    
           05 WS-CNT-WRITE-L366         PIC 9(8)    VALUE 0.                    
           05 WS-NO-OF-PAGE             PIC 9(8)    VALUE 0.                    
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY              PIC 9(4).                               
           05  WS-SYS-MM                PIC 9(2).                               
           05  WS-SYS-DD                PIC 9(2).                               
                                                                                
       01  WS-EDITED-DATE               PIC X(10)   VALUE SPACES.               
       01  WS-SCH-ACC-NO                PIC X(14)   VALUE SPACES.               
       01  BLANK-LINE                   PIC X(80)   VALUE SPACES.               
                                                                                
      *-----------------------------------------------------------*             
      *    WS FOR CONTROL LISTING                                 *             
      *-----------------------------------------------------------*             
                                                                                
       01  L366-PR-HEAD-01.                                                     
           05  FILLER                   PIC X(14) VALUE 'BP13L366'.             
           05  FILLER                   PIC X(8)  VALUE 'HDBCAT 3'.             
           05  FILLER                   PIC X(25) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(13) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE: '.               
           05  L366-PRINT-DATE-1        PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'PAGE: '.               
           05  L366-PRINT-PAGE          PIC ZZZ9.                               
                                                                                
       01  L366-PR-HEAD-02.                                                     
           05  FILLER                   PIC X(35) VALUE SPACES.                 
           05  FILLER                   PIC X(90) VALUE 'TP DATE PROCESS        
      -    'ED FOR UNIT WHERE IT HAS NOT BEEN SURRENDERED'.                     
                                                                                
       01  L366-PR-HEAD-03.                                                     
           05  FILLER               PIC X(5)  VALUE ' S-NO'.                    
           05  FILLER               PIC X(1)  VALUE SPACES.                     
           05  FILLER               PIC X(12) VALUE 'SCHEME A/C'.               
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  FILLER               PIC X(10) VALUE 'TP DTE'.                   
           05  FILLER               PIC X(10) VALUE SPACES.                     
           05  FILLER               PIC X(18) VALUE 'PREV SCHEME A/C'.          
           05  FILLER               PIC X(06) VALUE SPACES.                     
           05  FILLER               PIC X(17) VALUE 'PREV TP DTE'.              
                                                                                
       01  L366-PR-HEAD-04.                                                     
           05  FILLER                 PIC X(132) VALUE ALL '-'.                 
                                                                                
       01  L366-PR-DETAILS-1.                                                   
           05  L366-NUMBER            PIC ZZZZ9.                                
           05  FILLER                 PIC X(1)  VALUE SPACES.                   
           05  L366-SCH-ACC           PIC X(14) VALUE SPACES.                   
           05  FILLER                 PIC X(10) VALUE SPACES.                   
           05  L366-TP-DTE            PIC X(10) VALUE SPACES.                   
           05  FILLER                 PIC X(10) VALUE SPACES.                   
           05  L366-SCH-ACC-PREV      PIC X(14) VALUE SPACES.                   
           05  FILLER                 PIC X(10) VALUE SPACES.                   
           05  L366-TP-DTE-PREV       PIC X(10) VALUE SPACES.                   
                                                                                
      *----------------------------------------------------------------*        
       PROCEDURE DIVISION.                                                      
      *----------------------------------------------------------------*        
                                                                                
      *----------------------------------------------------------------*        
       0000-MAIN-ROUTINE.                                                       
      *----------------------------------------------------------------*        
                                                                                
           PERFORM  1000-OPEN-FILES      THRU    1000-EXIT.                     
           PERFORM  2000-READ-BP13F690   THRU    2000-EXIT.                     
           PERFORM  3000-PROCESS         THRU    3000-EXIT                      
                    UNTIL WS-F690-EOF = 'Y'.                                    
           PERFORM  9000-CLOSE-ROUTINE   THRU    9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       1000-OPEN-FILES.                                                         
      *----------------------------------------------------------------*        
                                                                                
           OPEN INPUT  BP13F690                                                 
                OUTPUT BP13L366.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATE.                      
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       2000-READ-BP13F690.                                                      
      *----------------------------------------------------------------*        
                                                                                
           READ BP13F690                                                        
                AT END MOVE 'Y' TO WS-F690-EOF                                  
                GO TO 2000-EXIT                                                 
           END-READ.                                                            
           ADD 1 TO WS-CNT-F690-READ.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3000-PROCESS.                                                            
      *----------------------------------------------------------------*        
                                                                                
           IF F690-NUM-SCH-ACC(1:9) = WS-PREV-SCH-ACC(1:9)                      
              PERFORM 3500-MOVE-PARA        THRU 3500-EXIT                      
              PERFORM 4000-WRITE-BP13L366   THRU 4000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE F690-NUM-SCH-ACC   TO  WS-PREV-SCH-ACC.                         
           MOVE F690-DTE-KEY-ISSUE TO  WS-PREV-TP-DATE.                         
                                                                                
           PERFORM 2000-READ-BP13F690 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3500-MOVE-PARA.                                                          
      *----------------------------------------------------------------*        
                                                                                
           MOVE  SPACES           TO BP13L366-REC.                              
                                                                                
           STRING F690-NUM-SCH-ACC(1:4) '-'                                     
                  F690-NUM-SCH-ACC(5:4) '-'                                     
                  F690-NUM-SCH-ACC(9:1) '-'                                     
                  F690-NUM-SCH-ACC(10:2)                                        
                  DELIMITED BY SIZE                                             
                  INTO WS-SCH-ACC-NO                                            
           END-STRING.                                                          
                                                                                
           MOVE  WS-SCH-ACC-NO      TO L366-SCH-ACC.                            
                                                                                
           STRING F690-DTE-KEY-ISSUE(7:2) '/'                                   
                  F690-DTE-KEY-ISSUE(5:2) '/'                                   
                  F690-DTE-KEY-ISSUE                                            
                  DELIMITED BY SIZE                                             
                  INTO  L366-TP-DTE                                             
           END-STRING.                                                          
                                                                                
           STRING WS-PREV-SCH-ACC(1:4) '-'                                      
                  WS-PREV-SCH-ACC(5:4) '-'                                      
                  WS-PREV-SCH-ACC(9:1) '-'                                      
                  WS-PREV-SCH-ACC(10:2)                                         
                  DELIMITED BY SIZE                                             
                  INTO WS-SCH-ACC-NO                                            
           END-STRING.                                                          
                                                                                
           MOVE  WS-SCH-ACC-NO      TO L366-SCH-ACC-PREV.                       
                                                                                
           STRING WS-PREV-TP-DATE(7:2) '/'                                      
                  WS-PREV-TP-DATE(5:2) '/'                                      
                  WS-PREV-TP-DATE(1:4)                                          
                  DELIMITED BY SIZE                                             
                  INTO  L366-TP-DTE-PREV                                        
           END-STRING.                                                          
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4000-WRITE-BP13L366.                                                     
      *----------------------------------------------------------------*        
                                                                                
           IF WS-CNT-LINE-ON-PAGE > 48                                          
              PERFORM 4500-PRINT-HEADING THRU 4500-EXIT                         
              MOVE 1 TO WS-CNT-LINE-ON-PAGE                                     
           END-IF.                                                              
                                                                                
           MOVE  WS-CNT-PRINT-L366    TO    L366-NUMBER.                        
                                                                                
           WRITE BP13L366-REC      FROM  L366-PR-DETAILS-1 AFTER 1.             
                                                                                
           ADD   1                 TO    WS-CNT-LINE-ON-PAGE.                   
           ADD   1                 TO    WS-CNT-PRINT-L366.                     
           ADD   1                 TO    WS-CNT-WRITE-L366.                     
                                                                                
           MOVE  SPACES            TO    L366-PR-DETAILS-1.                     
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4500-PRINT-HEADING.                                                      
      *----------------------------------------------------------------*        
                                                                                
           ADD     1                  TO     WS-NO-OF-PAGE.                     
           MOVE    WS-NO-OF-PAGE      TO     L366-PRINT-PAGE.                   
           MOVE    WS-EDITED-DATE     TO     L366-PRINT-DATE-1.                 
                                                                                
           MOVE    SPACES             TO     BP13L366-REC.                      
                                                                                
           WRITE   BP13L366-REC       AFTER  PAGE.                              
           WRITE   BP13L366-REC       FROM   L366-PR-HEAD-01 AFTER 1.           
           WRITE   BP13L366-REC       FROM   L366-PR-HEAD-02 AFTER 1.           
           WRITE   BP13L366-REC       FROM   L366-PR-HEAD-03 AFTER 2.           
           WRITE   BP13L366-REC       FROM   L366-PR-HEAD-04 AFTER 1.           
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       9000-CLOSE-ROUTINE.                                                      
      *----------------------------------------------------------------*        
                                                                                
           CLOSE BP13F690                                                       
                 BP13L366.                                                      
                                                                                
           DISPLAY '                                    '.                      
           DISPLAY '***** BP13C366 CONTROL TOTALS ***** '.                      
           DISPLAY '                                    '.                      
           DISPLAY 'NO OF RECORDS READ FROM  (BP13F690) = '                     
                    WS-CNT-F690-READ.                                           
           DISPLAY 'NO OF RECORD PRINTED     (BP13L366) = '                     
                    WS-CNT-WRITE-L366.                                          
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
